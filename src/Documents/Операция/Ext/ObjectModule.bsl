#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.Операция.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	ПроведениеДокументовУНФ.ОтразитьУправленческий(ДополнительныеСвойства, Движения, Отказ);
	
	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	УчетВалютныхОпераций = Константы.ФункциональнаяУчетВалютныхОпераций.Получить();
	
	Если УчетВалютныхОпераций Тогда
		Для каждого СтрокаТЧ Из Проводки Цикл
			Если СтрокаТЧ.СчетДт.Валютный
			И Не ЗначениеЗаполнено(СтрокаТЧ.ВалютаДт) Тогда
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'Не заполнена колонка ""Валюта Дт"" для валютного счета в строке %1 списка ""Проводки"".'"),
					СтрокаТЧ.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Проводки", СтрокаТЧ.НомерСтроки,
					"ВалютаДт");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
			КонецЕсли;
			Если СтрокаТЧ.СчетДт.Валютный
			И Не ЗначениеЗаполнено(СтрокаТЧ.СуммаВалДт) Тогда
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'Не заполнена колонка ""Сумма (вал.) Дт"" для валютного счета в строке %1 списка ""Проводки"".'"),
					СтрокаТЧ.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Проводки", СтрокаТЧ.НомерСтроки,
					"СуммаВалДт");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
			КонецЕсли;
			Если СтрокаТЧ.СчетКт.Валютный
			И НЕ ЗначениеЗаполнено(СтрокаТЧ.ВалютаКт) Тогда
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'Не заполнена колонка ""Валюта Кт"" для валютного счета в строке %1 списка ""Проводки"".'"),
					СтрокаТЧ.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Проводки", СтрокаТЧ.НомерСтроки,
					"ВалютаКт");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
			КонецЕсли;
			Если СтрокаТЧ.СчетКт.Валютный
			И НЕ ЗначениеЗаполнено(СтрокаТЧ.СуммаВалКт) Тогда
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'Не заполнена колонка ""Сумма (вал.) Кт"" для валютного счета в строке %1 списка ""Проводки"".'"),
					СтрокаТЧ.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Проводки", СтрокаТЧ.НомерСтроки,
					"СуммаВалКт");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если НЕ Константы.ФункциональнаяУчетВалютныхОпераций.Получить() Тогда
		Для каждого СтрокаТабличнойЧасти Из Проводки Цикл
			Если СтрокаТабличнойЧасти.СчетДт.Валютный Тогда
				СтрокаТабличнойЧасти.ВалютаДт = Константы.НациональнаяВалюта.Получить();
				СтрокаТабличнойЧасти.СуммаВалДт = СтрокаТабличнойЧасти.Сумма;
			КонецЕсли;
			Если СтрокаТабличнойЧасти.СчетКт.Валютный Тогда
				СтрокаТабличнойЧасти.ВалютаКт = Константы.НациональнаяВалюта.Получить();
				СтрокаТабличнойЧасти.СуммаВалКт = СтрокаТабличнойЧасти.Сумма;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СуммаДокумента = Проводки.Итог("Сумма");
	
КонецПроцедуры // ПередЗаписью()

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли