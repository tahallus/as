#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РедактируемыйДокументСсылка = Параметры.РедактируемыйДокументСсылка;
	Сотрудник = Параметры.Сотрудник;
	ИспользоватьЗамещение = Параметры.ИспользоватьЗамещение;
	
	ДанныеИзВременногоХранилищаВДанныеФормы(Параметры.АдресВоВременномХранилище);
	
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник, "ФизЛицо");
	КонецЕсли;
	
	УстановитьЗаголовокФормы();
	УстановитьВидимостьКолонок();
	УстановитьПараметрыВыбораЗаявлений();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеДанныхФизическогоЛица" И Не ТолькоПросмотр Тогда 
		ЗаполнитьИзменившиесяДанныеФизическогоЛица(Параметр);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗаявлениеОПродолженииПриИзменении(Элемент)
	
	ЗаявлениеПриИзмененииНаСервере("ЗаявлениеОПродолжении");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявлениеОПредоставленииПриИзменении(Элемент)
	
	ЗаявлениеПриИзмененииНаСервере("ЗаявлениеОПредоставлении");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМероприятия

&НаКлиенте
Процедура МероприятияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ТекущиеДанные = Элементы.Мероприятия.ТекущиеДанные;
		ТекущиеДанные.Идентификатор = Строка(Новый УникальныйИдентификатор);
		ТекущиеДанные.СотрудникЗаписи = Сотрудник;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МероприятияПослеУдаления(Элемент)
	
	УстановитьВидимостьКолонок();
	
КонецПроцедуры

&НаКлиенте
Процедура МероприятияВидМероприятияПриИзменении(Элемент)
	
	УстановитьВидимостьКолонок();
	
КонецПроцедуры

&НаКлиенте
Процедура МероприятияДатаОтменыПриИзменении(Элемент)
	
	УстановитьВидимостьКолонок();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ВыбратьИЗакрыть();	
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();	
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьДанныеФизическогоЛица(Команда)
	
	ПараметрыФормы = Новый Структура("Ключ, АктивнаяСтраница, РедактируемоеПоле", Сотрудник, "ЛичныеДанные");
	ОткрытьФорму("Справочник.Сотрудники.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДанныеИзВременногоХранилищаВДанныеФормы(АдресВоВременномХранилище)
	
	Модифицированность = Ложь;
	
	ДанныеДокументаПоСотруднику = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	
	Если ДанныеДокументаПоСотруднику = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Не удалось получить редактируемые данные.'");	
	КонецЕсли;
	
	Сотрудник = ДанныеДокументаПоСотруднику.Сотрудник;
	Фамилия = ДанныеДокументаПоСотруднику.Фамилия;
	Имя = ДанныеДокументаПоСотруднику.Имя;
	Отчество = ДанныеДокументаПоСотруднику.Отчество;
	СтраховойНомерПФР = ДанныеДокументаПоСотруднику.СтраховойНомерПФР;
	ДатаРождения = ДанныеДокументаПоСотруднику.ДатаРождения;
	ЯвляетсяСовместителем = ДанныеДокументаПоСотруднику.ЯвляетсяСовместителем;
	ЗаявлениеОПродолжении = ДанныеДокументаПоСотруднику.ЗаявлениеОПродолжении;
	ЗаявлениеОПредоставлении = ДанныеДокументаПоСотруднику.ЗаявлениеОПредоставлении;
	ЗаявлениеОПродолженииОтмена = ДанныеДокументаПоСотруднику.ЗаявлениеОПродолженииОтмена;
	ЗаявлениеОПредоставленииОтмена = ДанныеДокументаПоСотруднику.ЗаявлениеОПредоставленииОтмена;
	
	Мероприятия.Очистить();
	
	Для Каждого СтрокаТаблицы Из ДанныеДокументаПоСотруднику.Мероприятия Цикл
		СтрокаТаблицыФормы = Мероприятия.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыФормы, СтрокаТаблицы);
	КонецЦикла;	
	
	Мероприятия.Сортировать("ДатаМероприятия");
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Заголовок = НСтр("ru = 'Мероприятия'");
	ИнфонадписьЗаголовокКарточки = НСтр("ru = 'Данные застрахованного лица'");
	
КонецПроцедуры	

&НаКлиенте
Процедура ВыбратьИЗакрыть(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	АдресВоВременномХранилище = ДанныеСотрудникаВоВременноеХранилище();
	
	ПараметрыОповещения = Новый Структура("РедактируемыйДокументСсылка, Сотрудник, АдресВоВременномХранилище");
	ПараметрыОповещения.РедактируемыйДокументСсылка = РедактируемыйДокументСсылка;
	ПараметрыОповещения.Сотрудник = Сотрудник;
	ПараметрыОповещения.АдресВоВременномХранилище = АдресВоВременномХранилище;	
	
	Оповестить("РедактированиеМероприятийПоСотруднику", ПараметрыОповещения, ЭтаФорма);
	
	Модифицированность = Ложь;

	Закрыть();
	
КонецПроцедуры

&НаСервере 
Функция ДанныеСотрудникаВоВременноеХранилище()	
	
	ДанныеСотрудника = Новый Структура;
	ДанныеСотрудника.Вставить("Сотрудник", Сотрудник);
	ДанныеСотрудника.Вставить("СтраховойНомерПФР", СтраховойНомерПФР);
	ДанныеСотрудника.Вставить("Фамилия", Фамилия);
	ДанныеСотрудника.Вставить("Имя", Имя);
	ДанныеСотрудника.Вставить("Отчество", Отчество);
	ДанныеСотрудника.Вставить("ДатаРождения", ДатаРождения);
	ДанныеСотрудника.Вставить("ЗаявлениеОПродолжении", ЗаявлениеОПродолжении);
	ДанныеСотрудника.Вставить("ЗаявлениеОПредоставлении", ЗаявлениеОПредоставлении);
	ДанныеСотрудника.Вставить("ЗаявлениеОПродолженииОтмена", ЗаявлениеОПродолженииОтмена);
	ДанныеСотрудника.Вставить("ЗаявлениеОПредоставленииОтмена", ЗаявлениеОПредоставленииОтмена);
	ДанныеСотрудника.Вставить("Модифицированность", Модифицированность);
	ДанныеСотрудника.Вставить("РедактируемыйДокументСсылка", РедактируемыйДокументСсылка);
	ДанныеСотрудника.Вставить("ИзменилисьДанныеФизическогоЛица", ИзменилисьДанныеФизическогоЛица);
	
	СписокСвойств = ЭлектронныеТрудовыеКнижки.ДоступныеПоляВсехВидовМероприятийДокумента();
	ДоступныеПоля = ЭлектронныеТрудовыеКнижки.ИменаДоступныхПолейВидовМероприятий();
	
	МероприятияСотрудника = Новый Массив;
	ДанныеСотрудника.Вставить("Мероприятия", МероприятияСотрудника);
	
	СтруктураПолейМероприятия = Документы.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД.СтруктураПолейМероприятий();
	Для Каждого Мероприятие Из Мероприятия Цикл
		Если Не ЗначениеЗаполнено(Мероприятие.ВидМероприятия) Тогда
			Продолжить;
		КонецЕсли;
		СтруктураПолей = ОбщегоНазначения.СкопироватьРекурсивно(СтруктураПолейМероприятия);
		
		ИменаСвойств = СписокСвойств + "," + ДоступныеПоля[Мероприятие.ВидМероприятия];
		
		ЗаполнитьЗначенияСвойств(СтруктураПолей, Мероприятие, ИменаСвойств);
		СтруктураПолей.Сотрудник = Сотрудник;
		Если ЗначениеЗаполнено(Мероприятие.Идентификатор) Тогда
			СтруктураПолей.ИдМероприятия = Новый УникальныйИдентификатор(Мероприятие.Идентификатор);
		КонецЕсли;
		МероприятияСотрудника.Добавить(СтруктураПолей);
	КонецЦикла;	
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеСотрудника);
	
КонецФункции	

&НаКлиенте
Процедура ЗаполнитьИзменившиесяДанныеФизическогоЛица(ДанныеФизическогоЛица)	
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеФизическогоЛица);
	ИзменилисьДанныеФизическогоЛица = Истина;
	
КонецПроцедуры	
                                                                                                      
&НаСервере 
Процедура УстановитьВидимостьКолонок()
	
	ДоступныеКолонки = СтрРазделить(ЭлектронныеТрудовыеКнижки.ДоступныеПоляВсехВидовМероприятий(), ",");
	
	ДоступныеПоля = ЭлектронныеТрудовыеКнижки.ИменаДоступныхПолейВидовМероприятий();
	ВидыМероприятий = ОбщегоНазначения.ВыгрузитьКолонку(Мероприятия, "ВидМероприятия", Истина);
	Для Каждого ВидМероприятия Из ВидыМероприятий Цикл 
		ИменаПолей = ДоступныеПоля[ВидМероприятия];
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДоступныеКолонки, СтрРазделить(ИменаПолей, ","), Истина);
	КонецЦикла;
	
	Отбор = Новый Структура("ДатаОтмены", Дата(1, 1, 1));
	Если Мероприятия.НайтиСтроки(Отбор).Количество() = Мероприятия.Количество() Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ДоступныеКолонки, "Идентификатор");
	КонецЕсли;
	
	Колонки = Мероприятия.Выгрузить(Новый Массив).Колонки;
	Для Каждого Колонка Из Колонки Цикл 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
			"Мероприятия" + Колонка.Имя, "Видимость", ДоступныеКолонки.Найти(Колонка.Имя) <> Неопределено);
	КонецЦикла;
	
	Если Не ИспользоватьЗамещение Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "МероприятияКодПоРееструДолжностей", "Видимость", Ложь);
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "МероприятияРазрядКатегория", "Видимость", Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере 
Процедура ЗаявлениеПриИзмененииНаСервере(ИмяПоля)
	
	Заявление = ЭтаФорма[ИмяПоля];
	Если Не ЗначениеЗаполнено(Заявление) Тогда
		ЭтаФорма[ИмяПоля + "Отмена"] = Ложь;
		Возврат;
	КонецЕсли;
	
	ВидЗаявления = ?(ИмяПоля = "ЗаявлениеОПродолжении",
		Перечисления.ВидыЗаявленийОПредоставленииСведенийОТрудовойДеятельности.ВедениеБумажнойТрудовойКнижки,
		Перечисления.ВидыЗаявленийОПредоставленииСведенийОТрудовойДеятельности.СведенияОТрудовойДеятельностиВЭлектроннойФорме);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", Заявление);
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("ВидЗаявления", ВидЗаявления);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники.ЗаявлениеОтозвано КАК ЗаявлениеОтозвано
	               |ИЗ
	               |	Документ.ЗаявлениеОПредоставленииСведенийОТрудовойДеятельности.Сотрудники КАК ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники
	               |ГДЕ
	               |	ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники.Ссылка = &Ссылка
	               |	И ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники.Сотрудник = &Сотрудник
	               |	И ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники.ВидЗаявления = &ВидЗаявления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда 
		ТекстСообщения = НСтр("ru = 'Выбранный документ не содержит заявления данного типа по сотруднику.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		ЭтаФорма[ИмяПоля] = Неопределено;
		ЭтаФорма[ИмяПоля + "Отмена"] = Ложь;
		Возврат;
	КонецЕсли;
	
	ЭтаФорма[ИмяПоля + "Отмена"] = Выборка.ЗаявлениеОтозвано;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораЗаявлений()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("ВедениеБумажнойТрудовойКнижки", Перечисления.ВидыЗаявленийОПредоставленииСведенийОТрудовойДеятельности.ВедениеБумажнойТрудовойКнижки);
	Запрос.УстановитьПараметр("СведенияОТрудовойДеятельностиВЭлектроннойФорме", Перечисления.ВидыЗаявленийОПредоставленииСведенийОТрудовойДеятельности.СведенияОТрудовойДеятельностиВЭлектроннойФорме);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.ЗаявлениеОПредоставленииСведенийОТрудовойДеятельности.Сотрудники КАК ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники
	               |ГДЕ
	               |	ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники.Ссылка.Проведен
	               |	И ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники.ВидЗаявления = &ВедениеБумажнойТрудовойКнижки
	               |	И ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники.Сотрудник = &Сотрудник";
	
	ЗаявленияОПродолжении = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ПараметрыВыбора = Новый Массив;
	ПараметрВыбора = Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(ЗаявленияОПродолжении));
	ПараметрыВыбора.Добавить(ПараметрВыбора);
	
	Элементы.ЗаявлениеОПродолжении.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.ЗаявлениеОПредоставленииСведенийОТрудовойДеятельности.Сотрудники КАК ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники
	               |ГДЕ
	               |	ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники.Ссылка.Проведен
	               |	И ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники.ВидЗаявления = &СведенияОТрудовойДеятельностиВЭлектроннойФорме
	               |	И ЗаявлениеОПредоставленииСведенийОТрудовойДеятельностиСотрудники.Сотрудник = &Сотрудник";
	
	ЗаявленияОПредоставлении = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ПараметрыВыбора = Новый Массив;
	ПараметрВыбора = Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(ЗаявленияОПредоставлении));
	ПараметрыВыбора.Добавить(ПараметрВыбора);
	
	Элементы.ЗаявлениеОПредоставлении.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
КонецПроцедуры

#КонецОбласти