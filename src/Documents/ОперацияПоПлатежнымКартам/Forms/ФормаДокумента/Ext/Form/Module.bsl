#Область ОписаниеПеременных

&НаКлиенте
Перем ОбновитьПодчиненнуюСчетФактуру;

#КонецОбласти

#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПечатьДокументовУНФ.УстановитьОтображениеПодменюПечати(Элементы.ПодменюПечать);
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = УправлениеСвойствамиУНФ.ЗаполнитьДополнительныеПараметры(Объект, "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	ФормыДокументовДеньги.ПриСозданииНаСервере(ЭтаФорма);
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПолучитьСписокВыбораВидовПлатежныхКарт();
	КонецЕсли;
	
	ИспользоватьПодключаемоеОборудование = УправлениеНебольшойФирмойПовтИсп.ИспользоватьПодключаемоеОборудование();
	Если ИспользоватьПодключаемоеОборудование Тогда
		ПолучитьСсылкиНаОборудование();
	КонецЕсли;
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Если ДокументОбъект.ЭтоНовый()
	И НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Если ЗначениеЗаполнено(Параметры.ДокументОснование) Тогда
			ДокументОбъект.Заполнить(Параметры.ДокументОснование);
			ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
		Иначе
			Если НЕ (Параметры.ЗначенияЗаполнения.Свойство("ШаблонДокумента") И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.ШаблонДокумента)) Тогда
				НалогообложениеЗаполненоВОбработкеЗаполнения = ДенежныеСредстваФормы.НалогообложениеЗаполненоВОбработкеЗаполнения(Объект.Ссылка, Параметры.Основание);
				Если НЕ НалогообложениеЗаполненоВОбработкеЗаполнения Тогда
					Объект.СпециальныйНалоговыйРежим = НалогиУНФ.СпециальныйНалоговыйРежим(Объект.Организация,,Объект.Дата);
				КонецЕсли;
			КонецЕсли
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.БанковскийСчет)
			И ТипЗнч(Объект.БанковскийСчет.Владелец) <> Тип("СправочникСсылка.Организации") Тогда
			Объект.БанковскийСчет = Справочники.БанковскиеСчета.ПустаяСсылка();
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.Организация)
			И ЗначениеЗаполнено(Объект.БанковскийСчет)
			И Объект.БанковскийСчет.Владелец <> Объект.Организация Тогда
			Объект.Организация = Объект.БанковскийСчет.Владелец;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
			Если ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
				Если Объект.Организация.БанковскийСчетПоУмолчанию.ВалютаДенежныхСредств = Объект.ВалютаДенежныхСредств Тогда
					Объект.БанковскийСчет = Объект.Организация.БанковскийСчетПоУмолчанию;
				КонецЕсли;
			Иначе
				Объект.БанковскийСчет = Объект.Организация.БанковскийСчетПоУмолчанию;
				Объект.ВалютаДенежныхСредств = Объект.БанковскийСчет.ВалютаДенежныхСредств;
			КонецЕсли;
		Иначе
			Объект.ВалютаДенежныхСредств = Объект.БанковскийСчет.ВалютаДенежныхСредств;
		КонецЕсли;
		// Статья может прийти из шаблона документа и тогда её заполнять не нужно.
		Если НЕ (Параметры.ЗначенияЗаполнения.Свойство("ШаблонДокумента") И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.ШаблонДокумента) И
			Параметры.ЗначенияЗаполнения.Свойство("Статья") И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Статья)) Тогда
			УстановитьСтатьюДДС();
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.ЭквайринговыйТерминал) 
			ИЛИ Объект.Организация <> Объект.ЭквайринговыйТерминал.Организация Тогда
			Объект.ЭквайринговыйТерминал = Справочники.ЭквайринговыеТерминалы.ПолучитьЭквайринговыйТерминалПоУмолчаниюДляОперацииЭквайринга(Объект.БанковскийСчет, Объект.Организация);
		КонецЕсли;
		
		ЭквайринговыйТерминалПриИзмененииНаСервере();
		
		Если ЗначениеЗаполнено(Объект.ВидПлатежнойКарты) Тогда
			ВидПлатежнойКартыПриИзмененииФрагментНаСервере();
		КонецЕсли;
	КонецЕсли;
	
	// Установка реквизитов формы.
	Эквайрер = Объект.ЭквайринговыйТерминал.Эквайрер;
	ЭТИспользоватьБезПодключенияОборудования = Объект.ЭквайринговыйТерминал.ИспользоватьБезПодключенияОборудования;
	
	РасчетКомиссииВОтчетеЭквайера = Объект.ЭквайринговыйТерминал.Договор.РасчетКомиссииВОтчетеЭквайера;
	
	СтруктураПоВалюте = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Объект.Дата, Новый Структура("Валюта", Константы.ВалютаУчета.Получить()));
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка)
	   И НЕ ЗначениеЗаполнено(Параметры.Основание)
	   И НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		ЗаполнитьСтавкуНДСПоОрганизацииНалогообложениеНДС();
	Иначе
		УстановитьВидимостьНалогообложениеНДС();
	КонецЕсли;
	
	Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда
		СтавкаНДСПоУмолчанию = Справочники.СтавкиНДС.СтавкаНДС(Объект.Организация.ВидСтавкиНДСПоУмолчанию, ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса()));
	ИначеЕсли Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
	Иначе
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
	КонецЕсли;
	
	ВалютаДенежныхСредств = Объект.ВалютаДенежныхСредств;
	Если ТипЗнч(Объект.ЭквайринговыйТерминал.Касса) = Тип("СправочникСсылка.Кассы") ТОгда
		КассаККМИспользоватьБезПодключенияОборудования = Истина;
	Иначе
		КассаККМИспользоватьБезПодключенияОборудования = Объект.КассаККМ.ИспользоватьБезПодключенияОборудования;
	КонецЕсли;
	КассаВЭТ = Объект.ЭквайринговыйТерминал.Касса;
	
	ЭквайринговыйТерминалРеквизитФормы = Объект.ЭквайринговыйТерминал;
	
	КэшЗначений = новый Структура;
	КэшЗначений.Вставить("ВерсияПодчиненногоСчетФактуры", Неопределено);
	КэшЗначений.Вставить("МодифицированностьФормы", Ложь);
	
	//Установить надписи счет-фактура и основание
	СчетФактураСсылка = СчетаФактурыУНФ.ПолучитьПодчиненныйСчетФактуру(Объект.Ссылка, Ложь);
	Элементы.СчетФактураНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьСчетФактура(СчетФактураСсылка);
	КэшЗначений.Вставить("СчетФактураСсылка", ?(ТипЗнч(СчетФактураСсылка) = Тип("Структура"), СчетФактураСсылка.Ссылка, Неопределено));
	
	НапечататьЧекДоступность = Ложь;
	
	Кнопка = Элементы.Найти("НапечататьЧек");
	Если Кнопка <> Неопределено Тогда
		
		НапечататьЧекДоступность = Истина;
		
		Кнопка.Доступность = НапечататьЧекДоступность;
		
	КонецЕсли;
	Кнопка = Элементы.Найти("ПредварительныйПросмотрЧека");
	Если Кнопка <> Неопределено Тогда
		
		Кнопка.Доступность = НапечататьЧекДоступность;
		
	КонецЕсли;
	
	
	
	// Заполнение табличной части при вводе документа из рабочего места.
	Если ТипЗнч(Параметры.ЗначенияЗаполнения) = Тип("Структура")
	   И Параметры.ЗначенияЗаполнения.Свойство("ЗаполнитьРасшифровкаПлатежа")
	   И Параметры.ЗначенияЗаполнения.ЗаполнитьРасшифровкаПлатежа Тогда
		
		СтрокаТабличнойЧасти = Объект.РасшифровкаПлатежа[0];
		
		СтрокаТабличнойЧасти.СуммаПлатежа = Объект.СуммаДокумента;
		СтрокаТабличнойЧасти.Курс = ?(
			СтрокаТабличнойЧасти.Курс = 0,
			1,
			СтрокаТабличнойЧасти.Курс
		);
		
		СтрокаТабличнойЧасти.Кратность = ?(
			СтрокаТабличнойЧасти.Кратность = 0,
			1,
			СтрокаТабличнойЧасти.Кратность
		);
		
		СтрокаТабличнойЧасти.СуммаРасчетов = ЦенообразованиеСервер.ПересчитатьИзВалютыВВалюту(
			СтрокаТабличнойЧасти.СуммаПлатежа,
			Курс,
			СтрокаТабличнойЧасти.Курс,
			Кратность,
			СтрокаТабличнойЧасти.Кратность
		);
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
			СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
		КонецЕсли;
		
		СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.СуммаПлатежа - (СтрокаТабличнойЧасти.СуммаПлатежа) / ((СтрокаТабличнойЧасти.СтавкаНДС.Ставка + 100) / 100);
		
	КонецЕсли;
	
	УстановитьВидимостьЭлементовВЗависимостиОтВидаОперации();
	УстановитьВидимостьРеквизитовРасчетов();
	УстановитьВидимостьИДоступность();
	РасчетыРаботаСФормамиВызовСервера.УстановитьСвязьПараметровВыбораПоОрганизации(ЭтотОбъект);
	
	АвтоподборКонтактов.ПодключитьОбработчикиСобытияАвтоподбор(ЭтотОбъект);
	
	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	// Остатки ДС и взаиморасчетов на форме
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	// Конец Остатки ДС и взаиморасчетов на форме
	
	УстановитьСвязиПараметровВыбораДоступныеТипы();
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Справочники.Контрагенты.ДанныеКИКонтрагентаДляВыбораНаФорме(Объект.Контрагент, Элементы, ПоляКИДляОтправкиЧека());
	КонецЕсли;
	
	// МобильныйКлиент
	МобильныйКлиентУНФ.НастроитьФормуОбъектаМобильныйКлиент(Элементы);
	// Конец МобильныйКлиент
	
КонецПроцедуры // ПриСозданииНаСервере()

// Процедура - обработчик события ОбработкаОповещения формы.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ИмяСобытия = "ОбновлениеТекстаПроСчетФактуру" Тогда
		
		ПредставлениеЗаголовка = РаботаСФормойДокументаКлиент.ПредставлениеЗаголовкаПодчиненногоСчетаФактуры(Объект.Ссылка, Источник, Параметр, КэшЗначений);
		Если ПредставлениеЗаголовка <> Неопределено Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СчетФактураНадпись", "Заголовок", ПредставлениеЗаголовка);
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_Контрагент"
		И ЗначениеЗаполнено(Параметр)
		И Объект.Контрагент = Параметр Тогда
		
		УстановитьВидимостьРеквизитовРасчетов();
		
	КонецЕсли;
	
КонецПроцедуры //ОбработкаОповещения()

// Процедура - обработчик события ПриЧтенииНаСервере.
//
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	ПолучитьСписокВыбораВидовПлатежныхКарт();
	
	УстановитьДоступностьКнопок();
	
	КассаВЭТ = Объект.ЭквайринговыйТерминал.Касса;
	ЭквайринговыйТерминалРеквизитФормы = Объект.ЭквайринговыйТерминал;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры // ПриЧтенииНаСервере()

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ДополнительныеСвойства.Свойство("ВерсияПодчиненногоСчетФактуры", КэшЗначений.ВерсияПодчиненногоСчетФактуры);
	
	УстановитьДоступностьКнопок();
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

// Процедура - обработчик события ПослеЗаписи.
//
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	
	Если КэшЗначений.МодифицированностьФормы = Истина
		И КэшЗначений.ВерсияПодчиненногоСчетФактуры = "1.6.11" Тогда
		
		ТекстВопроса = НСтр("ru = 'В накладную были внесены изменения.
			|Требуется самостоятельно поправить подчиненный документ счет-фактура'");
		
		ПоказатьПредупреждение(, ТекстВопроса, 0, НСтр("ru ='Счет-фактура'"));
		
	КонецЕсли;
	
	Если СозданПоКомандеИзФормыСписка Тогда
		ПараметрыОповещения = Новый Структура("Ссылка", Объект.Ссылка);
		Оповестить("Запись_ОперацияПоПлатежнымКартам", ПараметрыОповещения);
	КонецЕсли;
	
	// Оповещение об оплате.
	ОповеститьОбОплатеСчета = Ложь;
	ОповеститьОбОплатеЗаказа = Ложь;
	
	Для каждого ТекСтрока Из Объект.РасшифровкаПлатежа Цикл
		ОповеститьОбОплатеСчета = ?(
			ОповеститьОбОплатеСчета,
			ОповеститьОбОплатеСчета,
			ЗначениеЗаполнено(ТекСтрока.СчетНаОплату)
		);
		ОповеститьОбОплатеЗаказа = ?(
			ОповеститьОбОплатеЗаказа,
			ОповеститьОбОплатеЗаказа,
			ЗначениеЗаполнено(ТекСтрока.Заказ)
		);
	КонецЦикла;
	
	Если ОповеститьОбОплатеСчета Тогда
		ОповеститьОбИзменении(Тип("ДокументСсылка.СчетНаОплату"));
	КонецЕсли;
	
	Если ОповеститьОбОплатеЗаказа Тогда
		Оповестить("ОповещениеОбОплатеЗаказа");
	КонецЕсли;
	
	Оповестить("ОповещениеОбИзмененииДолга");
	
КонецПроцедуры // ПослеЗаписи()

// Процедура - обработчик события ПриОткрытии.
// В процедуре устанавливается текущая страница в зависимости от операции.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьТекущуюСтраницу();
	
	УправлениеНебольшойФирмойКлиент.ЗаполнитьТелефонАдресЭП(ЭтаФорма, ТелефонАдресЭП);
	УправлениеНебольшойФирмойКлиент.УстановитьДоступностьТелефонАдресЭП(ЭтаФорма, ТелефонАдресЭП);
	УстановитьВидимостьРеквизитовРасшифровкиПлатежа(Элементы, Объект.СпособОплаты);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьДоступностьПечатиЧека();
	
КонецПроцедуры // ПриОткрытии()

//Процедура - обработчик события формы ПередЗаписью
//
&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	УправлениеНебольшойФирмойКлиент.УстановитьДоступностьТелефонАдресЭП(ЭтаФорма, ТелефонАдресЭП, Истина);
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ОценкаПроизводительностиКлиент.ЗамерВремени("Проведение"+ РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	КонецЕсли;
	// СтандартныеПодсистемы.ОценкаПроизводительности
	
	КэшЗначений.МодифицированностьФормы = Модифицированность;
	
КонецПроцедуры

// Процедура обработчик события ПередЗаписьюНаСервере.
//
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ТекстСообщения = "";
		Справочники.ДоговорыКонтрагентов.ПроверитьСоответствиеДоговораУсловиямДокумента(
			ТекстСообщения,
			,
			Объект.Ссылка,
			Объект.Организация,
			Объект.Контрагент,
			Отказ,
			Объект.ВидОперации,
			Объект.РасшифровкаПлатежа);
		
		Если ТекстСообщения <> "" Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ?(Отказ, НСтр("ru = 'Документ не проведен! '") + ТекстСообщения, ТекстСообщения);
			Сообщение.Сообщить();
			
			Если Отказ Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Объект.ВидОперации = ВидОперацииВозврат Тогда
			НеСоответствуютРеквизитыОплаты = Ложь;
			Если ТипЗнч(Объект.ОперацияПоПлатежнымКартамШапка) = Тип("ДокументСсылка.ЧекККМ") Тогда
				
				ПараметрыПоиска = Новый Структура("ВидПлатежнойКарты, НомерПлатежнойКарты, ЭквайринговыйТерминал",
					Объект.ВидПлатежнойКарты, Объект.НомерПлатежнойКарты, Объект.ЭквайринговыйТерминал
				);
				
				МассивСтрок = Объект.ОперацияПоПлатежнымКартамШапка.БезналичнаяОплата.НайтиСтроки(ПараметрыПоиска);
				Если МассивСтрок.Количество() = 0 Тогда
				
					Если Объект.ЭквайринговыйТерминал <> Объект.ОперацияПоПлатежнымКартамШапка.ЭквайринговыйТерминал Тогда
						НеСоответствуютРеквизитыОплаты = Истина;
					Иначе
						ПараметрыПоиска = Новый Структура("ВидПлатежнойКарты, НомерПлатежнойКарты", Объект.ВидПлатежнойКарты, Объект.НомерПлатежнойКарты);
						
						МассивСтрок = Объект.ОперацияПоПлатежнымКартамШапка.БезналичнаяОплата.НайтиСтроки(ПараметрыПоиска);
						Если МассивСтрок.Количество() = 0 Тогда
							НеСоответствуютРеквизитыОплаты = Истина;
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
				
			Иначе
				Если ЗначениеЗаполнено(Объект.ОперацияПоПлатежнымКартамШапка) Тогда
					
					ВидПлатежнойКартыОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ОперацияПоПлатежнымКартамШапка, "ВидПлатежнойКарты");
					НомерПлатежнойКартыОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ОперацияПоПлатежнымКартамШапка, "НомерПлатежнойКарты");
					ТермниалОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ОперацияПоПлатежнымКартамШапка, "ЭквайринговыйТерминал");
					
					Если Объект.ВидПлатежнойКарты <> ВидПлатежнойКартыОснования ИЛИ Объект.НомерПлатежнойКарты <> НомерПлатежнойКартыОснования
						ИЛИ Объект.ЭквайринговыйТерминал <> ТермниалОснования Тогда
					НеСоответствуютРеквизитыОплаты = Истина;
				КонецЕсли;
					
				Иначе
					НеСоответствуютРеквизитыОплаты = Истина;
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Объект.ОперацияПоПлатежнымКартамШапка) И НеСоответствуютРеквизитыОплаты Тогда
				ТекстСообщения = НСтр("ru = 'Для информации: в текущем документе терминал, вид или номер карты не совпадают с реквизитами в документе оплаты.'");
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = ТекстСообщения;
				Сообщение.Сообщить();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры // ПередЗаписьюНаСервере()

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	
КонецПроцедуры

// Процедура обработчик события ОбработкаПроверкиЗаполненияНаСервере.
//
&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область СчетФактураИОснование

&НаКлиенте
Процедура СчетФактураНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СчетаФактурыУНФКлиент.ОткрытьСчетФактуру(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ФормыДокументовДеньгиКлиент.ДокументОснованиеНадписьОбработкаНавигационнойСсылки(ЭтаФорма, Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборТипаОснованияЗавершение(ВыбранноеИмяФормы, Параметры) Экспорт
	
	Если ВыбранноеИмяФормы<>Неопределено Тогда
		
		СтруктураПараметровОтбора = Новый Структура();
		Для каждого элОтбора Из ПараметрыВыбораДокументаОснования Цикл
			ИмяПоляОтбора = СтрЗаменить(элОтбора.Имя,"Отбор.","");
			СтруктураПараметровОтбора.Вставить(ИмяПоляОтбора, элОтбора.Значение);
		КонецЦикла;
		Если СтрНайти(ВыбранноеИмяФормы.Значение, "Документ.ПриходнаяНакладная.") <> 0 Тогда
			ИмяПоляОтбора = "ВидОперации";
			СтруктураПараметровОтбора.Вставить(ИмяПоляОтбора, ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя"));
		КонецЕсли;
		Если НЕ Объект.Контрагент.Пустая() Тогда
			СтруктураПараметровОтбора.Вставить("Контрагент", Объект.Контрагент);
		КонецЕсли;
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыбратьОснованиеЗавершение", ЭтотОбъект);
		ПараметрыОткрытияФормы = Новый Структура("Отбор", СтруктураПараметровОтбора);
		ОткрытьФорму(ВыбранноеИмяФормы.Значение, ПараметрыОткрытияФормы, ЭтотОбъект, ,,,ОповещениеОЗакрытии);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОснованиеЗавершение(ВыбранноеЗначение, Параметры) Экспорт

	Если ВыбранноеЗначение<>Неопределено Тогда
		Объект.ДокументОснование = ВыбранноеЗначение;
		Элементы.ДокументОснованиеНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(ВыбранноеЗначение);
		Модифицированность = Истина;
		
		ФормыДокументовДеньгиКлиент.ЗаполнитьПоОснованиюНачало(ЭтаФорма);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

// Процедура - обработчик события ПриИзменении поля ввода Контрагент.
//
&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	СтруктураДанные = ПолучитьДанныеКонтрагентИДокОплатыШапкаПриИзменении(Объект.Контрагент, Объект.Организация, Объект.Дата);
	
	ВладелецДоговораПоУмолчанию = Объект.Контрагент;
	ДоговорКонтрагентаПоУмолчанию = СтруктураДанные.Договор;
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда 
		
		Объект.РасшифровкаПлатежа[0].Договор = СтруктураДанные.Договор;
		
		Если ЗначениеЗаполнено(Объект.РасшифровкаПлатежа[0].Договор) Тогда
			Объект.РасшифровкаПлатежа[0].Курс = ?(
				СтруктураДанные.ДоговорВалютаКурсКратность.Курс = 0,
				1,
				СтруктураДанные.ДоговорВалютаКурсКратность.Курс
			);
			Объект.РасшифровкаПлатежа[0].Кратность = ?(
				СтруктураДанные.ДоговорВалютаКурсКратность.Кратность = 0,
				1,
				СтруктураДанные.ДоговорВалютаКурсКратность.Кратность
			);
		КонецЕсли;
		
		Объект.РасшифровкаПлатежа[0].Курс = ?(
			Объект.РасшифровкаПлатежа[0].Курс = 0,
			1,
			Объект.РасшифровкаПлатежа[0].Курс
		);
		Объект.РасшифровкаПлатежа[0].Кратность = ?(
			Объект.РасшифровкаПлатежа[0].Кратность = 0,
			1,
			Объект.РасшифровкаПлатежа[0].Кратность
		);
		
		Объект.РасшифровкаПлатежа[0].СуммаРасчетов = ВалютыУНФКлиентСервер.Пересчитать(
			Объект.РасшифровкаПлатежа[0].СуммаПлатежа,
			Курс,
			Объект.РасшифровкаПлатежа[0].Курс,
			Кратность,
			Объект.РасшифровкаПлатежа[0].Кратность
		);
		
	КонецЕсли;
	
	Если ТипЗнч(Объект.ОперацияПоПлатежнымКартамШапка) <> Тип("ДокументСсылка.ЧекККМ")
		ИЛИ (ЗначениеЗаполнено(СтруктураДанные.КонтрагентВДокументеОплаты)
		И СтруктураДанные.КонтрагентВДокументеОплаты <> Объект.Контрагент) Тогда
		Объект.ОперацияПоПлатежнымКартамШапка = Неопределено;
	КонецЕсли;
	
КонецПроцедуры // КонтрагентПриИзменении()

// Процедура - обработчик события ВидОперацииПриИзменении.
// Управляет страницами при изменении вида операции документа.
//
&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	ВидОперацииПередИзменением = ВидОперации;
	ВидОперации = Объект.ВидОперации;
	
	Если ВидОперации <> ВидОперацииПередИзменением Тогда
		УстановитьТекущуюСтраницу();
		ОчиститьРеквизитыНеОтносящиесяКОперации();
		ВидОперацииПриИзмененииНаСервере();
		Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
			Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВидОперацииПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
		
		// Обновление курсов расчетов
		СтруктураДанные.Вставить("ОбновитьКурсыРасчетов", (Объект.РасшифровкаПлатежа.Количество() > 0 И Объект.РасшифровкаПлатежа.Итог("Курс") > 0 И
			Объект.РасшифровкаПлатежа.Количество() <> Объект.РасшифровкаПлатежа.Итог("Курс")));
		СтруктураДанные.Вставить("ОбновитьКурсыРасчетовТекстВопроса", НСтр("ru = 'Изменилась дата документа. Установить курс расчетов в соответствии с курсом валюты договора?'"));
		// Конец Обновление курсов расчетов
		
		ТекстСообщения = НСтр("ru = 'Изменился курс валюты кассы. Пересчитать суммы документа?'");
		ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств(СтруктураДанные, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры // ДатаПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода Организация.
// В процедуре осуществляется очистка номера документа,
// а также производится установка параметров функциональных опций формы.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	// Обработка события изменения организации.
	Объект.Номер = "";
	СтруктураДанные = ПолучитьДанныеОрганизацияПриИзменении();
	Компания = СтруктураДанные.Компания;
	
	Объект.БанковскийСчет = СтруктураДанные.БанковскийСчет;
	
	ВалютаДенежныхСредствПередИзменением = ВалютаДенежныхСредств;
	Объект.ВалютаДенежныхСредств = СтруктураДанные.ВалютаДенежныхСредств;
	ВалютаДенежныхСредств = СтруктураДанные.ВалютаДенежныхСредств;
	
	Объект.ЭквайринговыйТерминал = СтруктураДанные.ЭквайринговыйТерминал;
	ЭквайринговыйТерминалПриИзмененииНаСервере();
	БанковскийСчетПриИзменении();
	
	// Если валюта не изменилась, то ничего не делаем.
	Если ВалютаДенежныхСредств = ВалютаДенежныхСредствПередИзменением Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.РасшифровкаПлатежа.Количество() > 0 Тогда
		Объект.РасшифровкаПлатежа[0].Договор = Неопределено;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = 'Изменилась валюта банковского счета. Пересчитать суммы документа?'");
	ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств(СтруктураДанные, ТекстСообщения);
	УстановитьНастройкиУчетаВНалогообложении();
	
КонецПроцедуры // ОрганизацияПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода Валюта
// Осуществляет пересчет табличной части РасшифровкаПлатежа.
//
&НаКлиенте
Процедура ВалютаДенежныхСредствПриИзменении(Элемент)
	
	ВалютаДенежныхСредствПередИзменением = ВалютаДенежныхСредств;
	ВалютаДенежныхСредств = Объект.ВалютаДенежныхСредств;
	
	// Если валюта не изменилась, то ничего не делаем.
	Если ВалютаДенежныхСредств = ВалютаДенежныхСредствПередИзменением Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанные = ПолучитьДанныеВалютаДенежныхСредствПриИзменении(
		Объект.Дата,
		Объект.ВалютаДенежныхСредств
	);
	
	ТекстСообщения = НСтр("ru = 'Пересчитать суммы документа?'");
	ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств(СтруктураДанные, ТекстСообщения);
	
КонецПроцедуры // ВалютаДенежныхСредствПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода СуммаДокумента.
//
&НаКлиенте
Процедура СуммаДокументаПриИзменении(Элемент)
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
	
		СтрокаТабличнойЧасти = Объект.РасшифровкаПлатежа[0];
		
		СтрокаТабличнойЧасти.СуммаПлатежа = Объект.СуммаДокумента;
		СтрокаТабличнойЧасти.Курс = ?(
			СтрокаТабличнойЧасти.Курс = 0,
			1,
			СтрокаТабличнойЧасти.Курс
		);
		
		СтрокаТабличнойЧасти.Кратность = ?(
			СтрокаТабличнойЧасти.Кратность = 0,
			1,
			СтрокаТабличнойЧасти.Кратность
		);
		
		СтрокаТабличнойЧасти.СуммаРасчетов = ВалютыУНФКлиентСервер.Пересчитать(
			СтрокаТабличнойЧасти.СуммаПлатежа,
			Курс,
			СтрокаТабличнойЧасти.Курс,
			Кратность,
			СтрокаТабличнойЧасти.Кратность
		);
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
			СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
		КонецЕсли;
		
		РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
		
	КонецЕсли;
	
	РассчитатьСуммуУчета();
	РассчитатьСуммуКомиссии();
	
КонецПроцедуры // СуммаДокументаПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода НалогообложениеНДС.
//
&НаКлиенте
Процедура НалогообложениеНДСПриИзменении(Элемент)
	
	ЗаполнитьСтавкуНДСПоНалогообложениеНДС();
	
КонецПроцедуры // НалогообложениеНДСПриИзменении()

// Получает набор данных с сервера для процедуры ВалютаДенежныхСредствПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеБанковскийСчетПриИзменении(Дата, БанковскийСчет)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"ВалютаКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
			Дата,
			Новый Структура("Валюта", БанковскийСчет.ВалютаДенежныхСредств)
		)
	);
	СтруктураДанные.Вставить(
		"ВалютаДенежныхСредств",
		БанковскийСчет.ВалютаДенежныхСредств
	);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеБанковскийСчетПриИзменении()

// Процедура - обработчик события ПриИзменении поля ЭквайринговыйТерминал на сервере.
//
&НаСервере
Процедура ЭквайринговыйТерминалПриИзмененииНаСервере()
	
	Объект.БанковскийСчет = Объект.ЭквайринговыйТерминал.БанковскийСчетЭквайринг;
	
	ЭТКасса = Объект.ЭквайринговыйТерминал.Касса;
	Если ТипЗнч(ЭТКасса) = Тип("СправочникСсылка.КассыККМ") ТОгда
		Объект.КассаККМ = Объект.ЭквайринговыйТерминал.Касса;
		КассаККМИспользоватьБезПодключенияОборудования = Объект.КассаККМ.ИспользоватьБезПодключенияОборудования;
	Иначе
		Объект.Касса = Объект.ЭквайринговыйТерминал.Касса;
		КассаККМИспользоватьБезПодключенияОборудования = Истина;
	КонецЕсли;
	КассаВЭТ = Объект.ЭквайринговыйТерминал.Касса;
	
	Эквайрер = Объект.ЭквайринговыйТерминал.Эквайрер;
	ЭТИспользоватьБезПодключенияОборудования = Объект.ЭквайринговыйТерминал.ИспользоватьБезПодключенияОборудования;
	
	РасчетКомиссииВОтчетеЭквайера = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЭквайринговыйТерминал.Договор, "РасчетКомиссииВОтчетеЭквайера");
	
	Если РасчетКомиссииВОтчетеЭквайера Тогда
		Объект.СуммаКомиссииПоДоговору = 0;
		Объект.ПроцентКомиссии = 0;
	КонецЕсли;
	
	ПолучитьСсылкиНаОборудование();
	ПолучитьСписокВыбораВидовПлатежныхКарт();
	
	УстановитьВидимостьЭлементовВЗависимостиОтВидаОперации();
	
КонецПроцедуры // ЭквайринговыйТерминалПриИзмененииНаСервере()

// Процедура - обработчик события ПриИзменении поля ввода ЭквайринговыйТерминал.
//
&НаКлиенте
Процедура ЭквайринговыйТерминалПриИзменении(Элемент)
	
	ЭквайринговыйТерминалРеквизитФормыПередИзменением = ЭквайринговыйТерминалРеквизитФормы;
	ЭквайринговыйТерминалРеквизитФормы = Объект.ЭквайринговыйТерминал;
	
	Если ЭквайринговыйТерминалРеквизитФормы <> ЭквайринговыйТерминалРеквизитФормыПередИзменением Тогда
		ЭквайринговыйТерминалПриИзмененииНаСервере();
		ВидПлатежнойКартыПриИзмененииФрагментНаКлиенте();
		БанковскийСчетПриИзменении();
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода БанковскийСчет.
//
&НаКлиенте
Процедура БанковскийСчетПриИзменении()
	
	ВалютаДенежныхСредствПередИзменением = ВалютаДенежныхСредств;
	
	СтруктураДанные = ПолучитьДанныеБанковскийСчетПриИзменении(
		Объект.Дата,
		Объект.БанковскийСчет
	);
	
	Объект.ВалютаДенежныхСредств = СтруктураДанные.ВалютаДенежныхСредств;
	ВалютаДенежныхСредств = СтруктураДанные.ВалютаДенежныхСредств;
	
	// Если валюта не изменилась, то ничего не делаем.
	Если ВалютаДенежныхСредств = ВалютаДенежныхСредствПередИзменением Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = 'Изменилась валюта банковского счета. Пересчитать суммы документа?'");
	ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств(СтруктураДанные, ТекстСообщения);
	
КонецПроцедуры // БанковскийСчетПриИзменении()

&НаКлиенте
Процедура ОстатокВзаиморасчетовНажатие(Элемент)
	
	РаботаСФормойДокументаКлиент.ОткрытьОтчетВзаиморасчетыСКонтрагентом(Объект.Контрагент);
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ОбработкаНавигационнойСсылки");
	
КонецПроцедуры

&НаКлиенте
Процедура ОстатокВзаиморасчетовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСФормойДокументаКлиент.ОткрытьОтчетВзаиморасчетыСКонтрагентом(Объект.Контрагент);
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ОбработкаНавигационнойСсылки");
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "НачалоВыбора");
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОплатыПриИзменении(Элемент)
	
	УстановитьВидимостьРеквизитовРасшифровкиПлатежа(Элементы, Объект.СпособОплаты);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьРеквизитовРасшифровкиПлатежа(Элементы, СпособОплаты)
	
	Если СпособОплаты = ПредопределенноеЗначение("Перечисление.СпособыОплатыЭквайринг.ОплатаОнлайн") Тогда
		Элементы.ВидПлатежнойКарты.Заголовок = НСтр("ru = 'Вид оплаты'");
		Элементы.НомерПлатежнойКарты.Заголовок = НСтр("ru = 'Номер транзакции'");
		Элементы.НомерЧекаЭТ.Видимость = Ложь;
		Элементы.СсылочныйНомер.Видимость = Ложь;
		Элементы.ОплатитьКартой.Видимость = Ложь;
	Иначе
		Элементы.ВидПлатежнойКарты.Заголовок = НСтр("ru = 'Вид карты'");
		Элементы.НомерПлатежнойКарты.Заголовок = НСтр("ru = '№ карты'");
		Элементы.НомерЧекаЭТ.Видимость = Истина;
		Элементы.СсылочныйНомер.Видимость = Истина;
		Элементы.ОплатитьКартой.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРасшифровкаПлатежа

// Процедура - обработчик события ПередУдалением табличной части РасшифровкаПлатежа.
//
&НаКлиенте
Процедура РасшифровкаПлатежаПередУдалением(Элемент, Отказ)
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаПередУдалением()

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаДоговор.
// Устанавливает курс и кратность валюты договора.
//
&НаКлиенте
Процедура РасшифровкаПлатежаДоговорПриИзменении(Элемент)
	
	ОбработатьИзменениеДоговораКонтрагента();
	
КонецПроцедуры // РасшифровкаПлатежаДоговорПриИзменении()

// Процедура - обработчик события НачалоВыбора поля ввода РасшифровкаПлатежаДоговор.
// Устанавливает курс и кратность валюты договора.
//
&НаКлиенте
Процедура РасшифровкаПлатежаДоговорНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбработатьНачалоВыбораДоговораКонтрагента(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаВидРасчетов.
// Очищает реквизит документ если тип расчетов - "Аванс".
//
&НаКлиенте
Процедура РасшифровкаПлатежаПризнакАвансаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЭквайринга.ПоступлениеОплатыОтПокупателя") Тогда
		Если СтрокаТабличнойЧасти.ПризнакАванса Тогда
			СтрокаТабличнойЧасти.Документ = Неопределено;
		Иначе
			СтрокаТабличнойЧасти.ДокументПланирования = Неопределено;
		КонецЕсли;
	Иначе
		Если ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.ОперацияПоПлатежнымКартам")
			ИЛИ ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.ЧекККМ") Тогда
			СтрокаТабличнойЧасти.ПризнакАванса = Истина;
			ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Для данного типа документа расчетов признак аванса всегда установлен!'"));
		ИначеЕсли ТипЗнч(СтрокаТабличнойЧасти.Документ) <> Тип("ДокументСсылка.Взаимозачет") Тогда
			СтрокаТабличнойЧасти.ПризнакАванса = Ложь;
			ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Для данного типа документа расчетов нельзя установить признак аванса!'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаПризнакАвансаПриИзменении()

// Процедура - обработчик события НачалоВыбора поля ввода РасшифровкаПлатежаДокумент.
// Передает в параметры текущее значение реквизита.
//
&НаКлиенте
Процедура РасшифровкаПлатежаДокументНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти.ПризнакАванса
		И Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЭквайринга.ПоступлениеОплатыОтПокупателя")
		Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Для вида расчета с признаком ""Предоплата"" документом расчетов будет текущий!'"));
		
	Иначе
		
		ЭтоРасчетыСПокупателями = Истина;
		
		СтруктураОтбор = Новый Структура();
		СтруктураОтбор.Вставить("Контрагент", Объект.Контрагент);
		
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Договор) Тогда
			СтруктураОтбор.Вставить("Договор", СтрокаТабличнойЧасти.Договор);
		КонецЕсли;
		Если НЕ УчетПоКомпании Тогда
			СтруктураОтбор.Вставить("Организация", Объект.Организация);
		КонецЕсли;
		
		Если Объект.ВидОперации = ВидОперацииОплата Тогда
			СтрокаИсключений = "ОперацияПоПлатежнымКартам, ПоступлениеВКассу, ПоступлениеНаСчет";
		Иначе
			СтрокаИсключений = "РасходИзКассы, РасходСоСчета";
		КонецЕсли;
		
		СтруктураПараметры = Новый Структура;
		СтруктураПараметры.Вставить("Отбор", СтруктураОтбор);
		СтруктураПараметры.Вставить("ЭтоРасчетыСПокупателями", ЭтоРасчетыСПокупателями);
		СтруктураПараметры.Вставить("ТипДокумента", ТипЗнч(Объект.Ссылка));
		СтруктураПараметры.Вставить("СтрокаИсключений", СтрокаИсключений);
		
		ОткрытьФорму("ОбщаяФорма.ФормаВыборДокументаРасчетов", СтруктураПараметры, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаДокументНачалоВыбора()

// Процедура - обработчик события ОбработкаВыбора поля ввода РасшифровкаПлатежаДокумент.
//
&НаКлиенте
Процедура РасшифровкаПлатежаДокументОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработатьВыборДокументаРасчетов(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДокументСоздание(Элемент, СтандартнаяОбработка)
	
	Если Элементы.РасшифровкаПлатежа.ТекущиеДанные <> Неопределено Тогда
		Если ТипЗнч(Элементы.РасшифровкаПлатежа.ТекущиеДанные.Документ) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			ПараметрыВыбораМассив = Новый Массив;
			ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("Отбор.ВидОперации", ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд")));
			Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораМассив);
		Иначе
			Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(Новый Массив);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля в РасшифровкаПлатежаСуммаРасчетов.
// Рассчитывает сумму платежа.
//
&НаКлиенте
Процедура РасшифровкаПлатежаСуммаРасчетовПриИзменении(Элемент)
	
	РассчитатьСуммуПлатежа(Элементы.РасшифровкаПлатежа.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
		РассчитатьСуммуКомиссии();
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаСуммаРасчетовПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаКурс.
// Рассчитывает сумму платежа.
//
&НаКлиенте
Процедура РасшифровкаПлатежаКурсПриИзменении(Элемент)
	
	РассчитатьСуммуПлатежа(Элементы.РасшифровкаПлатежа.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаКурсПриИзменении()

&НаКлиенте
Процедура РасшифровкаПлатежаКурсНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РасчетыРаботаСФормамиКлиент.ПредоплатаКурсНачалоВыбора(ЭтаФорма, Элемент, ДанныеВыбора, СтандартнаяОбработка, "РасшифровкаПлатежа");
	
КонецПроцедуры


// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаКратность.
// Рассчитывает сумму платежа.
//
&НаКлиенте
Процедура РасшифровкаПлатежаКратностьПриИзменении(Элемент)
	
	РассчитатьСуммуПлатежа(Элементы.РасшифровкаПлатежа.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаКратностьПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаСуммаПлатежа.
// Рассчитывает курс и кратность валюты расчетов и сумму НДС.
//
&НаКлиенте
Процедура РасшифровкаПлатежаСуммаПлатежаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	СтрокаТабличнойЧасти.Курс = ?(
		СтрокаТабличнойЧасти.Курс = 0,
		1,
		СтрокаТабличнойЧасти.Курс
	);
	СтрокаТабличнойЧасти.Кратность = ?(
		СтрокаТабличнойЧасти.Кратность = 0,
		1,
		СтрокаТабличнойЧасти.Кратность
	);
	
	СтрокаТабличнойЧасти.Курс = ?(
		СтрокаТабличнойЧасти.СуммаРасчетов = 0,
		1,
		СтрокаТабличнойЧасти.СуммаПлатежа / СтрокаТабличнойЧасти.СуммаРасчетов * Курс
	);
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
		СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
	КонецЕсли;
	
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	
КонецПроцедуры // РасшифровкаПлатежаСуммаПлатежаПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаСтавкаНДС.
// Рассчитывает сумму НДС.
//
&НаКлиенте
Процедура РасшифровкаПлатежаСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	
КонецПроцедуры // РасшифровкаПлатежаСтавкаНДСПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаДокумент.
//
&НаКлиенте
Процедура РасшифровкаПлатежаДокументПриИзменении(Элемент)
	
	ВыполнитьДействияПриИзмененииДокументаРасчетов();
	
КонецПроцедуры // РасшифровкаПлатежаДокументПриИзменении() 

// Процедура - обработчик события ПриОкончанииРедактирования табличной части РасшифровкаПлатежа.
//
&НаКлиенте
Процедура РасшифровкаПлатежаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Объект.СуммаКомиссииПоДоговору = Объект.ПроцентКомиссии * Объект.СуммаДокумента / 100;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ВидПлатежнойКарты.
//
&НаКлиенте
Процедура ВидПлатежнойКартыПриИзменении(Элемент)
	
	ВидПлатежнойКартыПриИзмененииФрагментНаКлиенте();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ПроцентКомиссии.
//
&НаКлиенте
Процедура ПроцентКомиссииПриИзменении(Элемент)
	
	РассчитатьСуммуКомиссии();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ВидПлатежнойКарты.
//
&НаКлиенте
Процедура ВидПлатежнойКартыПриИзмененииФрагментНаКлиенте()
	
	СтрокиКомиссии = ТаблицаВидовКартИПроцентовКомиссии.НайтиСтроки(Новый Структура("ВидПлатежнойКарты", Объект.ВидПлатежнойКарты));
	
	Если СтрокиКомиссии.Количество() > 0 Тогда
		
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЭквайринга.ПоступлениеОплатыОтПокупателя") Тогда
			Объект.ПроцентКомиссии = СтрокиКомиссии[0].ПроцентКомиссии;
		ИначеЕсли ЭтоОтменаОплаты(Объект.Дата, Объект.НомерСменыККМ, Объект.ОперацияПоПлатежнымКартамШапка) Тогда
			Объект.ПроцентКомиссии = СтрокиКомиссии[0].ПроцентКомиссииПриОтмене;
		Иначе // Это возврат
			Объект.ПроцентКомиссии = СтрокиКомиссии[0].ПроцентКомиссииПриВозврате;
		КонецЕсли;
		
		Объект.СуммаКомиссииПоДоговору =Объект.ПроцентКомиссии * Объект.СуммаДокумента / 100;
		
	Иначе
		Объект.ПроцентКомиссии = 0;
		Объект.СуммаКомиссииПоДоговору = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВидПлатежнойКартыПриИзмененииФрагментНаСервере()
	
	СтрокиКомиссии = ТаблицаВидовКартИПроцентовКомиссии.НайтиСтроки(Новый Структура("ВидПлатежнойКарты", Объект.ВидПлатежнойКарты));
	
	Если СтрокиКомиссии.Количество() > 0 Тогда
		
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЭквайринга.ПоступлениеОплатыОтПокупателя") Тогда
			Объект.ПроцентКомиссии = СтрокиКомиссии[0].ПроцентКомиссии;
		ИначеЕсли ЭтоОтменаОплаты(Объект.Дата, Объект.НомерСменыККМ, Объект.ОперацияПоПлатежнымКартамШапка) Тогда
			Объект.ПроцентКомиссии = СтрокиКомиссии[0].ПроцентКомиссииПриОтмене;
		Иначе // Это возврат
			Объект.ПроцентКомиссии = СтрокиКомиссии[0].ПроцентКомиссииПриВозврате;
		КонецЕсли;
		
		Объект.СуммаКомиссииПоДоговору =Объект.ПроцентКомиссии * Объект.СуммаДокумента / 100;
		
	Иначе
		Объект.ПроцентКомиссии = 0;
		Объект.СуммаКомиссииПоДоговору = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ТекущиеДанные = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено
			И НЕ Копирование Тогда
			РасчетыРаботаСФормамиКлиент.ЗаполнитьДоговорВНовойСтрокеРасшифровки(ЭтотОбъект, ТекущиеДанные);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура вызывается при нажатии кнопки "НапечататьЧек" командной панели.
//
&НаКлиенте
Процедура НапечататьЧек(Команда)
	
	Если Объект.ВалютаДенежныхСредств <> УправлениеНебольшойФирмойПовтИсп.ПолучитьНациональнуюВалюту() Тогда
		ТекстСообщения = НСтр("ru = 'Валюта оплаты отличается от национальной!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат
	КонецЕсли;
	
	Если Объект.НомерЧекаККМ <> 0 Тогда
		ТекстСообщения = НСтр("ru = 'Чек уже пробит на оборудовании!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если ПечатьДокументовУНФКлиент.ПечатьЧекаНевозможна(ЭтотОбъект, НСтр("ru = 'Не удалось провести документ'")) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(КассаВЭТ) = Тип("СправочникСсылка.Кассы") ТОгда
		
		Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ПодключенияФискальногоРегистратораЗавершение", ЭтотОбъект);
			МассивОборудования = Новый Массив;
			МассивОборудования.Добавить("ФискальныйРегистратор");
			МассивОборудования.Добавить("ККТ");
			МассивОборудования.Добавить("ПринтерЧеков");
			МенеджерОборудованияКлиент.ПредложитьВыбратьУстройство(ОписаниеОповещения, МассивОборудования,
					НСтр("ru='Выберите оборудование'"), НСтр("ru='Оборудование не подключено.'"));
			
		Иначе
			
			ТекстСообщения = НСтр("ru = 'Предварительно необходимо выбрать рабочее место внешнего оборудования текущего сеанса.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
		
	Иначе
		
		Если НЕ ИспользоватьПодключаемоеОборудование 
			ИЛИ КассаККМИспользоватьБезПодключенияОборудования Тогда
			// Внешнее оборудование не используется
			Объект.Дата = ОбщегоНазначенияКлиент.ДатаСеанса();
			Если НЕ ЗначениеЗаполнено(Объект.НомерЧекаККМ) Тогда
				Объект.НомерЧекаККМ = 1;
			КонецЕсли; 		
			Модифицированность = Истина;
			Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.Пробит");
			Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
			ПоказатьПредупреждение(Неопределено, НСтр(
				"ru = 'Печать не выполнена. В карточке кассы ККМ установлен флаг ""Без подключения"" или выключена опция ""Подключаемое оборудование"".'"));
			Возврат;
		КонецЕсли;
			
		ИдентификаторУстройстваФР = ?(ЗначениеЗаполнено(ФискальныйРегистратор),
			ФискальныйРегистратор,
			Неопределено);
		
		ПодключенияФискальногоРегистратораЗавершение(ИдентификаторУстройстваФР, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры // НапечататьЧек()

&НаКлиенте
Процедура ПодключенияФискальногоРегистратораЗавершение(ИдентификаторУстройстваФР, Параметры) Экспорт
	
	Если ИдентификаторУстройстваФР = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Устройство для печати чеков не выбрано.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.Доступность = Ложь;
	
	ОбщиеПараметры = ПодготовитьДанныеДляПробитияЧека(ИдентификаторУстройстваФР);
	
	Если ОбщиеПараметры = Неопределено Тогда
		ЭтотОбъект.Доступность = Истина;
		Возврат;
	КонецЕсли;
	
	ОбщиеПараметры.ДатаВремя = ТекущаяДата();
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОбщиеПараметры", ОбщиеПараметры);
	ДополнительныеПараметры.Вставить("ИдентификаторУстройстваФР", ИдентификаторУстройстваФР);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПроверитьКодМаркировкиСредствамиККТЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РозничныеПродажиКлиент.ПроверитьКодМаркировкиСредствамиККТ(ОбщиеПараметры, ЭтотОбъект, НСтр("ru = 'Пробить чек'"), ОповещениеОЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКодМаркировкиСредствамиККТЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ШтрихкодированиеИСМПКлиент.РезультатПроверкиСредствамиККТТребуетФискализации(Результат)
		И ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ОбщиеПараметры") Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПробитьЧекЗавершение", ЭтотОбъект);
		
		МенеджерОборудованияКлиент.НачатьФискализациюЧекаНаФискальномУстройстве(
			Оповещение,
			УникальныйИдентификатор,
			ДополнительныеПараметры.ОбщиеПараметры,
			ДополнительныеПараметры.ИдентификаторУстройстваФР);
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПодготовитьДанныеДляПробитияЧека(ИдентификаторУстройстваФР)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Подготовка таблицы товаров
	ОбщиеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека();
	
	РозничныеПродажиСервер.ДополнитьТоварамиПараметрыПриПробитииЧека(Объект, ОбщиеПараметры);
	СуммаСтрокЧека = РозничныеПродажиСервер.СуммаСтрокЧека(ОбщиеПараметры);
	
	// Общие параметры чека
	РеквизитыОрганизация = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Организация, "НаименованиеПолное,ИНН,КПП");
	
	Если ТипЗнч(КассаВЭТ) = Тип("СправочникСсылка.Кассы") ТОгда
		КассаККМ = Справочники.КассыККМ.ПолучитьКассуККМПоЭкземпляруОборудования(ИдентификаторУстройстваФР);
		Если НЕ ЗначениеЗаполнено(КассаККМ) Тогда
			ТекстСообщения = НСтр("ru = 'Не найдена касса ККМ соответствующая устройству %устройство%.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%устройство%", ИдентификаторУстройстваФР);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		КассаККМ = Объект.КассаККМ;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("ЭлектронныйЧекSMSПередаютсяПрограммой1С,ЭлектронныйЧекEmailПередаютсяПрограммой1С,СерийныйНомер,Код,ПодключаемоеОборудование,СтруктурнаяЕдиница");
	СтруктураРеквизитов.Вставить("СпособФорматноЛогическогоКонтроля", "ПодключаемоеОборудование.СпособФорматноЛогическогоКонтроля");
	СтруктураРеквизитов.Вставить("ДопустимоеРасхождениеФорматноЛогическогоКонтроля", "ПодключаемоеОборудование.ДопустимоеРасхождениеФорматноЛогическогоКонтроля");
	СтруктураРеквизитов.Вставить("ТипОборудования", "ПодключаемоеОборудование.ТипОборудования");
	РеквизитыКассыККМ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(КассаККМ, СтруктураРеквизитов);
	
	РеквизитыКассира = РозничныеПродажиСервер.ПолучитьРеквизитыКассира(Объект.Автор, Объект.ПодписьКассира);
	
	ОбщиеПараметры.ТипРасчета = ?(Объект.ВидОперации = Перечисления.ВидыОперацийЭквайринга.ВозвратОплатыПокупателю,
		Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств,
		Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств);
		
	ОбщиеПараметры.Электронно = Ложь;
	
	Если ЗначениеЗаполнено(Объект.Телефон) Тогда
		Если РеквизитыКассыККМ.ЭлектронныйЧекSMSПередаютсяПрограммой1С Тогда
			ОбщиеПараметры.Отправляет1СSMS = Истина;
		КонецЕсли;
		ОбщиеПараметры.ПокупательНомер = "+7" + РозничныеПродажиСервер.УбратьРазделителиВНомереТелефона(Объект.Телефон);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.АдресЭП) Тогда
		Если РеквизитыКассыККМ.ЭлектронныйЧекEmailПередаютсяПрограммой1С Тогда
			ОбщиеПараметры.Отправляет1СEmail = Истина;
		КонецЕсли;
		ОбщиеПараметры.ПокупательEmail = Объект.АдресЭП;
	КонецЕсли;
	
	СведенияОКонтрагенте = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Контрагент, "НаименованиеПолное,ИНН,ВидКонтрагента");
	Если СведенияОКонтрагенте.ВидКонтрагента = Перечисления.ВидыКонтрагентов.ЮридическоеЛицо 
		ИЛИ ЗначениеЗаполнено(СведенияОКонтрагенте.ИНН) Тогда			
		ОбщиеПараметры.Получатель    = СведенияОКонтрагенте.НаименованиеПолное;
		ОбщиеПараметры.ПолучательИНН = СведенияОКонтрагенте.ИНН;		
	КонецЕсли;
	
	ОбщиеПараметры.ДокументОснование = Объект.Ссылка;
	ОбщиеПараметры.ТорговыйОбъект = РеквизитыКассыККМ.СтруктурнаяЕдиница;
	
	// Параметры необходимые для чека ЕНВД на принтере чеков
	ОбщиеПараметры.Кассир = РеквизитыКассира.ИмяКассираИДолжность;
	ОбщиеПараметры.Вставить("ИмяКассира", РеквизитыКассира.ИмяКассира);
	ОбщиеПараметры.КассирИНН = РеквизитыКассира.КассирИНН;
	
	ОбщиеПараметры.ОрганизацияНазвание = РеквизитыОрганизация.НаименованиеПолное;
	ОбщиеПараметры.ОрганизацияИНН = РеквизитыОрганизация.ИНН;
	ОбщиеПараметры.ОрганизацияКПП = РеквизитыОрганизация.КПП;
	ОбщиеПараметры.НомерКассы     = "00001";
	ОбщиеПараметры.НомерЧека      = "1";
	ОбщиеПараметры.НомерСмены     = "1";
	
	АдресМагазина = ПечатьДокументовУНФ.КонтактнаяИнформация(Объект.Организация, ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ФактАдресОрганизации"));
	
	СерийныйНомер = РеквизитыКассыККМ.СерийныйНомер;
	Если НЕ ЗначениеЗаполнено(СерийныйНомер) Тогда
		СерийныйНомер = "1";
	КонецЕсли;
	
	ОбщиеПараметры.АдресМагазина = АдресМагазина;
	ОбщиеПараметры.НаименованиеМагазина = Строка(Объект.Организация);
	ОбщиеПараметры.СерийныйНомер = СерийныйНомер;
	ОбщиеПараметры.СистемаНалогообложения = РозничныеПродажиСервер.ПолучитьТипСистемыНалогообложенияККТ(
		Объект.Организация,
		,
		Объект.Дата,
		Объект.СпециальныйНалоговыйРежим);
		
	Если РеквизитыКассыККМ.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККТ Тогда
		ОбщиеПараметры.СпособФорматноЛогическогоКонтроля = РеквизитыКассыККМ.СпособФорматноЛогическогоКонтроля;
		ОбщиеПараметры.ДопустимоеРасхождениеФорматноЛогическогоКонтроля = РеквизитыКассыККМ.ДопустимоеРасхождениеФорматноЛогическогоКонтроля;
		Если ФорматноЛогическийКонтрольКлиентСервер.НуженФорматноЛогическийКонтроль(ОбщиеПараметры) Тогда
			ФорматноЛогическийКонтрольКлиентСервер.ПровестиФорматноЛогическийКонтроль(ОбщиеПараметры);
		КонецЕсли;
	КонецЕсли;
	
	СтрокаОплаты = Новый Структура("ТипОплаты,Наименование,Сумма", Перечисления.ТипыОплатыККТ.Электронно, НСтр("ru = 'Оплата по карте'"), Объект.СуммаДокумента);
	ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	
	РазницаСумм = СуммаСтрокЧека - Объект.СуммаДокумента;
	Если РазницаСумм > 0 Тогда
		СтрокаОплаты = Новый Структура("ТипОплаты,Наименование,Сумма", Перечисления.ТипыОплатыККТ.Постоплата, НСтр("ru = 'Постоплата'"), РазницаСумм);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
		
	Возврат ОбщиеПараметры;
	
КонецФункции // ПодготовитьДанныеДляПробитияЧека()

&НаКлиенте
Процедура ПробитьЧекЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		
		// Установить полученное значение номера чека реквизиту документа.
		Если РезультатВыполнения.ВыходныеПараметры <> Неопределено Тогда
			Объект.НомерСменыККМ = РезультатВыполнения.ВыходныеПараметры[0];
			Объект.НомерЧекаККМ = РезультатВыполнения.ВыходныеПараметры[1];
		КонецЕсли;
		
		Объект.Дата = ОбщегоНазначенияКлиент.ДатаСеанса();
		
		Если НЕ ЗначениеЗаполнено(Объект.НомерЧекаККМ) Тогда
			Объект.НомерЧекаККМ = 1;
		КонецЕсли;
		Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.Пробит");
		
		Модифицированность = Истина;
		
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
		
		ЭтотОбъект.Доступность = Истина;
		
	Иначе
		
		ЭтотОбъект.Доступность = Истина;
		
		ТекстСообщения = СтрШаблон(НСтр(
		"ru = 'При печати чека произошла ошибка.
		|Чек не напечатан на фискальном регистраторе.
		|Дополнительное описание:
		|%1'"), РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
	УстановитьДоступностьПечатиЧека();
	
КонецПроцедуры

// Процедура - обработчик события Выполнить команды Подбор.
// Открывает форму подбора долгов.
//
&НаКлиенте
Процедура Подбор(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Укажите вначале контрагента!'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Укажите вначале валюту!'"));
		Возврат;
	КонецЕсли;
	
	АдресРасшифровкаПлатежаВХранилище = ПоместитьРасшифровкаПлатежаВХранилище();
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("АдресРасшифровкаПлатежаВХранилище", АдресРасшифровкаПлатежаВХранилище);
	ПараметрыПодбора.Вставить("Компания", Компания);
	ПараметрыПодбора.Вставить("Дата", Объект.Дата);
	ПараметрыПодбора.Вставить("Контрагент", Объект.Контрагент);
	ПараметрыПодбора.Вставить("Ссылка", Объект.Ссылка);
	ПараметрыПодбора.Вставить("ВидОперации", Объект.ВидОперации);
	ПараметрыПодбора.Вставить("ВалютаДенежныхСредств", Объект.ВалютаДенежныхСредств);
	ПараметрыПодбора.Вставить("СуммаДокумента", Объект.СуммаДокумента);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЭквайринга.ВозвратОплатыПокупателю") Тогда
		ОткрытьФорму("ОбщаяФорма.ФормаПодбораДолговПоставщикам", ПараметрыПодбора, , , , ,
			Новый ОписаниеОповещения("ПодборЗавершение", ЭтотОбъект,
			Новый Структура("АдресРасшифровкаПлатежаВХранилище", АдресРасшифровкаПлатежаВХранилище)));
	Иначе
		ОткрытьФорму("ОбщаяФорма.ФормаПодбораДолговПокупателей", ПараметрыПодбора, , , , ,
			Новый ОписаниеОповещения("ПодборЗавершение", ЭтотОбъект,
			Новый Структура("АдресРасшифровкаПлатежаВХранилище", АдресРасшифровкаПлатежаВХранилище)));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	АдресРасшифровкаПлатежаВХранилище = ДополнительныеПараметры.АдресРасшифровкаПлатежаВХранилище;
	
	Если Результат = КодВозвратаДиалога.OK Тогда
		
		ПолучитьРасшифровкаПлатежаИзХранилища(АдресРасшифровкаПлатежаВХранилище);
		Для каждого СтрокаРасшифровкаПлатежа Из Объект.РасшифровкаПлатежа Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаРасшифровкаПлатежа.СтавкаНДС) Тогда
				СтрокаРасшифровкаПлатежа.СтавкаНДС = СтавкаНДСПоУмолчанию;
			КонецЕсли;
			РассчитатьСуммуПлатежа(СтрокаРасшифровкаПлатежа);
		КонецЦикла;
		
		УстановитьТекущуюСтраницу();
		
		Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
			Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // Подбор()

// Процедура вызывается при нажатии кнопки "ЗаполнитьПоОснованию" командной панели
// табличного поля.
//
&НаКлиенте
Процедура ЗаполнитьПоОснованию(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru='Не выбран документ основание!'"));
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоОснованиюЗавершение", ЭтотОбъект), НСтр("ru = 'Документ будет полностью перезаполнен по ""Основанию""! Продолжить выполнение операции?'"), РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокОплатаПлатежнымиКартами(ДокументОснование)
	
	СписокОплатаКартами = Новый СписокЗначений;
	Сч = 0;
	
	Для Каждого ОплатаПлатежнойКартой Из ДокументОснование.БезналичнаяОплата Цикл
		Если ОплатаПлатежнойКартой.ВидОплаты <> Перечисления.ВидыБезналичныхОплат.БанковскаяКарта Тогда
			Продолжить;
		КонецЕсли;
		Сч = Сч + 1;
		Текст = ""+Сч+". "+ОплатаПлатежнойКартой.ВидПлатежнойКарты+" "+ОплатаПлатежнойКартой.НомерПлатежнойКарты+". Сумма = "+ОплатаПлатежнойКартой.Сумма;
		СписокОплатаКартами.Добавить(Сч-1, Текст);
	КонецЦикла;
	
	Возврат СписокОплатаКартами;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьПоОснованиюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ОрганизацияДоИзменения = Объект.Организация;
		
		ЗаполнитьПоДокументу(Объект.ДокументОснование);
		
		Если Объект.РасшифровкаПлатежа.Количество() = 0 Тогда
			Объект.РасшифровкаПлатежа.Добавить();
			Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
		КонецЕсли;
		
		ВидОперацииПриИзменении(Элементы.ВидОперации);
		Если ОрганизацияДоИзменения <> Объект.Организация Тогда
			ОрганизацияПриИзменении(Элементы.Организация);
		Иначе
			ЭквайринговыйТерминалПриИзменении(Элементы.ЭквайринговыйТерминал);
		КонецЕсли;
		ДатаДокумента = Объект.Дата;
		
		РассчитатьСуммуКомиссии();
		ЗаполнитьПоОснованиюЗавершениеНаСервере();
		
	КонецЕсли;

КонецПроцедуры // ЗаполнитьПоОснованию()

&НаКлиенте
Процедура ЗаполнитьПоОперацииОплатыЗавершениеПослеВыбораСтрокиОплаты(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт

	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	Иначе
		ВыбранноеЗначение = ВыбранныйЭлемент.Значение;
	КонецЕсли;
	
	ЗаполнитьПоОперацииОплатыЗавершениеФрагмент(ДополнительныеПараметры, ВыбранноеЗначение);
	
КонецПроцедуры

// Процедура вызывается при нажатии кнопки "ЗаполнитьПоОперацииОплаты" командной панели
// табличного поля.
//
&НаКлиенте
Процедура ЗаполнитьПоОперацииОплаты(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ОперацияПоПлатежнымКартамШапка) Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru='Не выбран документ оплаты!'"));
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоОперацииОплатыЗавершение", ЭтотОбъект), НСтр("ru = 'Документ будет полностью перезаполнен по документу оплаты! Продолжить выполнение операции?'"), РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОперацииОплатыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Если ТипЗнч(Объект.ОперацияПоПлатежнымКартамШапка) = Тип("ДокументСсылка.ЧекККМ") Тогда
			СписокОплатаПлатежнымиКартами = ПолучитьСписокОплатаПлатежнымиКартами(Объект.ОперацияПоПлатежнымКартамШапка);
			
			Если СписокОплатаПлатежнымиКартами.Количество() = 0 Тогда
				ПоказатьПредупреждение(Неопределено, "В чеке ККМ нет оплаты платежными картами!");
				Возврат;
			ИначеЕсли СписокОплатаПлатежнымиКартами.Количество() = 1 Тогда
				ЗаполнитьПоОперацииОплатыЗавершениеФрагмент(ДополнительныеПараметры);
			Иначе
				СписокОплатаПлатежнымиКартами.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ЗаполнитьПоОперацииОплатыЗавершениеПослеВыбораСтрокиОплаты", ЭтотОбъект), "Выберите строку оплаты");
			КонецЕсли;
		Иначе
			ЗаполнитьПоОперацииОплатыЗавершениеФрагмент(ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОперацииОплатыЗавершениеФрагмент(ДополнительныеПараметры, НомерСтрокиОплатыКартамиВЧекеККМ = 0) Экспорт
	
	Если ТипЗнч(Объект.ОперацияПоПлатежнымКартамШапка) = Тип("ДокументСсылка.ЧекККМ") Тогда
		СтруктураДляЗаполнения = Новый Структура("Документ, НомерСтроки", Объект.ОперацияПоПлатежнымКартамШапка, НомерСтрокиОплатыКартамиВЧекеККМ);
		ЭтоВозвратНаОснованииПрихода = ЗаполнитьПоДокументу(СтруктураДляЗаполнения);
	Иначе
		ЭтоВозвратНаОснованииПрихода = ЗаполнитьПоДокументу(Объект.ОперацияПоПлатежнымКартамШапка);
	КонецЕсли;
	
	Если Объект.РасшифровкаПлатежа.Количество() = 0 Тогда
		Объект.РасшифровкаПлатежа.Добавить();
		Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
	КонецЕсли;
	
	Если ВидОперации = ВидОперацииОплата Тогда
		ВидОперации = Неопределено;
		ВидОперацииПриИзменении(Элементы.ВидОперации);
	КонецЕсли;
	ЭквайринговыйТерминалПриИзменении(Элементы.ЭквайринговыйТерминал);
	
	Если ЭтоВозвратНаОснованииПрихода Тогда
		ВидПлатежнойКартыПриИзменении(Элементы.ВидПлатежнойКарты);
	КонецЕсли;
		
	ДатаДокумента = Объект.Дата;
	
	СуммаДокументаПриИзменении(Элементы.СуммаДокумента);
	
КонецПроцедуры // ЗаполнитьПоОснованию()

// Процедура - обработчик команды ЗаполнитьРасшифровку.
//
&НаКлиенте
Процедура ЗаполнитьРасшифровку(Команда)
	
	Если Объект.СуммаДокумента = 0 Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru='Укажите вначале сумму документа.'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Укажите вначале валюту!'"));
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьРасшифровкуЗавершение", ЭтотОбъект), 
		НСтр("ru='Расшифровка будет полностью перезаполнена. Продолжить?'"),
		РежимДиалогаВопрос.ДаНет
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРасшифровкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Объект.РасшифровкаПлатежа.Очистить();
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЭквайринга.ПоступлениеОплатыОтПокупателя") Тогда
		ЗаполнитьРасшифровкуПлатежа();
	КонецЕсли;
	
	УстановитьТекущуюСтраницу();

КонецПроцедуры // ЗаполнитьРасшифровку()

// Функция проверяет возможность печати чека на фискальном регистраторе.
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - Форма документа
//
// Возвращаемое значение:
//	Булево - Признак возможности печати
//
&НаКлиенте
Функция ПроверитьВозможностьПечатиЧека(ПоказыватьПредупреждение = Ложь) Экспорт
	
	ПечататьЧек = Истина;
	
	// Если объект модифицирован - выполним проведение.
	Если Модифицированность Тогда
		
		Попытка
			Если ЗначениеЗаполнено(ЭквайринговыйТерминал) Тогда
				Если НЕ Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение)) Тогда
					ПечататьЧек = Ложь;
				КонецЕсли;
			Иначе
				Если НЕ Объект.Проведен Тогда
					Если НЕ Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись)) Тогда
						ПечататьЧек = Ложь;
					КонецЕсли;
				Иначе
					Если НЕ Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение)) Тогда
						ПечататьЧек = Ложь;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		Исключение
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ОписаниеОшибки();
			Сообщение.Сообщить();
			
			ПоказыватьПредупреждение = Истина;
			ПечататьЧек = Ложь;
		КонецПопытки;
			
	КонецЕсли;
	
	Возврат ПечататьЧек;

КонецФункции // ПроверитьВозможностьПечатиЧека()

// Процедура - обработчик команды ДобавитьОплатуКартой.
//
&НаКлиенте
Процедура ДобавитьОплатуКартой(Команда)
	
	Попытка
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не удалось выполнить проведение документа!'");
		Сообщение.Сообщить();
		Возврат;
	КонецПопытки;
	
	Если ПустаяСтрока(Объект.НомерПлатежнойКарты) Тогда
		ДобавитьОплатуКартойПослеОтветаНаВопрос(КодВозвратаДиалога.Да);
	Иначе
		ОписаниеОповещенияДобавитьОплатуКартой = Новый ОписаниеОповещения("ДобавитьОплатуКартойПослеОтветаНаВопрос", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещенияДобавитьОплатуКартой, НСтр("ru = 'Номер карты уже заполнен. 
		|Продолжить оформление оплаты картой с использованием эквайрингового терминала?'"), РежимДиалогаВопрос.ДаНет,,
		КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьНеобходимыеДанныеДляРаботыСКартой(ПараметрыКассыККМ, ПараметрыЭТ)
	
	ПараметрыЭТ = УправлениеНебольшойФирмойПовтИсп.ПолучитьПараметрыЭТ(Объект.ЭквайринговыйТерминал);
	ПараметрыЭТ.ИспользоватьБезПодключенияОборудования = ПараметрыЭТ.ИспользоватьБезПодключенияОборудования ИЛИ НЕ ИспользоватьПодключаемоеОборудование;
	ПараметрыКассыККМ = УправлениеНебольшойФирмойПовтИсп.ПолучитьПараметрыКассыККМ(Справочники.КассыККМ.ПустаяСсылка());
	ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования = ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования ИЛИ НЕ ИспользоватьПодключаемоеОборудование;
	
КонецПроцедуры

&НаСервере
Функция ОпределитьТипТранзакцииОплаты()
	
	ТипТранзакции = "";
	
	Если Объект.ОперацияПоПлатежнымКартамШапка.Пустая() Тогда
		ТипТранзакции = "AuthorizeRefund";
	Иначе
		Если НачалоДня(Объект.Дата) = НачалоДня(Объект.ОперацияПоПлатежнымКартамШапка.Дата) Тогда
			ТипТранзакции = "AuthorizeVoid";
		Иначе
			ТипТранзакции = "AuthorizeRefund";
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТипТранзакции;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьОплатуКартойПослеОтветаНаВопрос(Значение, ДополнительныеПараметры = Неопределено) Экспорт
	
	Перем ПараметрыКассыККМ, ПараметрыЭТ;
	
	Если Значение <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПроверитьВозможностьПечатиЧека(Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	ПолучитьНеобходимыеДанныеДляРаботыСКартой(ПараметрыКассыККМ, ПараметрыЭТ);
	
	// Предварительно авторизуем операцию.
	ПараметрыФормы = Новый Структура("Сумма, ПределСуммы", Объект.СуммаДокумента, Объект.СуммаДокумента);
	ПараметрыФормы.Вставить("ЗапретРедактированияСуммы", Истина);
	Если ПараметрыЭТ.ИспользоватьБезПодключенияОборудования Тогда
		ПараметрыФормы.Вставить("ПоказыватьНомерКарты", Истина);
	КонецЕсли;
	ПараметрыФормы.Вставить("СписокТиповКарт", Новый СписокЗначений());
	Индекс = 0;
	Для Каждого ВидКарты Из Элементы.ВидПлатежнойКарты.СписокВыбора Цикл
		ПараметрыФормы.СписокТиповКарт.Добавить(Индекс, ВидКарты.Значение);
		Индекс = Индекс + 1;
	КонецЦикла;
	
	ПараметрДействия = Новый Структура;
	ПараметрДействия.Вставить("ПараметрыКассыККМ", ПараметрыКассыККМ);
	ПараметрДействия.Вставить("ПараметрыЭТ", ПараметрыЭТ);
	ПараметрДействия.Вставить("СуммаОперации", 0);
	ПараметрДействия.Вставить("НомерКарты", "");
	ПараметрДействия.Вставить("НомерСсылкиОперации", "");
	ПараметрДействия.Вставить("НомерЧекаЭТ", 0);
	ПараметрДействия.Вставить("НомерЧекаККМ", Объект.Номер);
	ПараметрДействия.Вставить("СтрокаСлипЧека", "");
	ПараметрДействия.Вставить("ТипКарты", Неопределено);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЭквайринга.ВозвратОплатыПокупателю") Тогда
		ПараметрДействия.Вставить("ТипТранзакции", ОпределитьТипТранзакцииОплаты());
		ПараметрыФормы.Вставить("УказатьДополнительныеДанные", Истина);
		ПараметрыФормы.Вставить("СсылочныйНомер", Объект.СсылочныйНомер);
		ПараметрыФормы.Вставить("НомерЧекаЭТ", Объект.НомерЧекаЭТ);
	Иначе
		ПараметрДействия.Вставить("ТипТранзакции", "AuthorizeSales");
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПараметрДействия", ПараметрДействия);
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеОткрытьФормуАвторизацииЭТ", ЭтотОбъект, ДополнительныеПараметры);
	Режим = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
	ОткрытьФорму("Справочник.ПодключаемоеОборудование.Форма.ФормаАвторизацииЭТ", ПараметрыФормы,,,,, ОбработчикОповещения, Режим); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОткрытьФормуАвторизацииЭТ(РезультатОткрытияФормы, ДополнительныеПараметры) Экспорт
	
	ПараметрДействия = ДополнительныеПараметры.ПараметрДействия;
	
	Если ТипЗнч(РезультатОткрытияФормы) = Тип("Структура") Тогда
		
		ПараметрДействия.СуммаОперации = РезультатОткрытияФормы.Сумма;
		ПараметрДействия.ТипКарты			 = РезультатОткрытияФормы.ТипКарты;
		
		Если ПараметрДействия.ПараметрыЭТ.ИспользоватьБезПодключенияОборудования Тогда
			
			ПараметрДействия.НомерКарты          = РезультатОткрытияФормы.НомерКарты;
			ПараметрДействия.НомерСсылкиОперации = РезультатОткрытияФормы.СсылочныйНомер;
			ПараметрДействия.НомерЧекаЭТ         = РезультатОткрытияФормы.НомерЧека;
			
			ТекстВопроса = НСтр("ru = 'Требуется выполнить операцию оплаты на эквайринговом терминале.'") + Символы.ПС;
			ТекстВопроса = ТекстВопроса + НСтр("ru = 'Сумма операции:'") + " "+ ПараметрДействия.СуммаОперации  + Символы.ПС;
			ТекстВопроса = ТекстВопроса + Символы.ПС;
			ТекстВопроса = ТекстВопроса + НСтр("ru = 'Операция оплаты на эквайринговом терминале прошла успешно?'");
			
			ДополнительныеПараметры = Новый Структура; 
			ДополнительныеПараметры.Вставить("ПараметрДействия", ПараметрДействия);
			ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросУспешностиОплатыКартой", ЭтотОбъект, ДополнительныеПараметры);
			ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
		Иначе
			
			ЭтаФорма.Доступность = Ложь;
			
			ПараметрыОперации = МенеджерОборудованияКлиент.ПараметрыВыполненияЭквайринговойОперации();
			ПараметрыОперации.ТипТранзакции  = ПараметрДействия.ТипТранзакции;
			ПараметрыОперации.СуммаОперации  = РезультатОткрытияФормы.Сумма;
			ПараметрыОперации.НомерЧека      = РезультатОткрытияФормы.НомерЧека;
			ПараметрыОперации.СсылочныйНомер = РезультатОткрытияФормы.СсылочныйНомер;
			
			Если ФискальныйРегистратор.Пустая() Тогда
				УстройствоПечати = ПараметрДействия.ПараметрыКассыККМ.ИдентификаторУстройства;
				УстройствоПечати = ?(ПустаяСтрока(УстройствоПечати), Неопределено, УстройствоПечати);
			Иначе
				УстройствоПечати = ФискальныйРегистратор;
			КонецЕсли;
			
			Оповещение = Новый ОписаниеОповещения("ОповещениеОткрытьФормуАвторизацииЭТЗавершение", ЭтотОбъект, ПараметрДействия);
			МенеджерОборудованияКлиент.НачатьВыполнениеОперацииНаЭквайринговомТерминале(Оповещение, УникальныйИдентификатор, 
			ПараметрДействия.ПараметрыЭТ.ИдентификаторУстройства, УстройствоПечати, ПараметрыОперации);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВопросУспешностиОплатыКартой(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ПараметрДействия = ДополнительныеПараметры.ПараметрДействия;
	
	Если НЕ (РезультатВопроса = КодВозвратаДиалога.Да) Тогда
		ТекстОписаниеОшибки = НСтр("ru = 'Операция отменена.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	ЗавершитьОплатуКартой(ПараметрДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОткрытьФормуАвторизацииЭТЗавершение(РезультатВыполнения, ПараметрДействия) Экспорт 
	
	ЭтаФорма.Доступность = Истина;
	
	Если РезультатВыполнения.Результат Тогда
		Если ПараметрДействия.ТипТранзакции <> "AuthorizeVoid" Тогда
			ПараметрДействия.НомерКарты          = РезультатВыполнения.НомерКарты;
			ПараметрДействия.НомерСсылкиОперации = РезультатВыполнения.СсылочныйНомер;
			ПараметрДействия.НомерЧекаЭТ         = РезультатВыполнения.НомерЧекаЭТ;
			ПараметрДействия.СтрокаСлипЧека      = РезультатВыполнения.ТекстСлипЧека;
		Иначе
			ПараметрДействия.НомерКарты          = Объект.НомерПлатежнойКарты;
			ПараметрДействия.НомерСсылкиОперации = Объект.СсылочныйНомер;
			ПараметрДействия.НомерЧекаЭТ         = Объект.НомерЧекаЭТ;
			ПараметрДействия.СтрокаСлипЧека      = РезультатВыполнения.ТекстСлипЧека;
		КонецЕсли;
		ЗавершитьОплатуКартой(ПараметрДействия);
	Иначе
		ТекстСообщения = НСтр("ru = 'При выполнении операции возникла ошибка:
			|""%ОписаниеОшибки%"".
			|Оплата по карте не была произведена.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьОплатуКартой(ПараметрДействия)
	
	
	//Сохранить в таблице данные оплаты картой
	ВидКарты = Элементы.ВидПлатежнойКарты.СписокВыбора[ПараметрДействия.ТипКарты].Значение;
	Объект.ВидПлатежнойКарты = ВидКарты;
	ВидПлатежнойКартыПриИзменении(Элементы.ВидПлатежнойКарты);
	
	Объект.НомерПлатежнойКарты = ПараметрДействия.НомерКарты; // Возможна запись пустого номера карты или номера вида "****************"
	
	Если Объект.СуммаДокумента <> ПараметрДействия.СуммаОперации Тогда
		Объект.СуммаДокумента = ПараметрДействия.СуммаОперации;
		СуммаДокументаПриИзменении(Элементы.СуммаДокумента);
	КонецЕсли;
	
	Объект.СсылочныйНомер      = ПараметрДействия.НомерСсылкиОперации;	
	Объект.НомерЧекаЭТ         = ПараметрДействия.НомерЧекаЭТ;

	Попытка
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не удалось выполнить проведение документа!'");
		Сообщение.Сообщить();
		Записать();
	КонецПопытки;
	
КонецПроцедуры

// Процедура - обработчик команды УдалитьОплатуКартой.
//
&НаКлиенте
Процедура УдалитьОплатуКартой(Команда)
	
	ДобавитьОплатуКартойПослеОтветаНаВопрос(КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьНапечататьПоследнийСлипЧекЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	ЭтотОбъект.Доступность = Истина;
	Если НЕ РезультатВыполнения.Результат Тогда  
		Если ЗначениеЗаполнено(РезультатВыполнения.ОписаниеОшибки) Тогда
			ТекстСообщения = НСтр("ru = 'При подключении фискального регистратора произошла ошибка:
				|""%ОписаниеОшибки%"".'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", РезультатВыполнения.ОписаниеОшибки);
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик команды НапечататьПоследнийСлипЧек.
//
&НаКлиенте
Процедура НапечататьПоследнийСлипЧек(Команда)
	
	Если ИспользоватьПодключаемоеОборудование Тогда // Проверка на включенную ФО "Использовать ВО"
		Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента()Тогда // Проверка на определенность рабочего места ВО
			
			СтрокаСлипЧека = "";
			
			Если Не глПодключаемоеОборудование.Свойство("ПоследнийСлипЧек", СтрокаСлипЧека)
			 Или ТипЗнч(СтрокаСлипЧека) <> Тип("Строка")
			 Или ПустаяСтрока(СтрокаСлипЧека) Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Слип-чек отсутствует.
				|Возможно для данного сеанса еще не выполнялась эквайринговая операция.'"));
				Возврат;
			КонецЕсли;
			
			ЭтотОбъект.Доступность = Ложь;
			Оповещение = Новый ОписаниеОповещения("ВыполнитьНапечататьПоследнийСлипЧекЗавершение", ЭтотОбъект);
			МенеджерОборудованияКлиент.НачатьПечатьТекста(Оповещение, УникальныйИдентификатор, глПодключаемоеОборудование.ПоследнийСлипЧек);
			
		Иначе
			ТекстСообщения = НСтр("ru = 'Предварительно необходимо выбрать рабочее место внешнего оборудования текущего сеанса.'");
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТелефонАдресЭППриИзменении(Элемент)
	
	УправлениеНебольшойФирмойКлиент.УстановитьДоступностьТелефонАдресЭП(ЭтаФорма, ТелефонАдресЭП, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОснованиюЗавершениеНаСервере()
	
	РасчетыРаботаСФормамиВызовСервера.ЗаполнитьРеквизитыКурсИКратность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКурсВТекущейСтрокеРасшифровки(Команда)
	
	ТекущиеДанные = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЭквайринга.ВозвратОплатыПокупателю") 
		И ЗначениеЗаполнено(ТекущиеДанные.Документ) И ТекущиеДанные.ПризнакАванса Тогда
		
		КурсКратностьДокументаРасчетов = РасчетыРаботаСФормамиВызовСервера.ПолучитьКурсКратностьДокументаРасчетов(ТекущиеДанные.Документ);
		ТекущиеДанные.Курс = КурсКратностьДокументаРасчетов.Курс;
		ТекущиеДанные.Кратность = КурсКратностьДокументаРасчетов.Кратность;
		
	Иначе
		СтруктураДанных = РасчетыРаботаСФормамиВызовСервера.ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(Объект.Дата, ТекущиеДанные.Договор);
		ТекущиеДанные.Курс = СтруктураДанных.ДоговорВалютаКурсКратность.Курс;
		ТекущиеДанные.Кратность = СтруктураДанных.ДоговорВалютаКурсКратность.Кратность;
	КонецЕсли;
	
	РасшифровкаПлатежаКурсПриИзменении(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДополнительныйРеквизит(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущийНаборСвойств", ПредопределенноеЗначение("Справочник.НаборыДополнительныхРеквизитовИСведений.Документ_ОперацияПоПлатежнымКартам"));
	ПараметрыФормы.Вставить("ЭтоДополнительноеСведение", Ложь);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта", ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотрЧека(Команда)
	
	Если ТипЗнч(КассаВЭТ) = Тип("СправочникСсылка.Кассы") Тогда
	
		Если Модифицированность Тогда
			Если Объект.Проведен Тогда
				ТекстВопроса = НСтр("ru = 'Для выполнения команды ""Предварительный просмотр""
					|документ будет проведен. Продолжить?'");
				Кнопки = Новый СписокЗначений;
				Кнопки.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Провести и продолжить'"));
			Иначе
				ТекстВопроса = НСтр("ru = 'Для выполнения команды ""Предварительный просмотр""
					|документ будет записан. Продолжить?'");
				Кнопки = Новый СписокЗначений;
				Кнопки.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Записать и продолжить'"));
			КонецЕсли;
			Обработчик = Новый ОписаниеОповещения("ПредварительныйПросмотрЧекаПродолжение", ЭтотОбъект);
			Кнопки.Добавить(КодВозвратаДиалога.Отмена);
			ПоказатьВопрос(Обработчик, ТекстВопроса, Кнопки);
		Иначе
			ПредварительныйПросмотрЧекаПродолжение(Неопределено, "");
		КонецЕсли;
		
	Иначе
		
		Если НЕ ИспользоватьПодключаемоеОборудование 
			ИЛИ КассаККМИспользоватьБезПодключенияОборудования Тогда
			// Внешнее оборудование не используется
			Объект.Дата = ОбщегоНазначенияКлиент.ДатаСеанса();
			Если НЕ ЗначениеЗаполнено(Объект.НомерЧекаККМ) Тогда
				Объект.НомерЧекаККМ = 1;
			КонецЕсли;
			ПоказатьПредупреждение(Неопределено, НСтр(
				"ru = 'Просмотр невозможен. В карточке кассы ККМ установлен флаг ""Без подключения"" или выключена опция ""Подключаемое оборудование"".'"));
			Возврат;
		КонецЕсли;
			
		ИдентификаторУстройстваФР = ?(ЗначениеЗаполнено(ФискальныйРегистратор),
			ФискальныйРегистратор,
			Неопределено);
		
		ПредварительныйПросмотрЧекаЗавершение(ИдентификаторУстройстваФР, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотрЧекаПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.ОК Тогда
		Если Объект.Проведен Тогда
			РезультатЗаписи = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
		Иначе
			РезультатЗаписи = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись));
		КонецЕсли;
		Если Не РезультатЗаписи Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПредварительныйПросмотрЧекаЗавершение", ЭтотОбъект);
		МассивОборудования = Новый Массив;
		МассивОборудования.Добавить("ФискальныйРегистратор");
		МассивОборудования.Добавить("ККТ");
		МассивОборудования.Добавить("ПринтерЧеков");
		МенеджерОборудованияКлиент.ПредложитьВыбратьУстройство(ОписаниеОповещения, МассивОборудования,
				НСтр("ru='Выберите оборудование'"), НСтр("ru='Оборудование не подключено.'"));
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Предварительно необходимо выбрать рабочее место внешнего оборудования текущего сеанса.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотрЧекаЗавершение(ИдентификаторУстройстваФР, Параметры) Экспорт
	
	Если ИдентификаторУстройстваФР = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Устройство для печати чеков не выбрано.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОбщиеПараметры = ПодготовитьДанныеДляПробитияЧека(ИдентификаторУстройстваФР);
	
	Если ОбщиеПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщиеПараметры.ДатаВремя = ТекущаяДата();
	
	РабочееМестоКассираКлиент.ПредпросмотрЧекаДенежныхДокументов(ОбщиеПараметры, ЭтаФорма, ИдентификаторУстройстваФР);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьПечатиЧека()
	
	Если Объект.НомерЧекаККМ <> 0 Тогда
		Элементы.ГруппаОтправкаЧека.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Процедура обработки изменения поля Вид операции на сервере.
//
&НаСервере
Процедура УстановитьСтатьюДДСПриСменеВидаОперации()
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийЭквайринга.ВозвратОплатыПокупателю Тогда
		Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.Прочее;
	Иначе
		Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей;
	КонецЕсли;
	
КонецПроцедуры // ВидОперацииПриИзмененииНаСервере()

// Процедура устанавливает статью ДДС при открытии формы.
//
&НаСервере
Процедура УстановитьСтатьюДДС()
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийЭквайринга.ВозвратОплатыПокупателю Тогда
		Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.Прочее;
	Иначе
		Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей;
	КонецЕсли;
	
КонецПроцедуры // ВидОперацииПриИзмененииНаСервере()

// Процедура вызывает обработку заполнения документа по основанию.
//
&НаСервере
Функция ЗаполнитьПоДокументу(ДокОснование)
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Заполнить(ДокОснование);
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;
	
	УстановитьВидимостьНалогообложениеНДС();
	УстановитьВидимостьРеквизитовРасчетов();
	
	Возврат (ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ПриходнаяНакладная"));
	
КонецФункции // ЗаполнитьПоДокументу()

// Функция помещает табличную часть РасшифровкаРасчетов во временное хранилище
// и возвращает адрес
//
&НаСервере
Функция ПоместитьРасшифровкаПлатежаВХранилище()
	
	Возврат ПоместитьВоВременноеХранилище(
		Объект.РасшифровкаПлатежа.Выгрузить(,
			"Договор,
			|ПризнакАванса,
			|Документ,
			|Заказ,
			|СуммаРасчетов,
			|Курс,
			|Кратность"
		),
		УникальныйИдентификатор
	);
	
КонецФункции // ПоместитьРасшифровкаПлатежаВХранилище()

// Функция получает табличную часть РасшифровкаРасчетов из временного хранилища.
//
&НаСервере
Процедура ПолучитьРасшифровкаПлатежаИзХранилища(АдресРасшифровкаПлатежаВХранилище)
	
	ТаблицаРасшифровкаПлатежа = ПолучитьИзВременногоХранилища(АдресРасшифровкаПлатежаВХранилище);
	Объект.РасшифровкаПлатежа.Очистить();
	Для каждого СтрокаРасшифровкаПлатежа Из ТаблицаРасшифровкаПлатежа Цикл
		Строка = Объект.РасшифровкаПлатежа.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, СтрокаРасшифровкаПлатежа);
	КонецЦикла;
	
КонецПроцедуры // ПолучитьРасшифровкаПлатежаИзХранилища()

// Выполняет пересчет сумм по валюте табличной части документа после изменения
// банковского счета или кассы.
//
&НаКлиенте
Процедура ПересчитатьСуммыДокумента(Курс, Кратность, ПересчитатьСуммуПлатежа)
	
	Для каждого СтрокаТабличнойЧасти Из Объект.РасшифровкаПлатежа Цикл
		Если ПересчитатьСуммуПлатежа Тогда
			СтрокаТабличнойЧасти.СуммаПлатежа = ВалютыУНФКлиентСервер.Пересчитать(
				СтрокаТабличнойЧасти.СуммаРасчетов,
				СтрокаТабличнойЧасти.Курс,
				Курс,
				СтрокаТабличнойЧасти.Кратность,
				Кратность
			);
			РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
		Иначе
			СтрокаТабличнойЧасти.Курс = ?(
				СтрокаТабличнойЧасти.Курс = 0,
				1,
				СтрокаТабличнойЧасти.Курс
			);
			СтрокаТабличнойЧасти.Кратность = ?(
				СтрокаТабличнойЧасти.Кратность = 0,
				1,
				СтрокаТабличнойЧасти.Кратность
			);
			СтрокаТабличнойЧасти.СуммаРасчетов = ВалютыУНФКлиентСервер.Пересчитать(
				СтрокаТабличнойЧасти.СуммаПлатежа,
				Курс,
				СтрокаТабличнойЧасти.Курс,
				Кратность,
				СтрокаТабличнойЧасти.Кратность
			);
		КонецЕсли;
	КонецЦикла;
	
	Если ПересчитатьСуммуПлатежа Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	КонецЕсли;
	
КонецПроцедуры // ПересчитатьСуммыДокумента()

// Выполняет пересчет сумм по валюте денежных средств.
//
&НаКлиенте
Процедура ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств(СтруктураДанные, ТекстСообщения)
	
	КурсПередИзменением = Курс;
	КратностьПередИзменением = Кратность;
	
	Если ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
		Курс = ?(
			СтруктураДанные.ВалютаКурсКратность.Курс = 0,
			1,
			СтруктураДанные.ВалютаКурсКратность.Курс
		);
		Кратность = ?(
			СтруктураДанные.ВалютаКурсКратность.Кратность = 0,
			1,
			СтруктураДанные.ВалютаКурсКратность.Кратность
		);
	КонецЕсли;
	
	// Если курс валюты не изменился или не заполнена валюта денежных средств
	// или документ не заполнен, то ничего не делаем.
	Если (Курс = КурсПередИзменением
		И Кратность = КратностьПередИзменением)
	 ИЛИ (НЕ ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств))
	 ИЛИ (Объект.РасшифровкаПлатежа.Итог("СуммаРасчетов") = 0
	 И НЕ ЗначениеЗаполнено(Объект.СуммаДокумента)) Тогда
		
		// Обновить курсы расчетов
		// Если в расшифровке платежа есть курс <> 1, то предложим его перезаполнить.
		Если СтруктураДанные.Свойство("ОбновитьКурсыРасчетов") И СтруктураДанные.ОбновитьКурсыРасчетов Тогда
			ПоказатьВопрос(Новый ОписаниеОповещения("ОбновитьКурсВРасшифровкеПлатежаИПересчитатьСуммы", ЭтотОбъект), СтруктураДанные.ОбновитьКурсыРасчетовТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
		// Конец Обновить курсы расчетов
		
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КурсПередИзменением", КурсПередИзменением);
	ДополнительныеПараметры.Вставить("КратностьПередИзменением", КратностьПередИзменением);
	
	// Обновить курсы расчетов
	Если СтруктураДанные.Свойство("ОбновитьКурсыРасчетов") Тогда
		ДополнительныеПараметры.Вставить("ОбновитьКурсыРасчетов", СтруктураДанные.ОбновитьКурсыРасчетов);
		ДополнительныеПараметры.Вставить("ОбновитьКурсыРасчетовТекстВопроса", СтруктураДанные.ОбновитьКурсыРасчетовТекстВопроса);
	КонецЕсли;
	// Конец Обновить курсы расчетов
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОпределитьНеобходимостьПересчетаСуммыДокумента", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстСообщения, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры // ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств()

// Выполняем пересчет суммы платежа в переданной строке табличной части.
//
&НаКлиенте
Процедура РассчитатьСуммуПлатежа(СтрокаТабличнойЧасти) Экспорт
	
	СтруктураДанные = РасчетыРаботаСФормамиВызовСервера.ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(Объект.Дата, СтрокаТабличнойЧасти.Договор);
	
	СтрокаТабличнойЧасти.Курс = ?(
		СтрокаТабличнойЧасти.Курс = 0,
		1,
		СтрокаТабличнойЧасти.Курс
	);
	СтрокаТабличнойЧасти.Кратность = ?(
		СтрокаТабличнойЧасти.Кратность = 0,
		1,
		СтрокаТабличнойЧасти.Кратность
	);
	
	Если Объект.ВалютаДенежныхСредств = СтруктураДанные.ВалютаРасчетов Тогда
		СтрокаТабличнойЧасти.СуммаПлатежа = СтрокаТабличнойЧасти.СуммаРасчетов;
	Иначе
	
		СтрокаТабличнойЧасти.СуммаПлатежа = ВалютыУНФКлиентСервер.Пересчитать(
			СтрокаТабличнойЧасти.СуммаРасчетов,
			СтрокаТабличнойЧасти.Курс,
			Курс,
			СтрокаТабличнойЧасти.Кратность,
			Кратность
		);
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
		СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
	КонецЕсли;
	
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	
КонецПроцедуры // РассчитатьСуммуПлатежа()

// Выполняем пересчет суммы платежа в переданной строке табличной части.
//
&НаКлиенте
Процедура РассчитатьСуммуКомиссии()
	
	Объект.СуммаКомиссииПоДоговору = Объект.ПроцентКомиссии * Объект.СуммаДокумента / 100;
	
КонецПроцедуры

// Выполняем пересчет суммы учета.
//
&НаКлиенте
Процедура РассчитатьСуммуУчета()
	
	Объект.Курс = ?(
		Объект.Курс = 0,
		1,
		Объект.Курс
	);
	Объект.Кратность = ?(
		Объект.Кратность = 0,
		1,
		Объект.Кратность
	);
	
	Объект.СуммаУчета = ВалютыУНФКлиентСервер.Пересчитать(
		Объект.СуммаДокумента,
		Объект.Курс,
		КурсВалютыУчета,
		Объект.Кратность,
		КратностьВалютыУчета
	);
	
КонецПроцедуры // РассчитатьСуммуПлатежа()

// Выполняет пересчет сумм по валюте табличной части документа после изменения
// банковского счета или кассы.
//
&НаКлиенте
Процедура РассчитатьСуммуНДС(СтрокаТабличнойЧасти)
	
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
	
	СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.СуммаПлатежа - (СтрокаТабличнойЧасти.СуммаПлатежа) / ((СтавкаНДС + 100) / 100);
	
КонецПроцедуры // РассчитатьСуммуНДС()

// Получает набор данных с сервера для процедуры КонтрагентПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеКонтрагентИДокОплатыШапкаПриИзменении(Контрагент, Организация, Дата)
	
	ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Контрагент, Организация, Объект.ВидОперации);
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"КонтрагентНаименованиеПолное",
		Контрагент.НаименованиеПолное
	);
	
	СтруктураДанные.Вставить(
		"Договор",
		ДоговорПоУмолчанию
	);
	
	СтруктураДанные.Вставить(
		"ДоговорВалютаКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
			Дата,
			Новый Структура("Валюта", ДоговорПоУмолчанию.ВалютаРасчетов)
		)
	);
	
	СтруктураДанные.Вставить(
		"ВестиРасчетыПоДоговорам",
		Контрагент.ВестиРасчетыПоДоговорам
	);
	
	СтруктураДанные.Вставить(
		"ВестиРасчетыПоДокументам",
		Контрагент.ВестиРасчетыПоДокументам
	);
	
	СтруктураДанные.Вставить(
		"ВестиРасчетыПоЗаказам",
		Контрагент.ВестиРасчетыПоЗаказам
	);
	
	СтруктураДанные.Вставить(
		"ВестиУчетОплатыПоСчетам",
		Контрагент.ВестиУчетОплатыПоСчетам
	);
	
	СтруктураДанные.Вставить(
		"КонтрагентВДокументеОплаты",
		?(ЗначениеЗаполнено(Объект.ОперацияПоПлатежнымКартамШапка), Объект.ОперацияПоПлатежнымКартамШапка.Контрагент, Справочники.Контрагенты.ПустаяСсылка())
	);
	
	УстановитьВидимостьРеквизитовРасчетов();
	
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	
	ДанныеКИКонтрагента = Справочники.Контрагенты.ДанныеКИКонтрагентаДляВыбораНаФорме(Контрагент, Элементы,
		ПоляКИДляОтправкиЧека());
	ЗаполнитьПоляКИДляОтправкиЧекаПоУмолчанию(ДанныеКИКонтрагента);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеКонтрагентПриИзменении()

// Процедура устанавливает видимость реквизитов расчетов.
//
&НаСервере
Процедура УстановитьВидимостьРеквизитовРасчетов()
	
	Элементы.РасшифровкаПлатежаДоговор.Видимость = Объект.Контрагент.ВестиРасчетыПоДоговорам;
	Элементы.РасшифровкаПлатежаДокумент.Видимость = Объект.Контрагент.ВестиРасчетыПоДокументам;
	Элементы.РасшифровкаПлатежаЗаказ.Видимость = Объект.Контрагент.ВестиРасчетыПоЗаказам;
	Элементы.РасшифровкаПлатежаСчетНаОплату.Видимость = Объект.Контрагент.ВестиУчетОплатыПоСчетам;
	
КонецПроцедуры // УстановитьВидимостьРеквизитовРасчетов()

// Получает набор данных с сервера для процедуры ВалютаДенежныхСредствПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеВалютаДенежныхСредствПриИзменении(Дата, ВалютаДенежныхСредств)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"ВалютаКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
			Дата,
			Новый Структура("Валюта", ВалютаДенежныхСредств)
		)
	);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеВалютаДенежныхСредствПриИзменении()

// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(Дата, Договор)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"ДоговорВалютаКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
			Дата,
			Новый Структура("Валюта", Договор.ВалютаРасчетов)
		)
	);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении()

// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением)
	
	РазностьДат = ДокументыУНФ.ПроверитьНомерДокумента(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
	ВалютаКурсКратность = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Объект.Дата, Новый Структура("Валюта", Объект.ВалютаДенежныхСредств));
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"РазностьДат",
		РазностьДат
	);
	СтруктураДанные.Вставить(
		"ВалютаКурсКратность",
		ВалютаКурсКратность
	);
	
	ЗаполнитьСтавкуНДСПоОрганизацииНалогообложениеНДС();
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДатаПриИзменении()

// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеОрганизацияПриИзменении()
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"Компания",
		Константы.УчетПоКомпании.Компания(Объект.Организация)
	);
	СтруктураДанные.Вставить(
		"БанковскийСчет",
		Объект.Организация.БанковскийСчетПоУмолчанию
	);
	СтруктураДанные.Вставить(
		"ВалютаКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
			Объект.Дата,
			Новый Структура("Валюта", Объект.Организация.БанковскийСчетПоУмолчанию.ВалютаДенежныхСредств)
		)
	);
	СтруктураДанные.Вставить(
		"ВалютаДенежныхСредств",
		Объект.Организация.БанковскийСчетПоУмолчанию.ВалютаДенежныхСредств
	);
	
	СтруктураДанные.Вставить(
		"ЭквайринговыйТерминал", 
		Справочники.ЭквайринговыеТерминалы.ПолучитьЭквайринговыйТерминалПоУмолчаниюДляОперацииЭквайринга(Объект.Организация.БанковскийСчетПоУмолчанию, Объект.Организация)
	);
	
	ЗаполнитьСтавкуНДСПоОрганизацииНалогообложениеНДС();
	
	Возврат СтруктураДанные;

КонецФункции // ПолучитьДанныеОрганизацияПриИзменении()

// Процедура заполняет ставку НДС по умолчанию.
//
&НаСервере
Процедура ЗаполнитьСтавкуНДСПоУмолчанию()
	
	Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда
		СтавкаНДСПоУмолчанию = Справочники.СтавкиНДС.СтавкаНДС(Объект.Организация.ВидСтавкиНДСПоУмолчанию, ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса()));
	ИначеЕсли Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
	Иначе
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСтавкуНДСПоУмолчанию()

// Процедура заполняет Ставку НДС в табличной части по системе налогообложения
// организации.
// 
&НаСервере
Процедура ЗаполнитьСтавкуНДСПоОрганизацииНалогообложениеНДС()
	
	НалогообложениеПередИзменением = Объект.НалогообложениеНДС;
	
	Объект.НалогообложениеНДС = НалогиУНФ.НалогообложениеНДС(Объект.Организация,, Объект.Дата);
	
	Если НЕ (Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПодотчетника
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.Прочее
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ПокупкаВалюты)
	   И НЕ НалогообложениеПередИзменением = Объект.НалогообложениеНДС Тогда
		
		ЗаполнитьСтавкуНДСПоНалогообложениеНДС();
		
	Иначе
		
		ЗаполнитьСтавкуНДСПоУмолчанию();
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСтавкуНДСПоНалогообложениеНДС()

// Процедура заполняет Ставку НДС в табличной части по системе налогообложения.
//
&НаСервере
Процедура ЗаполнитьСтавкуНДСПоНалогообложениеНДС(ВосстанавливатьСтавкиНДС = Истина)
	
	ЗаполнитьСтавкуНДСПоУмолчанию();
	
	Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда
		
		Если Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПокупателя
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПоставщика Тогда
			
			Элементы.РасшифровкаПлатежаСтавкаНДС.Видимость = Истина;
			Элементы.РасшифровкаПлатежаСуммаНДС.Видимость = Истина;
			Элементы.СуммаНДС.Видимость = Истина;
			
		ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет Тогда
			
			Элементы.РозничнаяВыручкаРасшифровкаПлатежаСтавкаНДС.Видимость = Истина;
			Элементы.РозничнаяВыручкаРасшифровкаПлатежаСуммаНДС.Видимость = Истина;
			Элементы.СуммаНДС.Видимость = Истина;
			
		КонецЕсли;
		
		СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтавкаНДСПоУмолчанию);
		
		Если ВосстанавливатьСтавкиНДС Тогда
			Для каждого СтрокаТабличнойЧасти Из Объект.РасшифровкаПлатежа Цикл
				СтрокаТабличнойЧасти.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(Объект.Организация.ВидСтавкиНДСПоУмолчанию, ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса()));
				СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.СуммаПлатежа - (СтрокаТабличнойЧасти.СуммаПлатежа) / ((СтавкаНДС + 100) / 100);
			КонецЦикла;
		КонецЕсли;
		
	Иначе
		
		Если Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПокупателя
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПоставщика Тогда
			
			Элементы.РасшифровкаПлатежаСтавкаНДС.Видимость = Ложь;
			Элементы.РасшифровкаПлатежаСуммаНДС.Видимость = Ложь;
			Элементы.СуммаНДС.Видимость = Ложь;
			
		ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет Тогда
			
			Элементы.РозничнаяВыручкаРасшифровкаПлатежаСтавкаНДС.Видимость = Ложь;
			Элементы.РозничнаяВыручкаРасшифровкаПлатежаСуммаНДС.Видимость = Ложь;
			Элементы.СуммаНДС.Видимость = Ложь;
			
		КонецЕсли;
		
		Если ВосстанавливатьСтавкиНДС Тогда
			Для каждого СтрокаТабличнойЧасти Из Объект.РасшифровкаПлатежа Цикл
				СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
				СтрокаТабличнойЧасти.СуммаНДС = 0;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСтавкуНДСПоНалогообложениеНДС()

// Процедура устанавливает видимость поля Налогообложение.
//
&НаСервере
Процедура УстановитьВидимостьНалогообложениеНДС()
	
	Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда
		
		Если Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПокупателя
		 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПоставщика Тогда
			
			Элементы.РасшифровкаПлатежаСтавкаНДС.Видимость = Истина;
			Элементы.РасшифровкаПлатежаСуммаНДС.Видимость = Истина;
			
		ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет Тогда
			
			Элементы.РозничнаяВыручкаРасшифровкаПлатежаСтавкаНДС.Видимость = Истина;
			Элементы.РозничнаяВыручкаРасшифровкаПлатежаСуммаНДС.Видимость = Истина;
			
		КонецЕсли;
		
		СтавкаНДСПоУмолчанию = Справочники.СтавкиНДС.СтавкаНДС(Объект.Организация.ВидСтавкиНДСПоУмолчанию, ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса()));
		
	Иначе
		
		Если Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПокупателя
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПоставщика Тогда
			
			Элементы.РасшифровкаПлатежаСтавкаНДС.Видимость = Ложь;
			Элементы.РасшифровкаПлатежаСуммаНДС.Видимость = Ложь;
			
		ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет Тогда
			
			Элементы.РозничнаяВыручкаРасшифровкаПлатежаСтавкаНДС.Видимость = Ложь;
			Элементы.РозничнаяВыручкаРасшифровкаПлатежаСуммаНДС.Видимость = Ложь;
			
		КонецЕсли;
		
		Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда	
			СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
		Иначе
			СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // УстановитьВидимостьНалогообложениеНДС()

// Процедура устанавливает видимость элементов в зависимости от вида операции.
//
&НаСервере
Процедура УстановитьВидимостьЭлементовВЗависимостиОтВидаОперации()
	
	Элементы.СуммаДокумента.Ширина = 14;
	Элементы.Контрагент.Видимость = Ложь;
	
	Элементы.РасшифровкаПлатежаПодбор.Видимость = Истина;
	Элементы.РасшифровкаПлатежаЗаполнитьРасшифровку.Видимость = Истина;
	Элементы.Контрагент.Видимость = Истина;
	Элементы.Контрагент.Заголовок = НСтр("ru='Покупатель'");
	НовыйМассив = Новый Массив();
	НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Контрагент", "Объект.Контрагент");
	НовыйМассив.Добавить(НоваяСвязь);
	НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
	Элементы.РасшифровкаПлатежаСчетНаОплату.СвязиПараметровВыбора = НовыеСвязи;
	
	Элементы.СуммаПлатежа.Видимость = Истина;
	Элементы.СуммаПлатежа.Заголовок = НСтр("ru='Сумма платежа'");
	Элементы.СуммаРасчетов.Видимость = НЕ ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийЭквайринга.ВозвратОплатыПокупателю Тогда
		Элементы.ОперацияЗаполнить.Видимость = Истина;
		Элементы.СчетФактураНадпись.Видимость = Ложь;
	Иначе
		Элементы.ПроцентКомиссии.Видимость = Истина;
		Элементы.СуммаКомиссииПоДоговору.Видимость = Истина;
		Элементы.ОперацияЗаполнить.Видимость = Ложь;
		Элементы.СчетФактураНадпись.Видимость = Истина;
	КонецЕсли;
	
	Элементы.ОплатитьКартой.Видимость = (Объект.ВидОперации = ВидОперацииОплата);
	Элементы.ВернутьНаКарту.Видимость = (Объект.ВидОперации <> ВидОперацииОплата);
	Элементы.ДекорацияРазделитель2.Видимость = (Не Элементы.ОплатитьКартой.Видимость И Не Элементы.ВернутьНаКарту.Видимость);
	УстановитьДоступностьКнопок();
	
	Если ЗначениеЗаполнено(Объект.ЭквайринговыйТерминал) Тогда
		Элементы.Касса.Видимость = (ТипЗнч(Объект.ЭквайринговыйТерминал.Касса) = Тип("СправочникСсылка.Кассы"));
		Элементы.КассаККМ.Видимость = Не Элементы.Касса.Видимость;
	Иначе
		Элементы.Касса.Видимость = Ложь;
		Элементы.КассаККМ.Видимость = Истина;
	КонецЕсли;
	
	Элементы.ВидПлатежнойКарты.ТолькоПросмотр = НЕ ЭТИспользоватьБезПодключенияОборудования;
	Элементы.НомерПлатежнойКарты.ТолькоПросмотр = НЕ ЭТИспользоватьБезПодключенияОборудования;
	Элементы.НомерЧекаЭТ.ТолькоПросмотр = НЕ ЭТИспользоватьБезПодключенияОборудования;
	Элементы.СсылочныйНомер.ТолькоПросмотр = НЕ ЭТИспользоватьБезПодключенияОборудования;
	
	Если РасчетКомиссииВОтчетеЭквайера Тогда
		Элементы.ГруппаКомиссия.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьРеквизитовРасшифровкиПлатежа(Элементы, Объект.СпособОплаты);
	
КонецПроцедуры // УстановитьВидимостьЭлементовВЗависимостиОтВидаОперации()

// Процедура выполняет действия при изменении договора контрагента.
//
&НаКлиенте
Процедура ОбработатьИзменениеДоговораКонтрагента()
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Договор) Тогда
		СтруктураДанные = ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(
			Объект.Дата,
			СтрокаТабличнойЧасти.Договор
		);
		СтрокаТабличнойЧасти.Курс = ?(
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс = 0,
			1,
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс
		);
		СтрокаТабличнойЧасти.Кратность = ?(
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность = 0,
			1,
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность
		);
	КонецЕсли;
	
	СтрокаТабличнойЧасти.СуммаРасчетов = ВалютыУНФКлиентСервер.Пересчитать(
		СтрокаТабличнойЧасти.СуммаПлатежа,
		Курс,
		СтрокаТабличнойЧасти.Курс,
		Кратность,
		СтрокаТабличнойЧасти.Кратность
	);
	
КонецПроцедуры // ОбработатьИзменениеДоговораКонтрагента()

// Процедура выполняет действия при начале выбора договора контрагента.
//
&НаКлиенте
Процедура ОбработатьНачалоВыбораДоговораКонтрагента(Элемент, СтандартнаяОбработка)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = ПолучитьПараметрыФормыВыбора(Объект.Ссылка, Объект.Организация, Объект.Контрагент, СтрокаТабличнойЧасти.Договор, Объект.ВидОперации);
	Если ПараметрыФормы.КонтролироватьВыборДоговора Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаВыбора", ПараметрыФормы, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры // ОбработатьИзменениеДоговораКонтрагента()

// Процедура заполняет строку ТЧ РасшифровкаПлатежа данными документа расчетов.
//
&НаКлиенте
Процедура ОбработатьВыборДокументаРасчетов(ДанныеДокумента)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	Если ТипЗнч(ДанныеДокумента) = Тип("Структура") Тогда
		
		СтрокаТабличнойЧасти.Документ = ДанныеДокумента.Документ;
		Если ДанныеДокумента.УстановитьЗаказ Тогда
			СтрокаТабличнойЧасти.Заказ = ДанныеДокумента.Заказ;
		КонецЕсли;
		СтрокаТабличнойЧасти.СчетНаОплату = ДанныеДокумента.СчетНаОплату;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Договор) Тогда
			СтрокаТабличнойЧасти.Договор = ДанныеДокумента.Договор;
			ОбработатьИзменениеДоговораКонтрагента();
		КонецЕсли;
		
		ВыполнитьДействияПриИзмененииДокументаРасчетов();
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработатьВыборДокументаРасчетов()

// Процедура определяет признак аванса в зависимости от типа документа расчетов.
//
&НаКлиенте
Процедура ВыполнитьДействияПриИзмененииДокументаРасчетов()
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЭквайринга.ВозвратОплатыПокупателю") Тогда
		
		Если ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.ОперацияПоПлатежнымКартам")
			ИЛИ ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.ЧекККМ") Тогда
			
			СтрокаТабличнойЧасти.ПризнакАванса = Истина;
			
			КурсКратностьДокументаРасчетов = РасчетыРаботаСФормамиВызовСервера.ПолучитьКурсКратностьДокументаРасчетов(СтрокаТабличнойЧасти.Документ);
			
			Если ЗначениеЗаполнено(КурсКратностьДокументаРасчетов) Тогда
				ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, КурсКратностьДокументаРасчетов);
				
				РассчитатьСуммуПлатежа(СтрокаТабличнойЧасти);
				Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
					Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
				КонецЕсли;
				
			КонецЕсли
			
		Иначе
			
			СтрокаТабличнойЧасти.ПризнакАванса = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьДействияПриИзмененииДокументаРасчетов()

// Процедура заполняет расшифровку платежа.
//
&НаСервере
Процедура ЗаполнитьРасшифровкуПлатежа(ТекущийОбъект = Неопределено)
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ЗаполнитьРасшифровкуПлатежа();
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;
	
КонецПроцедуры // ЗаполнитьРасшифровкуПлатежа()

// Получает структуру параметров формы выбора договора контрагента.
//
&НаСервереБезКонтекста
Функция ПолучитьПараметрыФормыВыбора(Документ, Организация, Контрагент, Договор, ВидОперации)
	
	СписокВидовДоговоров = Справочники.ДоговорыКонтрагентов.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КонтролироватьВыборДоговора", Контрагент.ВестиРасчетыПоДоговорам);
	ПараметрыФормы.Вставить("Контрагент", Контрагент);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("ВидыДоговоров", СписокВидовДоговоров);
	ПараметрыФормы.Вставить("ТекущаяСтрока", Договор);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	
	Возврат ПараметрыФормы;
	
КонецФункции

// Получает договор по умолчанию в зависимости от способа ведения расчетов.
//
&НаСервереБезКонтекста
Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация, ВидОперации) Экспорт
	
	Если НЕ Контрагент.ВестиРасчетыПоДоговорам Тогда
		Возврат Справочники.ДоговорыКонтрагентов.ДоговорПоУмолчанию(Контрагент);
	КонецЕсли;
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, СписокВидовДоговоров);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

&НаСервере
Процедура ОбновитьВидимостьИОстаткиВзаиморасчетов()
	
	Элементы.ОстатокВзаиморасчетов.Видимость = НЕ ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.Контрагент);

	Если НЕ Элементы.ОстатокВзаиморасчетов.Видимость Тогда
		//Только для новых объектов
		Элементы.Контрагент.АвтоМаксимальнаяШирина = Истина;
		Возврат;
	КонецЕсли;
	
	Элементы.Контрагент.АвтоМаксимальнаяШирина = Ложь;
	Элементы.ОстатокВзаиморасчетов.Заголовок = РаботаСКонтрагентамиУНФ.ЗаголовокНадписиВзаиморасчетов(Объект.Контрагент);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкиУчетаВНалогообложении()
	
	Объект.СпециальныйНалоговыйРежим = НалогиУНФ.СпециальныйНалоговыйРежим(Объект.Организация,,Объект.Дата);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКурсВРасшифровкеПлатежаИПересчитатьСуммы(КнопкаРезультат, ДополнительныеПараметры) Экспорт

	Если КнопкаРезультат = КодВозвратаДиалога.Да Тогда
		ОбновитьКурсыРасчетовРасшифровкиПлатежаНаСервере();
	КонецЕсли;

КонецПроцедуры // ОбновитьКурсВРасшифровкеПлатежаИПересчитатьСуммыЗавершение()

&НаСервере
Процедура ОбновитьКурсыРасчетовРасшифровкиПлатежаНаСервере()
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.РасшифровкаПлатежа Цикл
			
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Договор) Тогда
			СтруктураДанные = РасчетыРаботаСФормамиВызовСервера.ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(
				Объект.Дата,
				СтрокаТабличнойЧасти.Договор
			);
			СтрокаТабличнойЧасти.Курс = ?(
				СтруктураДанные.ДоговорВалютаКурсКратность.Курс = 0,
				1,
				СтруктураДанные.ДоговорВалютаКурсКратность.Курс
			);
			СтрокаТабличнойЧасти.Кратность = ?(
				СтруктураДанные.ДоговорВалютаКурсКратность.Кратность = 0,
				1,
				СтруктураДанные.ДоговорВалютаКурсКратность.Кратность
			);
		КонецЕсли;
		
		СтрокаТабличнойЧасти.СуммаРасчетов = ЦенообразованиеСервер.ПересчитатьИзВалютыВВалюту(
			СтрокаТабличнойЧасти.СуммаПлатежа,
			Курс,
			СтрокаТабличнойЧасти.Курс,
			Кратность,
			СтрокаТабличнойЧасти.Кратность
		);
		
	КонецЦикла;
	
КонецПроцедуры // ОбработатьИзменениеДоговораКонтрагента()

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоОтменаОплаты(ДатаВозврата, НомерСменыККМВозврата, ИсходныйДокумент)
	
	Если НЕ ЗначениеЗаполнено(ИсходныйДокумент) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаВозврата) Тогда
		ДатаВозврата = УправлениеНебольшойФирмойВызовСервера.ДатаСеанса();
	КонецЕсли;
	
	РеквизитыИсходногоДокумента = УправлениеНебольшойФирмойВызовСервера.ЗначенияРеквизитовОбъекта(ИсходныйДокумент, "Дата,НомерСменыККМ");
	
	Возврат НачалоДня(ДатаВозврата) = НачалоДня(РеквизитыИсходногоДокумента.Дата) И НомерСменыККМВозврата = РеквизитыИсходногоДокумента.НомерСменыККМ;
	
КонецФункции

#Область ПодключаемоеОборудование

// Процедура заполнения списка видов платежных карт.
//
&НаСервере
Процедура ПолучитьСписокВыбораВидовПлатежныхКарт()
	
	ТаблицаВидовКартИПроцентовКомиссии.Очистить();
	
	ТекущаяТаблицаВидовКартИПроцентовКомиссии = Справочники.ЭквайринговыеТерминалы.ВидыПлатежныхКартИПроцентыКомиссии(Объект.ЭквайринговыйТерминал);
	Если ТекущаяТаблицаВидовКартИПроцентовКомиссии.Количество() > 0 Тогда
		ТаблицаВидовКартИПроцентовКомиссии.Загрузить(ТекущаяТаблицаВидовКартИПроцентовКомиссии);
		Элементы.ВидПлатежнойКарты.СписокВыбора.ЗагрузитьЗначения(ТекущаяТаблицаВидовКартИПроцентовКомиссии.ВыгрузитьКолонку("ВидПлатежнойКарты"));
	Иначе
		Элементы.ВидПлатежнойКарты.СписокВыбора.Очистить();
	КонецЕсли;
	
КонецПроцедуры // ПолучитьСписокВыбораВидовПлатежныхКарт()

// Получает ссылки на внешнее оборудование.
//
&НаСервере
Процедура ПолучитьСсылкиНаОборудование()

	ФискальныйРегистратор = ?(
		ИспользоватьПодключаемоеОборудование // Проверка на включенную ФО "Использовать ПО"
	  И ЗначениеЗаполнено(Объект.КассаККМ)
	  И ЗначениеЗаполнено(Объект.КассаККМ.ПодключаемоеОборудование),
	  Объект.КассаККМ.ПодключаемоеОборудование.Ссылка,
	  Справочники.ПодключаемоеОборудование.ПустаяСсылка()
	);

	ЭквайринговыйТерминал = ?(
		ИспользоватьПодключаемоеОборудование
	  И ЗначениеЗаполнено(Объект.ЭквайринговыйТерминал)
	  И ЗначениеЗаполнено(Объект.ЭквайринговыйТерминал.ПодключаемоеОборудование)
	  И НЕ Объект.ЭквайринговыйТерминал.ИспользоватьБезПодключенияОборудования,
	  Объект.ЭквайринговыйТерминал.ПодключаемоеОборудование,
	  Справочники.ПодключаемоеОборудование.ПустаяСсылка()
	);

	// Контекстное меню
	Элементы.КонтекстноеМенюГруппаАвтоматизированнойОплатыКартами.Видимость = ЗначениеЗаполнено(ЭквайринговыйТерминал);
	Элементы.НапечататьПоследнийСлипЧек.Видимость = ЗначениеЗаполнено(ЭквайринговыйТерминал);
	
КонецПроцедуры // ПолучитьСсылкиНаОборудование()

#КонецОбласти

&НаСервере
Функция ПоляКИДляОтправкиЧека()
	
	ПоляКИ = Новый Соответствие;
	ПоляКИ.Вставить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, Элементы.АдресЭП.Имя);
	ПоляКИ.Вставить(Перечисления.ТипыКонтактнойИнформации.Телефон, Элементы.Телефон.Имя);
	
	Возврат ПоляКИ;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПоляКИДляОтправкиЧекаПоУмолчанию(ДанныеКИ)
	
	Если ДанныеКИ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.АдресЭП = ДанныеКИ.Получить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Объект.Телефон = ДанныеКИ.Получить(Перечисления.ТипыКонтактнойИнформации.Телефон);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРезультатовИнтерактивныхДействий

// Процедура-обработчик результата вопроса о пересчете суммы документа. 
//
//
&НаКлиенте
Процедура ОпределитьНеобходимостьПересчетаСуммыДокумента(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = КодВозвратаДиалога.Да Тогда
		
		Если Объект.РасшифровкаПлатежа.Количество() > 0 Тогда
			ПересчитатьСуммыДокумента(Курс, Кратность, Истина);
		Иначе
			Объект.СуммаДокумента = ВалютыУНФКлиентСервер.Пересчитать(
				Объект.СуммаДокумента,
				ДополнительныеПараметры.КурсПередИзменением,
				Курс,
				ДополнительныеПараметры.КратностьПередИзменением,
				Кратность
			);
		КонецЕсли;
		
	Иначе
		
		Если Объект.РасшифровкаПлатежа.Количество() > 0 Тогда
			ПересчитатьСуммыДокумента(Курс, Кратность, Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	// Обновить курсы расчетов
	Если ДополнительныеПараметры.Свойство("ОбновитьКурсыРасчетов")  И ДополнительныеПараметры.ОбновитьКурсыРасчетов Тогда
			ПоказатьВопрос(Новый ОписаниеОповещения("ОбновитьКурсВРасшифровкеПлатежаИПересчитатьСуммы", ЭтотОбъект),
				ДополнительныеПараметры.ОбновитьКурсыРасчетовТекстВопроса,
				РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	// Конец Обновить курсы расчетов
	
	РассчитатьСуммуКомиссии();
	
КонецПроцедуры // ОпределитьНеобходимостьЗаполненияДокументаПоОснованию()

#КонецОбласти

#Область УправлениеВнешнимВидомФормы

// Процедура устанавливает основное подразделение и доступность кнопки "НапечататьЧек".
//
&НаСервере
Процедура ВидОперацииПриИзмененииНаСервере()
	
	УстановитьСвязиПараметровВыбораДоступныеТипы();
	
	НапечататьЧекДоступность = Ложь;
	
	Кнопка = Элементы.Найти("НапечататьЧек");
	Если Кнопка <> Неопределено Тогда
		
		НапечататьЧекДоступность = Истина;
		
		Кнопка.Доступность = НапечататьЧекДоступность;
		Элементы.НомерЧекаККМ.Видимость = НапечататьЧекДоступность;
		
	КонецЕсли;
	Кнопка = Элементы.Найти("ПредварительныйПросмотрЧека");
	Если Кнопка <> Неопределено Тогда
		
		Кнопка.Доступность = НапечататьЧекДоступность;
		
	КонецЕсли;
	
	ЗаполнитьСтавкуНДСПоОрганизацииНалогообложениеНДС();
	УстановитьВидимостьЭлементовВЗависимостиОтВидаОперации();
	УстановитьСтатьюДДСПриСменеВидаОперации();
	
КонецПроцедуры // УстановитьОсновноеПодразделениеИДоступностьПечатиЧека()

// Процедура устанавливает текущую страницу в зависимости от вида операции.
//
&НаКлиенте
Процедура УстановитьТекущуюСтраницу()
	
	КоличествоСтрок = Объект.РасшифровкаПлатежа.Количество();
	
	Если КоличествоСтрок = 0 Тогда
		Объект.РасшифровкаПлатежа.Добавить();
		Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
		КоличествоСтрок = 1;
	КонецЕсли;
	
КонецПроцедуры // УстановитьТекущуюСтраницу()

// Процедура очищает реквизиты, которые ранее могли быть заполнены, но не
// относятся к текущей операции.
//
&НаКлиенте
Процедура ОчиститьРеквизитыНеОтносящиесяКОперации()
	
	Если ВидОперации = ВидОперацииВозврат Тогда
		Объект.ОперацияПоПлатежнымКартамШапка = ПредопределенноеЗначение("Документ.ОперацияПоПлатежнымКартам.ПустаяСсылка");
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
		СтрокаТаблицы.Заказ = Неопределено;
		СтрокаТаблицы.Документ = Неопределено;
		СтрокаТаблицы.ПризнакАванса = Ложь;
	КонецЦикла;
	
КонецПроцедуры // ОчиститьРеквизитыНеОтносящиесяКОперации()

// Процедура устанавливает связи параметров выбора и доступные типы.
//
&НаСервере
Процедура УстановитьСвязиПараметровВыбораДоступныеТипы()
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЭквайринга.ВозвратОплатыПокупателю") Тогда
		
		Массив = Новый Массив();
		Массив.Добавить(Тип("ДокументСсылка.АктВыполненныхРабот"));
		Массив.Добавить(Тип("ДокументСсылка.Взаимозачет"));
		Массив.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
		Массив.Добавить(Тип("ДокументСсылка.ОтчетКомиссионера"));
		Массив.Добавить(Тип("ДокументСсылка.ОтчетОПереработке"));
		Массив.Добавить(Тип("ДокументСсылка.ПередачаВА"));
		Массив.Добавить(Тип("ДокументСсылка.ПриходнаяНакладная"));
		Массив.Добавить(Тип("ДокументСсылка.РасходнаяНакладная"));
		Массив.Добавить(Тип("ДокументСсылка.ОперацияПоПлатежнымКартам"));
		
		ДопустимыеТипы = Новый ОписаниеТипов(Массив, ,);
		Элементы.РасшифровкаПлатежаДокумент.ОграничениеТипа = ДопустимыеТипы;
		
		ДопустимыеТипы = Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя", , );
		Элементы.РасшифровкаПлатежаЗаказ.ОграничениеТипа = ДопустимыеТипы;
		
		ДопустимыеТипы = Новый ОписаниеТипов("ДокументСсылка.СчетНаОплатуПоставщика", , );
		Элементы.РасшифровкаПлатежаСчетНаОплату.ОграничениеТипа = ДопустимыеТипы;
		
		Элементы.РасшифровкаПлатежаДокумент.Заголовок = НСтр("ru = 'Документ к зачету'");
		Элементы.РасшифровкаПлатежаДокумент.Подсказка = НСтр("ru = 'Документ расчетов с контрагентом, по которому осуществляется возврат денежных средств'");
		
	Иначе
		
		Если ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ПриемИПередачаВРемонт") 
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "ВариантРемонта") <> Перечисления.ВариантыРемонта.НашаМастерскаяМногоэтапныйРемонт
			Тогда
			//ПриемИПередачаВРемонт это одновременно и заказ, и документ расхода
			Элементы.РасшифровкаПлатежаДокумент.ОграничениеТипа = Новый ОписаниеТипов("ДокументСсылка.ПриемИПередачаВРемонт");
			Элементы.РасшифровкаПлатежаЗаказ.ОграничениеТипа = Новый ОписаниеТипов("ДокументСсылка.ПриемИПередачаВРемонт");
		Иначе
			
			Массив = Новый Массив();
			Массив.Добавить(Тип("ДокументСсылка.ПередачаВА"));
			Массив.Добавить(Тип("ДокументСсылка.АктВыполненныхРабот"));
			Массив.Добавить(Тип("ДокументСсылка.ПриходнаяНакладная"));
			Массив.Добавить(Тип("ДокументСсылка.РасходнаяНакладная"));
			Массив.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
			Массив.Добавить(Тип("ДокументСсылка.ОтчетКомиссионера"));
			Массив.Добавить(Тип("ДокументСсылка.ОтчетОПереработке"));
			Массив.Добавить(Тип("ДокументСсылка.Взаимозачет"));
			
			ДопустимыеТипы = Новый ОписаниеТипов(Массив, , );
			Элементы.РасшифровкаПлатежа.ПодчиненныеЭлементы.РасшифровкаПлатежаДокумент.ОграничениеТипа = ДопустимыеТипы;
			
			ДопустимыеТипы = Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя", , );
			Элементы.РасшифровкаПлатежаЗаказ.ОграничениеТипа = ДопустимыеТипы;
			
		КонецЕсли;
		
		ДопустимыеТипы = Новый ОписаниеТипов("ДокументСсылка.СчетНаОплату", , );
		Элементы.РасшифровкаПлатежаСчетНаОплату.ОграничениеТипа = ДопустимыеТипы;
		
		Элементы.РасшифровкаПлатежаДокумент.Заголовок = НСтр("ru = 'Документ к зачету'");
		Элементы.РасшифровкаПлатежаДокумент.Подсказка = НСтр("ru = 'Оплачиваемый документ отгрузки товаров, работ и услуг контрагенту'");

	КонецЕсли;
	
КонецПроцедуры // УстановитьСвязиПараметровВыбораДоступныеТипы()

&НаСервере
Процедура УстановитьДоступностьКнопок()
	
	Элементы.ОплатитьКартой.Доступность = ЗначениеЗаполнено(ЭквайринговыйТерминал);
	Элементы.ВернутьНаКарту.Доступность = ЗначениеЗаполнено(ЭквайринговыйТерминал);
	
КонецПроцедуры

// Процедура устанавливает доступность элементов формы.
//
// Параметры:
//  Нет.
//
&НаСервере
Процедура УстановитьВидимостьИДоступность(ИзмененВидОперации = Ложь)
	
	// Документ основание.
	НовыйМассив = Новый Массив();
	НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
	ПараметрыВыбораДокументаОснования = НовыеПараметры;
	// Конец Документ основание.
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОплатуКартамиВОптовойТорговле") ИЛИ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Элементы.ДокументОснованиеНадпись.Видимость = Истина;
	Иначе
		Элементы.ДокументОснованиеНадпись.Видимость = Ложь;
	КонецЕсли;
	
	Если ТолькоПросмотр И НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Элементы.ДокументОснованиеНадпись.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры // УстановитьВидимостьИДоступность()

#КонецОбласти

#Область ЗаполнениеОбъектов

&НаКлиенте
Процедура СохранитьДокументКакШаблон(Параметр) Экспорт
	
	ЗаполнениеОбъектовУНФКлиент.СохранитьДокументКакШаблон(Объект, ОтображаемыеРеквизиты(), Параметр);
	
КонецПроцедуры

&НаСервере
Функция ОтображаемыеРеквизиты()
	
	Возврат ЗаполнениеОбъектовУНФ.ОтображаемыеРеквизиты(ЭтотОбъект);
	
КонецФункции

#КонецОбласти

#Область АвтоподборКонтактов

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	АвтоподборКонтактовКлиент.Подключаемый_ОбработкаВыбора(ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_АвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	АвтоподборКонтактовКлиент.Подключаемый_АвтоПодбор(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, Параметры, Ожидание,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
//@skip-warning
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма);
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти
