
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработкаКомандыЗавершение", ЭтотОбъект, ПараметрКоманды);
	
	ПоказатьВопрос(ОписаниеОповещенияОЗавершении,
		НСтр("ru = 'Выбранные отчеты о розничных продажах будут перезаполнены по данным в чеках ККМ (в т.ч. архивных). Если документ проведён, то он будет перепроведен. Продолжить?'"),
		РежимДиалогаВопрос.ДаНет,
		,
		КодВозвратаДиалога.Нет
	);
	
КонецПроцедуры // ОбработкаКоманды()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьЗаполнениеПоЧекамККМНаСервере(ПараметрКоманды)

	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтчетОРозничныхПродажах.Ссылка КАК Ссылка,
	|	ОтчетОРозничныхПродажах.СтатусКассовойСмены КАК СтатусКассовойСмены,
	|	ОтчетОРозничныхПродажах.КассаККМ.ТипКассы КАК ТипКассы,
	|	ОтчетОРозничныхПродажах.Проведен КАК Проведен
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Ссылка В(&ПараметрКоманды)";
	
	Запрос.УстановитьПараметр("ПараметрКоманды", ПараметрКоманды);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ОтчетОРозничныхПродажахОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ОписаниеОшибки = "";
		
		Документы.ОтчетОРозничныхПродажах.ЗаполнитьОтчетОРозничныхПродажахПоДаннымВЧеках(ОтчетОРозничныхПродажахОбъект);
		
		Если ОтчетОРозничныхПродажахОбъект.Проведен Тогда
			Попытка
				ОтчетОРозничныхПродажахОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				
				ТекстОшибки = ОписаниеОшибки();
				Если СтрНайти(ТекстОшибки, НСтр("ru = 'Не удалось провести'")) > 0 Тогда
					ОписаниеОшибки = НСтр("ru = 'Не удалось провести '") + ОтчетОРозничныхПродажахОбъект;
				Иначе
					ОписаниеОшибки = НСтр(
						"ru = 'При проведении отчета о розничных продажах произошла ошибка.'")
						+ Символы.ПС + ОписаниеОшибки();
				КонецЕсли;
				
				ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки, ОтчетОРозничныхПродажахОбъект);
				
			КонецПопытки;
		Иначе
			Попытка
				ОтчетОРозничныхПродажахОбъект.Записать(РежимЗаписиДокумента.Запись);
			Исключение
				
				ОписаниеОшибки = НСтр(
					"ru = 'При записи отчета о розничных продажах произошла ошибка.'")
					+ Символы.ПС + ОписаниеОшибки();
					
				ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки, ОтчетОРозничныхПродажахОбъект);
				
			КонецПопытки;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ВыполнитьАрхивациюЧековНаСервере()

&НаКлиенте
Процедура ОбработкаКомандыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ВыполнитьЗаполнениеПоЧекамККМНаСервере(ДополнительныеПараметры);
		
		Оповестить("ОбновитьФормыПослеЗакрытияКассовойСмены");
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Заполнение чека выполнено'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
