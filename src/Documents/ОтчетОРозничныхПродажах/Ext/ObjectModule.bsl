#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыЗаполненияДокумента

// Инициализирует документ
//
Процедура ИнициализироватьДокумент(ДанныеЗаполнения, СтратегияЗаполнения)
	
	НачалоКассовойСмены    = НачалоДня(ТекущаяДатаСеанса());
	ОкончаниеКассовойСмены = КонецДня(ТекущаяДатаСеанса());
	
	Для Каждого ТекСтратегия Из СтратегияЗаполнения Цикл
		
		Если ТипЗнч(ДанныеЗаполнения) = ТекСтратегия.Ключ Тогда
			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	
	КассаККМ = Справочники.КассыККМ.ПолучитьКассуККМПоУмолчанию();
	Если ЗначениеЗаполнено(КассаККМ) Тогда
		// Приводим ДанныеЗаполнения для вызова обработчика ЗаполнитьДокументПоОтбору
		ДанныеЗаполнения = Новый Структура("КассаККМ, ПодписьЗаведующегоОтделом, ПодписьКассира", КассаККМ, КассаККМ.Подразделение.ПодписьМОЛ, КассаККМ.СтруктурнаяЕдиница.ПодписьМОЛ);
	КонецЕсли;
	
КонецПроцедуры // ИнициализироватьДокумент()

// Заполняет отчет о розничных продажах в соответствии с отбором.
//
// Параметры
//  ДанныеЗаполнения - Структура - значение отбора.
Процедура ЗаполнитьДокументПоОтбору(ДанныеЗаполнения) Экспорт
	
	Если ДанныеЗаполнения.Свойство("КассаККМ") Тогда
		ЗаполнитьДокументПоКассеККМ(ДанныеЗаполнения.КассаККМ);
		ДанныеЗаполнения.Вставить("СуммаВключаетНДС", СуммаВключаетНДС);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьДокументПоОтбору()

// Заполняет табличную часть документа по инвентаризации товаров на складе.
//
Процедура ЗаполнитьТабличнуюЧастьЗапасыПоИнвентаризацииТоваровНаСкладе(ДанныеЗаполнения)

	ИнвентаризацияЗапасов = ДанныеЗаполнения.Ссылка;
	Организация = ДанныеЗаполнения.Организация;
	СтруктурнаяЕдиница = ДанныеЗаполнения.СтруктурнаяЕдиница;
	
	НалогообложениеНДС = НалогиУНФ.НалогообложениеНДС(Организация, СтруктурнаяЕдиница, Дата);
	
	Запрос = Новый Запрос( Справочники.СтавкиНДС.ПолучитьТекстЗапросаСозданияВТСтавкиНДС(?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса())) +
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МИНИМУМ(ИнвентаризацияЗапасов.НомерСтроки) КАК НомерСтроки,
	|	ИнвентаризацияЗапасов.Номенклатура КАК Номенклатура,
	|	ИнвентаризацияЗапасов.Характеристика КАК Характеристика,
	|	ИнвентаризацияЗапасов.Партия КАК Партия,
	|	ИнвентаризацияЗапасов.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	МАКСИМУМ(ИнвентаризацияЗапасов.КоличествоУчет - ИнвентаризацияЗапасов.Количество) КАК КоличествоОтклонениеИнвентаризации,
	|	СУММА(ВЫБОР
	|			КОГДА ОтчетОРозничныхПродажах.Количество ЕСТЬ NULL
	|				ТОГДА 0
	|			ИНАЧЕ ОтчетОРозничныхПродажах.Количество
	|		КОНЕЦ) КАК КоличествоОприходованное,
	|	ИнвентаризацияЗапасов.Цена КАК Цена,
	|	ЕСТЬNULL(ВТСтавкиНДС.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)) КАК СтавкаНДС,
	|	ИнвентаризацияЗапасов.КлючСвязи КАК КлючСвязи
	|ИЗ
	|	Документ.ИнвентаризацияЗапасов.Запасы КАК ИнвентаризацияЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.Запасы КАК ОтчетОРозничныхПродажах
	|		ПО ИнвентаризацияЗапасов.Номенклатура = ОтчетОРозничныхПродажах.Номенклатура
	|			И ИнвентаризацияЗапасов.Характеристика = ОтчетОРозничныхПродажах.Характеристика
	|			И ИнвентаризацияЗапасов.Партия = ОтчетОРозничныхПродажах.Партия
	|			И ИнвентаризацияЗапасов.Ссылка = ОтчетОРозничныхПродажах.Ссылка.ИнвентаризацияЗапасов
	|			И (ОтчетОРозничныхПродажах.Ссылка <> &ДокументСсылка)
	|			И (ОтчетОРозничныхПродажах.Ссылка.Проведен)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНДС КАК ВТСтавкиНДС
	|		ПО (ИнвентаризацияЗапасов.Номенклатура.ВидСтавкиНДС = ВТСтавкиНДС.ВидСтавкиНДС)
	|ГДЕ
	|	ИнвентаризацияЗапасов.Ссылка = &ДокументОснование
	|	И ИнвентаризацияЗапасов.КоличествоУчет - ИнвентаризацияЗапасов.Количество > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияЗапасов.Номенклатура,
	|	ИнвентаризацияЗапасов.Характеристика,
	|	ИнвентаризацияЗапасов.Партия,
	|	ИнвентаризацияЗапасов.ЕдиницаИзмерения,
	|	ИнвентаризацияЗапасов.Цена,
	|	ВТСтавкиНДС.СтавкаНДС,
	|	ИнвентаризацияЗапасов.КлючСвязи
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки");
		
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения.Ссылка);
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
		
	РезультатЗапроса = Запрос.Выполнить();
		
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
			
		// Заполнение табличной части документа.
		Запасы.Очистить();
		
		Пока Выборка.Следующий() Цикл
			
			КоличествоОприходовать = Выборка.КоличествоОтклонениеИнвентаризации - Выборка.КоличествоОприходованное;
			Если КоличествоОприходовать <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаТабличнойЧасти = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
			СтрокаТабличнойЧасти.Количество       = КоличествоОприходовать;
			СтрокаТабличнойЧасти.Сумма 			  = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
			
			Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда
				
				СтрокаТабличнойЧасти.СтавкаНДС = Выборка.СтавкаНДС;
				СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
				СтрокаТабличнойЧасти.СуммаНДС = ?(СуммаВключаетНДС,
												СтрокаТабличнойЧасти.Сумма
												- (СтрокаТабличнойЧасти.Сумма) / ((СтавкаНДС + 100) / 100),
												СтрокаТабличнойЧасти.Сумма * СтавкаНДС / 100);
			
				СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
				
			Иначе
				Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда	
				
					СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
				
				Иначе
				
					СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
				
				КонецЕсли;
				
				СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
				СтрокаТабличнойЧасти.СуммаНДС = 0;
			
				СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма;
				
			КонецЕсли;
			
		КонецЦикла;
				
	КонецЕсли;
		
	Если Запасы.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Нет данных для заполнения по инвентаризации.'"));
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьТабличнуюЧастьТоварыПоИнвентаризацииТоваровНаСкладе()

// Заполняет документ по кассе ККМ 
//
Процедура ЗаполнитьДокументПоКассеККМ(КассаККМ)
	
	РеквизитыКассыККМ = Справочники.КассыККМ.ПолучитьРеквизитыКассыККМ(КассаККМ);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыКассыККМ);
	НалогообложениеНДС = НалогиУНФ.НалогообложениеНДС(Организация, СтруктурнаяЕдиница, Дата);
	
КонецПроцедуры // ЗаполнитьДокументПоКассеККМ()

// Заполняет документ по кассе ККМ склада если Касса ККМ на складе одна.
//
Процедура ЗаполнитьДокументПоСкладу(СтруктурнаяЕдиница)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	КассыККМ.Ссылка КАК КассаККМ
	|ИЗ
	|	Справочник.КассыККМ КАК КассыККМ
	|ГДЕ
	|	КассыККМ.СтруктурнаяЕдиница = &СтруктурнаяЕдиница";
	
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1
		И Выборка.Следующий()
		Тогда
		КассаККМ = Выборка.КассаККМ;
		ЗаполнитьДокументПоКассеККМ(КассаККМ);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет отчет о розничных продажах на основании документа инвентаризация товаров на складе.
//
// Параметры
//  ДанныеЗаполнения - Структура - значение отбора.
Процедура ЗаполнитьПоИнвентаризацииЗапасовНаСкладе(ДанныеЗаполнения) Экспорт
	
	Если ДанныеЗаполнения.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы <> Перечисления.ТипыСтруктурныхЕдиниц.Розница Тогда
		ВызватьИсключение (НСтр(
			"ru = 'Документ может быть заполнен только на основании инвентаризации по розничному складу.'"));
	КонецЕсли;
	
	СтруктурнаяЕдиница = ДанныеЗаполнения.СтруктурнаяЕдиница;
	
	ЗаполнитьДокументПоСкладу(СтруктурнаяЕдиница);
	
	ЗаполнитьТабличнуюЧастьЗапасыПоИнвентаризацииТоваровНаСкладе(ДанныеЗаполнения);
	
КонецПроцедуры

// Добавляет дополнительные реквизиты, необходимые для проведения документа в
// переданную структуру.
//
// Параметры:
//  СтруктураДополнительныеСвойства - Структура - дополнительные свойства документа.
//
Процедура ДобавитьРеквизитыВДополнительныеСвойстваДляПроведения(СтруктураДополнительныеСвойства)
	
	Если КассаККМ.ТипКассы = Перечисления.ТипыКассККМ.ФискальныйРегистратор 
		ИЛИ КассаККМ.ТипКассы = Перечисления.ТипыКассККМ.ККМED Тогда
		ПолноеПроведение = СтатусКассовойСмены = Перечисления.СтатусыОтчетаОРозничныхПродажах.ЗакрытаЧекиЗаархивированы;
	Иначе
		ПолноеПроведение = (СтатусКассовойСмены = Перечисления.СтатусыОтчетаОРозничныхПродажах.ЗакрытаЧекиЗаархивированы)
					   ИЛИ (СтатусКассовойСмены = Перечисления.СтатусыОтчетаОРозничныхПродажах.Закрыта);
	КонецЕсли;
	
	СтруктураДополнительныеСвойства.ДляПроведения.Вставить("ПолноеПроведение", ПолноеПроведение);
	
	// При закрытии смены обработка заполнения не вызывается, а значит нужно подготовить данные по серийным номерам.
	Если Не СтруктураДополнительныеСвойства.Свойство("ДанныеСерийНоменклатурыДляПроведения") Тогда
		ДанныеСерийНоменклатурыДляПроведения = СерииНоменклатурыУНФ.СвернутьСерииНоменклатурыДляПроверки(Запасы, СерииНоменклатуры);
		СтруктураДополнительныеСвойства.Вставить("ДанныеСерийНоменклатурыДляПроведения", ДанныеСерийНоменклатурыДляПроведения);
	КонецЕсли;
	
	СтруктураДополнительныеСвойства.ДляПроведения.Вставить(
		"КомиссияОбработана",
		УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеКонстанты("ОбработкаКомиссииЗавершена"));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	// Прослеживаемость
	СведенияПрослеживаемости.Очистить();
	// Конец Прослеживаемость
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("Структура")] = "ЗаполнитьДокументПоОтбору";
	СтратегияЗаполнения[Тип("ДокументСсылка.ИнвентаризацияЗапасов")] = "ЗаполнитьПоИнвентаризацииЗапасовНаСкладе";
	
	ИнициализироватьДокумент(ДанныеЗаполнения, СтратегияЗаполнения);
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения, "ВидЦен");
	
	// Прослеживаемость
	ПрослеживаемостьУНФ.ОбновитьПризнакПрослеживаемости(Запасы, Дата);
	
КонецПроцедуры // ОбработкаЗаполнения()

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Положение ответственного
	Если ПоложениеОтветственный <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
			СтрокаТабличнойЧасти.Ответственный = Ответственный;
		КонецЦикла;
	Иначе
		Ответственный = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(Запасы, "Ответственный");
	КонецЕсли;
	// Склад в ТЧ
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтруктурнаяЕдиница) Тогда
			Продолжить;
		КонецЕсли; 
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = СтруктурнаяЕдиница;
	КонецЦикла;
	
	СуммаДокумента = Запасы.Итог("Всего");
	
	//+ ГИСМ
	ЕстьМаркируемаяПродукцияГИСМ = ИнтеграцияГИСМУНФ.ЕстьМаркируемаяПродукцияГИСМ(Запасы);
	//- ГИСМ
	
	// Заполнение безналичной оплаты для старых документов
	Если БезналичнаяОплата.Количество() > 0 Тогда
		РаботаСПодарочнымиСертификатами.ПроверитьЗаполнитьБезналичнуюОплатуДокумента(ЭтотОбъект);
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоПерепроведение", Проведен);
	
	// Прослеживаемость
	ТребуетсяПодобратьРНПТ = ПрослеживаемостьУНФ.ТребуетсяПодобратьРНПТ(Запасы, Дата, Истина);
	Если ТребуетсяПодобратьРНПТ Тогда
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			ПрослеживаемостьУНФ.ПодобратьРНПТРозница(ЭтотОбъект, Отказ);
		КонецЕсли; 
	Иначе
		СведенияПрослеживаемости.Очистить();
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из ЭтотОбъект.Запасы Цикл
		Если СтрокаТаблицы.ПрослеживаемыйТовар Тогда
			СтрокаТаблицы.НомерГТД = Неопределено;
		КонецЕсли;
	КонецЦикла;
	// Конец Прослеживаемость
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ОбработкаПроверкиЗаполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ТипКассы = Справочники.КассыККМ.ПолучитьРеквизитыКассыККМ(КассаККМ).ТипКассы;
	
	Если ТипКассы = Перечисления.ТипыКассККМ.ФискальныйРегистратор
		И НЕ ЗначениеЗаполнено(КассоваяСмена) Тогда
		
		ОткрытаяКассоваяСмена = Документы.ОтчетОРозничныхПродажах.ПолучитьОткрытуюКассовуюСмену(КассаККМ, Ссылка,
			НачалоКассовойСмены, ОкончаниеКассовойСмены);
		Если ОткрытаяКассоваяСмена <> Неопределено И ОткрытаяКассоваяСмена <> Ссылка Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru='По данной кассе на дату %1 уже зарегистрирован %2'"), Дата,
				ОткрытаяКассоваяСмена);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , , Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ОкончаниеКассовойСмены) И ЗначениеЗаполнено(СтатусКассовойСмены)
			И СтатусКассовойСмены <> Перечисления.СтатусыОтчетаОРозничныхПродажах.Открыта Тогда
			ТекстСообщения = НСтр("ru = 'Поле ""Окончание смены"" не заполнено.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОкончаниеКассовойСмены", , Отказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОкончаниеКассовойСмены) И ОкончаниеКассовойСмены < НачалоКассовойСмены Тогда
			ТекстСообщения = НСтр("ru = 'Время начала кассовой смены больше времени окончания кассовой смены.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОкончаниеКассовойСмены", , Отказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтатусКассовойСмены)
			И СтатусКассовойСмены = Перечисления.СтатусыОтчетаОРозничныхПродажах.Открыта
			И НачалоКассовойСмены <> Дата Тогда
			ТекстСообщения = НСтр("ru = 'Время начала кассовой смены отличается от даты документа.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "НачалоКассовойСмены", , Отказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтатусКассовойСмены) И СтатусКассовойСмены
			<> Перечисления.СтатусыОтчетаОРозничныхПродажах.Открыта И ОкончаниеКассовойСмены <> Дата Тогда
			ТекстСообщения = НСтр("ru = 'Время окончания кассовой смены отличается от даты документа.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОкончаниеКассовойСмены", , Отказ);
		КонецЕсли;
	
	КонецЕсли;
	
	// Скидка 100%.
	Если Константы.ФункциональнаяОпцияИспользоватьРучныеСкидкиНаценкиПродажи.Получить() Тогда
		Для каждого СтрокаЗапасы Из Запасы Цикл
			Если (СтрокаЗапасы.Количество*СтрокаЗапасы.Цена) > (СтрокаЗапасы.СуммаАвтоматическойСкидки+СтрокаЗапасы.СуммаСкидкиНаценки+СтрокаЗапасы.СуммаСкидкиОплатыБонусом)
				И НЕ ЗначениеЗаполнено(СтрокаЗапасы.Сумма)
				И (СтрокаЗапасы.Резерв = 0 И 
					ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыВРозничнойТорговле") И 
					ПолучитьФункциональнуюОпцию("РезервированиеЗапасов"))
				Тогда
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'Не заполнена колонка ""Сумма"" в строке %1 списка ""Запасы"".'"), СтрокаЗапасы.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки,
					"Сумма");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Заказы покупателей в рознице
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыВРозничнойТорговле") И ПолучитьФункциональнуюОпцию("РезервированиеЗапасов") Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Запасы.Количество"));
		
		Для каждого СтрокаЗапасы Из Запасы Цикл
			Если СтрокаЗапасы.Количество = 0 И СтрокаЗапасы.Резерв = 0 Тогда
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'Не заполнена колонка ""Количество"" в строке %1 списка ""Запасы"".'"),
					СтрокаЗапасы.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки,
					"Количество");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	// Конец Заказы покупателей в рознице
	
	// Серии номенклатуры
	ДанныеДляПроверкиСерийНоменклатуры = СерииНоменклатурыУНФ.СвернутьСерииНоменклатурыДляПроверки(Запасы, СерииНоменклатуры);
	ДополнительныеСвойства.Вставить("ДанныеСерийНоменклатурыДляПроведения", ДанныеДляПроверкиСерийНоменклатуры);
	СерииНоменклатурыУНФ.ПроверкаЗаполненияСерийНоменклатуры(
		Отказ,
		ДанныеДляПроверкиСерийНоменклатуры.Запасы,
		ДанныеДляПроверкиСерийНоменклатуры.СерииНоменклатуры,
		СтруктурнаяЕдиница,
		ЭтотОбъект);
	
	// Наборы
	НаборыСервер.ПроверитьТабличнуюЧасть(ЭтотОбъект, "Запасы", Отказ);
	// КонецНаборы
	
	// ПодарочныеСертификаты
	Если Константы.ФункциональнаяОпцияИспользоватьПодарочныеСертификаты.Получить() Тогда
		
		Если Не РаботаСПодарочнымиСертификатами.УказанКонтрагентДляПредоплаты() Тогда
			ТекстСообщения = НСтр(
				"ru = 'Не заполнена константа ""Служебный контрагент для подарочных сертификатов"".
				|Необходимо указать контрагента: Продажи - Еще больше возможностей.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , , Отказ);
		КонецЕсли;
		
		СтруктураДляПроверкиОбластиДействия = ПодготовитьСтруктуруСертификатовДляПроверки();
		РаботаСПодарочнымиСертификатами.ПроверитьОбластьДействияСертификатов(СтруктураДляПроверкиОбластиДействия, Ложь);
		
	КонецЕсли;
	// Конец ПодарочныеСертификаты
	
	// Бонусы
	Если ПолучитьФункциональнуюОпцию("ИспользоватьБонусныеПрограммы") Тогда
		Если Не БезналичнаяОплата.Найти(Перечисления.ВидыБезналичныхОплат.Бонусы, "ВидОплаты") = Неопределено Тогда
			
			ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("БезналичнаяОплата.Сумма"));
			
			Для Каждого СтрокаОплаты Из БезналичнаяОплата Цикл
				
				Если СтрокаОплаты.ВидОплаты = Перечисления.ВидыБезналичныхОплат.Бонусы Тогда
					Если Не ЗначениеЗаполнено(СтрокаОплаты.СуммаБонусов) Тогда
						ТекстСообщения = СтрШаблон(НСтр("ru = 'В строке №%1 не указана сумма оплаты.'"),
							СтрокаОплаты.НомерСтроки);
						КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("БезналичнаяОплата",
							СтрокаОплаты.НомерСтроки, "СуммаБонусов");
						ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
					КонецЕсли;
				Иначе
					Если Не ЗначениеЗаполнено(СтрокаОплаты.Сумма) Тогда
						ТекстСообщения = СтрШаблон(НСтр("ru = 'В строке №%1 не указана сумма оплаты.'"),
							СтрокаОплаты.НомерСтроки);
						КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("БезналичнаяОплата",
							СтрокаОплаты.НомерСтроки, "Сумма");
						ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	// Конец Бонусы
	
	НоменклатураВДокументахСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, Отказ, Истина);
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

// Процедура - обработчик события ОбработкаПроведения.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если (КассаККМ.ТипКассы = Перечисления.ТипыКассККМ.ФискальныйРегистратор И ДополнительныеСвойства.Свойство("ЭтоПерепроведение")
		И НЕ ДополнительныеСвойства.ЭтоПерепроведение)
		ИЛИ (ДополнительныеСвойства.Свойство("ЭтоЗакрытиеСмены") И ДополнительныеСвойства.ЭтоЗакрытиеСмены) Тогда
		// Нужно удалить движения по регистру Запасы из чеков.
		// Если отчет о розничных продажах не проведется, то нужно откатить удаление движений.
		// Т.к. проведение выполняется в транзакции, то реализуем удаление в обработке проведения.
		// Если это делать в отдельной транзакции, то придется запрещать закрывать смену,
		// если отчет о розничных продажах не провелся, а это значит, что пользователь не сможет открыть новую смену,
		// пока не решит вопрос с проведением отчета о розничных продажах.
		Документы.ОтчетОРозничныхПродажах.УдалитьДвиженияПоРегиструЗапасыВПробитыхЧекахККМ(ЭтотОбъект);
	КонецЕсли;
	
	// Инициализация дополнительных свойств для проведения документа.
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	ДобавитьРеквизитыВДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.ОтчетОРозничныхПродажах.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ПроведениеДокументовУНФ.ОтразитьДвижения("Запасы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыВРазрезеГТД", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("Продажи", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыНаСкладах", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходыНераспределенные", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходыКассовыйМетод", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДенежныеСредстваВКассахККМ", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыИАгентскиеУслугиПринятые", ТаблицыДляДвижений, Движения, Отказ);
	
	// ДисконтныеКарты
	ПроведениеДокументовУНФ.ОтразитьДвижения("ПродажиПоДисконтнымКартам", ТаблицыДляДвижений, Движения, Отказ);
	
	// СерииНоменклатуры
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатурыГарантии", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДвиженияСерииНоменклатуры", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатуры", ТаблицыДляДвижений, Движения, Отказ);
	
	// Подарочные сертификаты
	ПроведениеДокументовУНФ.ОтразитьДвижения("ПодарочныеСертификаты", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ОплатаПодарочнымиСертификатами", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("РасчетыСПокупателями", ТаблицыДляДвижений, Движения, Отказ);
	
	// Бонусы
	ПроведениеДокументовУНФ.ОтразитьДвижения("БонусныеБаллы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("НачисленияБонусныхБаллов", ТаблицыДляДвижений, Движения, Отказ);
	
	// АвтоматическиеСкидки
	ПроведениеДокументовУНФ.ОтразитьДвижения("ПредоставленныеСкидки", ТаблицыДляДвижений, Движения, Отказ);
	// Эквайринг
	ПроведениеДокументовУНФ.ОтразитьДвижения("ОплатаПлатежнымиКартами", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("РасчетыПоЭквайрингу", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходыКассовыйМетодЭквайринг", ТаблицыДляДвижений, Движения, Отказ);
	// Конец Эквайринг
	
	// Интеркампани
	ПроведениеДокументовУНФ.ОтразитьДвижения("РезервыТоваровОрганизаций", ТаблицыДляДвижений, Движения, Отказ);
	// Конец Интеркампани
	
	ПроведениеДокументовУНФ.ОтразитьУправленческий(ДополнительныеСвойства, Движения, Отказ);
	
	// Заказ покупателя в розничной торговле
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗаказыПокупателей", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ОплатаСчетовИЗаказов", ТаблицыДляДвижений, Движения, Отказ);
	
	// Прослеживаемость
	УправлениеНебольшойФирмойСервер.ОтразитьПрослеживаемыеТовары(ДополнительныеСвойства, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ОперацииСПрослеживаемымиТоварами", ТаблицыДляДвижений, Движения, Отказ);
	
	// Запись наборов записей.
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль возникновения отрицательного остатка.
	Если КассаККМ.ТипКассы <> Перечисления.ТипыКассККМ.ККМED Тогда
		Документы.ОтчетОРозничныхПродажах.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	КонецЕсли;
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаУдаленияПроведения.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль возникновения отрицательного остатка.
	Если КассаККМ.ТипКассы <> Перечисления.ТипыКассККМ.ККМED Тогда
		Документы.ОтчетОРозничныхПродажах.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ, Истина);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ПриКопировании(ОбъектКопирования)
	
	// Прослеживаемость
	СведенияПрослеживаемости.Очистить();
	ПрослеживаемостьУНФ.ОбновитьПризнакПрослеживаемости(Запасы, Дата);
	// Конец Прослеживаемость
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПодготовитьСтруктуруСертификатовДляПроверки()
	
	СтруктураДляПроверки = Новый Структура;
	
	СтруктураДляПроверки.Вставить("Запасы", Запасы.Выгрузить(,"Номенклатура, Характеристика, Сумма"));
	СтруктураДляПроверки.Вставить("Сертификаты", БезналичнаяОплата.Выгрузить(
		Новый Структура("ВидОплаты", Перечисления.ВидыБезналичныхОплат.ПодарочныйСертификат),
		"ПодарочныйСертификат, Сумма"));
		
	Возврат СтруктураДляПроверки;
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли