#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Контрагент)
	|	И ЗначениеРазрешено(СтруктурнаяЕдиница)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
// @skip-warning
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//   Настройки - Структура - настройки подсистемы.
// @skip-warning
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Ограничивает видимость реквизитов объекта в отчете по версии.
//
// Параметры:
//   Реквизиты - Массив - список имен реквизитов объекта.
// @skip-warning
Процедура ПриПолученииСлужебныхРеквизитов(Реквизиты) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область СлужебныйИнтерфейс

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасы(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаЗапасы.ВидДвижения КАК ВидДвижения,
	|	ТаблицаЗапасы.Период КАК Период,
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ТаблицаЗапасы.КоррОрганизация КАК КоррОрганизация,
	|	ЕСТЬNULL(ТаблицаЗапасы.СтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК СтруктурнаяЕдиница,
	|	ЕСТЬNULL(ТаблицаЗапасы.КоррСтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)) КАК КоррСтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ТаблицаЗапасы.КоррСчетУчета КАК КоррСчетУчета,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость КАК СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.НоменклатураНабора КАК НоменклатураНабора,
	|	ТаблицаЗапасы.КоррНоменклатура КАК КоррНоменклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ТаблицаЗапасы.КоррХарактеристика КАК КоррХарактеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.КоррПартия КАК КоррПартия,
	|	ТаблицаЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|			ТОГДА ТаблицаЗапасы.ДокументОснование.Ответственный
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
	|	КОНЕЦ КАК Ответственный,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Возврат,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|				И (ЕСТЬNULL(ВЫРАЗИТЬ(ТаблицаЗапасы.ДокументОснование КАК Документ.РасходнаяНакладная).ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю)
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(ТаблицаЗапасы.ДокументОснование КАК Документ.ЗаказПокупателя).ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд))
	|			ТОГДА ТаблицаЗапасы.ДокументОснование
	|		КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|				И ТаблицаЗапасы.ДокументОснование ССЫЛКА Документ.ЧекККМ
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаЗапасы.ДокументОснование КАК Документ.ЧекККМ).КассоваяСмена
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДокументПродажи,
	|	ВЫБОР
	|		КОГДА НЕ ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				И НЕ ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|				И НЕ ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера)
	|			ТОГДА ТаблицаЗапасы.Заказ
	|		КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика)
	|			ТОГДА ТаблицаЗапасы.Заказ.ЗаказПокупателя
	|		КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|				И ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ТаблицаЗапасы.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				И ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|			ТОГДА ТаблицаЗапасы.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаказПродажи,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|			ТОГДА ТаблицаЗапасы.ПодразделениеПродажи
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК Подразделение,
	|	ТаблицаЗапасы.НаправлениеДеятельностиПродажи,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И (ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|					ИЛИ ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию))
	|			ТОГДА ТаблицаЗапасы.Заказ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ИсточникОбеспечения,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.КоррЗаказ ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ТаблицаЗапасы.КоррЗаказ
	|		КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика)
	|				И ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ТаблицаЗапасы.КоррЗаказ.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК КоррЗаказПокупателя,
	|	ТаблицаЗапасы.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
	|	ТаблицаЗапасы.СодержаниеПроводки КАК СодержаниеПроводки,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|				ТОГДА -1 * ТаблицаЗапасы.Количество
	|			ИНАЧЕ ТаблицаЗапасы.Количество
	|		КОНЕЦ) КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию)
	|					ИЛИ ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемВПереработку)
	|					ИЛИ ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаОтветХранение)
	|					ИЛИ ТаблицаЗапасы.ТоварыНаКомиссии
	|					ИЛИ ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|						И (ЕСТЬNULL(ВЫРАЗИТЬ(ТаблицаЗапасы.ДокументОснование КАК Документ.РасходнаяНакладная).ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю)
	|							ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(ТаблицаЗапасы.ДокументОснование КАК Документ.ЗаказПокупателя).ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд)
	|							ИЛИ ТаблицаЗапасы.ДокументОснование ССЫЛКА Документ.ЧекККМ)
	|				ТОГДА 0
	|			КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|				ТОГДА -1 * ТаблицаЗапасы.Себестоимость
	|			КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|					И ТаблицаЗапасы.ВключатьРасходыВСебестоимость
	|				ТОГДА ТаблицаЗапасы.Сумма + ТаблицаЗапасы.СуммаРасходов
	|			ИНАЧЕ ТаблицаЗапасы.Сумма
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|					И ЕСТЬNULL(ВЫРАЗИТЬ(ТаблицаЗапасы.ДокументОснование КАК Документ.РасходнаяНакладная).ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю)
	|					И ЕСТЬNULL(ВЫРАЗИТЬ(ТаблицаЗапасы.ДокументОснование КАК Документ.ЗаказПокупателя).ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд)
	|					И НЕ ТаблицаЗапасы.ДокументОснование ССЫЛКА Документ.ЧекККМ
	|				ТОГДА ТаблицаЗапасы.Себестоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Себестоимость
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасыДляДвижений
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы,
	|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
	|ГДЕ
	|	НЕ ТаблицаЗапасы.ПеремещениеВРозницуСуммовойУчет
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Период,
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.КоррОрганизация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.КоррСтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.КоррСчетУчета,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.НоменклатураНабора,
	|	ТаблицаЗапасы.ХарактеристикаНабора,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.КоррНоменклатура,
	|	ТаблицаЗапасы.КоррХарактеристика,
	|	ТаблицаЗапасы.КоррПартия,
	|	ТаблицаЗапасы.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|			ТОГДА ТаблицаЗапасы.ДокументОснование.Ответственный
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ТаблицаЗапасы.ФиксированнаяСтоимость,
	|	ТаблицаЗапасы.СодержаниеПроводки,
	|	ТаблицаЗапасы.ВидДвижения,
	|	ВЫБОР
	|		КОГДА НЕ ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				И НЕ ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|				И НЕ ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера)
	|			ТОГДА ТаблицаЗапасы.Заказ
	|		КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика)
	|			ТОГДА ТаблицаЗапасы.Заказ.ЗаказПокупателя
	|		КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|				И ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ТаблицаЗапасы.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				И ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|			ТОГДА ТаблицаЗапасы.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|			ТОГДА ТаблицаЗапасы.ПодразделениеПродажи
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаЗапасы.НаправлениеДеятельностиПродажи,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И (ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|					ИЛИ ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию))
	|			ТОГДА ТаблицаЗапасы.Заказ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.КоррЗаказ ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ТаблицаЗапасы.КоррЗаказ
	|		КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика)
	|				И ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ТаблицаЗапасы.КоррЗаказ.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|				И (ЕСТЬNULL(ВЫРАЗИТЬ(ТаблицаЗапасы.ДокументОснование КАК Документ.РасходнаяНакладная).ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю)
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(ТаблицаЗапасы.ДокументОснование КАК Документ.ЗаказПокупателя).ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд))
	|			ТОГДА ТаблицаЗапасы.ДокументОснование
	|		КОГДА ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|				И ТаблицаЗапасы.ДокументОснование ССЫЛКА Документ.ЧекККМ
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаЗапасы.ДокументОснование КАК Документ.ЧекККМ).КассоваяСмена
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаЗапасы.НомерСтроки КАК НомерСтроки,
	|	ВременнаяТаблицаЗапасы.ВидДвижения КАК ВидДвижения,
	|	ВременнаяТаблицаЗапасы.Период КАК Период,
	|	ВременнаяТаблицаЗапасы.Организация КАК Организация,
	|	ВременнаяТаблицаЗапасы.СценарийПланирования КАК СценарийПланирования,
	|	ВременнаяТаблицаЗапасы.КоррОрганизация КАК КоррОрганизация,
	|	ВременнаяТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВременнаяТаблицаЗапасы.КоррСтруктурнаяЕдиница КАК КоррСтруктурнаяЕдиница,
	|	ВременнаяТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ВременнаяТаблицаЗапасы.КоррСчетУчета КАК КоррСчетУчета,
	|	ВременнаяТаблицаЗапасы.СчетУчетаСебестоимость КАК СчетУчетаСебестоимость,
	|	ВременнаяТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ВременнаяТаблицаЗапасы.НоменклатураНабора КАК НоменклатураНабора,
	|	ВременнаяТаблицаЗапасы.КоррНоменклатура КАК КоррНоменклатура,
	|	ВременнаяТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ВременнаяТаблицаЗапасы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ВременнаяТаблицаЗапасы.КоррХарактеристика КАК КоррХарактеристика,
	|	ВременнаяТаблицаЗапасы.Партия КАК Партия,
	|	ВременнаяТаблицаЗапасы.КоррПартия КАК КоррПартия,
	|	ВременнаяТаблицаЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ВременнаяТаблицаЗапасы.Ответственный КАК Ответственный,
	|	ВременнаяТаблицаЗапасы.Возврат КАК Возврат,
	|	ВременнаяТаблицаЗапасы.ДокументПродажи КАК ДокументПродажи,
	|	ВременнаяТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВременнаяТаблицаЗапасы.ЗаказПродажи КАК ЗаказПродажи,
	|	ВременнаяТаблицаЗапасы.Подразделение КАК Подразделение,
	|	ВременнаяТаблицаЗапасы.НаправлениеДеятельностиПродажи КАК НаправлениеДеятельностиПродажи,
	|	ВременнаяТаблицаЗапасы.ИсточникОбеспечения КАК ИсточникОбеспечения,
	|	ВременнаяТаблицаЗапасы.КоррЗаказПокупателя КАК КоррЗаказПокупателя,
	|	ВременнаяТаблицаЗапасы.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
	|	ВременнаяТаблицаЗапасы.СодержаниеПроводки КАК СодержаниеПроводки,
	|	ВременнаяТаблицаЗапасы.Количество КАК Количество,
	|	ВременнаяТаблицаЗапасы.Сумма КАК Сумма,
	|	ВременнаяТаблицаЗапасы.Себестоимость КАК Себестоимость,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаЗапасы.ЗаказПокупателя ССЫЛКА Документ.ЗаказПокупателя
	|				И ВременнаяТаблицаЗапасы.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	ВременнаяТаблицаЗапасыДляДвижений КАК ВременнаяТаблицаЗапасы
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасы", РезультатЗапроса.Выгрузить());
	
	Если ДокументСсылкаПриходнаяНакладная.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика
		 Или ДокументСсылкаПриходнаяНакладная.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию
		 Или ДокументСсылкаПриходнаяНакладная.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПриемНаОтветХранение Тогда

		СформироватьТаблицаЗапасыПоступление(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);

	ИначеЕсли ДокументСсылкаПриходнаяНакладная.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера
			  Или ДокументСсылкаПриходнаяНакладная.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика
			  Или ДокументСсылкаПриходнаяНакладная.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения
			  Или ДокументСсылкаПриходнаяНакладная.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя Тогда

		СформироватьТаблицаЗапасыВозврат(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);

	КонецЕсли;

	Если ДокументСсылкаПриходнаяНакладная.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика Тогда

		СформироватьТаблицаЗапасыРасходы(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);

	КонецЕсли;
	
КонецПроцедуры // СформироватьТаблицаЗапасы()

Процедура СформироватьТаблицаЗапасыВРазрезеГТД(СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Если СтруктураДополнительныеСвойства.ДляПроведения.Свойство("ЭтоКонтрагентИзЕАЭС") Тогда
		Запрос.УстановитьПараметр("ЭтоПоступлениеИзЕАЭС", СтруктураДополнительныеСвойства.ДляПроведения.ЭтоКонтрагентИзЕАЭС);
	Иначе
		Запрос.УстановитьПараметр("ЭтоПоступлениеИзЕАЭС", Ложь);
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыНаСкладах.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаЗапасыНаСкладах.Период КАК Период,
	|	ТаблицаЗапасыНаСкладах.Организация КАК Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия КАК Партия,
	|	ТаблицаЗапасыНаСкладах.НомерГТД КАК НомерГТД,
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(ТаблицаЗапасыНаСкладах.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыНаСкладах
	|ГДЕ
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|	И ТаблицаЗапасыНаСкладах.СтранаПроисхождения <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|	И ТаблицаЗапасыНаСкладах.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|	И ТаблицаЗапасыНаСкладах.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя))
	|	И (НЕ &ЭтоПоступлениеИзЕАЭС
	|			ИЛИ НЕ ТаблицаЗапасыНаСкладах.ПрослеживаемыйТовар
	|			ИЛИ ТаблицаЗапасыНаСкладах.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыНаСкладах.Период,
	|	ТаблицаЗапасыНаСкладах.Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия,
	|	ТаблицаЗапасыНаСкладах.НомерГТД,
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыНаСкладах.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаЗапасыНаСкладах.Период КАК Период,
	|	ТаблицаЗапасыНаСкладах.Организация КАК Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия КАК Партия,
	|	ТаблицаЗапасыНаСкладах.НомерГТД КАК НомерГТД,
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(ТаблицаЗапасыНаСкладах.Количество) КАК Количество,
	|	ТаблицаЗапасыНаСкладах.Документ КАК ДокументПриема
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыНаСкладах
	|ГДЕ
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|	И ТаблицаЗапасыНаСкладах.СтранаПроисхождения <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|	И ТаблицаЗапасыНаСкладах.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|	И ТаблицаЗапасыНаСкладах.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыНаСкладах.Период,
	|	ТаблицаЗапасыНаСкладах.Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия,
	|	ТаблицаЗапасыНаСкладах.НомерГТД,
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения,
	|	ТаблицаЗапасыНаСкладах.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыНаСкладах.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗапасыНаСкладах.Период КАК Период,
	|	ТаблицаЗапасыНаСкладах.Организация КАК Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия КАК Партия,
	|	ТаблицаЗапасыНаСкладах.НомерГТД КАК НомерГТД,
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(ТаблицаЗапасыНаСкладах.Количество) КАК Количество,
	|	ТаблицаЗапасыНаСкладах.Документ КАК ДокументПриема
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыНаСкладах
	|ГДЕ
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|	И ТаблицаЗапасыНаСкладах.СтранаПроисхождения <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|	И ТаблицаЗапасыНаСкладах.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|	И ТаблицаЗапасыНаСкладах.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыНаСкладах.Период,
	|	ТаблицаЗапасыНаСкладах.Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия,
	|	ТаблицаЗапасыНаСкладах.НомерГТД,
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения,
	|	ТаблицаЗапасыНаСкладах.Документ";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыВРазрезеГТД", РезультатЗапроса[0].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыПринятыеВРазрезеГТД", РезультатЗапроса[1].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыПереданныеВРазрезеГТД", РезультатЗапроса[2].Выгрузить());
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыПоступление(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	ТаблицаЗапасы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.СкопироватьКолонки();
	
	Для н = 0 По СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Количество() - 1 Цикл
		
		ДобавитьСтрокиЗапасыПоРазмещению(СтруктураДополнительныеСвойства, ТаблицаЗапасы, СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы[н]);
		
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы = ТаблицаЗапасы;
	
КонецПроцедуры // СформироватьТаблицаЗапасыПоступление()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыРасходы(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаЗапасы.ВидДвижения КАК ВидДвижения,
	|	ТаблицаЗапасы.ТипСчета КАК ТипСчета,
	|	ТаблицаЗапасы.ВключатьРасходыВСебестоимость КАК ВключатьРасходыВСебестоимость,
	|	ТаблицаЗапасы.Период КАК Период,
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.ЗаказПоставщику КАК ИсточникОбеспечения,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ТаблицаЗапасы.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ТаблицаЗапасы.Заказ КАК ЗаказРазмещения,
	|	СУММА(ТаблицаЗапасы.Количество) КАК Количество,
	|	СУММА(ТаблицаЗапасы.Сумма) КАК Сумма,
	|	ИСТИНА КАК ФиксированнаяСтоимость,
	|	&ПрочиеРасходы КАК СодержаниеПроводки,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|				И ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				И ТаблицаЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаЗапасы,
	|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Период,
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.ЗаказПоставщику,
	|	ТаблицаЗапасы.Заказ,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ТаблицаЗапасы.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаЗапасы.ВидДвижения,
	|	ТаблицаЗапасы.ТипСчета,
	|	ТаблицаЗапасы.ВключатьРасходыВСебестоимость,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|				И ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				И ТаблицаЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ПрочиеРасходы", НСтр("ru = 'Отражение затрат'"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ВключатьРасходыВСебестоимость Тогда
			ДобавитьРазмещениеСУчетомОстатков(СтруктураДополнительныеСвойства,
				СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказов, Выборка);
		ИначеЕсли Выборка.ТипСчета <> Перечисления.ТипыСчетов.НезавершенноеПроизводство
				  И Выборка.ТипСчета <> Перечисления.ТипыСчетов.КосвенныеЗатраты Тогда
			ДобавитьРазмещениеСУчетомОстатков(СтруктураДополнительныеСвойства,
				СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказов, Выборка);
		Иначе
			ДобавитьСтрокиЗапасыПоРазмещению(СтруктураДополнительныеСвойства,
				СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы, Выборка, Истина);
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьРазмещениеСУчетомОстатков(СтруктураДополнительныеСвойства, ТаблицаРазмещениеЗаказов, ИсточникЗаполнения)
	
	Если СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказовОстатки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДляПоиска = Новый Структура;
	СтруктураДляПоиска.Вставить("ИсточникОбеспечения",	ИсточникЗаполнения.ИсточникОбеспечения);
	СтруктураДляПоиска.Вставить("Номенклатура",			ИсточникЗаполнения.Номенклатура);
	СтруктураДляПоиска.Вставить("Характеристика",		ИсточникЗаполнения.Характеристика);
	Если ЗначениеЗаполнено(ИсточникЗаполнения.ЗаказРазмещения) Тогда
		СтруктураДляПоиска.Вставить("ЗаказПокупателя", 	ИсточникЗаполнения.ЗаказРазмещения);
	КонецЕсли;
	МассивРазмещенныхЗаказов = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказовОстатки.НайтиСтроки(
		СтруктураДляПоиска);
	
	Если ЗначениеЗаполнено(ИсточникЗаполнения.ЗаказРазмещения) Тогда
		НоваяСтрокаТаблицаРазмещениеЗаказов = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицаРазмещениеЗаказов, ИсточникЗаполнения);
		НоваяСтрокаТаблицаРазмещениеЗаказов.ЗаказПокупателя = ИсточникЗаполнения.ЗаказРазмещения;
		НоваяСтрокаТаблицаРазмещениеЗаказов.ВидДвижения = ВидДвиженияНакопления.Расход;
		// Остатки размещения
		КоличествоРазмещения = НоваяСтрокаТаблицаРазмещениеЗаказов.Количество;
		Для Каждого СтрокаМассива Из МассивРазмещенныхЗаказов Цикл
			СтрокаКоличество = Мин(КоличествоРазмещения, СтрокаМассива.Количество);
			СтрокаМассива.Количество = СтрокаМассива.Количество - СтрокаКоличество;
			КоличествоРазмещения = КоличествоРазмещения - СтрокаКоличество;
			Если КоличествоРазмещения <= 0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаОстатка Из МассивРазмещенныхЗаказов Цикл
		Количество = Мин(СтрокаОстатка.Количество, ИсточникЗаполнения.Количество);
		Если Количество <= 0 Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрокаТаблицаРазмещениеЗаказов = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицаРазмещениеЗаказов, ИсточникЗаполнения);
		НоваяСтрокаТаблицаРазмещениеЗаказов.ВидДвижения = ВидДвиженияНакопления.Расход;
		НоваяСтрокаТаблицаРазмещениеЗаказов.ЗаказПокупателя = СтрокаОстатка.ЗаказПокупателя;
		НоваяСтрокаТаблицаРазмещениеЗаказов.Количество = Количество;
		СтрокаОстатка.Количество = СтрокаОстатка.Количество - Количество;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьСтрокиЗапасыПоРазмещению(СтруктураДополнительныеСвойства, ТаблицаЗапасы, ИсточникЗаполнения,
	ЭтоРасходы = Ложь)
	
	СтрокаТаблицаЗапасы = ТаблицаЗапасы.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТаблицаЗапасы, ИсточникЗаполнения);
	
	Если СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказовОстатки.Количество() = 0 Тогда
		// Нет размещенных заказов, автоматический подбор заказов покупателей невозможен 
		Если ЭтоРасходы Тогда
			СтрокаТаблицаЗапасы.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
			СтрокаТаблицаЗапасы.Количество = 0;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СтруктураДляПоиска = Новый Структура;
	СтруктураДляПоиска.Вставить("ИсточникОбеспечения",	СтрокаТаблицаЗапасы.ИсточникОбеспечения);
	СтруктураДляПоиска.Вставить("Номенклатура",			СтрокаТаблицаЗапасы.Номенклатура);
	СтруктураДляПоиска.Вставить("Характеристика",		СтрокаТаблицаЗапасы.Характеристика);
	
	Если ЗначениеЗаполнено(СтрокаТаблицаЗапасы.ЗаказПокупателя) Тогда
		СтруктураДляПоиска.Вставить("ЗаказПокупателя", 	СтрокаТаблицаЗапасы.ЗаказПокупателя);
	КонецЕсли; 
	
	МассивРазмещенныхЗаказов = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказовОстатки.НайтиСтроки(
		СтруктураДляПоиска);
	
	Если ЗначениеЗаполнено(СтрокаТаблицаЗапасы.ЗаказПокупателя) Тогда
		
		Если ЗначениеЗаполнено(СтрокаТаблицаЗапасы.ИсточникОбеспечения) Тогда
			// Размещение. Заказ указан явно
			НоваяСтрокаТаблицаРазмещениеЗаказов = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицаРазмещениеЗаказов, СтрокаТаблицаЗапасы);
			НоваяСтрокаТаблицаРазмещениеЗаказов.ВидДвижения = ВидДвиженияНакопления.Расход;
			// Остатки размещения
			КоличествоРазмещения = НоваяСтрокаТаблицаРазмещениеЗаказов.Количество;
			Для каждого СтрокаМассива Из МассивРазмещенныхЗаказов Цикл
				СтрокаКоличество = Мин(КоличествоРазмещения, СтрокаМассива.Количество);
				СтрокаМассива.Количество = СтрокаМассива.Количество - СтрокаКоличество;
				КоличествоРазмещения = КоличествоРазмещения - СтрокаКоличество;
				Если КоличествоРазмещения<=0 Тогда
					Прервать;
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли; 
		
		Если ЭтоРасходы Тогда
			СтрокаТаблицаЗапасы.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
			СтрокаТаблицаЗапасы.Количество = 0;
		КонецЕсли;
		Возврат;
		
	КонецЕсли; 
	
	СтрокаТаблицаЗапасыКоличество = СтрокаТаблицаЗапасы.Количество;
	
	Для Каждого СтрокаМассива Из МассивРазмещенныхЗаказов Цикл

		Если СтрокаТаблицаЗапасыКоличество > 0 И СтрокаМассива.Количество >= СтрокаТаблицаЗапасыКоличество Тогда
			
			// Размещение
			НоваяСтрокаТаблицаРазмещениеЗаказов = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицаРазмещениеЗаказов, СтрокаМассива);

			НоваяСтрокаТаблицаРазмещениеЗаказов.Количество = СтрокаТаблицаЗапасыКоличество;
			
			// Запасы
			СтрокаТаблицаЗапасы.ЗаказПокупателя = СтрокаМассива.ЗаказПокупателя;
			СтрокаТаблицаЗапасыКоличество = 0;
			
			// Остатки размещения
			СтрокаМассива.Количество = СтрокаМассива.Количество - СтрокаТаблицаЗапасыКоличество;

		ИначеЕсли СтрокаТаблицаЗапасыКоличество > 0
				  И СтрокаМассива.Количество > 0
				  И СтрокаМассива.Количество < СтрокаТаблицаЗапасыКоличество Тогда
			
			// Размещение
			НоваяСтрокаТаблицаРазмещениеЗаказов = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицаРазмещениеЗаказов, СтрокаМассива);
			
			// Запасы
			СуммаКСписанию = Окр(СтрокаТаблицаЗапасы.Сумма * СтрокаМассива.Количество / СтрокаТаблицаЗапасыКоличество,
				2, 1);

			НоваяСтрокаТаблицаЗапасы = ТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицаЗапасы, СтрокаТаблицаЗапасы);
			НоваяСтрокаТаблицаЗапасы.ЗаказПокупателя = СтрокаМассива.ЗаказПокупателя;
			НоваяСтрокаТаблицаЗапасы.Количество = СтрокаМассива.Количество;
			НоваяСтрокаТаблицаЗапасы.Сумма = СуммаКСписанию;

			СтрокаТаблицаЗапасыКоличество = СтрокаТаблицаЗапасыКоличество - СтрокаМассива.Количество;

			СтрокаТаблицаЗапасы.Количество = СтрокаТаблицаЗапасыКоличество;
			СтрокаТаблицаЗапасы.Сумма = СтрокаТаблицаЗапасы.Сумма - СуммаКСписанию;
			
			// Остатки размещения
			СтрокаМассива.Количество = 0;

			Если ЭтоРасходы Тогда
				НоваяСтрокаТаблицаЗапасы.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
				НоваяСтрокаТаблицаЗапасы.Количество = 0;
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;
	
	Если ЭтоРасходы Тогда
		СтрокаТаблицаЗапасы.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
		СтрокаТаблицаЗапасы.Количество = 0;
	КонецЕсли;
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыВозврат(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылкаПриходнаяНакладная,
		"ДокументОснование, ВидОперации");
	ЭтоВозвратОтПокупателя = (ЗначенияРеквизитов.ВидОперации
							  = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя);
	
	ОснованиеНакладная = Неопределено;
	Если ЗначениеЗаполнено(ЗначенияРеквизитов.ДокументОснование) Тогда
		Если ТипЗнч(ЗначенияРеквизитов.ДокументОснование) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
			ОснованиеНакладная = ЗначенияРеквизитов.ДокументОснование;
		ИначеЕсли ТипЗнч(ЗначенияРеквизитов.ДокументОснование) = Тип("ДокументСсылка.ЧекККМ") Тогда
			ОснованиеНакладная = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначенияРеквизитов.ДокументОснование, 
				"КассоваяСмена", Истина);
			Если НЕ ЗначениеЗаполнено(ОснованиеНакладная) Тогда
				ОснованиеНакладная = Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	// Установка исключительной блокировки контролируемых остатков запасов.
	Если ЭтоВозвратОтПокупателя Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаЗапасы.Организация КАК Организация,
		|	ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
		|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
		|	ТаблицаЗапасы.Характеристика КАК Характеристика,
		|	ТаблицаЗапасы.Партия КАК Партия,
		|	ВЫБОР
		|		КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
		|			ТОГДА ТаблицаЗапасы.Заказ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|	КОНЕЦ КАК ЗаказПокупателя
		|ИЗ
		|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
		|ГДЕ
		|	(НЕ ТаблицаЗапасы.ПеремещениеВРозницуСуммовойУчет)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаЗапасы.Организация,
		|	ТаблицаЗапасы.СтруктурнаяЕдиница,
		|	ТаблицаЗапасы.СчетУчета,
		|	ТаблицаЗапасы.Номенклатура,
		|	ТаблицаЗапасы.Характеристика,
		|	ТаблицаЗапасы.Партия,
		|	ВЫБОР
		|		КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
		|			ТОГДА ТаблицаЗапасы.Заказ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|	КОНЕЦ";
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаЗапасы.КоррОрганизация КАК Организация,
		|	ТаблицаЗапасы.КоррСтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ТаблицаЗапасы.КоррСчетУчета КАК СчетУчета,
		|	ТаблицаЗапасы.КоррНоменклатура КАК Номенклатура,
		|	ТаблицаЗапасы.КоррХарактеристика КАК Характеристика,
		|	ТаблицаЗапасы.КоррПартия КАК Партия,
		|	ВЫБОР
		|		КОГДА ТаблицаЗапасы.КоррЗаказ ССЫЛКА Документ.ЗаказПокупателя
		|			ТОГДА ТаблицаЗапасы.КоррЗаказ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|	КОНЕЦ КАК ЗаказПокупателя
		|ИЗ
		|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
		|ГДЕ
		|	(НЕ ТаблицаЗапасы.ПеремещениеВРозницуСуммовойУчет)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаЗапасы.КоррОрганизация,
		|	ТаблицаЗапасы.КоррСтруктурнаяЕдиница,
		|	ТаблицаЗапасы.КоррСчетУчета,
		|	ТаблицаЗапасы.КоррНоменклатура,
		|	ТаблицаЗапасы.КоррХарактеристика,
		|	ТаблицаЗапасы.КоррПартия,
		|	ВЫБОР
		|		КОГДА ТаблицаЗапасы.КоррЗаказ ССЫЛКА Документ.ЗаказПокупателя
		|			ТОГДА ТаблицаЗапасы.КоррЗаказ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|	КОНЕЦ";
	КонецЕсли; 
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Запасы");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	СебестоимостьПоОснованию = ЗначениеЗаполнено(ОснованиеНакладная);
	
	// Получение остатков запасов по стоимости.
	Если СебестоимостьПоОснованию Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Запасы.Организация КАК Организация,
		|	Запасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	Запасы.СчетУчета КАК СчетУчета,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Партия КАК Партия,
		|	Запасы.ЗаказПокупателя КАК ЗаказПокупателя,
		|	СУММА(Запасы.Количество) КАК КоличествоОстаток,
		|	СУММА(Запасы.Сумма) КАК СуммаОстаток
		|ИЗ
		|	РегистрНакопления.Запасы КАК Запасы
		|ГДЕ
		|	Запасы.Регистратор = &ОснованиеНакладная
		|
		|СГРУППИРОВАТЬ ПО
		|	Запасы.Организация,
		|	Запасы.СтруктурнаяЕдиница,
		|	Запасы.СчетУчета,
		|	Запасы.Номенклатура,
		|	Запасы.Характеристика,
		|	Запасы.Партия,
		|	Запасы.ЗаказПокупателя";
		
		Запрос.УстановитьПараметр("ОснованиеНакладная", ОснованиеНакладная);
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗапасыОстатки.Организация КАК Организация,
		|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ЗапасыОстатки.СчетУчета КАК СчетУчета,
		|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
		|	ЗапасыОстатки.Характеристика КАК Характеристика,
		|	ЗапасыОстатки.Партия КАК Партия,
		|	ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
		|	СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоОстаток,
		|	СУММА(ЗапасыОстатки.СуммаОстаток) КАК СуммаОстаток
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗапасыОстатки.Организация КАК Организация,
		|		ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|		ЗапасыОстатки.СчетУчета КАК СчетУчета,
		|		ЗапасыОстатки.Номенклатура КАК Номенклатура,
		|		ЗапасыОстатки.Характеристика КАК Характеристика,
		|		ЗапасыОстатки.Партия КАК Партия,
		|		ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
		|		СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоОстаток,
		|		СУММА(ЗапасыОстатки.СуммаОстаток) КАК СуммаОстаток
		|	ИЗ
		|		РегистрНакопления.Запасы.Остатки(
		|				&МоментКонтроля,
		|				(Организация, СтруктурнаяЕдиница, СчетУчета, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
		|					(ВЫБРАТЬ
		|						ТаблицаЗапасы.КоррОрганизация,
		|						ТаблицаЗапасы.КоррСтруктурнаяЕдиница,
		|						ТаблицаЗапасы.КоррСчетУчета,
		|						ТаблицаЗапасы.КоррНоменклатура,
		|						ТаблицаЗапасы.КоррХарактеристика,
		|						ТаблицаЗапасы.КоррПартия,
		|						ВЫБОР
		|							КОГДА ТаблицаЗапасы.КоррЗаказ ССЫЛКА Документ.ЗаказПокупателя
		|								ТОГДА ТаблицаЗапасы.КоррЗаказ
		|							ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|						КОНЕЦ
		|					ИЗ
		|						ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы)) КАК ЗапасыОстатки
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ЗапасыОстатки.Организация,
		|		ЗапасыОстатки.СтруктурнаяЕдиница,
		|		ЗапасыОстатки.СчетУчета,
		|		ЗапасыОстатки.Номенклатура,
		|		ЗапасыОстатки.Характеристика,
		|		ЗапасыОстатки.Партия,
		|		ЗапасыОстатки.ЗаказПокупателя
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДвиженияДокументаЗапасы.Организация,
		|		ДвиженияДокументаЗапасы.СтруктурнаяЕдиница,
		|		ДвиженияДокументаЗапасы.СчетУчета,
		|		ДвиженияДокументаЗапасы.Номенклатура,
		|		ДвиженияДокументаЗапасы.Характеристика,
		|		ДвиженияДокументаЗапасы.Партия,
		|		ДвиженияДокументаЗапасы.ЗаказПокупателя,
		|		ВЫБОР
		|			КОГДА ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
		|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗапасы.Сумма, 0)
		|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗапасы.Сумма, 0)
		|		КОНЕЦ
		|	ИЗ
		|		РегистрНакопления.Запасы КАК ДвиженияДокументаЗапасы
		|	ГДЕ
		|		ДвиженияДокументаЗапасы.Регистратор = &Ссылка
		|		И ДвиженияДокументаЗапасы.Период <= &ПериодКонтроля) КАК ЗапасыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.СчетУчета,
		|	ЗапасыОстатки.Номенклатура,
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Партия,
		|	ЗапасыОстатки.ЗаказПокупателя";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриходнаяНакладная);
	Запрос.УстановитьПараметр("МоментКонтроля",
		Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаЗапасыОстатки = РезультатЗапроса.Выгрузить();
	ТаблицаЗапасыОстатки.Индексы.Добавить(
		"Организация,СтруктурнаяЕдиница,СчетУчета,Номенклатура,Характеристика,Партия,ЗаказПокупателя");
	
	ТаблицаЗапасы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.СкопироватьКолонки();
	Для н = 0 По СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Количество() - 1 Цикл
		
		СтрокаТаблицаЗапасы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы[н];
				
		СтруктураДляПоиска = Новый Структура;
		СтруктураДляПоиска.Вставить("Организация", СтрокаТаблицаЗапасы.Организация);
		Если ЭтоВозвратОтПокупателя Тогда
			СтруктураДляПоиска.Вставить("СтруктурнаяЕдиница", СтрокаТаблицаЗапасы.СтруктурнаяЕдиница);
			СтруктураДляПоиска.Вставить("СчетУчета", СтрокаТаблицаЗапасы.СчетУчета);
			СтруктураДляПоиска.Вставить("Номенклатура", СтрокаТаблицаЗапасы.Номенклатура);
			СтруктураДляПоиска.Вставить("Характеристика", СтрокаТаблицаЗапасы.Характеристика);
			СтруктураДляПоиска.Вставить("Партия", СтрокаТаблицаЗапасы.Партия);
			КоличествоТребуется = -СтрокаТаблицаЗапасы.Количество;
		Иначе
			СтруктураДляПоиска.Вставить("СтруктурнаяЕдиница", СтрокаТаблицаЗапасы.КоррСтруктурнаяЕдиница);
			СтруктураДляПоиска.Вставить("СчетУчета", СтрокаТаблицаЗапасы.КоррСчетУчета);
			СтруктураДляПоиска.Вставить("Номенклатура", СтрокаТаблицаЗапасы.КоррНоменклатура);
			СтруктураДляПоиска.Вставить("Характеристика", СтрокаТаблицаЗапасы.КоррХарактеристика);
			СтруктураДляПоиска.Вставить("Партия", СтрокаТаблицаЗапасы.КоррПартия);
			СтруктураДляПоиска.Вставить("ЗаказПокупателя", СтрокаТаблицаЗапасы.КоррЗаказПокупателя);
			КоличествоТребуется = СтрокаТаблицаЗапасы.Количество;
		КонецЕсли; 
		
		Если КоличествоТребуется > 0 Тогда
			
			Если СтрокаТаблицаЗапасы.Себестоимость > 0 Тогда
				
				// Себестоимость указана явно в документе
				СуммаКСписанию = СтрокаТаблицаЗапасы.Себестоимость;
				
			Иначе
				
				МассивСтрокОстатков = ТаблицаЗапасыОстатки.НайтиСтроки(СтруктураДляПоиска);
				Если МассивСтрокОстатков.Количество() = 0 И СебестоимостьПоОснованию Тогда
					// Возврат на другой склад
					СтруктураДляПоиска.Удалить("СтруктурнаяЕдиница");
					МассивСтрокОстатков = ТаблицаЗапасыОстатки.НайтиСтроки(СтруктураДляПоиска);
				КонецЕсли;
				КоличествоОстаток = 0;
				СуммаОстаток = 0;
				Если МассивСтрокОстатков.Количество() > 0 Тогда
					КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток;
					СуммаОстаток = МассивСтрокОстатков[0].СуммаОстаток;
				КонецЕсли;
				
				Если КоличествоОстаток > 0 И КоличествоОстаток > КоличествоТребуется Тогда
				
					СуммаКСписанию = Окр(СуммаОстаток * КоличествоТребуется / КоличествоОстаток , 2, 1);
					
					МассивСтрокОстатков[0].КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток - КоличествоТребуется;
					МассивСтрокОстатков[0].СуммаОстаток = МассивСтрокОстатков[0].СуммаОстаток - СуммаКСписанию;
					
				ИначеЕсли КоличествоОстаток = КоличествоТребуется Тогда
					
					СуммаКСписанию = СуммаОстаток;
					
					МассивСтрокОстатков[0].КоличествоОстаток = 0;
					МассивСтрокОстатков[0].СуммаОстаток = 0;
					
				Иначе
					
					СуммаКСписанию = 0;
					
				КонецЕсли;
				
			КонецЕсли; 
			
			// Расход.
			СтрокаТаблицыРасход = ТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
			
			СтрокаТаблицыРасход.Сумма = - СуммаКСписанию;
			СтрокаТаблицыРасход.Количество = - КоличествоТребуется;
			
			СтрокаТаблицыРасход.ЗаказПокупателя = Документы.ЗаказПокупателя.ПустаяСсылка();
			
			СтрокаТаблицыРасход.Возврат = Истина;
			
			Если ЭтоВозвратОтПокупателя Тогда

				Если ЗначениеЗаполнено(ОснованиеНакладная) И СтрокаТаблицаЗапасы.Себестоимость = 0 И Окр(
					СуммаКСписанию, 2, 1) <> 0 Тогда
					
					// Сформируем проводки.
					СтрокаТаблицаУправленческий = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицаУправленческий, СтрокаТаблицаЗапасы);
					СтрокаТаблицаУправленческий.СчетДт = СтрокаТаблицаЗапасы.СчетУчетаСебестоимость;
					СтрокаТаблицаУправленческий.ВалютаДт = Неопределено;
					СтрокаТаблицаУправленческий.СуммаВалДт = 0;
					СтрокаТаблицаУправленческий.СчетКт = СтрокаТаблицаЗапасы.СчетУчета;
					СтрокаТаблицаУправленческий.ВалютаКт = Неопределено;
					СтрокаТаблицаУправленческий.СуммаВалКт = 0;
					СтрокаТаблицаУправленческий.Сумма = -СуммаКСписанию;
					СтрокаТаблицаУправленческий.Содержание = НСтр("ru = 'Сторнирование себестоимости'");
					
					// Себестоимость продаж
					СтруктураОтбора = Новый Структура;
					СтруктураОтбора.Вставить("НомерСтроки", СтрокаТаблицаЗапасы.НомерСтроки);
					СтрокиПродажи = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПродажи.НайтиСтроки(
						СтруктураОтбора);
					Если СтрокиПродажи.Количество() = 1 Тогда
						СтрокаПродажи = СтрокиПродажи[0];
						СтрокаПродажи.Себестоимость = СтрокаПродажи.Себестоимость - СуммаКСписанию;
					КонецЕсли;
					
					// Доходы и расходы
					СтрокаДоходыРасходы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаДоходыРасходы, СтрокаТаблицаЗапасы);
					СтрокаДоходыРасходы.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.Подразделение;
					СтрокаДоходыРасходы.СчетУчета = СтрокаТаблицаЗапасы.СчетУчетаСебестоимость;
					СтрокаДоходыРасходы.СодержаниеПроводки = НСтр("ru = 'Отражение расходов'");
					СтрокаДоходыРасходы.СуммаРасходов = -СуммаКСписанию;
					СтрокаДоходыРасходы.ЗаказПокупателя = СтрокаТаблицаЗапасы.ЗаказПродажи;
					СтрокаДоходыРасходы.НаправлениеДеятельности = СтрокаТаблицаЗапасы.НаправлениеДеятельностиПродажи;
					СтрокаДоходыРасходы.Проект = ДокументСсылкаПриходнаяНакладная.Проект;
					
				КонецЕсли;

			Иначе
				
				// Сформируем проводки.
				Если Окр(СуммаКСписанию, 2, 1) <> 0 Тогда
					СтрокаТаблицаУправленческий = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицаУправленческий, СтрокаТаблицаЗапасы);
					СтрокаТаблицаУправленческий.СчетДт = СтрокаТаблицаЗапасы.СчетУчета;
					СтрокаТаблицаУправленческий.ВалютаДт = Неопределено;
					СтрокаТаблицаУправленческий.СуммаВалДт = 0;
					СтрокаТаблицаУправленческий.СчетКт = СтрокаТаблицаЗапасы.КоррСчетУчета;
					СтрокаТаблицаУправленческий.ВалютаКт = Неопределено;
					СтрокаТаблицаУправленческий.СуммаВалКт = 0;
					СтрокаТаблицаУправленческий.Сумма = -СуммаКСписанию;
				КонецЕсли;
				
				// Приход.
				СтрокаТаблицыПриход = ТаблицаЗапасы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаТаблицаЗапасы, ,
					"СтруктурнаяЕдиница, КоррСтруктурнаяЕдиница");

				СтрокаТаблицыПриход.ВидДвижения = ВидДвиженияНакопления.Приход;

				СтрокаТаблицыПриход.Организация = СтрокаТаблицаЗапасы.КоррОрганизация;
				СтрокаТаблицыПриход.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.КоррСтруктурнаяЕдиница;
				СтрокаТаблицыПриход.СчетУчета = СтрокаТаблицаЗапасы.КоррСчетУчета;
				СтрокаТаблицыПриход.Номенклатура = СтрокаТаблицаЗапасы.КоррНоменклатура;
				СтрокаТаблицыПриход.Характеристика = СтрокаТаблицаЗапасы.КоррХарактеристика;
				СтрокаТаблицыПриход.Партия = СтрокаТаблицаЗапасы.КоррПартия;

				СтрокаТаблицыПриход.ЗаказПокупателя = СтрокаТаблицаЗапасы.КоррЗаказПокупателя;

				СтрокаТаблицыПриход.КоррОрганизация = СтрокаТаблицаЗапасы.Организация;
				СтрокаТаблицыПриход.КоррСтруктурнаяЕдиница = СтрокаТаблицаЗапасы.СтруктурнаяЕдиница;
				СтрокаТаблицыПриход.КоррСчетУчета = СтрокаТаблицаЗапасы.СчетУчета;
				СтрокаТаблицыПриход.КоррНоменклатура = СтрокаТаблицаЗапасы.Номенклатура;
				СтрокаТаблицыПриход.КоррХарактеристика = СтрокаТаблицаЗапасы.Характеристика;
				СтрокаТаблицыПриход.КоррПартия = СтрокаТаблицаЗапасы.Партия;

				СтрокаТаблицыПриход.КоррЗаказПокупателя = Документы.ЗаказПокупателя.ПустаяСсылка();

				СтрокаТаблицыПриход.Сумма = -СуммаКСписанию;
				СтрокаТаблицыПриход.Количество = -КоличествоТребуется;

				СтрокаТаблицыПриход.СодержаниеПроводки = НСтр("ru='Передача запасов'");

				СтрокаТаблицыПриход.СчетУчета = СтрокаТаблицаЗапасы.КоррСчетУчета;

				СтрокаТаблицыПриход.Возврат = Истина;

			КонецЕсли;

		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы = ТаблицаЗапасы;
	
КонецПроцедуры // СформироватьТаблицаЗапасыВозврат()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходы(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	МИНИМУМ(ТаблицаДоходыИРасходы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаДоходыИРасходы.Период КАК Период,
	|	ТаблицаДоходыИРасходы.Организация КАК Организация,
	|	ТаблицаДоходыИРасходы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаДоходыИРасходы.Проект КАК Проект,
	|	ВЫБОР
	|		КОГДА ТаблицаДоходыИРасходы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее)
	|		ИНАЧЕ ТаблицаДоходыИРасходы.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ТаблицаДоходыИРасходы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаДоходыИРасходы.Заказ
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ТаблицаДоходыИРасходы.СчетУчета КАК СчетУчета,
	|	ТаблицаДоходыИРасходы.Номенклатура КАК Аналитика,
	|	ВЫРАЗИТЬ(&ПрочиеРасходы КАК СТРОКА(100)) КАК СодержаниеПроводки,
	|	0 КАК СуммаДоходов,
	|	СУММА(ТаблицаДоходыИРасходы.Сумма) КАК СуммаРасходов,
	|	СУММА(ТаблицаДоходыИРасходы.Сумма) КАК Сумма,
	|	ВЫБОР
	|		КОГДА ТаблицаДоходыИРасходы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Распределять,
	|	ТаблицаДоходыИРасходы.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ТаблицаДоходыИРасходы.Номенклатура КАК Номенклатура,
	|	ТаблицаДоходыИРасходы.Характеристика КАК Характеристика,
	|	ТаблицаДоходыИРасходы.Количество КАК Количество
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаДоходыИРасходы
	|ГДЕ
	|	ТаблицаДоходыИРасходы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|	И НЕ ТаблицаДоходыИРасходы.ВключатьРасходыВСебестоимость
	|	И (ТаблицаДоходыИРасходы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
	|			ИЛИ ТаблицаДоходыИРасходы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДоходыИРасходы.Период,
	|	ТаблицаДоходыИРасходы.Организация,
	|	ТаблицаДоходыИРасходы.СтруктурнаяЕдиница,
	|	ТаблицаДоходыИРасходы.Проект,
	|	ВЫБОР
	|		КОГДА ТаблицаДоходыИРасходы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее)
	|		ИНАЧЕ ТаблицаДоходыИРасходы.НаправлениеДеятельности
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДоходыИРасходы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаДоходыИРасходы.Заказ
	|	КОНЕЦ,
	|	ТаблицаДоходыИРасходы.СчетУчета,
	|	ТаблицаДоходыИРасходы.Номенклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаДоходыИРасходы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ТаблицаДоходыИРасходы.ЗаказПоставщику,
	|	ТаблицаДоходыИРасходы.Характеристика,
	|	ТаблицаДоходыИРасходы.Количество,
	|	ТаблицаДоходыИРасходы.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	МИНИМУМ(ТаблицаДоходыИРасходы.НомерСтроки),
	|	ТаблицаДоходыИРасходы.Период,
	|	ТаблицаДоходыИРасходы.Организация,
	|	ТаблицаДоходыИРасходы.ПодразделениеПродажи,
	|	ТаблицаДоходыИРасходы.Проект,
	|	ТаблицаДоходыИРасходы.НаправлениеДеятельностиПродажи,
	|	ТаблицаДоходыИРасходы.Заказ,
	|	ТаблицаДоходыИРасходы.СчетУчетаПродажи,
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫРАЗИТЬ(&ОтражениеДоходов КАК СТРОКА(100)),
	|	-СУММА(ТаблицаДоходыИРасходы.Сумма),
	|	0,
	|	-СУММА(ТаблицаДоходыИРасходы.Сумма),
	|	ЛОЖЬ,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	0
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДоходыИРасходы
	|ГДЕ
	|	ТаблицаДоходыИРасходы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|	И НЕ ТаблицаДоходыИРасходы.ТоварыНаКомиссии
	|	И НЕ ТаблицаДоходыИРасходы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДоходыИРасходы.Период,
	|	ТаблицаДоходыИРасходы.Организация,
	|	ТаблицаДоходыИРасходы.ПодразделениеПродажи,
	|	ТаблицаДоходыИРасходы.Проект,
	|	ТаблицаДоходыИРасходы.НаправлениеДеятельностиПродажи,
	|	ТаблицаДоходыИРасходы.Заказ,
	|	ТаблицаДоходыИРасходы.СчетУчетаПродажи
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	МИНИМУМ(ТаблицаДоходыИРасходы.НомерСтроки),
	|	ТаблицаДоходыИРасходы.Период,
	|	ТаблицаДоходыИРасходы.Организация,
	|	ТаблицаДоходыИРасходы.ПодразделениеПродажи,
	|	ТаблицаДоходыИРасходы.Проект,
	|	ТаблицаДоходыИРасходы.НаправлениеДеятельностиПродажи,
	|	ТаблицаДоходыИРасходы.Заказ,
	|	ТаблицаДоходыИРасходы.СчетУчетаСебестоимость,
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫРАЗИТЬ(&ОтражениеРасходов КАК СТРОКА(100)),
	|	0,
	|	-СУММА(ТаблицаДоходыИРасходы.Себестоимость),
	|	-СУММА(ТаблицаДоходыИРасходы.Себестоимость),
	|	ЛОЖЬ,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	0
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДоходыИРасходы
	|ГДЕ
	|	ТаблицаДоходыИРасходы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|	И ЕСТЬNULL(ТаблицаДоходыИРасходы.ДокументОснование.ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю)
	|	И ЕСТЬNULL(ТаблицаДоходыИРасходы.ДокументОснование.ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд)
	|	И НЕ ТаблицаДоходыИРасходы.ДокументОснование ССЫЛКА Документ.ЧекККМ
	|	И НЕ ТаблицаДоходыИРасходы.ТоварыНаКомиссии
	|	И ТаблицаДоходыИРасходы.Себестоимость > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДоходыИРасходы.Период,
	|	ТаблицаДоходыИРасходы.Организация,
	|	ТаблицаДоходыИРасходы.ПодразделениеПродажи,
	|	ТаблицаДоходыИРасходы.Проект,
	|	ТаблицаДоходыИРасходы.НаправлениеДеятельностиПродажи,
	|	ТаблицаДоходыИРасходы.Заказ,
	|	ТаблицаДоходыИРасходы.СчетУчетаСебестоимость
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА &ОтрицательнаяКурсоваяРазницаСчетУчета
	|		ИНАЧЕ &ПоложительнаяКурсоваяРазницаСчетУчета
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ЛОЖЬ,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	0
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата КАК Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация КАК Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта КАК Валюта,
	|		СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) КАК СуммаКурсовойРазницы
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Дата КАК Дата,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.Валюта КАК Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы КАК СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаКурсовыхРазницРасчетыСПоставщиками
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) >= 0.005
	|			ИЛИ СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) <= -0.005)) КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА &ПоложительнаяКурсоваяРазницаСчетУчета
	|		ИНАЧЕ &ОтрицательнаяКурсоваяРазницаСчетУчета
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ЛОЖЬ,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	0
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ПрочиеРасходы", НСтр("ru = 'Отражение затрат'"));
	Запрос.УстановитьПараметр("ПоложительнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеДоходы);
	Запрос.УстановитьПараметр("ОтрицательнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеРасходы);
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	Запрос.УстановитьПараметр("ОтражениеДоходов", НСтр("ru='Отражение доходов'"));
	Запрос.УстановитьПараметр("ОтражениеРасходов", НСтр("ru='Отражение расходов'"));
	
	ТаблицаРазмещениеЗаказов = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказов.Скопировать();
	ТаблицаДоходыРасходы = Запрос.Выполнить().Выгрузить();
	Результат = ТаблицаДоходыРасходы.СкопироватьКолонки();
	
	Для каждого СтрокаТаблицаДоходыРасходы Из ТаблицаДоходыРасходы Цикл
		
		Если НЕ СтрокаТаблицаДоходыРасходы.Распределять
			ИЛИ ЗначениеЗаполнено(СтрокаТаблицаДоходыРасходы.ЗаказПокупателя)
			ИЛИ СтрокаТаблицаДоходыРасходы.Количество=0 Тогда
			НоваяСтрока = Результат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицаДоходыРасходы);
			Продолжить;
		КонецЕсли; 
		
		СтруктураДляПоиска = Новый Структура;
		СтруктураДляПоиска.Вставить("ИсточникОбеспечения",	СтрокаТаблицаДоходыРасходы.ЗаказПоставщику);
		СтруктураДляПоиска.Вставить("Номенклатура",			СтрокаТаблицаДоходыРасходы.Номенклатура);
		СтруктураДляПоиска.Вставить("Характеристика",		СтрокаТаблицаДоходыРасходы.Характеристика);
		МассивРазмещенныхЗаказов = ТаблицаРазмещениеЗаказов.НайтиСтроки(СтруктураДляПоиска);
		
		РаспределитьКоличество = СтрокаТаблицаДоходыРасходы.Количество;
		РаспределитьСумма = СтрокаТаблицаДоходыРасходы.Сумма;
		
		Для каждого СтрокаМассива Из МассивРазмещенныхЗаказов Цикл
			
			Количество = Мин(РаспределитьКоличество, СтрокаМассива.Количество);
			Если Количество<=0 Тогда
				Продолжить;
			КонецЕсли;
			Сумма = СтрокаТаблицаДоходыРасходы.Сумма * Количество / СтрокаТаблицаДоходыРасходы.Количество;
			РаспределитьКоличество = РаспределитьКоличество - Количество;
			СтрокаМассива.Количество = СтрокаМассива.Количество - Количество;
			РаспределитьСумма = РаспределитьСумма - Сумма;
			
			НоваяСтрока = Результат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицаДоходыРасходы);
			НоваяСтрока.Количество = Количество;
			НоваяСтрока.Сумма = Сумма;
			НоваяСтрока.СуммаРасходов = Сумма;
			НоваяСтрока.ЗаказПокупателя = СтрокаМассива.ЗаказПокупателя;
			
		КонецЦикла;
		
		Если РаспределитьКоличество>0 Тогда
			
			Сумма = СтрокаТаблицаДоходыРасходы.Сумма * РаспределитьКоличество / СтрокаТаблицаДоходыРасходы.Количество;
			РаспределитьСумма = РаспределитьСумма - Сумма;
			
			НоваяСтрока = Результат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицаДоходыРасходы);
			НоваяСтрока.Количество = РаспределитьКоличество;
			НоваяСтрока.Сумма = Сумма;
			НоваяСтрока.СуммаРасходов = Сумма;
			
		КонецЕсли;
		
		Если РаспределитьСумма<>0 Тогда
			
			НоваяСтрока.Сумма = НоваяСтрока.Сумма + РаспределитьСумма;
			НоваяСтрока.СуммаРасходов = НоваяСтрока.СуммаРасходов + РаспределитьСумма;
			
		КонецЕсли; 
			
	КонецЦикла; 
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходы", Результат);
	ТаблицаРазмещениеЗаказов = Неопределено;
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗакупки(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗакупки.Период КАК Период,
	|	ТаблицаЗакупки.Организация КАК Организация,
	|	ТаблицаЗакупки.Номенклатура КАК Номенклатура,
	|	ТаблицаЗакупки.Характеристика КАК Характеристика,
	|	ТаблицаЗакупки.Партия КАК Партия,
	|	ТаблицаЗакупки.Заказ КАК ЗаказПоставщику,
	|	ТаблицаЗакупки.Документ КАК Документ,
	|	ТаблицаЗакупки.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ТаблицаЗакупки.Количество) КАК Количество,
	|	СУММА(ТаблицаЗакупки.СуммаНДСЗакупкиПродажи) КАК СуммаНДС,
	|	СУММА(ТаблицаЗакупки.Сумма) КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗакупки
	|ГДЕ
	|	(ТаблицаЗакупки.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|			ИЛИ ТаблицаЗакупки.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗакупки.Период,
	|	ТаблицаЗакупки.Организация,
	|	ТаблицаЗакупки.Номенклатура,
	|	ТаблицаЗакупки.Характеристика,
	|	ТаблицаЗакупки.Партия,
	|	ТаблицаЗакупки.Заказ,
	|	ТаблицаЗакупки.Документ,
	|	ТаблицаЗакупки.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаЗакупки.Период,
	|	ТаблицаЗакупки.Организация,
	|	ТаблицаЗакупки.Номенклатура,
	|	ТаблицаЗакупки.Характеристика,
	|	ТаблицаЗакупки.Партия,
	|	ТаблицаЗакупки.ЗаказПоставщику,
	|	ТаблицаЗакупки.Документ,
	|	ТаблицаЗакупки.СтавкаНДС,
	|	СУММА(ТаблицаЗакупки.Количество),
	|	СУММА(ТаблицаЗакупки.СуммаНДСЗакупкиПродажи),
	|	СУММА(ТаблицаЗакупки.Сумма)
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаЗакупки
	|ГДЕ
	|	ТаблицаЗакупки.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗакупки.Период,
	|	ТаблицаЗакупки.Организация,
	|	ТаблицаЗакупки.Номенклатура,
	|	ТаблицаЗакупки.Характеристика,
	|	ТаблицаЗакупки.Партия,
	|	ТаблицаЗакупки.ЗаказПоставщику,
	|	ТаблицаЗакупки.Документ,
	|	ТаблицаЗакупки.СтавкаНДС";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗакупки", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗакупки()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПродажи(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПродажи.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПродажи.Период КАК Период,
	|	ТаблицаПродажи.Организация КАК Организация,
	|	ТаблицаПродажи.Номенклатура КАК Номенклатура,
	|	ТаблицаПродажи.Характеристика КАК Характеристика,
	|	ТаблицаПродажи.НоменклатураНабора КАК НоменклатураНабора,
	|	ТаблицаПродажи.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ТаблицаПродажи.Партия КАК Партия,
	|	ТаблицаПродажи.Заказ КАК ЗаказПокупателя,
	|	ТаблицаПродажи.СтруктурнаяЕдиница КАК Склад,
	|	ВЫБОР
	|		КОГДА ТаблицаПродажи.ДокументОснование = ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка)
	|				ИЛИ ТаблицаПродажи.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|				ИЛИ ТаблицаПродажи.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				ИЛИ ТаблицаПродажи.ДокументОснование = ЗНАЧЕНИЕ(Документ.ПриходныйОрдер.ПустаяСсылка)
	|				ИЛИ ТаблицаПродажи.ДокументОснование = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|				ИЛИ ТаблицаПродажи.ДокументОснование = НЕОПРЕДЕЛЕНО
	|				ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаПродажи.ДокументОснование) = ТИП(Документ.ЧекККМ)
	|				ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаПродажи.ДокументОснование) = ТИП(Документ.ТТНВходящаяЕГАИС)
	|				ИЛИ ТаблицаПродажи.ДокументОснование ССЫЛКА Документ.ПриемИПередачаВРемонт
	|			ТОГДА ТаблицаПродажи.Документ
	|		ИНАЧЕ ТаблицаПродажи.ДокументОснование
	|	КОНЕЦ КАК Документ,
	|	ТаблицаПродажи.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаПродажи.ПодразделениеПродажи КАК Подразделение,
	|	ТаблицаПродажи.Проект КАК Проект,
	|	ТаблицаПродажи.ДокументОснование.Ответственный КАК Ответственный,
	|	-СУММА(ТаблицаПродажи.Количество) КАК Количество,
	|	-СУММА(ТаблицаПродажи.СуммаНДСЗакупкиПродажи) КАК СуммаНДС,
	|	-СУММА(ТаблицаПродажи.Сумма) КАК Сумма,
	|	-СУММА(ТаблицаПродажи.Сумма + ТаблицаПродажи.СуммаСкидкиНаценки) КАК СуммаБезСкидки,
	|	-СУММА(ВЫБОР
	|			КОГДА ТаблицаПродажи.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|					И ЕСТЬNULL(ТаблицаПродажи.ДокументОснование.ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю)
	|					И ЕСТЬNULL(ТаблицаПродажи.ДокументОснование.ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд)
	|					И НЕ ТаблицаПродажи.ДокументОснование ССЫЛКА Документ.ЧекККМ
	|				ТОГДА ТаблицаПродажи.Себестоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Себестоимость
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаПродажи
	|ГДЕ
	|	ТаблицаПродажи.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|	И НЕ ТаблицаПродажи.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПродажи.НомерСтроки,
	|	ТаблицаПродажи.Период,
	|	ТаблицаПродажи.Организация,
	|	ТаблицаПродажи.Номенклатура,
	|	ТаблицаПродажи.Характеристика,
	|	ТаблицаПродажи.НоменклатураНабора,
	|	ТаблицаПродажи.ХарактеристикаНабора,
	|	ТаблицаПродажи.Партия,
	|	ТаблицаПродажи.Заказ,
	|	ТаблицаПродажи.СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ТаблицаПродажи.ДокументОснование = ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка)
	|				ИЛИ ТаблицаПродажи.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|				ИЛИ ТаблицаПродажи.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				ИЛИ ТаблицаПродажи.ДокументОснование = ЗНАЧЕНИЕ(Документ.ПриходныйОрдер.ПустаяСсылка)
	|				ИЛИ ТаблицаПродажи.ДокументОснование = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|				ИЛИ ТаблицаПродажи.ДокументОснование = НЕОПРЕДЕЛЕНО
	|				ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаПродажи.ДокументОснование) = ТИП(Документ.ЧекККМ)
	|				ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаПродажи.ДокументОснование) = ТИП(Документ.ТТНВходящаяЕГАИС)
	|				ИЛИ ТаблицаПродажи.ДокументОснование ССЫЛКА Документ.ПриемИПередачаВРемонт
	|			ТОГДА ТаблицаПродажи.Документ
	|		ИНАЧЕ ТаблицаПродажи.ДокументОснование
	|	КОНЕЦ,
	|	ТаблицаПродажи.СтавкаНДС,
	|	ТаблицаПродажи.ПодразделениеПродажи,
	|	ТаблицаПродажи.Проект,
	|	ТаблицаПродажи.ДокументОснование.Ответственный";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПродажи", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаПродажи()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыНаСкладах(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыНаСкладах.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаЗапасыНаСкладах.Период КАК Период,
	|	ТаблицаЗапасыНаСкладах.Организация КАК Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия КАК Партия,
	|	ТаблицаЗапасыНаСкладах.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасыНаСкладах.Ячейка КАК Ячейка,
	|	СУММА(ТаблицаЗапасыНаСкладах.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыНаСкладах
	|ГДЕ
	|	(НЕ ТаблицаЗапасыНаСкладах.ПеремещениеВРозницуСуммовойУчет)
	|	И (НЕ ТаблицаЗапасыНаСкладах.ОрдерныйСклад)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыНаСкладах.Период,
	|	ТаблицаЗапасыНаСкладах.Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия,
	|	ТаблицаЗапасыНаСкладах.СтруктурнаяЕдиница,
	|	ТаблицаЗапасыНаСкладах.Ячейка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыНаСкладах", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗапасыНаСкладах()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыКПоступлениюНаСклады(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыКПоступлениюНаСклады.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Период КАК Период,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Организация КАК Организация,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Партия КАК Партия,
	|	ТаблицаЗапасыКПоступлениюНаСклады.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	СУММА(ТаблицаЗапасыКПоступлениюНаСклады.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыКПоступлениюНаСклады
	|ГДЕ
	|	(НЕ ТаблицаЗапасыКПоступлениюНаСклады.ПеремещениеВРозницуСуммовойУчет)
	|	И ТаблицаЗапасыКПоступлениюНаСклады.ОрдерныйСклад
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыКПоступлениюНаСклады.Период,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Организация,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Номенклатура,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Характеристика,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Партия,
	|	ТаблицаЗапасыКПоступлениюНаСклады.СтруктурнаяЕдиница";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыКПоступлениюНаСклады",
		РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗапасыКПоступлениюНаСклады()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыПринятые(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыПринятые.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаЗапасыПринятые.Период КАК Период,
	|	ТаблицаЗапасыПринятые.Организация КАК Организация,
	|	ТаблицаЗапасыПринятые.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыПринятые.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыПринятые.Партия КАК Партия,
	|	ТаблицаЗапасыПринятые.Контрагент КАК Контрагент,
	|	ТаблицаЗапасыПринятые.Договор КАК Договор,
	|	ТаблицаЗапасыПринятые.Заказ КАК Заказ,
	|	ТаблицаЗапасыПринятые.СчетУчета КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасыПринятые.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ПоступлениеОтКомитента)
	|		КОГДА ТаблицаЗапасыПринятые.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемВПереработку)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ПоступлениеВПереработку)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ОтветственноеХранение)
	|	КОНЕЦ КАК ТипПриемаПередачи,
	|	СУММА(ТаблицаЗапасыПринятые.Количество) КАК Количество,
	|	СУММА(ТаблицаЗапасыПринятые.СуммаРасчетовПринятыеПереданные) КАК СуммаРасчетов,
	|	СУММА(ТаблицаЗапасыПринятые.СуммаРасчетовПринятыеПереданные) КАК Сумма,
	|	0 КАК СуммаПродажи,
	|	&ВалютаУпрУчета КАК Валюта,
	|	СУММА(ТаблицаЗапасыПринятые.СуммаРасчетовПринятыеПереданные) КАК СуммаВал,
	|	ВЫРАЗИТЬ(&ПриемЗапасов КАК СТРОКА(100)) КАК СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыПринятые
	|ГДЕ
	|	(ТаблицаЗапасыПринятые.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию)
	|			ИЛИ ТаблицаЗапасыПринятые.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемВПереработку)
	|			ИЛИ ТаблицаЗапасыПринятые.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаОтветХранение))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыПринятые.Период,
	|	ТаблицаЗапасыПринятые.Организация,
	|	ТаблицаЗапасыПринятые.Номенклатура,
	|	ТаблицаЗапасыПринятые.Характеристика,
	|	ТаблицаЗапасыПринятые.Партия,
	|	ТаблицаЗапасыПринятые.Контрагент,
	|	ТаблицаЗапасыПринятые.Договор,
	|	ТаблицаЗапасыПринятые.Заказ,
	|	ТаблицаЗапасыПринятые.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасыПринятые.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ПоступлениеОтКомитента)
	|		КОГДА ТаблицаЗапасыПринятые.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемВПереработку)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ПоступлениеВПереработку)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ОтветственноеХранение)
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыПринятые.НомерСтроки),
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаЗапасыПринятые.Период,
	|	ТаблицаЗапасыПринятые.Организация,
	|	ТаблицаЗапасыПринятые.Номенклатура,
	|	ТаблицаЗапасыПринятые.Характеристика,
	|	ТаблицаЗапасыПринятые.Партия,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаЗапасыПринятые.Заказ,
	|	ТаблицаЗапасыПринятые.СчетУчетаРасчетовСПоставщиком,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ОтчетКомитенту),
	|	-СУММА(ТаблицаЗапасыПринятые.Количество),
	|	0,
	|	-СУММА(ТаблицаЗапасыПринятые.Сумма),
	|	-СУММА(ТаблицаЗапасыПринятые.Сумма),
	|	&ВалютаУпрУчета,
	|	СУММА(ТаблицаЗапасыПринятые.Сумма),
	|	ВЫРАЗИТЬ(&ПриемЗапасовВозврат КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыПринятые
	|ГДЕ
	|	ТаблицаЗапасыПринятые.ТоварыНаКомиссии
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыПринятые.Период,
	|	ТаблицаЗапасыПринятые.Организация,
	|	ТаблицаЗапасыПринятые.Номенклатура,
	|	ТаблицаЗапасыПринятые.Характеристика,
	|	ТаблицаЗапасыПринятые.Партия,
	|	ТаблицаЗапасыПринятые.Заказ,
	|	ТаблицаЗапасыПринятые.СчетУчетаРасчетовСПоставщиком";
		
	Запрос.УстановитьПараметр("ПриемЗапасов", "");
	Запрос.УстановитьПараметр("ПриемЗапасовВозврат", НСтр("ru = 'Прием запасов'"));
	Запрос.УстановитьПараметр("ВалютаУпрУчета", Константы.ВалютаУчета.Получить());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыИАгентскиеУслугиПринятые", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗапасыПринятые()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыПереданные(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыПереданные.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаЗапасыПереданные.Период КАК Период,
	|	ТаблицаЗапасыПереданные.Организация КАК Организация,
	|	ТаблицаЗапасыПереданные.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыПереданные.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыПереданные.Партия КАК Партия,
	|	ТаблицаЗапасыПереданные.Контрагент КАК Контрагент,
	|	ТаблицаЗапасыПереданные.Договор КАК Договор,
	|	ТаблицаЗапасыПереданные.Заказ КАК Заказ,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасыПереданные.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ПередачаКомиссионеру)
	|		КОГДА ТаблицаЗапасыПереданные.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ПередачаВПереработку)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ОтветственноеХранение)
	|	КОНЕЦ КАК ТипПриемаПередачи,
	|	-СУММА(ТаблицаЗапасыПереданные.Количество) КАК Количество,
	|	-СУММА(ТаблицаЗапасыПереданные.СуммаРасчетовПринятыеПереданные) КАК СуммаРасчетов
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыПереданные
	|ГДЕ
	|	(ТаблицаЗапасыПереданные.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера)
	|			ИЛИ ТаблицаЗапасыПереданные.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика)
	|			ИЛИ ТаблицаЗапасыПереданные.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыПереданные.Период,
	|	ТаблицаЗапасыПереданные.Организация,
	|	ТаблицаЗапасыПереданные.Номенклатура,
	|	ТаблицаЗапасыПереданные.Характеристика,
	|	ТаблицаЗапасыПереданные.Партия,
	|	ТаблицаЗапасыПереданные.Контрагент,
	|	ТаблицаЗапасыПереданные.Договор,
	|	ТаблицаЗапасыПереданные.Заказ,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасыПереданные.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ПередачаКомиссионеру)
	|		КОГДА ТаблицаЗапасыПереданные.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ПередачаВПереработку)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ОтветственноеХранение)
	|	КОНЕЦ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыПереданные", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗапасыПереданные()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗаказыПокупателей(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗаказыПокупателей.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗаказыПокупателей.Период КАК Период,
	|	ТаблицаЗаказыПокупателей.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаЗаказыПокупателей.УчетПотребностиПоСкладам
	|				И ТаблицаЗаказыПокупателей.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ТаблицаЗаказыПокупателей.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК Склад,
	|	ТаблицаЗаказыПокупателей.Номенклатура КАК Номенклатура,
	|	ТаблицаЗаказыПокупателей.Характеристика КАК Характеристика,
	|	ТаблицаЗаказыПокупателей.Заказ КАК ЗаказПокупателя,
	|	-СУММА(ТаблицаЗаказыПокупателей.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗаказыПокупателей
	|ГДЕ
	|	ТаблицаЗаказыПокупателей.Заказ <> НЕОПРЕДЕЛЕНО
	|	И ТаблицаЗаказыПокупателей.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|	И НЕ ТаблицаЗаказыПокупателей.ЗаказНаряд
	|	И (ТаблицаЗаказыПокупателей.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|			ИЛИ ТаблицаЗаказыПокупателей.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера)
	|			ИЛИ ТаблицаЗаказыПокупателей.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗаказыПокупателей.Период,
	|	ТаблицаЗаказыПокупателей.Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаЗаказыПокупателей.УчетПотребностиПоСкладам
	|				И ТаблицаЗаказыПокупателей.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ТаблицаЗаказыПокупателей.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаЗаказыПокупателей.Номенклатура,
	|	ТаблицаЗаказыПокупателей.Характеристика,
	|	ТаблицаЗаказыПокупателей.Заказ";
	
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗаказыПокупателей", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗаказыПокупателей()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗаказыПоставщикам(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗаказыПоставщикам.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗаказыПоставщикам.Период КАК Период,
	|	ТаблицаЗаказыПоставщикам.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаЗаказыПоставщикам.УчетПотребностиПоСкладам
	|				И ТаблицаЗаказыПоставщикам.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ТаблицаЗаказыПоставщикам.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК Склад,
	|	ТаблицаЗаказыПоставщикам.Номенклатура КАК Номенклатура,
	|	ТаблицаЗаказыПоставщикам.Характеристика КАК Характеристика,
	|	ТаблицаЗаказыПоставщикам.Заказ КАК ЗаказПоставщику,
	|	СУММА(ТаблицаЗаказыПоставщикам.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗаказыПоставщикам
	|ГДЕ
	|	ТаблицаЗаказыПоставщикам.Заказ <> НЕОПРЕДЕЛЕНО
	|	И ТаблицаЗаказыПоставщикам.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|	И (ТаблицаЗаказыПоставщикам.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|			ИЛИ ТаблицаЗаказыПоставщикам.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию)
	|			ИЛИ ТаблицаЗаказыПоставщикам.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаОтветХранение))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗаказыПоставщикам.Период,
	|	ТаблицаЗаказыПоставщикам.Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаЗаказыПоставщикам.УчетПотребностиПоСкладам
	|				И ТаблицаЗаказыПоставщикам.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ТаблицаЗаказыПоставщикам.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаЗаказыПоставщикам.Номенклатура,
	|	ТаблицаЗаказыПоставщикам.Характеристика,
	|	ТаблицаЗаказыПоставщикам.Заказ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗаказыПоставщикам.НомерСтроки),
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ТаблицаЗаказыПоставщикам.Период,
	|	ТаблицаЗаказыПоставщикам.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка),
	|	ТаблицаЗаказыПоставщикам.Номенклатура,
	|	ТаблицаЗаказыПоставщикам.Характеристика,
	|	ТаблицаЗаказыПоставщикам.ЗаказПоставщику,
	|	СУММА(ТаблицаЗаказыПоставщикам.Количество)
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаЗаказыПоставщикам
	|ГДЕ
	|	ТаблицаЗаказыПоставщикам.ЗаказПоставщику <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	И ТаблицаЗаказыПоставщикам.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗаказыПоставщикам.Период,
	|	ТаблицаЗаказыПоставщикам.Организация,
	|	ТаблицаЗаказыПоставщикам.Номенклатура,
	|	ТаблицаЗаказыПоставщикам.Характеристика,
	|	ТаблицаЗаказыПоставщикам.ЗаказПоставщику";
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЗаказыПоставщикам = РезультатЗапроса.Выгрузить();
	РегистрыНакопления.ЗаказыПоставщикам.ДобавитьДвиженияСУчетомСкладов(ДокументСсылкаПриходнаяНакладная, ТаблицаЗаказыПоставщикам);
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗаказыПоставщикам", ТаблицаЗаказыПоставщикам);
	
КонецПроцедуры // СформироватьТаблицаЗаказыПоставщикам()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРазмещениеЗаказов(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	// Размещение запасов и расходов.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаРазмещение.Период КАК Период,
	|	ТаблицаРазмещение.Организация КАК Организация,
	|	ТаблицаРазмещение.Номенклатура КАК Номенклатура,
	|	ТаблицаРазмещение.Характеристика КАК Характеристика,
	|	ТаблицаРазмещение.Заказ КАК Заказ,
	|	СУММА(ТаблицаРазмещение.Количество) КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблицаРазмещение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаРазмещениеЗапасы.Период КАК Период,
	|		ТаблицаРазмещениеЗапасы.Организация КАК Организация,
	|		ТаблицаРазмещениеЗапасы.Номенклатура КАК Номенклатура,
	|		ТаблицаРазмещениеЗапасы.Характеристика КАК Характеристика,
	|		ТаблицаРазмещениеЗапасы.Заказ КАК Заказ,
	|		ТаблицаРазмещениеЗапасы.Количество КАК Количество
	|	ИЗ
	|		ВременнаяТаблицаЗапасы КАК ТаблицаРазмещениеЗапасы
	|	ГДЕ
	|		НЕ ТаблицаРазмещениеЗапасы.Заказ В (ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка), НЕОПРЕДЕЛЕНО)
	|		И (ТаблицаРазмещениеЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|				ИЛИ ТаблицаРазмещениеЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию)
	|				ИЛИ ТаблицаРазмещениеЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемВПереработку))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаРазмещениеРасходы.Период,
	|		ТаблицаРазмещениеРасходы.Организация,
	|		ТаблицаРазмещениеРасходы.Номенклатура,
	|		ТаблицаРазмещениеРасходы.Характеристика,
	|		ТаблицаРазмещениеРасходы.ЗаказПоставщику,
	|		ТаблицаРазмещениеРасходы.Количество
	|	ИЗ
	|		ВременнаяТаблицаРасходы КАК ТаблицаРазмещениеРасходы
	|	ГДЕ
	|		НЕ ТаблицаРазмещениеРасходы.ЗаказПоставщику В (ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка), НЕОПРЕДЕЛЕНО)
	|		И ТаблицаРазмещениеРасходы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)) КАК ТаблицаРазмещение
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРазмещение.Период,
	|	ТаблицаРазмещение.Организация,
	|	ТаблицаРазмещение.Номенклатура,
	|	ТаблицаРазмещение.Характеристика,
	|	ТаблицаРазмещение.Заказ";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых размещений заказов.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаРазмещениеЗаказов.Организация КАК Организация,
	|	ТаблицаРазмещениеЗаказов.Номенклатура КАК Номенклатура,
	|	ТаблицаРазмещениеЗаказов.Характеристика КАК Характеристика,
	|	ТаблицаРазмещениеЗаказов.Заказ КАК ИсточникОбеспечения
	|ИЗ
	|	ВременнаяТаблицаРазмещение КАК ТаблицаРазмещениеЗаказов";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РазмещениеЗаказов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	// Получение остатков.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаРазмещениеЗаказов.Период КАК Период,
	|	ТаблицаРазмещениеЗаказов.Организация КАК Организация,
	|	РазмещениеЗаказовОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаРазмещениеЗаказов.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаРазмещениеЗаказов.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоУслуга,
	|	ТаблицаРазмещениеЗаказов.Характеристика КАК Характеристика,
	|	ТаблицаРазмещениеЗаказов.Заказ КАК ИсточникОбеспечения,
	|	ВЫБОР
	|		КОГДА ТаблицаРазмещениеЗаказов.Количество > ЕСТЬNULL(РазмещениеЗаказовОстатки.Количество, 0)
	|			ТОГДА ЕСТЬNULL(РазмещениеЗаказовОстатки.Количество, 0)
	|		КОГДА ТаблицаРазмещениеЗаказов.Количество <= ЕСТЬNULL(РазмещениеЗаказовОстатки.Количество, 0)
	|			ТОГДА ТаблицаРазмещениеЗаказов.Количество
	|	КОНЕЦ КАК Количество
	|ИЗ
	|	ВременнаяТаблицаРазмещение КАК ТаблицаРазмещениеЗаказов
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			РазмещениеЗаказовОстатки.Организация КАК Организация,
	|			РазмещениеЗаказовОстатки.Номенклатура КАК Номенклатура,
	|			РазмещениеЗаказовОстатки.Характеристика КАК Характеристика,
	|			РазмещениеЗаказовОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|			РазмещениеЗаказовОстатки.ИсточникОбеспечения КАК ИсточникОбеспечения,
	|			СУММА(РазмещениеЗаказовОстатки.КоличествоОстаток) КАК Количество
	|		ИЗ
	|			(ВЫБРАТЬ
	|				РазмещениеЗаказовОстатки.Организация КАК Организация,
	|				РазмещениеЗаказовОстатки.Номенклатура КАК Номенклатура,
	|				РазмещениеЗаказовОстатки.Характеристика КАК Характеристика,
	|				РазмещениеЗаказовОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|				РазмещениеЗаказовОстатки.ИсточникОбеспечения КАК ИсточникОбеспечения,
	|				РазмещениеЗаказовОстатки.КоличествоОстаток КАК КоличествоОстаток
	|			ИЗ
	|				РегистрНакопления.РазмещениеЗаказов.Остатки(
	|						&МоментКонтроля,
	|						(Организация, Номенклатура, Характеристика, ИсточникОбеспечения) В
	|							(ВЫБРАТЬ
	|								ТаблицаРазмещениеЗаказов.Организация КАК Организация,
	|								ТаблицаРазмещениеЗаказов.Номенклатура КАК Номенклатура,
	|								ТаблицаРазмещениеЗаказов.Характеристика КАК Характеристика,
	|								ТаблицаРазмещениеЗаказов.Заказ КАК ИсточникОбеспечения
	|							ИЗ
	|								ВременнаяТаблицаРазмещение КАК ТаблицаРазмещениеЗаказов)) КАК РазмещениеЗаказовОстатки
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				ДвиженияДокументаРазмещениеЗаказов.Организация,
	|				ДвиженияДокументаРазмещениеЗаказов.Номенклатура,
	|				ДвиженияДокументаРазмещениеЗаказов.Характеристика,
	|				ДвиженияДокументаРазмещениеЗаказов.ЗаказПокупателя,
	|				ДвиженияДокументаРазмещениеЗаказов.ИсточникОбеспечения,
	|				ВЫБОР
	|					КОГДА ДвиженияДокументаРазмещениеЗаказов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|						ТОГДА ЕСТЬNULL(ДвиженияДокументаРазмещениеЗаказов.Количество, 0)
	|					ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаРазмещениеЗаказов.Количество, 0)
	|				КОНЕЦ
	|			ИЗ
	|				РегистрНакопления.РазмещениеЗаказов КАК ДвиженияДокументаРазмещениеЗаказов
	|			ГДЕ
	|				ДвиженияДокументаРазмещениеЗаказов.Регистратор = &Ссылка
	|				И ДвиженияДокументаРазмещениеЗаказов.Период <= &ПериодКонтроля) КАК РазмещениеЗаказовОстатки
	|		
	|		СГРУППИРОВАТЬ ПО
	|			РазмещениеЗаказовОстатки.Организация,
	|			РазмещениеЗаказовОстатки.Номенклатура,
	|			РазмещениеЗаказовОстатки.Характеристика,
	|			РазмещениеЗаказовОстатки.ЗаказПокупателя,
	|			РазмещениеЗаказовОстатки.ИсточникОбеспечения) КАК РазмещениеЗаказовОстатки
	|		ПО ТаблицаРазмещениеЗаказов.Организация = РазмещениеЗаказовОстатки.Организация
	|			И ТаблицаРазмещениеЗаказов.Номенклатура = РазмещениеЗаказовОстатки.Номенклатура
	|			И ТаблицаРазмещениеЗаказов.Характеристика = РазмещениеЗаказовОстатки.Характеристика
	|			И ТаблицаРазмещениеЗаказов.Заказ = РазмещениеЗаказовОстатки.ИсточникОбеспечения
	|ГДЕ
	|	РазмещениеЗаказовОстатки.ЗаказПокупателя ЕСТЬ НЕ NULL 
	|	И ВЫБОР
	|			КОГДА ТаблицаРазмещениеЗаказов.Количество > ЕСТЬNULL(РазмещениеЗаказовОстатки.Количество, 0)
	|				ТОГДА ЕСТЬNULL(РазмещениеЗаказовОстатки.Количество, 0)
	|			КОГДА ТаблицаРазмещениеЗаказов.Количество <= ЕСТЬNULL(РазмещениеЗаказовОстатки.Количество, 0)
	|				ТОГДА ТаблицаРазмещениеЗаказов.Количество
	|		КОНЕЦ <> 0";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриходнаяНакладная);
	Запрос.УстановитьПараметр("МоментКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментКонтроля);
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.ПериодКонтроля);
	
	ТаблицаРазмещениеЗаказов = Запрос.Выполнить().Выгрузить();
	
	Если ДокументСсылкаПриходнаяНакладная.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПриемВПереработку Тогда
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРазмещениеЗаказов", ТаблицаРазмещениеЗаказов);
		
	Иначе
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРазмещениеЗаказовОстатки", ТаблицаРазмещениеЗаказов);
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРазмещениеЗаказов", ТаблицаРазмещениеЗаказов.СкопироватьКолонки());
		
	КонецЕсли; 
	
КонецПроцедуры // СформироватьТаблицаРазмещениеЗаказов()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПотребностьВЗапасах(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИспользоватьРезервирование", СтруктураДополнительныеСвойства.УчетнаяПолитика.РезервированиеЗапасов);
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаЗапасы.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженийЗапасов.Поступление) КАК ТипДвижения,
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка) КАК Склад,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.УчетПотребностиПоСкладам
	|				И ТаблицаЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ТаблицаЗапасы.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК СкладОтгрузка,
	|	ВЫБОР
	|		КОГДА (&ИспользоватьРезервирование
	|				ИЛИ ТаблицаЗапасы.УчетПотребностиПоЗаказам)
	|				И ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ТаблицаЗапасы.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка) КАК ЗаказНаПроизводство,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	СУММА(ТаблицаЗапасы.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|ГДЕ
	|	ТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемВПереработку)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Период,
	|	ТаблицаЗапасы.Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.УчетПотребностиПоСкладам
	|				И ТаблицаЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ТаблицаЗапасы.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА (&ИспользоватьРезервирование
	|				ИЛИ ТаблицаЗапасы.УчетПотребностиПоЗаказам)
	|				И ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ТаблицаЗапасы.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
		
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаПотребностьВЗапасах = РезультатЗапроса.Выгрузить();
	ТаблицаОбщихДвижений = ТаблицаПотребностьВЗапасах.Скопировать();
	РегистрыНакопления.ПотребностьВЗапасах.ДобавитьДвиженияСУчетомСкладовИОстатков(ДокументСсылкаПриходнаяНакладная, ТаблицаОбщихДвижений);
	
	// Движения в режиме сторно для потребности по поступлению
	Для каждого СтрокаТаблицыРасход Из ТаблицаОбщихДвижений Цикл
		СтрокаТаблицыРасход.Количество = -СтрокаТаблицыРасход.Количество;
		СтрокаТаблицыРасход.ВидДвижения = ВидДвиженияНакопления.Приход;
	КонецЦикла; 
	
	// Потребность по отгрузке
	Для каждого СтрокаПотребность Из ТаблицаПотребностьВЗапасах Цикл
		СтрокаТаблицыПриход = ТаблицаОбщихДвижений.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаПотребность);
		СтрокаТаблицыПриход.ВидДвижения = ВидДвиженияНакопления.Приход; 
		СтрокаТаблицыПриход.ТипДвижения = Перечисления.ТипыДвиженийЗапасов.Отгрузка;
		СтрокаТаблицыПриход.Склад = СтрокаПотребность.СкладОтгрузка;
	КонецЦикла; 
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПотребностьВЗапасах", ТаблицаОбщихДвижений);
			
КонецПроцедуры // СформироватьТаблицаПотребностьВЗапасах()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПоставщиками(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриходнаяНакладная);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ВозникновениеОбязательствПередПоставщиком", НСтр("ru='Возникновение обязательств перед поставщиком'"));
	Запрос.УстановитьПараметр("ЗачетАванса", НСтр("ru='Зачет предоплаты'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Если ДокументСсылкаПриходнаяНакладная.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика Тогда
		
		РасчетыПроведениеДокументов.СформироватьДвиженияПоВзаиморасчетам(СтруктураДополнительныеСвойства, ДокументСсылкаПриходнаяНакладная);
		РасчетыПроведениеДокументов.ПересчитатьТаблицыПоКурсамВалют(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
		РасчетыПроведениеДокументов.ПоместитьПересчитанныеТаблицыВМенеджерВременныхТаблиц(СтруктураДополнительныеСвойства);
		ПолучатьРеквизитыРасчетов = Истина;
		
	Иначе
		
		РасчетыПроведениеДокументов.СформироватьПустуюВременнуюТаблицуРасчетов(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства, Запрос.Параметры);
		ПолучатьРеквизитыРасчетов = Ложь;
		
	КонецЕсли;
	
	// Установка исключительной блокировки контролируемых остатков расчетов с контрагентами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПоставщиками.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Договор КАК Договор,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Документ КАК Документ,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Заказ КАК Заказ,
	|	ВременнаяТаблицаРасчетыСПоставщиками.ТипРасчетов КАК ТипРасчетов
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПоставщиками КАК ВременнаяТаблицаРасчетыСПоставщиками";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПоставщиками");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = КурсовыеРазницыУНФ.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыСПоставщиками(
		Запрос.МенеджерВременныхТаблиц, Истина, НомерЗапроса, ПолучатьРеквизитыРасчетов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПоставщиками",
		МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПокупателями(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриходнаяНакладная);
	Запрос.УстановитьПараметр("МоментВремени",
		Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("СторнированиеДолга", НСтр("ru='Сторнирование долга'"));
	Запрос.УстановитьПараметр("ВосстановлениеПредоплаты", НСтр("ru='Восстановление предоплаты'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	Запрос.УстановитьПараметр("СторнированиеПредоплаты", НСтр("ru='Сторнирование предоплаты'"));
	
	Для Каждого КлючИЗначение Из СтруктураДополнительныеСвойства.ДляСертификатов Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	Если ДокументСсылкаПриходнаяНакладная.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя Тогда
		СформироватьТаблицуВзаиморасчетовПоОперацииВозврата(Запрос);
	Иначе
		СтруктураДополнительныеСвойства.ПараметрыДокумента.ЭтоРасчетыСПоставщиком = Не СтруктураДополнительныеСвойства.ПараметрыДокумента.ЭтоРасчетыСПоставщиком;
		РасчетыПроведениеДокументов.СформироватьПустуюВременнуюТаблицуРасчетов(ДокументСсылкаПриходнаяНакладная,
			СтруктураДополнительныеСвойства, Запрос.Параметры);
		СтруктураДополнительныеСвойства.ПараметрыДокумента.ЭтоРасчетыСПоставщиком = Не СтруктураДополнительныеСвойства.ПараметрыДокумента.ЭтоРасчетыСПоставщиком;
	КонецЕсли;
	
	// Установка исключительной блокировки контролируемых остатков расчетов с контрагентами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПокупателями.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыСПокупателями.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасчетыСПокупателями.Договор КАК Договор,
	|	ВременнаяТаблицаРасчетыСПокупателями.Документ КАК Документ,
	|	ВременнаяТаблицаРасчетыСПокупателями.Заказ КАК Заказ,
	|	ВременнаяТаблицаРасчетыСПокупателями.ТипРасчетов КАК ТипРасчетов
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПокупателями";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПокупателями");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = КурсовыеРазницыУНФ.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыСПокупателями(
		Запрос.МенеджерВременныхТаблиц, Ложь, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПокупателями",
		МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыОтложенные(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриходнаяНакладная);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов",
		СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Период КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.НаправлениеДеятельностиПродажи КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Сумма КАК СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаДокумента.НаправлениеДеятельности,
	|	ТаблицаДокумента.Сумма
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Организация КАК Организация,
	|	СУММА(ТаблицаДокумента.Сумма) КАК СуммаКСписанию
	|ИЗ
	|	ВременнаяТаблицаПредоплатаДляКассовогоМетода КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Статья КАК Статья
	|ИЗ
	|	ВременнаяТаблицаПредоплатаДляКассовогоМетода КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаЗапасыДоходыИРасходыОтложенные = МассивРезультатов[0].Выгрузить();
	ВыборкаРезультатаЗапроса = МассивРезультатов[1].Выбрать();
	
	ТаблицаПредоплатаДоходыИРасходыОтложенные = ТаблицаЗапасыДоходыИРасходыОтложенные.Скопировать();
	ТаблицаПредоплатаДоходыИРасходыОтложенные.Очистить();
	
	Если ВыборкаРезультатаЗапроса.Следующий() Тогда
		СуммаКСписанию = ВыборкаРезультатаЗапроса.СуммаКСписанию;
		Для каждого СтрокаЗапасыДоходыИРасходыОтложенные Из ТаблицаЗапасыДоходыИРасходыОтложенные Цикл
			Если СуммаКСписанию = 0 Тогда
				Продолжить
			ИначеЕсли СтрокаЗапасыДоходыИРасходыОтложенные.СуммаРасходов <= СуммаКСписанию Тогда
				СтрокаПредоплатаДоходыИРасходыОтложенные = ТаблицаПредоплатаДоходыИРасходыОтложенные.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПредоплатаДоходыИРасходыОтложенные, СтрокаЗапасыДоходыИРасходыОтложенные);
				СуммаКСписанию = СуммаКСписанию - СтрокаЗапасыДоходыИРасходыОтложенные.СуммаРасходов;
			ИначеЕсли СтрокаЗапасыДоходыИРасходыОтложенные.СуммаРасходов > СуммаКСписанию Тогда
				СтрокаПредоплатаДоходыИРасходыОтложенные = ТаблицаПредоплатаДоходыИРасходыОтложенные.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПредоплатаДоходыИРасходыОтложенные, СтрокаЗапасыДоходыИРасходыОтложенные);
				СтрокаПредоплатаДоходыИРасходыОтложенные.СуммаРасходов = СуммаКСписанию;
				СуммаКСписанию = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для каждого СтрокаПредоплатаДоходыИРасходыОтложенные Из ТаблицаПредоплатаДоходыИРасходыОтложенные Цикл
		СтрокаЗапасыДоходыИРасходыОтложенные = ТаблицаЗапасыДоходыИРасходыОтложенные.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗапасыДоходыИРасходыОтложенные, СтрокаПредоплатаДоходыИРасходыОтложенные);
		СтрокаЗапасыДоходыИРасходыОтложенные.ВидДвижения = ВидДвиженияНакопления.Расход;
	КонецЦикла;
	
	ВыборкаРезультатаЗапроса = МассивРезультатов[2].Выбрать();
	
	Если ВыборкаРезультатаЗапроса.Следующий() Тогда
		Статья = ВыборкаРезультатаЗапроса.Статья;
	Иначе
		Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам;
	КонецЕсли;

	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	Таблица.Период КАК Период,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Документ КАК Документ,
	|	&Статья КАК Статья,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Таблица.СуммаРасходов КАК СуммаРасходов
	|ПОМЕСТИТЬ ВременнаяТаблицаПредоплатаДоходыИРасходыОтложенные
	|ИЗ
	|	&Таблица КАК Таблица";
	Запрос.УстановитьПараметр("Таблица", ТаблицаПредоплатаДоходыИРасходыОтложенные);
	Запрос.УстановитьПараметр("Статья", Статья);
	
	Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыОтложенные",
		ТаблицаЗапасыДоходыИРасходыОтложенные);
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыОтложенные()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыНераспределенные(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов",
		СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Если ДокументСсылкаПриходнаяНакладная.СпособЗачетаПредоплаты = Перечисления.СпособыЗачетаИРаспределенияПлатежей.Авто Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ТаблицаДокумента.Период КАК Период,
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
		|			ТОГДА ТаблицаДокумента.Документ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Документ,
		|	ТаблицаДокумента.Статья КАК Статья,
		|	ТаблицаДокумента.Сумма КАК СуммаРасходов
		|ИЗ
		|	ВременнаяТаблицаПредоплатаДляКассовогоМетода КАК ТаблицаДокумента,
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|ГДЕ
		|	&КассовыйМетодУчетаДоходовИРасходов
		|	И ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
		|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания";
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ТаблицаДокумента.Период КАК Период,
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
		|			ТОГДА ТаблицаДокумента.Документ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Документ,
		|	ТаблицаДокумента.Статья КАК Статья,
		|	ТаблицаДокумента.Сумма КАК СуммаРасходов
		|ИЗ
		|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|ГДЕ
		|	&КассовыйМетодУчетаДоходовИРасходов
		|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
		|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания";
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыНераспределенные",
		РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыНераспределенные()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыКассовыйМетод(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриходнаяНакладная);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени",
		Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов",
		СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.ДокументДата КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|		ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО 
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	-ТаблицаДокумента.Сумма КАК СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаПредоплатаДляКассовогоМетода КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Период,
	|	Таблица.Организация,
	|	Таблица.НаправлениеДеятельности,
	|	Таблица.Статья,
	|	Таблица.СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаПредоплатаДоходыИРасходыОтложенные КАК Таблица";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыКассовыйМетод", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыКассовыйМетод() 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСуммовойУчетВРознице(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриходнаяНакладная);
	Запрос.УстановитьПараметр("МоментВремени",
		Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ПоступлениеВРозницу", НСтр("ru = 'Поступление в розницу'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Период КАК Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.РозничныйВидЦен КАК РозничныйВидЦен,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаДокумента.ВалютаЦены КАК Валюта,
	|	ТаблицаДокумента.СчетУчетаВРознице КАК СчетУчета,
	|	ТаблицаДокумента.СчетУчетаНаценки КАК СчетУчетаНаценки,
	|	СУММА(ВЫРАЗИТЬ(ЦеныНоменклатурыСрезПоследних.Цена * ТаблицаДокумента.Количество * КурсыЦенВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыЦенВалюты.Кратность) / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) КАК ЧИСЛО(15, 2))) КАК Сумма,
	|	СУММА(ВЫРАЗИТЬ(ЦеныНоменклатурыСрезПоследних.Цена * ТаблицаДокумента.Количество / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) КАК ЧИСЛО(15, 2))) КАК СуммаВал,
	|	СУММА(ВЫРАЗИТЬ(ЦеныНоменклатурыСрезПоследних.Цена * ТаблицаДокумента.Количество * КурсыЦенВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыЦенВалюты.Кратность) / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) КАК ЧИСЛО(15, 2))) КАК СуммаДляОстатка,
	|	СУММА(ВЫРАЗИТЬ(ЦеныНоменклатурыСрезПоследних.Цена * ТаблицаДокумента.Количество / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) КАК ЧИСЛО(15, 2))) КАК СуммаВалДляОстатка,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.ВключатьРасходыВСебестоимость
	|				ТОГДА ТаблицаДокумента.Сумма + ТаблицаДокумента.СуммаРасходов
	|			ИНАЧЕ ТаблицаДокумента.Сумма
	|		КОНЕЦ) КАК Себестоимость,
	|	&ПоступлениеВРозницу КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаСуммовойУчетВРознице
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|				&МоментВремени,
	|				(ВидЦен, Номенклатура, Характеристика) В
	|					(ВЫБРАТЬ
	|						ВременнаяТаблицаЗапасы.РозничныйВидЦен,
	|						ВременнаяТаблицаЗапасы.Номенклатура,
	|						ВременнаяТаблицаЗапасы.Характеристика
	|					ИЗ
	|						ВременнаяТаблицаЗапасы)) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ТаблицаДокумента.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|			И ТаблицаДокумента.РозничныйВидЦен = ЦеныНоменклатурыСрезПоследних.ВидЦен
	|			И ТаблицаДокумента.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаВалютаУчета.Значение
	|					ИЗ
	|						Константа.ВалютаУчета КАК КонстантаВалютаУчета)) КАК КурсыУпрВалюты
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыЦенВалюты
	|		ПО ТаблицаДокумента.ВалютаЦены = КурсыЦенВалюты.Валюта
	|ГДЕ
	|	ТаблицаДокумента.ПеремещениеВРозницуСуммовойУчет
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.РозничныйВидЦен,
	|	ТаблицаДокумента.Номенклатура,
	|	ТаблицаДокумента.Характеристика,
	|	ТаблицаДокумента.СтруктурнаяЕдиница,
	|	ТаблицаДокумента.ВалютаЦены,
	|	ТаблицаДокумента.СчетУчетаВРознице,
	|	ТаблицаДокумента.СчетУчетаНаценки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	СтруктурнаяЕдиница,
	|	Валюта,
	|	СчетУчета";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков денежных средств.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаСуммовойУчетВРознице.Организация КАК Организация,
	|	ВременнаяТаблицаСуммовойУчетВРознице.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВременнаяТаблицаСуммовойУчетВРознице.Валюта КАК Валюта
	|ИЗ
	|	ВременнаяТаблицаСуммовойУчетВРознице КАК ВременнаяТаблицаСуммовойУчетВРознице";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.СуммовойУчетВРознице");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = КурсовыеРазницыУНФ.ПолучитьТекстЗапросаКурсовыеРазницыСуммовойУчетВРознице(
		Запрос.МенеджерВременныхТаблиц, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСуммовойУчетВРознице",
		МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаУправленческий(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ТаблицаУправленческий.НомерСтроки КАК НомерСтроки,
	|	ТаблицаУправленческий.Период КАК Период,
	|	ТаблицаУправленческий.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ТаблицаУправленческий.СчетУчета КАК СчетДт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалДт,
	|	ТаблицаУправленческий.СчетУчетаРасчетовСПоставщиком КАК СчетКт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПоставщикомВалютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаКт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПоставщикомВалютный
	|			ТОГДА ТаблицаУправленческий.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалКт,
	|	ТаблицаУправленческий.Сумма КАК Сумма,
	|	&ОприходованиеЗапасов КАК Содержание
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаУправленческий
	|ГДЕ
	|	ТаблицаУправленческий.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|	И НЕ ТаблицаУправленческий.ВключатьРасходыВСебестоимость
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	ТаблицаУправленческий.НомерСтроки,
	|	ТаблицаУправленческий.Период,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаУправленческий.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.СуммаВал + ТаблицаУправленческий.СуммаРасходовВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаУправленческий.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПоставщикомВалютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПоставщикомВалютный
	|			ТОГДА ТаблицаУправленческий.СуммаВал + ТаблицаУправленческий.СуммаРасходовВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаУправленческий.Сумма + ТаблицаУправленческий.СуммаРасходов,
	|	&ОприходованиеЗапасов
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаУправленческий
	|ГДЕ
	|	ТаблицаУправленческий.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|	И ТаблицаУправленческий.ВключатьРасходыВСебестоимость
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	ТаблицаУправленческий.НомерСтроки,
	|	ТаблицаУправленческий.Период,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаУправленческий.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчета.Валютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчета.Валютный
	|			ТОГДА ТаблицаУправленческий.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаУправленческий.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПоставщиком.Валютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПоставщиком.Валютный
	|			ТОГДА ТаблицаУправленческий.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаУправленческий.Сумма,
	|	&ПрочиеРасходы
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаУправленческий
	|ГДЕ
	|	ТаблицаУправленческий.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|	И НЕ ТаблицаУправленческий.ВключатьРасходыВСебестоимость
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	ТаблицаУправленческий.НомерСтроки,
	|	ТаблицаУправленческий.Период,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаУправленческий.СчетУчетаРасчетовСПокупателем,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПокупателем.Валютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПокупателем.Валютный
	|			ТОГДА -ТаблицаУправленческий.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаУправленческий.СчетУчетаПродажи,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	-ТаблицаУправленческий.Сумма,
	|	&СторнированиеВыручки
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаУправленческий
	|ГДЕ
	|	ТаблицаУправленческий.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|	И НЕ ТаблицаУправленческий.ТоварыНаКомиссии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	6,
	|	ТаблицаУправленческий.НомерСтроки,
	|	ТаблицаУправленческий.Период,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|			ТОГДА ТаблицаУправленческий.СчетУчетаЗатрат
	|		ИНАЧЕ ТаблицаУправленческий.СчетУчетаСебестоимость
	|	КОНЕЦ,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	ТаблицаУправленческий.СчетУчета,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	-ТаблицаУправленческий.Себестоимость,
	|	&СторнированиеЗапасов
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаУправленческий
	|ГДЕ
	|	ТаблицаУправленческий.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|	И ЕСТЬNULL(ТаблицаУправленческий.ДокументОснование.ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю)
	|	И ЕСТЬNULL(ТаблицаУправленческий.ДокументОснование.ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд)
	|	И НЕ ТаблицаУправленческий.ДокументОснование ССЫЛКА Документ.ЧекККМ
	|	И НЕ ТаблицаУправленческий.ТоварыНаКомиссии
	|	И ТаблицаУправленческий.Себестоимость > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	7,
	|	1,
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаРасчетовСПоставщикомВалютный
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаРасчетовСПоставщикомВалютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетУчетаАвансовПоставщику,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаАвансовПоставщикуВалютный
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаАвансовПоставщикуВалютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	&ЗачетПредоплаты
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаДокумента.Период КАК Период,
	|		ТаблицаДокумента.Организация КАК Организация,
	|		ТаблицаДокумента.СчетУчетаАвансовПоставщику КАК СчетУчетаАвансовПоставщику,
	|		ТаблицаДокумента.СчетУчетаАвансовПоставщикуВалютный КАК СчетУчетаАвансовПоставщикуВалютный,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПоставщикомВалютный КАК СчетУчетаРасчетовСПоставщикомВалютный,
	|		ТаблицаДокумента.ВалютаРасчетов КАК ВалютаРасчетов,
	|		СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВал,
	|		СУММА(ТаблицаДокумента.Сумма) КАК Сумма
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Период КАК Период,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.Контрагент.СчетУчетаАвансовПоставщику КАК СчетУчетаАвансовПоставщику,
	|			ТаблицаДокумента.Контрагент.СчетУчетаАвансовПоставщику.Валютный КАК СчетУчетаАвансовПоставщикуВалютный,
	|			ТаблицаДокумента.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|			ТаблицаДокумента.Контрагент.СчетУчетаРасчетовСПоставщиком.Валютный КАК СчетУчетаРасчетовСПоставщикомВалютный,
	|			ТаблицаДокумента.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|			ТаблицаДокумента.СуммаВал КАК СуммаВал,
	|			ТаблицаДокумента.Сумма КАК Сумма
	|		ИЗ
	|			ВременнаяТаблицаРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
	|			И ТаблицаДокумента.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			И (ТаблицаДокумента.СуммаВал <> 0 ИЛИ ТаблицаДокумента.Сумма <> 0)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.Контрагент.СчетУчетаАвансовПоставщику,
	|			ТаблицаДокумента.Контрагент.СчетУчетаАвансовПоставщику.Валютный,
	|			ТаблицаДокумента.Контрагент.СчетУчетаРасчетовСПоставщиком,
	|			ТаблицаДокумента.Контрагент.СчетУчетаРасчетовСПоставщиком.Валютный,
	|			ТаблицаДокумента.Валюта,
	|			0,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаДокумента
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаДокумента.Период,
	|		ТаблицаДокумента.Организация,
	|		ТаблицаДокумента.СчетУчетаАвансовПоставщику,
	|		ТаблицаДокумента.СчетУчетаАвансовПоставщикуВалютный,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПоставщикомВалютный,
	|		ТаблицаДокумента.ВалютаРасчетов
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаДокумента.Сумма) >= 0.005
	|			ИЛИ СУММА(ТаблицаДокумента.Сумма) <= -0.005
	|			ИЛИ СУММА(ТаблицаДокумента.СуммаВал) >= 0.005
	|			ИЛИ СУММА(ТаблицаДокумента.СуммаВал) <= -0.005)) КАК ТаблицаДокумента
	|ГДЕ
	|	&ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	8,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.СчетУчетаАвансовПокупателя,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаАвансовПокупателя.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаАвансовПокупателя.Валютный
	|			ТОГДА -ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаРасчетовСПокупателем.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаРасчетовСПокупателем.Валютный
	|			ТОГДА -ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	-ТаблицаДокумента.Сумма,
	|	&СторнированиеПредоплаты
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	9,
	|	1,
	|	ТаблицаУправленческий.Дата,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА &ОтрицательнаяКурсоваяРазницаСчетУчета
	|		ИНАЧЕ ТаблицаУправленческий.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы < 0
	|				И ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаУправленческий.СчетУчета
	|		ИНАЧЕ &ПоложительнаяКурсоваяРазницаСчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|				И ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаУправленческий.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаУправленческий.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата КАК Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация КАК Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчета КАК СчетУчета,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчетаВалютный КАК СчетУчетаВалютный,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта КАК Валюта,
	|		СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) КАК СуммаКурсовойРазницы
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Дата КАК Дата,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.СчетУчета КАК СчетУчета,
	|			ТаблицаДокумента.СчетУчета.Валютный КАК СчетУчетаВалютный,
	|			ТаблицаДокумента.Валюта КАК Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы КАК СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.СчетУчета,
	|			ТаблицаДокумента.СчетУчета.Валютный,
	|			ТаблицаДокумента.Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаКурсовыхРазницРасчетыСПоставщиками
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчета,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчетаВалютный,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) >= 0.005
	|			ИЛИ СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) <= -0.005)) КАК ТаблицаУправленческий
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	10,
	|	ТаблицаУправленческий.НомерСтроки,
	|	ТаблицаУправленческий.Дата,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаУправленческий.СчетУчета
	|		ИНАЧЕ &ОтрицательнаяКурсоваяРазницаСчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|				И ТаблицаУправленческий.СчетУчета.Валютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА &ПоложительнаяКурсоваяРазницаСчетУчета
	|		ИНАЧЕ ТаблицаУправленческий.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы < 0
	|				И ТаблицаУправленческий.СчетУчета.Валютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаУправленческий.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаУправленческий.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаУправленческий
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	11,
	|	ТаблицаУправленческий.НомерСтроки,
	|	ТаблицаУправленческий.Дата,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаУправленческий.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчета.Валютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ТаблицаУправленческий.СчетУчетаНаценки,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаНаценки.Валютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ТаблицаУправленческий.Сумма - ТаблицаУправленческий.Себестоимость,
	|	&Наценка
	|ИЗ
	|	ВременнаяТаблицаСуммовойУчетВРознице КАК ТаблицаУправленческий
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	12,
	|	ТаблицаУправленческий.НомерСтроки,
	|	ТаблицаУправленческий.Период,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.ТоварыНаКомиссии
	|			ТОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПоставщиком
	|		ИНАЧЕ ТаблицаУправленческий.СчетУчетаПродажи
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.ТоварыНаКомиссии
	|			ТОГДА &ВалютаУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.ТоварыНаКомиссии
	|			ТОГДА -ТаблицаУправленческий.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	&СчетУчетаАвансовПокупателя,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	-ТаблицаУправленческий.Сумма,
	|	&СторнированиеАвансаПокупателя
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаУправленческий
	|ГДЕ
	|	ТаблицаУправленческий.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|	И ТаблицаУправленческий.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
		
	Запрос.УстановитьПараметр("ОприходованиеЗапасов", НСтр("ru = 'Оприходование запасов'"));
	Запрос.УстановитьПараметр("ПрочиеРасходы", НСтр("ru = 'Отражение затрат'"));
	Запрос.УстановитьПараметр("ЗачетПредоплаты", НСтр("ru = 'Зачет предоплаты'"));
	Запрос.УстановитьПараметр("СторнированиеПредоплаты", НСтр("ru = 'Сторнирование предоплаты'"));
	Запрос.УстановитьПараметр("СторнированиеЗапасов", НСтр("ru = 'Сторнирование себестоимости'"));
	Запрос.УстановитьПараметр("СторнированиеВыручки", НСтр("ru = 'Сторнирование выручки'"));
	Запрос.УстановитьПараметр("СторнированиеАвансаПокупателя", НСтр("ru = 'Сторнирование аванса покупателя'"));
	Запрос.УстановитьПараметр("Наценка", НСтр("ru = 'Наценка'"));
	Запрос.УстановитьПараметр("ПоложительнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеДоходы);
	Запрос.УстановитьПараметр("ОтрицательнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеРасходы);
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	Запрос.УстановитьПараметр("ВидОперации", ДокументСсылкаПриходнаяНакладная.ВидОперации);
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	
	Для Каждого КлючИЗначение Из СтруктураДополнительныеСвойства.ДляСертификатов Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл  
		НоваяПроводка = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПроводка, Выборка);
	КонецЦикла;
	
КонецПроцедуры // СформироватьТаблицаУправленческий()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗакупкиПоставщиковДляКУДиР(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриходнаяНакладная);
	Запрос.УстановитьПараметр("ДатаДокумента", СтруктураДополнительныеСвойства.ДляПроведения.Дата);
	Запрос.УстановитьПараметр("ДатаПереходаНаНДС20", Дата('20190101'));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПоставщиками.Дата КАК Период,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Дата КАК ДатаДокумента,
	|	ДопРеквизитыДокумента.Организация КАК Организация,
	|	&Ссылка КАК ТоварныйДокумент,
	|	ВременнаяТаблицаРасчетыСПоставщиками.ДокументОплаты КАК ДенежныйДокумент,
	|	ВременнаяТаблицаРасчетыСПоставщиками.ДокументОплаты.УчитыватьВНУ КАК УчитыватьВНУ,
	|	ВЫБОР
	|		КОГДА ДопРеквизитыДокумента.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДС)
	|			ТОГДА ВЫБОР
	|					КОГДА ВременнаяТаблицаРасчетыСПоставщиками.Дата < &ДатаПереходаНаНДС20
	|						ТОГДА 18
	|					ИНАЧЕ 20
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтавкаНДС,
	|	ЛОЖЬ КАК ЭтоТоварыКРеализации,
	|	СУММА(ВременнаяТаблицаРасчетыСПоставщиками.СуммаВал) КАК СуммаВал,
	|	ВременнаяТаблицаРасчетыСПоставщиками.ВалютаПлатежа КАК Валюта
	|ПОМЕСТИТЬ ВременнаяТаблицаЗакупкиПоставщиковДляКУДиР
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПоставщиками КАК ВременнаяТаблицаРасчетыСПоставщиками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриходнаяНакладная КАК ДопРеквизитыДокумента
	|		ПО (ДопРеквизитыДокумента.Ссылка = &Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПримененияЕНВД.СрезПоследних(&ДатаДокумента, ) КАК ПримененияЕНВД
	|		ПО (ДопРеквизитыДокумента.Организация = ПримененияЕНВД.Организация)
	|			И (ПримененияЕНВД.СтруктурнаяЕдиница = ДопРеквизитыДокумента.СтруктурнаяЕдиница)
	|ГДЕ
	|	ДопРеквизитыДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|	И ВременнаяТаблицаРасчетыСПоставщиками.ТипРасчетов = Значение(Перечисление.ТипыРасчетов.Аванс)
	|	И ВременнаяТаблицаРасчетыСПоставщиками.ДокументОплаты <> Неопределено
	|	И ДопРеквизитыДокумента.Контрагент.ВестиРасчетыПоДокументам
	|	И ДопРеквизитыДокумента.Организация.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|	И НЕ ЕСТЬNULL(ПримененияЕНВД.РозничнаяТорговляОблагаетсяЕНВД, ЛОЖЬ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасчетыСПоставщиками.Дата,
	|	ДопРеквизитыДокумента.Организация,
	|	ВременнаяТаблицаРасчетыСПоставщиками.ДокументОплаты.УчитыватьВНУ,
	|	ДопРеквизитыДокумента.НалогообложениеНДС,
	|	ВременнаяТаблицаРасчетыСПоставщиками.ВалютаПлатежа,
	|	ВременнаяТаблицаРасчетыСПоставщиками.ДокументОплаты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ТоварныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТЗакупкиПоставщиковДляКУДиР.Период КАК Период,
	|	ВТЗакупкиПоставщиковДляКУДиР.ДатаДокумента КАК ДатаДокумента,
	|	ВТЗакупкиПоставщиковДляКУДиР.Организация КАК Организация,
	|	ВТЗакупкиПоставщиковДляКУДиР.ДенежныйДокумент КАК ДенежныйДокумент,
	|	ВТЗакупкиПоставщиковДляКУДиР.ТоварныйДокумент КАК ТоварныйДокумент,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ВТЗакупкиПоставщиковДляКУДиР.УчитыватьВНУ КАК УчитыватьВНУ,
	|	ВТЗакупкиПоставщиковДляКУДиР.ЭтоТоварыКРеализации КАК ЭтоТоварыКРеализации,
	|	ВТЗакупкиПоставщиковДляКУДиР.СуммаВал КАК СуммаВал,
	|	ВТЗакупкиПоставщиковДляКУДиР.СуммаВал КАК Сумма,
	|	ВТЗакупкиПоставщиковДляКУДиР.СтавкаНДС КАК СтавкаНДС,
	|	0 КАК Количество,
	|	0 КАК СуммаНДС
	|ИЗ
	|	ВременнаяТаблицаЗакупкиПоставщиковДляКУДиР КАК ВТЗакупкиПоставщиковДляКУДиР
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Ссылка КАК ТоварныйДокумент,
	|	ПриходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
	|	ПриходнаяНакладнаяЗапасы.Всего КАК Сумма,
	|	ЕСТЬNULL(ПриходнаяНакладнаяЗапасы.СтавкаНДС.Ставка, 0) КАК СтавкаНДС,
	|	НЕ ПриходнаяНакладнаяЗапасы.ТоварыДляПроизводства КАК ЭтоТоварыКРеализации,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА ПриходнаяНакладнаяЗапасы.Количество
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.Количество * ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Количество
	|ИЗ
	|	Документ.ПриходнаяНакладная.Запасы КАК ПриходнаяНакладнаяЗапасы
	|ГДЕ
	|	ПриходнаяНакладнаяЗапасы.Ссылка = &Ссылка
	|ИТОГИ ПО
	|	ТоварныйДокумент";
	
	Если ДокументСсылкаПриходнаяНакладная.ВидОперации <> Перечисления.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДокументОплаты", "Документ");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ТаблицаОплаты = РезультатЗапроса[1].Выгрузить();
	ТаблицаРезультат = ТаблицаОплаты.Скопировать(Новый Массив());
	ВыборкаДокументовСТоварами = РезультатЗапроса[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	УчетВалюты = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
	НацВалюта = Константы.НациональнаяВалюта.Получить();
	ВалютаДокумента = ДокументСсылкаПриходнаяНакладная.ВалютаДокумента;
	
	Если УчетВалюты И ВалютаДокумента <> НацВалюта Тогда
		ТекущийКурс = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ДокументСсылкаПриходнаяНакладная.Дата);
	КонецЕсли;
	
	Пока ВыборкаДокументовСТоварами.Следующий() Цикл
		СвободныеОплаты = ТаблицаОплаты.НайтиСтроки(Новый Структура("ТоварныйДокумент", ВыборкаДокументовСТоварами.ТоварныйДокумент));
		ВыборкаЗаписей = ВыборкаДокументовСТоварами.Выбрать();
		СуммаЗаписи = 0;
		Для Каждого ДокументОплаты Из СвободныеОплаты Цикл
			Если СуммаЗаписи > 0 Тогда
				НовСтр = ТаблицаРезультат.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаЗаписей);
				ЗаполнитьЗначенияСвойств(НовСтр, ДокументОплаты,,"ЭтоТоварыКРеализации");
				Если ДокументОплаты.Сумма >= СуммаЗаписи Тогда
					НовСтр.Сумма = СуммаЗаписи;
				Иначе
					НовСтр.Сумма = ДокументОплаты.Сумма;
				КонецЕсли;
				НовСтр.Количество = ?(ВыборкаЗаписей.Сумма = 0, 0, ВыборкаЗаписей.Количество
																   * НовСтр.Сумма
																   / ВыборкаЗаписей.Сумма);
				ДокументОплаты.Сумма = ДокументОплаты.Сумма - НовСтр.Сумма;
				НовСтр.СуммаВал = НовСтр.Сумма;
				
				Если УчетВалюты И ВалютаДокумента <> НацВалюта Тогда
					НовСтр.Сумма = НовСтр.Сумма * ТекущийКурс.Курс / ТекущийКурс.Кратность;
				КонецЕсли;
				НовСтр.СуммаНДС = НовСтр.Сумма - НовСтр.Сумма /((100+ ВыборкаЗаписей.СтавкаНДС)/100);
				НовСтр.Номенклатура = ВыборкаЗаписей.Номенклатура;
				НовСтр.Сумма = НовСтр.Сумма - НовСтр.СуммаНДС;
				СуммаЗаписи = СуммаЗаписи - НовСтр.Сумма - НовСтр.СуммаНДС;
				Если СуммаЗаписи > 0 Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Пока ДокументОплаты.Сумма <> 0 И ВыборкаЗаписей.Следующий() Цикл
				СуммаЗаписи = ВыборкаЗаписей.Сумма;
				НовСтр = ТаблицаРезультат.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаЗаписей);
				ЗаполнитьЗначенияСвойств(НовСтр, ДокументОплаты,,"ЭтоТоварыКРеализации");
				Если ДокументОплаты.Сумма >= ВыборкаЗаписей.Сумма Тогда
					НовСтр.Сумма = ВыборкаЗаписей.Сумма;
					НовСтр.Количество =  ВыборкаЗаписей.Количество;
				Иначе
					НовСтр.Сумма = ДокументОплаты.Сумма;
					НовСтр.Количество = ?(ВыборкаЗаписей.Сумма = 0, ВыборкаЗаписей.Количество, ВыборкаЗаписей.Количество
																							   * ДокументОплаты.Сумма
																							   / ВыборкаЗаписей.Сумма);
				КонецЕсли;
				ДокументОплаты.Сумма = ДокументОплаты.Сумма - НовСтр.Сумма;
				НовСтр.СуммаВал = НовСтр.Сумма;
				
				Если УчетВалюты И ВалютаДокумента <> НацВалюта Тогда
					НовСтр.Сумма = НовСтр.Сумма * ТекущийКурс.Курс / ТекущийКурс.Кратность;
				КонецЕсли;
				НовСтр.СуммаНДС = НовСтр.Сумма - НовСтр.Сумма /((100+ ВыборкаЗаписей.СтавкаНДС)/100);
				НовСтр.Номенклатура = ВыборкаЗаписей.Номенклатура;
				НовСтр.Сумма = НовСтр.Сумма - НовСтр.СуммаНДС;
				СуммаЗаписи = СуммаЗаписи - НовСтр.Сумма - НовСтр.СуммаНДС;
			КонецЦикла;
			Если ДокументОплаты.Сумма <> 0 Тогда
				НовСтр = ТаблицаРезультат.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, ДокументОплаты);
				ДокументОплаты.Сумма = 0;
				НовСтр.СуммаВал = НовСтр.Сумма;
				Если УчетВалюты И ВалютаДокумента <> НацВалюта Тогда
					НовСтр.Сумма = НовСтр.Сумма * ТекущийКурс.Курс / ТекущийКурс.Кратность;
				КонецЕсли;
				НовСтр.СуммаНДС = НовСтр.Сумма - НовСтр.Сумма /((100+ ДокументОплаты.СтавкаНДС)/100);
				НовСтр.Сумма = НовСтр.Сумма - НовСтр.СуммаНДС;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ДокументОплаты Из ТаблицаОплаты Цикл
		Если ДокументОплаты.Сумма <> 0 Тогда
			НовСтр = ТаблицаРезультат.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, ДокументОплаты);
			НовСтр.СуммаВал = НовСтр.Сумма;
			Если УчетВалюты И ВалютаДокумента <> НацВалюта Тогда
				НовСтр.Сумма = НовСтр.Сумма * ТекущийКурс.Курс / ТекущийКурс.Кратность;
			КонецЕсли;
			НовСтр.СуммаНДС = НовСтр.Сумма - НовСтр.Сумма /((100+ ДокументОплаты.СтавкаНДС)/100);
			НовСтр.Сумма = НовСтр.Сумма - НовСтр.СуммаНДС;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗакупкиДляКУДиР", ТаблицаРезультат);
	
КонецПроцедуры // СформироватьТаблицаЗакупкиПоставщиковДляКУДиР()

// Помещает в менеджер временных таблиц запроса данные для формирования взаиморасчетов по операции возврата
//
Процедура СформироватьТаблицуВзаиморасчетовПоОперацииВозврата(Запрос)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК НомерЗапроса,
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&СторнированиеДолга КАК СодержаниеПроводки,
	|	ТаблицаДокумента.Период КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Контрагент КАК Контрагент,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем КАК СчетУчета,
	|	ТаблицаДокумента.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.ПриходныйОрдер.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = НЕОПРЕДЕЛЕНО
	|							ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументОснование) = ТИП(Документ.ЧекККМ)
	|							ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументОснование) = ТИП(Документ.ТТНВходящаяЕГАИС)
	|							ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументОснование.ВидЗаказа) = ТИП(Справочник.ВидыЗаказовПокупателей)
	|						ТОГДА ТаблицаДокумента.Документ
	|					ИНАЧЕ ТаблицаДокумента.ДокументОснование
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Заказ = НЕОПРЕДЕЛЕНО
	|				ИЛИ НЕ ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаДокумента.Заказ
	|	КОНЕЦ КАК Заказ,
	|	ТаблицаДокумента.ВалютаРасчетов КАК Валюта,
	|	ТаблицаДокумента.Курс КАК Курс,
	|	ТаблицаДокумента.Кратность КАК Кратность,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг) КАК ТипРасчетов,
	|	-СУММА(ТаблицаДокумента.Сумма) КАК Сумма,
	|	-СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВал,
	|	СУММА(0) КАК СуммаРег,
	|	-СУММА(ТаблицаДокумента.Сумма) КАК СуммаДляОстатка,
	|	-СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВалДляОстатка
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПокупателями
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.ПриходныйОрдер.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = НЕОПРЕДЕЛЕНО
	|							ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументОснование) = ТИП(Документ.ЧекККМ)
	|							ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументОснование) = ТИП(Документ.ТТНВходящаяЕГАИС)
	|							ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументОснование.ВидЗаказа) = ТИП(Справочник.ВидыЗаказовПокупателей)
	|						ТОГДА ТаблицаДокумента.Документ
	|					ИНАЧЕ ТаблицаДокумента.ДокументОснование
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Заказ = НЕОПРЕДЕЛЕНО
	|				ИЛИ НЕ ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаДокумента.Заказ
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.Курс,
	|	ТаблицаДокумента.Кратность,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки),
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&ВосстановлениеПредоплаты,
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.ПриходныйОрдер.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = НЕОПРЕДЕЛЕНО
	|							ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументОснование) = ТИП(Документ.ЧекККМ)
	|							ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументОснование) = ТИП(Документ.ТТНВходящаяЕГАИС)
	|						ТОГДА ВЫБОР
	|								КОГДА ТаблицаДокумента.ОплатаСертификатом
	|									ТОГДА НЕОПРЕДЕЛЕНО
	|								ИНАЧЕ ТаблицаДокумента.ДокументКуда
	|							КОНЕЦ
	|					ИНАЧЕ ТаблицаДокумента.ДокументОснование
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Заказ = НЕОПРЕДЕЛЕНО
	|				ИЛИ НЕ ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаДокумента.Заказ
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.Курс,
	|	ТаблицаДокумента.Кратность,
	|	ТаблицаДокумента.ТипРасчетовКуда,
	|	-СУММА(ТаблицаДокумента.Сумма),
	|	-СУММА(ТаблицаДокумента.СуммаВал),
	|	СУММА(0),
	|	-СУММА(ТаблицаДокумента.Сумма),
	|	-СУММА(ТаблицаДокумента.СуммаВал)
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.Договор,
	|	ТаблицаДокумента.ТипРасчетовКуда,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.Курс,
	|	ТаблицаДокумента.Кратность,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.ПриходныйОрдер.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|							ИЛИ ТаблицаДокумента.ДокументОснование = НЕОПРЕДЕЛЕНО
	|							ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументОснование) = ТИП(Документ.ЧекККМ)
	|							ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументОснование) = ТИП(Документ.ТТНВходящаяЕГАИС)
	|						ТОГДА ВЫБОР
	|								КОГДА ТаблицаДокумента.ОплатаСертификатом
	|									ТОГДА НЕОПРЕДЕЛЕНО
	|								ИНАЧЕ ТаблицаДокумента.ДокументКуда
	|							КОНЕЦ
	|					ИНАЧЕ ТаблицаДокумента.ДокументОснование
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Заказ = НЕОПРЕДЕЛЕНО
	|				ИЛИ НЕ ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаДокумента.Заказ
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки),
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&ВосстановлениеПредоплаты,
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ОплатаСертификатом
	|			ТОГДА &КонтрагентДляПредоплаты
	|		ИНАЧЕ ТаблицаДокумента.Контрагент
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ОплатаСертификатом
	|			ТОГДА &СчетУчетаАвансовПокупателя
	|		ИНАЧЕ ТаблицаДокумента.СчетУчетаАвансовПоставщику
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ОплатаСертификатом
	|			ТОГДА &ДоговорПоУмолчанию
	|		ИНАЧЕ ТаблицаДокумента.Договор
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ОплатаСертификатом
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ОплатаСертификатом
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		КОГДА ТаблицаДокумента.Заказ = НЕОПРЕДЕЛЕНО
	|				ИЛИ НЕ ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаДокумента.Заказ
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.Курс,
	|	ТаблицаДокумента.Кратность,
	|	ТаблицаДокумента.ТипРасчетов,
	|	-СУММА(ТаблицаДокумента.Сумма),
	|	-СУММА(ТаблицаДокумента.СуммаВал),
	|	СУММА(0),
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал)
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.Курс,
	|	ТаблицаДокумента.Кратность,
	|	ТаблицаДокумента.ТипРасчетов,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ОплатаСертификатом
	|			ТОГДА &КонтрагентДляПредоплаты
	|		ИНАЧЕ ТаблицаДокумента.Контрагент
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ОплатаСертификатом
	|			ТОГДА &СчетУчетаАвансовПокупателя
	|		ИНАЧЕ ТаблицаДокумента.СчетУчетаАвансовПоставщику
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ОплатаСертификатом
	|			ТОГДА &ДоговорПоУмолчанию
	|		ИНАЧЕ ТаблицаДокумента.Договор
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ОплатаСертификатом
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ОплатаСертификатом
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		КОГДА ТаблицаДокумента.Заказ = НЕОПРЕДЕЛЕНО
	|				ИЛИ НЕ ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаДокумента.Заказ
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4,
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки),
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&СторнированиеПредоплаты,
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	&КонтрагентДляПредоплаты,
	|	&СчетУчетаАвансовПокупателя,
	|	&ДоговорПоУмолчанию,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка),
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.Курс,
	|	ТаблицаДокумента.Кратность,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс),
	|	-СУММА(ТаблицаДокумента.Сумма),
	|	-СУММА(ТаблицаДокумента.СуммаВал),
	|	СУММА(0),
	|	-СУММА(ТаблицаДокумента.Сумма),
	|	-СУММА(ТаблицаДокумента.СуммаВал)
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|	И ТаблицаДокумента.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.Курс,
	|	ТаблицаДокумента.Кратность";
	
	// @skip-warning используется для записи временной таблицы
	РезультатЗапроса = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру "СуммыДокументовРегламентированныйУчет".
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСуммыДокументовРегламентированныйУчет(ДокументСсылкаРасходнаяНакладная,
	СтруктураДополнительныеСвойства)

	Если ДокументСсылкаРасходнаяНакладная.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика
		 И ДокументСсылкаРасходнаяНакладная.Договор.ВалютаРасчетов <> СтруктураДополнительныеСвойства.НациональнаяВалюта Тогда

		РасчетыПроведениеДокументов.СформироватьТаблицаСуммыДокументовРегламентированныйУчет(
			ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства);

	Иначе

		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСуммыДокументовРегламентированныйУчет",
			Новый ТаблицаЗначений);

	КонецЕсли;

КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру "ОплатаДокументов".
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаОплатаДокументов(ДокументСсылка, СтруктураДополнительныеСвойства)

	МассивДоступныхОпераций = Новый Массив;
	МассивДоступныхОпераций.Добавить(Перечисления.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика);

	РасчетыПроведениеДокументов.СформироватьТаблицаОплатаДокументовНакладные(ДокументСсылка,
		СтруктураДополнительныеСвойства, МассивДоступныхОпераций);

КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру "ОплатаДокументов".
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаОплатаСчетовИЗаказов(ДокументСсылка, СтруктураДополнительныеСвойства)

	МассивДоступныхОпераций = Новый Массив;
	МассивДоступныхОпераций.Добавить(Перечисления.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика);
	РасчетыПроведениеДокументов.СформироватьТаблицаОплатаСчетовИЗаказов(ДокументСсылка,
		СтруктураДополнительныеСвойства, МассивДоступныхОпераций);

КонецПроцедуры

Функция ПолучитьТекстЗапросаИнициализироватьДанные()
	
	ТекстЗапроса = 
	
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладная.Договор.ЭтоДоговорОбслуживания
	|				И ПриходнаяНакладная.Договор.ДоговорОбслуживанияНаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВестиУчетРасходовПоДоговорамОбслуживания,
	|	ПриходнаяНакладная.ПоложениеСклада КАК ПоложениеСклада,
	|	ПриходнаяНакладная.Договор.ДоговорОбслуживанияНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ПриходнаяНакладная.Контрагент КАК Контрагент,
	|	ПриходнаяНакладная.ВалютаДокумента КАК ВалютаДокумента,
	|	ПриходнаяНакладная.Дата КАК Дата,
	|	ПриходнаяНакладная.Договор.ВалютаРасчетов КАК ДоговорВалютаРасчетов,
	|	ПриходнаяНакладная.Договор.РасчетыВУсловныхЕдиницах КАК ДоговорРасчетыВУсловныхЕдиницах,
	|	ПриходнаяНакладная.Ссылка КАК Ссылка,
	|	ПриходнаяНакладная.Дата КАК Период,
	|	ПриходнаяНакладная.Договор КАК Договор,
	|	ПриходнаяНакладная.Проект КАК Проект,
	|	ПриходнаяНакладная.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ПриходнаяНакладная.СпособЗачетаПредоплаты КАК СпособЗачетаПредоплаты,
	|	ПриходнаяНакладная.Заказ КАК Заказ,
	|	ПриходнаяНакладная.ПоложениеЗаказаПоставщику КАК ПоложениеЗаказаПоставщику,
	|	ПриходнаяНакладная.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	ПриходнаяНакладная.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ПриходнаяНакладная.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	ПриходнаяНакладная.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладная.Кратность = 0
	|			ТОГДА 1
	|		ИНАЧЕ ПриходнаяНакладная.Кратность
	|	КОНЕЦ КАК Кратность,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладная.Курс = 0
	|			ТОГДА 1
	|		ИНАЧЕ ПриходнаяНакладная.Курс
	|	КОНЕЦ КАК Курс,
	|	ПриходнаяНакладная.НДСВключатьВСтоимость КАК НДСВключатьВСтоимость,
	|	ПриходнаяНакладная.ВключатьРасходыВСебестоимость КАК ВключатьРасходыВСебестоимость,
	|	ПриходнаяНакладная.ВидОперации КАК ВидОперации,
	|	ПриходнаяНакладная.ДокументОснование КАК ДокументОснование,
	|	ПриходнаяНакладная.Ответственный КАК Ответственный,
	|	ПриходнаяНакладная.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ПриходнаяНакладная.Подразделение КАК Подразделение,
	|	ПриходнаяНакладная.ПринятьПоКолонке КАК ПринятьПоКолонке,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|				И ПриходнаяНакладная.ДокументОснование ССЫЛКА Документ.РасходнаяНакладная
	|				И ПриходнаяНакладная.ДокументОснование <> ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка)
	|			ТОГДА ПриходнаяНакладная.ДокументОснование
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КорректируемыйДокумент,
	|	ПриходнаяНакладная.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ПриходнаяНакладная.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ПриходнаяНакладная.Организация КАК Организация
	|ПОМЕСТИТЬ ВременнаяТаблицаШапка
	|ИЗ
	|	Документ.ПриходнаяНакладная КАК ПриходнаяНакладная
	|ГДЕ
	|	ПриходнаяНакладная.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ ВременнаяТаблицаКурсыВалютСрезПоследних
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта В (&ВалютаУчета, &ВалютаНациональная)) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаКурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаКурсыВалютСрезПоследних.Курс = 0
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(ВременнаяТаблицаКурсыВалютСрезПоследних.Курс, 1)
	|	КОНЕЦ КАК Курс,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаКурсыВалютСрезПоследних.Кратность = 0
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(ВременнаяТаблицаКурсыВалютСрезПоследних.Кратность, 1)
	|	КОНЕЦ КАК Кратность
	|ПОМЕСТИТЬ ВременнаяТаблицаКурсыВалютСрезПоследнихСПроверкой
	|ИЗ
	|	ВременнаяТаблицаКурсыВалютСрезПоследних КАК ВременнаяТаблицаКурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриходнаяНакладнаяЗапасы.НомерСтроки КАК НомерСтроки,
	|	ВременнаяТаблицаШапка.ВидОперации КАК ВидОперации,
	|	ПриходнаяНакладнаяЗапасы.Ссылка КАК Документ,
	|	ВременнаяТаблицаШапка.ДокументОснование КАК ДокументОснование,
	|	ВременнаяТаблицаШапка.ПоложениеСклада КАК ПоложениеСклада,
	|	ВременнаяТаблицаШапка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаШапка,
	|	ВременнаяТаблицаШапка.СтруктурнаяЕдиница.ОрдерныйСклад КАК СтруктурнаяЕдиницаШапкаОрдерныйСклад,
	|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаШапка.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	ВременнаяТаблицаШапка.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ВременнаяТаблицаШапка.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	ВременнаяТаблицаШапка.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	ВременнаяТаблицаШапка.Договор КАК Договор,
	|	ВременнаяТаблицаШапка.Ответственный КАК Ответственный,
	|	ПриходнаяНакладнаяЗапасы.СтруктурнаяЕдиница.СчетУчетаНаценки КАК СчетУчетаНаценки,
	|	ПриходнаяНакладнаяЗапасы.СтруктурнаяЕдиница.РозничныйВидЦен КАК РозничныйВидЦен,
	|	ПриходнаяНакладнаяЗапасы.СтруктурнаяЕдиница.РозничныйВидЦен.ВалютаЦены КАК ВалютаЦены,
	|	ПриходнаяНакладнаяЗапасы.СтруктурнаяЕдиница.СчетУчетаВРознице КАК СчетУчетаВРознице,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемВПереработку), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаОтветХранение))
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.РозницаСуммовойУчет)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПеремещениеВРозницуСуммовойУчет,
	|	ВременнаяТаблицаШапка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения))
	|			ТОГДА &Организация
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КоррОрганизация,
	|	ПриходнаяНакладнаяЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения))
	|			ТОГДА ВременнаяТаблицаШапка.Контрагент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КоррСтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.СтруктурнаяЕдиница.ОрдерныйСклад
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдерныйСклад,
	|	ВЫБОР
	|		КОГДА &УчетПоЯчейкам
	|			ТОГДА ПриходнаяНакладнаяЗапасы.Ячейка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Ячейка,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.РозницаСуммовойУчет)
	|			ТОГДА ПриходнаяНакладнаяЗапасы.СтруктурнаяЕдиница.СчетУчетаВРознице
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.Номенклатура.СчетУчетаЗапасов
	|	КОНЕЦ КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.РозницаСуммовойУчет)
	|			ТОГДА ПриходнаяНакладнаяЗапасы.СтруктурнаяЕдиница.СчетУчетаВРознице.Валютный
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.Номенклатура.СчетУчетаЗапасов.Валютный
	|	КОНЕЦ КАК СчетУчетаВалютный,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения))
	|			ТОГДА ПриходнаяНакладнаяЗапасы.Номенклатура.СчетУчетаЗапасов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КоррСчетУчета,
	|	ПриходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
	|	ПриходнаяНакладнаяЗапасы.НоменклатураНабора КАК НоменклатураНабора,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения))
	|			ТОГДА ПриходнаяНакладнаяЗапасы.Номенклатура
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КоррНоменклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА ПриходнаяНакладнаяЗапасы.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА ПриходнаяНакладнаяЗапасы.ХарактеристикаНабора
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК ХарактеристикаНабора,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения))
	|			ТОГДА ВЫБОР
	|					КОГДА &ИспользоватьХарактеристики
	|						ТОГДА ПриходнаяНакладнаяЗапасы.Характеристика
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КоррХарактеристика,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПартии
	|			ТОГДА ПриходнаяНакладнаяЗапасы.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Партия,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения))
	|			ТОГДА ВЫБОР
	|					КОГДА &ИспользоватьПартии
	|						ТОГДА ПриходнаяНакладнаяЗапасы.Партия
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КоррПартия,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				И ПриходнаяНакладнаяЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ПриходнаяНакладнаяЗапасы.Заказ
	|		КОГДА ПриходнаяНакладнаяЗапасы.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ПриходнаяНакладнаяЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ПриходнаяНакладнаяЗапасы.Заказ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Заказ,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				И ПриходнаяНакладнаяЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяЗапасы.Заказ КАК Документ.ЗаказПокупателя).УчетПотребностиПоСкладам
	|		КОГДА ПриходнаяНакладнаяЗапасы.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ПриходнаяНакладнаяЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяЗапасы.Заказ КАК Документ.ЗаказПоставщику).УчетПотребностиПоСкладам
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК УчетПотребностиПоСкладам,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				И ПриходнаяНакладнаяЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяЗапасы.Заказ КАК Документ.ЗаказПокупателя).УчетПотребностиПоЗаказам
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК УчетПотребностиПоЗаказам,
	|	ПриходнаяНакладнаяЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				И ВЫРАЗИТЬ(ПриходнаяНакладнаяЗапасы.Заказ КАК Документ.ЗаказПокупателя).ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЗаказНаряд,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения))
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходнаяНакладнаяЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|							И ПриходнаяНакладнаяЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|						ТОГДА ПриходнаяНакладнаяЗапасы.Заказ
	|					КОГДА ПриходнаяНакладнаяЗапасы.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|							И ПриходнаяНакладнаяЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|						ТОГДА ПриходнаяНакладнаяЗапасы.Заказ
	|					ИНАЧЕ НЕОПРЕДЕЛЕНО
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КоррЗаказ,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПартии
	|				И ПриходнаяНакладнаяЗапасы.Партия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварыНаКомиссии)
	|				И ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТоварыНаКомиссии,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ДокументОснование ССЫЛКА Документ.РасходнаяНакладная
	|				И ВременнаяТаблицаШапка.ДокументОснование <> ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(ВременнаяТаблицаШапка.ДокументОснование КАК Документ.РасходнаяНакладная).Подразделение
	|		ИНАЧЕ ВременнаяТаблицаШапка.Подразделение
	|	КОНЕЦ КАК ПодразделениеПродажи,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.Номенклатура.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельностиПродажи,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности.СчетУчетаВыручкиОтПродаж
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.Номенклатура.НаправлениеДеятельности.СчетУчетаВыручкиОтПродаж
	|	КОНЕЦ КАК СчетУчетаПродажи,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности.СчетУчетаСебестоимостиПродаж
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.Номенклатура.НаправлениеДеятельности.СчетУчетаСебестоимостиПродаж
	|	КОНЕЦ КАК СчетУчетаСебестоимость,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.ЕдиницыИзмерения)
	|			ТОГДА ПриходнаяНакладнаяЗапасы.Количество * ВЫРАЗИТЬ(ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.Количество
	|	КОНЕЦ КАК Количество,
	|	ПриходнаяНакладнаяЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = &ВалютаНациональная
	|						ТОГДА ПриходнаяНакладнаяЗапасы.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|					ИНАЧЕ ПриходнаяНакладнаяЗапасы.СуммаНДС * ВременнаяТаблицаШапка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВременнаяТаблицаШапка.Кратность)
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ПриходнаяНакладнаяЗапасы.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ПриходнаяНакладнаяЗапасы.СуммаНДС * ВременнаяТаблицаШапка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВременнаяТаблицаШапка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСЗакупкиПродажи,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ПриходнаяНакладнаяЗапасы.Всего * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ПриходнаяНакладнаяЗапасы.Всего * ВременнаяТаблицаШапка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВременнаяТаблицаШапка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ПриходнаяНакладнаяЗапасы.СуммаРасходов * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ПриходнаяНакладнаяЗапасы.СуммаРасходов * ВременнаяТаблицаШапка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВременнаяТаблицаШапка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаРасходов,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ПриходнаяНакладнаяЗапасы.СуммаРасходов * КурсыРегВалюты.Курс * ВременнаяТаблицаШапка.Кратность / (ВременнаяТаблицаШапка.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ПриходнаяНакладнаяЗапасы.СуммаРасходов
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаРасходовВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ПриходнаяНакладнаяЗапасы.Себестоимость * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ПриходнаяНакладнаяЗапасы.Себестоимость * ВременнаяТаблицаШапка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВременнаяТаблицаШапка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Себестоимость,
	|	ПриходнаяНакладнаяЗапасы.Всего КАК СуммаРасчетовПринятыеПереданные,
	|	ВременнаяТаблицаШапка.ВключатьРасходыВСебестоимость КАК ВключатьРасходыВСебестоимость,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя))
	|				И (ВременнаяТаблицаШапка.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|						И ВЫРАЗИТЬ(ВременнаяТаблицаШапка.ДокументОснование КАК Документ.ЗаказПокупателя).ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд)
	|					ИЛИ ВременнаяТаблицаШапка.ДокументОснование ССЫЛКА Документ.ЧекККМ)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ФиксированнаяСтоимость,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемВПереработку), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаОтветХранение))
	|			ТОГДА ВЫРАЗИТЬ(&ОприходованиеЗапасов КАК СТРОКА(100))
	|		КОГДА ВременнаяТаблицаШапка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения))
	|			ТОГДА ВЫРАЗИТЬ(&СписаниеЗапасов КАК СТРОКА(100))
	|	КОНЕЦ КАК СодержаниеПроводки,
	|	ВременнаяТаблицаШапка.Контрагент.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|	ВременнаяТаблицаШапка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	ВременнаяТаблицаШапка.Контрагент.СчетУчетаРасчетовСПоставщиком.Валютный КАК СчетУчетаРасчетовСПоставщикомВалютный,
	|	ПриходнаяНакладнаяЗапасы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ПриходнаяНакладнаяЗапасы.НомерГТД КАК НомерГТД,
	|	ВременнаяТаблицаШапка.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ПриходнаяНакладнаяЗапасы.Всего * КурсыРегВалюты.Курс * ВременнаяТаблицаШапка.Кратность / (ВременнаяТаблицаШапка.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ПриходнаяНакладнаяЗапасы.Всего
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ПриходнаяНакладнаяЗапасы.Всего
	|			ИНАЧЕ ПриходнаяНакладнаяЗапасы.Всего * ВременнаяТаблицаШапка.Курс / КурсыРегВалюты.Кратность
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаРег,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = &ВалютаНациональная
	|						ТОГДА ПриходнаяНакладнаяЗапасы.СуммаНДС * КурсыРегВалюты.Курс * ВременнаяТаблицаШапка.Кратность / (ВременнаяТаблицаШапка.Курс * КурсыРегВалюты.Кратность)
	|					ИНАЧЕ ПриходнаяНакладнаяЗапасы.СуммаНДС
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = &ВалютаНациональная
	|						ТОГДА ПриходнаяНакладнаяЗапасы.СуммаНДС
	|					ИНАЧЕ ПриходнаяНакладнаяЗапасы.СуммаНДС * ВременнаяТаблицаШапка.Курс / КурсыРегВалюты.Кратность
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСРег,
	|	ПриходнаяНакладнаяЗапасы.КлючСвязи КАК КлючСвязи,
	|	ПриходнаяНакладнаяЗапасы.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ПриходнаяНакладнаяЗапасы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ПриходнаяНакладнаяЗапасы.Номенклатура.ПроизвольныйНоминал
	|				ТОГДА ПриходнаяНакладнаяЗапасы.Сумма / ВЫБОР
	|						КОГДА ПриходнаяНакладнаяЗапасы.Номенклатура.ИспользоватьСерииНоменклатуры
	|							ТОГДА ПриходнаяНакладнаяЗапасы.Количество
	|						ИНАЧЕ 1
	|					КОНЕЦ
	|			ИНАЧЕ ПриходнаяНакладнаяЗапасы.Номенклатура.Номинал / ВЫБОР
	|					КОГДА ПриходнаяНакладнаяЗапасы.Номенклатура.ИспользоватьСерииНоменклатуры
	|						ТОГДА 1
	|					ИНАЧЕ 1 / ПриходнаяНакладнаяЗапасы.Количество
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК НоминалСертификата,
	|	ПриходнаяНакладнаяЗапасы.Номенклатура.ПроизвольныйНоминал КАК ПроизвольныйНоминал,
	|	ПриходнаяНакладнаяЗапасы.Номенклатура.СчетУчетаЗатрат КАК СчетУчетаЗатрат,
	|	ПриходнаяНакладнаяЗапасы.Номенклатура.ИспользоватьСерииНоменклатуры КАК ИспользоватьСерииНоменклатуры,
	|	ПриходнаяНакладнаяЗапасы.Цена КАК Цена,
	|	ПриходнаяНакладнаяЗапасы.СуммаНДС КАК СуммаНДСВалютаДокумента,
	|	ПриходнаяНакладнаяЗапасы.Всего КАК ВсегоВалютаДокумента,
	|	ПриходнаяНакладнаяЗапасы.СуммаСкидкиНаценки КАК СуммаСкидкиНаценки,
	|	ВременнаяТаблицаШапка.Курс КАК Курс,
	|	ВременнаяТаблицаШапка.Кратность КАК Кратность,
	|	ВременнаяТаблицаШапка.Проект КАК Проект,
	|	ЕСТЬNULL(ПолитикиУчетаСерий.ПолитикаУчетаСерий, ПриходнаяНакладнаяЗапасы.Номенклатура.ПолитикаУчетаСерий) КАК ПолитикаУчетаСерий,
	|	NULL КАК КорректируемыйДокумент,
	|	ПриходнаяНакладнаяЗапасы.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасы
	|ИЗ
	|	Документ.ПриходнаяНакладная.Запасы КАК ПриходнаяНакладнаяЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследнихСПроверкой КАК КурсыУпрВалюты
	|		ПО (КурсыУпрВалюты.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследнихСПроверкой КАК КурсыРегВалюты
	|		ПО (КурсыРегВалюты.Валюта = &ВалютаНациональная)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|		ПО ПриходнаяНакладнаяЗапасы.Номенклатура = ПолитикиУчетаСерий.Владелец
	|			И ПриходнаяНакладнаяЗапасы.СтруктурнаяЕдиница = ПолитикиУчетаСерий.СтруктурнаяЕдиница
	|			И (&Организация = ПолитикиУчетаСерий.Организация),
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	ПриходнаяНакладнаяЗапасы.Ссылка = &Ссылка
	|	И ПриходнаяНакладнаяЗапасы.Количество > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриходнаяНакладнаяРасходы.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ПриходнаяНакладнаяРасходы.Ссылка.Дата КАК Период,
	|	ПриходнаяНакладнаяРасходы.Ссылка КАК Документ,
	|	ПриходнаяНакладнаяРасходы.Ссылка.ВидОперации КАК ВидОперации,
	|	&Организация КАК Организация,
	|	ПриходнаяНакладнаяРасходы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента КАК Валюта,
	|	ПриходнаяНакладнаяРасходы.Номенклатура.СчетУчетаЗатрат КАК СчетУчета,
	|	ПриходнаяНакладнаяРасходы.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК ЗапасыНоменклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка) КАК Партия,
	|	ПриходнаяНакладнаяРасходы.Заказ КАК Заказ,
	|	ПриходнаяНакладнаяРасходы.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА ПриходнаяНакладнаяРасходы.Количество
	|		ИНАЧЕ ПриходнаяНакладнаяРасходы.Количество * ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Количество,
	|	ПриходнаяНакладнаяРасходы.СтавкаНДС КАК СтавкаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ПриходнаяНакладнаяРасходы.Всего * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ПриходнаяНакладнаяРасходы.Всего * ПриходнаяНакладнаяРасходы.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВЫБОР
	|					КОГДА ПриходнаяНакладнаяРасходы.Ссылка.Кратность = 0
	|						ТОГДА 1
	|					ИНАЧЕ ПриходнаяНакладнаяРасходы.Ссылка.Кратность
	|				КОНЕЦ)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ПриходнаяНакладнаяРасходы.Всего * КурсыРегВалюты.Курс * ПриходнаяНакладнаяРасходы.Ссылка.Кратность / (ВЫБОР
	|						КОГДА ПриходнаяНакладнаяРасходы.Ссылка.Курс = 0
	|							ТОГДА 1
	|						ИНАЧЕ ПриходнаяНакладнаяРасходы.Ссылка.Курс
	|					КОНЕЦ * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ПриходнаяНакладнаяРасходы.Всего
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ПриходнаяНакладнаяРасходы.Всего
	|			ИНАЧЕ ПриходнаяНакладнаяРасходы.Всего * ПриходнаяНакладнаяРасходы.Ссылка.Курс / ВЫБОР
	|					КОГДА ПриходнаяНакладнаяРасходы.Ссылка.Кратность = 0
	|						ТОГДА 1
	|					ИНАЧЕ ПриходнаяНакладнаяРасходы.Ссылка.Кратность
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаРег,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ПриходнаяНакладнаяРасходы.Ссылка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|						ТОГДА ПриходнаяНакладнаяРасходы.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|					ИНАЧЕ ПриходнаяНакладнаяРасходы.СуммаНДС * ПриходнаяНакладнаяРасходы.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВЫБОР
	|							КОГДА ПриходнаяНакладнаяРасходы.Ссылка.Кратность = 0
	|								ТОГДА 1
	|							ИНАЧЕ ПриходнаяНакладнаяРасходы.Ссылка.Кратность
	|						КОНЕЦ)
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ПриходнаяНакладнаяРасходы.Ссылка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|						ТОГДА ПриходнаяНакладнаяРасходы.СуммаНДС
	|					ИНАЧЕ ПриходнаяНакладнаяРасходы.СуммаНДС * ПриходнаяНакладнаяРасходы.Ссылка.Курс / ВЫБОР
	|							КОГДА ПриходнаяНакладнаяРасходы.Ссылка.Кратность = 0
	|								ТОГДА 1
	|							ИНАЧЕ ПриходнаяНакладнаяРасходы.Ссылка.Кратность
	|						КОНЕЦ
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСРег,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ПриходнаяНакладнаяРасходы.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ПриходнаяНакладнаяРасходы.СуммаНДС * ПриходнаяНакладнаяРасходы.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВЫБОР
	|					КОГДА ПриходнаяНакладнаяРасходы.Ссылка.Кратность = 0
	|						ТОГДА 1
	|					ИНАЧЕ ПриходнаяНакладнаяРасходы.Ссылка.Кратность
	|				КОНЕЦ)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСЗакупкиПродажи,
	|	ПриходнаяНакладнаяРасходы.Ссылка.ВключатьРасходыВСебестоимость КАК ВключатьРасходыВСебестоимость,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяРасходы.Номенклатура.СчетУчетаЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
	|				И НЕ ПриходнаяНакладнаяРасходы.Ссылка.ВключатьРасходыВСебестоимость
	|			ТОГДА ПриходнаяНакладнаяРасходы.НаправлениеДеятельности
	|		ИНАЧЕ ПриходнаяНакладнаяРасходы.Номенклатура.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ПриходнаяНакладнаяРасходы.Ссылка.Контрагент КАК Контрагент,
	|	ПриходнаяНакладнаяРасходы.Ссылка.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	ПриходнаяНакладнаяРасходы.Ссылка.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ПриходнаяНакладнаяРасходы.Ссылка.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	ПриходнаяНакладнаяРасходы.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	ПриходнаяНакладнаяРасходы.Ссылка.Договор КАК Договор,
	|	ПриходнаяНакладнаяРасходы.Ссылка.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ПриходнаяНакладнаяРасходы.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	ПриходнаяНакладнаяРасходы.Номенклатура.СчетУчетаЗатрат.ТипСчета КАК ТипСчета,
	|	ПриходнаяНакладнаяРасходы.Всего КАК ВсегоВалютаДокумента,
	|	ПриходнаяНакладнаяРасходы.Ссылка.Проект КАК Проект
	|ПОМЕСТИТЬ ВременнаяТаблицаРасходы
	|ИЗ
	|	Документ.ПриходнаяНакладная.Расходы КАК ПриходнаяНакладнаяРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследнихСПроверкой КАК КурсыУпрВалюты
	|		ПО (КурсыУпрВалюты.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследнихСПроверкой КАК КурсыРегВалюты
	|		ПО (КурсыРегВалюты.Валюта = &ВалютаНациональная)
	|ГДЕ
	|	ПриходнаяНакладнаяРасходы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Ссылка.ВидОперации КАК ВидОперации,
	|	ТаблицаДокумента.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаДокумента.Ссылка.Контрагент КАК Контрагент,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПокупателя КАК СчетУчетаАвансовПокупателя,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПоставщику КАК СчетУчетаАвансовПоставщику,
	|	ТаблицаДокумента.Ссылка.Организация КАК ОрганизацияВНУ,
	|	ТаблицаДокумента.Ссылка.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаДокумента.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаДокумента.Ссылка.Договор КАК Договор,
	|	ТаблицаДокумента.Ссылка.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ТаблицаДокумента.Заказ КАК Заказ,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее) КАК НаправлениеДеятельностиПродажи,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс) КАК ТипРасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг) КАК ТипРасчетовКуда,
	|	&Ссылка КАК ДокументКуда,
	|	ТаблицаДокумента.Документ КАК Документ,
	|	ТаблицаДокумента.Ссылка.ДокументОснование КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам
	|				ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Документ) = ТИП(Документ.АвансовыйОтчет)
	|				ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Документ) = ТИП(Документ.Взаимозачет)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаДокумента.Документ ССЫЛКА Документ.РасходСоСчета
	|					ТОГДА ВЫРАЗИТЬ(ТаблицаДокумента.Документ КАК Документ.РасходСоСчета).Статья
	|				КОГДА ТаблицаДокумента.Документ ССЫЛКА Документ.ПоступлениеНаСчет
	|					ТОГДА ВЫРАЗИТЬ(ТаблицаДокумента.Документ КАК Документ.ПоступлениеНаСчет).Статья
	|				КОГДА ТаблицаДокумента.Документ ССЫЛКА Документ.ПоступлениеВКассу
	|					ТОГДА ВЫРАЗИТЬ(ТаблицаДокумента.Документ КАК Документ.ПоступлениеВКассу).Статья
	|				КОГДА ТаблицаДокумента.Документ ССЫЛКА Документ.РасходИзКассы
	|					ТОГДА ВЫРАЗИТЬ(ТаблицаДокумента.Документ КАК Документ.РасходИзКассы).Статья
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам)
	|			КОНЕЦ
	|	КОНЕЦ КАК Статья,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаДокумента.Документ ССЫЛКА Документ.РасходСоСчета
	|						ТОГДА ВЫРАЗИТЬ(ТаблицаДокумента.Документ КАК Документ.РасходСоСчета).Дата
	|					КОГДА ТаблицаДокумента.Документ ССЫЛКА Документ.ПоступлениеНаСчет
	|						ТОГДА ВЫРАЗИТЬ(ТаблицаДокумента.Документ КАК Документ.ПоступлениеНаСчет).Дата
	|					КОГДА ТаблицаДокумента.Документ ССЫЛКА Документ.ПоступлениеВКассу
	|						ТОГДА ВЫРАЗИТЬ(ТаблицаДокумента.Документ КАК Документ.ПоступлениеВКассу).Дата
	|					КОГДА ТаблицаДокумента.Документ ССЫЛКА Документ.РасходИзКассы
	|						ТОГДА ВЫРАЗИТЬ(ТаблицаДокумента.Документ КАК Документ.РасходИзКассы).Дата
	|					КОГДА ТаблицаДокумента.Документ ССЫЛКА Документ.АвансовыйОтчет
	|						ТОГДА ВЫРАЗИТЬ(ТаблицаДокумента.Документ КАК Документ.АвансовыйОтчет).Дата
	|					КОГДА ТаблицаДокумента.Документ ССЫЛКА Документ.Взаимозачет
	|						ТОГДА ВЫРАЗИТЬ(ТаблицаДокумента.Документ КАК Документ.Взаимозачет).Дата
	|				КОНЕЦ
	|		ИНАЧЕ ТаблицаДокумента.Ссылка.Дата
	|	КОНЕЦ КАК ДокументДата,
	|	ВЫБОР
	|		КОГДА КонстантаВалютаУчета.Значение = &ВалютаРасчетов
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		КОГДА КонстантаВалютаУчета.Значение = КонстантаНациональнаяВалюта.Значение
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаДокумента.СуммаРасчетов * ТаблицаДокумента.Курс / ВЫБОР
	|						КОГДА ТаблицаДокумента.Кратность = 0
	|							ТОГДА 1
	|						ИНАЧЕ ТаблицаДокумента.Кратность
	|					КОНЕЦ КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаДокумента.СуммаРасчетов * ТаблицаДокумента.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * ВЫБОР
	|					КОГДА ТаблицаДокумента.Кратность = 0
	|						ТОГДА 1
	|					ИНАЧЕ ТаблицаДокумента.Кратность
	|				КОНЕЦ) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК Сумма,
	|	ТаблицаДокумента.СуммаРасчетов КАК СуммаВал,
	|	ТаблицаДокумента.ОплатаСертификатом КАК ОплатаСертификатом,
	|	ТаблицаДокумента.НомерСертификата КАК НомерСертификата,
	|	ЕСТЬNULL(Номенклатура.Номинал, 0) КАК НоминалСертификата,
	|	ТаблицаДокумента.СуммаПлатежа КАК СуммаСертификата,
	|	ЕСТЬNULL(Номенклатура.ЧастичноеПогашение, ЛОЖЬ) КАК ЧастичноеПогашение,
	|	ТаблицаДокумента.Ссылка.Договор.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	ВЫРАЗИТЬ(ТаблицаДокумента.СуммаРасчетов * ТаблицаДокумента.Курс / ВЫБОР
	|			КОГДА ТаблицаДокумента.Кратность = 0
	|				ТОГДА 1
	|			ИНАЧЕ ТаблицаДокумента.Кратность
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаРубПоКурсуАванса,
	|	ТаблицаДокумента.Курс КАК Курс,
	|	ТаблицаДокумента.Кратность КАК Кратность,
	|	ТаблицаДокумента.СуммаРасчетов КАК СуммаРасчетов,
	|	ТаблицаДокумента.СуммаПлатежа КАК СуммаПлатежа
	|ПОМЕСТИТЬ ВременнаяТаблицаПредоплата
	|ИЗ
	|	Документ.ПриходнаяНакладная.Предоплата КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО ТаблицаДокумента.Документ = Номенклатура.Ссылка,
	|	Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта,
	|	Константа.ВалютаУчета КАК КонстантаВалютаУчета
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриходнаяНакладнаяСерииНоменклатуры.КлючСвязи КАК КлючСвязи,
	|	ПриходнаяНакладнаяСерииНоменклатуры.Серия КАК Серия,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяСерииНоменклатуры.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ ПриходнаяНакладнаяСерииНоменклатуры.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаЗапасы.ПолитикаУчетаСерий = ЗНАЧЕНИЕ(Справочник.ПолитикаУчетаСерий.ПустаяСсылка)
	|			ТОГДА &ОстаткиСерийНоменклатуры
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВременнаяТаблицаЗапасы.ПолитикаУчетаСерий.ТипПолитики = ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.УправлениеОстаткамиСерий)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|	КОНЕЦ КАК ОстаткиСерийНоменклатуры
	|ПОМЕСТИТЬ ВременнаяТаблицаСерииНоменклатуры
	|ИЗ
	|	Документ.ПриходнаяНакладная.СерииНоменклатуры КАК ПриходнаяНакладнаяСерииНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаЗапасы КАК ВременнаяТаблицаЗапасы
	|		ПО (ВременнаяТаблицаЗапасы.КлючСвязи = ПриходнаяНакладнаяСерииНоменклатуры.КлючСвязи)
	|ГДЕ
	|	&ИспользоватьСерииНоменклатуры
	|	И ПриходнаяНакладнаяСерииНоменклатуры.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	СведенияПрослеживаемости.НомерСтроки КАК НомерСтроки,
	|	СведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	СведенияПрослеживаемости.Количество КАК Количество,
	|	СведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	СведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВременнаяТаблицаСведенияПрослеживаемости
	|ИЗ
	|	Документ.ПриходнаяНакладная.СведенияПрослеживаемости КАК СведенияПрослеживаемости
	|ГДЕ
	|	СведенияПрослеживаемости.Ссылка = &Ссылка
	|	И &ВестиУчетПрослеживаемыхТоваров";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = ПолучитьТекстЗапросаИнициализироватьДанные(); // (обратно через "Ctrl"+"-")

	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриходнаяНакладная);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.Дляпроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени",
		Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики",
		СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьХарактеристики);
	Запрос.УстановитьПараметр("ИспользоватьПартии", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьПартии);
	Запрос.УстановитьПараметр("УчетПоЯчейкам", СтруктураДополнительныеСвойства.УчетнаяПолитика.УчетПоЯчейкам);
	Запрос.УстановитьПараметр("ИспользоватьСерииНоменклатуры",
		СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьСерииНоменклатуры);
	Запрос.УстановитьПараметр("ОстаткиСерийНоменклатуры", СтруктураДополнительныеСвойства.УчетнаяПолитика.ОстаткиСерийНоменклатуры);
	// Прослеживаемость
	Запрос.УстановитьПараметр("ВестиУчетПрослеживаемыхТоваров",
		СтруктураДополнительныеСвойства.УчетнаяПолитика.ВестиУчетПрослеживаемыхТоваров);

	Запрос.УстановитьПараметр("ОприходованиеЗапасов", НСтр("ru = 'Оприходование запасов'"));
	Запрос.УстановитьПараметр("СписаниеЗапасов", НСтр("ru = 'Списание запасов'"));

	Запрос.УстановитьПараметр("ВалютаУчета", СтруктураДополнительныеСвойства.ВалютаУчета);
	Запрос.УстановитьПараметр("ВалютаНациональная", СтруктураДополнительныеСвойства.НациональнаяВалюта);
	Запрос.УстановитьПараметр("ВалютаРасчетов", СтруктураДополнительныеСвойства.Договор.ВалютаРасчетов);
	
	// Перед выполнением запроса проверим, необходим ли пересчет из валюты в валюту
	РасчетыПроведениеДокументов.МодифицироватьЗапросДляПересчетаВВалюту(ДокументСсылкаПриходнаяНакладная, Запрос,
		СтруктураДополнительныеСвойства);

	Запрос.ВыполнитьПакет();

	Если Не ДокументСсылкаПриходнаяНакладная.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика Тогда
		СтруктураДополнительныеСвойства.Вставить("БезУчетаАвансов", Истина);
		РасчетыПроведениеДокументов.ПересчитатьТаблицыПоКурсамВалют(ДокументСсылкаПриходнаяНакладная,
			СтруктураДополнительныеСвойства);
		РасчетыПроведениеДокументов.ПоместитьПересчитанныеТаблицыВМенеджерВременныхТаблиц(
			СтруктураДополнительныеСвойства);
	КонецЕсли;
	
	// Формирование проводок документа.
	ПроведениеДокументовУНФ.СформироватьТаблицуПроводок(ДокументСсылкаПриходнаяНакладная,
		СтруктураДополнительныеСвойства);

	СформироватьТаблицаРасчетыСПоставщиками(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРасчетыСПокупателями(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);

	СформироватьТаблицаЗакупки(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПродажи(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасыНаСкладах(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасыКПоступлениюНаСклады(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасыПринятые(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасыПереданные(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗаказыПокупателей(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗаказыПоставщикам(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРазмещениеЗаказов(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПотребностьВЗапасах(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	Если ДокументСсылкаПриходнаяНакладная.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя Тогда
		СформироватьТаблицаДоходыИРасходы(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
		СформироватьТаблицаЗапасы(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	Иначе
		СформироватьТаблицаЗапасы(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
		СформироватьТаблицаДоходыИРасходы(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	КонецЕсли; 
	
	СформироватьТаблицаЗапасыВРазрезеГТД(СтруктураДополнительныеСвойства);
	// Прослеживаемость
	ПрослеживаемостьУНФ.СформироватьДвиженияПоступлениеТоваров(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	
	// Кассовый метод
	РасчетыПроведениеДокументов.СформироватьВременнаяТаблицаПредоплатаДляКассовогоМетода(
		СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыОтложенные(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыНераспределенные(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыКассовыйМетод(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	// Конец Кассовый метод
	СформироватьТаблицаСуммовойУчетВРознице(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);

	СформироватьТаблицаУправленческий(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	
	// ДисконтныеКарты
	СформироватьТаблицаПродажиПоДисконтнойКарте(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);

	// Серии
	СформироватьТаблицаСерииНоменклатуры(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСерииНоменклатурыКПоступлению(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	
	// Подарочные сертификаты
	СформироватьТаблицаПодарочныеСертификаты(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаОплатаПодарочнымиСертификатами(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	
	// Отчетность
	СформироватьТаблицаЗакупкиПоставщиковДляКУДиР(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	
	// Взаиморасчеты
	СформироватьТаблицаОплатаДокументов(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);
	СформироватьТаблицаОплатаСчетовИЗаказов(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства);

	СформироватьТаблицаСуммыДокументовРегламентированныйУчет(ДокументСсылкаПриходнаяНакладная,
		СтруктураДополнительныеСвойства);

КонецПроцедуры // ИнициализироватьДанныеДокумента()

// Выполняет контроль возникновения отрицательных остатков.
//
Процедура ВыполнитьКонтроль(ДокументСсылкаПриходнаяНакладная, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт

	Если ПроведениеДокументовУНФ.КонтрольОстатковВыключен() Тогда
		Возврат;
	КонецЕсли;
	
	// МобильноеПриложение
	Если МобильноеПриложениеВызовСервера.НужноОграничитьФункциональность() Тогда
		Возврат;
	КонецЕсли;
	// Конец МобильноеПриложение

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Если временные таблицы "ДвиженияЗапасыИзменение", "ДвиженияЗапасыНаСкладахИзменение", 
	// "ДвиженияЗапасыКПоступлениюНаСкладыИзменение", "ДвиженияЗапасыПереданныеИзменение",
	// "ДвиженияЗапасыПринятыеИзменение", "ДвиженияРазмещениеЗаказовИзменение",
	// "ДвиженияПотребностьВЗапасахИзменение" содержат записи, необходимо выполнить контроль 
	// реализации товаров.
	Если СтруктураВременныеТаблицы.ДвиженияЗапасыИзменение
		 Или СтруктураВременныеТаблицы.ДвиженияЗапасыНаСкладахИзменение
		 Или СтруктураВременныеТаблицы.ДвиженияЗапасыКПоступлениюНаСкладыИзменение
		 Или СтруктураВременныеТаблицы.ДвиженияЗапасыПереданныеИзменение
		 Или СтруктураВременныеТаблицы.ДвиженияЗапасыПринятыеИзменение
		 Или СтруктураВременныеТаблицы.ДвиженияРазмещениеЗаказовИзменение
		 Или СтруктураВременныеТаблицы.ДвиженияЗаказыПокупателейИзменение
		 Или СтруктураВременныеТаблицы.ДвиженияЗаказыПоставщикамИзменение
		 Или СтруктураВременныеТаблицы.ДвиженияПотребностьВЗапасахИзменение
		// Для режима зачета аванса Авто тоже выполняем контроль, т.к. может быть более поздняя оплата.
		 Или СтруктураВременныеТаблицы.ДвиженияРасчетыСПоставщикамиИзменение
		 Или СтруктураВременныеТаблицы.ДвиженияСуммовойУчетВРозницеИзменение
		 Или СтруктураВременныеТаблицы.ДвиженияЗапасыВРазрезеГТДИзменение
		 Или СтруктураВременныеТаблицы.ДвиженияЗапасыПереданныеВРазрезеГТДИзменение
		 Или СтруктураВременныеТаблицы.ДвиженияПрослеживаемыеТоварыИзменение Тогда

		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияЗапасыНаСкладахИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗапасыНаСкладахИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.Партия КАК ПартияПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.Ячейка КАК ЯчейкаПредставление,
		|	ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
		|	ЗапасыНаСкладахОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыНаСкладахИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) + ЕСТЬNULL(РезервыТоваровОрганизацийОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыНаСкладах,
		|	ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) + ЕСТЬNULL(РезервыТоваровОрганизацийОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыНаСкладах
		|ИЗ
		|	ДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыНаСкладах.Остатки(
		|				&МоментКонтроля,
		|				(Организация, СтруктурнаяЕдиница, Номенклатура, Характеристика, Партия, Ячейка) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыНаСкладахИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|						ДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыНаСкладахИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыНаСкладахИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыНаСкладахИзменение.Ячейка КАК Ячейка
		|					ИЗ
		|						ДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение)) КАК ЗапасыНаСкладахОстатки
		|		ПО ДвиженияЗапасыНаСкладахИзменение.Организация = ЗапасыНаСкладахОстатки.Организация
		|			И ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница = ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница
		|			И ДвиженияЗапасыНаСкладахИзменение.Номенклатура = ЗапасыНаСкладахОстатки.Номенклатура
		|			И ДвиженияЗапасыНаСкладахИзменение.Характеристика = ЗапасыНаСкладахОстатки.Характеристика
		|			И ДвиженияЗапасыНаСкладахИзменение.Партия = ЗапасыНаСкладахОстатки.Партия
		|			И ДвиженияЗапасыНаСкладахИзменение.Ячейка = ЗапасыНаСкладахОстатки.Ячейка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
		|				&МоментКонтроля,
		|				(Организация, СтруктурнаяЕдиница, Номенклатура, Характеристика, Партия, Ячейка) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыНаСкладахИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|						ДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыНаСкладахИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыНаСкладахИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыНаСкладахИзменение.Ячейка КАК Ячейка
		|					ИЗ
		|						ДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение)) КАК РезервыТоваровОрганизацийОстатки
		|		ПО (&ПередачаТоваровМеждуОрганизациями)
		|			И ДвиженияЗапасыНаСкладахИзменение.Организация = РезервыТоваровОрганизацийОстатки.Организация
		|			И ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница = РезервыТоваровОрганизацийОстатки.СтруктурнаяЕдиница
		|			И ДвиженияЗапасыНаСкладахИзменение.Номенклатура = РезервыТоваровОрганизацийОстатки.Номенклатура
		|			И ДвиженияЗапасыНаСкладахИзменение.Характеристика = РезервыТоваровОрганизацийОстатки.Характеристика
		|			И ДвиженияЗапасыНаСкладахИзменение.Партия = РезервыТоваровОрганизацийОстатки.Партия
		|			И ДвиженияЗапасыНаСкладахИзменение.Ячейка = РезервыТоваровОрганизацийОстатки.Ячейка
		|ГДЕ
		|	ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) + ЕСТЬNULL(РезервыТоваровОрганизацийОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗапасыИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияЗапасыИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаПредставление,
		|	ДвиженияЗапасыИзменение.СчетУчета КАК СчетУчетаПредставление,
		|	ДвиженияЗапасыИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияЗапасыИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ДвиженияЗапасыИзменение.Партия КАК ПартияПредставление,
		|	ДвиженияЗапасыИзменение.ЗаказПокупателя КАК ЗаказПокупателяПредставление,
		|	ЗапасыОстатки.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
		|	ЗапасыОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) + ЕСТЬNULL(РезервыТоваровОрганизацийОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасы,
		|	ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) + ЕСТЬNULL(РезервыТоваровОрганизацийОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасы,
		|	ЕСТЬNULL(ЗапасыОстатки.СуммаОстаток, 0) КАК СуммаОстатокЗапасы
		|ИЗ
		|	ДвиженияЗапасыИзменение КАК ДвиженияЗапасыИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Запасы.Остатки(
		|				&МоментКонтроля,
		|				(Организация, СтруктурнаяЕдиница, СчетУчета, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|						ДвиженияЗапасыИзменение.СчетУчета КАК СчетУчета,
		|						ДвиженияЗапасыИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыИзменение.ЗаказПокупателя КАК ЗаказПокупателя
		|					ИЗ
		|						ДвиженияЗапасыИзменение КАК ДвиженияЗапасыИзменение)) КАК ЗапасыОстатки
		|		ПО ДвиженияЗапасыИзменение.Организация = ЗапасыОстатки.Организация
		|			И ДвиженияЗапасыИзменение.СтруктурнаяЕдиница = ЗапасыОстатки.СтруктурнаяЕдиница
		|			И ДвиженияЗапасыИзменение.СчетУчета = ЗапасыОстатки.СчетУчета
		|			И ДвиженияЗапасыИзменение.Номенклатура = ЗапасыОстатки.Номенклатура
		|			И ДвиженияЗапасыИзменение.Характеристика = ЗапасыОстатки.Характеристика
		|			И ДвиженияЗапасыИзменение.Партия = ЗапасыОстатки.Партия
		|			И ДвиженияЗапасыИзменение.ЗаказПокупателя = ЗапасыОстатки.ЗаказПокупателя
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
		|				&МоментКонтроля,
		|				(Организация, СтруктурнаяЕдиница, Номенклатура, Характеристика, Партия, Ячейка) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыНаСкладахИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|						ДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыНаСкладахИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыНаСкладахИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыНаСкладахИзменение.Ячейка КАК Ячейка
		|					ИЗ
		|						ДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение)) КАК РезервыТоваровОрганизацийОстатки
		|		ПО (&ПередачаТоваровМеждуОрганизациями)
		|			И ДвиженияЗапасыИзменение.Организация = РезервыТоваровОрганизацийОстатки.Организация
		|			И ДвиженияЗапасыИзменение.СтруктурнаяЕдиница = РезервыТоваровОрганизацийОстатки.СтруктурнаяЕдиница
		|			И ДвиженияЗапасыИзменение.Номенклатура = РезервыТоваровОрганизацийОстатки.Номенклатура
		|			И ДвиженияЗапасыИзменение.Характеристика = РезервыТоваровОрганизацийОстатки.Характеристика
		|			И ДвиженияЗапасыИзменение.Партия = РезервыТоваровОрганизацийОстатки.Партия
		|			И (ДвиженияЗапасыИзменение.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка))
		|ГДЕ
		|	ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) + ЕСТЬNULL(РезервыТоваровОрганизацийОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыПереданныеИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗапасыПереданныеИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияЗапасыПереданныеИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияЗапасыПереданныеИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ДвиженияЗапасыПереданныеИзменение.Партия КАК ПартияПредставление,
		|	ДвиженияЗапасыПереданныеИзменение.Контрагент КАК КонтрагентПредставление,
		|	ДвиженияЗапасыПереданныеИзменение.Договор КАК ДоговорПредставление,
		|	ДвиженияЗапасыПереданныеИзменение.Заказ КАК ЗаказПредставление,
		|	ДвиженияЗапасыПереданныеИзменение.ТипПриемаПередачи КАК ТипПриемаПередачиПредставление,
		|	ЗапасыПереданныеОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыПереданныеИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыПереданныеОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыПереданные,
		|	ЕСТЬNULL(ЗапасыПереданныеОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыПереданные,
		|	ЕСТЬNULL(ДвиженияЗапасыПереданныеИзменение.СуммаРасчетовИзменение, 0) + ЕСТЬNULL(ЗапасыПереданныеОстатки.СуммаРасчетовОстаток, 0) КАК СуммаРасчетовЗапасыПереданные,
		|	ЕСТЬNULL(ЗапасыПереданныеОстатки.СуммаРасчетовОстаток, 0) КАК СуммаРасчетовОстатокЗапасыПереданные
		|ИЗ
		|	ДвиженияЗапасыПереданныеИзменение КАК ДвиженияЗапасыПереданныеИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыПереданные.Остатки(&МоментКонтроля, ) КАК ЗапасыПереданныеОстатки
		|		ПО ДвиженияЗапасыПереданныеИзменение.Организация = ЗапасыПереданныеОстатки.Организация
		|			И ДвиженияЗапасыПереданныеИзменение.Номенклатура = ЗапасыПереданныеОстатки.Номенклатура
		|			И ДвиженияЗапасыПереданныеИзменение.Характеристика = ЗапасыПереданныеОстатки.Характеристика
		|			И ДвиженияЗапасыПереданныеИзменение.Партия = ЗапасыПереданныеОстатки.Партия
		|			И ДвиженияЗапасыПереданныеИзменение.Контрагент = ЗапасыПереданныеОстатки.Контрагент
		|			И ДвиженияЗапасыПереданныеИзменение.Договор = ЗапасыПереданныеОстатки.Договор
		|			И ДвиженияЗапасыПереданныеИзменение.Заказ = ЗапасыПереданныеОстатки.Заказ
		|			И ДвиженияЗапасыПереданныеИзменение.ТипПриемаПередачи = ЗапасыПереданныеОстатки.ТипПриемаПередачи
		|			И (ЕСТЬNULL(ЗапасыПереданныеОстатки.КоличествоОстаток, 0) < 0
		|				ИЛИ ЕСТЬNULL(ЗапасыПереданныеОстатки.СуммаРасчетовОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыПринятыеИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗапасыПринятыеИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияЗапасыПринятыеИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияЗапасыПринятыеИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ДвиженияЗапасыПринятыеИзменение.Партия КАК ПартияПредставление,
		|	ДвиженияЗапасыПринятыеИзменение.Контрагент КАК КонтрагентПредставление,
		|	ДвиженияЗапасыПринятыеИзменение.Договор КАК ДоговорПредставление,
		|	ДвиженияЗапасыПринятыеИзменение.Заказ КАК ЗаказПредставление,
		|	ДвиженияЗапасыПринятыеИзменение.ТипПриемаПередачи КАК ТипПриемаПередачиПредставление,
		|	ЗапасыПринятыеОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыПринятыеИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыПринятыеОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыПринятые,
		|	ЕСТЬNULL(ЗапасыПринятыеОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыПринятые,
		|	ЕСТЬNULL(ДвиженияЗапасыПринятыеИзменение.СуммаРасчетовИзменение, 0) + ЕСТЬNULL(ЗапасыПринятыеОстатки.СуммаРасчетовОстаток, 0) КАК СуммаРасчетовЗапасыПринятые,
		|	ЕСТЬNULL(ЗапасыПринятыеОстатки.СуммаРасчетовОстаток, 0) КАК СуммаРасчетовОстатокЗапасыПринятые
		|ИЗ
		|	ДвиженияЗапасыПринятыеИзменение КАК ДвиженияЗапасыПринятыеИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыИАгентскиеУслугиПринятые.Остатки(&МоментКонтроля, ) КАК ЗапасыПринятыеОстатки
		|		ПО ДвиженияЗапасыПринятыеИзменение.Организация = ЗапасыПринятыеОстатки.Организация
		|			И ДвиженияЗапасыПринятыеИзменение.Номенклатура = ЗапасыПринятыеОстатки.Номенклатура
		|			И ДвиженияЗапасыПринятыеИзменение.Характеристика = ЗапасыПринятыеОстатки.Характеристика
		|			И ДвиженияЗапасыПринятыеИзменение.Партия = ЗапасыПринятыеОстатки.Партия
		|			И ДвиженияЗапасыПринятыеИзменение.Контрагент = ЗапасыПринятыеОстатки.Контрагент
		|			И ДвиженияЗапасыПринятыеИзменение.Договор = ЗапасыПринятыеОстатки.Договор
		|			И ДвиженияЗапасыПринятыеИзменение.Заказ = ЗапасыПринятыеОстатки.Заказ
		|			И ДвиженияЗапасыПринятыеИзменение.ТипПриемаПередачи = ЗапасыПринятыеОстатки.ТипПриемаПередачи
		|			И (ЕСТЬNULL(ЗапасыПринятыеОстатки.КоличествоОстаток, 0) < 0
		|				ИЛИ ЕСТЬNULL(ЗапасыПринятыеОстатки.СуммаРасчетовОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗаказыПокупателейИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗаказыПокупателейИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияЗаказыПокупателейИзменение.Склад КАК СкладПредставление,
		|	ДвиженияЗаказыПокупателейИзменение.ЗаказПокупателя КАК ЗаказПредставление,
		|	ДвиженияЗаказыПокупателейИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияЗаказыПокупателейИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ЗаказыПокупателейОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗаказыПокупателейИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗаказыПокупателейОстатки.КоличествоОстаток, 0) КАК ОстатокЗаказыПокупателей,
		|	ЕСТЬNULL(ЗаказыПокупателейОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗаказыПокупателей
		|ИЗ
		|	ДвиженияЗаказыПокупателейИзменение КАК ДвиженияЗаказыПокупателейИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПокупателей.Остатки(&МоментКонтроля, ) КАК ЗаказыПокупателейОстатки
		|		ПО ДвиженияЗаказыПокупателейИзменение.Организация = ЗаказыПокупателейОстатки.Организация
		|			И ДвиженияЗаказыПокупателейИзменение.Склад = ЗаказыПокупателейОстатки.Склад
		|			И ДвиженияЗаказыПокупателейИзменение.ЗаказПокупателя = ЗаказыПокупателейОстатки.ЗаказПокупателя
		|			И ДвиженияЗаказыПокупателейИзменение.Номенклатура = ЗаказыПокупателейОстатки.Номенклатура
		|			И ДвиженияЗаказыПокупателейИзменение.Характеристика = ЗаказыПокупателейОстатки.Характеристика
		|			И (ЕСТЬNULL(ЗаказыПокупателейОстатки.КоличествоОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗаказыПоставщикамИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗаказыПоставщикамИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияЗаказыПоставщикамИзменение.Склад КАК СкладПредставление,
		|	ДвиженияЗаказыПоставщикамИзменение.ЗаказПоставщику КАК ЗаказПоставщикуПредставление,
		|	ДвиженияЗаказыПоставщикамИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияЗаказыПоставщикамИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ЗаказыПоставщикамОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗаказыПоставщикамИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗаказыПоставщикамОстатки.КоличествоОстаток, 0) КАК ОстатокЗаказыПоставщикам,
		|	ЕСТЬNULL(ЗаказыПоставщикамОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗаказыПоставщикам
		|ИЗ
		|	ДвиженияЗаказыПоставщикамИзменение КАК ДвиженияЗаказыПоставщикамИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПоставщикам.Остатки(&МоментКонтроля, ) КАК ЗаказыПоставщикамОстатки
		|		ПО ДвиженияЗаказыПоставщикамИзменение.Организация = ЗаказыПоставщикамОстатки.Организация
		|			И ДвиженияЗаказыПоставщикамИзменение.Склад = ЗаказыПоставщикамОстатки.Склад
		|			И ДвиженияЗаказыПоставщикамИзменение.ЗаказПоставщику = ЗаказыПоставщикамОстатки.ЗаказПоставщику
		|			И ДвиженияЗаказыПоставщикамИзменение.Номенклатура = ЗаказыПоставщикамОстатки.Номенклатура
		|			И ДвиженияЗаказыПоставщикамИзменение.Характеристика = ЗаказыПоставщикамОстатки.Характеристика
		|			И (ЕСТЬNULL(ЗаказыПоставщикамОстатки.КоличествоОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияПотребностьВЗапасахИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияПотребностьВЗапасахИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияПотребностьВЗапасахИзменение.ТипДвижения КАК ТипДвиженияПредставление,
		|	ДвиженияПотребностьВЗапасахИзменение.Склад КАК СкладПредставление,
		|	ДвиженияПотребностьВЗапасахИзменение.ЗаказПокупателя КАК ЗаказПокупателяПредставление,
		|	ДвиженияПотребностьВЗапасахИзменение.ЗаказНаПроизводство КАК ЗаказНаПроизводствоПредставление,
		|	ДвиженияПотребностьВЗапасахИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияПотребностьВЗапасахИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ПотребностьВЗапасахОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияПотребностьВЗапасахИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ПотребностьВЗапасахОстатки.КоличествоОстаток, 0) КАК ОстатокПотребностьВЗапасах,
		|	ЕСТЬNULL(ПотребностьВЗапасахОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокПотребностьВЗапасах
		|ИЗ
		|	ДвиженияПотребностьВЗапасахИзменение КАК ДвиженияПотребностьВЗапасахИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПотребностьВЗапасах.Остатки(&МоментКонтроля, ) КАК ПотребностьВЗапасахОстатки
		|		ПО ДвиженияПотребностьВЗапасахИзменение.Организация = ПотребностьВЗапасахОстатки.Организация
		|			И ДвиженияПотребностьВЗапасахИзменение.ТипДвижения = ПотребностьВЗапасахОстатки.ТипДвижения
		|			И ДвиженияПотребностьВЗапасахИзменение.Склад = ПотребностьВЗапасахОстатки.Склад
		|			И ДвиженияПотребностьВЗапасахИзменение.ЗаказПокупателя = ПотребностьВЗапасахОстатки.ЗаказПокупателя
		|			И ДвиженияПотребностьВЗапасахИзменение.ЗаказНаПроизводство = ПотребностьВЗапасахОстатки.ЗаказНаПроизводство
		|			И ДвиженияПотребностьВЗапасахИзменение.Номенклатура = ПотребностьВЗапасахОстатки.Номенклатура
		|			И ДвиженияПотребностьВЗапасахИзменение.Характеристика = ПотребностьВЗапасахОстатки.Характеристика
		|			И (ЕСТЬNULL(ПотребностьВЗапасахОстатки.КоличествоОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияРазмещениеЗаказовИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияРазмещениеЗаказовИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияРазмещениеЗаказовИзменение.ЗаказПокупателя КАК ЗаказПокупателяПредставление,
		|	ДвиженияРазмещениеЗаказовИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияРазмещениеЗаказовИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ДвиженияРазмещениеЗаказовИзменение.ИсточникОбеспечения КАК ИсточникОбеспеченияПредставление,
		|	РазмещениеЗаказовОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияРазмещениеЗаказовИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(РазмещениеЗаказовОстатки.КоличествоОстаток, 0) КАК ОстатокРазмещениеЗаказов,
		|	ЕСТЬNULL(РазмещениеЗаказовОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокРазмещениеЗаказов
		|ИЗ
		|	ДвиженияРазмещениеЗаказовИзменение КАК ДвиженияРазмещениеЗаказовИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РазмещениеЗаказов.Остатки(&МоментКонтроля, ) КАК РазмещениеЗаказовОстатки
		|		ПО ДвиженияРазмещениеЗаказовИзменение.Организация = РазмещениеЗаказовОстатки.Организация
		|			И ДвиженияРазмещениеЗаказовИзменение.ЗаказПокупателя = РазмещениеЗаказовОстатки.ЗаказПокупателя
		|			И ДвиженияРазмещениеЗаказовИзменение.Номенклатура = РазмещениеЗаказовОстатки.Номенклатура
		|			И ДвиженияРазмещениеЗаказовИзменение.Характеристика = РазмещениеЗаказовОстатки.Характеристика
		|			И ДвиженияРазмещениеЗаказовИзменение.ИсточникОбеспечения = РазмещениеЗаказовОстатки.ИсточникОбеспечения
		|			И (ЕСТЬNULL(РазмещениеЗаказовОстатки.КоличествоОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияРасчетыСПоставщикамиИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Контрагент КАК КонтрагентПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Договор КАК ДоговорПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Договор.ВалютаРасчетов КАК ВалютаПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Документ КАК ДокументПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Заказ КАК ЗаказПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов КАК ТипРасчетовПредставление,
		|	ЛОЖЬ КАК ДвиженияДенежныхДокументов,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалИзменение КАК СуммаВалИзменение,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалПриЗаписи - ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) КАК СуммаВыданныхАвансов,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалИзменение + ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) КАК СуммаНепогашеннойЗадолженности,
		|	ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
		|	ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов КАК ТипРасчетов
		|ИЗ
		|	ДвиженияРасчетыСПоставщикамиИзменение КАК ДвиженияРасчетыСПоставщикамиИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками.Остатки(&МоментКонтроля, ) КАК РасчетыСПоставщикамиОстатки
		|		ПО ДвиженияРасчетыСПоставщикамиИзменение.Организация = РасчетыСПоставщикамиОстатки.Организация
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Контрагент = РасчетыСПоставщикамиОстатки.Контрагент
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Договор = РасчетыСПоставщикамиОстатки.Договор
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Документ = РасчетыСПоставщикамиОстатки.Документ
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Заказ = РасчетыСПоставщикамиОстатки.Заказ
		|			И ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов = РасчетыСПоставщикамиОстатки.ТипРасчетов
		|			И (ВЫБОР
		|				КОГДА ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
		|					ТОГДА ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) > 0
		|				ИНАЧЕ ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) < 0
		|			КОНЕЦ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияСуммовойУчетВРозницеИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияСуммовойУчетВРозницеИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаПредставление,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СтруктурнаяЕдиница.РозничныйВидЦен.ВалютаЦены КАК ВалютаПредставление,
		|	ЕСТЬNULL(СуммовойУчетВРозницеОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаВалИзменение + ЕСТЬNULL(СуммовойУчетВРозницеОстатки.СуммаВалОстаток, 0) КАК ОстатокВРознице,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаВалИзменение КАК СуммаВалИзменение,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СебестоимостьПередЗаписью КАК СебестоимостьПередЗаписью,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СебестоимостьПриЗаписи КАК СебестоимостьПриЗаписи,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СебестоимостьИзменение КАК СебестоимостьИзменение
		|ИЗ
		|	ДвиженияСуммовойУчетВРозницеИзменение КАК ДвиженияСуммовойУчетВРозницеИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СуммовойУчетВРознице.Остатки(&МоментКонтроля, ) КАК СуммовойУчетВРозницеОстатки
		|		ПО ДвиженияСуммовойУчетВРозницеИзменение.Организация = СуммовойУчетВРозницеОстатки.Организация
		|			И ДвиженияСуммовойУчетВРозницеИзменение.СтруктурнаяЕдиница = СуммовойУчетВРозницеОстатки.СтруктурнаяЕдиница
		|			И (ЕСТЬNULL(СуммовойУчетВРозницеОстатки.СуммаВалОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыВРазрезеГТДИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД) КАК НомерГТДПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Партия) КАК ПартияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения) КАК СтранаПроисхожденияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗапасыВРазрезеГТДОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыВРазрезеГТДИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыВРазрезеГТД,
		|	ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыВРазрезеГТД
		|ИЗ
		|	ДвиженияЗапасыВРазрезеГТДИзменение КАК ДвиженияЗапасыВРазрезеГТДИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыВРазрезеГТД.Остатки(
		|				&МоментКонтроля,
		|				(Организация, НомерГТД, Номенклатура, Характеристика, Партия, СтранаПроисхождения) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД КАК НомерГТД,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения КАК СтранаПроисхождения
		|					ИЗ
		|						ДвиженияЗапасыВРазрезеГТДИзменение КАК ДвиженияЗапасыВРазрезеГТДИзменение)) КАК ЗапасыВРазрезеГТДОстатки
		|		ПО ДвиженияЗапасыВРазрезеГТДИзменение.Организация = ЗапасыВРазрезеГТДОстатки.Организация
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД = ЗапасыВРазрезеГТДОстатки.НомерГТД
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура = ЗапасыВРазрезеГТДОстатки.Номенклатура
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика = ЗапасыВРазрезеГТДОстатки.Характеристика
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Партия = ЗапасыВРазрезеГТДОстатки.Партия
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения = ЗапасыВРазрезеГТДОстатки.СтранаПроисхождения
		|ГДЕ
		|	ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.НомерГТД) КАК НомерГТДПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Партия) КАК ПартияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.СтранаПроисхождения) КАК СтранаПроисхожденияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗапасыПереданныеВРазрезеГТДОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыПереданныеВРазрезеГТДОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыВРазрезеГТД,
		|	ЕСТЬNULL(ЗапасыПереданныеВРазрезеГТДОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыВРазрезеГТД
		|ИЗ
		|	ДвиженияЗапасыПереданныеВРазрезеГТДИзменение КАК ДвиженияЗапасыПереданныеВРазрезеГТДИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыПереданныеВРазрезеГТД.Остатки(
		|				&МоментКонтроля,
		|				(Организация, НомерГТД, Номенклатура, Характеристика, Партия, СтранаПроисхождения) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД КАК НомерГТД,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения КАК СтранаПроисхождения
		|					ИЗ
		|						ДвиженияЗапасыВРазрезеГТДИзменение КАК ДвиженияЗапасыВРазрезеГТДИзменение)) КАК ЗапасыПереданныеВРазрезеГТДОстатки
		|		ПО ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Организация = ЗапасыПереданныеВРазрезеГТДОстатки.Организация
		|			И ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.НомерГТД = ЗапасыПереданныеВРазрезеГТДОстатки.НомерГТД
		|			И ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Номенклатура = ЗапасыПереданныеВРазрезеГТДОстатки.Номенклатура
		|			И ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Характеристика = ЗапасыПереданныеВРазрезеГТДОстатки.Характеристика
		|			И ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Партия = ЗапасыПереданныеВРазрезеГТДОстатки.Партия
		|			И ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.СтранаПроисхождения = ЗапасыПереданныеВРазрезеГТДОстатки.СтранаПроисхождения
		|ГДЕ
		|	ЕСТЬNULL(ЗапасыПереданныеВРазрезеГТДОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РезервыТоваровОрганизацийОстатки.Организация КАК Организация,
		|	РезервыТоваровОрганизацийОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	РезервыТоваровОрганизацийОстатки.Номенклатура КАК Номенклатура,
		|	РезервыТоваровОрганизацийОстатки.Характеристика КАК Характеристика,
		|	РезервыТоваровОрганизацийОстатки.Партия КАК Партия,
		|	РезервыТоваровОрганизацийОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
		|			&МоментКонтроля,
		|			&ПередачаТоваровМеждуОрганизациями
		|				И (Организация, СтруктурнаяЕдиница, Номенклатура, Характеристика, Партия, Ячейка) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыНаСкладахИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|						ДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыНаСкладахИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыНаСкладахИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыНаСкладахИзменение.Ячейка КАК Ячейка
		|					ИЗ
		|						ДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение)) КАК РезервыТоваровОрганизацийОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияПрослеживаемыеТоварыИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияПрослеживаемыеТоварыИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияПрослеживаемыеТоварыИзменение.РНПТ КАК РНПТПредставление,
		|	ДвиженияПрослеживаемыеТоварыИзменение.СтранаПроисхождения КАК СтранаПроисхожденияПредставление,
		|	ДвиженияПрослеживаемыеТоварыИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияПрослеживаемыеТоварыИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ДвиженияПрослеживаемыеТоварыИзменение.Партия КАК ПартияПредставление,
		|	ДвиженияПрослеживаемыеТоварыИзменение.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ДвиженияПрослеживаемыеТоварыИзменение.Номенклатура.ТоварнаяНоменклатураВЭД.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПрослеживаемостиПредставление,
		|	ЕСТЬNULL(ДвиженияПрослеживаемыеТоварыИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.КоличествоОстаток, 0) КАК ОстатокКоличество,
		|	ЕСТЬNULL(ДвиженияПрослеживаемыеТоварыИзменение.КоличествоПрослеживаемостиИзменение, 0) + ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.КоличествоПрослеживаемостиОстаток, 0) КАК ОстатокКоличествоПрослеживаемости,
		|	ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.КоличествоОстаток, 0) КАК ОстатокКоличествоТекущий,
		|	ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.КоличествоПрослеживаемостиОстаток, 0) КАК ОстатокКоличествоПрослеживаемостиТекущий
		|ИЗ
		|	ДвиженияПрослеживаемыеТоварыИзменение КАК ДвиженияПрослеживаемыеТоварыИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрослеживаемыеТовары.Остатки(&МоментКонтроля, ) КАК ПрослеживаемыеТоварыОстатки
		|		ПО ДвиженияПрослеживаемыеТоварыИзменение.Организация = ПрослеживаемыеТоварыОстатки.Организация
		|			И ДвиженияПрослеживаемыеТоварыИзменение.РНПТ = ПрослеживаемыеТоварыОстатки.РНПТ
		|			И ДвиженияПрослеживаемыеТоварыИзменение.СтранаПроисхождения = ПрослеживаемыеТоварыОстатки.СтранаПроисхождения
		|			И ДвиженияПрослеживаемыеТоварыИзменение.Номенклатура = ПрослеживаемыеТоварыОстатки.Номенклатура
		|			И ДвиженияПрослеживаемыеТоварыИзменение.Характеристика = ПрослеживаемыеТоварыОстатки.Характеристика
		|			И ДвиженияПрослеживаемыеТоварыИзменение.Партия = ПрослеживаемыеТоварыОстатки.Партия
		|			И (ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.КоличествоОстаток, 0) < 0
		|				ИЛИ ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.КоличествоПрослеживаемостиОстаток, 0) < 0)
		|			И ДвиженияПрослеживаемыеТоварыИзменение.Комиссионер = ПрослеживаемыеТоварыОстатки.Комиссионер
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки");

		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("МоментКонтроля", ДополнительныеСвойства.ДляПроведения.МоментКонтроля);

		// Интеркампани
		Запрос.УстановитьПараметр("ПередачаТоваровМеждуОрганизациями", ДополнительныеСвойства.УчетнаяПолитика.ПередачаТоваровМеждуОрганизациями);
		// Конец Интеркампани

		МассивРезультатов = Запрос.ВыполнитьПакет();

		Если Не МассивРезультатов[0].Пустой()
			 Или Не МассивРезультатов[1].Пустой()
			 Или Не МассивРезультатов[2].Пустой()
			 Или Не МассивРезультатов[3].Пустой()
			 Или Не МассивРезультатов[4].Пустой()
			 Или Не МассивРезультатов[5].Пустой()
			 Или Не МассивРезультатов[6].Пустой()
			 Или Не МассивРезультатов[7].Пустой()
			 Или Не МассивРезультатов[8].Пустой()
			 Или Не МассивРезультатов[9].Пустой()
			 Или Не МассивРезультатов[10].Пустой()
			 Или Не МассивРезультатов[11].Пустой()
			 Или Не МассивРезультатов[13].Пустой() Тогда

			ДокументОбъектПриходнаяНакладная = ДокументСсылкаПриходнаяНакладная.ПолучитьОбъект();

		КонецЕсли;
		
		// Отрицательный остаток запасов на складе.
		Если Не МассивРезультатов[0].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[0].Выбрать();
			КонтрольОстатковУНФ.ЗапасыНаСкладахСписком(
				ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток учета запасов.
		Если Не МассивРезультатов[1].Пустой() Тогда
			ОстаткиРезервов = МассивРезультатов[12].Выгрузить();
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[1].Выбрать();
			КонтрольОстатковУНФ.ЗапасыСписком(
				ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ, ОстаткиРезервов);
		КонецЕсли;
		
		// Отрицательный остаток запасов переданных.
		Если Не МассивРезультатов[2].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[2].Выбрать();
			КонтрольОстатковУНФ.ЗапасыПереданные(
				ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток запасов принятых.
		Если Не МассивРезультатов[3].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[3].Выбрать();
			КонтрольОстатковУНФ.ЗапасыПринятые(
				ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по заказу покупателя.
		Если Не МассивРезультатов[4].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[4].Выбрать();
			КонтрольОстатковУНФ.ЗаказыПокупателей(
				ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по заказу поставщику.
		Если Не МассивРезультатов[5].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[5].Выбрать();
			КонтрольОстатковУНФ.ЗаказыПоставщикам(
				ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, НСтр(
				"ru = 'Кликните правой кнопкой мыши в таблице ""Товары"" и выберите ""Распределить по количеству в заказе"" в контекстном меню.'"),
				Отказ);
		КонецЕсли;
		
		// Отрицательный остаток потребностей в запасах.
		Если Не МассивРезультатов[6].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[6].Выбрать();
			КонтрольОстатковУНФ.ПотребностьВЗапасах(
				ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток размещение заказов.
		Если Не МассивРезультатов[7].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[7].Выбрать();
			КонтрольОстатковУНФ.РазмещениеЗаказов(
				ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по расчетам с поставщиками.
		Если Не МассивРезультатов[8].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[8].Выбрать();
			КонтрольОстатковУНФ.РасчетыСПоставщиками(
				ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по суммовому учету в рознице.
		Если Не МассивРезультатов[9].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[9].Выбрать();
			КонтрольОстатковУНФ.СуммовойУчетВРознице(
				ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по остаткам запасов в разрезе номеров ГТД.
		Если Константы.КонтролироватьОстаткиПоНомерамГТД.Получить() Тогда

			Если Не МассивРезультатов[10].Пустой() Тогда

				ВыборкаИзРезультатаЗапроса = МассивРезультатов[10].Выбрать();
				КонтрольОстатковУНФ.ЗапасыВРазрезеГТД(
					ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);

			КонецЕсли;

			Если Не МассивРезультатов[11].Пустой() Тогда

				ВыборкаИзРезультатаЗапроса = МассивРезультатов[11].Выбрать();
				КонтрольОстатковУНФ.ЗапасыПереданныеВРазрезеГТД(
					ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);

			КонецЕсли;

		КонецЕсли;

		// Отрицательный остаток учета прослеживаемых товаров.
		Если Константы.ВестиУчетПрослеживаемыхТоваров.Получить()
			И НЕ МассивРезультатов[13].Пустой() Тогда
			
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[13].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструПрослеживаемыеТовары(
				ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
			
		КонецЕсли; 
		
	КонецЕсли;

КонецПроцедуры // ВыполнитьКонтроль()

// Делает записи в регистр сведений Цены номенклатуры контрагентов.
//
Процедура ЗарегистрироватьЦеныПоставщика(ДокументСсылкаПриходнаяНакладная, Отказ) Экспорт

	Если Не ДокументСсылкаПриходнаяНакладная.РегистрироватьЦеныПоставщика Или Отказ = Истина Тогда

		Возврат;

	КонецЕсли;

	Если ДокументСсылкаПриходнаяНакладная.Проведен Тогда

		УдалитьЦеныПоставщика(ДокументСсылкаПриходнаяНакладная);

	КонецЕсли;

	Если Не ЗначениеЗаполнено(ДокументСсылкаПриходнаяНакладная.ВидЦенКонтрагента) Тогда

		Возврат;

	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриходнаяНакладная);
	Запрос.УстановитьПараметр("ВалютаДокумента", ДокументСсылкаПриходнаяНакладная.ВалютаДокумента);
	Запрос.УстановитьПараметр("ДатаОбработки", ДокументСсылкаПриходнаяНакладная.Дата);
	
	// Две одинаковые позиции с разными ед. измерений приводят к ошибке записи регистра сведений
	// поэтому сразу выбираем уникальную номенклатуру + характеристику, а потом подтягиваем к строкам остальную информацию
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаЦены.Ссылка.Дата КАК Период,
	|	ТаблицаЦены.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ТаблицаЦены.Ссылка.ВидЦенКонтрагента КАК ВидЦенКонтрагента,
	|	МИНИМУМ(ТаблицаЦены.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаЦены.Номенклатура КАК Номенклатура,
	|	ТаблицаЦены.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ НоменклатураБезДублей
	|ИЗ
	|	Документ.ПриходнаяНакладная.Запасы КАК ТаблицаЦены
	|ГДЕ
	|	ТаблицаЦены.Ссылка = &Ссылка
	|	И ТаблицаЦены.Ссылка.РегистрироватьЦеныПоставщика
	|	И ТаблицаЦены.Цена <> 0
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦены.Ссылка.Дата,
	|	ТаблицаЦены.Ссылка.СуммаВключаетНДС,
	|	ТаблицаЦены.Ссылка.ВидЦенКонтрагента,
	|	ТаблицаЦены.Номенклатура,
	|	ТаблицаЦены.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураБезДублей.Период КАК Период,
	|	НоменклатураБезДублей.ВидЦенКонтрагента КАК ВидЦенКонтрагента,
	|	НоменклатураБезДублей.Номенклатура КАК Номенклатура,
	|	НоменклатураБезДублей.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА НоменклатураБезДублей.СуммаВключаетНДС = НоменклатураБезДублей.ВидЦенКонтрагента.ЦенаВключаетНДС
	|			ТОГДА ЕСТЬNULL(ТаблицаЦены.Цена * КурсВалютыДокумента.Курс * КурсВалютыВидЦен.Кратность / (КурсВалютыВидЦен.Курс *
	|				КурсВалютыДокумента.Кратность), 0)
	|		КОГДА НоменклатураБезДублей.СуммаВключаетНДС > ТаблицаЦены.Ссылка.ВидЦенКонтрагента.ЦенаВключаетНДС
	|			ТОГДА ЕСТЬNULL(ТаблицаЦены.Цена * КурсВалютыДокумента.Курс * КурсВалютыВидЦен.Кратность / (КурсВалютыВидЦен.Курс *
	|				КурсВалютыДокумента.Кратность) * 100 / (100 + ТаблицаЦены.СтавкаНДС.Ставка), 0)
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаЦены.Цена * КурсВалютыДокумента.Курс * КурсВалютыВидЦен.Кратность / (КурсВалютыВидЦен.Курс *
	|			КурсВалютыДокумента.Кратность) * (100 + ТаблицаЦены.СтавкаНДС.Ставка) / 100, 0)
	|	КОНЕЦ КАК Цена,
	|	ТаблицаЦены.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ИСТИНА КАК Актуальность,
	|	ТаблицаЦены.Ссылка КАК ДокументРегистратор,
	|	ТаблицаЦены.Ссылка.Автор КАК Автор,
	|	ИСТИНА КАК ЭтоЗапас
	|ПОМЕСТИТЬ ЦеныНоменклатурыТипаЗапас
	|ИЗ
	|	НоменклатураБезДублей КАК НоменклатураБезДублей
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходнаяНакладная.Запасы КАК ТаблицаЦены
	|		ПО НоменклатураБезДублей.НомерСтроки = ТаблицаЦены.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатурыКонтрагентов КАК ЦеныНоменклатурыКонтрагентов
	|		ПО ТаблицаЦены.Ссылка.ВидЦенКонтрагента = ЦеныНоменклатурыКонтрагентов.ВидЦенКонтрагента
	|		И ТаблицаЦены.Номенклатура = ЦеныНоменклатурыКонтрагентов.Номенклатура
	|		И ТаблицаЦены.Характеристика = ЦеныНоменклатурыКонтрагентов.Характеристика
	|		И НАЧАЛОПЕРИОДА(ТаблицаЦены.Ссылка.Дата, ДЕНЬ) = ЦеныНоменклатурыКонтрагентов.Период
	|		И ТаблицаЦены.Ссылка.Дата <= ЦеныНоменклатурыКонтрагентов.ДокументРегистратор.Дата
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОбработки,) КАК КурсВалютыВидЦен
	|		ПО ТаблицаЦены.Ссылка.ВидЦенКонтрагента.ВалютаЦены = КурсВалютыВидЦен.Валюта,
	|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОбработки, Валюта = &ВалютаДокумента) КАК КурсВалютыДокумента
	|ГДЕ
	|	ТаблицаЦены.Ссылка.РегистрироватьЦеныПоставщика
	|	И ЦеныНоменклатурыКонтрагентов.ВидЦенКонтрагента ЕСТЬ NULL
	|	И ТаблицаЦены.Ссылка = &Ссылка
	|	И ТаблицаЦены.Цена <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаРасходов.Ссылка.Дата КАК Период,
	|	ТаблицаРасходов.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ТаблицаРасходов.Ссылка.ВидЦенКонтрагента КАК ВидЦенКонтрагента,
	|	МИНИМУМ(ТаблицаРасходов.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаРасходов.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика
	|ПОМЕСТИТЬ НоменклатураУслугБезДублей
	|ИЗ
	|	Документ.ПриходнаяНакладная.Расходы КАК ТаблицаРасходов
	|ГДЕ
	|	ТаблицаРасходов.Ссылка = &Ссылка
	|	И ТаблицаРасходов.Ссылка.РегистрироватьЦеныПоставщика
	|	И ТаблицаРасходов.Цена <> 0
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасходов.Ссылка.Дата,
	|	ТаблицаРасходов.Ссылка.СуммаВключаетНДС,
	|	ТаблицаРасходов.Ссылка.ВидЦенКонтрагента,
	|	ТаблицаРасходов.Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураУслугБезДублей.Период КАК Период,
	|	НоменклатураУслугБезДублей.ВидЦенКонтрагента КАК ВидЦенКонтрагента,
	|	НоменклатураУслугБезДублей.Номенклатура КАК Номенклатура,
	|	НоменклатураУслугБезДублей.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА НоменклатураУслугБезДублей.СуммаВключаетНДС = НоменклатураУслугБезДублей.ВидЦенКонтрагента.ЦенаВключаетНДС
	|			ТОГДА ЕСТЬNULL(ТаблицаРасходов.Цена * КурсВалютыДокумента.Курс * КурсВалютыВидЦен.Кратность /
	|				(КурсВалютыВидЦен.Курс * КурсВалютыДокумента.Кратность), 0)
	|		КОГДА НоменклатураУслугБезДублей.СуммаВключаетНДС > ТаблицаРасходов.Ссылка.ВидЦенКонтрагента.ЦенаВключаетНДС
	|			ТОГДА ЕСТЬNULL(ТаблицаРасходов.Цена * КурсВалютыДокумента.Курс * КурсВалютыВидЦен.Кратность /
	|				(КурсВалютыВидЦен.Курс * КурсВалютыДокумента.Кратность) * 100 / (100 + ТаблицаРасходов.СтавкаНДС.Ставка), 0)
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаРасходов.Цена * КурсВалютыДокумента.Курс * КурсВалютыВидЦен.Кратность / (КурсВалютыВидЦен.Курс
	|			* КурсВалютыДокумента.Кратность) * (100 + ТаблицаРасходов.СтавкаНДС.Ставка) / 100, 0)
	|	КОНЕЦ КАК Цена,
	|	ТаблицаРасходов.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ИСТИНА КАК Актуальность,
	|	ТаблицаРасходов.Ссылка КАК ДокументРегистратор,
	|	ТаблицаРасходов.Ссылка.Автор КАК Автор,
	|	ЛОЖЬ КАК ЭтоЗапас
	|ПОМЕСТИТЬ ЦеныНоменклатурыТипаРасход
	|ИЗ
	|	НоменклатураУслугБезДублей КАК НоменклатураУслугБезДублей
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходнаяНакладная.Расходы КАК ТаблицаРасходов
	|		ПО НоменклатураУслугБезДублей.НомерСтроки = ТаблицаРасходов.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатурыКонтрагентов КАК ЦеныНоменклатурыКонтрагентов
	|		ПО ТаблицаРасходов.Ссылка.ВидЦенКонтрагента = ЦеныНоменклатурыКонтрагентов.ВидЦенКонтрагента
	|		И ТаблицаРасходов.Номенклатура = ЦеныНоменклатурыКонтрагентов.Номенклатура
	|		И ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) = ЦеныНоменклатурыКонтрагентов.Характеристика
	|		И НАЧАЛОПЕРИОДА(ТаблицаРасходов.Ссылка.Дата, ДЕНЬ) = ЦеныНоменклатурыКонтрагентов.Период
	|		И ТаблицаРасходов.Ссылка.Дата <= ЦеныНоменклатурыКонтрагентов.ДокументРегистратор.Дата
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОбработки,) КАК КурсВалютыВидЦен
	|		ПО ТаблицаРасходов.Ссылка.ВидЦенКонтрагента.ВалютаЦены = КурсВалютыВидЦен.Валюта,
	|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОбработки, Валюта = &ВалютаДокумента) КАК КурсВалютыДокумента
	|ГДЕ
	|	ТаблицаРасходов.Ссылка.РегистрироватьЦеныПоставщика
	|	И ЦеныНоменклатурыКонтрагентов.ВидЦенКонтрагента ЕСТЬ NULL
	|	И ТаблицаРасходов.Ссылка = &Ссылка
	|	И ТаблицаРасходов.Цена <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	ЦеныНоменклатурыТипаЗапас
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	ЦеныНоменклатурыТипаРасход";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Запись набора РС
	НаборЗаписейРегистра = РегистрыСведений.ЦеныНоменклатурыКонтрагентов.СоздатьНаборЗаписей();
	НаборЗаписейРегистра.Отбор.Период.Установить(ДокументСсылкаПриходнаяНакладная.Дата);
	НаборЗаписейРегистра.Отбор.ВидЦенКонтрагента.Установить(ДокументСсылкаПриходнаяНакладная.ВидЦенКонтрагента);
	НаборЗаписейРегистра.Прочитать();
	
	ТаблицаНовыеЦены	= РезультатЗапроса.Выгрузить();
	ТаблицаТекущиеЦены	= НаборЗаписейРегистра.Выгрузить();
	ЦенообразованиеСервер.ОтразитьЦеныКонтрагентов(ТаблицаНовыеЦены, ТаблицаТекущиеЦены, НаборЗаписейРегистра);
	
КонецПроцедуры // ЗарегистрироватьЦеныПоставщика()

// Удаляет записи из регистра сведений Цены номенклатуры контрагентов.
//
Процедура УдалитьЦеныПоставщика(ДокументСсылкаПриходнаяНакладная) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦеныНоменклатурыКонтрагентов.Период,
	|	ЦеныНоменклатурыКонтрагентов.ВидЦенКонтрагента,
	|	ЦеныНоменклатурыКонтрагентов.Номенклатура,
	|	ЦеныНоменклатурыКонтрагентов.Характеристика
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатурыКонтрагентов КАК ЦеныНоменклатурыКонтрагентов
	|ГДЕ
	|	ЦеныНоменклатурыКонтрагентов.ДокументРегистратор = &ДокументРегистратор";
	
	Запрос.УстановитьПараметр("ДокументРегистратор", ДокументСсылкаПриходнаяНакладная);
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЗаписей = РезультатЗапроса.Выгрузить();	
	
	Для каждого СтрокаТаблицы Из ТаблицаЗаписей Цикл
		НаборЗаписей = РегистрыСведений.ЦеныНоменклатурыКонтрагентов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(СтрокаТаблицы.Период);
		НаборЗаписей.Отбор.ВидЦенКонтрагента.Установить(СтрокаТаблицы.ВидЦенКонтрагента);
		НаборЗаписей.Отбор.Номенклатура.Установить(СтрокаТаблицы.Номенклатура);
		НаборЗаписей.Отбор.Характеристика.Установить(СтрокаТаблицы.Характеристика);
		НаборЗаписей.Записать();
	КонецЦикла;	

КонецПроцедуры // УдалитьЦеныПоставщика()

Функция СообщениеКПередачеXML(ДокументСсылка, Операция) Экспорт
	
	Если Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанных Тогда
		Возврат ВозвратТоваровОтКлиентаXML(ДокументСсылка);
	ИначеЕсли Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанныхПолучениеКвитанции Тогда
		Возврат ИнтеграцияГИСМВызовСервера.ЗапросКвитанцииОФиксацииПоСсылкеXML(ДокументСсылка,
			Перечисления.ОперацииОбменаГИСМ.ПередачаДанных);
	КонецЕсли;
	
КонецФункции

Функция ВозвратТоваровОтКлиентаXML(ДокументСсылка) Экспорт
	
	СообщенияXML = Новый Массив;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ГИСМПрисоединенныеФайлы.ВладелецФайла КАК Ссылка,
	|	КОЛИЧЕСТВО(ГИСМПрисоединенныеФайлы.Ссылка) КАК ПоследнийНомерВерсии
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	Справочник.ГИСМПрисоединенныеФайлы КАК ГИСМПрисоединенныеФайлы
	|ГДЕ
	|	ГИСМПрисоединенныеФайлы.ВладелецФайла = &Ссылка
	|	И ГИСМПрисоединенныеФайлы.Операция = ЗНАЧЕНИЕ(Перечисление.ОперацииОбменаГИСМ.ПередачаДанных)
	|	И ГИСМПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыСообщенийГИСМ.Исходящее)
	|СГРУППИРОВАТЬ ПО
	|	ГИСМПрисоединенныеФайлы.ВладелецФайла
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровОтКлиента.Дата                       КАК Дата,
	|	ВозвратТоваровОтКлиента.Номер                      КАК Номер,
	|	ЕСТЬNULL(ВременнаяТаблица.ПоследнийНомерВерсии, 0) КАК ПоследнийНомерВерсии,
	|	ВозвратТоваровОтКлиента.Организация                КАК Организация,
	|	ВозвратТоваровОтКлиента.Подразделение              КАК Подразделение
	|ИЗ
	|	Документ.ПриходнаяНакладная КАК ВозвратТоваровОтКлиента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблица КАК ВременнаяТаблица
	|		ПО ВозвратТоваровОтКлиента.Ссылка = ВременнаяТаблица.Ссылка
	|ГДЕ
	|	ВозвратТоваровОтКлиента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриходнаяНакладнаяЗапасы.НомерСтроки        КАК НомерСтроки,
	|	ПриходнаяНакладнаяЗапасы.Номенклатура       КАК Номенклатура,
	|	ПриходнаяНакладнаяЗапасы.Характеристика     КАК Характеристика,
	|	Серии.Серия.НомерКиЗГИСМ КАК НомерКиЗ,
	|	Серии.Серия.RFIDTID      КАК TID,
	|	Серии.Серия.RFIDEPC      КАК EPC
	|ИЗ
	|	Документ.ПриходнаяНакладная.СерииНоменклатуры КАК Серии
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриходнаяНакладная.Запасы КАК ПриходнаяНакладнаяЗапасы
	|		ПО Серии.КлючСвязи = ПриходнаяНакладнаяЗапасы.КлючСвязи
	|			И  ПриходнаяНакладнаяЗапасы.Ссылка = Серии.Ссылка
	|ГДЕ
	|	Серии.Ссылка = &Ссылка
	|	И ПриходнаяНакладнаяЗапасы.Номенклатура.ВидМаркировки <> ЗНАЧЕНИЕ(Перечисление.ВидыМаркировки.НеМаркируется)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Серии.НомерСтроки");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Результат = Запрос.ВыполнитьПакет();
	Шапка = Результат[1].Выбрать();
	Товары = Результат[2].Выгрузить();
	Если Не Шапка.Следующий()
		Или Товары.Количество() = 0 Тогда
		
		СообщениеXML = ИнтеграцияГИСМКлиентСервер.СтруктураСообщенияXML();
		СообщениеXML.Документ = ДокументСсылка;
		СообщениеXML.Описание = ИнтеграцияГИСМ.ОписаниеОперацииПередачиДанных(
			Перечисления.ОперацииОбменаГИСМ.ПередачаДанныхВозвратОтРозничногоПокупателя, ДокументСсылка);
		СообщениеXML.ТекстОшибки = НСтр("ru = 'Нет данных для выгрузки.'");
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	
	РеквизитыОрганизации = ИнтеграцияГИСМВызовСервера.ИННКППGLNОрганизации(Шапка.Организация, Шапка.Подразделение);
	
	СообщениеXML = ИнтеграцияГИСМКлиентСервер.СтруктураСообщенияXML();
	СообщениеXML.Описание = ИнтеграцияГИСМ.ОписаниеОперацииПередачиДанных(
		Перечисления.ОперацииОбменаГИСМ.ПередачаДанныхВозвратОтРозничногоПокупателя, ДокументСсылка, НомерВерсии);
	
	ИмяТипа   = "query";
	ИмяПакета = "return_signs";
	
	ПередачаДанных = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(Неопределено, ИмяТипа);
	
	ВозвратТоваров = ИнтеграцияГИСМ.ОбъектXDTO(ИмяПакета);
	ВозвратТоваров.action_id  = ВозвратТоваров.action_id;
	
	Попытка
		ВозвратТоваров.sender_gln = РеквизитыОрганизации.GLN;
	Исключение
		ИнтеграцияГИСМКлиентСервер.ДобавитьТекстОшибкиНеЗаполненGLNОрганизации(СообщениеXML, РеквизитыОрганизации.GLN,
			Шапка);
	КонецПопытки;
	
	ВозвратТоваров.return_doc_num  = Шапка.Номер;
	ВозвратТоваров.return_doc_date = Шапка.Дата;
	
	ХранилищеВременныхДат = Новый Соответствие;
	ИнтеграцияГИСМ.УстановитьДатуСЧасовымПоясом(
		ВозвратТоваров,
		"return_date",
		Шапка.Дата,
		ХранилищеВременныхДат);
	
	ВозвратТоваров.returns = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(ВозвратТоваров, "returns");
	ВозвратТоваров.returns.signs = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(ВозвратТоваров.returns, "signs");
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		
		НоваяСтрока = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(ВозвратТоваров.returns.signs, "sign");
		
		Если ЗначениеЗаполнено(СтрокаТЧ.НомерКиЗ) Тогда
			
			НоваяСтрока.sign_num = СтрокаТЧ.НомерКиЗ;
			
		ИначеЕсли ЗначениеЗаполнено(СтрокаТЧ.TID) Тогда
			
			НоваяСтрока.sign_tid = СтрокаТЧ.TID;
			
		ИначеЕсли ЗначениеЗаполнено(СтрокаТЧ.EPC) Тогда
			
			Попытка
				НоваяСтрока.sign_sgtin = МенеджерОборудованияКлиентСервер.ПреобразоватьHEXВБинарнуюСтроку(СтрокаТЧ.EPC);
			Исключение
				ПредставлениеНоменклатуры = Справочники.Номенклатура.Представление(СтрокаТЧ.Номенклатура, СтрокаТЧ.Характеристика);
				ИнтеграцияГИСМКлиентСервер.ДобавитьТекстОшибки(
					СообщениеXML, СтрШаблон(НСтр("ru = 'Для номенклатуры %1 указан некорректный EPC ""%2"".'"),
					ПредставлениеНоменклатуры, СтрокаТЧ.EPC));
			КонецПопытки;
			
		КонецЕсли;
		
		ВозвратТоваров.returns.signs.sign.Добавить(НоваяСтрока);
		
	КонецЦикла;
	
	ПередачаДанных.version    = ПередачаДанных.version;
	ПередачаДанных[ИмяПакета] = ВозвратТоваров;
	
	ТекстСообщенияXML = ИнтеграцияГИСМ.ОбъектXDTOВXML(ПередачаДанных, ИмяТипа);
	ТекстСообщенияXML = ИнтеграцияГИСМ.ПреобразоватьВременныеДаты(ХранилищеВременныхДат, ТекстСообщенияXML);
	
	СообщениеXML.ТекстСообщенияXML  = ТекстСообщенияXML;
	СообщениеXML.КонвертSOAP = ИнтеграцияГИСМВызовСервера.ПоместитьТекстСообщенияXMLВКонвертSOAP(ТекстСообщенияXML);
	
	СообщениеXML.ТипСообщения = Перечисления.ТипыСообщенийГИСМ.Исходящее;
	СообщениеXML.Организация  = Шапка.Организация;
	СообщениеXML.Операция     = Перечисления.ОперацииОбменаГИСМ.ПередачаДанных;
	СообщениеXML.Документ     = ДокументСсылка;
	СообщениеXML.Версия       = НомерВерсии;
	
	СообщенияXML.Добавить(СообщениеXML);
	
	Возврат СообщенияXML;
	
КонецФункции

Функция ДопустимыеТипыЗаказа(ВидОперации) Экспорт
	
	Если ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию Тогда

		ДопустимыеТипы = Новый ОписаниеТипов("ДокументСсылка.ЗаказПоставщику");

	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПриемВПереработку
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера Тогда

		ДопустимыеТипы = Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя");

	Иначе

		МассивОтбора = Новый Массив;
		МассивОтбора.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));
		МассивОтбора.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
		ДопустимыеТипы = Новый ОписаниеТипов(МассивОтбора);

	КонецЕсли;
	
	Возврат ДопустимыеТипы;
	
КонецФункции

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ, ДоговорДляОтложенногоПроведения = Неопределено) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриходнаяНакладная.Ссылка КАК Ссылка,
	|	ПриходнаяНакладная.Дата КАК Период,
	|	ПриходнаяНакладная.Организация КАК Организация,
	|	ПриходнаяНакладная.СтруктурнаяЕдиница КАК Склад
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.ПриходнаяНакладная КАК ПриходнаяНакладная
	|ГДЕ
	|	ПриходнаяНакладная.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Ссылка КАК Основание,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Склад КАК Склад
	|ИЗ
	|	Реквизиты КАК Реквизиты";
	
	Результат = Запрос.Выполнить();
	
	Реквизиты = ПрослеживаемостьВызовСервераПереопределяемый.ПолучитьСтруктуруИзРезультатаЗапроса(Результат);
	
	Если НЕ ПрослеживаемостьПереопределяемый.УчетнаяПолитикаСуществует(
		Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	НомераТаблиц = Новый Структура;

	Запрос.Текст = Документы.ИнвентаризацияЗапасов.ТекстЗапросаТаблицыДокумента(НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения; 
	
КонецФункции 


//Формирует текст запроса для определения прослеживаемого товара в документе
//
// Параметры:
// ДокументОснование - ДокументСсылка - Документ приходная накладная
// ДополнительныйРеквизит - Булево - Дополнительный реквизит. не используется в этом модуле
//
// Возвращаемые параметры:
// Строка - Текст сформированного запроса
//
Функция ТекстЗапросаТаблицаПрослеживаемыхТоваровПоОснованию() Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ПриходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
	|	СУММА(ПриходнаяНакладнаяЗапасы.Количество) КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА ПриходнаяНакладная.СуммаВключаетНДС
	|				ТОГДА ПриходнаяНакладнаяЗапасы.Сумма - ПриходнаяНакладнаяЗапасы.СуммаНДС
	|			ИНАЧЕ ПриходнаяНакладнаяЗапасы.Сумма
	|		КОНЕЦ) КАК Сумма,
	|	ПриходнаяНакладная.Организация КАК Организация,
	|	ПриходнаяНакладная.Дата КАК ПериодСобытия,
	|	ПриходнаяНакладнаяЗапасы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ПриходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
	|	ПриходнаяНакладнаяЗапасы.Партия КАК Партия
	|ПОМЕСТИТЬ ВТ_ТаблицаТоваров
	|ИЗ
	|	Документ.ПриходнаяНакладная.Запасы КАК ПриходнаяНакладнаяЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходнаяНакладная КАК ПриходнаяНакладная
	|		ПО ПриходнаяНакладнаяЗапасы.Ссылка = ПриходнаяНакладная.Ссылка
	|ГДЕ
	|	ПриходнаяНакладнаяЗапасы.Ссылка = &Основание
	|	И ПриходнаяНакладнаяЗапасы.ПрослеживаемыйТовар
	|	И ПриходнаяНакладная.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриходнаяНакладнаяЗапасы.Номенклатура,
	|	ПриходнаяНакладная.Организация,
	|	ПриходнаяНакладная.Дата,
	|	ПриходнаяНакладнаяЗапасы.СтранаПроисхождения,
	|	ПриходнаяНакладнаяЗапасы.Характеристика,
	|	ПриходнаяНакладнаяЗапасы.Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураСправочник.ТоварнаяНоменклатураВЭД КАК ТНВЭД,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Количество КАК Количество,
	|	ТаблицаТоваров.Сумма КАК Сумма,
	|	ТаблицаТоваров.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.ВвозТоваровИзЕАЭС) КАК ВидОперации,
	|	ТаблицаТоваров.ПериодСобытия КАК ПериодСобытия,
	|	ТаблицаТоваров.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Партия КАК Партия
	|ПОМЕСТИТЬ ВТ_ТоварыПоОснованию
	|ИЗ
	|	ВТ_ТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО ТаблицаТоваров.Номенклатура = НоменклатураСправочник.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаТоваров.Количество КАК Количество,
	|	ВТ_ТаблицаТоваров.Сумма КАК Сумма,
	|	ВТ_ТаблицаТоваров.Организация КАК Организация,
	|	ВТ_ТаблицаТоваров.ПериодСобытия КАК ПериодСобытия,
	|	ЛОЖЬ КАК РанееРегистрировалсяВПрослеживаемости,
	|	0 КАК КоличествоУчет,
	|	ВТ_ТаблицаТоваров.ТНВЭД КАК ТНВЭД,
	|	ВТ_ТаблицаТоваров.ВидОперации КАК ВидОперации,
	|	ВТ_ТаблицаТоваров.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВТ_ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ВТ_ТаблицаТоваров.Партия КАК Партия
	|ПОМЕСТИТЬ ВТ_ТаблицаТоваровРазмеченный
	|ИЗ
	|	ВТ_ТоварыПоОснованию КАК ВТ_ТаблицаТоваров";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#Область ДисконтныеКарты

// Формирует таблицу значений, содержащую данные для проведения по регистру ПродажиПоДисконтнымКартам.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПродажиПоДисконтнойКарте(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)

	Если ДокументСсылкаПриходнаяНакладная.ВидОперации <> Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПродажиПоДисконтнымКартам",
			Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли;
	Если ДокументСсылкаПриходнаяНакладная.ДисконтнаяКарта.Пустая() Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПродажиПоДисконтнымКартам",
			Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;

	Запрос.Текст = ДисконтныеКартыУНФСервер.ТекстЗапросаПродажиПоДисконтнойКарте("ПриходнаяНакладная");
	Запрос.УстановитьПараметр("ПроцентСкидки", ДисконтныеКартыУНФСервер.ВычислитьПроцентСкидкиПоДисконтнойКарте(
		СтруктураДополнительныеСвойства.ДляПроведения.Дата, ДокументСсылкаПриходнаяНакладная.ДисконтнаяКарта));

	РезультатЗапроса = Запрос.Выполнить();

	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПродажиПоДисконтнымКартам",
		РезультатЗапроса.Выгрузить());

КонецПроцедуры // СформироватьТаблицаПродажиПоДисконтнойКарте()

#КонецОбласти

#Область РаботаССериямиНоменклатуры

// Формирует таблицу значений, содержащую данные для проведения по регистру СерииНоменклатурыГарантии.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСерииНоменклатуры(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Если ДокументСсылка.СерииНоменклатуры.Количество() = 0 Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерииНоменклатуры", Новый ТаблицаЗначений);
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерииНоменклатурыГарантии", Новый ТаблицаЗначений);
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДвиженияСерииНоменклатуры", Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаЗапасы.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|		ИЛИ ВременнаяТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера)
	|		ИЛИ
	|			ВременнаяТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика)
	|		ИЛИ ВременнаяТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ОперацииСерийНоменклатуры.Возврат)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОперацииСерийНоменклатуры.Приход)
	|	КОНЕЦ КАК Операция,
	|	ВременнаяТаблицаЗапасы.Период КАК ДатаСобытия,
	|	СерииНоменклатуры.Серия КАК Серия,
	|	СерииНоменклатуры.ОстаткиСерийНоменклатуры КАК ОстаткиСерийНоменклатуры,
	|	ВременнаяТаблицаЗапасы.Организация КАК Организация,
	|	ВременнаяТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ВременнаяТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ВременнаяТаблицаЗапасы.Партия КАК Партия,
	|	ВременнаяТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВременнаяТаблицаЗапасы.Ячейка КАК Ячейка,
	|	СерииНоменклатуры.Количество КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ВременнаяТаблицаЗапасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ВременнаяТаблицаЗапасы.КлючСвязи = СерииНоменклатуры.КлючСвязи
	|ГДЕ
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаЗапасы.ПоложениеСклада = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|			ТОГДА НЕ ВременнаяТаблицаЗапасы.ОрдерныйСклад
	|		ИНАЧЕ
	|		НЕ ВременнаяТаблицаЗапасы.СтруктурнаяЕдиницаШапкаОрдерныйСклад
	|	КОНЕЦ";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Если Не СтруктураДополнительныеСвойства.УчетнаяПолитика.МиграцияСерийНоменклатурыВыполнена Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерииНоменклатурыГарантии", РезультатЗапроса);
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДвиженияСерииНоменклатуры", Новый ТаблицаЗначений);
	Иначе
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДвиженияСерииНоменклатуры", РезультатЗапроса);
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерииНоменклатурыГарантии", Новый ТаблицаЗначений);
	КонецЕсли;

	ПараметрыОтбора = Новый Структура("ОстаткиСерийНоменклатуры", Истина);
	ОстаткиСерийНоменклатурыСтроки = РезультатЗапроса.НайтиСтроки(ПараметрыОтбора);
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерииНоменклатуры", РезультатЗапроса.Скопировать(ОстаткиСерийНоменклатурыСтроки));
	
КонецПроцедуры // СформироватьТаблицаСерииНоменклатуры()

// Формирует таблицу значений, содержащую данные для проведения по регистру СерииНоменклатурыКПоступлению.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСерииНоменклатурыКПоступлению(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Если ДокументСсылка.СерииНоменклатуры.Количество() = 0 Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерииНоменклатурыКПоступлению", Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаЗапасы.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	СерииНоменклатуры.Серия КАК Серия,
	|	СерииНоменклатуры.ОстаткиСерийНоменклатуры КАК ОстаткиСерийНоменклатуры,
	|	ВременнаяТаблицаЗапасы.Организация КАК Организация,
	|	ВременнаяТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ВременнаяТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ВременнаяТаблицаЗапасы.Партия КАК Партия,
	|	ВременнаяТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	СерииНоменклатуры.Количество КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ВременнаяТаблицаЗапасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ВременнаяТаблицаЗапасы.КлючСвязи = СерииНоменклатуры.КлючСвязи
	|ГДЕ
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаЗапасы.ПоложениеСклада = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|			ТОГДА ВременнаяТаблицаЗапасы.ОрдерныйСклад
	|		ИНАЧЕ ВременнаяТаблицаЗапасы.СтруктурнаяЕдиницаШапкаОрдерныйСклад
	|	КОНЕЦ";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	ПараметрыОтбора = Новый Структура("ОстаткиСерийНоменклатуры", Истина);
	ОстаткиСерийНоменклатурыСтроки = РезультатЗапроса.НайтиСтроки(ПараметрыОтбора);
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерииНоменклатурыКПоступлению", РезультатЗапроса.Скопировать(ОстаткиСерийНоменклатурыСтроки));
	
КонецПроцедуры


#КонецОбласти

#Область ДействияПриОбменеГИСМ

Функция ОбновитьСтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция) Экспорт
	
	НовыйСтатус        = Неопределено;
	ДальнейшееДействие = Неопределено;
	
	ИспользоватьАвтоматическийОбмен = ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическуюОтправкуПолучениеДанныхГИСМ");
	
	Если Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанных Тогда
		НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.КПередаче;
		Если ИспользоватьАвтоматическийОбмен Тогда
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПередачуДанныхРегламентнымЗаданием;
		Иначе
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ВыполнитеОбмен;
		КонецЕсли;
	КонецЕсли;
	
	Если НовыйСтатус = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	РегистрыСведений.СтатусыИнформированияГИСМ.ОбновитьСтатус(
		ДокументСсылка,
		НовыйСтатус,
		ДальнейшееДействие);
	
	Возврат НовыйСтатус;
	
КонецФункции

Функция ОбновитьСтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки) Экспорт
	
	НовыйСтатус     = Неопределено;
	ДальнейшееДействие = Неопределено;
	
	Если Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанных Тогда
		
		Если СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Принято Тогда
			
			НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.Передано;
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПолучениеКвитанцииОФиксации;
			
		ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Отклонено
			ИЛИ СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Ошибка Тогда
			
			НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.ОтклоненоГИСМ;
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанные;
			
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанныхПолучениеКвитанции Тогда
		
		Если СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Принято Тогда
			
			НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.ПринятоГИСМ;
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.НеТребуется;
			
		ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Отклонено
			ИЛИ СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Ошибка Тогда
			
			НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.ОтклоненоГИСМ;
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанные;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НовыйСтатус = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	РегистрыСведений.СтатусыИнформированияГИСМ.ОбновитьСтатус(ДокументСсылка, НовыйСтатус, ДальнейшееДействие);
	
	Возврат НовыйСтатус;
	
КонецФункции

#КонецОбласти

#Область РаботаСПодарочнымиСертификатами

Процедура СформироватьТаблицаПодарочныеСертификаты(ДокументСсылкаПриходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаЗапасы.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВременнаяТаблицаЗапасы.Номенклатура КАК ПодарочныйСертификат,
	|	СерииНоменклатуры.Серия КАК НомерСертификата,
	|	-ВременнаяТаблицаЗапасы.НоминалСертификата КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ВременнаяТаблицаЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ВременнаяТаблицаЗапасы.КлючСвязи = СерииНоменклатуры.КлючСвязи
	|			И (&ИспользоватьСерииНоменклатуры)
	|ГДЕ
	|	ВременнаяТаблицаЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|	И ВременнаяТаблицаЗапасы.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаПредоплата.Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВременнаяТаблицаПредоплата.Документ,
	|	ВременнаяТаблицаПредоплата.НомерСертификата,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаПредоплата.ЧастичноеПогашение
	|			ТОГДА -ВременнаяТаблицаПредоплата.СуммаСертификата
	|		ИНАЧЕ -ВременнаяТаблицаПредоплата.НоминалСертификата
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ВременнаяТаблицаПредоплата
	|ГДЕ
	|	ВременнаяТаблицаПредоплата.ОплатаСертификатом";
	Запрос.УстановитьПараметр("ИспользоватьСерииНоменклатуры", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьСерииНоменклатуры);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПодарочныеСертификаты", РезультатЗапроса);
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру ОплатаПодарочнымиСертификатами.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаОплатаПодарочнымиСертификатами(ДокументСсылкаПриходнаяНакладная,
	СтруктураДополнительныеСвойства)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;

	Запрос.Текст = РаботаСПодарочнымиСертификатами.СформироватьТекстЗапросаПоОплатеПодарочнымиСертификатами(Истина,
		Истина);

	РезультатЗапроса = Запрос.Выполнить();

	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОплатаПодарочнымиСертификатами",
		РезультатЗапроса.Выгрузить());

КонецПроцедуры // СформироватьТаблицаОплатаПодарочнымиСертификатами()

#Конецобласти

#Область ЗагрузкаДанныхИзВнешнегоИсточника

Процедура ПоляЗагрузкиДанныхИзВнешнегоИсточника(ТаблицаПолейЗагрузки, НастройкиЗагрузкиДанных) Экспорт
	
	//
	// Для группы полей действует правило: хотя бы одно поле в группе должно быть выбрано в колонках
	//
	
	ПолноеИмяОбъектаЗаполнения = НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения;
	
	ОписаниеТиповСтрока11 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(11));
	ОписаниеТиповСтрока25 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(25));
	ОписаниеТиповСтрока30 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(30));
	ОписаниеТиповСтрока50 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50));
	ОписаниеТиповСтрока100 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(100));
	ОписаниеТиповСтрока110 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(110));
	ОписаниеТиповСтрока150 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(150));
	ОписаниеТиповСтрока200 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(200));
	ОписаниеТиповСтрока1000 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(1000));
	ОписаниеТиповЧисло15_2 = Новый ОписаниеТипов("Число", , , , Новый КвалификаторыЧисла(15, 2,
		ДопустимыйЗнак.Неотрицательный));
	ОписаниеТиповЧисло15_3 = Новый ОписаниеТипов("Число", , , , Новый КвалификаторыЧисла(15, 3,
		ДопустимыйЗнак.Неотрицательный));
	ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");
	
	ЭтоЗапасы = (ПолноеИмяОбъектаЗаполнения = "Документ.ПриходнаяНакладная.ТабличнаяЧасть.Запасы");
	
	ПоказыватьНоменклатуруПоставщиков = ЭтоЗапасы И ПолучитьФункциональнуюОпцию("УчетНоменклатурыПоставщиков");
	Если ПоказыватьНоменклатуруПоставщиков Тогда
	
		ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.НоменклатураПоставщиков");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Идентификатор",
			НСтр("ru = 'Идентификатор'"), ОписаниеТиповСтрока110, ОписаниеТиповКолонка, "НоменклатураПоставщиков", 1, , Ложь, ПоказыватьНоменклатуруПоставщиков);
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "АртикулПоставщика",
			НСтр("ru = 'Артикул поставщика'"), ОписаниеТиповСтрока25, ОписаниеТиповКолонка, "НоменклатураПоставщиков", 2, , Ложь, ПоказыватьНоменклатуруПоставщиков);
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "НоменклатураПоставщиковНаименование",
			НСтр("ru = 'Номенклатура поставщиков (наименование)'"), ОписаниеТиповСтрока100, ОписаниеТиповКолонка, "НоменклатураПоставщиков", 3, , Ложь, ПоказыватьНоменклатуруПоставщиков);
	
	КонецЕсли;
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ОбязательноеЗаполнениеГруппыНоменклатуры = Не ПолучитьФункциональнуюОпцию("УчетНоменклатурыПоставщиков");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(
		ТаблицаПолейЗагрузки, "Штрихкод",
		НСтр("ru = 'Штрихкод'"), ОписаниеТиповСтрока200, ОписаниеТиповКолонка, "Номенклатура",
		1, , ОбязательноеЗаполнениеГруппыНоменклатуры, ЭтоЗапасы);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(
		ТаблицаПолейЗагрузки, "Артикул",
		НСтр("ru = 'Артикул'"), ОписаниеТиповСтрока25, ОписаниеТиповКолонка, "Номенклатура",
		2, , ОбязательноеЗаполнениеГруппыНоменклатуры);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(
		ТаблицаПолейЗагрузки, "НоменклатураНаименование",
		НСтр("ru = 'Номенклатура (наименование)'"), ОписаниеТиповСтрока100, ОписаниеТиповКолонка, "Номенклатура",
		3, , ОбязательноеЗаполнениеГруппыНоменклатуры);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(
		ТаблицаПолейЗагрузки, "НоменклатураНаименованиеПолное",
		НСтр("ru = 'Номенклатура (полное наименование)'"), ОписаниеТиповСтрока1000, ОписаниеТиповКолонка, "Номенклатура",
		5, , ОбязательноеЗаполнениеГруппыНоменклатуры);

	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Содержание",
		НСтр("ru = 'Содержание'"), ОписаниеТиповСтрока1000, ОписаниеТиповСтрока1000, , , , ,
		НастройкиЗагрузкиДанных.СодержаниеВидимо);

	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "РодительКод",
		НСтр("ru = 'Код'", ОбщегоНазначения.КодОсновногоЯзыка()), ОписаниеТиповСтрока11, ОписаниеТиповКолонка, "Родитель", 1);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "РодительНаименование", 
		НСтр("ru = 'Группа (наименование)'", ОбщегоНазначения.КодОсновногоЯзыка()), ОписаниеТиповСтрока100, ОписаниеТиповКолонка, "Родитель", 2);	
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
		
		ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ХарактеристикаНаименование", НСтр("ru = 'Характеристика (наименование)'"), ОписаниеТиповСтрока150, ОписаниеТиповКолонка, "Характеристика", 1);
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ХарактеристикаАртикул", НСтр("ru = 'Характеристика (артикул)'"), ОписаниеТиповСтрока25, ОписаниеТиповКолонка, "Характеристика", 2);
		
	КонецЕсли;
	
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ЭтоУслуга", 
		НСтр("ru = 'Это услуга'", ОбщегоНазначения.КодОсновногоЯзыка()), ОписаниеТиповСтрока11, ОписаниеТиповБулево, ); 		
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Картинка", 
		НСтр("ru = 'Картинка'", ОбщегоНазначения.КодОсновногоЯзыка()), ОписаниеТиповСтрока1000, ОписаниеТиповСтрока1000,,,,,Ложь);   		
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.КатегорииНоменклатуры");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "КатегорияНоменклатуры", 
		НСтр("ru = 'Категория номенклатуры'", ОбщегоНазначения.КодОсновногоЯзыка()), ОписаниеТиповСтрока100, ОписаниеТиповКолонка);   

	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартии") Тогда
		
		ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.ПартииНоменклатуры");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Партия",
			НСтр("ru = 'Партия (наименование)'"), ОписаниеТиповСтрока150, ОписаниеТиповКолонка);
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") Тогда
		
		ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Серия",
			НСтр("ru = 'Серия'"), ОписаниеТиповСтрока1000, ОписаниеТиповКолонка);
		
	КонецЕсли;
	
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Количество", НСтр("ru = 'Количество'"),
		ОписаниеТиповСтрока25, ОписаниеТиповЧисло15_3, , , Истина);

	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.КлассификаторЕдиницИзмерения, СправочникСсылка.ЕдиницыИзмерения");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ЕдиницаИзмерения",
		НСтр("ru = 'Ед. изм.'"), ОписаниеТиповСтрока25, ОписаниеТиповКолонка, , , , ,
		ПолучитьФункциональнуюОпцию("УчетВРазличныхЕдиницахИзмерения"));

	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Цена", НСтр("ru = 'Цена'"),
		ОписаниеТиповСтрока25, ОписаниеТиповЧисло15_2, , , Истина);

	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СтавкаНДС",
		НСтр("ru = 'Ставка НДС'"), ОписаниеТиповСтрока50, ОписаниеТиповКолонка);

	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СуммаНДС",
		НСтр("ru = 'Сумма НДС'"), ОписаниеТиповСтрока25, ОписаниеТиповЧисло15_2);
		
	ОписаниеТиповКолонка = Новый ОписаниеТипов("ПланСчетовСсылка.Управленческий");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СчетУчетаДоходов", 
		НСтр("ru = 'Счет учета доходов'"), ОписаниеТиповСтрока100, ОписаниеТиповКолонка,,,,, Ложь);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СчетУчетаЗапасов", 
		НСтр("ru = 'Счет учета запасов'"), ОписаниеТиповСтрока100, ОписаниеТиповКолонка,,,,, Ложь);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СчетУчетаЗатрат", 
		НСтр("ru = 'Счет учета затрат'"), ОписаниеТиповСтрока100, ОписаниеТиповКолонка,,,,, Ложь);
		
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "НаправлениеДеятельности",
		НСтр("ru = 'Направление деятельности'", ОбщегоНазначения.КодОсновногоЯзыка()), ОписаниеТиповСтрока100, ОписаниеТиповКолонка);
		
	ОписаниеТиповКолонка = Новый ОписаниеТипов("ПеречислениеСсылка.МетодОценкиЗапасов");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "МетодОценки",
		НСтр("ru = 'Способ списания'", ОбщегоНазначения.КодОсновногоЯзыка()), ОписаниеТиповСтрока100, ОписаниеТиповКолонка);
		
	ОписаниеТиповКолонка = Новый ОписаниеТипов("ПеречислениеСсылка.СпособыПополненияЗапасов");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СпособПополнения",
		НСтр("ru = 'Способ пополнения'", ОбщегоНазначения.КодОсновногоЯзыка()), ОписаниеТиповСтрока100, ОписаниеТиповКолонка);

	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));   	
	ОписаниеТиповКолонка = Новый ОписаниеТипов(МассивТипов);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Заказ",
		НСтр("ru = 'Заказ (покупатель/поставщик)'"), ОписаниеТиповСтрока50, ОписаниеТиповКолонка);
	
	ВидимостьСклада = (ЭтоЗапасы
					   И ПолучитьФункциональнуюОпцию("УчетПоНесколькимСкладам")
					   И НастройкиЗагрузкиДанных.Свойство("ПоложениеСклада")
					   И НастройкиЗагрузкиДанных.ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти);
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.СтруктурныеЕдиницы");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СтруктурнаяЕдиница",
		НСтр("ru = 'Склад'"), ОписаниеТиповСтрока50, ОписаниеТиповКолонка, , , , , ВидимостьСклада);

	Если ПолучитьФункциональнуюОпцию("УчетГТД") Тогда

		ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.СтраныМира");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СтранаПроисхождения",
			НСтр("ru = 'Страна происхождения'"), ОписаниеТиповСтрока100, ОписаниеТиповКолонка);

		ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.НомераГТД");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "НомерГТД",
			НСтр("ru = 'Номер ГТД'"), ОписаниеТиповСтрока30, ОписаниеТиповКолонка);

	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОпределенииОбразцовЗагрузкиДанных(НастройкиЗагрузкиДанных, УникальныйИдентификатор) Экспорт
	
	Образец_xlsx = ПолучитьМакет("ОбразецЗагрузкиДанных_xlsx");
	ОбразецЗагрузкиДанных_xlsx = ПоместитьВоВременноеХранилище(Образец_xlsx, УникальныйИдентификатор);
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_xlsx", ОбразецЗагрузкиДанных_xlsx);
	
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_mxl", "ОбразецЗагрузкиДанных_mxl");
	
	Образец_csv = ПолучитьМакет("ОбразецЗагрузкиДанных_csv");
	ОбразецЗагрузкиДанных_csv = ПоместитьВоВременноеХранилище(Образец_csv, УникальныйИдентификатор);
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_csv", ОбразецЗагрузкиДанных_csv);
	
КонецПроцедуры

Процедура СопоставитьЗагружаемыеДанныеИзВнешнегоИсточника(ПараметрыСопоставления, АдресРезультата) Экспорт
	
	ТаблицаСопоставленияДанных	= ПараметрыСопоставления.ТаблицаСопоставленияДанных;
	РазмерТаблицыДанных			= ТаблицаСопоставленияДанных.Количество();
	НастройкиЗагрузкиДанных		= ПараметрыСопоставления.НастройкиЗагрузкиДанных;
	НастройкиПоиска				= НастройкиЗагрузкиДанных.НастройкиПоиска;

	ТаблицаДублирующихСтрок = ЗагрузкаДанныхИзВнешнегоИсточника.ПустаяТаблицаДублирующихСтрокНоменклатуры();
	НастройкиПоиска.Вставить("ТаблицаДублирующихСтрок", ТаблицаДублирующихСтрок);
	
	ПолноеИмяОбъектаЗаполнения = НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения;
	
	// ТаблицаСопоставленияДанных - Тип ДанныеФормыКоллекция
	Для каждого СтрокаТаблицыФормы Из ТаблицаСопоставленияДанных Цикл
		
		НоменклатураСопоставлена = Ложь;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицыФормы, "НоменклатураПоставщиков") 
			И (ЗначениеЗаполнено(СтрокаТаблицыФормы.АртикулПоставщика)
			ИЛИ ЗначениеЗаполнено(СтрокаТаблицыФормы.Идентификатор)
			ИЛИ ЗначениеЗаполнено(СтрокаТаблицыФормы.НоменклатураПоставщиковНаименование)) Тогда
		
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьНоменклатуруПоставщиков(СтрокаТаблицыФормы, НастройкиЗагрузкиДанных);	
			НоменклатураСопоставлена = ЗначениеЗаполнено(СтрокаТаблицыФормы.НоменклатураПоставщиков);
		КонецЕсли;
		
		СтрокаТаблицыФормы.СчетУчетаЗапасов = ПланыСчетов.Управленческий.СырьеИМатериалы;
		СтрокаТаблицыФормы.СчетУчетаЗатрат = ?(ПолучитьФункциональнуюОпцию("ИспользоватьПодсистемуПроизводство"), 
			ПланыСчетов.Управленческий.НезавершенноеПроизводство, ПланыСчетов.Управленческий.КоммерческиеРасходы);
		СтрокаТаблицыФормы.СчетУчетаДоходов = ?(СтрокаТаблицыФормы.Номенклатура.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.ПодарочныйСертификат"), 
			ПланыСчетов.Управленческий.ПрочиеДоходы, ПланыСчетов.Управленческий.ПустаяСсылка());
			
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицыФормы, "НаправлениеДеятельности") Тогда
				
			ЗначениеПоУмолчанию = Справочники.НаправленияДеятельности.Прочее;
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьНаправлениеДеятельности(СтрокаТаблицыФормы.НаправлениеДеятельности, СтрокаТаблицыФормы.НаправлениеДеятельности_ВходящиеДанные, ЗначениеПоУмолчанию);
			
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицыФормы, "МетодОценки") Тогда
				
			ЗначениеПоУмолчанию = Перечисления.МетодОценкиЗапасов.ПоСредней;
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьМетодОценки(СтрокаТаблицыФормы.МетодОценки, СтрокаТаблицыФормы.МетодОценки_ВходящиеДанные, ЗначениеПоУмолчанию);
			
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицыФормы, "СпособПополнения") Тогда
				
			ЗначениеПоУмолчанию = Перечисления.СпособыПополненияЗапасов.Закупка;
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьСпособПополнения(СтрокаТаблицыФормы.СпособПополнения, СтрокаТаблицыФормы.СпособПополнения_ВходящиеДанные, ЗначениеПоУмолчанию);
			
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицыФормы, "КатегорияНоменклатуры_ВходящиеДанные")
			И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицыФормы, "КатегорияНоменклатуры") Тогда
		
			ЗначениеПоУмолчанию = Справочники.КатегорииНоменклатуры.БезКатегории;					
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьКатегориюНоменклатуры(СтрокаТаблицыФормы.КатегорияНоменклатуры, СтрокаТаблицыФормы.КатегорияНоменклатуры_ВходящиеДанные, ЗначениеПоУмолчанию);
			
		КонецЕсли;
		
		Если НЕ НоменклатураСопоставлена Тогда
		
			// Номенклатура по ШтрихКоду, Артикулу, Наименованию, НаименованиеПолное
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьНоменклатуру(СтрокаТаблицыФормы, НастройкиПоиска);
		
		КонецЕсли; 
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицыФормы, "Родитель")
			И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицыФормы, "РодительНаименование")
			И ЗначениеЗаполнено(СтрокаТаблицыФормы.РодительНаименование) Тогда
			
			ЗначениеПоУмолчанию = Справочники.Номенклатура.ПустаяСсылка();
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьРодителяНоменклатуры(СтрокаТаблицыФормы.Родитель, "", СтрокаТаблицыФормы.РодительНаименование, ЗначениеПоУмолчанию);
			
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицыФормы, "ЭтоУслуга_ВходящиеДанные") Тогда
			
			Если ЗначениеЗаполнено(СтрокаТаблицыФормы.ЭтоУслуга_ВходящиеДанные) Тогда
				ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.ЭтоУслуга,
					СтрокаТаблицыФормы.ЭтоУслуга_ВходящиеДанные);
			Иначе
				СтрокаТаблицыФормы.ЭтоУслуга = НастройкиЗагрузкиДанных.ЗагрузкаТабличнойЧастиУслуги;
				СтрокаТаблицыФормы.ЭтоУслуга_ВходящиеДанные = НастройкиЗагрузкиДанных.ЗагрузкаТабличнойЧастиУслуги;
			КонецЕсли;			
			                      			
		КонецЕсли;
		
		Если ПолноеИмяОбъектаЗаполнения = "Документ.ПриходнаяНакладная.ТабличнаяЧасть.Расходы" Тогда
			
			Если СтрокаТаблицыФормы.Номенклатура.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Услуга Тогда
				
				СтрокаТаблицыФормы.Номенклатура = Неопределено;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПолноеИмяОбъектаЗаполнения = "Документ.ПриходнаяНакладная.ТабличнаяЧасть.Запасы" Тогда
			
			Если СтрокаТаблицыФормы.Номенклатура.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Запас Тогда
				
				СтрокаТаблицыФормы.Номенклатура = Неопределено;
				
			КонецЕсли;
			
			Если НЕ НоменклатураСопоставлена И ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") И ЗначениеЗаполнено(СтрокаТаблицыФормы.Номенклатура) Тогда
				
				// Характеристика по Владельцу и Наименованию
				ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьХарактеристику(СтрокаТаблицыФормы);
				
			КонецЕсли;
			
			Если ПолучитьФункциональнуюОпцию("ИспользоватьПартии") Тогда
				
				Если ЗначениеЗаполнено(СтрокаТаблицыФормы.Номенклатура) Тогда
					
					// Партия по Владельцу и Наименованию
					ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьПартию(СтрокаТаблицыФормы.Партия, СтрокаТаблицыФормы.Номенклатура, СтрокаТаблицыФормы.Штрихкод, СтрокаТаблицыФормы.Партия_ВходящиеДанные);
					
				КонецЕсли;
				
			КонецЕсли;
			
			// Серия
			Если ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") Тогда
				
				Если ЗначениеЗаполнено(СтрокаТаблицыФормы.Номенклатура) 
					И  ЗначениеЗаполнено(СтрокаТаблицыФормы.Серия_ВходящиеДанные) Тогда
					
					ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьСерия(СтрокаТаблицыФормы.Номенклатура, СтрокаТаблицыФормы.Серия, СтрокаТаблицыФормы.Серия_ВходящиеДанные);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Содержание
		Если ЗначениеЗаполнено(СтрокаТаблицыФормы.Номенклатура) Тогда
			
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СкопироватьСтрокуВЗначениеСтроковогоТипа(СтрокаТаблицыФормы.Содержание, СтрокаТаблицыФормы.Содержание_ВходящиеДанные);
			
		КонецЕсли;
		
		// Количество
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.Количество, СтрокаТаблицыФормы.Количество_ВходящиеДанные, 1);
		
		// ЕдиницыИзмерения по Наименованию (так же рассмотреть возможность прикрутить пользовательские ЕИ)
		ЗначениеПоУмолчанию = ?(ЗначениеЗаполнено(СтрокаТаблицыФормы.Номенклатура), СтрокаТаблицыФормы.Номенклатура.ЕдиницаИзмерения, Справочники.КлассификаторЕдиницИзмерения.шт);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьЕдиницыИзмерения(СтрокаТаблицыФормы.Номенклатура, СтрокаТаблицыФормы.ЕдиницаИзмерения, СтрокаТаблицыФормы.ЕдиницаИзмерения_ВходящиеДанные, ЗначениеПоУмолчанию);
		
		// Цена
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.Цена, СтрокаТаблицыФормы.Цена_ВходящиеДанные, 1);
		
		// СтавкаНДС по наименованию
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьСтавкуНДС(СтрокаТаблицыФормы.СтавкаНДС, СтрокаТаблицыФормы.СтавкаНДС_ВходящиеДанные, Неопределено);
		
		// СуммаНДС
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.СуммаНДС, СтрокаТаблицыФормы.СуммаНДС_ВходящиеДанные, 0);
		
		// Заказ по номеру, дате, признаку
		ДопустимыеТипыЗаказа = Документы.ПриходнаяНакладная.ДопустимыеТипыЗаказа(НастройкиЗагрузкиДанных.ВидОперации);
		Если ДопустимыеТипыЗаказа.СодержитТип(Тип("ДокументСсылка.ЗаказПоставщику")) Тогда
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьЗаказПоставщику(СтрокаТаблицыФормы.Заказ, СтрокаТаблицыФормы.Заказ_ВходящиеДанные);
		Иначе
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьЗаказ(СтрокаТаблицыФормы.Заказ, СтрокаТаблицыФормы.Заказ_ВходящиеДанные);
		КонецЕсли;
		
		// Склад
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьСтруктурнуюЕдиницу(СтрокаТаблицыФормы.СтруктурнаяЕдиница, СтрокаТаблицыФормы.СтруктурнаяЕдиница_ВходящиеДанные, Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		
		// Страна и ГТД
		Если ПолучитьФункциональнуюОпцию("УчетГТД") Тогда
			
			ЗначениеПоУмолчанию = Неопределено;
			Если ЗначениеЗаполнено(СтрокаТаблицыФормы.Номенклатура) Тогда
				
				ЗначениеПоУмолчанию = СтрокаТаблицыФормы.Номенклатура.СтранаПроисхождения;
				
			КонецЕсли;
			
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьСтрануПроисхождения(СтрокаТаблицыФормы.СтранаПроисхождения, СтрокаТаблицыФормы.СтранаПроисхождения_ВходящиеДанные, ЗначениеПоУмолчанию);
			
			Если ЗначениеЗаполнено(СтрокаТаблицыФормы.СтранаПроисхождения)
				И СтрокаТаблицыФормы.СтранаПроисхождения <> Справочники.СтраныМира.Россия Тогда
				
				ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьНомерГТД(СтрокаТаблицыФормы.НомерГТД, СтрокаТаблицыФормы.НомерГТД_ВходящиеДанные);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ПроверитьКорректностьДанныхВСтрокеТаблицы(СтрокаТаблицыФормы, ПолноеИмяОбъектаЗаполнения);
		
		ЗагрузкаДанныхИзВнешнегоИсточника.ПрогрессСопоставленияДанных(ТаблицаСопоставленияДанных.Индекс(СтрокаТаблицыФормы), РазмерТаблицыДанных);
		
	КонецЦикла;
	
	ТаблицаСопоставленияДанных.ЗагрузитьКолонку(ТаблицаДублирующихСтрок.ВыгрузитьКолонку("КлючСвязи"), "_КлючСвязи");
	ПоместитьВоВременноеХранилище(ТаблицаСопоставленияДанных, АдресРезультата);
	
КонецПроцедуры

Процедура ПроверитьКорректностьДанныхВСтрокеТаблицы(СтрокаТаблицыФормы, ПолноеИмяОбъектаЗаполнения = "") Экспорт

	ИмяСлужебногоПоля = ЗагрузкаДанныхИзВнешнегоИсточника.ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна();

	НоменклатураЗаполнена = ЗначениеЗаполнено(СтрокаТаблицыФормы.Номенклатура);
	ЗагрузкаНоменклатурыВозможна = (ЗначениеЗаполнено(СтрокаТаблицыФормы.НоменклатураНаименование) ИЛИ ЗначениеЗаполнено(СтрокаТаблицыФормы.НоменклатураНаименованиеПолное));
	
	Если ПолноеИмяОбъектаЗаполнения = "Документ.ПриходнаяНакладная.ТабличнаяЧасть.Запасы" Тогда
		Если НоменклатураЗаполнена 
			И СтрокаТаблицыФормы.Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Запас Тогда
			
			СтрокаТаблицыФормы._СтрокаСопоставлена = НоменклатураЗаполнена;
			СтрокаТаблицыФормы[ИмяСлужебногоПоля] = СтрокаТаблицыФормы.Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Запас
			И Не СтрокаТаблицыФормы.Номенклатура.ЭтоНабор // Исключая наборы
			И СтрокаТаблицыФормы.Количество <> 0
			И Не СтрокаТаблицыФормы.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Подразделение;
			
		Иначе   				
			
			СтрокаТаблицыФормы[ИмяСлужебногоПоля] = ЗагрузкаНоменклатурыВозможна И НЕ СтрокаТаблицыФормы.ЭтоУслуга;
			
		КонецЕсли;		
		
	ИначеЕсли ПолноеИмяОбъектаЗаполнения = "Документ.ПриходнаяНакладная.ТабличнаяЧасть.Расходы" Тогда
		Если НоменклатураЗаполнена 
			И СтрокаТаблицыФормы.Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда 
			
			СтрокаТаблицыФормы._СтрокаСопоставлена = НоменклатураЗаполнена;
			СтрокаТаблицыФормы[ИмяСлужебногоПоля] = СтрокаТаблицыФормы.Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга
			И Не СтрокаТаблицыФормы.Номенклатура.ЭтоНабор // Исключая наборы
			И СтрокаТаблицыФормы.Количество <> 0
			И Не СтрокаТаблицыФормы.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Подразделение;
			
		Иначе
			
			СтрокаТаблицыФормы[ИмяСлужебногоПоля] = ЗагрузкаНоменклатурыВозможна И СтрокаТаблицыФормы.ЭтоУслуга;
			
		КонецЕсли;		
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ИнтерфейсПечати

Функция ДанныеПолученныхДокументовРегУчет(МассивОбъектов, ИспользоватьФаксимиле, ПечатнаяФормаТолькоВРублях = Истина,
	Ошибки = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ИспользоватьФаксимиле", ИспользоватьФаксимиле);
	Запрос.УстановитьПараметр("ПечатнаяФормаТолькоВРублях", ПечатнаяФормаТолькоВРублях);
	Запрос.УстановитьПараметр("НациональнаяВалюта", Константы.НациональнаяВалюта.Получить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПриходнаяНакладная.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО КАК ДатаДокумента,
	|	НЕОПРЕДЕЛЕНО КАК Номер,
	|	НЕОПРЕДЕЛЕНО КАК ЦифровойИндексОбособленногоПодразделения,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ФаксимилеПустая,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСчетФактура.Продажа) КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладная.Контрагент.ГоловнойКонтрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ПриходнаяНакладная.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ ПриходнаяНакладная.Контрагент
	|	КОНЕЦ КАК Организация,
	|	ПриходнаяНакладная.Контрагент КАК ОбособленноеПодразделениеПоставщика,
	|	ПриходнаяНакладная.Контрагент КАК Грузоотправитель,
	|	НЕОПРЕДЕЛЕНО КАК Префикс,
	|	НЕОПРЕДЕЛЕНО КАК Логотип,
	|	НЕОПРЕДЕЛЕНО КАК ФаксимилеПечати,
	|	ВЫБОР
	|		КОГДА &ИспользоватьФаксимиле = ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДаНет.Да)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ДаНет.Нет)
	|	КОНЕЦ КАК ИспользоватьФаксимиле,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладная.Контрагент.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладная.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.НеОблагаетсяНДС)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СтатусУПД,
	|	ЛОЖЬ КАК ЭтоСводныйСчетФактура,
	|	ЛОЖЬ КАК ЭтоКорректировка,
	|	ПриходнаяНакладная.Контрагент.БанковскийСчетПоУмолчанию КАК БанковскийСчет,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеПодразделения,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеСкладаСписания,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладная.Организация.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ПриходнаяНакладная.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ ПриходнаяНакладная.Организация
	|	КОНЕЦ КАК Контрагент,
	|	ПриходнаяНакладная.Организация КАК ОбособленноеПодразделениеПокупателя,
	|	ПриходнаяНакладная.Организация КАК Грузополучатель,
	|	ПриходнаяНакладная.Организация.БанковскийСчетПоУмолчанию КАК БанковскийСчетКонтрагента,
	|	НЕОПРЕДЕЛЕНО КАК АдресДоставки,
	|	ПриходнаяНакладная.ПодписьКладовщика.РасшифровкаПодписи КАК РасшифровкаПодписиКонтрагента,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьНомер,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьДата,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьВыдана,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьЛицо,
	|	ПриходнаяНакладная.Договор.Представление КАК Основание,
	|	ПриходнаяНакладная.Договор.Представление КАК ПредставлениеОснования,
	|	ПриходнаяНакладная.Договор КАК ОснованиеПечатиСсылка,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|			ТОГДА НациональнаяВалюта.Значение
	|		ИНАЧЕ ПриходнаяНакладная.ВалютаДокумента
	|	КОНЕЦ КАК ВалютаДокумента,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|			ТОГДА НациональнаяВалюта.Значение.НаименованиеПолное
	|		ИНАЧЕ ПриходнаяНакладная.ВалютаДокумента.НаименованиеПолное
	|	КОНЕЦ КАК ВалютаНаименование,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|			ТОГДА НациональнаяВалюта.Значение.Код
	|		ИНАЧЕ ПриходнаяНакладная.ВалютаДокумента.Код
	|	КОНЕЦ КАК ВалютаКод,
	|	ПриходнаяНакладная.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ПриходнаяНакладная.НДСВключатьВСтоимость КАК НДСВключатьВСтоимость,
	|	ПриходнаяНакладная.Курс КАК Курс,
	|	ПриходнаяНакладная.Кратность КАК Кратность,
	|	ПриходнаяНакладная.Договор.НомерДоговора КАК ОснованиеНомер,
	|	ПриходнаяНакладная.Договор.ДатаДоговора КАК ОснованиеДата,
	|	НЕОПРЕДЕЛЕНО КАК ТранспортнаяНакладнаяНомер,
	|	НЕОПРЕДЕЛЕНО КАК ТранспортнаяНакладнаяДата,
	|	СвязиКонтрагентКонтакт.Должность КАК ДолжностьРуководителя,
	|	ПриходнаяНакладная.КонтактноеЛицоПодписант.Наименование КАК РасшифровкаПодписиРуководителя,
	|	НЕОПРЕДЕЛЕНО КАК ФаксимилеРуководителя,
	|	НЕОПРЕДЕЛЕНО КАК РасшифровкаПодписиГлавногоБухгалтера,
	|	НЕОПРЕДЕЛЕНО КАК ФаксимилеГлавногоБухгалтера,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьКладовщика,
	|	НЕОПРЕДЕЛЕНО КАК РасшифровкаПодписиКладовщика,
	|	НЕОПРЕДЕЛЕНО КАК ФаксимилеКладовщика,
	|	0 КАК Вес,
	|	0 КАК Объем,
	|	НЕОПРЕДЕЛЕНО КАК НоменклатураДоставки,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеНоменклатурыДоставки,
	|	НЕОПРЕДЕЛЕНО КАК КодНоменклатурыДоставки,
	|	НЕОПРЕДЕЛЕНО КАК АртикулНоменклатурыДоставки,
	|	НЕОПРЕДЕЛЕНО КАК СтоимостьДоставки,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДСДоставки,
	|	НЕОПРЕДЕЛЕНО КАК СуммаНДСДоставки,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторГосКонтракта,
	|	ПриходнаяНакладная.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ПриходнаяНакладная.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ПриходнаяНакладная.Запасы.(
	|		Ссылка.НомерВходящегоДокумента КАК НомерОтгрузочногоДокумента,
	|		Ссылка.ДатаВходящегоДокумента КАК ДатаОтгрузочногоДокумента,
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Номенклатура,
	|		Содержание КАК Содержание,
	|		ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(ПриходнаяНакладная.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|				ТОГДА ПриходнаяНакладная.Запасы.Номенклатура.Наименование
	|			ИНАЧЕ ВЫРАЗИТЬ(ПриходнаяНакладная.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|		КОНЕЦ КАК ПредставлениеНоменклатуры,
	|		Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|		Номенклатура.Код КАК ЗапасКод,
	|		Номенклатура.Код КАК Код,
	|		Номенклатура.Артикул КАК Артикул,
	|		Номенклатура.Штрихкод КАК Штрихкод,
	|		Номенклатура.ТоварнаяНоменклатураВЭД.Код КАК КодТНВЭД,
	|		ВЫБОР
	|			КОГДА ПриходнаяНакладная.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И ПриходнаяНакладная.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА ПриходнаяНакладная.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Наименование
	|			ИНАЧЕ ПриходнаяНакладная.Запасы.Номенклатура.ЕдиницаИзмерения.Наименование
	|		КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Наименование,
	|		ВЫБОР
	|			КОГДА ПриходнаяНакладная.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И ПриходнаяНакладная.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА ПриходнаяНакладная.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Код
	|			ИНАЧЕ ПриходнаяНакладная.Запасы.Номенклатура.ЕдиницаИзмерения.Код
	|		КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Код,
	|		Характеристика КАК Характеристика,
	|		Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		НЕОПРЕДЕЛЕНО КАК ВидУпаковки,
	|		ВЫБОР
	|			КОГДА ПриходнаяНакладная.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				ТОГДА ПриходнаяНакладная.Запасы.ЕдиницаИзмерения.Коэффициент
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК КоэффициентЕдиницыИзмерения,
	|		Количество КАК Количество,
	|		НЕОПРЕДЕЛЕНО КАК КоличествоВОдномМесте,
	|		НЕОПРЕДЕЛЕНО КАК КоличествоМест,
	|		СтавкаНДС КАК СтавкаНДС,
	|		0 КАК МассаБрутто,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|					ИЛИ ПриходнаяНакладная.Запасы.Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА
	|						И ПриходнаяНакладная.Запасы.Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение
	|				ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладная.Запасы.Цена * ПриходнаяНакладная.Курс / ПриходнаяНакладная.Кратность КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ ПриходнаяНакладная.Запасы.Цена
	|		КОНЕЦ КАК Цена,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|					ИЛИ ПриходнаяНакладная.Запасы.Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА
	|						И ПриходнаяНакладная.Запасы.Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение
	|				ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладная.Запасы.Сумма * ПриходнаяНакладная.Курс / ПриходнаяНакладная.Кратность КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ ПриходнаяНакладная.Запасы.Сумма
	|		КОНЕЦ КАК Сумма,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|					ИЛИ ПриходнаяНакладная.Запасы.Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА
	|						И ПриходнаяНакладная.Запасы.Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение
	|				ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладная.Запасы.СуммаНДС * ПриходнаяНакладная.Курс / ПриходнаяНакладная.Кратность КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ ПриходнаяНакладная.Запасы.СуммаНДС
	|		КОНЕЦ КАК СуммаНДС,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|					ИЛИ ПриходнаяНакладная.Запасы.Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА
	|						И ПриходнаяНакладная.Запасы.Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение
	|				ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладная.Запасы.Всего * ПриходнаяНакладная.Курс / ПриходнаяНакладная.Кратность КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ ПриходнаяНакладная.Запасы.Всего
	|		КОНЕЦ КАК Всего,
	|		Ссылка КАК Ссылка,
	|		НоменклатураНабора КАК НоменклатураНабора,
	|		ХарактеристикаНабора КАК ХарактеристикаНабора,
	|		ЛОЖЬ КАК ЭтоНабор,
	|		ВЫБОР
	|			КОГДА ПриходнаяНакладная.Запасы.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|					И ПриходнаяНакладная.Запасы.НоменклатураНабора.ВариантПечатиНабора = ЗНАЧЕНИЕ(Перечисление.ВариантыПечатиНаборов.НаборИКомплектующие)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК НеобходимоВыделитьКакСоставНабора,
	|		СтранаПроисхождения КАК СтранаСсылка,
	|		СтранаПроисхождения.Код КАК СтранаКод,
	|		ПРЕДСТАВЛЕНИЕ(ПриходнаяНакладная.Запасы.СтранаПроисхождения) КАК СтранаПредставление,
	|		НомерГТД.РегистрационныйНомер КАК ПредставлениеГТД
	|	) КАК ТаблицаЗапасы,
	|	ПриходнаяНакладная.Расходы.(
	|		Ссылка.НомерВходящегоДокумента КАК НомерОтгрузочногоДокумента,
	|		Ссылка.ДатаВходящегоДокумента КАК ДатаОтгрузочногоДокумента,
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Номенклатура,
	|		НЕОПРЕДЕЛЕНО КАК Содержание,
	|		ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(ПриходнаяНакладная.Расходы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|				ТОГДА ПриходнаяНакладная.Расходы.Номенклатура.Наименование
	|			ИНАЧЕ ВЫРАЗИТЬ(ПриходнаяНакладная.Расходы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|		КОНЕЦ КАК ПредставлениеНоменклатуры,
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга) КАК ТипНоменклатуры,
	|		Номенклатура.Код КАК ЗапасКод,
	|		Номенклатура.Код КАК Код,
	|		Номенклатура.Артикул КАК Артикул,
	|		Номенклатура.Штрихкод КАК Штрихкод,
	|		Номенклатура.ТоварнаяНоменклатураВЭД.Код КАК КодТНВЭД,
	|		Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ВЫБОР
	|			КОГДА ПриходнаяНакладная.Расходы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И ПриходнаяНакладная.Расходы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА ПриходнаяНакладная.Расходы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Наименование
	|			ИНАЧЕ ПриходнаяНакладная.Расходы.Номенклатура.ЕдиницаИзмерения.Наименование
	|		КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Наименование,
	|		ВЫБОР
	|			КОГДА ПриходнаяНакладная.Расходы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И ПриходнаяНакладная.Расходы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА ПриходнаяНакладная.Расходы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Код
	|			ИНАЧЕ ПриходнаяНакладная.Расходы.Номенклатура.ЕдиницаИзмерения.Код
	|		КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Код,
	|		НЕОПРЕДЕЛЕНО КАК Характеристика,
	|		НЕОПРЕДЕЛЕНО КАК ВидУпаковки,
	|		ВЫБОР
	|			КОГДА ПриходнаяНакладная.Расходы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				ТОГДА ПриходнаяНакладная.Расходы.ЕдиницаИзмерения.Коэффициент
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК КоэффициентЕдиницыИзмерения,
	|		Количество КАК Количество,
	|		НЕОПРЕДЕЛЕНО КАК КоличествоВОдномМесте,
	|		НЕОПРЕДЕЛЕНО КАК КоличествоМест,
	|		СтавкаНДС КАК СтавкаНДС,
	|		0 КАК МассаБрутто,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|					ИЛИ ПриходнаяНакладная.Расходы.Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА
	|						И ПриходнаяНакладная.Расходы.Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение
	|				ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладная.Расходы.Цена * ПриходнаяНакладная.Ссылка.Курс / ПриходнаяНакладная.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ ПриходнаяНакладная.Расходы.Цена
	|		КОНЕЦ КАК Цена,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|					ИЛИ ПриходнаяНакладная.Расходы.Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА
	|						И ПриходнаяНакладная.Расходы.Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение
	|				ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладная.Расходы.Сумма * ПриходнаяНакладная.Ссылка.Курс / ПриходнаяНакладная.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ ПриходнаяНакладная.Расходы.Сумма
	|		КОНЕЦ КАК Сумма,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|					ИЛИ ПриходнаяНакладная.Расходы.Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА
	|						И ПриходнаяНакладная.Расходы.Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение
	|				ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладная.Расходы.СуммаНДС * ПриходнаяНакладная.Ссылка.Курс / ПриходнаяНакладная.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ ПриходнаяНакладная.Расходы.СуммаНДС
	|		КОНЕЦ КАК СуммаНДС,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|					ИЛИ ПриходнаяНакладная.Расходы.Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА
	|						И ПриходнаяНакладная.Расходы.Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение
	|				ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладная.Расходы.Всего * ПриходнаяНакладная.Ссылка.Курс / ПриходнаяНакладная.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ ПриходнаяНакладная.Расходы.Всего
	|		КОНЕЦ КАК Всего,
	|		Ссылка КАК Ссылка,
	|		НЕОПРЕДЕЛЕНО КАК НоменклатураНабора,
	|		НЕОПРЕДЕЛЕНО КАК ХарактеристикаНабора,
	|		ЛОЖЬ КАК ЭтоНабор,
	|		ЛОЖЬ КАК НеобходимоВыделитьКакСоставНабора,
	|		НЕОПРЕДЕЛЕНО КАК СтранаСсылка,
	|		НЕОПРЕДЕЛЕНО КАК СтранаКод,
	|		НЕОПРЕДЕЛЕНО КАК СтранаПредставление,
	|		НЕОПРЕДЕЛЕНО КАК ПредставлениеГТД
	|	) КАК ТаблицаРасходы,
	|	ПриходнаяНакладная.ДобавленныеНаборы.(
	|		НоменклатураНабора КАК НоменклатураНабора,
	|		ХарактеристикаНабора КАК ХарактеристикаНабора,
	|		Количество КАК Количество,
	|		ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(ПриходнаяНакладная.ДобавленныеНаборы.НоменклатураНабора.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|				ТОГДА ПриходнаяНакладная.ДобавленныеНаборы.НоменклатураНабора.Наименование
	|			ИНАЧЕ ВЫРАЗИТЬ(ПриходнаяНакладная.ДобавленныеНаборы.НоменклатураНабора.НаименованиеПолное КАК СТРОКА(1000))
	|		КОНЕЦ КАК ЗапасНабора,
	|		НоменклатураНабора.ВариантПечатиНабора КАК ВариантПечатиНабора,
	|		НоменклатураНабора.ТипНоменклатуры КАК ТипНоменклатурыНабора,
	|		НоменклатураНабора.Артикул КАК АртикулНабора,
	|		НоменклатураНабора.Код КАК КодНабора,
	|		НоменклатураНабора.ЕдиницаИзмерения КАК ЕдиницаИзмеренияНабора,
	|		НоменклатураНабора.ЕдиницаИзмерения.Код КАК КодЕдиницыИзмеренияНабора,
	|		ВЫБОР
	|			КОГДА ПриходнаяНакладная.ДобавленныеНаборы.НоменклатураНабора.ВариантПечатиНабора = ЗНАЧЕНИЕ(Перечисление.ВариантыПечатиНаборов.ТолькоНабор)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК ВыводитьИтоги
	|	) КАК ТаблицаДобавленныеНаборы,
	|	НЕОПРЕДЕЛЕНО КАК ПлатежныеДокументы,
	|	ЛОЖЬ КАК ЕстьПрослеживаемыеТовары,
	|	ПриходнаяНакладная.СведенияПрослеживаемости.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		РНПТ КАК РНПТ,
	|		Количество КАК Количество,
	|		КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|		ИдентификаторСтроки КАК ИдентификаторСтроки
	|	) КАК СведенияПрослеживаемости
	|ИЗ
	|	Документ.ПриходнаяНакладная КАК ПриходнаяНакладная
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвязиКонтрагентКонтакт.СрезПоследних КАК СвязиКонтрагентКонтакт
	|		ПО (СвязиКонтрагентКонтакт.Контакт = ПриходнаяНакладная.КонтактноеЛицоПодписант)
	|			И (СвязиКонтрагентКонтакт.Контрагент = ПриходнаяНакладная.Контрагент),
	|	Константа.НациональнаяВалюта КАК НациональнаяВалюта,
	|	Константа.ПечатьПользовательскихЕдиницИзмерения КАК ПечатьПользовательскихЕдиницИзмерения
	|ГДЕ
	|	ПриходнаяНакладная.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПриходнаяНакладная.Запасы.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
	|	ВЫРАЗИТЬ(ПриходнаяНакладнаяЗапасы.Содержание КАК СТРОКА(1000)) КАК Содержание,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(ПриходнаяНакладнаяЗапасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|			ТОГДА ПриходнаяНакладнаяЗапасы.Номенклатура.Наименование
	|		ИНАЧЕ ВЫРАЗИТЬ(ПриходнаяНакладнаяЗапасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|	КОНЕЦ КАК ПредставлениеНоменклатуры,
	|	ПриходнаяНакладнаяЗапасы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ПриходнаяНакладнаяЗапасы.Номенклатура.Код КАК ЗапасКод,
	|	ПриходнаяНакладнаяЗапасы.Номенклатура.Артикул КАК Артикул,
	|	ПриходнаяНакладнаяЗапасы.Номенклатура.Штрихкод КАК Штрихкод,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|				И ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|			ТОГДА ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Наименование
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.Номенклатура.ЕдиницаИзмерения.Наименование
	|	КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Наименование,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|				И ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|			ТОГДА ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Код
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.Номенклатура.ЕдиницаИзмерения.Код
	|	КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Код,
	|	ПриходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|				И ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|			ТОГДА ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.Номенклатура.ЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения КАК ВидУпаковки,
	|	ПриходнаяНакладнаяЗапасы.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				И ПечатьПользовательскихЕдиницИзмерения.Значение <> ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|			ТОГДА ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения.Коэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КоэффициентЕдиницыИзмерения,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|				И ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|			ТОГДА 1
	|		КОГДА ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.КлассификаторЕдиницИзмерения
	|			ТОГДА 1
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК КоличествоВОдномМесте,
	|	ПриходнаяНакладнаяЗапасы.Количество КАК КоличествоМест,
	|	ПриходнаяНакладнаяЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	0 КАК Вес,
	|	ПриходнаяНакладнаяЗапасы.Цена КАК Цена,
	|	ПриходнаяНакладнаяЗапасы.НомерСтроки КАК НомерСтроки,
	|	ПриходнаяНакладнаяЗапасы.Сумма КАК Сумма,
	|	ПриходнаяНакладнаяЗапасы.СуммаНДС КАК СуммаНДС,
	|	ПриходнаяНакладнаяЗапасы.Всего КАК Всего,
	|	ПриходнаяНакладнаяЗапасы.Ссылка КАК Ссылка,
	|	ПриходнаяНакладнаяЗапасы.НоменклатураНабора КАК НоменклатураНабора,
	|	ПриходнаяНакладнаяЗапасы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ЭтоНабор,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				И ПриходнаяНакладнаяЗапасы.НоменклатураНабора.ВариантПечатиНабора = ЗНАЧЕНИЕ(Перечисление.ВариантыПечатиНаборов.НаборИКомплектующие)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеобходимоВыделитьКакСоставНабора,
	|	ПриходнаяНакладнаяЗапасы.СтранаПроисхождения КАК СтранаСсылка,
	|	ПРЕДСТАВЛЕНИЕ(ПриходнаяНакладнаяЗапасы.СтранаПроисхождения) КАК СтранаПредставление,
	|	ПриходнаяНакладнаяЗапасы.СтранаПроисхождения.Код КАК СтранаКод,
	|	ПриходнаяНакладнаяЗапасы.НомерГТД.РегистрационныйНомер КАК ПредставлениеГТД,
	|	ПриходнаяНакладнаяЗапасы.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	ПриходнаяНакладнаяЗапасы.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ПриходнаяНакладнаяЗапасы.Ссылка.НомерВходящегоДокумента КАК НомерОтгрузочногоДокумента,
	|	ПриходнаяНакладнаяЗапасы.Ссылка.ДатаВходящегоДокумента КАК ДатаОтгрузочногоДокумента,
	|	ПриходнаяНакладнаяЗапасы.Номенклатура.ТоварнаяНоменклатураВЭД КАК КодТНВЭД
	|ПОМЕСТИТЬ ВременнаяТаблица_ПриходнаяНакладнаяЗапасы
	|ИЗ
	|	Документ.ПриходнаяНакладная.Запасы КАК ПриходнаяНакладнаяЗапасы,
	|	Константа.ПечатьПользовательскихЕдиницИзмерения КАК ПечатьПользовательскихЕдиницИзмерения
	|ГДЕ
	|	ПриходнаяНакладнаяЗапасы.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
	|	ПриходнаяНакладнаяЗапасы.Содержание КАК Содержание,
	|	ПриходнаяНакладнаяЗапасы.ПредставлениеНоменклатуры КАК ПредставлениеНоменклатуры,
	|	ПриходнаяНакладнаяЗапасы.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ПриходнаяНакладнаяЗапасы.ЗапасКод КАК ЗапасКод,
	|	ПриходнаяНакладнаяЗапасы.ЗапасКод КАК Код,
	|	ПриходнаяНакладнаяЗапасы.Артикул КАК Артикул,
	|	ПриходнаяНакладнаяЗапасы.ЕдиницаИзмеренияПоОКЕИ_Наименование КАК ЕдиницаИзмеренияПоОКЕИ_Наименование,
	|	ПриходнаяНакладнаяЗапасы.ЕдиницаИзмеренияПоОКЕИ_Код КАК ЕдиницаИзмеренияПоОКЕИ_Код,
	|	ПриходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
	|	ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения КАК ВидУпаковки,
	|	ПриходнаяНакладнаяЗапасы.Количество КАК Количество,
	|	ПриходнаяНакладнаяЗапасы.КоэффициентЕдиницыИзмерения КАК КоэффициентЕдиницыИзмерения,
	|	ПриходнаяНакладнаяЗапасы.КоличествоВОдномМесте КАК КоличествоВОдномМесте,
	|	ПриходнаяНакладнаяЗапасы.КоличествоМест КАК КоличествоМест,
	|	ПриходнаяНакладнаяЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ПриходнаяНакладнаяЗапасы.Вес КАК МассаБрутто,
	|	ПриходнаяНакладнаяЗапасы.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходнаяНакладнаяЗапасы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|						ТОГДА ПриходнаяНакладнаяЗапасы.Цена
	|					КОГДА СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|						ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяЗапасы.Цена * ПриходнаяНакладнаяЗапасы.Ссылка.Курс / ПриходнаяНакладнаяЗапасы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ПриходнаяНакладнаяЗапасы.Количество = 0
	|								ТОГДА СуммыДокументовРегламентированныйУчет.Всего - СуммыДокументовРегламентированныйУчет.НДС
	|							ИНАЧЕ ВЫРАЗИТЬ((СуммыДокументовРегламентированныйУчет.Всего - СуммыДокументовРегламентированныйУчет.НДС) / ПриходнаяНакладнаяЗапасы.Количество КАК ЧИСЛО(15, 2))
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.Цена
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходнаяНакладнаяЗапасы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|						ТОГДА ПриходнаяНакладнаяЗапасы.Сумма
	|					КОГДА СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|						ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяЗапасы.Сумма * ПриходнаяНакладнаяЗапасы.Ссылка.Курс / ПриходнаяНакладнаяЗапасы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ПриходнаяНакладнаяЗапасы.Ссылка.СуммаВключаетНДС
	|								ТОГДА ВЫРАЗИТЬ(СуммыДокументовРегламентированныйУчет.Всего КАК ЧИСЛО(15, 2))
	|							ИНАЧЕ ВЫРАЗИТЬ(СуммыДокументовРегламентированныйУчет.Всего - СуммыДокументовРегламентированныйУчет.НДС КАК ЧИСЛО(15, 2))
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходнаяНакладнаяЗапасы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|						ТОГДА ПриходнаяНакладнаяЗапасы.СуммаНДС
	|					КОГДА СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|						ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяЗапасы.СуммаНДС * ПриходнаяНакладнаяЗапасы.Ссылка.Курс / ПриходнаяНакладнаяЗапасы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ СуммыДокументовРегламентированныйУчет.НДС
	|				КОНЕЦ
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.СуммаНДС
	|	КОНЕЦ КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходнаяНакладнаяЗапасы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|						ТОГДА ПриходнаяНакладнаяЗапасы.Всего
	|					КОГДА СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|						ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяЗапасы.Всего * ПриходнаяНакладнаяЗапасы.Ссылка.Курс / ПриходнаяНакладнаяЗапасы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ СуммыДокументовРегламентированныйУчет.Всего
	|				КОНЕЦ
	|		ИНАЧЕ ПриходнаяНакладнаяЗапасы.Всего
	|	КОНЕЦ КАК Всего,
	|	ПриходнаяНакладнаяЗапасы.Ссылка КАК Ссылка,
	|	ПриходнаяНакладнаяЗапасы.НоменклатураНабора КАК НоменклатураНабора,
	|	ПриходнаяНакладнаяЗапасы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ПриходнаяНакладнаяЗапасы.ЭтоНабор КАК ЭтоНабор,
	|	ПриходнаяНакладнаяЗапасы.НеобходимоВыделитьКакСоставНабора КАК НеобходимоВыделитьКакСоставНабора,
	|	ПриходнаяНакладнаяЗапасы.СтранаСсылка КАК СтранаСсылка,
	|	ПриходнаяНакладнаяЗапасы.СтранаПредставление КАК СтранаПредставление,
	|	ПриходнаяНакладнаяЗапасы.СтранаКод КАК СтранаКод,
	|	ПриходнаяНакладнаяЗапасы.ПредставлениеГТД КАК ПредставлениеГТД,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ПриходнаяНакладнаяЗапасы.Номенклатура.ТоварнаяНоменклатураВЭД.Код
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КодТНВЭД,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяЗапасы.ПрослеживаемыйТовар
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПрослеживаемыйТоварЧисло,
	|	ПриходнаяНакладнаяЗапасы.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ПриходнаяНакладнаяЗапасы.НомерОтгрузочногоДокумента КАК НомерОтгрузочногоДокумента,
	|	ПриходнаяНакладнаяЗапасы.ДатаОтгрузочногоДокумента КАК ДатаОтгрузочногоДокумента,
	|	ПриходнаяНакладнаяЗапасы.КодТНВЭД КАК КодТНВЭД1,
	|	ПриходнаяНакладнаяЗапасы.КодТНВЭД.Код КАК КодТНВЭДКод,
	|	ПриходнаяНакладнаяЗапасы.КодТНВЭД.ЕдиницаИзмерения КАК КодТНВЭДЕдиницаИзмерения,
	|	ПриходнаяНакладнаяЗапасы.КодТНВЭД.ЕдиницаИзмерения.Код КАК КодТНВЭДЕдиницаИзмеренияКод,
	|	ПриходнаяНакладнаяЗапасы.КодТНВЭД.ЕдиницаИзмерения.Наименование КАК КодТНВЭДЕдиницаИзмеренияНаименование,
	|	ПриходнаяНакладнаяЗапасы.КодТНВЭД.ЕдиницаИзмерения.НаименованиеПолное КАК КодТНВЭДЕдиницаИзмеренияНаименованиеПолное,
	|	ПриходнаяНакладнаяЗапасы.КодТНВЭД.ЕдиницаИзмерения.МеждународноеСокращение КАК КодТНВЭДЕдиницаИзмеренияМеждународноеСокращение
	|ИЗ
	|	ВременнаяТаблица_ПриходнаяНакладнаяЗапасы КАК ПриходнаяНакладнаяЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовРегламентированныйУчет КАК СуммыДокументовРегламентированныйУчет
	|		ПО ПриходнаяНакладнаяЗапасы.Ссылка = СуммыДокументовРегламентированныйУчет.Регистратор
	|			И ПриходнаяНакладнаяЗапасы.НомерСтроки = СуммыДокументовРегламентированныйУчет.НомерСтрокиДокумента
	|			И (СуммыДокументовРегламентированныйУчет.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Запасы))
	|ГДЕ
	|	ПриходнаяНакладнаяЗапасы.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриходнаяНакладнаяРасходы.Номенклатура КАК Номенклатура,
	|	ВЫРАЗИТЬ(ПриходнаяНакладнаяРасходы.Содержание КАК СТРОКА(1000)) КАК Содержание,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(ПриходнаяНакладнаяРасходы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|			ТОГДА ПриходнаяНакладнаяРасходы.Номенклатура.Наименование
	|		ИНАЧЕ ВЫРАЗИТЬ(ПриходнаяНакладнаяРасходы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|	КОНЕЦ КАК ПредставлениеНоменклатуры,
	|	ПриходнаяНакладнаяРасходы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ПриходнаяНакладнаяРасходы.Номенклатура.Код КАК ЗапасКод,
	|	ПриходнаяНакладнаяРасходы.Номенклатура.Артикул КАК Артикул,
	|	ПриходнаяНакладнаяРасходы.Номенклатура.Штрихкод КАК Штрихкод,
	|	НЕОПРЕДЕЛЕНО КАК КодТНВЭД,
	|	НЕОПРЕДЕЛЕНО КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|				И ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|			ТОГДА ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Наименование
	|		ИНАЧЕ ПриходнаяНакладнаяРасходы.Номенклатура.ЕдиницаИзмерения.Наименование
	|	КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Наименование,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|				И ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|			ТОГДА ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Код
	|		ИНАЧЕ ПриходнаяНакладнаяРасходы.Номенклатура.ЕдиницаИзмерения.Код
	|	КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Код,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|				И ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|			ТОГДА ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору
	|		ИНАЧЕ ПриходнаяНакладнаяРасходы.Номенклатура.ЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	НЕОПРЕДЕЛЕНО КАК ВидУпаковки,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				И ПечатьПользовательскихЕдиницИзмерения.Значение <> ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|			ТОГДА ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения.Коэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КоэффициентЕдиницыИзмерения,
	|	ПриходнаяНакладнаяРасходы.Количество КАК Количество,
	|	НЕОПРЕДЕЛЕНО КАК КоличествоВОдномМесте,
	|	НЕОПРЕДЕЛЕНО КАК КоличествоМест,
	|	ПриходнаяНакладнаяРасходы.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходнаяНакладнаяРасходы.Ссылка.Договор.РасчетыВУсловныхЕдиницах
	|							И ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента <> &НациональнаяВалюта
	|						ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяРасходы.Цена * ПриходнаяНакладнаяРасходы.Ссылка.Курс / ПриходнаяНакладнаяРасходы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ ПриходнаяНакладнаяРасходы.Цена
	|				КОНЕЦ
	|		ИНАЧЕ ПриходнаяНакладнаяРасходы.Цена
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходнаяНакладнаяРасходы.Ссылка.Договор.РасчетыВУсловныхЕдиницах
	|							И ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента <> &НациональнаяВалюта
	|						ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяРасходы.Сумма * ПриходнаяНакладнаяРасходы.Ссылка.Курс / ПриходнаяНакладнаяРасходы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ ПриходнаяНакладнаяРасходы.Сумма
	|				КОНЕЦ
	|		ИНАЧЕ ПриходнаяНакладнаяРасходы.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходнаяНакладнаяРасходы.Ссылка.Договор.РасчетыВУсловныхЕдиницах
	|							И ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента <> &НациональнаяВалюта
	|						ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяРасходы.СуммаНДС * ПриходнаяНакладнаяРасходы.Ссылка.Курс / ПриходнаяНакладнаяРасходы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ ПриходнаяНакладнаяРасходы.СуммаНДС
	|				КОНЕЦ
	|		ИНАЧЕ ПриходнаяНакладнаяРасходы.СуммаНДС
	|	КОНЕЦ КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходнаяНакладнаяРасходы.Ссылка.Договор.РасчетыВУсловныхЕдиницах
	|							И ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента <> &НациональнаяВалюта
	|						ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяРасходы.Всего * ПриходнаяНакладнаяРасходы.Ссылка.Курс / ПриходнаяНакладнаяРасходы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ ПриходнаяНакладнаяРасходы.Всего
	|				КОНЕЦ
	|		ИНАЧЕ ПриходнаяНакладнаяРасходы.Всего
	|	КОНЕЦ КАК Всего,
	|	ПриходнаяНакладнаяРасходы.НомерСтроки КАК НомерСтроки,
	|	ПриходнаяНакладнаяРасходы.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО КАК НоменклатураНабора,
	|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ЭтоНабор,
	|	ЛОЖЬ КАК НеобходимоВыделитьКакСоставНабора
	|ПОМЕСТИТЬ ВременнаяТаблица_ПриходнаяНакладнаяРасходы
	|ИЗ
	|	Документ.ПриходнаяНакладная.Расходы КАК ПриходнаяНакладнаяРасходы,
	|	Константа.ПечатьПользовательскихЕдиницИзмерения КАК ПечатьПользовательскихЕдиницИзмерения
	|ГДЕ
	|	ПриходнаяНакладнаяРасходы.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриходнаяНакладнаяРасходы.Номенклатура КАК Номенклатура,
	|	ПриходнаяНакладнаяРасходы.Содержание КАК Содержание,
	|	ПриходнаяНакладнаяРасходы.ПредставлениеНоменклатуры КАК ПредставлениеНоменклатуры,
	|	ПриходнаяНакладнаяРасходы.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ПриходнаяНакладнаяРасходы.ЗапасКод КАК ЗапасКод,
	|	ПриходнаяНакладнаяРасходы.ЗапасКод КАК Код,
	|	ПриходнаяНакладнаяРасходы.Артикул КАК Артикул,
	|	ПриходнаяНакладнаяРасходы.ЕдиницаИзмеренияПоОКЕИ_Наименование КАК ЕдиницаИзмеренияПоОКЕИ_Наименование,
	|	ПриходнаяНакладнаяРасходы.ЕдиницаИзмеренияПоОКЕИ_Код КАК ЕдиницаИзмеренияПоОКЕИ_Код,
	|	ПриходнаяНакладнаяРасходы.Характеристика КАК Характеристика,
	|	ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПриходнаяНакладнаяРасходы.ЕдиницаИзмерения КАК ВидУпаковки,
	|	ПриходнаяНакладнаяРасходы.Количество КАК Количество,
	|	ПриходнаяНакладнаяРасходы.КоэффициентЕдиницыИзмерения КАК КоэффициентЕдиницыИзмерения,
	|	ПриходнаяНакладнаяРасходы.КоличествоВОдномМесте КАК КоличествоВОдномМесте,
	|	ПриходнаяНакладнаяРасходы.КоличествоМест КАК КоличествоМест,
	|	ПриходнаяНакладнаяРасходы.СтавкаНДС КАК СтавкаНДС,
	|	ПриходнаяНакладнаяРасходы.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|						ТОГДА ПриходнаяНакладнаяРасходы.Цена
	|					КОГДА СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|						ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяРасходы.Цена * ПриходнаяНакладнаяРасходы.Ссылка.Курс / ПриходнаяНакладнаяРасходы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ПриходнаяНакладнаяРасходы.Количество = 0
	|								ТОГДА СуммыДокументовРегламентированныйУчет.Всего - СуммыДокументовРегламентированныйУчет.НДС
	|							ИНАЧЕ ВЫРАЗИТЬ((СуммыДокументовРегламентированныйУчет.Всего - СуммыДокументовРегламентированныйУчет.НДС) / ПриходнаяНакладнаяРасходы.Количество КАК ЧИСЛО(15, 2))
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ПриходнаяНакладнаяРасходы.Цена
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|						ТОГДА ПриходнаяНакладнаяРасходы.Сумма
	|					КОГДА СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|						ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяРасходы.Сумма * ПриходнаяНакладнаяРасходы.Ссылка.Курс / ПриходнаяНакладнаяРасходы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ПриходнаяНакладнаяРасходы.Ссылка.СуммаВключаетНДС
	|								ТОГДА ВЫРАЗИТЬ(СуммыДокументовРегламентированныйУчет.Всего КАК ЧИСЛО(15, 2))
	|							ИНАЧЕ ВЫРАЗИТЬ(СуммыДокументовРегламентированныйУчет.Всего - СуммыДокументовРегламентированныйУчет.НДС КАК ЧИСЛО(15, 2))
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ПриходнаяНакладнаяРасходы.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|						ТОГДА ПриходнаяНакладнаяРасходы.СуммаНДС
	|					КОГДА СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|						ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяРасходы.СуммаНДС * ПриходнаяНакладнаяРасходы.Ссылка.Курс / ПриходнаяНакладнаяРасходы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ СуммыДокументовРегламентированныйУчет.НДС
	|				КОНЕЦ
	|		ИНАЧЕ ПриходнаяНакладнаяРасходы.СуммаНДС
	|	КОНЕЦ КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях
	|			ТОГДА ВЫБОР
	|					КОГДА ПриходнаяНакладнаяРасходы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|						ТОГДА ПриходнаяНакладнаяРасходы.Всего
	|					КОГДА СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|						ТОГДА ВЫРАЗИТЬ(ПриходнаяНакладнаяРасходы.Всего * ПриходнаяНакладнаяРасходы.Ссылка.Курс / ПриходнаяНакладнаяРасходы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ СуммыДокументовРегламентированныйУчет.Всего
	|				КОНЕЦ
	|		ИНАЧЕ ПриходнаяНакладнаяРасходы.Всего
	|	КОНЕЦ КАК Всего,
	|	ПриходнаяНакладнаяРасходы.Ссылка КАК Ссылка,
	|	ПриходнаяНакладнаяРасходы.Ссылка.НомерВходящегоДокумента КАК НомерОтгрузочногоДокумента,
	|	ПриходнаяНакладнаяРасходы.Ссылка.ДатаВходящегоДокумента КАК ДатаОтгрузочногоДокумента,
	|	ПриходнаяНакладнаяРасходы.НоменклатураНабора КАК НоменклатураНабора,
	|	ПриходнаяНакладнаяРасходы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ПриходнаяНакладнаяРасходы.ЭтоНабор КАК ЭтоНабор,
	|	НЕОПРЕДЕЛЕНО КАК СтранаСсылка,
	|	НЕОПРЕДЕЛЕНО КАК СтранаПредставление,
	|	НЕОПРЕДЕЛЕНО КАК СтранаКод,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеГТД,
	|	ПриходнаяНакладнаяРасходы.НеобходимоВыделитьКакСоставНабора КАК НеобходимоВыделитьКакСоставНабора,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладнаяРасходы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ПриходнаяНакладнаяРасходы.Номенклатура.ТоварнаяНоменклатураВЭД.Код
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КодТНВЭД
	|ИЗ
	|	ВременнаяТаблица_ПриходнаяНакладнаяРасходы КАК ПриходнаяНакладнаяРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовРегламентированныйУчет КАК СуммыДокументовРегламентированныйУчет
	|		ПО ПриходнаяНакладнаяРасходы.Ссылка = СуммыДокументовРегламентированныйУчет.Регистратор
	|			И ПриходнаяНакладнаяРасходы.НомерСтроки = СуммыДокументовРегламентированныйУчет.НомерСтрокиДокумента
	|			И (СуммыДокументовРегламентированныйУчет.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Расходы))
	|ГДЕ
	|	ПриходнаяНакладнаяРасходы.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	Результат = МассивРезультатов[0].Выгрузить();
	
	ТЗСтрокЗапасы = МассивРезультатов[2].Выгрузить();
	ТЗСтрокРасходы = МассивРезультатов[4].Выгрузить();
	
	Для Каждого ТекущаяСтрока Из Результат Цикл
		
		НайденныеСтроки = ТЗСтрокЗапасы.НайтиСтроки(Новый Структура("Ссылка", ТекущаяСтрока.Ссылка));
		Если НайденныеСтроки.Количество() > 0 Тогда
			ТекущаяСтрока.ТаблицаЗапасы = ТЗСтрокЗапасы.Скопировать(НайденныеСтроки);
			
			ТекущаяСтрока.ЕстьПрослеживаемыеТовары = (ТекущаяСтрока.ТаблицаЗапасы.Итог("ПрослеживаемыйТоварЧисло") > 0);
			Если ТекущаяСтрока.ЕстьПрослеживаемыеТовары Тогда
				ДополнитьСведениямиОПрослеживаемости(ТекущаяСтрока.СведенияПрослеживаемости, ТекущаяСтрока.ТаблицаЗапасы);
			КонецЕсли;

		КонецЕсли;
		
		НайденныеСтроки = ТЗСтрокРасходы.НайтиСтроки(Новый Структура("Ссылка", ТекущаяСтрока.Ссылка));
		Если НайденныеСтроки.Количество() > 0 Тогда
			ТекущаяСтрока.ТаблицаРасходы = ТЗСтрокРасходы.Скопировать(НайденныеСтроки);
		КонецЕсли;
		
		// Для приходной накладной расходы должны попасть в табличную часть запасы
		НомерДобавляемойСтроки = ТекущаяСтрока.ТаблицаЗапасы.Количество();
		Для каждого СтрокаРасходов Из ТекущаяСтрока.ТаблицаРасходы Цикл
			
			НоваяСтрока = ТекущаяСтрока.ТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасходов);
			
			НомерДобавляемойСтроки = НомерДобавляемойСтроки + 1;
			НоваяСтрока.НомерСтроки = НомерДобавляемойСтроки;
			
		КонецЦикла;
	КонецЦикла;
	
	// Наборы
	НаборыСервер.КомпоноватьТабличнуюЧастьПоНаборам(Результат, "ТаблицаЗапасы", Ошибки);
	НаборыСервер.КомпоноватьТабличнуюЧастьПоНаборам(Результат, "ТаблицаРасходы", Ошибки);
	
	Возврат Результат;
	
КонецФункции

Процедура ДополнитьСведениямиОПрослеживаемости(СведенияОПрослеживаемости, ТаблицаДокумента)
	
	МассивИдентифицирующихРеквизитов = Новый Массив();
	МассивИдентифицирующихРеквизитов.Добавить("ИдентификаторСтроки");
	
	ТаблицаДокумента.Колонки.Добавить("СведенияОПрослеживаемости");
	
	Для Каждого СтрокаТаблицыДокумента Из ТаблицаДокумента Цикл
		
		ТаблицаСведенийОПрослеживаемости = НовыйТаблицаСведенийОПрослеживаемости();
		
		ПараметрыОтбора = ПараметрыОтбораСтрокДляРНПТ(МассивИдентифицирующихРеквизитов, СтрокаТаблицыДокумента);
		СтрокиСРНПТ = СведенияОПрослеживаемости.НайтиСтроки(ПараметрыОтбора);
		Для Каждого НайденнаяСтрока Из СтрокиСРНПТ Цикл
			СтрокаСведенийОПрослеживаемости = ТаблицаСведенийОПрослеживаемости.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаСведенийОПрослеживаемости, НайденнаяСтрока);
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ТаблицаСведенийОПрослеживаемости) Тогда
			СтрокаТаблицыДокумента.СведенияОПрослеживаемости = ТаблицаСведенийОПрослеживаемости;
		Иначе
			СтрокаТаблицыДокумента.СведенияОПрослеживаемости = Неопределено;;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПараметрыОтбораСтрокДляРНПТ(ИдентифицирующиеРеквизитыСтроки, СтрокаСчетаФактуры)
	
	СтруктураОтбора = Новый Структура;
	Для Каждого ИдентифицирующийРеквизит Из ИдентифицирующиеРеквизитыСтроки Цикл
		СтруктураОтбора.Вставить(ИдентифицирующийРеквизит,СтрокаСчетаФактуры[ИдентифицирующийРеквизит]); 
	КонецЦикла;
	
	Возврат СтруктураОтбора;
	
КонецФункции

Функция НовыйТаблицаСведенийОПрослеживаемости()
	
	ТаблицаСведенийОПрослеживаемости = Новый ТаблицаЗначений();
	ТаблицаСведенийОПрослеживаемости.Колонки.Добавить("ИдентификаторСтроки");
	ТаблицаСведенийОПрослеживаемости.Колонки.Добавить("РНПТ");
	ТаблицаСведенийОПрослеживаемости.Колонки.Добавить("Количество");
	ТаблицаСведенийОПрослеживаемости.Колонки.Добавить("КоличествоПрослеживаемости");
	
	Возврат ТаблицаСведенийОПрослеживаемости;
	
КонецФункции

Функция УниверсальныйЗапросПоДаннымДокумента(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПредставлениеРегистратора", НСтр("ru = 'Приходная накладная'"));
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПриходнаяНакладная.Ссылка КАК Ссылка,
	|	&ПредставлениеРегистратора КАК ПредставлениеРегистратора,
	|	ПриходнаяНакладная.Дата КАК ДатаДокумента,
	|	ПриходнаяНакладная.Номер КАК Номер,
	|	ПриходнаяНакладная.Ячейка КАК ПредставлениеЯчейки,
	|	ПриходнаяНакладная.Организация КАК Организация,
	|	ПриходнаяНакладная.Организация.КодПоОКПО КАК ОрганизацияПоОКПО,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиПоОКДП,
	|	ПриходнаяНакладная.Организация.Префикс КАК Префикс,
	|	ПриходнаяНакладная.Подразделение КАК ПредставлениеПодразделения,
	|	ПриходнаяНакладная.СтруктурнаяЕдиница КАК ПредставлениеСклада,
	|	ПриходнаяНакладная.СтруктурнаяЕдиница.УсловияХранения КАК УсловияХранения,
	|	ПриходнаяНакладная.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ПриходнаяНакладная.ВалютаДокумента КАК ВалютаДокумента,
	|	ПриходнаяНакладная.Курс КАК Курс,
	|	ПриходнаяНакладная.Кратность КАК Кратность,
	|	ПриходнаяНакладная.Контрагент КАК Контрагент,
	|	ПриходнаяНакладная.Контрагент.КодПоОКПО КАК ПоклажедательПоОКПО,
	|	ПриходнаяНакладная.СрокХранения КАК СрокХранения,
	|	НЕОПРЕДЕЛЕНО КАК ВидОперации,
	|	""Запасы"" КАК ТипНоменклатурыТаблицы,
	|	ПриходнаяНакладная.КонтактноеЛицоПодписант.Наименование КАК РасшифровкаПодписиКонтрагента,
	|	ПриходнаяНакладная.Договор.НомерДоговора КАК ДоговорНомер,
	|	ПриходнаяНакладная.Договор.ДатаДоговора КАК ДоговорДата,
	|	ПриходнаяНакладная.ПодписьКладовщика.Должность КАК ДолжностьКладовщика,
	|	ПриходнаяНакладная.ПодписьКладовщика.РасшифровкаПодписи КАК РасшифровкаПодписиКладовщика,
	|	ПриходнаяНакладная.Запасы.(
	|		НомерСтроки КАК НомерСтроки,
	|		Содержание КАК Содержание,
	|		ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(ПриходнаяНакладная.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(100))) = """"
	|				ТОГДА ПриходнаяНакладная.Запасы.Номенклатура.Наименование
	|			ИНАЧЕ ПриходнаяНакладная.Запасы.Номенклатура.НаименованиеПолное
	|		КОНЕЦ КАК ПредставлениеНоменклатуры,
	|		Номенклатура.Артикул КАК Артикул,
	|		Номенклатура.Штрихкод КАК Штрихкод,
	|		Номенклатура.Код КАК Код,
	|		Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|		Номенклатура.Склад КАК Склад,
	|		Номенклатура.Ячейка КАК Ячейка,
	|		Характеристика КАК Характеристика,
	|		Партия КАК Партия,
	|		ВЫБОР
	|			КОГДА ПриходнаяНакладная.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И ПриходнаяНакладная.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА ПриходнаяНакладная.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Наименование
	|			ИНАЧЕ ПриходнаяНакладная.Запасы.Номенклатура.ЕдиницаИзмерения.Наименование
	|		КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Наименование,
	|		ВЫБОР
	|			КОГДА ПриходнаяНакладная.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И ПриходнаяНакладная.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА ПриходнаяНакладная.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Код
	|			ИНАЧЕ ПриходнаяНакладная.Запасы.Номенклатура.ЕдиницаИзмерения.Код
	|		КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Код,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ВЫБОР
	|			КОГДА ПриходнаяНакладная.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение <> ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|				ТОГДА ПриходнаяНакладная.Запасы.Количество * ПриходнаяНакладная.Запасы.ЕдиницаИзмерения.Коэффициент
	|			ИНАЧЕ ПриходнаяНакладная.Запасы.Количество
	|		КОНЕЦ КАК КоличествоПоКоэффициенту,
	|		Количество КАК Количество,
	|		Цена КАК Цена,
	|		Сумма КАК Сумма,
	|		СтавкаНДС КАК СтавкаНДС,
	|		СуммаНДС КАК СуммаНДС,
	|		Всего КАК Всего,
	|		0 КАК ПроцентСкидкиНаценки,
	|		0 КАК ЕстьСкидка,
	|		0 КАК СуммаАвтоматическойСкидки,
	|		ЛОЖЬ КАК ЭтоРазделитель,
	|		КлючСвязи КАК КлючСвязи,
	|		Заказ КАК Заказ
	|	) КАК ТаблицаЗапасы,
	|	ПриходнаяНакладная.СерииНоменклатуры.(
	|		Серия КАК Серия,
	|		КлючСвязи КАК КлючСвязи
	|	) КАК ТаблицаСерииНоменклатуры
	|ИЗ
	|	Документ.ПриходнаяНакладная КАК ПриходнаяНакладная,
	|	Константа.ПечатьПользовательскихЕдиницИзмерения КАК ПечатьПользовательскихЕдиницИзмерения
	|ГДЕ
	|	ПриходнаяНакладная.Ссылка В(&МассивОбъектов)";
	
	ДанныеДокументов = Запрос.Выполнить().Выгрузить();
	
	ИспользуемыеТЧ = Новый Соответствие;
	ИспользуемыеТЧ.Вставить("ТаблицаЗапасы", Перечисления.ТабличныеЧастиДокументов.Запасы);
	ПечатьДокументовУНФ.ДобавитьСуммовыеПоляЭквивалентовВНациональнойВалюте(ДанныеДокументов, ИспользуемыеТЧ);
	
	Возврат ДанныеДокументов;
	
КонецФункции

// Функция формирует печатную форму документа.
//
// Параметры:
//	ТабличныйДокумент - ТабличныйДокумент в который будет выводится печатная
//				   форма.
Функция ПечатнаяФормаНакладная(МассивОбъектов, ОбъектыПечати)
	Перем ПервыйДокумент, НомерСтрокиНачало;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПриходнаяНакладная";
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПриходнаяНакладная_Накладная";
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПриходнаяНакладная.ПФ_MXL_ПриходнаяНакладная");
		
	ПредставлениеСкидки = Константы.ПредставлениеСкидкиВПечатнойФорме.Получить();
	
	ПараметрыНоменклатуры = Новый Структура;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриходнаяНакладная.Ссылка КАК Ссылка,
	|	ПриходнаяНакладная.Дата КАК ДатаДокумента,
	|	ПриходнаяНакладная.Контрагент КАК Организация,
	|	ПриходнаяНакладная.Организация КАК Контрагент,
	|	ПриходнаяНакладная.КонтактноеЛицоПодписант.Наименование КАК КонтактноеЛицоПодписант,
	|	ПриходнаяНакладная.ПодписьКладовщика.Должность КАК ДолжностьКладовщика,
	|	ПриходнаяНакладная.ПодписьКладовщика.РасшифровкаПодписи КАК РасшифровкаПодписиКладовщика,
	|	ПриходнаяНакладная.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ПриходнаяНакладная.Номер,
	|	ПриходнаяНакладная.Организация.Префикс КАК Префикс,
	|	ПриходнаяНакладная.ВалютаДокумента КАК ВалютаДокумента,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|			ТОГДА ""(Поступление от поставщика)""
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию)
	|			ТОГДА ""(Прием на комиссию)""
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемВПереработку)
	|			ТОГДА ""(Прием в переработку)""
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаОтветХранение)
	|			ТОГДА ""(Прием от контрагента на ответственное хранение)""
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|			ТОГДА ""(Возврат от покупателя)""
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера)
	|			ТОГДА ""(Возврат от комиссионера)""
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика)
	|			ТОГДА ""(Возврат от переработчика)""
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения)
	|			ТОГДА ""(Возврат от контрагента с ответственного хранения)""
	|	КОНЕЦ КАК ВидОперации,
	|	ПриходнаяНакладная.ВидОперации КАК ВидОперацииСсылка,
	|	ПриходнаяНакладная.Запасы.(
	|		НомерСтроки КАК НомерСтроки,
	|		ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(ПриходнаяНакладная.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|				ТОГДА ПриходнаяНакладная.Запасы.Номенклатура.Наименование
	|			ИНАЧЕ ВЫРАЗИТЬ(ПриходнаяНакладная.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|		КОНЕЦ КАК Запас,
	|		Номенклатура.Код КАК Код,
	|		Номенклатура.Артикул КАК Артикул,
	|		Номенклатура.Штрихкод КАК Штрихкод,
	|		ЕдиницаИзмерения КАК ЕдиницаХранения,
	|		Количество КАК Количество,
	|		Цена КАК Цена,
	|		ПроцентСкидкиНаценки,
	|		0 КАК СуммаАвтоматическойСкидки,
	|		ВЫБОР
	|			КОГДА ПриходнаяНакладная.Запасы.ПроцентСкидкиНаценки <> 0
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ЕстьСкидка,
	|		Сумма КАК Сумма,
	|		СуммаНДС,
	|		Всего,
	|		Характеристика,
	|		Партия,
	|		Содержание,
	|		КлючСвязи,
	|		ЛОЖЬ КАК ЭтоНабор,
	|		ВЫБОР
	|			КОГДА НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|					И НоменклатураНабора.ВариантПечатиНабора = ЗНАЧЕНИЕ(Перечисление.ВариантыПечатиНаборов.НаборИКомплектующие)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК НеобходимоВыделитьКакСоставНабора,
	|		НоменклатураНабора КАК НоменклатураНабора,
	|		ХарактеристикаНабора КАК ХарактеристикаНабора
	|	) КАК ТаблицаЗапасы,
	|	ПриходнаяНакладная.ДобавленныеНаборы.(
	|		НоменклатураНабора КАК НоменклатураНабора,
	|		ХарактеристикаНабора КАК ХарактеристикаНабора,
	|		Количество КАК Количество,
	|		ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(НоменклатураНабора.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|				ТОГДА НоменклатураНабора.Наименование
	|			ИНАЧЕ ВЫРАЗИТЬ(НоменклатураНабора.НаименованиеПолное КАК СТРОКА(1000))
	|		КОНЕЦ КАК ЗапасНабора,
	|		НоменклатураНабора.ВариантПечатиНабора КАК ВариантПечатиНабора,
	|		НоменклатураНабора.ТипНоменклатуры КАК ТипНоменклатурыНабора,
	|		НоменклатураНабора.Артикул КАК АртикулНабора,
	|		НоменклатураНабора.Код КАК КодНабора,
	|		НоменклатураНабора.ЕдиницаИзмерения КАК ЕдиницаИзмеренияНабора,
	|		НоменклатураНабора.ЕдиницаИзмерения.Код КАК КодЕдиницыИзмеренияНабора,
	|		ИСТИНА КАК ВыводитьИтоги
	|	) КАК ТаблицаДобавленныеНаборы,
	|	ПриходнаяНакладная.СерииНоменклатуры.(
	|		Серия,
	|		КлючСвязи
	|	) КАК ТаблицаСерииНоменклатуры
	|ИЗ
	|	Документ.ПриходнаяНакладная КАК ПриходнаяНакладная
	|ГДЕ
	|	ПриходнаяНакладная.Ссылка В (&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО Ссылка, НомерСтроки";
		
	ДанныеДокументов = Запрос.Выполнить().Выгрузить();
	
	// Наборы
	НаборыСервер.КомпоноватьТабличнуюЧастьПоНаборам(ДанныеДокументов, "ТаблицаЗапасы");
	
	Для Каждого ДанныеДокумента Из ДанныеДокументов Цикл

		ПечатьДокументовУНФ.ПередНачаломФормированияДокумента(ТабличныйДокумент, ПервыйДокумент, НомерСтрокиНачало);

		СведенияОбОрганизации = ПечатьДокументовУНФ.СведенияОЮрФизЛице(ДанныеДокумента.Организация,
			ДанныеДокумента.ДатаДокумента, , );
		СведенияОбКонтрагенте = ПечатьДокументовУНФ.СведенияОЮрФизЛице(ДанныеДокумента.Контрагент,
			ДанныеДокумента.ДатаДокумента, , );

		НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(ДанныеДокумента.ДатаДокумента,
			ДанныеДокумента.Номер, ДанныеДокумента.Префикс);

		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		ОбластьЗаголовок.Параметры.ТекстЗаголовка = СтрШаблон(НСтр("ru = 'Приходная накладная № %1 от %2'"),
			НомерДокумента, Формат(ДанныеДокумента.ДатаДокумента, "ДЛФ=DD"));

		ОбластьЗаголовок.Параметры.Заполнить(ДанныеДокумента);
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьЗаголовок,
			ДанныеДокумента.Ссылка);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);

		ОбластьПоставщик = Макет.ПолучитьОбласть("Поставщик");
		ОбластьПоставщик.Параметры.Заполнить(ДанныеДокумента);
		ОбластьПоставщик.Параметры.ПредставлениеПоставщика = ПечатьДокументовУНФ.ОписаниеОрганизации(
			СведенияОбОрганизации, "ПолноеНаименование,ИНН,КПП,РегистрационныйНомер,ЮридическийАдрес,Телефоны,");
		ТабличныйДокумент.Вывести(ОбластьПоставщик);

		ОбластьПокупатель = Макет.ПолучитьОбласть("Покупатель");
		ОбластьПокупатель.Параметры.ПредставлениеПолучателя = ПечатьДокументовУНФ.ОписаниеОрганизации(
			СведенияОбКонтрагенте, "ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,");
		ТабличныйДокумент.Вывести(ОбластьПокупатель);

		ЕстьСкидки = ДанныеДокумента.ТаблицаЗапасы.Итог("ЕстьСкидка") <> 0;

		Если ЕстьСкидки Тогда

			ОбластьШапка = Макет.ПолучитьОбласть("ШапкаТаблицыСоСкидкой");
			ТабличныйДокумент.Вывести(ОбластьШапка);
			ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаСоСкидкой");

		Иначе

			ОбластьШапка = Макет.ПолучитьОбласть("ШапкаТаблицы");
			ТабличныйДокумент.Вывести(ОбластьШапка);
			ОбластьСтрока = Макет.ПолучитьОбласть("Строка");

		КонецЕсли;

		Итоги = Новый Структура;
		Итоги.Вставить("Сумма", 0);
		Итоги.Вставить("СуммаНДС", 0);
		Итоги.Вставить("Всего", 0);
		Итоги.Вставить("Количество", 0);
		Итоги.Вставить("НомерСтроки", 0);
		Итоги.Вставить("ЕстьСкидки", ЕстьСкидки);
		Итоги.Вставить("ПредставлениеСкидки", ПредставлениеСкидки);
		ДанныеПечати = Новый Структура;

		Для Каждого СтрокаЗапасы Из ДанныеДокумента.ТаблицаЗапасы Цикл

			Если СтрокаЗапасы.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;

			ЗаполнитьДанныеПечатиПоСтрокеТабличнойЧасти(СтрокаЗапасы, ДанныеПечати, ПараметрыНоменклатуры, Итоги,
				ДанныеДокумента);

			ОбластьСтрока.Параметры.Заполнить(СтрокаЗапасы);
			ОбластьСтрока.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			// Наборы
			НаборыСервер.УчестьОформлениеСтрокиНабора(ТабличныйДокумент, ОбластьСтрока, СтрокаЗапасы);

		КонецЦикла;

		ВывестиОбластиИтогиНДССуммаПрописью(Итоги, Макет, ДанныеДокумента, ТабличныйДокумент);
		
		ОбластьПодписи = Макет.ПолучитьОбласть("Подписи");
		ОбластьПодписи.Параметры.Заполнить(ДанныеДокумента);
		ТабличныйДокумент.Вывести(ОбластьПодписи);

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати,
			ДанныеДокумента.Ссылка);

	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Функция формирует печатную форму документа.
//
// Параметры:
//	ТабличныйДокумент - ТабличныйДокумент в который будет выводится печатная
//				   форма.
Функция ПечатнаяФормаНакладнаяСРасходами(МассивОбъектов, ОбъектыПечати)
	Перем ПервыйДокумент, НомерСтрокиНачало;

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПриходнаяНакладная";
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПриходнаяНакладная_Накладная";
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПриходнаяНакладная.ПФ_MXL_ПриходнаяНакладная");

	ПредставлениеСкидки = Константы.ПредставлениеСкидкиВПечатнойФорме.Получить();

	ПараметрыНоменклатуры = Новый Структура;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриходнаяНакладная.Ссылка КАК Ссылка,
	|	ПриходнаяНакладная.Дата КАК ДатаДокумента,
	|	ПриходнаяНакладная.Организация КАК Контрагент,
	|	ПриходнаяНакладная.Организация.Префикс КАК Префикс,
	|	ПриходнаяНакладная.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ПриходнаяНакладная.Номер,
	|	ПриходнаяНакладная.Контрагент КАК Организация,
	|	ПриходнаяНакладная.КонтактноеЛицоПодписант.Наименование КАК КонтактноеЛицоПодписант,
	|	ПриходнаяНакладная.ПодписьКладовщика.Должность КАК ДолжностьКладовщика,
	|	ПриходнаяНакладная.ПодписьКладовщика.РасшифровкаПодписи КАК РасшифровкаПодписиКладовщика,
	|	ПриходнаяНакладная.ВалютаДокумента КАК ВалютаДокумента,
	|	ВЫБОР
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
	|			ТОГДА ""(Поступление от поставщика)""
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию)
	|			ТОГДА ""(Прием на комиссию)""
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемВПереработку)
	|			ТОГДА ""(Прием в переработку)""
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаОтветХранение)
	|			ТОГДА ""(Прием от контрагента на ответственное хранение)""
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
	|			ТОГДА ""(Возврат от покупателя)""
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера)
	|			ТОГДА ""(Возврат от комиссионера)""
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика)
	|			ТОГДА ""(Возврат от переработчика)""
	|		КОГДА ПриходнаяНакладная.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения)
	|			ТОГДА ""(Возврат от контрагента с ответственного хранения)""
	|	КОНЕЦ КАК ВидОперации,
	|	ПриходнаяНакладная.ВидОперации КАК ВидОперацииСсылка,
	|	ПриходнаяНакладная.Запасы.(
	|		НомерСтроки КАК НомерСтроки,
	|		ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(ПриходнаяНакладная.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|				ТОГДА ПриходнаяНакладная.Запасы.Номенклатура.Наименование
	|			ИНАЧЕ ВЫРАЗИТЬ(ПриходнаяНакладная.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|		КОНЕЦ КАК Запас,
	|		Номенклатура.Артикул КАК Артикул,
	|		Номенклатура.Штрихкод КАК Штрихкод,
	|		Номенклатура.Код КАК Код,
	|		ЕдиницаИзмерения КАК ЕдиницаХранения,
	|		Количество КАК Количество,
	|		Цена КАК Цена,
	|		ПроцентСкидкиНаценки,
	|		0 КАК СуммаАвтоматическойСкидки,
	|		ВЫБОР
	|			КОГДА ПриходнаяНакладная.Запасы.ПроцентСкидкиНаценки <> 0
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ЕстьСкидка,
	|		Сумма КАК Сумма,
	|		СуммаНДС,
	|		Всего,
	|		Характеристика,
	|		Партия,
	|		Содержание,
	|		КлючСвязи,
	|		ЛОЖЬ КАК ЭтоНабор,
	|		ВЫБОР
	|			КОГДА НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|					И НоменклатураНабора.ВариантПечатиНабора = ЗНАЧЕНИЕ(Перечисление.ВариантыПечатиНаборов.НаборИКомплектующие)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК НеобходимоВыделитьКакСоставНабора,
	|		НоменклатураНабора КАК НоменклатураНабора,
	|		ХарактеристикаНабора КАК ХарактеристикаНабора
	|	) КАК ТаблицаЗапасы,
	|	ПриходнаяНакладная.ДобавленныеНаборы.(
	|		НоменклатураНабора КАК НоменклатураНабора,
	|		ХарактеристикаНабора КАК ХарактеристикаНабора,
	|		Количество КАК Количество,
	|		ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(НоменклатураНабора.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|				ТОГДА НоменклатураНабора.Наименование
	|			ИНАЧЕ ВЫРАЗИТЬ(НоменклатураНабора.НаименованиеПолное КАК СТРОКА(1000))
	|		КОНЕЦ КАК ЗапасНабора,
	|		НоменклатураНабора.ВариантПечатиНабора КАК ВариантПечатиНабора,
	|		НоменклатураНабора.ТипНоменклатуры КАК ТипНоменклатурыНабора,
	|		НоменклатураНабора.Артикул КАК АртикулНабора,
	|		НоменклатураНабора.Код КАК КодНабора,
	|		НоменклатураНабора.ЕдиницаИзмерения КАК ЕдиницаИзмеренияНабора,
	|		НоменклатураНабора.ЕдиницаИзмерения.Код КАК КодЕдиницыИзмеренияНабора,
	|		ИСТИНА КАК ВыводитьИтоги
	|	) КАК ТаблицаДобавленныеНаборы,
	|	ПриходнаяНакладная.Расходы.(
	|		НомерСтроки,
	|		ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(ПриходнаяНакладная.Расходы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|				ТОГДА ПриходнаяНакладная.Расходы.Номенклатура.Наименование
	|			ИНАЧЕ ВЫРАЗИТЬ(ПриходнаяНакладная.Расходы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|		КОНЕЦ КАК Расход,
	|		Номенклатура,
	|		Номенклатура.Код КАК Код,
	|		Номенклатура.Артикул КАК Артикул,
	|		Номенклатура.Штрихкод КАК Штрихкод,
	|		ЕдиницаИзмерения КАК ЕдиницаХранения,
	|		Количество,
	|		Цена,
	|		ПроцентСкидкиНаценки,
	|		0 КАК СуммаАвтоматическойСкидки,
	|		Сумма,
	|		СтавкаНДС,
	|		СуммаНДС,
	|		Всего,
	|		ЗаказПоставщику,
	|		Заказ,
	|		СтруктурнаяЕдиница,
	|		НаправлениеДеятельности,
	|		Содержание
	|	) КАК ТаблицаРасходы,
	|	ПриходнаяНакладная.СерииНоменклатуры.(
	|		Серия,
	|		КлючСвязи
	|	) КАК ТаблицаСерииНоменклатуры
	|ИЗ
	|	Документ.ПриходнаяНакладная КАК ПриходнаяНакладная
	|ГДЕ
	|	ПриходнаяНакладная.Ссылка В (&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка, НомерСтроки";

	ДанныеДокументов = Запрос.Выполнить().Выгрузить();
	
	// Наборы
	НаборыСервер.КомпоноватьТабличнуюЧастьПоНаборам(ДанныеДокументов, "ТаблицаЗапасы");

	Для Каждого Шапка Из ДанныеДокументов Цикл

		ПечатьДокументовУНФ.ПередНачаломФормированияДокумента(ТабличныйДокумент, ПервыйДокумент, НомерСтрокиНачало);

		СведенияОбОрганизации = ПечатьДокументовУНФ.СведенияОЮрФизЛице(Шапка.Организация,
			Шапка.ДатаДокумента, , );
		СведенияОбКонтрагенте = ПечатьДокументовУНФ.СведенияОЮрФизЛице(Шапка.Контрагент,
			Шапка.ДатаДокумента, , );

		НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(Шапка.ДатаДокумента,
			Шапка.Номер, Шапка.Префикс);

		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		ОбластьЗаголовок.Параметры.ТекстЗаголовка = СтрШаблон(НСтр("ru = 'Приходная накладная № %1 от %2'"),
			НомерДокумента, Формат(Шапка.ДатаДокумента, "ДЛФ=DD"));

		ОбластьЗаголовок.Параметры.Заполнить(Шапка);
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьЗаголовок,
			Шапка.Ссылка);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);

		ОбластьПоставщик = Макет.ПолучитьОбласть("Поставщик");
		ОбластьПоставщик.Параметры.Заполнить(Шапка);
		ОбластьПоставщик.Параметры.ПредставлениеПоставщика = ПечатьДокументовУНФ.ОписаниеОрганизации(
			СведенияОбОрганизации, "ПолноеНаименование,ИНН,КПП,РегистрационныйНомер,ЮридическийАдрес,Телефоны,");
		ТабличныйДокумент.Вывести(ОбластьПоставщик);

		ОбластьПокупатель = Макет.ПолучитьОбласть("Покупатель");
		ОбластьПокупатель.Параметры.ПредставлениеПолучателя = ПечатьДокументовУНФ.ОписаниеОрганизации(
			СведенияОбКонтрагенте, "ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,");
		ТабличныйДокумент.Вывести(ОбластьПокупатель);

		ЕстьСкидки = Шапка.ТаблицаЗапасы.Итог("ЕстьСкидка") <> 0;

		Если ЕстьСкидки Тогда

			ОбластьШапка = Макет.ПолучитьОбласть("ШапкаТаблицыСоСкидкой");
			ТабличныйДокумент.Вывести(ОбластьШапка);
			ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаСоСкидкой");

		Иначе

			ОбластьШапка = Макет.ПолучитьОбласть("ШапкаТаблицы");
			ТабличныйДокумент.Вывести(ОбластьШапка);
			ОбластьСтрока = Макет.ПолучитьОбласть("Строка");

		КонецЕсли;

		Итоги = Новый Структура;
		Итоги.Вставить("Сумма", 0);
		Итоги.Вставить("СуммаНДС", 0);
		Итоги.Вставить("Всего", 0);
		Итоги.Вставить("Количество", 0);
		Итоги.Вставить("НомерСтроки", 0);
		Итоги.Вставить("ЕстьСкидки", ЕстьСкидки);
		Итоги.Вставить("ПредставлениеСкидки", ПредставлениеСкидки);
		ПараметрыНоменклатуры = Новый Структура;
		ДанныеПечати = Новый Структура;

		Для Каждого СтрокаЗапасы Из Шапка.ТаблицаЗапасы Цикл

			Если СтрокаЗапасы.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;

			ЗаполнитьДанныеПечатиПоСтрокеТабличнойЧасти(СтрокаЗапасы, ДанныеПечати, ПараметрыНоменклатуры, Итоги, Шапка);

			ОбластьСтрока.Параметры.Заполнить(СтрокаЗапасы);
			ОбластьСтрока.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			// Наборы
			НаборыСервер.УчестьОформлениеСтрокиНабора(ТабличныйДокумент, ОбластьСтрока, СтрокаЗапасы);

		КонецЦикла;
		
		// Расходы
		Для Каждого СтрокаРасходы Из Шапка.ТаблицаРасходы Цикл

			ЗаполнитьДанныеПечатиПоСтрокеТабличнойЧастиРасходы(СтрокаРасходы, ДанныеПечати, ПараметрыНоменклатуры,
				Итоги, Шапка);

			ОбластьСтрока.Параметры.Заполнить(СтрокаРасходы);
			ОбластьСтрока.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьСтрока);

		КонецЦикла;

		ВывестиОбластиИтогиНДССуммаПрописью(Итоги, Макет, Шапка, ТабличныйДокумент);

		ОбластьПодписи = Макет.ПолучитьОбласть("Подписи");
		ОбластьПодписи.Параметры.Заполнить(Шапка);
		ТабличныйДокумент.Вывести(ОбластьПодписи);

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати,
			Шапка.Ссылка);

	КонецЦикла;

	Возврат ТабличныйДокумент;

КонецФункции

// Функция формирует печатную форму документа.
//
// Параметры:
//	ТабличныйДокумент - ТабличныйДокумент в который будет выводится печатная
//				   форма.
Функция ПечатнаяФормаНакладнаяВРозничныхЦенах(МассивОбъектов, ОбъектыПечати)
	Перем ПервыйДокумент, НомерСтрокиНачало;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПриходнаяНакладная";
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПриходнаяНакладная_Накладная";
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПриходнаяНакладная.ПФ_MXL_ПриходнаяНакладная");
	
	ПараметрыНоменклатуры = Новый Структура;
	
	Для каждого ТекущийДокумент Из МассивОбъектов Цикл
	
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
			
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ШапкаДокумента.Ссылка КАК Ссылка,
		|	ШапкаДокумента.Дата КАК ДатаДокумента,
		|	ШапкаДокумента.Контрагент КАК Организация,
		|	ШапкаДокумента.Организация КАК Контрагент,
		|	ШапкаДокумента.КонтактноеЛицоПодписант.Наименование КАК КонтактноеЛицоПодписант,
		|	ШапкаДокумента.ПодписьКладовщика.Должность КАК ДолжностьКладовщика,
		|	ШапкаДокумента.ПодписьКладовщика.РасшифровкаПодписи КАК РасшифровкаПодписиКладовщика,
		|	ШапкаДокумента.СуммаВключаетНДС КАК СуммаВключаетНДС,
		|	ШапкаДокумента.ВалютаДокумента КАК ВалютаДокумента,
		|	ВЫБОР
		|		КОГДА ШапкаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
		|			ТОГДА ""(Поступление от поставщика)""
		|		КОГДА ШапкаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию)
		|			ТОГДА ""(Прием на комиссию)""
		|		КОГДА ШапкаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемВПереработку)
		|			ТОГДА ""(Прием в переработку)""
		|		КОГДА ШапкаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаОтветХранение)
		|			ТОГДА ""(Прием от контрагента на ответственное хранение)""
		|		КОГДА ШапкаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
		|			ТОГДА ""(Возврат от покупателя)""
		|		КОГДА ШапкаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера)
		|			ТОГДА ""(Возврат от комиссионера)""
		|		КОГДА ШапкаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика)
		|			ТОГДА ""(Возврат от переработчика)""
		|		КОГДА ШапкаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения)
		|			ТОГДА ""(Возврат от контрагента с ответственного хранения)""
		|	КОНЕЦ КАК ВидОперации,
		|	ШапкаДокумента.ВидОперации КАК ВидОперацииСсылка,
		|	ШапкаДокумента.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК СтруктурнаяЕдиницаТипСтруктурнойЕдиницы,
		|	ШапкаДокумента.Номер КАК Номер,
		|	ШапкаДокумента.Организация.Префикс КАК Префикс,
		|	ШапкаДокумента.Запасы.(
		|		Ссылка КАК Ссылка
		|	) КАК ТаблицаЗапасы,
		|	ШапкаДокумента.ДобавленныеНаборы.(
		|		Ссылка КАК Ссылка
		|	) КАК ТаблицаДобавленныеНаборы,
		|	ШапкаДокумента.СерииНоменклатуры.(
		|		Ссылка КАК Ссылка
		|	) КАК ТаблицаСерииНоменклатуры
		|ИЗ
		|	Документ.ПриходнаяНакладная КАК ШапкаДокумента
		|ГДЕ
		|	ШапкаДокумента.Ссылка = &ТекущийДокумент
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриходнаяНакладная.Ссылка КАК Ссылка,
		|	ПриходнаяНакладная.НомерСтроки КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ПриходнаяНакладная.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
		|			ТОГДА ПриходнаяНакладная.Номенклатура.Наименование
		|		ИНАЧЕ ВЫРАЗИТЬ(ПриходнаяНакладная.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
		|	КОНЕЦ КАК Запас,
		|	ПриходнаяНакладная.Номенклатура.Артикул КАК Артикул,
		|	ПриходнаяНакладная.Номенклатура.Штрихкод КАК Штрихкод,
		|	ПриходнаяНакладная.Номенклатура.Код КАК Код,
		|	ПриходнаяНакладная.ЕдиницаИзмерения КАК ЕдиницаХранения,
		|	ПриходнаяНакладная.Количество КАК Количество,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА ЦеныНоменклатурыСрезПоследних.ВидЦен.ЦенаВключаетНДС = ПриходнаяНакладная.Ссылка.СуммаВключаетНДС
		|				ТОГДА ЦеныНоменклатурыСрезПоследних.Цена
		|			КОГДА НЕ ЦеныНоменклатурыСрезПоследних.ВидЦен.ЦенаВключаетНДС
		|				ТОГДА ЦеныНоменклатурыСрезПоследних.Цена * (ПриходнаяНакладная.СтавкаНДС.Ставка + 100) / 100
		|			КОГДА ЦеныНоменклатурыСрезПоследних.ВидЦен.ЦенаВключаетНДС
		|				ТОГДА ЦеныНоменклатурыСрезПоследних.Цена / (ПриходнаяНакладная.СтавкаНДС.Ставка + 100) * 100
		|		КОНЕЦ, 0) КАК Цена,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА ЦеныНоменклатурыСрезПоследних.ВидЦен.ЦенаВключаетНДС = ПриходнаяНакладная.Ссылка.СуммаВключаетНДС
		|				ТОГДА ЦеныНоменклатурыСрезПоследних.Цена * ПриходнаяНакладная.Количество
		|			КОГДА НЕ ЦеныНоменклатурыСрезПоследних.ВидЦен.ЦенаВключаетНДС
		|				ТОГДА ЦеныНоменклатурыСрезПоследних.Цена * ПриходнаяНакладная.Количество * (ПриходнаяНакладная.СтавкаНДС.Ставка + 100) / 100
		|			КОГДА ЦеныНоменклатурыСрезПоследних.ВидЦен.ЦенаВключаетНДС
		|				ТОГДА ЦеныНоменклатурыСрезПоследних.Цена * ПриходнаяНакладная.Количество / (ПриходнаяНакладная.СтавкаНДС.Ставка + 100) * 100
		|		КОНЕЦ, 0) КАК Сумма,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА ЦеныНоменклатурыСрезПоследних.ВидЦен.ЦенаВключаетНДС
		|				ТОГДА ЦеныНоменклатурыСрезПоследних.Цена * ПриходнаяНакладная.Количество * ПриходнаяНакладная.СтавкаНДС.Ставка / (100 + ПриходнаяНакладная.СтавкаНДС.Ставка)
		|			КОГДА НЕ ЦеныНоменклатурыСрезПоследних.ВидЦен.ЦенаВключаетНДС
		|				ТОГДА ЦеныНоменклатурыСрезПоследних.Цена * ПриходнаяНакладная.Количество * ПриходнаяНакладная.СтавкаНДС.Ставка / 100
		|		КОНЕЦ, 0) КАК СуммаНДС,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА ЦеныНоменклатурыСрезПоследних.ВидЦен.ЦенаВключаетНДС
		|				ТОГДА ЦеныНоменклатурыСрезПоследних.Цена * ПриходнаяНакладная.Количество
		|			КОГДА НЕ ЦеныНоменклатурыСрезПоследних.ВидЦен.ЦенаВключаетНДС
		|				ТОГДА ЦеныНоменклатурыСрезПоследних.Цена * ПриходнаяНакладная.Количество * (ПриходнаяНакладная.СтавкаНДС.Ставка + 100) / 100
		|		КОНЕЦ, 0) КАК Всего,
		|	ПриходнаяНакладная.Характеристика КАК Характеристика,
		|	ПриходнаяНакладная.Партия КАК Партия,
		|	ПриходнаяНакладная.Содержание КАК Содержание,
		|	ПриходнаяНакладная.КлючСвязи КАК КлючСвязи,
		|	ЛОЖЬ КАК ЭтоНабор,
		|	ВЫБОР
		|		КОГДА ПриходнаяНакладная.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|				И ПриходнаяНакладная.НоменклатураНабора.ВариантПечатиНабора = ЗНАЧЕНИЕ(Перечисление.ВариантыПечатиНаборов.НаборИКомплектующие)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НеобходимоВыделитьКакСоставНабора,
		|	ПриходнаяНакладная.НоменклатураНабора КАК НоменклатураНабора,
		|	ПриходнаяНакладная.ХарактеристикаНабора КАК ХарактеристикаНабора
		|ИЗ
		|	Документ.ПриходнаяНакладная.Запасы КАК ПриходнаяНакладная
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ДатаДокумента, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
		|		ПО ПриходнаяНакладная.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
		|			И ПриходнаяНакладная.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
		|			И ПриходнаяНакладная.Ссылка.СтруктурнаяЕдиница.РозничныйВидЦен = ЦеныНоменклатурыСрезПоследних.ВидЦен
		|ГДЕ
		|	ПриходнаяНакладная.Ссылка = &ТекущийДокумент
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриходнаяНакладнаяСерииНоменклатуры.Ссылка КАК Ссылка,
		|	ПриходнаяНакладнаяСерииНоменклатуры.Серия КАК Серия,
		|	ПриходнаяНакладнаяСерииНоменклатуры.КлючСвязи КАК КлючСвязи
		|ИЗ
		|	Документ.ПриходнаяНакладная.СерииНоменклатуры КАК ПриходнаяНакладнаяСерииНоменклатуры
		|ГДЕ
		|	ПриходнаяНакладнаяСерииНоменклатуры.Ссылка = &ТекущийДокумент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриходнаяНакладнаяДобавленныеНаборы.Ссылка КАК Ссылка,
		|	ПриходнаяНакладнаяДобавленныеНаборы.НоменклатураНабора КАК НоменклатураНабора,
		|	ПриходнаяНакладнаяДобавленныеНаборы.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	ПриходнаяНакладнаяДобавленныеНаборы.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ПриходнаяНакладнаяДобавленныеНаборы.НоменклатураНабора.НаименованиеПолное КАК СТРОКА(1000))) = """"
		|			ТОГДА ПриходнаяНакладнаяДобавленныеНаборы.НоменклатураНабора.Наименование
		|		ИНАЧЕ ВЫРАЗИТЬ(ПриходнаяНакладнаяДобавленныеНаборы.НоменклатураНабора.НаименованиеПолное КАК СТРОКА(1000))
		|	КОНЕЦ КАК ЗапасНабора,
		|	ПриходнаяНакладнаяДобавленныеНаборы.НоменклатураНабора.ВариантПечатиНабора КАК ВариантПечатиНабора,
		|	ПриходнаяНакладнаяДобавленныеНаборы.НоменклатураНабора.ТипНоменклатуры КАК ТипНоменклатурыНабора,
		|	ПриходнаяНакладнаяДобавленныеНаборы.НоменклатураНабора.Артикул КАК АртикулНабора,
		|	ПриходнаяНакладнаяДобавленныеНаборы.НоменклатураНабора.Код КАК КодНабора,
		|	ПриходнаяНакладнаяДобавленныеНаборы.НоменклатураНабора.ЕдиницаИзмерения КАК ЕдиницаИзмеренияНабора,
		|	ПриходнаяНакладнаяДобавленныеНаборы.НоменклатураНабора.ЕдиницаИзмерения.Код КАК КодЕдиницыИзмеренияНабора,
		|	ИСТИНА КАК ВыводитьИтоги
		|ИЗ
		|	Документ.ПриходнаяНакладная.ДобавленныеНаборы КАК ПриходнаяНакладнаяДобавленныеНаборы
		|ГДЕ
		|	ПриходнаяНакладнаяДобавленныеНаборы.Ссылка = &ТекущийДокумент";
		
		Запрос.УстановитьПараметр("ВидЦен", ТекущийДокумент.СтруктурнаяЕдиница.РозничныйВидЦен);
		Запрос.УстановитьПараметр("ДатаДокумента", ТекущийДокумент.Дата);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		ДанныеДокументов = МассивРезультатов[0].Выгрузить();
		ТаблицаЗапасы = МассивРезультатов[1].Выгрузить();
		ТаблицаСерииНоменклатуры = МассивРезультатов[2].Выгрузить();
		ТаблицаДобавленныеНаборы = МассивРезультатов[3].Выгрузить();
		Для каждого Шапка Из ДанныеДокументов Цикл
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("Ссылка", Шапка.Ссылка);
			Шапка.ТаблицаЗапасы = ТаблицаЗапасы.Скопировать(СтруктураОтбора);
			Шапка.ТаблицаСерииНоменклатуры = ТаблицаСерииНоменклатуры.Скопировать(СтруктураОтбора);
			Шапка.ТаблицаДобавленныеНаборы = ТаблицаДобавленныеНаборы.Скопировать(СтруктураОтбора);
		КонецЦикла;
		
		// Наборы
		НаборыСервер.КомпоноватьТабличнуюЧастьПоНаборам(ДанныеДокументов, "ТаблицаЗапасы");

		Для Каждого ДанныеДокумента Из ДанныеДокументов Цикл

			ПечатьДокументовУНФ.ПередНачаломФормированияДокумента(ТабличныйДокумент, ПервыйДокумент, НомерСтрокиНачало);

			СведенияОбОрганизации = ПечатьДокументовУНФ.СведенияОЮрФизЛице(ДанныеДокумента.Организация,
				ДанныеДокумента.ДатаДокумента, , );
			СведенияОбКонтрагенте = ПечатьДокументовУНФ.СведенияОЮрФизЛице(ДанныеДокумента.Контрагент,
				ДанныеДокумента.ДатаДокумента, , );

			НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(ДанныеДокумента.ДатаДокумента,
				ДанныеДокумента.Номер, ДанныеДокумента.Префикс);

			ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
			ОбластьЗаголовок.Параметры.ТекстЗаголовка = СтрШаблон(НСтр("ru = 'Приходная накладная № %1 от %2'"),
				НомерДокумента, Формат(ДанныеДокумента.ДатаДокумента, "ДЛФ=DD"));

			ОбластьЗаголовок.Параметры.Заполнить(ДанныеДокумента);
			ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьЗаголовок,
				ДанныеДокумента.Ссылка);
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);

			ОбластьПоставщик = Макет.ПолучитьОбласть("Поставщик");
			ОбластьПоставщик.Параметры.Заполнить(ДанныеДокумента);
			ОбластьПоставщик.Параметры.ПредставлениеПоставщика = ПечатьДокументовУНФ.ОписаниеОрганизации(
				СведенияОбОрганизации, "ПолноеНаименование,ИНН,КПП,РегистрационныйНомер,ЮридическийАдрес,Телефоны,");

			ТабличныйДокумент.Вывести(ОбластьПоставщик);

			ОбластьПокупатель = Макет.ПолучитьОбласть("Покупатель");
			ОбластьПокупатель.Параметры.ПредставлениеПолучателя = ПечатьДокументовУНФ.ОписаниеОрганизации(
				СведенияОбКонтрагенте, "ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,");

			ТабличныйДокумент.Вывести(ОбластьПокупатель);

			ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
			ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);

			ОбластьСтрока = Макет.ПолучитьОбласть("Строка");

			Итоги = Новый Структура;
			Итоги.Вставить("Сумма", 0);
			Итоги.Вставить("СуммаНДС", 0);
			Итоги.Вставить("Всего", 0);
			Итоги.Вставить("Количество", 0);
			Итоги.Вставить("НомерСтроки", 0);
			Итоги.Вставить("ЕстьСкидки", Ложь);
			ПараметрыНоменклатуры = Новый Структура;
			ДанныеПечати = Новый Структура;

			Для Каждого СтрокаЗапасы Из ДанныеДокумента.ТаблицаЗапасы Цикл

				Если СтрокаЗапасы.Количество = 0 Тогда
					Продолжить;
				КонецЕсли;

				ЗаполнитьДанныеПечатиПоСтрокеТабличнойЧасти(СтрокаЗапасы, ДанныеПечати, ПараметрыНоменклатуры, Итоги,
					ДанныеДокумента);

				ОбластьСтрока.Параметры.Заполнить(СтрокаЗапасы);
				ОбластьСтрока.Параметры.Заполнить(ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьСтрока);
				
				// Наборы
				НаборыСервер.УчестьОформлениеСтрокиНабора(ТабличныйДокумент, ОбластьСтрока, СтрокаЗапасы);

			КонецЦикла;

			ВывестиОбластиИтогиНДССуммаПрописью(Итоги, Макет, ДанныеДокумента, ТабличныйДокумент);
			
			ОбластьПодписи = Макет.ПолучитьОбласть("Подписи");
			ОбластьПодписи.Параметры.Заполнить(ДанныеДокумента);
			ТабличныйДокумент.Вывести(ОбластьПодписи);

			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати,
				ТекущийДокумент);

		КонецЦикла; 
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

Процедура ЗаполнитьДанныеПечатиПоСтрокеТабличнойЧасти(СтрокаТабличнойЧасти, ДанныеПечати, ПараметрыНоменклатуры,
	Итоги, ДанныеДокумента)

	ДанныеПечати.Очистить();

	Если СтрокаТабличнойЧасти.ЭтоНабор Тогда
		НомерСтроки = 0;
	Иначе
		Итоги.НомерСтроки = Итоги.НомерСтроки + 1;
		НомерСтроки = Итоги.НомерСтроки;
	КонецЕсли;
	ДанныеПечати.Вставить("НомерСтроки", НомерСтроки);

	ПараметрыНоменклатуры.Очистить();
	ПараметрыНоменклатуры.Вставить("Содержание", СтрокаТабличнойЧасти.Содержание);
	ПараметрыНоменклатуры.Вставить("ПредставлениеНоменклатуры", СтрокаТабличнойЧасти.Запас);
	ПараметрыНоменклатуры.Вставить("ПредставлениеХарактеристики", СтрокаТабличнойЧасти.Характеристика);
	ПараметрыНоменклатуры.Вставить("ПредставлениеАртикула", СтрокаТабличнойЧасти.Артикул);
	ПараметрыНоменклатуры.Вставить("ПредставлениеСерииНоменклатуры", СерииНоменклатурыУНФ.СтрокаСерииНоменклатуры(
		ДанныеДокумента.ТаблицаСерииНоменклатуры, СтрокаТабличнойЧасти.КлючСвязи));
	// Наборы
	ПараметрыНоменклатуры.Вставить("НеобходимоВыделитьКакСоставНабора",
		СтрокаТабличнойЧасти.НеобходимоВыделитьКакСоставНабора);

	ДанныеПечати.Вставить("Запас", ПечатьДокументовУНФ.ПредставлениеНоменклатуры(ПараметрыНоменклатуры));

	ДанныеПечати.Вставить("ПредставлениеКодаНоменклатуры", ПечатьДокументовУНФ.ПредставлениеКодаНоменклатуры(
		СтрокаТабличнойЧасти));

	Если Итоги.ЕстьСкидки Тогда

		ДанныеПечати.Вставить("ПредставлениеСкидки", ПечатьДокументовУНФ.ПредставлениеСкидки(СтрокаТабличнойЧасти,
			Итоги));

	КонецЕсли;

	Если Не СтрокаТабличнойЧасти.ЭтоНабор Тогда
		Итоги.Сумма = Итоги.Сумма + СтрокаТабличнойЧасти.Сумма;
		Итоги.СуммаНДС = Итоги.СуммаНДС + СтрокаТабличнойЧасти.СуммаНДС;
		Итоги.Всего = Итоги.Всего + СтрокаТабличнойЧасти.Всего;
		Итоги.Количество = Итоги.Количество + 1;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьДанныеПечатиПоСтрокеТабличнойЧастиРасходы(СтрокаТабличнойЧасти, ДанныеПечати, ПараметрыНоменклатуры,
	Итоги, Шапка)

	ДанныеПечати.Очистить();

	Итоги.НомерСтроки = Итоги.НомерСтроки + 1;
	ДанныеПечати.Вставить("НомерСтроки", Итоги.НомерСтроки);

	ПараметрыНоменклатуры.Очистить();
	ПараметрыНоменклатуры.Вставить("Содержание", "");
	ПараметрыНоменклатуры.Вставить("ПредставлениеНоменклатуры", СтрокаТабличнойЧасти.Номенклатура);
	ПараметрыНоменклатуры.Вставить("ПредставлениеАртикула", СтрокаТабличнойЧасти.Артикул);

	ДанныеПечати.Вставить("Запас", ПечатьДокументовУНФ.ПредставлениеНоменклатуры(ПараметрыНоменклатуры));

	ДанныеПечати.Вставить("ПредставлениеКодаНоменклатуры", ПечатьДокументовУНФ.ПредставлениеКодаНоменклатуры(
		СтрокаТабличнойЧасти));

	Если Итоги.ЕстьСкидки Тогда

		ДанныеПечати.Вставить("ПредставлениеСкидки", ПечатьДокументовУНФ.ПредставлениеСкидки(СтрокаТабличнойЧасти,
			Итоги));

	КонецЕсли;

	Итоги.Сумма = Итоги.Сумма + СтрокаТабличнойЧасти.Сумма;
	Итоги.СуммаНДС = Итоги.СуммаНДС + СтрокаТабличнойЧасти.СуммаНДС;
	Итоги.Всего = Итоги.Всего + СтрокаТабличнойЧасти.Всего;
	Итоги.Количество = Итоги.Количество + 1;

КонецПроцедуры

Процедура ВывестиОбластиИтогиНДССуммаПрописью(Итоги, Макет, ДанныеДокумента, ТабличныйДокумент)

	ОбластьИтого = Макет.ПолучитьОбласть("Итого");
	ОбластьИтого.Параметры.Всего = ПечатьДокументовУНФ.ФорматСумм(Итоги.Сумма);
	ТабличныйДокумент.Вывести(ОбластьИтого);

	ОбластьИтогоНДС = Макет.ПолучитьОбласть("ИтогоНДС");

	Если Итоги.СуммаНДС = 0 Тогда

		ОбластьИтогоНДС.Параметры.НДС = НСтр("ru = 'Без налога (НДС)'");
		ОбластьИтогоНДС.Параметры.ВсегоНДС = "-";

	Иначе

		Если ДанныеДокумента.СуммаВключаетНДС Тогда
			ОбластьИтогоНДС.Параметры.НДС = НСтр("ru = 'В том числе НДС:'");
		Иначе
			ОбластьИтогоНДС.Параметры.НДС = НСтр("ru = 'Сумма НДС:'");
		КонецЕсли;

		ОбластьИтогоНДС.Параметры.ВсегоНДС = ПечатьДокументовУНФ.ФорматСумм(Итоги.СуммаНДС);

	КонецЕсли;

	ТабличныйДокумент.Вывести(ОбластьИтогоНДС);

	ОбластьСуммаПрописью = Макет.ПолучитьОбласть("СуммаПрописью");
	СуммаКПрописи = Итоги.Всего;
	ОбластьСуммаПрописью.Параметры.ИтоговаяСтрока = СтрШаблон(НСтр("ru = 'Всего наименований %1, на сумму %2'"),
		Итоги.Количество, ПечатьДокументовУНФ.ФорматСумм(СуммаКПрописи, ДанныеДокумента.ВалютаДокумента));

	ОбластьСуммаПрописью.Параметры.СуммаПрописью = РаботаСКурсамиВалют.СформироватьСуммуПрописью(СуммаКПрописи,
		ДанныеДокумента.ВалютаДокумента);

	ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);

КонецПроцедуры

#КонецОбласти

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Накладная", "Приходная накладная", ПечатнаяФормаНакладная(МассивОбъектов, ОбъектыПечати));
		
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "НакладнаяСРасходами") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "НакладнаяСРасходами", "Приходная накладная (с расходами)", ПечатнаяФормаНакладнаяСРасходами(МассивОбъектов, ОбъектыПечати));
		
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "НакладнаяВРозничныхЦенах") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "НакладнаяВРозничныхЦенах", "Приходная накладная", ПечатнаяФормаНакладнаяВРозничныхЦенах(МассивОбъектов, ОбъектыПечати));
		
	КонецЕсли;
	
	ОписаниеПечатнойФормы = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, Обработки.ПечатьМХ1.ИдентификаторПечатнойФормы());
	Если ОписаниеПечатнойФормы <> Неопределено Тогда
		
		ОписаниеПечатнойФормы.ТабличныйДокумент = Новый ТабличныйДокумент;
		ОписаниеПечатнойФормы.ТабличныйДокумент.КлючПараметровПечати = Обработки.ПечатьМХ1.КлючПараметровПечати();
		ОписаниеПечатнойФормы.ПолныйПутьКМакету = Обработки.ПечатьМХ1.ПолныйПутьКМакету();
		ОписаниеПечатнойФормы.СинонимМакета = Обработки.ПечатьМХ1.ПредставлениеПФ();
		
		ДанныеОбъектовПечати = УниверсальныйЗапросПоДаннымДокумента(МассивОбъектов);
		Обработки.ПечатьМХ1.СформироватьПФ(ОписаниеПечатнойФормы, ДанныеОбъектовПечати, ОбъектыПечати);
		
	КонецЕсли;
	
	ОписаниеПечатнойФормы = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "БланкТоварногоНаполнения");
	Если ОписаниеПечатнойФормы <> Неопределено Тогда
		
		ОписаниеПечатнойФормы.ТабличныйДокумент = Новый ТабличныйДокумент;
		ОписаниеПечатнойФормы.ТабличныйДокумент.КлючПараметровПечати = Обработки.ПечатьБланкТоварногоНаполнения.КлючПараметровПечати();
		ОписаниеПечатнойФормы.ПолныйПутьКМакету = Обработки.ПечатьБланкТоварногоНаполнения.ПолныйПутьКМакету();
		ОписаниеПечатнойФормы.СинонимМакета = Обработки.ПечатьБланкТоварногоНаполнения.ПредставлениеПФ();
		
		ДанныеОбъектовПечати = УниверсальныйЗапросПоДаннымДокумента(МассивОбъектов);
		Обработки.ПечатьБланкТоварногоНаполнения.СформироватьПФ(ОписаниеПечатнойФормы, ДанныеОбъектовПечати, ОбъектыПечати);
		
	КонецЕсли;
	
	// параметры отправки печатных форм по электронной почте
	ЭлектроннаяПочтаУНФ.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов,
		КоллекцияПечатныхФорм);
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "НакладнаяСРасходами";
	КомандаПечати.Представление = НСтр("ru = 'Приходная накладная (с услугами)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 4;
	
	// МобильноеПриложение
	Если МобильноеПриложениеВызовСервера.НужноОграничитьФункциональность() Тогда
		Возврат;
	КонецЕсли;
	// Конец МобильноеПриложение

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Приходная накладная'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 1;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "НакладнаяВРозничныхЦенах";
	КомандаПечати.Представление = НСтр("ru = 'Приходная накладная (в розничных ценах)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 7;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = Обработки.ПечатьМХ1.ИдентификаторПечатнойФормы();
	КомандаПечати.Представление = Обработки.ПечатьМХ1.ПредставлениеПФ();
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.ФункциональныеОпции = "ПриемЗапасовНаОтветХранение";
	КомандаПечати.Порядок = 10;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "БланкТоварногоНаполнения";
	КомандаПечати.Представление = Обработки.ПечатьБланкТоварногоНаполнения.ПредставлениеПФ();
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 14;
	
	Если ПравоДоступа("Просмотр", Метаданные.Обработки.ПечатьЭтикетокИЦенников)
		И ПолучитьФункциональнуюОпцию("ПечатьЭтикетокИЦенников")
		И НЕ УправлениеДоступомУНФ.ЕстьПрофильРабочееМестоКассира() Тогда
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "ПечатьДокументовУНФКлиент.ПечатьЭтикетокИЦенниковИзДокументов";
		КомандаПечати.Идентификатор = "ПечатьЭтикетокИзПриходнойНакладной";
		КомандаПечати.Представление = НСтр("ru = 'Печать этикеток'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		КомандаПечати.Порядок = 17;
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "ПечатьДокументовУНФКлиент.ПечатьЭтикетокИЦенниковИзДокументов";
		КомандаПечати.Идентификатор = "ПечатьЦенниковИзПриходнойНакладной";
		КомандаПечати.Представление = НСтр("ru = 'Печать ценников'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		КомандаПечати.Порядок = 20;
		
	КонецЕсли;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Обработка.ПечатьСчетФактура.СчетФактураПолученный";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура (полученный)'");;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 23;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Конверт";
	КомандаПечати.Представление = НСтр("ru = 'Конверт'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.Обработчик = "ПечатьДокументовУНФКлиент.ПечатьКонверта";
	КомандаПечати.Порядок = 26;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли