
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
	ЗагрузкаДанныхИзВнешнегоИсточника.ПриСозданииНаСервере(Метаданные.Документы.ПриходнаяНакладная.ТабличныеЧасти.Запасы, НастройкиЗагрузкиДанных, ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПечатьДокументовУНФ.УстановитьОтображениеПодменюПечати(Элементы.ПодменюПечать);
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = УправлениеСвойствамиУНФ.ЗаполнитьДополнительныеПараметры(Объект, "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	ЭтотОбъект.ОснованиеСоздания = Параметры.Основание;
	
	ФормыДокументовДеньги.ПриСозданииНаСервере(ЭтаФорма);
	
	Если Объект.Ссылка.Пустая() И НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Если ЗначениеЗаполнено(Параметры.ДокументОснование) Тогда
			ЗаполнитьПоДокументу(Параметры.ДокументОснование);
		ИначеЕсли Не ЗначениеЗаполнено(Объект.ВидОперации) Тогда
			Объект.ВидОперации = ВидОперацииОтПокупателя;
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.БанковскийСчет)
			И ТипЗнч(Объект.БанковскийСчет.Владелец) <> Тип("СправочникСсылка.Организации") Тогда
			Объект.БанковскийСчет = Справочники.БанковскиеСчета.ПустаяСсылка();
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.Организация)
			И ЗначениеЗаполнено(Объект.БанковскийСчет)
			И Объект.БанковскийСчет.Владелец <> Объект.Организация Тогда
			Объект.Организация = Объект.БанковскийСчет.Владелец;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
			Если ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
				Если Объект.Организация.БанковскийСчетПоУмолчанию.ВалютаДенежныхСредств = Объект.ВалютаДенежныхСредств Тогда
					Объект.БанковскийСчет = Объект.Организация.БанковскийСчетПоУмолчанию;
				КонецЕсли;
			Иначе
				Объект.БанковскийСчет = Объект.Организация.БанковскийСчетПоУмолчанию;
				Объект.ВалютаДенежныхСредств = Объект.БанковскийСчет.ВалютаДенежныхСредств;
			КонецЕсли;
		Иначе
			Объект.ВалютаДенежныхСредств = Объект.БанковскийСчет.ВалютаДенежныхСредств;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Параметры.ДокументОснование) Тогда
			НалогообложениеЗаполненоВОбработкеЗаполнения = ДенежныеСредстваФормы.НалогообложениеЗаполненоВОбработкеЗаполнения(Объект.Ссылка, Параметры.Основание);
			Если НЕ НалогообложениеЗаполненоВОбработкеЗаполнения Тогда
				Объект.СпециальныйНалоговыйРежим = НалогиУНФ.СпециальныйНалоговыйРежим(Объект.Организация,, Объект.Дата);
			ИначеЕсли Объект.СпециальныйНалоговыйРежим = ПредопределенноеЗначение("Перечисление.СпециальныеНалоговыеРежимы.ПСН") Тогда
				Объект.УчитыватьВНУ = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	СозданиеНового = Ложь;
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) 
	   И НЕ ЗначениеЗаполнено(Параметры.Основание) 
	   И НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования)
	   И НЕ Параметры.Свойство("ДокументОснование") Тогда
		ФормыДокументовДеньги.ЗаполнитьСтавкуНДСПоОрганизацииНалогообложениеНДС(ЭтаФорма);
		СозданиеНового = Истина;
	КонецЕсли;
	
	ФормыДокументовДеньги.ЗаполнитьСтавкуНДСПоНалогообложениеНДС(ЭтаФорма);
	
	КэшЗначений = Новый Структура;
	КэшЗначений.Вставить("ВерсияПодчиненногоСчетФактуры", Неопределено);
	КэшЗначений.Вставить("МодифицированностьФормы", Ложь);
	
	// Счет фактура и документ-основание
	СчетФактураСсылка = СчетаФактурыУНФ.ПолучитьПодчиненныйСчетФактуру(Объект.Ссылка, Ложь);
	Элементы.СчетФактураНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьСчетФактура(СчетФактураСсылка);
	
	КэшЗначений.Вставить("СчетФактураСсылка", ?(ТипЗнч(СчетФактураСсылка) = Тип("Структура"), СчетФактураСсылка.Ссылка, Неопределено));
	
	// Конец Счет фактура и документ-основание
	
	// ПодключаемоеОборудование
	ИспользоватьПодключаемоеОборудование = УправлениеНебольшойФирмойПовтИсп.ИспользоватьПодключаемоеОборудование();
	УстановитьВидимостьПечатиЧека();
	Если ИспользоватьПодключаемоеОборудование И ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Справочники.Контрагенты.ДанныеКИКонтрагентаДляВыбораНаФорме(Объект.Контрагент, Элементы, ПоляКИДляОтправкиЧека());
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	// Зачет долгов
	ОбновитьИнформациюОбАвансеВРасшифровкеПлатежа();
	ПересчитатьИтогиПриИзмененииАвансаНаСервере();
	// Конец Зачет долгов
	
	ВестиРасчетыПоДокументам = Объект.Контрагент.ВестиРасчетыПоДокументам;
	ВестиРасчетыПоДоговорам = Объект.Контрагент.ВестиРасчетыПоДоговорам;
	
	ФормыДокументовДеньги.УстановитьВидимостьЭлементовВЗависимостиОтВидаОперации(ЭтаФорма, Истина);
	УстановитьВидимостьИДоступность();
	УстановитьУсловноеОформление();
	РасчетыРаботаСФормамиВызовСервера.УстановитьСвязьПараметровВыбораПоОрганизации(ЭтотОбъект);
	
	// Заполнение табличной части при вводе документа из рабочего места.
	Если ТипЗнч(Параметры.ЗначенияЗаполнения) = Тип("Структура")
		И Параметры.ЗначенияЗаполнения.Свойство("ЗаполнитьРасшифровкаПлатежа")
		И Параметры.ЗначенияЗаполнения.ЗаполнитьРасшифровкаПлатежа Тогда
		
		СтрокаТабличнойЧасти = Объект.РасшифровкаПлатежа[0];
		
		СтрокаТабличнойЧасти.СуммаПлатежа = Объект.СуммаДокумента;
		СтрокаТабличнойЧасти.Курс = ?(
			СтрокаТабличнойЧасти.Курс = 0,
			1,
			СтрокаТабличнойЧасти.Курс
		);
		
		СтрокаТабличнойЧасти.Кратность = ?(
			СтрокаТабличнойЧасти.Кратность = 0,
			1,
			СтрокаТабличнойЧасти.Кратность
		);
		
		СтрокаТабличнойЧасти.СуммаРасчетов = ЦенообразованиеСервер.ПересчитатьИзВалютыВВалюту(
			СтрокаТабличнойЧасти.СуммаПлатежа,
			Курс,
			СтрокаТабличнойЧасти.Курс,
			Кратность,
			СтрокаТабличнойЧасти.Кратность
		);
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
			СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
		КонецЕсли;
		
		СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.СуммаПлатежа - (СтрокаТабличнойЧасти.СуммаПлатежа) / ((СтрокаТабличнойЧасти.СтавкаНДС.Ставка + 100) / 100);
		
	КонецЕсли;
	
	ФормыДокументовДеньги.УстановитьВидимостьРеквизитовРасчетов(ЭтаФорма);
	НастроитьРеквизитыЭлементовФормы();
	
	// Эквайринг
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКартам") Тогда
		ЭквайринговыйТерминал = Объект.ЭквайринговыйТерминал;
		Для Каждого ТекущаяСтрока Из Объект.ЭквайринговыеОперации Цикл
			ТекущаяСтрока.СуммаПлатежаИтогоПоСтроке = ТекущаяСтрока.СуммаРасчетов-ТекущаяСтрока.СуммаРасчетовВозврата
								-ТекущаяСтрока.СуммаРасчетовКомиссии-ТекущаяСтрока.СуммаРасчетовКомиссииВозврата;
			ТекущаяСтрока.СуммаКомиссииИтогоПоСтроке = ТекущаяСтрока.СуммаРасчетовКомиссии+ТекущаяСтрока.СуммаРасчетовКомиссииВозврата;
		КонецЦикла;
		ТипСчетаЗатратШапка = Объект.ЭквайринговыйТерминал.СчетЗатрат.ТипСчета;
		НастроитьВидимостьЭлементовВЗависимостиОтТипаСчетаЗатрат(Объект.Ссылка.Пустая());
	// Конец Эквайринг
	КонецЕсли;
	
	АвтоподборКонтактов.ПодключитьОбработчикиСобытияАвтоподбор(ЭтаФорма);
	
	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	// Остатки ДС и взаиморасчетов на форме
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	ОбновитьВидимостьИОстаткиДС();
	
	УстановитьСвязиПараметровВыбораДоступныеТипы();
	
	ФормыДокументовДеньги.ОпределитьВидимостьНастроекУчетаВНалогообложении(ЭтаФорма);
	
	// МобильныйКлиент
	МобильныйКлиентУНФ.НастроитьФормуОбъектаМобильныйКлиент(Элементы);
	// Конец МобильныйКлиент
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьТекущуюСтраницу();
	
	СтатистикаИспользованияФормКлиент.ПроверитьЗаписатьСтатистикуКоманды(
		"СоздатьНаОсновании.ПоступлениеНаСчет",
		ЭтотОбъект.ВладелецФормы,
		ЭтотОбъект.ОснованиеСоздания
	);
	
	УправлениеНебольшойФирмойКлиент.ЗаполнитьТелефонАдресЭП(ЭтаФорма, ТелефонАдресЭП);
	УправлениеНебольшойФирмойКлиент.УстановитьДоступностьТелефонАдресЭП(ЭтаФорма, ТелефонАдресЭП);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьДоступностьПечатиЧека();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Документ.СчетФактура.Форма.ФормаДокумента" Тогда
		Элементы.СчетФактураНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьСчетФактура(
			ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеТекстаПроСчетФактуру" Тогда
		
		ПредставлениеЗаголовка = РаботаСФормойДокументаКлиент.ПредставлениеЗаголовкаПодчиненногоСчетаФактуры(Объект.Ссылка, Источник, Параметр, КэшЗначений);
		Если ПредставлениеЗаголовка <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СчетФактураНадпись", "Заголовок", ПредставлениеЗаголовка);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_Контрагент" Тогда
		Если ЗначениеЗаполнено(Параметр)
		   И Объект.Контрагент = Параметр Тогда
			УстановитьВидимостьРеквизитовРасчетов();
		КонецЕсли;
	КонецЕсли;
	
	// Прочие расчеты
	Если ИмяСобытия = "Запись_ПланСчетовУправленческий" Тогда
		Если ЗначениеЗаполнено(Параметр)
		   И Объект.Корреспонденция = Параметр Тогда
			УстановитьВидимостьРеквизитовВЗависимостиОтКорреспонденции(Ложь);
		КонецЕсли;
	КонецЕсли;
	// Конец Прочие расчеты
	
	// Обсуждения
	ОбсужденияУНФКлиент.ОбработкаОповещения(ИмяСобытия, Параметр, Источник, ЭтаФорма, Объект.Ссылка);
	// Конец Обсуждения
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// Зачет долгов
	Если ФормаСоздана Тогда
		ТаблицаДокументовДляИзменения.Очистить();
		НадписьДокументЗачетаПредоплаты = "";
		Элементы.ГруппаЗачетПредоплатыПоСтроке.Видимость = Ложь;
		ФормыДокументовДеньги.НастроитьЭлементыРаспределенияДолговНаСервере(ЭтаФорма);
	КонецЕсли;
	
	ОбработатьРеквизитыРасчетов();
	
	СпособЗачетаПоУмолчаниюДляКонтрагента = РасчетыРаботаСФормамиВызовСервера.ПолучитьСпособЗачетаДляКонтрагента(Объект.Контрагент, Ложь);
	// Конец Зачет долгов
	
	ВестиРасчетыПоДокументам = Объект.Контрагент.ВестиРасчетыПоДокументам;
	ВестиРасчетыПоДоговорам = Объект.Контрагент.ВестиРасчетыПоДоговорам;
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	УправлениеНебольшойФирмойКлиент.УстановитьДоступностьТелефонАдресЭП(ЭтаФорма, ТелефонАдресЭП, Истина);
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ОценкаПроизводительностиКлиент.ЗамерВремени("Проведение"
			+ РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	КонецЕсли;
	// СтандартныеПодсистемы.ОценкаПроизводительности
	
	Если (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКартам")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКредитам"))
		И Объект.ПоложениеНастроекНалоговогоУчета = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти")
		И ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Для каждого СтрокаОперации Из Объект.ЭквайринговыеОперации Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаОперации.НалогообложениеНДС) Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = НСтр("ru = 'Не заполнено поле ""Налоговый учет"".'");
				Сообщение.Поле = "ЭквайринговыеОперации["+(СтрокаОперации.НомерСтроки-1)+"].НалоговыйУчет";
				Сообщение.ПутьКДанным = "Объект";
				Сообщение.Сообщить();
				
				Отказ = Истина;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли; 
	
	КэшЗначений.МодифицированностьФормы = Модифицированность;
	
	ОбработатьРасшифровкуПлатежаПередЗаписью();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Взаиморасчеты
	РасчетыРаботаСФормамиВызовСервера.ПередЗаписьюНаСервереДеньги(ЭтотОбъект, ТекущийОбъект);
	// Конец Взаиморасчеты
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ТекстСообщения = "";
		Справочники.ДоговорыКонтрагентов.ПроверитьСоответствиеДоговораУсловиямДокумента(
			ТекстСообщения,
			,
			Объект.Ссылка,
			Объект.Организация,
			Объект.Контрагент,
			Отказ,
			Объект.ВидОперации,
			Объект.РасшифровкаПлатежа,
			Объект.ДоговорКредитаЗайма);
		
		Если ТекстСообщения <> "" Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Если Отказ Тогда
				Сообщение.Текст = СтрШаблон(НСтр("ru = 'Документ не проведен. %1'"), ТекстСообщения);
			Иначе
				Сообщение.Текст = ТекстСообщения;
			КонецЕсли;
			Сообщение.Сообщить();
			
			Если Отказ Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		// Эквайринг
		Если (ТекущийОбъект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКартам
			ИЛИ ТекущийОбъект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКредитам)
			И ДоговорЭквайринга.КонтрольВзаиморасчетовЭквайринг Тогда
			
			// Дата документа должна быть больше даты операции.
			Для Каждого ТекущаяСтрока Из ТекущийОбъект.ЭквайринговыеОперации Цикл
				Если ТекущаяСтрока.Документ <> Неопределено И ТекущаяСтрока.Документ.Дата >= ТекущийОбъект.Дата Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = НСтр("ru = 'Дата операции больше или равна дате документа.'");
					Сообщение.Поле = "ЭквайринговыеОперации["+(ТекущаяСтрока.НомерСтроки-1)+"].Документ";
					Сообщение.ПутьКДанным = "Объект";
					Сообщение.Сообщить();
					
					Отказ = Истина;
				КонецЕсли;
			КонецЦикла;
			
			Если Отказ Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = НСтр("ru = 'Дата документа должна быть больше даты всех операций, которые указаны в табличной части ""Эквайринговые операции"". Документ не проведен.'");
				Сообщение.Сообщить();
				
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
		// Конец Эквайринг
		
		// Курьерская компания и почта
		Если НЕ ПолучитьФункциональнуюОпцию("КорректировкиДолга")
			И Объект.ВидОперации = ВидОперацииОтКурьерскойКомпанииПочты Тогда
			УстановитьПривилегированныйРежим(Истина);
			Константы.ФункциональнаяОпцияИспользоватьКорректировкиДолга.Установить(Истина);
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;
		// Конец. Курьерская компания и почта
		
	КонецЕсли;
	
	// Расчеты
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ТаблицаДокументовДляИзменения", ТаблицаДокументовДляИзменения);
	// Конец Расчеты
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// Обсуждения
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Модифицированность",Модифицированность);
	// Конец Обсуждения
	
	// Расчеты с курьерской компанией или почтой
	Если Объект.Ссылка.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтКурьерскойКомпанииПочты Тогда
		СписокКорректировокДляУдаления.Очистить();
		Для Каждого СтрокаРасшифровкиОтАгента Из Объект.Ссылка.РасшифровкаПлатежаОтАгента Цикл
			СписокКорректировокДляУдаления.Добавить(СтрокаРасшифровкиОтАгента.КорректировкаДолга);
		КонецЦикла;
	КонецЕсли;
	// Конец. Расчеты с курьерской компанией или почтой
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Прочие расчеты
	Если ТекущийОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ВозвратЗаймаСотрудником") Или
		ТекущийОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.РасчетыПоКредитам") Тогда
		ФормыДокументовДеньги.ЗаполнитьИнформациюПоКредитуЗаймуНаСервере(ЭтаФорма);
	КонецЕсли;
	// Конец Прочие расчеты
	
	// Эквайринг
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКартам
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКредитам Тогда
		ФормыДокументовДеньги.ОбновитьОтображениеНалоговогоУчетаВТабличнойЧасти(ЭтаФорма);
	КонецЕсли; 
	// Конец Эквайринг
	
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	ОбновитьВидимостьИОстаткиДС();
	
	ТекущийОбъект.ДополнительныеСвойства.Свойство("ВерсияПодчиненногоСчетФактуры", КэшЗначений.ВерсияПодчиненногоСчетФактуры);
	
	// Зачет долгов
	ОбновитьИнформациюОбАвансеВРасшифровкеПлатежа();
	
	ИсходнаяСуммаРавнаНулю = (Объект.СуммаДокумента = 0);
	
	НадписьДокументЗачетаПредоплаты = "";
	Элементы.ГруппаЗачетПредоплатыПоСтроке.Видимость = Ложь;
	
	Если ТекущийОбъект.Проведен ИЛИ ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		ТаблицаДокументовДляИзменения.Очистить();
		ЗаполнитьТаблицуПросмотраНаСервере(ТекущийОбъект.Проведен);
	КонецЕсли;
	
	ДатаПриСозданииНаСервере = Объект.Дата;
	ОбработатьРеквизитыРасчетов();
	// Конец Зачет долгов
	
	// Расчеты с курьерской компанией или почтой
	Если ЗначениеЗаполнено(СписокКорректировокДляУдаления) Тогда
		Для Каждого ЭлементСписка Из СписокКорректировокДляУдаления Цикл
			
			Если Не ЗначениеЗаполнено(ЭлементСписка.Значение) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТекущийОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтКурьерскойКомпанииПочты") Тогда
				НайденныеСтроки = Объект.РасшифровкаПлатежаОтАгента.НайтиСтроки(Новый Структура("КорректировкаДолга", ЭлементСписка.Значение));
				Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			ДокКорректировки = ЭлементСписка.Значение.ПолучитьОбъект();
			Попытка
				УстановитьПривилегированныйРежим(Истина);
				ДокКорректировки.УстановитьПометкуУдаления(Истина);
				УстановитьПривилегированныйРежим(Ложь);
			Исключение
				Сообщить(ОписаниеОшибки());
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
	Если ТекущийОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтКурьерскойКомпанииПочты") Тогда
		СоздатьОбновитьДокументыПереносаДолгаНаСервере(ТекущийОбъект);
	КонецЕсли;
	// Конец. Расчеты с курьерской компанией или почтой
	
	//Обсуждения
	ОбсужденияУНФ.ПослеЗаписиНаСервере(ТекущийОбъект);	
	// Конец Обсуждения
	
	АссистентУправления.ПослеЗаписиНаСервере(ТекущийОбъект);
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	
	Если КэшЗначений.МодифицированностьФормы = Истина
		И КэшЗначений.ВерсияПодчиненногоСчетФактуры = "1.6.11" Тогда
		
		ТекстВопроса = НСтр("ru = 'В накладную были внесены изменения.
			|Требуется самостоятельно поправить подчиненный документ счет-фактура'");
		
		ПоказатьПредупреждение(, ТекстВопроса, 0, НСтр("ru ='Счет-фактура'"));
		
	КонецЕсли;
	
	Если СозданПоКомандеИзФормыСписка Тогда
		ПараметрыОповещения = Новый Структура("Ссылка", Объект.Ссылка);
		Оповестить("Запись_ПоступлениеНаСчет", ПараметрыОповещения);
	КонецЕсли;
	
	// Оповещение об оплате.
	ОповеститьОбОплатеСчета = Ложь;
	ОповеститьОбОплатеЗаказа = Ложь;
	
	Для каждого ТекСтрока Из Объект.РасшифровкаПлатежа Цикл
		ОповеститьОбОплатеСчета = ?(
			ОповеститьОбОплатеСчета,
			ОповеститьОбОплатеСчета,
			ЗначениеЗаполнено(ТекСтрока.СчетНаОплату)
		);
		ОповеститьОбОплатеЗаказа = ?(
			ОповеститьОбОплатеЗаказа,
			ОповеститьОбОплатеЗаказа,
			ЗначениеЗаполнено(ТекСтрока.Заказ)
		);
	КонецЦикла;
	
	Если ОповеститьОбОплатеСчета Тогда
		ОповеститьОбИзменении(Тип("ДокументСсылка.СчетНаОплату"));
		ОповеститьОбИзменении(Тип("ДокументСсылка.СчетНаОплатуПоставщика"));
	КонецЕсли;
	
	Если ОповеститьОбОплатеЗаказа Тогда
		Оповестить("ОповещениеОбОплатеЗаказа", Объект.Ссылка);
	КонецЕсли;
	
	Оповестить("ОповещениеОбИзмененииДолга");
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеИнтерактивно(Команда)
	
	ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("ПометитьНаУдалениеИнтерактивноЗавершение", ЭтотОбъект);
	Если Модифицированность Тогда
		Если Объект.ПометкаУдаления Тогда
			ТекстВопроса = НСтр("ru = 'Для снятия пометки удаления необходимо записать внесенные вами изменения. Записать данные?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Для установки отметки удаления необходимо записать внесенные вами изменения. Записать данные?'");
		КонецЕсли;
	Иначе
		Если Объект.ПометкаУдаления Тогда
			ТекстВопроса = НСтр("ru = 'Снять с ""'")+Объект.Ссылка + НСтр("ru = '"" пометку на удаление?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Пометить ""'")+Объект.Ссылка + НСтр("ru = '"" на удаление?'");
		КонецЕсли;
	КонецЕсли;
	
	ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеИнтерактивноЗавершение(ОтветНаВопрос, ДопПараметры) Экспорт
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		ПометитьНаУдалениеИнтерактивноНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПометитьНаУдалениеИнтерактивноНаСервере()
	
	ДокОбъект = РеквизитФормыВЗначение("Объект");
	Попытка
		Если Модифицированность Тогда
			ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
		КонецЕсли;
		
		ДокОбъект.ПометкаУдаления = Не ДокОбъект.ПометкаУдаления;
		Если ДокОбъект.Проведен Тогда
			ДокОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
		КонецЕсли;
		
		ЗначениеВРеквизитФормы(ДокОбъект, "Объект");
		
		Модифицированность = Ложь;
		
		Если ДокОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтКурьерскойКомпанииПочты") Тогда
			СоздатьОбновитьДокументыПереносаДолгаНаСервере(ДокОбъект);
		КонецЕсли;
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ОписаниеОшибки();
		Сообщение.Сообщить();
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СчетФактураНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СчетаФактурыУНФКлиент.ОткрытьСчетФактуру(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ФормыДокументовДеньгиКлиент.ДокументОснованиеНадписьОбработкаНавигационнойСсылки(ЭтаФорма, Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	СтруктураДанные = ПолучитьДанныеКонтрагентПриИзменении(Объект.Контрагент, Объект.Организация, Объект.Дата);
	
	ВладелецДоговораПоУмолчанию = Объект.Контрагент;
	ДоговорКонтрагентаПоУмолчанию = СтруктураДанные.Договор;
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		
		Объект.РасшифровкаПлатежа[0].Договор = СтруктураДанные.Договор;
		
		Если ЗначениеЗаполнено(Объект.РасшифровкаПлатежа[0].Договор) Тогда
			Объект.РасшифровкаПлатежа[0].Курс = ?(
				СтруктураДанные.ДоговорВалютаКурсКратность.Курс = 0,
				1,
				СтруктураДанные.ДоговорВалютаКурсКратность.Курс
			);
			Объект.РасшифровкаПлатежа[0].Кратность = ?(
				СтруктураДанные.ДоговорВалютаКурсКратность.Кратность = 0,
				1,
				СтруктураДанные.ДоговорВалютаКурсКратность.Кратность
			);
		КонецЕсли;
		
		Объект.РасшифровкаПлатежа[0].Курс = ?(
			Объект.РасшифровкаПлатежа[0].Курс = 0,
			1,
			Объект.РасшифровкаПлатежа[0].Курс
		);
		Объект.РасшифровкаПлатежа[0].Кратность = ?(
			Объект.РасшифровкаПлатежа[0].Кратность = 0,
			1,
			Объект.РасшифровкаПлатежа[0].Кратность
		);
		
		Объект.РасшифровкаПлатежа[0].СуммаРасчетов = ВалютыУНФКлиентСервер.Пересчитать(
			Объект.РасшифровкаПлатежа[0].СуммаПлатежа, Курс, Объект.РасшифровкаПлатежа[0].Курс, Кратность,
			Объект.РасшифровкаПлатежа[0].Кратность);
		
		Если Объект.ВидОперации = ВидОперацииОтПокупателя Или Объект.ВидОперации = ВидОперацииОтНашейОрганизации Тогда
			Объект.РасшифровкаПлатежа[0].СпособЗачета = СтруктураДанные.СпособЗачета;
		Иначе
			Объект.РасшифровкаПлатежа[0].СпособЗачета = СпособЗачетаВручную;
		КонецЕсли;
		
		Если СтруктураДанные.Свойство("Проект") Тогда
			Объект.РасшифровкаПлатежа[0].Проект = СтруктураДанные.Проект;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтруктураДанные.Свойство("Проект") Тогда
		Объект.Проект = СтруктураДанные.Проект;
	КонецЕсли;
		
	// Статьи ДДС
	УстановитьСтатьиДДСВРасшифровкеПлатежа();
	// Конец Статьи ДДС
	
	// Прочие расчеты
	Если СтруктураДанные.Свойство("ДоговорКредитаЗаймаПоУмолчанию") Тогда
		Объект.ДоговорКредитаЗайма = СтруктураДанные.ДоговорКредитаЗаймаПоУмолчанию;
		ОбработатьИзменениеДоговораКредитаИлиЗайма();
	КонецЕсли;
	// Конец Прочие расчеты
	
	Если ЗначениеЗаполнено(СтруктураДанные.СтатьяДДСПоУмолчанию) Тогда
		Объект.Статья = СтруктураДанные.СтатьяДДСПоУмолчанию;
		Объект.УчитыватьВНУ = СтруктураДанные.УчитыватьВНУ;
	КонецЕсли;
	
	ВестиРасчетыПоДокументам = СтруктураДанные.ВестиРасчетыПоДокументам;
	ВестиРасчетыПоДоговорам = СтруктураДанные.ВестиРасчетыПоДоговорам;
	
	Если ВестиРасчетыПоДокументам И Объект.ВидОперации = ВидОперацииОтПоставщика Тогда
		Для Каждого ТекущаяСтрокаРасшифровки Из Объект.РасшифровкаПлатежа Цикл
			ТекущаяСтрокаРасшифровки.ПризнакАванса = Ложь;
		КонецЦикла;
	КонецЕсли;
	
	ЗаполнитьТаблицуПросмотраНаКлиенте();
	
	// Видимость флага "ПризнакАванса" зависит от установленных флагов учета в карточке контрагента.
	УстановитьУсловноеОформлениеПоДаннымКонтрагента();
	
КонецПроцедуры // КонтрагентПриИзменении()

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	ВидОперацииПередИзменением = ВидОперации;
	ВидОперации = Объект.ВидОперации;
	
	Если ВидОперации <> ВидОперацииПередИзменением Тогда
		УстановитьТекущуюСтраницу();
		ОчиститьРеквизитыНеОтносящиесяКОперации();
		ВидОперацииПриИзмененииНаСервере();
		Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
			Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВидОперацииПриИзменении()

// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
		
		// Обновление курсов расчетов
		СтруктураДанные.Вставить("ОбновитьКурсыРасчетов", (Объект.РасшифровкаПлатежа.Количество() > 0 И Объект.РасшифровкаПлатежа.Итог("Курс") > 0 И
			Объект.РасшифровкаПлатежа.Количество() <> Объект.РасшифровкаПлатежа.Итог("Курс")));
		СтруктураДанные.Вставить("ОбновитьКурсыРасчетовТекстВопроса", НСтр("ru = 'Изменилась дата документа. Установить курс расчетов в соответствии с курсом валюты договора?'"));
		// Конец Обновление курсов расчетов
		
		ТекстСообщения = НСтр("ru = 'Изменился курс валюты банковского счета. Пересчитать суммы документа?'");
		ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств(СтруктураДанные, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры // ДатаПриИзменении()

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	// Обработка события изменения организации.
	Объект.Номер = "";
	СтруктураДанные = ПолучитьДанныеОрганизацияПриИзменении();
	Компания = СтруктураДанные.Компания;
	Объект.БанковскийСчет = СтруктураДанные.БанковскийСчет;
	
	ВалютаДенежныхСредствПередИзменением = ВалютаДенежныхСредств;
	Объект.ВалютаДенежныхСредств = СтруктураДанные.ВалютаДенежныхСредств;
	ВалютаДенежныхСредств = СтруктураДанные.ВалютаДенежныхСредств;
	
	УстановитьНастройкиУчетаВНалогообложении();
	
	//Видимость итогов НДС в зависимости от настроек налогообложения организации
	Элементы.СуммаНДС.Видимость = Объект.НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДС");
	
	// Если валюта не изменилась, то ничего не делаем.
	Если ВалютаДенежныхСредств = ВалютаДенежныхСредствПередИзменением Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.РасшифровкаПлатежа.Количество() > 0 Тогда
		Для Каждого ТекущаяСтрока Из Объект.РасшифровкаПлатежа Цикл
			Если ВестиРасчетыПоДоговорам Тогда
				ТекущаяСтрока.Договор = Неопределено;
			ИначеЕсли ТекущаяСтрока.Договор.Пустая() Тогда
				ТекущаяСтрока.Договор = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Объект.Контрагент, Объект.Организация, Объект.ВидОперации);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если Объект.ДоговорыАвтоЗачетаДолгов.Количество() > 0 Тогда
		Для Каждого ТекущаяСтрока Из Объект.ДоговорыАвтоЗачетаДолгов Цикл
			Если ВестиРасчетыПоДоговорам Тогда
				ТекущаяСтрока.Договор = Неопределено;
			Иначе
				ТекущаяСтрока.Договор = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Объект.Контрагент, Объект.Организация, Объект.ВидОперации);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = 'Изменилась валюта банковского счета. Пересчитать суммы документа?'");
	ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств(СтруктураДанные, ТекстСообщения);
	
	ЗаполнитьТаблицуПросмотраНаКлиенте();
	
КонецПроцедуры // ОрганизацияПриИзменении()

&НаКлиенте
Процедура БанковскийСчетПриИзменении(Элемент)
	
	ВалютаДенежныхСредствПередИзменением = ВалютаДенежныхСредств;
	
	СтруктураДанные = ПолучитьДанныеБанковскийСчетПриИзменении(
		Объект.Дата,
		Объект.БанковскийСчет,
		Объект.СчетКонтрагента
	);
	
	Объект.ВалютаДенежныхСредств = СтруктураДанные.ВалютаДенежныхСредств;
	ВалютаДенежныхСредств = СтруктураДанные.ВалютаДенежныхСредств;
	
	Если Объект.ВидОперации = ВидОперацииЭквайринг Тогда
		Объект.СуммаДокумента = 0;
		Объект.СуммаКомиссииДокумента = 0;
		Объект.ЭквайринговыеОперации.Очистить();
	КонецЕсли;
	
	// Если валюта не изменилась, то ничего не делаем.
	Если ВалютаДенежныхСредств = ВалютаДенежныхСредствПередИзменением Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = 'Изменилась валюта банковского счета. Пересчитать суммы документа?'");
	ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств(СтруктураДанные, ТекстСообщения);
	
КонецПроцедуры // БанковскийСчетПриИзменении()

&НаКлиенте
Процедура СуммаДокументаПриИзменении(Элемент)
	
	ИсходнаяСуммаРавнаНулю = (Объект.СуммаДокумента = 0);
	
	СуммаДокументаПриИзмененииФрагмент();
	
КонецПроцедуры // СуммаДокументаПриИзменении()

&НаКлиенте
Процедура СуммаДокументаПриИзмененииФрагмент()
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
	 
		СтрокаТабличнойЧасти = Объект.РасшифровкаПлатежа[0];
		
		СтрокаТабличнойЧасти.СуммаПлатежа = Объект.СуммаДокумента;
			СтрокаТабличнойЧасти.Курс = ?(
			СтрокаТабличнойЧасти.Курс = 0,
			1,
		СтрокаТабличнойЧасти.Курс
		);
		
		СтрокаТабличнойЧасти.Кратность = ?(
			СтрокаТабличнойЧасти.Кратность = 0,
			1,
			СтрокаТабличнойЧасти.Кратность
		);
		
		СтрокаТабличнойЧасти.СуммаРасчетов = ВалютыУНФКлиентСервер.Пересчитать(
			СтрокаТабличнойЧасти.СуммаПлатежа, Курс, СтрокаТабличнойЧасти.Курс, Кратность,
			СтрокаТабличнойЧасти.Кратность);
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
			СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
		КонецЕсли;
		
		СтрокаТабличнойЧасти.Аванс = ?(СтрокаТабличнойЧасти.ПризнакАванса, СтрокаТабличнойЧасти.СуммаПлатежа, 0);
		
		РасчетыРаботаСФормамиКлиент.РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
		
	КонецЕсли;
	
	РассчитатьСуммуУчета();
	
	Если Объект.ВидОперации = ВидОперацииОтПокупателя Или Объект.ВидОперации = ВидОперацииОтНашейОрганизации Тогда
		
		ПересчитатьИтогиПриИзмененииАвансаНаСервере();
		
	Иначе
		
		СуммаПлатежаДляУсловногоОформления = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
		
	КонецЕсли;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПокупкаВалюты") Тогда
		ОбновитьОтображениеЭлементовПокупкаВалюты();
	КонецЕсли;
	
	ЗаполнитьТаблицуПросмотраНаКлиенте();
	
КонецПроцедуры // СуммаДокументаПриИзменении()

&НаКлиенте
Процедура СтатьяПриИзменении(Элемент)
	ФормыДокументовДеньгиКлиент.СтатьяПриИзменении(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОстатокВзаиморасчетовНажатие(Элемент)
	
	РаботаСФормойДокументаКлиент.ОткрытьОтчетВзаиморасчетыСКонтрагентом(Объект.Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОстатокВзаиморасчетовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	РаботаСФормойДокументаКлиент.ОткрытьОтчетВзаиморасчетыСКонтрагентом(Объект.Контрагент);

КонецПроцедуры

&НаКлиенте
Процедура ОстатокДенежныхСредствНажатие(Элемент)
	
	РаботаСФормойДокументаКлиент.ОткрытьОтчетДенежныеСредства(Объект.БанковскийСчет, Объект.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОстатокДенежныхСредствОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСФормойДокументаКлиент.ОткрытьОтчетДенежныеСредства(Объект.БанковскийСчет, Объект.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантЗаполненияРасшифровкиПриИзменении(Элемент)
	
	ВариантЗаполненияРасшифровкиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ВариантЗаполненияРасшифровкиПриИзмененииНаСервере()
	
	ФормыДокументовДеньги.НастроитьЭлементыРаспределенияДолговНаСервере(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();
	Если Объект.ВидОперации = ВидОперацииОтПокупателя Или Объект.ВидОперации = ВидОперацииОтНашейОрганизации Тогда
		
		СуммаИтог = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
		
		Если СуммаИтог <> 0 И СуммаИтог <> Объект.СуммаДокумента Тогда
			НадписьВСкобках = НСтр("ru = 'разнесено вручную'");
			Элемент.СписокВыбора.Добавить(СуммаИтог, ""+СуммаИтог+" ("+НадписьВСкобках+")");
		КонецЕсли;
		
		Если ОстатокВзаиморасчетов > 0 И (ОстатокВзаиморасчетов) <> Объект.СуммаДокумента Тогда
			Элемент.СписокВыбора.Добавить(ОстатокВзаиморасчетов, ""+(ОстатокВзаиморасчетов)+НСтр("ru = ' (должен нам)'"));
		КонецЕсли;
		
		Если Элемент.СписокВыбора.Количество() = 0 Тогда
			Элемент.СписокВыбора.Добавить(Объект.СуммаДокумента, НСтр("ru = 'Нет данных для заполнения'"));
		КонецЕсли;
		
	ИначеЕсли Объект.ВидОперации = ВидОперацииОтКурьерскойКомпанииПочты Тогда
		
		СуммаИтог = Объект.РасшифровкаПлатежаОтАгента.Итог("СуммаПлатежа");
		
		Если СуммаИтог <> 0 И СуммаИтог <> Объект.СуммаДокумента
			И Объект.ВариантЗаполненияРасшифровки <> ВариантЗаполненияРасшифровкиАвтоматически
			Тогда
			НадписьВСкобках = НСтр("ru = 'разнесено вручную'");
			
			Элемент.СписокВыбора.Добавить(СуммаИтог, ""+СуммаИтог+" ("+НадписьВСкобках+")");
		КонецЕсли;
		
		Если ОстатокВзаиморасчетов > 0 И (ОстатокВзаиморасчетов) <> Объект.СуммаДокумента Тогда
			Элемент.СписокВыбора.Добавить(ОстатокВзаиморасчетов, ""+(ОстатокВзаиморасчетов)+НСтр("ru = ' (должен нам)'"));
		КонецЕсли;
		
		Если Элемент.СписокВыбора.Количество() = 0 Тогда
			Элемент.СписокВыбора.Добавить(Объект.СуммаДокумента, НСтр("ru = 'Нет данных для заполнения'"));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.Подотчетник.Пустая() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Сначала нужно выбрать подотчетника'"), ,
			"Объект.Подотчетник");
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НалогообложениеНДСПриИзменении(Элемент)
	
	ЗаполнитьСтавкуНДСПоНалогообложениеНДС();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "НачалоВыбора");
	
КонецПроцедуры

&НаКлиенте
Процедура НазначениеПлатежаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.НазначениеПлатежа", НСтр("ru='Назначение платежа'"));
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "НачалоВыбора");
	
КонецПроцедуры

&НаКлиенте
Процедура ПокупкаВалютыДоговорНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		ТекущийДоговор = Объект.РасшифровкаПлатежа[0].Договор;
	Иначе
		ТекущийДоговор = ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка");
	КонецЕсли;
	
	РасчетыРаботаСФормамиКлиент.ОбработатьНачалоВыбораДоговораКонтрагента(ЭтотОбъект, Элемент, СтандартнаяОбработка, ТекущийДоговор);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Объект.СуммаДокумента = ВыбранноеЗначение;
	СуммаДокументаПриИзменении(Элемент);
	
КонецПроцедуры

// Процедура обработчик события "ПриИзменении" реквизита "СуммаКомиссии"
//
&НаКлиенте
Процедура СуммаКомиссииДокументаПриИзменении(Элемент)
	
	ДанныеДоговораЭквайринга = ПолучитьДанныеЭквайринговогоДоговора(ДоговорЭквайринга);
	
	Если ДанныеДоговораЭквайринга.РасчетКомиссииВОтчетеЭквайера И ДанныеДоговораЭквайринга.КонтрольВзаиморасчетовЭквайринг Тогда
		
		РаспределитьСуммуКомиссии();
		
		Для каждого СтрокаЭквайринга Из Объект.ЭквайринговыеОперации Цикл
			ПересчитатьСуммуПлатежаИКомиссииЭквайринга(СтрокаЭквайринга);
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеЭквайринговогоДоговора(ДоговорЭквайринга)
	
	СтруктураДоговора = Новый Структура;
	СтруктураДоговора.Вставить("КонтрольВзаиморасчетовЭквайринг", ДоговорЭквайринга.КонтрольВзаиморасчетовЭквайринг);
	СтруктураДоговора.Вставить("РасчетКомиссииВОтчетеЭквайера", 	  ДоговорЭквайринга.РасчетКомиссииВОтчетеЭквайера);
	
	Возврат СтруктураДоговора;
	
КонецФункции // ПолучитьДанныеЭквайринговогоДоговора()
 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРасшифровкаПлатежа

&НаКлиенте
Процедура РасшифровкаПлатежаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Копирование Тогда
		ПересчитатьИтогиПриИзмененииАвансаНаКлиенте();
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		ТекущиеДанные = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			Если ТекущиеДанные.СпособЗачета.Пустая() Тогда
				ТекущиеДанные.СпособЗачета = СпособЗачетаПоУмолчаниюДляКонтрагента;
			КонецЕсли;
			
			Если НЕ Копирование Тогда
				РасчетыРаботаСФормамиКлиент.ЗаполнитьДоговорВНовойСтрокеРасшифровки(ЭтотОбъект, ТекущиеДанные);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПередУдалением(Элемент, Отказ)
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаПередУдалением()

&НаКлиенте
Процедура РасшифровкаПлатежаПослеУдаления(Элемент)
	
	ПересчитатьИтогиПриИзмененииАвансаНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДоговорПриИзменении(Элемент)
	
	ОбработатьИзменениеДоговораКонтрагента();
	
КонецПроцедуры // РасшифровкаПлатежаДоговорПриИзменении()

&НаКлиенте
Процедура РасшифровкаПлатежаДоговорНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РасчетыРаботаСФормамиКлиент.ОбработатьНачалоВыбораДоговораКонтрагента(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДоговорСоздание(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаОбъекта",Новый Структура("Владелец, Организация, ВидДоговора, ОповеститьОЗаписиДоговора",Объект.Контрагент,Объект.Организация,ВидДоговораПриСоздании(Объект.ВидОперации), Истина), Элемент);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидДоговораПриСоздании(ВидОперации)
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтПокупателя
		Или ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтНашейОрганизации Тогда
		Возврат Перечисления.ВидыДоговоров.СПокупателем;
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтПоставщика Тогда
		Возврат Перечисления.ВидыДоговоров.СПоставщиком;
	Иначе
		Возврат Перечисления.ВидыДоговоров.Прочее;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура РасшифровкаПлатежаПризнакАвансаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если Объект.ВидОперации = ВидОперацииОтПокупателя Или Объект.ВидОперации = ВидОперацииОтНашейОрганизации Тогда
		Если СтрокаТабличнойЧасти.ПризнакАванса Тогда
			СтрокаТабличнойЧасти.Документ = Неопределено;
		Иначе
			СтрокаТабличнойЧасти.ДокументПланирования = Неопределено;
		КонецЕсли;
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтПоставщика")
		И ВестиРасчетыПоДокументам Тогда
		Если ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.РасходИзКассы")
		 ИЛИ ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.РасходСоСчета")
		 ИЛИ ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
			СтрокаТабличнойЧасти.ПризнакАванса = Истина;
			ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Для данного типа документа расчетов признак аванса всегда установлен.'"));
		ИначеЕсли ТипЗнч(СтрокаТабличнойЧасти.Документ) <> Тип("ДокументСсылка.Взаимозачет") Тогда
			СтрокаТабличнойЧасти.ПризнакАванса = Ложь;
			ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Для данного типа документа расчетов нельзя установить признак аванса.'"));
		КонецЕсли;
	КонецЕсли;
	
	СтрокаТабличнойЧасти.Аванс = ?(СтрокаТабличнойЧасти.ПризнакАванса , СтрокаТабличнойЧасти.СуммаПлатежа, 0);
	ПересчитатьИтогиПриИзмененииАвансаНаКлиенте();
	
КонецПроцедуры // РасшифровкаПлатежаПризнакАвансаПриИзменении()

&НаКлиенте
Процедура РасшифровкаПлатежаДокументНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти <> Неопределено И СтрокаТабличнойЧасти.СпособЗачета = СпособЗачетаАвтоматически Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоРасчетыСПокупателями = ВидОперации = ВидОперацииОтПокупателя Или ВидОперации = ВидОперацииОтНашейОрганизации;
	
	СтруктураОтбор = Новый Структура();
	СтруктураОтбор.Вставить("Контрагент", Объект.Контрагент);
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Договор) Тогда
		СтруктураОтбор.Вставить("Договор", СтрокаТабличнойЧасти.Договор);
	КонецЕсли;
	Если НЕ УчетПоКомпании Тогда
		СтруктураОтбор.Вставить("Организация", Объект.Организация);
	КонецЕсли;
	
	СтруктураПараметры = Новый Структура("Отбор, ЭтоРасчетыСПокупателями, ТипДокумента",
		СтруктураОтбор,
		ЭтоРасчетыСПокупателями,
		ТипЗнч(Объект.Ссылка)
	);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыборДокументаРасчетов", СтруктураПараметры, Элемент);
	
КонецПроцедуры // РасшифровкаПлатежаДокументНачалоВыбора()

&НаКлиенте
Процедура РасшифровкаПлатежаДокументОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработатьВыборДокументаРасчетов(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСуммаРасчетовПриИзменении(Элемент)
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуПлатежаНаКлиенте(ЭтотОбъект, Элементы.РасшифровкаПлатежа.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 ИЛИ ИсходнаяСуммаРавнаНулю Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаСуммаРасчетовПриИзменении()

&НаКлиенте
Процедура РасшифровкаПлатежаКурсПриИзменении(Элемент)
	
	РасшифровкаПлатежаКурсПриИзмененииФрагмент(Элементы.РасшифровкаПлатежа.ТекущиеДанные);
	
КонецПроцедуры // РасшифровкаПлатежаКурсПриИзменении()

&НаКлиенте
Процедура РасшифровкаПлатежаКурсНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РасчетыРаботаСФормамиКлиент.ПредоплатаКурсНачалоВыбора(ЭтаФорма, Элемент, ДанныеВыбора, СтандартнаяОбработка, "РасшифровкаПлатежа");
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаКурсПриИзмененииФрагмент(ТекущиеДанные)
	
	Если ТекущиеДанные.СуммаПлатежа = 0 И ТекущиеДанные.СуммаРасчетов = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуПлатежаНаКлиенте(ЭтотОбъект, ТекущиеДанные, "Курс");
		
	Если Объект.РасшифровкаПлатежа.Количество() = 1 ИЛИ ИсходнаяСуммаРавнаНулю Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаКурсПриИзменении()

&НаКлиенте
Процедура РасшифровкаПлатежаКратностьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	Если ТекущиеДанные.СуммаПлатежа = 0 И ТекущиеДанные.СуммаРасчетов = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуПлатежаНаКлиенте(ЭтотОбъект, Элементы.РасшифровкаПлатежа.ТекущиеДанные, "Кратность");
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 ИЛИ ИсходнаяСуммаРавнаНулю Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаКратностьПриИзменении()

&НаКлиенте
Процедура РасшифровкаПлатежаСуммаПлатежаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	РасчетыРаботаСФормамиКлиент.РасшифровкаПлатежаСуммаПлатежаПриИзмененииЗавершениеНаКлиенте(СтрокаТабличнойЧасти,
		Объект.Дата, Объект.ВалютаДенежныхСредств, Курс, Кратность, СтавкаНДСПоУмолчанию
	);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 ИЛИ ИсходнаяСуммаРавнаНулю Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	КонецЕсли;
		
	ПересчитатьИтогиПриИзмененииАвансаНаКлиенте();
	
КонецПроцедуры // РасшифровкаПлатежаСуммаПлатежаПриИзменении()

&НаКлиенте
Процедура РасшифровкаПлатежаСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
	
КонецПроцедуры // РасшифровкаПлатежаСтавкаНДСПриИзменении()

&НаКлиенте
Процедура СтавкаНДСРасчетыСАгентомПриИзменении(Элемент)
	
	Если Объект.РасшифровкаПлатежа.Количество() > 1 Тогда
		Объект.РасшифровкаПлатежа.Очистить();
	КонецЕсли;
	
	Если Объект.РасшифровкаПлатежа.Количество() = 0 Тогда
		СтрокаТабличнойЧасти = Объект.РасшифровкаПлатежа.Добавить();
		СтрокаТабличнойЧасти.СуммаПлатежа = Объект.СуммаДокумента;
	Иначе
		СтрокаТабличнойЧасти = Объект.РасшифровкаПлатежа[0];
	КонецЕсли;
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДокументПриИзменении(Элемент)
	
	ВыполнитьДействияПриИзмененииДокументаРасчетов();
	
КонецПроцедуры // РасшифровкаПлатежаДокументПриИзменении()

&НаКлиенте
Процедура РасшифровкаПлатежаСчетНаОплатуПриИзменении(Элемент)
	
	Если Объект.ВидОперации = ВидОперацииОтПокупателя Или Объект.ВидОперации = ВидОперацииОтНашейОрганизации Тогда
		РасшифровкаПлатежаСчетНаОплатуПриИзмененииФрагмент(Элементы.РасшифровкаПлатежа.ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РасшифровкаПлатежаСчетНаОплатуПриИзмененииФрагмент(ТекущиеДанныеИдентификатор)
	
	ТекущиеДанные = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ТекущиеДанныеИдентификатор);
	Если Объект.Контрагент.ВестиРасчетыПоЗаказам Тогда
		Если ЗначениеЗаполнено(ТекущиеДанные.СчетНаОплату) Тогда
			ТекущиеДанные.СпособЗачета = Перечисления.СпособыЗачетаИРаспределенияПлатежей.Вручную;
			Если НЕ ЗначениеЗаполнено(ТекущиеДанные.Заказ) И
				ТипЗнч(ТекущиеДанные.СчетНаОплату) = Тип("ДокументСсылка.СчетНаОплату") И
				ТипЗнч(ТекущиеДанные.СчетНаОплату.ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя")
			Тогда
				ТекущиеДанные.Заказ = ТекущиеДанные.СчетНаОплату.ДокументОснование;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаЗаказПриИзменении(Элемент)
	
	Если Объект.ВидОперации = ВидОперацииОтПокупателя Или Объект.ВидОперации = ВидОперацииОтНашейОрганизации Тогда
		ИзменилиСуммуПлатежа = РасшифровкаПлатежаЗаказПриИзмененииФрагмент(Элементы.РасшифровкаПлатежа.ТекущиеДанные.ПолучитьИдентификатор());
		Если ИзменилиСуммуПлатежа И Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
			Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
			СуммаДокументаПриИзменении(Элемент);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция РасшифровкаПлатежаЗаказПриИзмененииФрагмент(ТекущиеДанныеИдентификатор)
	
	Возврат РасчетыРаботаСФормамиВызовСервера.РасшифровкаПлатежаЗаказПриИзмененииФрагмент(Объект, ТекущиеДанныеИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура ДокументыПланированияРасшифровкаПлатежаСуммаПлатежаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ДокументыПланированияРасшифровкаПлатежа.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда
		ТекущаяСтрока.СуммаРасчетов = ТекущаяСтрока.СуммаПлатежа;
	КонецЕсли;
	ПересчитатьИтогиПриИзмененииАвансаНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДокументПланированияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	СтруктураДанных = ПолучитьДанныеДокументаПланированияПриИзменении(ТекущиеДанные.ДокументПланирования);
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ДокументПланирования) Тогда
		ТекущиеДанные.СтатьяДДС = СтруктураДанных.СтатьяДДС;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыДокументПланированияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные;
	СтруктураДанных = ПолучитьДанныеДокументаПланированияПриИзменении(ТекущиеДанные.ДокументПланирования);
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ДокументПланирования) Тогда
		ТекущиеДанные.СтатьяДДС = СтруктураДанных.СтатьяДДС;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыПланированияРасшифровкаПлатежаСчетНаОплатуПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДокументыПланированияРасшифровкаПлатежа.ТекущиеДанные;
	СтруктураДанных = ПолучитьДанныеДокументаПланированияПриИзменении(ТекущиеДанные.ДокументПланирования);
	
	Если ЗначениеЗаполнено(СтруктураДанных.СтатьяДДС) Тогда
		ТекущиеДанные.СтатьяДДС = СтруктураДанных.СтатьяДДС;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамДокументПланированияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РасшифровкаПлатежаРасчетыПоКредитам.ТекущиеДанные;
	СтруктураДанных = ПолучитьДанныеДокументаПланированияПриИзменении(ТекущиеДанные.ДокументПланирования);
	
	Если ЗначениеЗаполнено(СтруктураДанных.СтатьяДДС) Тогда
		ТекущиеДанные.СтатьяДДС = СтруктураДанных.СтатьяДДС;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подбор(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Укажите вначале контрагента.'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет)
	   И НЕ ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Укажите вначале банковский счет.'"));
		Возврат;
	КонецЕсли;
	
	АдресРасшифровкаПлатежаВХранилище = ПоместитьРасшифровкаПлатежаВХранилище();
	
	ПараметрыПодбора = Новый Структура(
		"АдресРасшифровкаПлатежаВХранилище,
		|Компания,
		|Дата,
		|Контрагент,
		|Ссылка,
		|ВидОперации,
		|ВалютаДенежныхСредств,
		|СуммаДокумента,
		|БанковскийСчет,
		|ЭквайринговыйТерминал",
		АдресРасшифровкаПлатежаВХранилище,
		Компания,
		Объект.Дата,
		Объект.Контрагент,
		Объект.Ссылка,
		Объект.ВидОперации,
		Объект.ВалютаДенежныхСредств,
		Объект.СуммаДокумента,
		Объект.БанковскийСчет,
		Объект.ЭквайринговыйТерминал
	);
	
	ОткрытьФорму("ОбщаяФорма.ФормаПодбораДолговПокупателей", ПараметрыПодбора, , , , ,
		Новый ОписаниеОповещения("ПодборЗавершение", ЭтотОбъект, Новый Структура("АдресРасшифровкаПлатежаВХранилище",
		АдресРасшифровкаПлатежаВХранилище)));
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборЗавершение(Результат1, ДополнительныеПараметры) Экспорт
	
	АдресРасшифровкаПлатежаВХранилище = ДополнительныеПараметры.АдресРасшифровкаПлатежаВХранилище;
	
	
	Результат = Результат1;
	Если Результат = КодВозвратаДиалога.OK Тогда
		
		ПолучитьРасшифровкаПлатежаИзХранилища(АдресРасшифровкаПлатежаВХранилище);
		ПодборЗавершениеФрагмент();
		
		УстановитьТекущуюСтраницу();
		
		Если Объект.РасшифровкаПлатежа.Количество() = 1 ИЛИ ИсходнаяСуммаРавнаНулю Тогда
			Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // Подбор()

&НаСервере
Процедура ПодборЗавершениеФрагмент()
	
	Для каждого СтрокаРасшифровкаПлатежа Из Объект.РасшифровкаПлатежа Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаРасшифровкаПлатежа.СтавкаНДС) Тогда
			СтрокаРасшифровкаПлатежа.СтавкаНДС = СтавкаНДСПоУмолчанию;
		КонецЕсли;
		РассчитатьСуммуПлатежаНаСервере(СтрокаРасшифровкаПлатежа);
		ЗаполнитьЗаказИСчетНаОплату(СтрокаРасшифровкаПлатежа);
		СтрокаРасшифровкаПлатежа.СпособЗачета = СпособЗачетаВручную;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗаказИСчетНаОплату(СтрокаРасшифровкаПлатежа)
	
	СтрокаРасшифровкаПлатежа.СчетНаОплату = УчетРасчетовСКонтрагентами.ПолучитьСчетНаОплату(СтрокаРасшифровкаПлатежа.Документ, СтрокаРасшифровкаПлатежа.Заказ, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОснованию(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru='Не выбран документ основание.'"));
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоОснованиюЗавершение", ЭтотОбъект), НСтр(
		"ru = 'Документ будет полностью перезаполнен по ""Документу-основанию"". Продолжить выполнение операции?'"),
		РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОснованиюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		// Возможно, что будет прерывание при заполнении на основании приема и передачи в ремонт и тогда банковский счет останется пустым.
		Если ТипЗнч(Объект.ДокументОснование) <> Тип("ДокументСсылка.ПриемИПередачаВРемонт") Тогда
			Объект.БанковскийСчет = Неопределено;
		КонецЕсли;
		Объект.СчетКонтрагента = Неопределено;
		ЗаполнитьПоДокументу(Объект.ДокументОснование);
		
		Если Объект.РасшифровкаПлатежа.Количество() = 0 Тогда
			ДобавитьПервуюСтрокуРасшифровкиНаСервере();
		КонецЕсли;
		
		ВидОперации = Объект.ВидОперации;
		ВалютаДенежныхСредств = Объект.ВалютаДенежныхСредств;
		ДатаДокумента = Объект.Дата;
		
		УстановитьТекущуюСтраницу();
		ЗаполнитьПоОснованиюЗавершениеНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОснованиюЗавершениеНаСервере()
	
	ВидОперацииПриИзмененииНаСервере(Истина);
	
	Если (Объект.ВидОперации = ВидОперацииОтПокупателя Или Объект.ВидОперации = ВидОперацииОтНашейОрганизации) И Объект.ВариантЗаполненияРасшифровки <> ВариантЗаполненияРасшифровкиВручную Тогда
		Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиВручную;
		ФормыДокументовДеньги.НастроитьЭлементыРаспределенияДолговНаСервере(ЭтаФорма);
	КонецЕсли;
	
	Если НЕ (ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ПоступлениеДСПлан")
		ИЛИ ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ПеремещениеДСПлан"))
	Тогда
		
		ФормыДокументовДеньги.УстановитьСтатьюДДС(ЭтотОбъект);
		Если НЕ Объект.Контрагент.Пустая() Тогда
			ФормыДокументовДеньги.УстановитьСтатьиДДСВРасшифровкеПлатежа(ЭтотОбъект);
		КонецЕсли;
		
	КонецЕсли;
	
	РасчетыРаботаСФормамиВызовСервера.ЗаполнитьРеквизитыКурсИКратность(ЭтотОбъект);
	ОбновитьВидимостьИОстаткиДС();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРасшифровку(Команда)
	
	Если Объект.СуммаДокумента = 0 Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru='Укажите вначале сумму документа.'"));
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.БанковскийСчет) И Не ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Укажите вначале банковский счет.'"));
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьРасшифровкуЗавершение", ЭтотОбъект), НСтр(
		"ru='Расшифровка будет полностью перезаполнена. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРасшифровкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Объект.РасшифровкаПлатежа.Очистить();
	
	Если Объект.ВидОперации = ВидОперацииОтПокупателя Или  Объект.ВидОперации = ВидОперацииОтНашейОрганизации Тогда
		
		ЗаполнитьРасшифровкуПлатежа();
		
	КонецЕсли;
	
	УстановитьТекущуюСтраницу();
	
КонецПроцедуры // ЗаполнитьРасшифровку()

&НаКлиенте
Процедура ОбновитьКурсВТекущейСтрокеРасшифровки(Команда)
	
	ТекущиеДанные = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтПоставщика") 
		И ЗначениеЗаполнено(ТекущиеДанные.Документ) И ТекущиеДанные.ПризнакАванса Тогда
		
		КурсКратностьДокументаРасчетов = РасчетыРаботаСФормамиВызовСервера.ПолучитьКурсКратностьДокументаРасчетов(ТекущиеДанные.Документ);
		ТекущиеДанные.Курс = КурсКратностьДокументаРасчетов.Курс;
		ТекущиеДанные.Кратность = КурсКратностьДокументаРасчетов.Кратность;
		
	Иначе
		СтруктураДанных = РасчетыРаботаСФормамиВызовСервера.ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(Объект.Дата, ТекущиеДанные.Договор);
		ТекущиеДанные.Курс = СтруктураДанных.ДоговорВалютаКурсКратность.Курс;
		ТекущиеДанные.Кратность = СтруктураДанных.ДоговорВалютаКурсКратность.Кратность;
	КонецЕсли;
	
	РасшифровкаПлатежаКурсПриИзмененииФрагмент(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТелефонАдресЭППриИзменении(Элемент)
	
	УправлениеНебольшойФирмойКлиент.УстановитьДоступностьТелефонАдресЭП(ЭтаФорма, ТелефонАдресЭП, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НапечататьЧек(Команда)
	
	Если Объект.ВалютаДенежныхСредств <> УправлениеНебольшойФирмойПовтИсп.ПолучитьНациональнуюВалюту() Тогда
		ТекстСообщения = НСтр("ru = 'Валюта оплаты отличается от национальной.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат
	КонецЕсли;
	
	Если Объект.НомерЧекаККМ <> 0 Тогда
		ТекстСообщения = НСтр("ru = 'Чек уже пробит на оборудовании.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если ПечатьДокументовУНФКлиент.ПечатьЧекаНевозможна(ЭтотОбъект, НСтр("ru = 'Не удалось провести документ'")) Тогда
		Возврат;
	КонецЕсли;
	
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПодключенияФискальногоРегистратораЗавершение", ЭтотОбъект);
		МассивОборудования = Новый Массив;
		МассивОборудования.Добавить("ФискальныйРегистратор");
		МассивОборудования.Добавить("ККТ");
		МассивОборудования.Добавить("ПринтерЧеков");
		МенеджерОборудованияКлиент.ПредложитьВыбратьУстройство(ОписаниеОповещения, МассивОборудования,
				НСтр("ru='Выберите оборудование'"), НСтр("ru='Оборудование не подключено.'"));
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Предварительно необходимо выбрать рабочее место внешнего оборудования текущего сеанса.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры // НапечататьЧек()

&НаСервере
Функция ПодготовитьДанныеДляПробитияЧека(ИдентификаторУстройстваФР, ЭтоВозврат) 
	
	// Подготовка таблицы товаров
	ОбщиеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека();
	
	РозничныеПродажиСервер.ДополнитьТоварамиПараметрыПриПробитииЧека(Объект, ОбщиеПараметры);
	СуммаСтрокЧека = РозничныеПродажиСервер.СуммаСтрокЧека(ОбщиеПараметры);
	
	// Общие параметры чека
	РеквизитыОрганизация = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Организация, "НаименованиеПолное,ИНН,КПП");
	
	КассаККМ = Справочники.КассыККМ.ПолучитьКассуККМПоЭкземпляруОборудования(ИдентификаторУстройстваФР);
	Если НЕ ЗначениеЗаполнено(КассаККМ) Тогда
		ТекстСообщения = НСтр("ru = 'Не найдена касса ККМ соответствующая устройству %устройство%.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%устройство%", ИдентификаторУстройстваФР);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("ЭлектронныйЧекSMSПередаютсяПрограммой1С,ЭлектронныйЧекEmailПередаютсяПрограммой1С,СерийныйНомер,Код,ПодключаемоеОборудование,СтруктурнаяЕдиница");
	СтруктураРеквизитов.Вставить("СпособФорматноЛогическогоКонтроля", "ПодключаемоеОборудование.СпособФорматноЛогическогоКонтроля");
	СтруктураРеквизитов.Вставить("ДопустимоеРасхождениеФорматноЛогическогоКонтроля", "ПодключаемоеОборудование.ДопустимоеРасхождениеФорматноЛогическогоКонтроля");
	СтруктураРеквизитов.Вставить("ТипОборудования", "ПодключаемоеОборудование.ТипОборудования");
	РеквизитыКассыККМ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(КассаККМ, СтруктураРеквизитов);
	
	РеквизитыКассира = РозничныеПродажиСервер.ПолучитьРеквизитыКассира(Объект.Автор, Объект.ПодписьКассира);
	
	ОбщиеПараметры.ТипРасчета = ?(ЭтоВозврат,
		Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратРасходаДенежныхСредств,
		Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств);
		
	ОбщиеПараметры.Электронно = Ложь;
	
	Если ЗначениеЗаполнено(Объект.Телефон) Тогда
		Если РеквизитыКассыККМ.ЭлектронныйЧекSMSПередаютсяПрограммой1С Тогда
			ОбщиеПараметры.Отправляет1СSMS = Истина;
		КонецЕсли;
		ОбщиеПараметры.ПокупательНомер = "+7" + РозничныеПродажиСервер.УбратьРазделителиВНомереТелефона(Объект.Телефон);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.АдресЭП) Тогда
		Если РеквизитыКассыККМ.ЭлектронныйЧекEmailПередаютсяПрограммой1С Тогда
			ОбщиеПараметры.Отправляет1СEmail = Истина;
		КонецЕсли;
		ОбщиеПараметры.ПокупательEmail = Объект.АдресЭП;
	КонецЕсли;
	
	ОбщиеПараметры.ДокументОснование = Объект.Ссылка;
	ОбщиеПараметры.ТорговыйОбъект = РеквизитыКассыККМ.СтруктурнаяЕдиница;
	
	// Параметры необходимые для чека ЕНВД на принтере чеков
	ОбщиеПараметры.КассаККМ = КассаККМ;
	ОбщиеПараметры.Кассир = РеквизитыКассира.ИмяКассираИДолжность;
	ОбщиеПараметры.Вставить("ИмяКассира", РеквизитыКассира.ИмяКассира);
	ОбщиеПараметры.КассирИНН = РеквизитыКассира.КассирИНН;
	
	ОбщиеПараметры.ОрганизацияНазвание = РеквизитыОрганизация.НаименованиеПолное;
	ОбщиеПараметры.ОрганизацияИНН = РеквизитыОрганизация.ИНН;
	ОбщиеПараметры.ОрганизацияКПП = РеквизитыОрганизация.КПП;
	ОбщиеПараметры.НомерКассы     = "00001";
	ОбщиеПараметры.НомерЧека      = "1";
	ОбщиеПараметры.НомерСмены     = "1";
	
	АдресМагазина = ПечатьДокументовУНФ.КонтактнаяИнформация(Объект.Организация, ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ФактАдресОрганизации"));
	
	СерийныйНомер = РеквизитыКассыККМ.СерийныйНомер;
	Если НЕ ЗначениеЗаполнено(СерийныйНомер) Тогда
		СерийныйНомер = "1";
	КонецЕсли;
	
	ОбщиеПараметры.АдресМагазина = АдресМагазина;
	ОбщиеПараметры.НаименованиеМагазина = Строка(Объект.Организация);
	ОбщиеПараметры.СерийныйНомер = СерийныйНомер;
	ОбщиеПараметры.СистемаНалогообложения = РозничныеПродажиСервер.ПолучитьТипСистемыНалогообложенияККТ(
		Объект.Организация,
		,
		Объект.Дата,
		Объект.СпециальныйНалоговыйРежим);
		
	Если РеквизитыКассыККМ.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККТ Тогда
		ОбщиеПараметры.СпособФорматноЛогическогоКонтроля = РеквизитыКассыККМ.СпособФорматноЛогическогоКонтроля;
		ОбщиеПараметры.ДопустимоеРасхождениеФорматноЛогическогоКонтроля = РеквизитыКассыККМ.ДопустимоеРасхождениеФорматноЛогическогоКонтроля;
		Если ФорматноЛогическийКонтрольКлиентСервер.НуженФорматноЛогическийКонтроль(ОбщиеПараметры) Тогда
			ФорматноЛогическийКонтрольКлиентСервер.ПровестиФорматноЛогическийКонтроль(ОбщиеПараметры);
		КонецЕсли;
	КонецЕсли;
	
	СтрокаОплаты = Новый Структура("ТипОплаты,Наименование,Сумма", Перечисления.ТипыОплатыККТ.Электронно, НСтр("ru = 'Электронно'"), Объект.СуммаДокумента);
	ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	
	РазницаСумм = СуммаСтрокЧека - Объект.СуммаДокумента;
	Если РазницаСумм > 0 Тогда
		СтрокаОплаты = Новый Структура("ТипОплаты,Наименование,Сумма", Перечисления.ТипыОплатыККТ.Постоплата, НСтр("ru = 'Постоплата'"), РазницаСумм);
		ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	КонецЕсли;
	
	Возврат ОбщиеПараметры;
	
КонецФункции // ПодготовитьДанныеДляПробитияЧека()

&НаКлиенте
Процедура ПодключенияФискальногоРегистратораЗавершение(ИдентификаторУстройстваФР, Параметры) Экспорт
	
	Если ИдентификаторУстройстваФР = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Устройство для печати чеков не выбрано.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ЭтоВозврат = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтПоставщика");
	
	ОбщиеПараметры = ПодготовитьДанныеДляПробитияЧека(ИдентификаторУстройстваФР, ЭтоВозврат);
	
	Если ОбщиеПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщиеПараметры.ДатаВремя = ТекущаяДата();
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОбщиеПараметры", ОбщиеПараметры);
	ДополнительныеПараметры.Вставить("ИдентификаторУстройстваФР", ИдентификаторУстройстваФР);
	ДополнительныеПараметры.Вставить("КассаККМ", ОбщиеПараметры.КассаККМ);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПроверитьКодМаркировкиСредствамиККТЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РозничныеПродажиКлиент.ПроверитьКодМаркировкиСредствамиККТ(ОбщиеПараметры, ЭтотОбъект, НСтр("ru = 'Пробить чек'"), ОповещениеОЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКодМаркировкиСредствамиККТЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ШтрихкодированиеИСМПКлиент.РезультатПроверкиСредствамиККТТребуетФискализации(Результат)
		И ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ОбщиеПараметры") Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПробитьЧекЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		МенеджерОборудованияКлиент.НачатьФискализациюЧекаНаФискальномУстройстве(
			Оповещение,
			УникальныйИдентификатор,
			ДополнительныеПараметры.ОбщиеПараметры,
			ДополнительныеПараметры.ИдентификаторУстройстваФР);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПробитьЧекЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	ЭтотОбъект.Доступность = Истина;
	
	Если РезультатВыполнения.Результат Тогда
		
		// Установить полученное значение номера чека реквизиту документа.
		Если РезультатВыполнения.ВыходныеПараметры <> Неопределено Тогда
			Объект.НомерСменыККМ = РезультатВыполнения.ВыходныеПараметры[0];
			Объект.НомерЧекаККМ = РезультатВыполнения.ВыходныеПараметры[1];
			Объект.КассаККМ = Параметры.КассаККМ;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.НомерЧекаККМ) Тогда
			Объект.НомерЧекаККМ = 1;
		КонецЕсли;
		
		Модифицированность = Истина;
		
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
		
	Иначе
		
		ТекстСообщения = НСтр(
			"ru = 'При печати чека произошла ошибка.
			|Чек не напечатан на фискальном регистраторе.
			|Дополнительное описание:
			|%ДополнительноеОписание%'"
		);
		ТекстСообщения = СтрЗаменить(
			ТекстСообщения,
			"%ДополнительноеОписание%",
			РезультатВыполнения.ОписаниеОшибки
		);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
	УстановитьДоступностьПечатиЧека();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДополнительныйРеквизит(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущийНаборСвойств", ПредопределенноеЗначение("Справочник.НаборыДополнительныхРеквизитовИСведений.Документ_ПоступлениеНаСчет"));
	ПараметрыФормы.Вставить("ЭтоДополнительноеСведение", Ложь);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта", ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотрЧека(Команда)
	
	Если Модифицированность Тогда
		Если Объект.Проведен Тогда
			ТекстВопроса = НСтр("ru = 'Для выполнения команды ""Предварительный просмотр""
				|документ будет проведен. Продолжить?'");
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Провести и продолжить'"));
		Иначе
			ТекстВопроса = НСтр("ru = 'Для выполнения команды ""Предварительный просмотр""
				|документ будет записан. Продолжить?'");
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Записать и продолжить'"));
		КонецЕсли;
		Обработчик = Новый ОписаниеОповещения("ПредварительныйПросмотрЧекаПродолжение", ЭтотОбъект);
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		ПоказатьВопрос(Обработчик, ТекстВопроса, Кнопки);
	Иначе
		ПредварительныйПросмотрЧекаПродолжение(Неопределено, "");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотрЧекаПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.ОК Тогда
		Если Объект.Проведен Тогда
			РезультатЗаписи = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
		Иначе
			РезультатЗаписи = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись));
		КонецЕсли;
		Если Не РезультатЗаписи Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПредварительныйПросмотрЧекаЗавершение", ЭтотОбъект);
		МассивОборудования = Новый Массив;
		МассивОборудования.Добавить("ФискальныйРегистратор");
		МассивОборудования.Добавить("ККТ");
		МассивОборудования.Добавить("ПринтерЧеков");
		МенеджерОборудованияКлиент.ПредложитьВыбратьУстройство(ОписаниеОповещения, МассивОборудования,
				НСтр("ru='Выберите оборудование'"), НСтр("ru='Оборудование не подключено.'"));
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Предварительно необходимо выбрать рабочее место внешнего оборудования текущего сеанса.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотрЧекаЗавершение(ИдентификаторУстройстваФР, Параметры) Экспорт
	
	Если ИдентификаторУстройстваФР = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Устройство для печати чеков не выбрано.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ЭтоВозврат = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтПоставщика");
	
	ОбщиеПараметры = ПодготовитьДанныеДляПробитияЧека(ИдентификаторУстройстваФР, ЭтоВозврат);
	
	Если ОбщиеПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщиеПараметры.ДатаВремя = ТекущаяДата();
	
	РабочееМестоКассираКлиент.ПредпросмотрЧекаДенежныхДокументов(ОбщиеПараметры, ЭтаФорма, ИдентификаторУстройстваФР);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыборТипаОснованияЗавершение(ВыбранноеИмяФормы, Параметры) Экспорт
	
	Если ВыбранноеИмяФормы<>Неопределено Тогда
		
		СтруктураПараметровОтбора = Новый Структура();
		Для каждого элОтбора Из ПараметрыВыбораДокументаОснования Цикл
			ИмяПоляОтбора = СтрЗаменить(элОтбора.Имя,"Отбор.","");
			СтруктураПараметровОтбора.Вставить(ИмяПоляОтбора, элОтбора.Значение);
		КонецЦикла;
		
		Если Объект.ВидОперации = ВидОперацииВозвратЗаймаСотрудником И
			СтрНайти(ВыбранноеИмяФормы.Значение, "ДоговорКредитаИЗайма") > 0 Тогда
			Если Не Объект.Подотчетник.Пустая() Тогда
				СтруктураПараметровОтбора.Вставить("Сотрудник", Объект.Подотчетник);
			КонецЕсли;
		Иначе
			Если НЕ Объект.Контрагент.Пустая() Тогда
				СтруктураПараметровОтбора.Вставить("Контрагент", Объект.Контрагент);
			КонецЕсли;
		КонецЕсли;
		ПараметрыОткрытияФормы = Новый Структура("Отбор", СтруктураПараметровОтбора);
		
		_ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыбратьОснованиеЗавершение", ЭтотОбъект);
		ОткрытьФорму(ВыбранноеИмяФормы.Значение, ПараметрыОткрытияФормы, ЭтотОбъект, ,,,_ОповещениеОЗакрытии);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОснованиеЗавершение(ВыбЗначение, Параметры) Экспорт

	Если ВыбЗначение <> Неопределено Тогда
		Объект.ДокументОснование = ВыбЗначение;
		Элементы.ДокументОснованиеНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(ВыбЗначение);
		Модифицированность = Истина;
		
		ФормыДокументовДеньгиКлиент.ЗаполнитьПоОснованиюНачало(ЭтаФорма);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьСтатьюДДСПриСменеВидаОперации()
	
	Если (Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтПокупателя
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтКурьерскойКомпанииПочты
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтНашейОрганизации)
		И (Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам
		ИЛИ Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.Прочее) Тогда
		Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей;
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтПоставщика
		И (Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей
		ИЛИ Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.Прочее) Тогда
		Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам;
	ИначеЕсли ((Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей И 
				Объект.ВидОперации <> Перечисления.ВидыОперацийПоступлениеНаСчет.ОтПокупателя)
		ИЛИ (Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам И
				Объект.ВидОперации <> Перечисления.ВидыОперацийПоступлениеНаСчет.ОтПоставщика)) Тогда
		Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.Прочее;
	КонецЕсли;
	
	СистемаНалогообложенияСтруктура = РегистрыСведений.СистемыНалогообложенияОрганизаций.ОпределитьСистемуНалогообложенияОрганизации(Объект.Организация, Объект.Дата);
	
	// Если используется стандартная статья Прочее, то нужно установить УчитыватьВНУ
	Если Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.Прочее
		И (Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКартам
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКредитам
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.Налоги) Тогда
		Объект.УчитыватьВНУ = СистемаНалогообложенияСтруктура.ПлательщикУСН;
	Иначе
		Если ЗначениеЗаполнено(Объект.Статья) Тогда
			Объект.УчитыватьВНУ = Объект.Статья.УчитыватьВНУ И СистемаНалогообложенияСтруктура.ПлательщикУСН;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВидОперацииПриИзмененииНаСервере()

&НаСервере
Процедура ЗаполнитьПоДокументу(ДокОснование)
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Заполнить(ДокОснование);
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;
	
	ФормыДокументовДеньги.ЗаполнитьСтавкуНДСПоНалогообложениеНДС(ЭтаФорма);
	ФормыДокументовДеньги.УстановитьВидимостьРеквизитовРасчетов(ЭтаФорма);
	
КонецПроцедуры // ЗаполнитьПоДокументу()

&НаСервере
Функция ПоместитьРасшифровкаПлатежаВХранилище() 
	
	Возврат ПоместитьВоВременноеХранилище(
		Объект.РасшифровкаПлатежа.Выгрузить(,
			"Договор,
			|ПризнакАванса,
			|Документ,
			|Заказ,
			|СуммаРасчетов,
			|Курс,
			|Кратность,
			|СуммаРасчетовКомиссии,
			|СпособЗачета,
			|СуммаПлатежа"
		),
		УникальныйИдентификатор
	);
	
КонецФункции // ПоместитьРасшифровкаПлатежаВХранилище()

&НаСервере
Функция ПоместитьРасшифровкаПлатежаОтАгентаВХранилище() 
	
	Возврат ПоместитьВоВременноеХранилище(
		Объект.РасшифровкаПлатежаОтАгента.Выгрузить(,
			"Документ,
			|Заказ,
			|СуммаПлатежа,
			|УдержаноАгентом,
			|ПолученоОтКлиента"
		),
		УникальныйИдентификатор
	);
	
КонецФункции // ПоместитьРасшифровкаПлатежаВХранилище()

&НаСервере
Процедура ПолучитьРасшифровкаПлатежаИзХранилища(АдресРасшифровкаПлатежаВХранилище, Очищать = Истина)
	
	ТаблицаРасшифровкаПлатежа = ПолучитьИзВременногоХранилища(АдресРасшифровкаПлатежаВХранилище);
	Если Очищать Тогда
		Объект.РасшифровкаПлатежа.Очистить();
	КонецЕсли;
	Для каждого СтрокаРасшифровкаПлатежа Из ТаблицаРасшифровкаПлатежа Цикл
		Строка = Объект.РасшифровкаПлатежа.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, СтрокаРасшифровкаПлатежа);
		Если НЕ Строка.Договор.Пустая() Тогда
			Строка.СтатьяДДС = Строка.Договор.СтатьяДДСПоУмолчанию;
		КонецЕсли;
		Если Строка.СпособЗачета.Пустая() И (Объект.ВидОперации = ВидОперацииОтПокупателя Или Объект.ВидОперации = ВидОперацииОтНашейОрганизации) Тогда
			Строка.СпособЗачета = РасчетыРаботаСФормамиВызовСервера.ПолучитьСпособЗачетаДляДоговора(Строка.Договор, Объект.Контрагент, Ложь);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПолучитьРасшифровкаПлатежаИзХранилища()

&НаСервере
Процедура ПересчитатьСуммыДокумента(Курс, Кратность, ПересчитатьСуммуПлатежа, ДополнительныеПараметры)
	
	Для каждого СтрокаТабличнойЧасти Из Объект.РасшифровкаПлатежа Цикл
		Если СтрокаТабличнойЧасти.Договор.ВалютаРасчетов = Объект.ВалютаДенежныхСредств Тогда
			Если ПересчитатьСуммуПлатежа Тогда
				СтрокаТабличнойЧасти.СуммаПлатежа = СтрокаТабличнойЧасти.СуммаРасчетов;
				СтрокаТабличнойЧасти.Аванс = ?(СтрокаТабличнойЧасти.ПризнакАванса, СтрокаТабличнойЧасти.СуммаПлатежа, 0);
				РасчетыРаботаСФормамиВызовСервера.РассчитатьСуммуНДСНаСервере(СтрокаТабличнойЧасти);
			Иначе
				СтрокаТабличнойЧасти.СуммаРасчетов = СтрокаТабличнойЧасти.СуммаПлатежа;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		Если ПересчитатьСуммуПлатежа Тогда
			СтрокаТабличнойЧасти.СуммаПлатежа = ЦенообразованиеСервер.ПересчитатьИзВалютыВВалюту(
				СтрокаТабличнойЧасти.СуммаРасчетов,
				СтрокаТабличнойЧасти.Курс,
				Курс,
				СтрокаТабличнойЧасти.Кратность,
				Кратность
			);
			СтрокаТабличнойЧасти.Аванс = ?(СтрокаТабличнойЧасти.ПризнакАванса, СтрокаТабличнойЧасти.СуммаПлатежа, 0);
			РасчетыРаботаСФормамиВызовСервера.РассчитатьСуммуНДСНаСервере(СтрокаТабличнойЧасти);
		Иначе
			СтрокаТабличнойЧасти.Курс = ?(
				СтрокаТабличнойЧасти.Курс = 0,
				1,
				СтрокаТабличнойЧасти.Курс
			);
			СтрокаТабличнойЧасти.Кратность = ?(
				СтрокаТабличнойЧасти.Кратность = 0,
				1,
				СтрокаТабличнойЧасти.Кратность
			);
			СтрокаТабличнойЧасти.СуммаРасчетов = ЦенообразованиеСервер.ПересчитатьИзВалютыВВалюту(
				СтрокаТабличнойЧасти.СуммаПлатежа,
				Курс,
				СтрокаТабличнойЧасти.Курс,
				Кратность,
				СтрокаТабличнойЧасти.Кратность
			);
		КонецЕсли;
	КонецЦикла;
	
	// Если нужно, то пересчитаем суммы в ТЧ ДоговорыАвтоЗачетаДолгов.
	Если ПересчитатьСуммуПлатежа Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	КонецЕсли;
	
	ПересчитатьИтогиПриИзмененииАвансаНаСервере();
	Если Объект.ВидОперации = ВидОперацииПомощника Тогда
		ЗаполнитьТаблицуПросмотраНаСервере();
	КонецЕсли;
	
КонецПроцедуры // ПересчитатьСуммыДокумента()

&НаКлиенте
Процедура ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств(СтруктураДанные, ТекстВопроса)
	
	КурсПередИзменением = Курс;
	КратностьПередИзменением = Кратность;
	
	Если ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
		Курс = ?(
			СтруктураДанные.ВалютаКурсКратность.Курс = 0,
			1,
			СтруктураДанные.ВалютаКурсКратность.Курс
		);
		Кратность = ?(
			СтруктураДанные.ВалютаКурсКратность.Кратность = 0,
			1,
			СтруктураДанные.ВалютаКурсКратность.Кратность
		);
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПокупкаВалюты") Тогда
			Объект.Курс = Курс;
			Объект.Кратность = Кратность;
		КонецЕсли;
	КонецЕсли;
	
	// Если курс валюты не изменился или не заполнена валюта денежных средств
	// или документ не заполнен, то ничего не делаем.
	Если (Курс = КурсПередИзменением
		И Кратность = КратностьПередИзменением)
	 ИЛИ (НЕ ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств))
	 ИЛИ (Объект.РасшифровкаПлатежа.Итог("СуммаРасчетов") = 0
	 И НЕ ЗначениеЗаполнено(Объект.СуммаДокумента)) Тогда
		
		// Обновить курсы расчетов
		// Если в расшифровке платежа есть курс <> 1, то предложим его перезаполнить.
		Если СтруктураДанные.Свойство("ОбновитьКурсыРасчетов") И СтруктураДанные.ОбновитьКурсыРасчетов Тогда
			ПоказатьВопрос(Новый ОписаниеОповещения("ОбновитьКурсВРасшифровкеПлатежаИПересчитатьСуммы", ЭтотОбъект), СтруктураДанные.ОбновитьКурсыРасчетовТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
		// Конец Обновить курсы расчетов
		
		Возврат;
	КонецЕсли;
	
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КурсПередИзменением", КурсПередИзменением);
	ДополнительныеПараметры.Вставить("КратностьПередИзменением", КратностьПередИзменением);
	
	// Обновить курсы расчетов
	Если СтруктураДанные.Свойство("ОбновитьКурсыРасчетов") Тогда
		ДополнительныеПараметры.Вставить("ОбновитьКурсыРасчетов", СтруктураДанные.ОбновитьКурсыРасчетов);
		ДополнительныеПараметры.Вставить("ОбновитьКурсыРасчетовТекстВопроса", СтруктураДанные.ОбновитьКурсыРасчетовТекстВопроса);
	КонецЕсли;
	// Конец Обновить курсы расчетов
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОпределитьНеобходимостьПересчетаСуммПриИзмененииКурса", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры // ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств()

&НаСервере
Процедура РассчитатьСуммуПлатежаНаСервере(СтрокаТабличнойЧасти, ИмяКолонки = "")
	
	СтруктураДанные = РасчетыРаботаСФормамиВызовСервера.ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(
			Объект.Дата,
			СтрокаТабличнойЧасти.Договор
		);
		
	СтрокаТабличнойЧасти.Курс = ?(
		СтрокаТабличнойЧасти.Курс = 0,
		?(СтруктураДанные.ДоговорВалютаКурсКратность.Курс =0, 1, СтруктураДанные.ДоговорВалютаКурсКратность.Курс),
		СтрокаТабличнойЧасти.Курс
	);
	СтрокаТабличнойЧасти.Кратность = ?(
		СтрокаТабличнойЧасти.Кратность = 0,
		1,
		СтрокаТабличнойЧасти.Кратность
	);
	
	Если СтрокаТабличнойЧасти.СуммаРасчетов = 0 Тогда
		СтрокаТабличнойЧасти.СуммаПлатежа = 0;
		СтрокаТабличнойЧасти.Курс = СтруктураДанные.ДоговорВалютаКурсКратность.Курс;
	ИначеЕсли Объект.ВалютаДенежныхСредств = СтруктураДанные.ВалютаРасчетов Тогда
		СтрокаТабличнойЧасти.СуммаПлатежа = СтрокаТабличнойЧасти.СуммаРасчетов;
	ИначеЕсли СтрокаТабличнойЧасти.СуммаПлатежа = 0 ИЛИ
		(ИмяКолонки = "Курс" ИЛИ ИмяКолонки = "Кратность") Тогда
		Если СтрокаТабличнойЧасти.Курс = 0 Тогда
			СтрокаТабличнойЧасти.СуммаПлатежа = 0;
		Иначе
			СтрокаТабличнойЧасти.СуммаПлатежа = ЦенообразованиеСервер.ПересчитатьИзВалютыВВалюту(
				СтрокаТабличнойЧасти.СуммаРасчетов,
				СтрокаТабличнойЧасти.Курс,
				Курс,
				СтрокаТабличнойЧасти.Кратность,
				Кратность
			);
		КонецЕсли;
	Иначе
		СтрокаТабличнойЧасти.Курс = ?(
			СтрокаТабличнойЧасти.СуммаРасчетов = 0 ИЛИ СтрокаТабличнойЧасти.СуммаПлатежа = 0,
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс, //СтрокаТабличнойЧасти.Курс,
			СтрокаТабличнойЧасти.СуммаПлатежа / СтрокаТабличнойЧасти.СуммаРасчетов * Курс
		);
		СтрокаТабличнойЧасти.Кратность = ?(
			СтрокаТабличнойЧасти.СуммаРасчетов = 0 ИЛИ СтрокаТабличнойЧасти.СуммаПлатежа = 0,
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность,
			СтрокаТабличнойЧасти.Кратность
		);
	КонецЕсли;
	
	СтрокаТабличнойЧасти.Аванс = ?(СтрокаТабличнойЧасти.ПризнакАванса, СтрокаТабличнойЧасти.СуммаПлатежа, 0);
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
		СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
	КонецЕсли;
	
	РасчетыРаботаСФормамиВызовСервера.РассчитатьСуммуНДСНаСервере(СтрокаТабличнойЧасти);
	ПересчитатьИтогиПриИзмененииАвансаНаСервере();
	
КонецПроцедуры // РассчитатьСуммуПлатежаНаСервере()

&НаКлиенте
Процедура РассчитатьСуммуУчета()
	
	Объект.Курс = ?(
		Объект.Курс = 0,
		1,
		Объект.Курс
	);
	Объект.Кратность = ?(
		Объект.Кратность = 0,
		1,
		Объект.Кратность
	);
	
	Объект.СуммаУчета = ВалютыУНФКлиентСервер.Пересчитать(Объект.СуммаДокумента, Объект.Курс, КурсВалютыУчета,
		Объект.Кратность, КратностьВалютыУчета);
	
КонецПроцедуры // РассчитатьСуммуУчета()

&НаСервереБезКонтекста
Функция ПолучитьДанныеДокументаПланированияПриИзменении(ДокументПланирования)
	
	СтруктураДанных = Новый Структура("СтатьяДДС", Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеДСПлан.СтатьяДвиженияДенежныхСредств
		|ИЗ
		|	Документ.ПоступлениеДСПлан КАК ПоступлениеДСПлан
		|ГДЕ
		|	ПоступлениеДСПлан.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументПланирования);
	
	Если ТипЗнч(ДокументПланирования) = Тип("ДокументСсылка.ПеремещениеДСПлан") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.ПоступлениеДСПлан КАК ПоступлениеДСПлан", "Документ.ПеремещениеДСПлан КАК ПоступлениеДСПлан");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		СтруктураДанных.Вставить("СтатьяДДС", Выборка.СтатьяДвиженияДенежныхСредств);
	КонецЕсли;
	
	Возврат СтруктураДанных;
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеКонтрагентПриИзменении(Контрагент, Организация, Дата)
	
	ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Контрагент, Организация, Объект.ВидОперации);
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"Договор",
		ДоговорПоУмолчанию
	);
	
	СтруктураДанные.Вставить(
		"ДоговорВалютаКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
			Дата,
			Новый Структура("Валюта", ДоговорПоУмолчанию.ВалютаРасчетов)
		)
	);
	
	СтруктураДанные.Вставить(
		"ВестиРасчетыПоДоговорам",
		Контрагент.ВестиРасчетыПоДоговорам
	);
	
	СтруктураДанные.Вставить(
		"ВестиРасчетыПоДокументам",
		Контрагент.ВестиРасчетыПоДокументам
	);
	
	СтруктураДанные.Вставить(
		"ВестиРасчетыПоДоговорам",
		Контрагент.ВестиРасчетыПоДоговорам
	);
	
	СтруктураДанные.Вставить(
		"ВестиРасчетыПоЗаказам",
		Контрагент.ВестиРасчетыПоЗаказам
	);
	
	СтруктураДанные.Вставить(
		"ВестиУчетОплатыПоСчетам",
		Контрагент.ВестиУчетОплатыПоСчетам
	);
	
	// Прочие расчеты
	Если Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.РасчетыПоКредитам Тогда
		ДоговорКредитаЗаймаПоУмолчанию = ПолучитьДоговорКредитаЗаймаПоУмолчанию(Объект.Ссылка, Контрагент, Организация, Объект.ВидОперации);
		СтруктураДанные.Вставить(
			"ДоговорКредитаЗаймаПоУмолчанию",
			ДоговорКредитаЗаймаПоУмолчанию
		);
	КонецЕсли;
	// Конец Прочие расчеты
	
	ФормыДокументовДеньги.УстановитьВидимостьРеквизитовРасчетов(ЭтаФорма);
	
	СтруктураДанные.Вставить("СтатьяДДСПоУмолчанию", Контрагент.СтатьяДДСПоУмолчанию);
	Если ЗначениеЗаполнено(Контрагент.СтатьяДДСПоУмолчанию) Тогда
		СтруктураДанные.Вставить("УчитыватьВНУ", РегламентированнаяОтчетностьУСН.НужноУчитыватьВНУ(Контрагент.СтатьяДДСПоУмолчанию, Объект.ВидОперации, Организация, Дата));
	Иначе
		СтруктураДанных = ФормыДокументовДеньги.ПолучитьДанныеСтатьиДДС(ЭтотОбъект);
		СтруктураДанные.Вставить("СтатьяДДСПоУмолчанию", СтруктураДанных.Статья);
		СтруктураДанные.Вставить("УчитыватьВНУ", СтруктураДанных.УчитыватьВНУ);
	КонецЕсли;
	
	СтруктураДанные.Вставить("СпособЗачета", РасчетыРаботаСФормамиВызовСервера.ПолучитьСпособЗачетаДляДоговора(ДоговорПоУмолчанию, Контрагент, Ложь));
	СпособЗачетаПоУмолчаниюДляКонтрагента = РасчетыРаботаСФормамиВызовСервера.ПолучитьСпособЗачетаДляКонтрагента(Объект.Контрагент, Ложь);
	
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	
	Если ИспользоватьПодключаемоеОборудование Тогда
		ДанныеКИКонтрагента = Справочники.Контрагенты.ДанныеКИКонтрагентаДляВыбораНаФорме(Контрагент, Элементы,
			ПоляКИДляОтправкиЧека());
		ЗаполнитьПоляКИДляОтправкиЧекаПоУмолчанию(ДанныеКИКонтрагента);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("УчетПоПроектам") И ЗначениеЗаполнено(ДоговорПоУмолчанию) И НЕ ЗначениеЗаполнено(Объект.Проект) Тогда
		СтруктураДанные.Вставить("Проект", Справочники.Проекты.ПолучитьПроектПоДоговору(ДоговорПоУмолчанию));
	КонецЕсли;
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеКонтрагентПриИзменении()

&НаСервере
Процедура УстановитьВидимостьРеквизитовРасчетов()
	
	ФормыДокументовДеньги.УстановитьВидимостьРеквизитовРасчетов(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеБанковскийСчетПриИзменении(Дата, БанковскийСчет, СчетКонтрагента)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"ВалютаКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
			Дата,
			Новый Структура("Валюта", БанковскийСчет.ВалютаДенежныхСредств)
		)
	);
	СтруктураДанные.Вставить(
		"ВалютаДенежныхСредств",
		БанковскийСчет.ВалютаДенежныхСредств
	);
	
	СтруктураДанные.Вставить(
		"СчетКонтрагента",
		?(ЗначениеЗаполнено(СчетКонтрагента) И СчетКонтрагента.ВалютаДенежныхСредств = БанковскийСчет.ВалютаДенежныхСредств, СчетКонтрагента, Неопределено)
	);
	
	СтруктураДанные.Вставить(
		"СчетЭквайринговогоТерминала",
		?(ЗначениеЗаполнено(Объект.ЭквайринговыйТерминал), Объект.ЭквайринговыйТерминал.БанковскийСчетЭквайринг, Неопределено)
	);
	
	ОбновитьВидимостьИОстаткиДС();
	
	УстановитьЗаголовкиКолонокТабличныхЧастей(СтруктураДанные.ВалютаДенежныхСредств);
	
	УстановитьВидимостьКурсаВалюты(БанковскийСчет.ВалютаДенежныхСредств);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеБанковскийСчетПриИзменении()

&НаСервере
Процедура УстановитьСтатьиДДСВРасшифровкеПлатежа();
	
	ФормыДокументовДеньги.УстановитьСтатьиДДСВРасшифровкеПлатежа(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением)
	
	РазностьДат = ДокументыУНФ.ПроверитьНомерДокумента(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
	ВалютаКурсКратность = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Объект.Дата, Новый Структура("Валюта", Объект.ВалютаДенежныхСредств));
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"РазностьДат",
		РазностьДат
	);
	СтруктураДанные.Вставить(
		"ВалютаКурсКратность",
		ВалютаКурсКратность
	);
	
	ФормыДокументовДеньги.ЗаполнитьСтавкуНДСПоОрганизацииНалогообложениеНДС(ЭтаФорма);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДатаПриИзменении()

&НаСервере
Функция ПолучитьДанныеОрганизацияПриИзменении()
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"Компания",
		Константы.УчетПоКомпании.Компания(Объект.Организация)
	);
	СтруктураДанные.Вставить(
		"БанковскийСчет",
		Объект.Организация.БанковскийСчетПоУмолчанию
	);
	СтруктураДанные.Вставить(
		"ВалютаКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
			Объект.Дата,
			Новый Структура("Валюта", Объект.Организация.БанковскийСчетПоУмолчанию.ВалютаДенежныхСредств)
		)
	);
	СтруктураДанные.Вставить(
		"ВалютаДенежныхСредств",
		Объект.Организация.БанковскийСчетПоУмолчанию.ВалютаДенежныхСредств
	);
	СтруктураДанные.Вставить(
		"СчетКонтрагента",
		?(ЗначениеЗаполнено(Объект.СчетКонтрагента) И СтруктураДанные.БанковскийСчет.ВалютаДенежныхСредств = Объект.СчетКонтрагента.ВалютаДенежныхСредств, Объект.СчетКонтрагента, Справочники.БанковскиеСчета.ПустаяСсылка())
	);
	
	ФормыДокументовДеньги.ЗаполнитьСтавкуНДСПоОрганизацииНалогообложениеНДС(ЭтаФорма);
	
	ОбновитьВидимостьИОстаткиДС(СтруктураДанные.БанковскийСчет);
	
	УстановитьЗаголовкиКолонокТабличныхЧастей(СтруктураДанные.ВалютаДенежныхСредств);
	
	УстановитьВидимостьКурсаВалюты(СтруктураДанные.БанковскийСчет.ВалютаДенежныхСредств);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеОрганизацияПриИзменении()

&НаСервере
Процедура ВидОперацииПриИзмененииНаСервере(ЭтоЗаполнениеПоОснованию = Ложь)
	
	УстановитьСвязиПараметровВыбораДоступныеТипы();
	
	УстановитьВидимостьПечатиЧека();
	
	// Прочие расчеты
	Если Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ПрочиеРасчеты 
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтКурьерскойКомпанииПочты
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ВзносНаличными
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ПереводСДругогоСчета Тогда
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
		СтавкаНДСПоУмолчаниюЧисло = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтавкаНДСПоУмолчанию);
		Объект.РасшифровкаПлатежа[0].СтавкаНДС = СтавкаНДСПоУмолчанию;
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ВозвратЗаймаСотрудником Тогда
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
		СтавкаНДСПоУмолчаниюЧисло = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтавкаНДСПоУмолчанию);
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ЛичныеСредстваПредпринимателя Тогда
		Объект.Корреспонденция = ПланыСчетов.Управленческий.НераспределеннаяПрибыль;
		Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ЛичныеСредстваПредпринимателя;
		Если Объект.Организация.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
			Объект.Организация = Справочники.Организации.ПустаяСсылка();
		КонецЕсли;
	// Конец Прочие расчеты
	ИначеЕсли НЕ ЭтоЗаполнениеПоОснованию Тогда
		ФормыДокументовДеньги.ЗаполнитьСтавкуНДСПоОрганизацииНалогообложениеНДС(ЭтаФорма);
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтКурьерскойКомпанииПочты Тогда
		Объект.Корреспонденция = ПланыСчетов.Управленческий.РасчетыСРазнымиКредиторами;
	КонецЕсли;
	
	ФормыДокументовДеньги.УстановитьВидимостьЭлементовВЗависимостиОтВидаОперации(ЭтаФорма);
	УстановитьСтатьюДДСПриСменеВидаОперации();
	
КонецПроцедуры // ВидОперацииПриИзмененииНаСервере()

&НаСервере
Процедура ЗаполнитьСтавкуНДСПоНалогообложениеНДС(ВосстанавливатьСтавкиНДС = Истина)
	
	ФормыДокументовДеньги.ЗаполнитьСтавкуНДСПоНалогообложениеНДС(ЭтаФорма, ВосстанавливатьСтавкиНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеДоговораКонтрагента(СтрокаТабличнойЧасти = Неопределено)
	
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Договор) Тогда
		СтруктураДанные = РасчетыРаботаСФормамиВызовСервера.ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(
			Объект.Дата,
			СтрокаТабличнойЧасти.Договор,
			СтрокаТабличнойЧасти.ДокументПланирования,
			СтрокаТабличнойЧасти.СтатьяДДС
		);
		СтрокаТабличнойЧасти.Курс = ?(
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс = 0,
			1,
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс
		);
		СтрокаТабличнойЧасти.Кратность = ?(
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность = 0,
			1,
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность
		);
		
		СтрокаТабличнойЧасти.СтатьяДДС = СтруктураДанные.СтатьяДДСПоУмолчанию;
		
		Если Объект.ВидОперации = ВидОперацииОтПокупателя Или Объект.ВидОперации = ВидОперацииОтНашейОрганизации Тогда
			СтрокаТабличнойЧасти.СпособЗачета = СтруктураДанные.СпособЗачета;
		Иначе
			СтрокаТабличнойЧасти.СпособЗачета = СпособЗачетаВручную;
		КонецЕсли;
		
		Если СтруктураДанные.Свойство("Проект") Тогда
			СтрокаТабличнойЧасти.Проект = СтруктураДанные.Проект;
		КонецЕсли;
		
	Иначе
		СтрокаТабличнойЧасти.СтатьяДДС = РасчетыРаботаСФормамиВызовСервера.ПолучитьСтатьюДДСПоУмолчаниюДляСтрокиРасшифровки(
			СтрокаТабличнойЧасти.Договор,
			СтрокаТабличнойЧасти.ДокументПланирования,
			СтрокаТабличнойЧасти.СтатьяДДС
		);
		Если Объект.ВидОперации = ВидОперацииОтПокупателя Или Объект.ВидОперации = ВидОперацииОтНашейОрганизации Тогда
			СтрокаТабличнойЧасти.СпособЗачета = РасчетыРаботаСФормамиВызовСервера.ПолучитьСпособЗачетаДляДоговора(
				СтрокаТабличнойЧасти.Договор,
				Объект.Контрагент,
				Ложь
			);
		Иначе
			СтрокаТабличнойЧасти.СпособЗачета = СпособЗачетаВручную;
		КонецЕсли;
	КонецЕсли;
	
	СтрокаТабличнойЧасти.СуммаРасчетов = ВалютыУНФКлиентСервер.Пересчитать(СтрокаТабличнойЧасти.СуммаПлатежа, Курс,
		СтрокаТабличнойЧасти.Курс, Кратность, СтрокаТабличнойЧасти.Кратность);
	
	СтрокаТабличнойЧасти.ЭтоВзаимозачет = (ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.Взаимозачет"));
	
КонецПроцедуры // ОбработатьИзменениеДоговораКонтрагента()

&НаСервере
Процедура ОбновитьКурсыРасчетовРасшифровкиПлатежаНаСервере()
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.РасшифровкаПлатежа Цикл
			
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Договор) Тогда
			СтруктураДанные = РасчетыРаботаСФормамиВызовСервера.ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(
				Объект.Дата,
				СтрокаТабличнойЧасти.Договор
			);
			СтрокаТабличнойЧасти.Курс = ?(
				СтруктураДанные.ДоговорВалютаКурсКратность.Курс = 0,
				1,
				СтруктураДанные.ДоговорВалютаКурсКратность.Курс
			);
			СтрокаТабличнойЧасти.Кратность = ?(
				СтруктураДанные.ДоговорВалютаКурсКратность.Кратность = 0,
				1,
				СтруктураДанные.ДоговорВалютаКурсКратность.Кратность
			);
		КонецЕсли;
		
		СтрокаТабличнойЧасти.СуммаРасчетов = ЦенообразованиеСервер.ПересчитатьИзВалютыВВалюту(
			СтрокаТабличнойЧасти.СуммаПлатежа,
			Курс,
			СтрокаТабличнойЧасти.Курс,
			Кратность,
			СтрокаТабличнойЧасти.Кратность
		);
		
	КонецЦикла;
	
	Если Объект.ВидОперации = ВидОперацииОтПокупателя Или Объект.ВидОперации = ВидОперацииОтНашейОрганизации Тогда
		ПересчитатьИтогиПриИзмененииАвансаНаСервере();
		ЗаполнитьТаблицуПросмотраНаСервере();
	КонецЕсли;
	
КонецПроцедуры // ОбработатьИзменениеДоговораКонтрагента()

&НаКлиенте
Процедура ОбновитьКурсВРасшифровкеПлатежаИПересчитатьСуммы(КнопкаРезультат, ДополнительныеПараметры) Экспорт

	Если КнопкаРезультат = КодВозвратаДиалога.Да Тогда
		ОбновитьКурсыРасчетовРасшифровкиПлатежаНаСервере();
	КонецЕсли;

КонецПроцедуры // ОбновитьКурсВРасшифровкеПлатежаИПересчитатьСуммыЗавершение()

&НаКлиенте
Процедура ОбработатьВыборДокументаРасчетов(ДанныеДокумента)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	Если ТипЗнч(ДанныеДокумента) = Тип("Структура") Тогда
		
		СтрокаТабличнойЧасти.Документ = ДанныеДокумента.Документ;
		Если ДанныеДокумента.УстановитьЗаказ Тогда
			СтрокаТабличнойЧасти.Заказ = ДанныеДокумента.Заказ;
		КонецЕсли;
		СтрокаТабличнойЧасти.СчетНаОплату = ДанныеДокумента.СчетНаОплату;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Договор) Тогда
			СтрокаТабличнойЧасти.Договор = ДанныеДокумента.Договор;
			ОбработатьИзменениеДоговораКонтрагента(СтрокаТабличнойЧасти);
		КонецЕсли;
		
		ВыполнитьДействияПриИзмененииДокументаРасчетов();
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработатьВыборДокументаРасчетов()

&НаКлиенте
Процедура ВыполнитьДействияПриИзмененииДокументаРасчетов()
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтПоставщика") Тогда
		
		Если ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.РасходИзКассы")
			ИЛИ ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.РасходСоСчета")
			ИЛИ ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
			
			СтрокаТабличнойЧасти.ПризнакАванса = Истина;
			
			КурсКратностьДокументаРасчетов = РасчетыРаботаСФормамиВызовСервера.ПолучитьКурсКратностьДокументаРасчетов(СтрокаТабличнойЧасти.Документ);
			
			Если ЗначениеЗаполнено(КурсКратностьДокументаРасчетов) Тогда
				ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, КурсКратностьДокументаРасчетов);
				РасшифровкаПлатежаКурсПриИзмененииФрагмент(СтрокаТабличнойЧасти);
			КонецЕсли
			
		Иначе
			
			СтрокаТабличнойЧасти.ПризнакАванса = Ложь;
			
		КонецЕсли;
		
	ИначеЕсли Объект.ВидОперации = ВидОперацииОтПокупателя Или Объект.ВидОперации = ВидОперацииОтНашейОрганизации Тогда
		
		СтрокаТабличнойЧасти.ПризнакАванса = НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Документ);
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Документ) Тогда
			СтрокаТабличнойЧасти.СпособЗачета = СпособЗачетаВручную;
		КонецЕсли;
		
	КонецЕсли;
	
	СтрокаТабличнойЧасти.ЭтоВзаимозачет = (ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.Взаимозачет"));
	
КонецПроцедуры // ВыполнитьДействияПриИзмененииДокументаРасчетов()

&НаСервере
Процедура ЗаполнитьРасшифровкуПлатежа(ТекущийОбъект = Неопределено)
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ЗаполнитьРасшифровкуПлатежа();
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;
	
КонецПроцедуры // ЗаполнитьРасшифровкуПлатежа()

&НаСервереБезКонтекста
Функция ПолучитьПараметрыФормыВыбора(Документ, Организация, Контрагент, Договор, ВидОперации)
	
	СписокВидовДоговоров = Справочники.ДоговорыКонтрагентов.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КонтролироватьВыборДоговора", Контрагент.ВестиРасчетыПоДоговорам);
	ПараметрыФормы.Вставить("Контрагент", Контрагент);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("ВидыДоговоров", СписокВидовДоговоров);
	ПараметрыФормы.Вставить("ТекущаяСтрока", Договор);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация, ВидОперации) Экспорт
	
	Если Не Контрагент.ВестиРасчетыПоДоговорам Тогда
		Возврат Справочники.ДоговорыКонтрагентов.ДоговорПоУмолчанию(Контрагент);
	КонецЕсли;
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, СписокВидовДоговоров);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

&НаСервере
Процедура ОбновитьВидимостьИОстаткиВзаиморасчетов()
	
	Элементы.ОстатокВзаиморасчетов.Видимость = НЕ ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.Контрагент);
	ОстатокВзаиморасчетов = 0;
	
	Если НЕ Элементы.ОстатокВзаиморасчетов.Видимость Тогда
		//Только для новых объектов
		Элементы.Контрагент.АвтоМаксимальнаяШирина = Истина;
	Иначе
		Элементы.Контрагент.АвтоМаксимальнаяШирина = Ложь;
		Элементы.Контрагент.МаксимальнаяШирина = 22;
	КонецЕсли;
	
	Элементы.ОстатокВзаиморасчетов.Заголовок = РаботаСКонтрагентамиУНФ.ЗаголовокНадписиВзаиморасчетов(Объект.Контрагент, ОстатокВзаиморасчетов);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьИОстаткиДС(БанковскийСчет = Неопределено)
	
	Если БанковскийСчет = Неопределено Тогда
		ТекущийБанковскийСчет = Объект.БанковскийСчет;
	Иначе
		ТекущийБанковскийСчет = БанковскийСчет;
	КонецЕсли;
	
	Элементы.ОстатокДенежныхСредств.Видимость = НЕ ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(ТекущийБанковскийСчет);
	ОстатокДенежныхСредств = 0;
	
	Если НЕ Элементы.ОстатокДенежныхСредств.Видимость Тогда
		//Только для новых объектов
		Элементы.БанковскийСчет.АвтоМаксимальнаяШирина = Истина;
		Возврат;
	КонецЕсли;
	
	Элементы.БанковскийСчет.АвтоМаксимальнаяШирина = Ложь;
	Элементы.БанковскийСчет.МаксимальнаяШирина = 22;
	Элементы.ОстатокДенежныхСредств.Заголовок = ДвиженияДенежныхСредствВызовСервера.ЗаголовокНадписиОстатковДС(ТекущийБанковскийСчет, Объект.Организация, ОстатокДенежныхСредств);
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьИтогиПриИзмененииАвансаНаКлиенте() Экспорт
	
	СуммаПлатежаАванс = Объект.РасшифровкаПлатежа.Итог("Аванс");
	СуммаПлатежаДляУсловногоОформления = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьИтогиПриИзмененииАвансаНаСервере()
	
	СуммаПлатежаАванс = Объект.РасшифровкаПлатежа.Итог("Аванс");
	СуммаПлатежаДляУсловногоОформления = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	
	РассчитатьСуммуНДСПоУмолчаниюНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСуммуНДСПоУмолчаниюНаСервере()
	
	ИтогоРазнесено = Объект.ДоговорыАвтоЗачетаДолгов.Итог("СуммаПлатежа");
	СуммаНДСПоУмолчанию = ИтогоРазнесено - (ИтогоРазнесено) / ((СтавкаНДСПоУмолчаниюЧисло + 100) / 100);
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьНеобходимостьПересчетаСуммПриИзмененииКурса(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = КодВозвратаДиалога.Да Тогда
		
		КурсПередИзменением = ДополнительныеПараметры.КурсПередИзменением;
		КратностьПередИзменением = ДополнительныеПараметры.КратностьПередИзменением;
		
		Если Объект.РасшифровкаПлатежа.Количество() > 0 Тогда
			
			Если Объект.ВидОперации = ВидОперацииОтПокупателя
				ИЛИ Объект.ВидОперации = ВидОперацииОтПоставщика
				ИЛИ Объект.ВидОперации = ВидОперацииОтНашейОрганизации Тогда
				
				ПересчитатьСуммыДокумента(Курс, Кратность, Истина, ДополнительныеПараметры);
				
			// Прочие расчеты
			ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПрочиеРасчеты")
				ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтКурьерскойКомпанииПочты")
				ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ВозвратЗаймаСотрудником") Тогда
				
				ПересчитатьСуммыДокумента(Курс, Кратность, Истина, ДополнительныеПараметры);
				
			// Конец Прочие расчеты
			ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПокупкаВалюты") Тогда
				
				Для каждого СтрокаТабличнойЧасти Из Объект.РасшифровкаПлатежа Цикл
					
					СтрокаТабличнойЧасти.СуммаРасчетов = ВалютыУНФКлиентСервер.Пересчитать(
						СтрокаТабличнойЧасти.СуммаРасчетов, КурсПередИзменением, Курс, КратностьПередИзменением,
						Кратность);
					
				КонецЦикла;
				
			Иначе
				
				СуммаДокументаРавнаИтогуСуммыПлатежа = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа") = Объект.СуммаДокумента;
				
				Для каждого СтрокаТабличнойЧасти Из Объект.РасшифровкаПлатежа Цикл // для видов операций с планируемыми платежами пересчитываем сумму плана.
					
					СтрокаТабличнойЧасти.СуммаПлатежа = ВалютыУНФКлиентСервер.Пересчитать(
						СтрокаТабличнойЧасти.СуммаПлатежа, КурсПередИзменением, Курс, КратностьПередИзменением,
						Кратность);
					
					РасчетыРаботаСФормамиКлиент.РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
				КонецЦикла;
					
				Если СуммаДокументаРавнаИтогуСуммыПлатежа Тогда
					
					Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
					
				Иначе
					
					Объект.СуммаДокумента = ВалютыУНФКлиентСервер.Пересчитать(Объект.СуммаДокумента, КурсПередИзменением,
						Курс, КратностьПередИзменением, Кратность);
					
				КонецЕсли;
				
				ПересчитатьИтогиПриИзмененииАвансаНаКлиенте();
				
			КонецЕсли;
			
		Иначе
			
			Объект.СуммаДокумента = ВалютыУНФКлиентСервер.Пересчитать(Объект.СуммаДокумента, КурсПередИзменением, Курс,
				КратностьПередИзменением, Кратность);
			
		КонецЕсли;
		
	Иначе
		
		Если Объект.РасшифровкаПлатежа.Количество() > 0 Тогда
			
			ПересчитатьСуммыДокумента(Курс, Кратность, Ложь, ДополнительныеПараметры);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПокупкаВалюты") Тогда
		
		РассчитатьСуммуУчета();
		ОбновитьОтображениеЭлементовПокупкаВалюты();
		
	КонецЕсли;
	
	// Обновить курсы расчетов
	Если НЕ ((Объект.ВидОперации = ВидОперацииОтПокупателя Или Объект.ВидОперации = ВидОперацииОтНашейОрганизации) И Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиАвтоматически) Тогда
		Если ДополнительныеПараметры.Свойство("ОбновитьКурсыРасчетов")  И ДополнительныеПараметры.ОбновитьКурсыРасчетов Тогда
			ПоказатьВопрос(Новый ОписаниеОповещения("ОбновитьКурсВРасшифровкеПлатежаИПересчитатьСуммы", ЭтотОбъект),
				ДополнительныеПараметры.ОбновитьКурсыРасчетовТекстВопроса,
				РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
	КонецЕсли;
	// Конец Обновить курсы расчетов
	
КонецПроцедуры // ОпределитьНеобходимостьПересчетаСуммПриИзмененииКурса()

&НаСервере
Функция ПоляКИДляОтправкиЧека()
	
	ПоляКИ = Новый Соответствие;
	ПоляКИ.Вставить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, Элементы.АдресЭП.Имя);
	ПоляКИ.Вставить(Перечисления.ТипыКонтактнойИнформации.Телефон, Элементы.Телефон.Имя);
	
	Возврат ПоляКИ;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПоляКИДляОтправкиЧекаПоУмолчанию(ДанныеКИ)
	
	Если ДанныеКИ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.АдресЭП = ДанныеКИ.Получить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Объект.Телефон = ДанныеКИ.Получить(Перечисления.ТипыКонтактнойИнформации.Телефон);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПараметрыФормыВыбораДоговора(Документ, Организация, Контрагент, Договор, ВидОперации)
	
	СписокВидовДоговоров = Справочники.ДоговорыКонтрагентов.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КонтролироватьВыборДоговора", Контрагент.ВестиРасчетыПоДоговорам);
	ПараметрыФормы.Вставить("Контрагент", Контрагент);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("ВидыДоговоров", СписокВидовДоговоров);
	ПараметрыФормы.Вставить("ТекущаяСтрока", Договор);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаКлиенте
Процедура РассчитатьСуммуПлатежаОтАгента(ДанныеСтроки)
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки.СуммаПлатежа = ДанныеСтроки.ПолученоОтКлиента - ДанныеСтроки.УдержаноАгентом;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСуммуПлатежаОтАгентаНаСервере(ДанныеСтроки)
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки.СуммаПлатежа = ДанныеСтроки.ПолученоОтКлиента - ДанныеСтроки.УдержаноАгентом;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаОтАгентаПолученоОтКлиентаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.РасшифровкаПлатежаОтАгента.ТекущиеДанные;
	РассчитатьСуммуПлатежаОтАгента(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаОтАгентаУдержаноАгентомПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.РасшифровкаПлатежаОтАгента.ТекущиеДанные;
	РассчитатьСуммуПлатежаОтАгента(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаОтАгентаСуммаПлатежаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.РасшифровкаПлатежаОтАгента.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда
		ТекущаяСтрока.ПолученоОтКлиента = ТекущаяСтрока.СуммаПлатежа + ТекущаяСтрока.УдержаноАгентом;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РасшифровкаПлатежаОтАгентаЗаказПриИзмененииНаСервере(ТекущиеДанныеИдентификатор)
	
	ТекущиеДанные = Объект.РасшифровкаПлатежаОтАгента.НайтиПоИдентификатору(ТекущиеДанныеИдентификатор);
	Если ТекущиеДанные <> Неопределено Тогда
		
		// Проверим валюту заказа
		Если ЗначениеЗаполнено(ТекущиеДанные.Заказ) И ТекущиеДанные.Заказ.Договор.ВалютаРасчетов <> Константы.НациональнаяВалюта.Получить() Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Не поддерживается выбор заказов, которые оформлены по договорам в валюте или у.е.'");
			Сообщение.Поле = "РасшифровкаПлатежаОтАгента["+Объект.РасшифровкаПлатежаОтАгента.Индекс(ТекущиеДанные)+"].Документ";
			Сообщение.Сообщить();
			
			ТекущиеДанные.Заказ = Неопределено;
			Возврат;
		КонецЕсли;
		
		// Заполним счет на оплату
		Если ТекущиеДанные.Заказ.Контрагент.ВестиУчетОплатыПоСчетам
			И ЗначениеЗаполнено(ТекущиеДанные.Заказ)
			И НЕ ЗначениеЗаполнено(ТекущиеДанные.СчетНаОплату) Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СчетНаОплату.Ссылка КАК СчетНаОплату
			|ИЗ
			|	Документ.СчетНаОплату КАК СчетНаОплату
			|ГДЕ
			|	СчетНаОплату.ДокументОснование = &ДокументОснование";
			
			Запрос.УстановитьПараметр("ДокументОснование", ТекущиеДанные.Заказ);
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Количество() = 1
				И Выборка.Следующий() Тогда
				
				ТекущиеДанные.СчетНаОплату = Выборка.СчетНаОплату;
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Заполним документ
		ПолучилиСуммуИзРасходнойНакладной = Ложь;
		// Если выбран заказ-наряд, то его можно сразу подставить в реквизит "Документ" ТЧ РасшифровкаПлатежаОтАгента.
		Если ЗначениеЗаполнено(ТекущиеДанные.Заказ)
			И ТекущиеДанные.Заказ.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд Тогда
			
			Если ТекущиеДанные.Документ <> ТекущиеДанные.Заказ Тогда
				ТекущиеДанные.Документ = ТекущиеДанные.Заказ;
				ПолучилиСуммуИзРасходнойНакладной = Истина;
				УстановитьСуммыСтрокиАгент(ТекущиеДанные, ТекущиеДанные.Заказ.СуммаДокумента);
			КонецЕсли;
			
		ИначеЕсли ТекущиеДанные.Заказ.Контрагент.ВестиРасчетыПоДокументам
			И ЗначениеЗаполнено(ТекущиеДанные.Заказ)
			И НЕ ЗначениеЗаполнено(ТекущиеДанные.Документ) Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	РасходнаяНакладнаяЗапасы.Ссылка КАК Ссылка,
				|	СУММА(РасходнаяНакладнаяЗапасы.Всего) КАК Всего
				|ИЗ
				|	Документ.РасходнаяНакладная.Запасы КАК РасходнаяНакладнаяЗапасы
				|ГДЕ
				|	РасходнаяНакладнаяЗапасы.Заказ = &Заказ
				|	И РасходнаяНакладнаяЗапасы.Ссылка.Проведен
				|
				|СГРУППИРОВАТЬ ПО
				|	РасходнаяНакладнаяЗапасы.Ссылка";
			
			Запрос.УстановитьПараметр("Заказ", ТекущиеДанные.Заказ);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Выборка = РезультатЗапроса.Выбрать();
			
			Если Выборка.Количество() = 1 Тогда
				
				Выборка.Следующий();
				
				ТекущиеДанные.Документ = Выборка.Ссылка;
				ПолучилиСуммуИзРасходнойНакладной = Истина;
				УстановитьСуммыСтрокиАгент(ТекущиеДанные, Выборка.Всего);
				
			КонецЕсли;
			
		ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.Заказ)
			И ЗначениеЗаполнено(ТекущиеДанные.Документ)
			И ТекущиеДанные.Документ.Заказ <> ТекущиеДанные.Заказ Тогда
			
			НашлиЗаказ = Ложь;
			Для Каждого ТекущаяСтрокаРН Из ТекущиеДанные.Документ.Запасы Цикл
				
				Если ТекущаяСтрокаРН.Заказ = ТекущиеДанные.Заказ Тогда
					НашлиЗаказ = Истина;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Если НЕ НашлиЗаказ Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = НСтр("ru = 'В расходной накладной не выбран заказ, который выбран в текущей строке. Выберите другой документ или другой заказ.'");
				Сообщение.Поле = "РасшифровкаПлатежаОтАгента["+Объект.РасшифровкаПлатежаОтАгента.Индекс(ТекущиеДанные)+"].Документ";
				Сообщение.Сообщить();
				
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ПолучилиСуммуИзРасходнойНакладной Тогда
			УстановитьСуммыСтрокиАгент(ТекущиеДанные, ТекущиеДанные.Заказ.СуммаДокумента);
		КонецЕсли;
		
		СвойстваЗаказа = РегистрыСведений.ЯндексДоставка.СвойстваЗаказа(ТекущиеДанные.Заказ);
		ТекущиеДанные.НомерЗаказаАгента = СвойстваЗаказа.НомерЗаказа;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСуммыСтрокиАгент(ТекущиеДанные, СуммаДокументаОтгрузкиЗаказа)
	
	Если ТекущиеДанные.СуммаПлатежа = 0
		И ТекущиеДанные.ПолученоОтКлиента = 0 Тогда
		ТекущиеДанные.ПолученоОтКлиента = СуммаДокументаОтгрузкиЗаказа;
		ТекущиеДанные.СуммаПлатежа = ТекущиеДанные.ПолученоОтКлиента - ТекущиеДанные.УдержаноАгентом;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаОтАгентаЗаказПриИзменении(Элемент)
	
	РасшифровкаПлатежаОтАгентаЗаказПриИзмененииНаСервере(Элементы.РасшифровкаПлатежаОтАгента.ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаСервере
Процедура РасшифровкаПлатежаОтАгентаДокументПриИзмененииНаСервере(ТекущиеДанныеИдентификатор)
	
	ТекущиеДанные = Объект.РасшифровкаПлатежаОтАгента.НайтиПоИдентификатору(ТекущиеДанныеИдентификатор);
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Документ) И ТекущиеДанные.Документ.Договор.ВалютаРасчетов <> Константы.НациональнаяВалюта.Получить() Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Не поддерживается выбор документов, которые оформлены по договорам в валюте или у.е.'");
			Сообщение.Поле = "РасшифровкаПлатежаОтАгента["+Объект.РасшифровкаПлатежаОтАгента.Индекс(ТекущиеДанные)+"].Документ";
			Сообщение.Сообщить();
			
			ТекущиеДанные.Документ = Неопределено;
			Возврат;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Документ)
			И ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			
			Если ТекущиеДанные.Заказ <> ТекущиеДанные.Документ Тогда
				ТекущиеДанные.Заказ = ТекущиеДанные.Документ;
				
				РасшифровкаПлатежаОтАгентаЗаказПриИзмененииНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
				Если ТекущиеДанные.Документ.ВалютаДокумента = Объект.ВалютаДенежныхСредств Тогда
					ТекущиеДанные.ПолученоОтКлиента = ТекущиеДанные.Документ.СуммаДокумента;
					ТекущиеДанные.СуммаПлатежа = ТекущиеДанные.ПолученоОтКлиента - ТекущиеДанные.УдержаноАгентом;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли НЕ ЗначениеЗаполнено(ТекущиеДанные.Заказ) И ЗначениеЗаполнено(ТекущиеДанные.Документ) И 
			ЗначениеЗаполнено(ТекущиеДанные.Документ.Заказ) Тогда
			
			ТекущиеДанные.Заказ = ТекущиеДанные.Документ.Заказ;
			
			РасшифровкаПлатежаОтАгентаЗаказПриИзмененииНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
			Если ТекущиеДанные.Документ.ВалютаДокумента = Объект.ВалютаДенежныхСредств Тогда
				ТекущиеДанные.ПолученоОтКлиента = ТекущиеДанные.Документ.СуммаДокумента;
				ТекущиеДанные.СуммаПлатежа = ТекущиеДанные.ПолученоОтКлиента - ТекущиеДанные.УдержаноАгентом;
			КонецЕсли;
			
		ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.Заказ)
			И ЗначениеЗаполнено(ТекущиеДанные.Документ)
			И ТекущиеДанные.Документ.Заказ <> ТекущиеДанные.Заказ Тогда
			
			НашлиЗаказ = Ложь;
			Для Каждого ТекущаяСтрокаРН Из ТекущиеДанные.Документ.Запасы Цикл
				
				Если ТекущаяСтрокаРН.Заказ = ТекущиеДанные.Заказ Тогда
					НашлиЗаказ = Истина;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Если НЕ НашлиЗаказ Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = НСтр("ru = 'В расходной накладной не выбран заказ, который выбран в текущей строке. Выберите другой документ или другой заказ.'");
				Сообщение.Поле = "РасшифровкаПлатежаОтАгента["+Объект.РасшифровкаПлатежаОтАгента.Индекс(ТекущиеДанные)+"].Документ";
				Сообщение.Сообщить();
				
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаОтАгентаДокументПриИзменении(Элемент)
	
	РасшифровкаПлатежаОтАгентаДокументПриИзмененииНаСервере(Элементы.РасшифровкаПлатежаОтАгента.ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаСервере
Процедура НастроитьРеквизитыЭлементовФормы()
	
	// ДоговорСАгентом
	МассивПараметровВыбора = Новый Массив;
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.ВалютаРасчетов", Константы.НациональнаяВалюта.Получить()));
	
	Элементы.ДоговорСАгентом.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
	
	// Расчеты с курьерской компанией или почтой
	// Документ и заказ от покупателя
	МассивПараметровВыбораЗаказ = Новый Массив;
	МассивПараметровВыбораЗаказ.Добавить(Новый ПараметрВыбора("Отбор.ВалютаДокумента", Константы.НациональнаяВалюта.Получить()));
	Если НЕ Константы.УчетПоКомпании.Получить() Тогда
		МассивПараметровВыбораЗаказ.Добавить(Новый ПараметрВыбора("Отбор.Организация", Объект.Организация));
	КонецЕсли; 
	
	МассивПараметровВыбораДокумент = Новый Массив;
	МассивПараметровВыбораДокумент.Добавить(Новый ПараметрВыбора("Отбор.ВалютаДокумента", Константы.НациональнаяВалюта.Получить()));
	МассивПараметровВыбораДокумент.Добавить(Новый ПараметрВыбора("ВыбиратьЗаказНаряд", Истина));
	
	Элементы.РасшифровкаПлатежаОтАгентаДокумент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбораДокумент);
	Элементы.РасшифровкаПлатежаОтАгентаЗаказ.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбораЗаказ);
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтКурьерскойКомпанииПочты
		И Объект.Ссылка.Пустая()
		И Объект.Корреспонденция.Пустая() Тогда
		Объект.Корреспонденция = ПланыСчетов.Управленческий.РасчетыСРазнымиКредиторами;
	КонецЕсли;
	
	// Если работы выключены, то заказ-наряд не используется и выбор типа не показываем.
	Если Не Константы.ФункциональнаяОпцияИспользоватьПодсистемуРаботы.Получить() Тогда
		Элементы.РасшифровкаПлатежаОтАгентаДокумент.ВыбиратьТип = Ложь;
		Элементы.РасшифровкаПлатежаОтАгентаДокумент.ОграничениеТипа = Новый ОписаниеТипов("ДокументСсылка.РасходнаяНакладная");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РасшифровкаПлатежаОтАгентаСчетНаОплатуПриИзмененииНаСервере(ТекущиеДанныеИдентификатор)
	
	ТекущиеДанные = Объект.РасшифровкаПлатежаОтАгента.НайтиПоИдентификатору(ТекущиеДанныеИдентификатор);
	Если ТекущиеДанные.СчетНаОплату.Контрагент.ВестиРасчетыПоЗаказам Тогда
		Если ТипЗнч(ТекущиеДанные.СчетНаОплату.ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			Если ЗначениеЗаполнено(ТекущиеДанные.СчетНаОплату) И 
				НЕ ЗначениеЗаполнено(ТекущиеДанные.Заказ) Тогда
				ТекущиеДанные.Заказ = ТекущиеДанные.СчетНаОплату.ДокументОснование;
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(ТекущиеДанные.СчетНаОплату) И 
				НЕ ЗначениеЗаполнено(ТекущиеДанные.Документ) Тогда
				ТекущиеДанные.Документ = ТекущиеДанные.СчетНаОплату.ДокументОснование;
				РасшифровкаПлатежаОтАгентаДокументПриИзмененииНаСервере(ТекущиеДанныеИдентификатор);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаОтАгентаСчетНаОплатуПриИзменении(Элемент)
	
	РасшифровкаПлатежаОтАгентаСчетНаОплатуПриИзмененииНаСервере(Элементы.РасшифровкаПлатежаОтАгента.ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаСервере
Процедура СоздатьОбновитьДокументыПереносаДолгаНаСервере(ДокОбъект)
	
	СтруктураДляЗаполнения = Новый Структура("Ссылка, ИндексСтрокиРасшифровки", ДокОбъект.Ссылка, 0);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого ТекущаяСтрока Из ДокОбъект.РасшифровкаПлатежаОтАгента Цикл
		
		Если ТекущаяСтрока.ПолученоОтКлиента = 0 Тогда
			СоздатьОбновить = Ложь;
			ПометитьНаУдаление = Истина;
		Иначе
			СоздатьОбновить = ДокОбъект.Проведен;
			ПометитьНаУдаление = ДокОбъект.ПометкаУдаления;
		КонецЕсли;
		
		Если СоздатьОбновить Тогда
			
			СтруктураДляЗаполнения.Вставить("ИндексСтрокиРасшифровки", ДокОбъект.РасшифровкаПлатежаОтАгента.Индекс(ТекущаяСтрока));
			Если НЕ ТекущаяСтрока.КорректировкаДолга.Пустая() Тогда
				ДокументПереносаДолга = ТекущаяСтрока.КорректировкаДолга.ПолучитьОбъект();
			Иначе
				ДокументПереносаДолга = Документы.Взаимозачет.СоздатьДокумент();
			КонецЕсли;
			ДокументПереносаДолга.Заполнить(СтруктураДляЗаполнения);
			Попытка
				Если ДокументПереносаДолга.ПометкаУдаления Тогда
					ДокументПереносаДолга.УстановитьПометкуУдаления(Ложь);
				КонецЕсли;
				ДокументПереносаДолга.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			Исключение
				ДокументПереносаДолга.Записать(РежимЗаписиДокумента.Запись, РежимПроведенияДокумента.Неоперативный);
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = НСтр("ru = 'Не получилось провести документ переноса долга.'") + Символы.ПС + ОписаниеОшибки();
				Сообщение.Поле = "Объект.РасшифровкаПлатежаОтАгента["+СтруктураДляЗаполнения.ИндексСтрокиРасшифровки+"].КорректировкаДолга";
				Сообщение.Сообщить();
			КонецПопытки;
			
			ТекущаяСтрока.КорректировкаДолга = ДокументПереносаДолга.Ссылка;
			
		ИначеЕсли ПометитьНаУдаление И НЕ ТекущаяСтрока.КорректировкаДолга.Пустая() И НЕ ТекущаяСтрока.КорректировкаДолга.ПометкаУдаления Тогда
			
			Попытка
				ТекущаяСтрока.КорректировкаДолга.
					ПолучитьОбъект().
					УстановитьПометкуУдаления(Истина);
			Исключение
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = НСтр("ru = 'Не получилось пометить на удаление документ переноса долга.'") + Символы.ПС + ОписаниеОшибки();
				Сообщение.Поле = "Объект.РасшифровкаПлатежаОтАгента["+СтруктураДляЗаполнения.ИндексСтрокиРасшифровки+"].КорректировкаДолга";
				Сообщение.Сообщить();
			КонецПопытки;
			
		ИначеЕсли НЕ ПометитьНаУдаление И НЕ СоздатьОбновить И НЕ ТекущаяСтрока.КорректировкаДолга.Пустая() Тогда
			
			Если ТекущаяСтрока.КорректировкаДолга.Проведен Тогда
				
				Попытка
					ТекущаяСтрока.КорректировкаДолга.
						ПолучитьОбъект().
						Записать(РежимЗаписиДокумента.ОтменаПроведения, РежимПроведенияДокумента.Неоперативный);
				Исключение
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = НСтр("ru = 'Не получилось отменить проведение документа переноса долга.'") + Символы.ПС + ОписаниеОшибки();
					Сообщение.Поле = "Объект.РасшифровкаПлатежаОтАгента["+СтруктураДляЗаполнения.ИндексСтрокиРасшифровки+"].КорректировкаДолга";
					Сообщение.Сообщить();
				КонецПопытки;
				
			ИначеЕсли ТекущаяСтрока.КорректировкаДолга.ПометкаУдаления Тогда
				
				Попытка
					ТекущаяСтрока.КорректировкаДолга.
						ПолучитьОбъект().
						УстановитьПометкуУдаления(Ложь);
				Исключение
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = НСтр("ru = 'Не получилось снять пометку удаления с документа переноса долга.'") + Символы.ПС + ОписаниеОшибки();
					Сообщение.Поле = "Объект.РасшифровкаПлатежаОтАгента["+СтруктураДляЗаполнения.ИндексСтрокиРасшифровки+"].КорректировкаДолга";
					Сообщение.Сообщить();
				КонецПопытки;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	// Если при интерактивном проведении, тогда перезапишем документ без перепроведения, иначе ссылки на корректировки не сохранятся.
	Если ДокОбъект.Проведен Тогда
		ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
		ЗначениеВРеквизитФормы(ДокОбъект, "Объект");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборРасшифровкиПлатежаОтАгента(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Укажите вначале контрагента.'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет)
	   И НЕ ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Укажите вначале банковский счет.'"));
		Возврат;
	КонецЕсли;
	
	АдресРасшифровкаПлатежаВХранилище = ПоместитьРасшифровкаПлатежаОтАгентаВХранилище();
	
	ПараметрыПодбора = Новый Структура(
		"АдресРасшифровкаПлатежаВХранилище,
		|Компания,
		|Дата,
		|Ссылка,
		|ВалютаДенежныхСредств,
		|СуммаДокумента,
		|Контрагент",
		АдресРасшифровкаПлатежаВХранилище,
		Компания,
		Объект.Дата,
		Объект.Ссылка,
		Объект.ВалютаДенежныхСредств,
		Объект.СуммаДокумента,
		Объект.Контрагент
	);
	
	ОткрытьФорму("ОбщаяФорма.ФормаПодбораРасшифровкиПлатежаОтАгента", ПараметрыПодбора, , , , ,
		Новый ОписаниеОповещения("ПодборОтАгентаЗавершение", ЭтотОбъект,
		Новый Структура("АдресРасшифровкаПлатежаВХранилище", АдресРасшифровкаПлатежаВХранилище)));
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборОтАгентаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	АдресРасшифровкаПлатежаВХранилище = ДополнительныеПараметры.АдресРасшифровкаПлатежаВХранилище;
	
	Если Результат = КодВозвратаДиалога.OK Тогда
		
		ПолучитьРасшифровкаПлатежаОтАгентаИзХранилища(АдресРасшифровкаПлатежаВХранилище);
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПолучитьРасшифровкаПлатежаОтАгентаИзХранилища(АдресРасшифровкаПлатежаВХранилище, Очищать = Истина)
	
	ТаблицаРасшифровкаПлатежа = ПолучитьИзВременногоХранилища(АдресРасшифровкаПлатежаВХранилище);
	Если Очищать Тогда
		Объект.РасшифровкаПлатежаОтАгента.Очистить();
	КонецЕсли;
	Для каждого СтрокаРасшифровкаПлатежа Из ТаблицаРасшифровкаПлатежа Цикл
		Строка = Объект.РасшифровкаПлатежаОтАгента.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, СтрокаРасшифровкаПлатежа);
		РассчитатьСуммуПлатежаОтАгентаНаСервере(Строка);
	КонецЦикла;
	
КонецПроцедуры // ПолучитьРасшифровкаПлатежаИзХранилища()

#Область Взаиморасчеты

&НаСервере
Процедура УстановитьЗаголовкиКолонокТабличныхЧастей(ВалютаСсылка)
	РасчетыРаботаСФормамиВызовСервера.УстановитьЗаголовкиКолонокТабличныхЧастей(ЭтотОбъект, ВалютаСсылка);
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформациюОбАвансеВРасшифровкеПлатежа()
	
	Для каждого ТекущаяСтрока Из Объект.РасшифровкаПлатежа Цикл
		ТекущаяСтрока.Аванс = ?(ТекущаяСтрока.ПризнакАванса , ТекущаяСтрока.СуммаПлатежа, 0);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПервуюСтрокуРасшифровкиНаСервере()
	
	Объект.РасшифровкаПлатежа.Добавить();
	Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
	Объект.РасшифровкаПлатежа[0].СпособЗачета = РасчетыРаботаСФормамиВызовСервера.ПолучитьСпособЗачетаДляДоговора(Объект.РасшифровкаПлатежа[0].Договор, Объект.Контрагент, Ложь);
	Объект.РасшифровкаПлатежа[0].ПризнакАванса = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПервуюСтрокуРасшифровкиНаКлиенте()
	
	Объект.РасшифровкаПлатежа.Добавить();
	Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
	Объект.РасшифровкаПлатежа[0].СпособЗачета = РасчетыРаботаСФормамиВызовСервера.ПолучитьСпособЗачетаДляДоговора(Объект.РасшифровкаПлатежа[0].Договор, Объект.Контрагент, Ложь);
	Объект.РасшифровкаПлатежа[0].ПризнакАванса = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРасшифровкуПлатежаПередЗаписью()
	
	Для Каждого ТекущаяСтрока Из Объект.РасшифровкаПлатежа Цикл
		Если ТекущаяСтрока.СЗачетом Тогда
			ТекущаяСтрока.СЗачетом = Ложь;
			ТекущаяСтрока.Документ = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруАдресовВХранилище()
	
	СтруктураДляВозврата = Новый Структура();
	
	СтруктураДляВозврата.Вставить("АдресРасшифровкаПлатежаВХранилище",
		ПоместитьВоВременноеХранилище(
			Объект.РасшифровкаПлатежа.Выгрузить(),
			УникальныйИдентификатор
		)
	);
	
	СтруктураДляВозврата.Вставить("АдресТаблицаДокументовДляИзмененияВХранилище",
		ПоместитьВоВременноеХранилище(
			ТаблицаДокументовДляИзменения.Выгрузить(),
			УникальныйИдентификатор
		)
	);
	
	Возврат СтруктураДляВозврата;
	
КонецФункции // ПоместитьРасшифровкаПлатежаВХранилище()

#Область РаботаСФормойРаспределенияСуммыПлатежа

&НаКлиенте
Процедура РаспределитьСуммуПлатежаПослеРаспределения(РезультатЗакрытия, ДопПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") И РезультатЗакрытия.Свойство("ДеревоРаспределенияСуммыПлатежа") Тогда
		
		СтруктураВидыИзменяемыхДокументов = РасчетыРаботаСФормамиКлиент.ЗаполнитьРасшифровкуПлатежаДеньгиПоДеревуРаспределения(ЭтотОбъект, РезультатЗакрытия, ДопПараметры);
		ЗаполнитьРасшифровкуПлатежаПоДеревуРаспределенияЗавершение(РезультатЗакрытия, ДопПараметры);
		
		РасчетыРаботаСФормамиКлиент.НастроитьЭлементыДляИзменяемыхДокументов(Истина, ЭтотОбъект, СтруктураВидыИзменяемыхДокументов);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРасшифровкуПлатежаПоДеревуРаспределенияЗавершение(РезультатЗакрытия, ДопПараметры)
	
	// Если больше строк не осталось, то добавить одну строку с автораспределением всей суммы на основной договор.
	Если Объект.РасшифровкаПлатежа.Количество() = 0 Тогда
		Объект.РасшифровкаПлатежа.Добавить();
		Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
		Объект.РасшифровкаПлатежа[0].Договор = РезультатЗакрытия.ДоговорПоУмолчанию;
		Объект.РасшифровкаПлатежа[0].СпособЗачета = РасчетыРаботаСФормамиВызовСервера.ПолучитьСпособЗачетаДляДоговора(Объект.РасшифровкаПлатежа[0].Договор, Объект.Контрагент, Ложь);
		Элементы.РасшифровкаПлатежа.ТекущаяСтрока = Объект.РасшифровкаПлатежа[0].ПолучитьИдентификатор();
		РасшифровкаПлатежаСуммаПлатежаПриИзменении(Неопределено);
	КонецЕсли;
	
	Если Элементы.СтраницыРасчетовСКонтрагентом.ТекущаяСтраница <> Элементы.СтраницаПросмотр Тогда
		ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиПросмотр;
		НастроитьЭлементыРаспределенияДолговНаСервере();
	КонецЕсли;
	ЗаполнитьТаблицуПросмотраНаКлиенте();
	
	Если Объект.СуммаДокумента = 0 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыРаспределенияДолговНаСервере()

	ФормыДокументовДеньги.НастроитьЭлементыРаспределенияДолговНаСервере(ЭтаФорма);

КонецПроцедуры


#КонецОбласти

&НаКлиенте
Процедура РаспределитьСуммуПлатежа(Команда) Экспорт
	
	Если Объект.Контрагент.Пустая() Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Укажите вначале контрагента.'"));
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая()
		ИЛИ Объект.Дата <> ДатаПриСозданииНаСервере Тогда
		
		Оповещение = Новый ОписаниеОповещения("ЗаписатьИОткрытьФормуРаспределенияПлатежа", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, 
		НСтр("ru = 'Заполнение калькуляции возможно только после записи данных.
					|Данные будут записаны.'"),
		РежимДиалогаВопрос.ОКОтмена);
		Возврат;
		
	КонецЕсли;
	
	ОткрытьФормуРаспределенияПлатежа();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИОткрытьФормуРаспределенияПлатежа(Ответ, ДополнительныеДанные) Экспорт
	
	Если Ответ=КодВозвратаДиалога.ОК Тогда
		Записать();
		Если Объект.Ссылка.Пустая() Или Модифицированность Тогда
			Возврат; // Запись не удалась, сообщения о причинах выводит платформа.
		КонецЕсли;
		ОткрытьФормуРаспределенияПлатежа();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРаспределенияПлатежа()
	
	СтруктураПараметров = Новый Структура();
	СтруктураАдресов = ПолучитьСтруктуруАдресовВХранилище();
	СтруктураПараметров.Вставить("СтруктураАдресов", СтруктураАдресов);
	СтруктураПараметров.Вставить("ВалютаДокумента", Объект.ВалютаДенежныхСредств);
	
	РасчетыРаботаСФормамиКлиент.ОткрытьФормуРаспределенияСуммыПлатежа(Истина, ЭтотОбъект, СтруктураПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТаблицуПросмотра(Команда)
	ЗаполнитьТаблицуПросмотраНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТаблицуПросмотраНаКлиенте(пПоДвижениям = Ложь)
	
	Если ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиПросмотр
		И (Объект.ВидОперации = ВидОперацииОтПокупателя Или Объект.ВидОперации = ВидОперацииОтНашейОрганизации) Тогда
		ЗаполнитьТаблицуПросмотраНаСервере(пПоДвижениям, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуПросмотраНаСервере(пПоДвижениям = Ложь, пПроверилиНаКлиенте = Ложь)
	
	РасчетыРаботаСФормамиВызовСервера.ЗаполнитьТаблицуПросмотраВДенежномДокументеНаСервере(ЭтотОбъект, пПоДвижениям, пПроверилиНаКлиенте);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДляПросмотраОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Если СтрНайти(Расшифровка, "ПереходитьВРежимРедактирования") <> 0 Тогда
		ВариантЗаполненияРасшифровки = СтруктураПараметровРасчетов.ВариантЗаполненияРасшифровкиВручную;
		ВариантЗаполненияРасшифровкиПриИзменении(Элементы.ВариантЗаполненияРасшифровки);
	КонецЕсли;
	
	РасчетыРаботаСФормамиКлиент.ТаблицаДляПросмотраОбработкаРасшифровки(ЭтотОбъект, Элемент, Расшифровка, СтандартнаяОбработка, СтруктураПараметровРасчетов);
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеНадписиСписокИзменяемыхДокументовНажатие(Элемент, СтандартнаяОбработка)
	
	СписокСсылок = РасчетыРаботаСФормамиКлиент.ДанныеНадписиСписокИзменяемыхДокументовНажатие(ЭтотОбъект, Элемент,
		СтандартнаяОбработка, 2);
	Если СписокСсылок <> Неопределено Тогда
		ОписаниеОповещенияВыбораИзМеню = Новый ОписаниеОповещения("ДанныеНадписиСписокИзменяемыхДокументовНажатиеЗавершение",
			РасчетыРаботаСФормамиКлиент);
		ПоказатьВыборИзМеню(ОписаниеОповещенияВыбораИзМеню, СписокСсылок, Элементы.НадписьДокументЗачетаПредоплаты);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСпособЗачетаПриИзменении(Элемент)
	
	РасчетыРаботаСФормамиКлиент.РасшифровкаПлатежаСпособЗачетаПриИзменении(Элементы.РасшифровкаПлатежа.ТекущиеДанные);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРеквизитыРасчетов()
	
	РасчетыРаботаСФормамиВызовСервера.ОбработатьОбщиеРеквизитыРасчетов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПриАктивизацииСтроки(Элемент)
	
	// Выведем информацию о документе, в котором будет зачтена предоплата.
	Если ТаблицаДокументовДляИзменения.Количество() > 0 Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбработатьАктивизациюСтрокиРасшифровкиПлатежа", 0.2, Истина);
	Иначе
		Элементы.ГруппаЗачетПредоплатыПоСтроке.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьАктивизациюСтрокиРасшифровкиПлатежа()
	
	РасчетыРаботаСФормамиКлиент.Подключаемый_ОбработатьАктивизациюСтрокиРасшифровкиПлатежа(ЭтотОбъект);
	
КонецПроцедуры // ОбработатьАктивизациюСтрокиСписка()

&НаКлиенте
Процедура ТаблицаДокументовДляИзмененияКартинка1Нажатие(Элемент, СтандартнаяОбработка)
	РасчетыРаботаСФормамиКлиент.ДанныеНадписиСписокИзменяемыхДокументовНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ПокупкаВалюты

&НаКлиенте
Процедура ПокупкаВалютыКурсПриИзменении(Элемент)
	
	РассчитатьСуммуУчета();
	ОбновитьОтображениеЭлементовПокупкаВалюты();
	
КонецПроцедуры // РассчитатьСуммуУчета()

&НаКлиенте
Процедура ПокупкаВалютыКратностьПриИзменении(Элемент)
	
	РассчитатьСуммуУчета();
	ОбновитьОтображениеЭлементовПокупкаВалюты();
	
КонецПроцедуры // РассчитатьСуммуУчета()

&НаКлиенте
Процедура ПокупкаВалютыСуммаУчетаПриИзменении(Элемент)
	
	Объект.Курс = ?(
		Объект.Курс = 0,
		1,
		Объект.Курс
	);
	
	Объект.Кратность = ?(
		Объект.Кратность = 0,
		1,
		Объект.Кратность
	);
	
	Объект.Курс = ?(
		Объект.СуммаДокумента = 0,
		1,
		Объект.СуммаУчета / Объект.СуммаДокумента * КурсВалютыУчета
	);
	
	ОбновитьОтображениеЭлементовПокупкаВалюты();
	
КонецПроцедуры // ПокупкаВалютыСуммаУчетаПриИзменении()


&НаКлиенте
Процедура РасчетКурсовойРазницыПокупкаВалютыПриИзменении(Элемент)
	
	ОбновитьОтображениеЭлементовПокупкаВалюты();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеЭлементовПокупкаВалюты()
	
	ФормыДокументовДеньги.ОбновитьОтображениеЭлементовПокупкаВалюты(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПокупкаВалютыКурсОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = 0 Тогда
		СтандартнаяОбработка = Ложь;
		ОбработчикОповещенияОЗакрытии = Новый ОписаниеОповещения("КурсПокупкаВалютыОбработкаВыбораЗавершение", ЭтотОбъект);
		ПоказатьВводДаты(ОбработчикОповещенияОЗакрытии, ДатаДокумента, "Укажите дату курса валюты", ЧастиДаты.Дата);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КурсПокупкаВалютыОбработкаВыбораЗавершение(ДатаКурса, ДополнительныеПараметры) Экспорт
	
	Если ДатаКурса <> Неопределено Тогда
		КурсНаДату = ПолучитьКурсВалютыНаСервере(Объект.ВалютаДенежныхСредств, ДатаКурса);
		Объект.Курс = КурсНаДату.Курс;
		Объект.Кратность = КурсНаДату.Кратность;
		ПокупкаВалютыКурсПриИзменении(Элементы.ПокупкаВалютыКурс);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКурсВалютыНаСервере(ВалютаДоговор, ДатаКурса) Экспорт
	
	Если ТипЗнч(ВалютаДоговор) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		Валюта = ВалютаДоговор.ВалютаРасчетов;
	Иначе
		Валюта = ВалютаДоговор;
	КонецЕсли;
	
	Возврат РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, ДатаКурса);
	
КонецФункции

#КонецОбласти

#Область УправлениеВнешнимВидомФормы

&НаСервере
Процедура УстановитьВидимостьПечатиЧека()
	
	Если ИспользоватьПодключаемоеОборудование
		И НЕ ТолькоПросмотр
		И (Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтПокупателя
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтПоставщика
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтНашейОрганизации) Тогда
		НапечататьЧекВидимость = Истина;
	Иначе
		НапечататьЧекВидимость = Ложь;
	КонецЕсли;

	Элементы.НапечататьЧек.Видимость = НапечататьЧекВидимость;
	Элементы.НомерЧекаККМ.Видимость = НапечататьЧекВидимость;
	Элементы.ГруппаОтправкаЧека.Видимость = НапечататьЧекВидимость;
	Элементы.СпециальныйНалоговыйРежим.Видимость = НапечататьЧекВидимость;
	Элементы.ПредварительныйПросмотрЧека.Видимость = НапечататьЧекВидимость;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьПечатиЧека()
	
	Если Объект.НомерЧекаККМ <> 0 Тогда
		Элементы.ГруппаОтправкаЧека.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущуюСтраницу()
	
	КоличествоСтрок = Объект.РасшифровкаПлатежа.Количество();
	
	Если КоличествоСтрок = 0 Тогда
		Объект.РасшифровкаПлатежа.Добавить();
		Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
		КоличествоСтрок = 1;
	КонецЕсли;
	
КонецПроцедуры // УстановитьТекущуюСтраницу()

&НаКлиенте
Процедура ОчиститьРеквизитыНеОтносящиесяКОперации()
	
	Если Объект.ВидОперации = ВидОперацииОтПокупателя
	ИЛИ Объект.ВидОперации = ВидОперацииОтПоставщика
	ИЛИ Объект.ВидОперации = ВидОперацииОтНашейОрганизации Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.ВидНалога = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.СуммаУчета = 0;
		Объект.Курс = 0;
		Объект.Кратность = 0;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.ЭтоВзаимозачет = Ложь;
		КонецЦикла;
		Объект.ЭквайринговыеОперации.Очистить();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтПодотчетника") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.ВидНалога = Неопределено;
		Объект.Контрагент = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.СуммаУчета = 0;
		Объект.Курс = 0;
		Объект.Кратность = 0;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Договор = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.СчетНаОплату = Неопределено;
			СтрокаТаблицы.ЭтоВзаимозачет = Ложь;
		КонецЦикла;
		Объект.ЭквайринговыеОперации.Очистить();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.Прочее") 
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ВзносНаличными")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПереводСДругогоСчета")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ЛичныеСредстваПредпринимателя") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.Контрагент = Неопределено;
		Объект.ВидНалога = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.СуммаУчета = 0;
		Объект.Курс = 0;
		Объект.Кратность = 0;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Договор = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.СчетНаОплату = Неопределено;
			СтрокаТаблицы.СтавкаНДС = Неопределено;
			СтрокаТаблицы.СуммаНДС = Неопределено;
			СтрокаТаблицы.ЭтоВзаимозачет = Ложь;
		КонецЦикла;
		Объект.ЭквайринговыеОперации.Очистить();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПокупкаВалюты") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.Контрагент = Неопределено;
		Объект.ВидНалога = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.Курс = ?(ЗначениеЗаполнено(Курс),
			Курс,
			1
		);
		Объект.Кратность = ?(ЗначениеЗаполнено(Кратность),
			Кратность, 1
		);
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Договор = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.СчетНаОплату = Неопределено;
			СтрокаТаблицы.СтавкаНДС = Неопределено;
			СтрокаТаблицы.СуммаНДС = Неопределено;
			СтрокаТаблицы.ЭтоВзаимозачет = Ложь;
		КонецЦикла;
		РассчитатьСуммуУчета();
		Объект.ЭквайринговыеОперации.Очистить();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.Налоги") Тогда
		Объект.Контрагент = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.Корреспонденция = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Если НЕ ФункциональнаяОпцияКассовыйМетодУчетаДоходовИРасходов Тогда
			Объект.НаправлениеДеятельности = Неопределено;
		КонецЕсли;
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Договор = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.СчетНаОплату = Неопределено;
			СтрокаТаблицы.СтавкаНДС = Неопределено;
			СтрокаТаблицы.СуммаНДС = Неопределено;
			СтрокаТаблицы.ЭтоВзаимозачет = Ложь;
		КонецЦикла;
		Объект.ЭквайринговыеОперации.Очистить();
	// Эквайринг
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКартам") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.ВидНалога = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.СуммаУчета = 0;
		Объект.Курс = 0;
		Объект.Кратность = 0;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.ЭквайринговыйТерминал = Неопределено;
		ДоговорЭквайринга = Неопределено;
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.СтатьяДДС = Неопределено;
			СтрокаТаблицы.ЭтоВзаимозачет = Ложь;
		КонецЦикла;
	// Конец Эквайринг
	// Кредиты
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКредитам") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.ВидНалога = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.СуммаУчета = 0;
		Объект.Курс = 0;
		Объект.Кратность = 0;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.ЭквайринговыйТерминал = Неопределено;
		ДоговорЭквайринга = Неопределено;
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.СтатьяДДС = Неопределено;
			СтрокаТаблицы.ЭтоВзаимозачет = Ложь;
		КонецЦикла;
	// Конец Кредиты
	// Прочие расчеты
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.РасчетыПоКредитам") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.Контрагент = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.НаправлениеДеятельности = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.СуммаУчета = 0;
		Объект.Курс = 0;
		Объект.Кратность = 0;
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Договор = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.СчетНаОплату = Неопределено;
			СтрокаТаблицы.СтавкаНДС = Неопределено;
			СтрокаТаблицы.СуммаНДС = Неопределено;
			СтрокаТаблицы.ЭтоВзаимозачет = Ложь;
		КонецЦикла;
		Объект.ЭквайринговыеОперации.Очистить();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ВозвратЗаймаСотрудником") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.Контрагент = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.НаправлениеДеятельности = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.СуммаУчета = 0;
		Объект.Курс = 0;
		Объект.Кратность = 0;
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Договор = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.СчетНаОплату = Неопределено;
			СтрокаТаблицы.СтавкаНДС = Неопределено;
			СтрокаТаблицы.СуммаНДС = Неопределено;
			СтрокаТаблицы.ЭтоВзаимозачет = Ложь;
		КонецЦикла;
		Объект.ЭквайринговыеОперации.Очистить();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПрочиеРасчеты") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.Контрагент = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.НаправлениеДеятельности = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.СуммаУчета = 0;
		Объект.Курс = 0;
		Объект.Кратность = 0;
		Объект.РасшифровкаПлатежа.Очистить();
		ДобавитьПервуюСтрокуРасшифровкиНаКлиенте();
		Объект.ЭквайринговыеОперации.Очистить();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтКурьерскойКомпанииПочты") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.Контрагент = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.НаправлениеДеятельности = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.СуммаУчета = 0;
		Объект.Курс = 0;
		Объект.Кратность = 0;
		Объект.РасшифровкаПлатежа.Очистить();
		ДобавитьПервуюСтрокуРасшифровкиНаКлиенте();
		Объект.ЭквайринговыеОперации.Очистить();
		Объект.РасшифровкаПлатежаОтАгента.Очистить();
	// Конец Прочие расчеты
	КонецЕсли;
	
	Корреспонденция = Объект.Корреспонденция;
	
КонецПроцедуры // ОчиститьРеквизитыНеОтносящиесяКОперации()

&НаСервере
Процедура УстановитьСвязиПараметровВыбораДоступныеТипы()
	
	// Прочие расчеты
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПрочиеРасчеты")
		Тогда
		УстановитьПараметрыВыбораПоУчетуПрочихРасчетовНаСервереДляЭлементаСчета();
	Иначе
		УстановитьПараметрыВыбораПоМетаданнымДляЭлементаСчета();
	КонецЕсли;
	// Конец Прочие расчеты
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтПокупателя")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтНашейОрганизации") Тогда
		
		Если ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ПриемИПередачаВРемонт") 
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "ВариантРемонта") <> Перечисления.ВариантыРемонта.НашаМастерскаяМногоэтапныйРемонт
			Тогда
			//ПриемИПередачаВРемонт это одновременно и заказ, и документ расхода
			Элементы.РасшифровкаПлатежаДокумент.ОграничениеТипа = Новый ОписаниеТипов("ДокументСсылка.ПриемИПередачаВРемонт");
			Элементы.РасшифровкаПлатежаЗаказ.ОграничениеТипа = Новый ОписаниеТипов("ДокументСсылка.ПриемИПередачаВРемонт");
		Иначе
		
			Массив = Новый Массив();
			Массив.Добавить(Тип("ДокументСсылка.ПередачаВА"));
			Массив.Добавить(Тип("ДокументСсылка.АктВыполненныхРабот"));
			Массив.Добавить(Тип("ДокументСсылка.ПриходнаяНакладная"));
			Массив.Добавить(Тип("ДокументСсылка.РасходнаяНакладная"));
			Массив.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
			Массив.Добавить(Тип("ДокументСсылка.ОтчетКомиссионера"));
			Массив.Добавить(Тип("ДокументСсылка.ОтчетОПереработке"));
			Массив.Добавить(Тип("ДокументСсылка.Взаимозачет"));
			Массив.Добавить(Тип("ДокументСсылка.ПриемИПередачаВРемонт"));
			Массив.Добавить(Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями"));
		
			ДопустимыеТипы = Новый ОписаниеТипов(Массив, , );
			Элементы.РасшифровкаПлатежаДокумент.ОграничениеТипа = ДопустимыеТипы;
			
			ДопустимыеТипы = Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя");
			Элементы.РасшифровкаПлатежаЗаказ.ОграничениеТипа = ДопустимыеТипы;
		
		КонецЕсли;
		
		ДопустимыеТипы = Новый ОписаниеТипов("ДокументСсылка.СчетНаОплату", , );
		Элементы.РасшифровкаПлатежаСчетНаОплату.ОграничениеТипа = ДопустимыеТипы;
		
		Элементы.РасшифровкаПлатежаДокумент.Подсказка = НСтр("ru = 'Оплачиваемый документ отгрузки товаров, работ и услуг контрагенту'");
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтПоставщика") Тогда
		
		Массив = Новый Массив();
		Массив.Добавить(Тип("ДокументСсылка.АвансовыйОтчет"));
		Массив.Добавить(Тип("ДокументСсылка.РасходИзКассы"));
		Массив.Добавить(Тип("ДокументСсылка.РасходСоСчета"));
		Массив.Добавить(Тип("ДокументСсылка.Взаимозачет"));
		Массив.Добавить(Тип("ДокументСсылка.ДополнительныеРасходы"));
		Массив.Добавить(Тип("ДокументСсылка.ОтчетКомитенту"));
		Массив.Добавить(Тип("ДокументСсылка.ОтчетПереработчика"));
		Массив.Добавить(Тип("ДокументСсылка.ПриходнаяНакладная"));
		Массив.Добавить(Тип("ДокументСсылка.РасходнаяНакладная"));
		
		ДопустимыеТипы = Новый ОписаниеТипов(Массив, , );
		Элементы.РасшифровкаПлатежаДокумент.ОграничениеТипа = ДопустимыеТипы;
		
		ДопустимыеТипы = Новый ОписаниеТипов("ДокументСсылка.ЗаказПоставщику", , );
		Элементы.РасшифровкаПлатежаЗаказ.ОграничениеТипа = ДопустимыеТипы;
		
		ДопустимыеТипы = Новый ОписаниеТипов("ДокументСсылка.СчетНаОплатуПоставщика", , );
		Элементы.РасшифровкаПлатежаСчетНаОплату.ОграничениеТипа = ДопустимыеТипы;
		
		Элементы.РасшифровкаПлатежаДокумент.Подсказка = НСтр("ru = 'Документ расчетов с контрагентом, по которому осуществляется возврат денежных средств'");
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКредитам") Тогда
		
		НовыйМассив = Новый Массив;
		
		ПараметрВыбора = Новый ПараметрВыбора("Отбор.ЭтоДоговорКредита", Истина);
		НовыйМассив.Добавить(ПараметрВыбора);
		
		ПараметрыВыбораДоговора = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.ДоговорЭквайринга.ПараметрыВыбора = ПараметрыВыбораДоговора;
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКартам") Тогда
		
		НовыйМассив = Новый Массив;
		
		ПараметрВыбора = Новый ПараметрВыбора("Отбор.ЭтоДоговорЭквайринга", Истина);
		НовыйМассив.Добавить(ПараметрВыбора);
		
		ПараметрВыбора = Новый ПараметрВыбора("Отбор.КонтрольВзаиморасчетовЭквайринг", Истина);
		НовыйМассив.Добавить(ПараметрВыбора);
		
		ПараметрыВыбораДоговора = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.ДоговорЭквайринга.ПараметрыВыбора = ПараметрыВыбораДоговора;
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтПодотчетника") Тогда
		
		Массив = Новый Массив();
		Массив.Добавить(Тип("ДокументСсылка.РасходИзКассы"));
		Массив.Добавить(Тип("ДокументСсылка.РасходСоСчета"));
		
		ДопустимыеТипы = Новый ОписаниеТипов(Массив, , );
		Элементы.РасчетыСПодотчетникомРасшифровкаПлатежаДокумент.ОграничениеТипа = ДопустимыеТипы;
		
		Связи = Новый Массив;
		Связи.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация", РежимИзмененияСвязанногоЗначения.Очищать));
		Связи.Добавить(Новый СвязьПараметраВыбора("Отбор.Подотчетник", "Объект.Подотчетник", РежимИзмененияСвязанногоЗначения.Очищать));
		Элементы.РасчетыСПодотчетникомРасшифровкаПлатежаДокумент.СвязиПараметровВыбора = Новый ФиксированныйМассив(Связи);
		
	КонецЕсли;
	
	// Интеркампани
	Связи = Новый Массив;
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеНаСчет.ОтНашейОрганизации") Тогда
		Связи.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.Организация", РежимИзмененияСвязанногоЗначения.Очищать));
		Связи.Добавить(Новый СвязьПараметраВыбора("ВидОперации", "Объект.ВидОперации", РежимИзмененияСвязанногоЗначения.НеИзменять));
	Иначе
		Связи.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.Контрагент", РежимИзмененияСвязанногоЗначения.Очищать));
	КонецЕсли;
	Элементы.РасшифровкаПлатежаДоговор.СвязиПараметровВыбора = Новый ФиксированныйМассив(Связи);
	// Конец Интеркампани
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКурсаВалюты(ВалютаДенежныхСредств)
	
	Элементы.Курс.Видимость = (ВалютаДенежныхСредств <> Константы.НациональнаяВалюта.Получить());
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьИДоступность(ИзмененВидОперации = Ложь)
	
	// Документ основание.
	НовыйМассив = Новый Массив();
	НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
	ПараметрыВыбораДокументаОснования = НовыеПараметры;
	// Конец Документ основание.
	
	Если ТолькоПросмотр И НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Элементы.ДокументОснованиеНадпись.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры // УстановитьВидимостьИДоступность()

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	РасчетыРаботаСФормамиВызовСервера.УстановитьУсловноеОформлениеРасчетов(ЭтотОбъект, Перечисления.ВидыОперацийПоступлениеНаСчет.ОтПоставщика);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.ЭквайринговыеОперации.СуммаРасчетовКомиссииВозврата", 0, ВидСравненияКомпоновкиДанных.Меньше);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ЭквайринговыеОперацииСуммаРасчетовКомиссииВозврата");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", ЦветаСтиля.ЦветСуммыПоступления);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.ЭквайринговыеОперации.НалогообложениеНДС", "", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ЭквайринговыеОперацииНалоговыйУчет");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", ЦветаСтиля.ЦветОтрицательногоЧисла);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеПоДаннымКонтрагента()
	
	РасчетыРаботаСФормамиВызовСервера.УстановитьУсловноеОформлениеРасчетовПоДаннымКонтрагента(ЭтотОбъект, Перечисления.ВидыОперацийПоступлениеНаСчет.ОтПоставщика);
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеОбъектов

&НаКлиенте
Процедура СохранитьДокументКакШаблон(Параметр) Экспорт
	
	ЗаполнениеОбъектовУНФКлиент.СохранитьДокументКакШаблон(Объект, ОтображаемыеРеквизиты(), Параметр);
	
КонецПроцедуры

&НаСервере
Функция ОтображаемыеРеквизиты()
	
	Возврат ЗаполнениеОбъектовУНФ.ОтображаемыеРеквизиты(ЭтаФорма);
	
КонецФункции

#КонецОбласти

#Область Эквайринг

&НаСервере
Функция ПоместитьЭквайринговыеОперацииВХранилище() 
	
	Возврат ПоместитьВоВременноеХранилище(
		Объект.ЭквайринговыеОперации.Выгрузить(),
		УникальныйИдентификатор
	);
	
КонецФункции // ПоместитьЭквайринговыеОперацииВХранилище()

&НаСервере
Процедура ПолучитьЭквайринговыеОперацииИзХранилища(АдресЭквайринговыеОперацииВХранилище)
	
	ТаблицаЭквайринговыеОперации = ПолучитьИзВременногоХранилища(АдресЭквайринговыеОперацииВХранилище);
	Объект.ЭквайринговыеОперации.Очистить();
	Для каждого СтрокаЭквайринговыеОперации Из ТаблицаЭквайринговыеОперации Цикл
		Строка = Объект.ЭквайринговыеОперации.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, СтрокаЭквайринговыеОперации);
	КонецЦикла;
	
КонецПроцедуры // ПолучитьРасшифровкаПлатежаИзХранилища()

&НаСервереБезКонтекста
Функция ПолучитьПараметрыЭквайринговогоТерминалаПриИзменении(ЭквайринговыйТерминал)
	
	ПараметрыЭквайринговогоТерминала = Новый Структура;
	
	ПараметрыЭквайринговогоТерминала.Вставить("Договор", ЭквайринговыйТерминал.Договор);
	Если Не ЭквайринговыйТерминал.СчетУчета.Пустая() Тогда
		ПараметрыЭквайринговогоТерминала.Вставить("СчетУчета", ЭквайринговыйТерминал.СчетУчета);
	Иначе
		ПараметрыЭквайринговогоТерминала.Вставить("СчетУчета", ПредопределенноеЗначение("ПланСчетов.Управленческий.ПереводыВПути"));
	КонецЕсли;
	Если Не ЭквайринговыйТерминал.СчетЗатрат.Пустая() Тогда
		ПараметрыЭквайринговогоТерминала.Вставить("СчетЗатрат", ЭквайринговыйТерминал.СчетЗатрат);
		ПараметрыЭквайринговогоТерминала.Вставить("ТипСчетаЗатрат", ЭквайринговыйТерминал.СчетЗатрат.ТипСчета);
	Иначе
		ПараметрыЭквайринговогоТерминала.Вставить("СчетЗатрат", ПредопределенноеЗначение("ПланСчетов.Управленческий.ПрочиеРасходы"));
		ПараметрыЭквайринговогоТерминала.Вставить("ТипСчетаЗатрат", ПараметрыЭквайринговогоТерминала.СчетЗатрат.ТипСчета);
	КонецЕсли;
	Если Не ЭквайринговыйТерминал.НаправлениеДеятельности.Пустая() Тогда
		ПараметрыЭквайринговогоТерминала.Вставить("НаправлениеДеятельности", ЭквайринговыйТерминал.НаправлениеДеятельности);
	Иначе
		ПараметрыЭквайринговогоТерминала.Вставить("НаправлениеДеятельности", ПредопределенноеЗначение("Справочник.НаправленияДеятельности.Прочее"));
	КонецЕсли;
	Если Не ЭквайринговыйТерминал.Подразделение.Пустая() Тогда
		ПараметрыЭквайринговогоТерминала.Вставить("Подразделение", ЭквайринговыйТерминал.Подразделение);
	Иначе
		Пользователь = Пользователи.ТекущийПользователь();
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновноеПодразделение");
		ПараметрыЭквайринговогоТерминала.Вставить("Подразделение", ?(ЗначениеЗаполнено(ЗначениеНастройки), ЗначениеНастройки, Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение));
	КонецЕсли;
	ПараметрыЭквайринговогоТерминала.Вставить("БанковскийСчетЭквайринг", ЭквайринговыйТерминал.БанковскийСчетЭквайринг);
	
	Возврат ПараметрыЭквайринговогоТерминала;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьЭквайринговыеОперации(Команда)
	
	ВыполнитьВозврат = ПроверитьРеквизитыПередЗаполнениемТабличнойЧасти();
	Если ВыполнитьВозврат Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ЗаполнитьЭквайринговыеОперацииЗавершение", ЭтотОбъект);
	Если Объект.ЭквайринговыеОперации.Количество() > 0 Тогда
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Эквайринговые операции будут полностью перезаполнены. Продолжить?'"), РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	Иначе
		ЗаполнитьЭквайринговыеОперацииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьРеквизитыПередЗаполнениемТабличнойЧасти()
	
	ВыполнитьВозврат = Ложь;
	Если Объект.Контрагент.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Укажите вначале контрагента.'");
		Сообщение.Поле = "Контрагент";
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Сообщить();
		
		ВыполнитьВозврат = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет)
	   И НЕ ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Укажите вначале банковский счет.'");
		Сообщение.Поле = "БанковскийСчет";
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Сообщить();
		
		ВыполнитьВозврат = Истина;
	КонецЕсли;

	Если Объект.ПоложениеЭквайринговогоТерминала = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке") Тогда
		Если Объект.ЭквайринговыйТерминал.Пустая() Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Укажите вначале эквайринговый терминал.'");
			Сообщение.Поле = "ЭквайринговыйТерминал";
			Сообщение.ПутьКДанным = "Объект";
			Сообщение.Сообщить();
			
			ВыполнитьВозврат = Истина;
		КонецЕсли;
	КонецЕсли; 
	
	Если ДоговорЭквайринга.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Укажите вначале договор эквайринга.'");
		Сообщение.Поле = "ДоговорЭквайринга";
		Сообщение.ПутьКДанным = "ДоговорЭквайринга";
		Сообщение.Сообщить();
		
		ВыполнитьВозврат = Истина;
	КонецЕсли;
	
	Возврат ВыполнитьВозврат;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьЭквайринговыеОперацииЗавершение(Ответ, Параметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЭквайринговыеОперацииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭквайринговыеОперацииНаСервере(ДатаНачала = '00010101', ДатаОкончания = '00010101')
	
	ЭквайринговыеОперацииСервер.ЗаполнитьЭквайринговыеОперации(Объект, ДатаНачала, ДатаОкончания, ДоговорЭквайринга);
	
	Модифицированность = Истина;
	
	ФормыДокументовДеньги.ОбновитьОтображениеНалоговогоУчетаВТабличнойЧасти(ЭтаФорма);
	РаспределитьСуммуКомиссии();
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайрингПодборЗавершение(Результат1, ДополнительныеПараметры) Экспорт
	
	АдресЭквайринговыеОперацииВХранилище = ДополнительныеПараметры.АдресЭквайринговыеОперацииВХранилище;
	
	Результат = Результат1;
	Если Результат = КодВозвратаДиалога.OK Тогда
		
		ПолучитьЭквайринговыеОперацииИзХранилища(АдресЭквайринговыеОперацииВХранилище);
		
		Для Каждого ТекущаяСтрока Из Объект.ЭквайринговыеОперации Цикл
			ТекущаяСтрока.СуммаПлатежаИтогоПоСтроке = ТекущаяСтрока.СуммаРасчетов-ТекущаяСтрока.СуммаРасчетовВозврата
									-ТекущаяСтрока.СуммаРасчетовКомиссии-ТекущаяСтрока.СуммаРасчетовКомиссииВозврата;
			ТекущаяСтрока.СуммаКомиссииИтогоПоСтроке = ТекущаяСтрока.СуммаРасчетовКомиссии+ТекущаяСтрока.СуммаРасчетовКомиссииВозврата;
			
			ТекущаяСтрока.НалоговыйУчет = ПолучитьПредставлениеНалоговогоУчетаСтроки(ТекущаяСтрока, ВидимостьУчитыватьВНУ, ВидимостьПатент);
			
		КонецЦикла;
		
		УстановитьТекущуюСтраницу();
		
		Если Объект.ЭквайринговыеОперации.Количество() = 1 Тогда
			Объект.СуммаДокумента = Объект.ЭквайринговыеОперации.Итог("СуммаПлатежаИтогоПоСтроке");
			Объект.СуммаКомиссииДокумента = Объект.ЭквайринговыеОперации.Итог("СуммаКомиссииИтогоПоСтроке");
		КонецЕсли;
		
	РаспределитьСуммуКомиссии();
	
	КонецЕсли;

КонецПроцедуры // ЭквайрингПодборЗавершение()

&НаКлиенте
Процедура ЭквайринговыйТерминалПриИзменении(Элемент)
	
	Если ЭквайринговыйТерминал <> Объект.ЭквайринговыйТерминал Тогда
		ОбработатьИзменениеТерминалаВШапке(Элемент);
		ПараметрыЭквайринговогоТерминала = ПолучитьПараметрыЭквайринговогоТерминалаПриИзменении(Объект.ЭквайринговыйТерминал);
		ТипСчетаЗатратШапка = ПараметрыЭквайринговогоТерминала.ТипСчетаЗатрат;
		ЭквайринговыйТерминалПриИзмененииНаСервере();
		Объект.НаправлениеДеятельностиЗатраты = ПараметрыЭквайринговогоТерминала.НаправлениеДеятельности;
		Объект.ПодразделениеЗатраты = ПараметрыЭквайринговогоТерминала.Подразделение;
		Если Объект.БанковскийСчет <> ПараметрыЭквайринговогоТерминала.БанковскийСчетЭквайринг И ЗначениеЗаполнено(ПараметрыЭквайринговогоТерминала.БанковскийСчетЭквайринг) Тогда
			Объект.БанковскийСчет = ПараметрыЭквайринговогоТерминала.БанковскийСчетЭквайринг;
			БанковскийСчетПриИзменении(Элемент);
		КонецЕсли;
		
		ЭквайринговыйТерминал = Объект.ЭквайринговыйТерминал;
		
		МассивДляУдаления = Новый Массив;
		Для каждого СтрокаОпераций Из Объект.ЭквайринговыеОперации Цикл
			Если ЗначениеЗаполнено(СтрокаОпераций.ЭквайринговыйТерминал) И СтрокаОпераций.ЭквайринговыйТерминал <> ЭквайринговыйТерминал Тогда
				 МассивДляУдаления.Добавить(СтрокаОпераций);
			Иначе
				СтрокаОпераций.ЭквайринговыйТерминал = ЭквайринговыйТерминал;
			КонецЕсли; 
		КонецЦикла; 
		
		Для каждого СтрокаДляУдаления Из МассивДляУдаления Цикл
			Объект.ЭквайринговыеОперации.Удалить(СтрокаДляУдаления);
		КонецЦикла; 
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЭквайринговыйТерминалПриИзмененииНаСервере()
	
	ДоговорЭквайринга = Объект.ЭквайринговыйТерминал.Договор;
	
	Если НЕ ДоговорЭквайринга.КонтрольВзаиморасчетовЭквайринг ИЛИ ДоговорЭквайринга.РасчетКомиссииВОтчетеЭквайера Тогда
		Объект.ПоложениеЭквайринговогоТерминала = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
	КонецЕсли; 
	
	НастроитьВидимостьЭлементовВЗависимостиОтТипаСчетаЗатрат();
	ФормыДокументовДеньги.УстановитьВидимостьЭлементовВЗависимостиОтВидаОперации(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайринговыйТерминалНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЕстьСообщения = Ложь;
	Если Объект.Контрагент.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Сначала выберите банк-эквайрер.'");
		Сообщение.Поле = "Объект.Контрагент";
		Сообщение.Сообщить();
		
		ЕстьСообщения = Истина;
	КонецЕсли;
	
	Если Объект.БанковскийСчет.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Сначала выберите банковский счет.'");
		Сообщение.Поле = "Объект.БанковскийСчет";
		Сообщение.Сообщить();
		
		ЕстьСообщения = Истина;
	КонецЕсли;
	
	Если ЕстьСообщения Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьВидимостьЭлементовВЗависимостиОтТипаСчетаЗатрат(УстанавливатьЗначения = Истина)
	
	Если ТипСчетаЗатратШапка = Перечисления.ТипыСчетов.Расходы Тогда
		ПараметрыЭквайринговогоТерминала = ПолучитьПараметрыЭквайринговогоТерминалаПриИзменении(Объект.ЭквайринговыйТерминал);
		
		Если УстанавливатьЗначения Тогда
			Объект.НаправлениеДеятельностиЗатраты = ПараметрыЭквайринговогоТерминала.НаправлениеДеятельности;
			Объект.ПодразделениеЗатраты = ПараметрыЭквайринговогоТерминала.Подразделение;
		КонецЕсли;
		
		Элементы.ПодразделениеЗатраты.Видимость = Истина;
		Элементы.НаправлениеДеятельностиЗатраты.Видимость = Истина;
	Иначе
		
		Если УстанавливатьЗначения Тогда
			Объект.НаправлениеДеятельностиЗатраты = Справочники.НаправленияДеятельности.Прочее;
			Объект.ПодразделениеЗатраты = Неопределено;
		КонецЕсли;
		
		Элементы.ПодразделениеЗатраты.Видимость = Ложь;
		Элементы.НаправлениеДеятельностиЗатраты.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайринговыеОперацииСуммаРасчетовПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЭквайринговыеОперации.ТекущиеДанные;
	
	Если ТекущаяСтрока.СуммаРасчетов <> 0 Тогда
		ТекущаяСтрока.СуммаРасчетовВозврата = 0;
	КонецЕсли;
	
	ПересчитатьСуммуПлатежаИКомиссииЭквайринга(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСуммуПлатежаИКомиссииЭквайринга(ТекущаяСтрока)
	
	Если ТекущаяСтрока <> Неопределено Тогда
		ТекущаяСтрока.СуммаПлатежаИтогоПоСтроке = ТекущаяСтрока.СуммаРасчетов-ТекущаяСтрока.СуммаРасчетовВозврата
								-ТекущаяСтрока.СуммаРасчетовКомиссии-ТекущаяСтрока.СуммаРасчетовКомиссииВозврата;
		ТекущаяСтрока.СуммаКомиссииИтогоПоСтроке = ТекущаяСтрока.СуммаРасчетовКомиссии + ТекущаяСтрока.СуммаРасчетовКомиссииВозврата;
		
		Если Объект.ЭквайринговыеОперации.Количество() = 1 Тогда
			Объект.СуммаДокумента = Объект.ЭквайринговыеОперации[0].СуммаПлатежаИтогоПоСтроке;
			Объект.СуммаКомиссииДокумента = Объект.ЭквайринговыеОперации[0].СуммаКомиссииИтогоПоСтроке;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайринговыеОперацииСуммаРасчетовВозвратаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЭквайринговыеОперации.ТекущиеДанные;
	
	Если ТекущаяСтрока.СуммаРасчетовВозврата <> 0 Тогда
		ТекущаяСтрока.СуммаРасчетов = 0;
	КонецЕсли;
	
	ПересчитатьСуммуПлатежаИКомиссииЭквайринга(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайринговыеОперацииСуммаРасчетовКомиссииПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЭквайринговыеОперации.ТекущиеДанные;
	ПересчитатьСуммуПлатежаИКомиссииЭквайринга(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайринговыеОперацииСуммаРасчетовКомиссииВозвратаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЭквайринговыеОперации.ТекущиеДанные;
	ПересчитатьСуммуПлатежаИКомиссииЭквайринга(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайринговыеОперацииДокументПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЭквайринговыеОперации.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда
		СтруктураПараметров = ПолучитьПараметрыЭквайринговойОперации(ТекущаяСтрока.Документ, Объект.ЭквайринговыйТерминал);
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтруктураПараметров);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПараметрыЭквайринговойОперации(СсылкаНаДокумент, ЭквайринговыйТерминал)
	
	СтруктураПараметров = Новый Структура("ДатаПлатежа, НомерПлатежнойКарты, ВидПлатежнойКарты");
	
	Если ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.ОперацияПоПлатежнымКартам") Тогда
		СтруктураПараметров.ДатаПлатежа = СсылкаНаДокумент.Дата;
		СтруктураПараметров.НомерПлатежнойКарты = СсылкаНаДокумент.НомерПлатежнойКарты;
		СтруктураПараметров.ВидПлатежнойКарты = СсылкаНаДокумент.ВидПлатежнойКарты;
		Если СсылкаНаДокумент.РасшифровкаПлатежа.Количество() = 1 Тогда
			Если ЗначениеЗаполнено(СсылкаНаДокумент.РасшифровкаПлатежа[0].Заказ) Тогда
				СтруктураПараметров.Вставить("Заказ", СсылкаНаДокумент.РасшифровкаПлатежа[0].Заказ);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		СтруктураПараметров.ДатаПлатежа = СсылкаНаДокумент.Дата;
		Если СсылкаНаДокумент.БезналичнаяОплата.Количество() > 0 Тогда
			ОплатаКартами = СсылкаНаДокумент.БезналичнаяОплата.Выгрузить(Новый Структура("ВидОплаты", Перечисления.ВидыБезналичныхОплат.БанковскаяКарта));
			Если ОплатаКартами.Количество() > 0 Тогда
				ПерваяСтрокаОплатыКартой = ОплатаКартами[0];
				Если ПерваяСтрокаОплатыКартой.ЭквайринговыйТерминал = ЭквайринговыйТерминал Тогда
					СтруктураПараметров.НомерПлатежнойКарты = ПерваяСтрокаОплатыКартой.НомерПлатежнойКарты;
					СтруктураПараметров.ВидПлатежнойКарты = ПерваяСтрокаОплатыКартой.ВидПлатежнойКарты;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтруктураПараметров;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьЭквайринговыеОперацииСВыборомПериода(Команда)
	
	ВыполнитьВозврат = ПроверитьРеквизитыПередЗаполнениемТабличнойЧасти();
	Если ВыполнитьВозврат Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ЗаполнитьЭквайринговыеОперацииСВыборомПериодаЗавершение", ЭтотОбъект);
	Если Объект.ЭквайринговыеОперации.Количество() > 0 Тогда
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Эквайринговые операции будут полностью перезаполнены. Продолжить?'"), РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	Иначе
		ОткрытьФормуВыбораПериодаРасчета();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЭквайринговыеОперацииСВыборомПериодаЗавершение(Ответ, Параметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуВыбораПериодаРасчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораПериодаРасчета()
	
	ПараметрыЗаполнения = Новый Структура("Контрагент, БанковскийСчет, ЭквайринговыйТерминал, Организация, Ссылка, ВалютаДенежныхСредств, ПоложениеЭквайринговогоТерминала, РасшифровкаПлатежа", 
		Объект.Контрагент, 
		Объект.БанковскийСчет, 
		Объект.ЭквайринговыйТерминал,
		Объект.Организация,
		Объект.Ссылка,
		Объект.ВалютаДенежныхСредств,
		Объект.ПоложениеЭквайринговогоТерминала,
		ПоместитьРасшифровкаПлатежаВХранилище());
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОткрытьФормуВыбораПериодаРасчетаЗавершение", ЭтотОбъект);
	ОткрытьФорму("Документ.ПоступлениеНаСчет.Форма.ФормаВыбораПериода", ПараметрыЗаполнения, ЭтотОбъект, УникальныйИдентификатор,,, ОписаниеОповещенияОЗавершении, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораПериодаРасчетаЗавершение(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ЗаполнитьЭквайринговыеОперацииНаСервере(Результат.ПериодРасчетовДатаНачала, Результат.ПериодРасчетовДатаОкончания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборЭквайринговыхОпераций(Команда)
	
	// Эквайринг
	ВыполнитьВозврат = ПроверитьРеквизитыПередЗаполнениемТабличнойЧасти();
	Если ВыполнитьВозврат Тогда
		Возврат;
	КонецЕсли;
	
	АдресЭквайринговыеОперацииВХранилище = ПоместитьЭквайринговыеОперацииВХранилище();
	
	ПараметрыПодбора = Новый Структура(
		"АдресЭквайринговыеОперацииВХранилище,
		|Компания,
		|Дата,
		|Контрагент,
		|Ссылка,
		|ВидОперации,
		|ВалютаДенежныхСредств,
		|СуммаДокумента,
		|БанковскийСчет,
		|ЭквайринговыйТерминал",
		АдресЭквайринговыеОперацииВХранилище,
		Компания,
		Объект.Дата,
		Объект.Контрагент,
		Объект.Ссылка,
		Объект.ВидОперации,
		Объект.ВалютаДенежныхСредств,
		Объект.СуммаДокумента,
		Объект.БанковскийСчет,
		Объект.ЭквайринговыйТерминал
	);
	
	//Если терминал указывается в табличной части, отбор по нему в форме подбора не нужен
	Если Объект.ПоложениеЭквайринговогоТерминала = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		ПараметрыПодбора.Удалить("ЭквайринговыйТерминал");
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.ФормаПодбораДолговПоЭквайрингу", ПараметрыПодбора, , , , ,
		Новый ОписаниеОповещения("ЭквайрингПодборЗавершение", ЭтотОбъект,
		Новый Структура("АдресЭквайринговыеОперацииВХранилище", АдресЭквайринговыеОперацииВХранилище)));
	
КонецПроцедуры

// Обработка выбора табличной части "ЭквайринговыеОперации"
//
&НаКлиенте
Процедура ЭквайринговыеОперацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ЭквайринговыеОперацииНалоговыйУчет" Тогда
		
		ОткрытьФормуРедактированияНастроекНалоговогоУчета(ВыбраннаяСтрока);
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

// Открытие формы настроек налогового учета для строки табличной части "ЭквайринговыеОперации"
//
&НаКлиенте
Процедура ОткрытьФормуРедактированияНастроекНалоговогоУчета(ВыбранноеЗначение)
	
	ВыбраннаяСтрока =  Объект.ЭквайринговыеОперации.НайтиПоИдентификатору(ВыбранноеЗначение);
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("ВидимостьУчитыватьВНУ",		 ВидимостьУчитыватьВНУ);
	ПараметрыПодбора.Вставить("ВидимостьНалогообложениеНДС", Истина);
	ПараметрыПодбора.Вставить("ВидимостьПатент",			 ВидимостьПатент);
	ПараметрыПодбора.Вставить("УчитыватьВНУ", 				 ВыбраннаяСтрока.УчитыватьВНУ);
	ПараметрыПодбора.Вставить("НалогообложениеНДС", 		 ВыбраннаяСтрока.НалогообложениеНДС);
	ПараметрыПодбора.Вставить("Патент", 					 ВыбраннаяСтрока.Патент);
	ПараметрыПодбора.Вставить("ИдентификаторСтроки",		 ВыбранноеЗначение);
	
	Обработчик = Новый ОписаниеОповещения("ПриЗавершенииВыбораНастроекНалогообложения", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ФормаНастройкиНалоговогоУчета", ПараметрыПодбора, ЭтаФорма,,,, Обработчик, ПредопределенноеЗначение("РежимОткрытияОкнаФормы.БлокироватьОкноВладельца"));
	
КонецПроцедуры

// Завершение подбора настроек налогового учета для строки табличной части "ЭквайринговыеОперации"
//
&НаКлиенте
Процедура ПриЗавершенииВыбораНастроекНалогообложения(СтруктураРезультата, ДополнительныеПараметры) Экспорт
	
	Если СтруктураРезультата = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если СтруктураРезультата.КодВозвратаДиалога = КодВозвратаДиалога.OK И СтруктураРезультата.БылиВнесеныИзменения Тогда
		
		Модифицированность = Истина;
		
		ВыбраннаяСтрока = Объект.ЭквайринговыеОперации.НайтиПоИдентификатору(СтруктураРезультата.ИдентификаторСтроки);
		
		ВыбраннаяСтрока.УчитыватьВНУ = СтруктураРезультата.УчитыватьВНУ;
		ВыбраннаяСтрока.НалогообложениеНДС = СтруктураРезультата.НалогообложениеНДС;
		ВыбраннаяСтрока.Патент = СтруктураРезультата.Патент; 
		
		ВыбраннаяСтрока.НалоговыйУчет = ПолучитьПредставлениеНалоговогоУчетаСтроки(ВыбраннаяСтрока, ВидимостьУчитыватьВНУ, ВидимостьПатент);
		
	КонецЕсли;
	
КонецПроцедуры

// Обработка события "ПриИзменении" поля "Терминал" табличной части "ЭквайринговыеОперации"
//
&НаКлиенте
Процедура ЭквайринговыеОперацииЭквайринговыйТерминалПриИзменении(Элемент)
	
	ВыбраннаяСтрока = Элементы.ЭквайринговыеОперации.ТекущиеДанные;
	
	ОбработатьИзменениеТерминалаВТЧ(ВыбраннаяСтрока);
	
	НалоговыйУчетТерминала = ПолучитьНастройкиНалоговогоУчетаТерминала(ВыбраннаяСтрока.ЭквайринговыйТерминал);
	
	Если НалоговыйУчетТерминала.СобственныеНастройкиНалоговогоУчета Тогда
		
		СтрокаЭквайринга = Объект.ЭквайринговыеОперации.НайтиПоИдентификатору(Элементы.ЭквайринговыеОперации.ТекущаяСтрока); 
		ЗаполнитьЗначенияСвойств(СтрокаЭквайринга, НалоговыйУчетТерминала);
		ВыбраннаяСтрока.НалоговыйУчет = ПолучитьПредставлениеНалоговогоУчетаСтроки(СтрокаЭквайринга, ВидимостьУчитыватьВНУ, ВидимостьПатент);
		
	КонецЕсли; 
	
КонецПроцедуры

// Функция возвращает строку с описанием настроек налогового учета в строке табличной части "ЭквайринговыеОперации"
//
&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПредставлениеНалоговогоУчетаСтроки(ВыбраннаяСтрока, ВидимостьУСН, ВидимостьПатент) Экспорт
	
	Если ВидимостьУСН Тогда
		УСНСтрокой = ?(ВыбраннаяСтрока.УчитыватьВНУ, НСтр("ru='УСН (Да), '"), НСтр("ru='УСН (Нет), '"));
	Иначе
		УСНСтрокой = "";
	КонецЕсли; 
	
	НалогообложениеСтрокой = РаботаСФормойДокументаКлиентСервер.КраткоеПредставлениеТипаНалогообложенияНДС(ВыбраннаяСтрока.НалогообложениеНДС);
	
	Если НЕ ЗначениеЗаполнено(НалогообложениеСтрокой) Тогда
		НалогообложениеСтрокой = НСтр("ru='<Не выбрано>'");
	КонецЕсли; 
	
	Если ВидимостьПатент Тогда
		ПатентСтрокой = ?(ЗначениеЗаполнено(ВыбраннаяСтрока.Патент), ", " + Строка(ВыбраннаяСтрока.Патент), "");
	Иначе
	    ПатентСтрокой = "";
	КонецЕсли;
	
	СтрокаНалоговогоУчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1%2%3'"), УСНСтрокой, НалогообложениеСтрокой, ПатентСтрокой);
	
	Возврат СтрокаНалоговогоУчета;
	
КонецФункции

// Функция возвращает структуру настроек налогообложения для эквайрингового терминала из параметра
//
&НаСервереБезКонтекста
Функция ПолучитьНастройкиНалоговогоУчетаТерминала(Терминал)

	Возврат ЭквайринговыеОперацииСервер.ПолучитьНастройкиНалоговогоУчетаТерминала(Терминал);

КонецФункции

// Обработка события "ПриИзменении" реквизита "ДоговорЭквайринга"
//
&НаКлиенте
Процедура ДоговорЭквайрингаПриИзменении(Элемент)
	
	ДоговорЭквайрингаПриИзмененииНаСервере();
	Если Объект.ПоложениеЭквайринговогоТерминала = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") И Объект.ЭквайринговыеОперации.Количество() > 0 Тогда
		Для Каждого ЭлементКоллекции Из Элементы.ЭквайринговыеОперации Цикл
			ОбработатьИзменениеТерминалаВТЧ(ЭлементКоллекции);
		КонецЦикла;
	Иначе
		ОбработатьИзменениеТерминалаВШапке(Элемент);
	КонецЕсли;
	
КонецПроцедуры

// Процедура приводит в соответствие с выбранным договором табличную часть "ЭквайринговыеОперации"
//
&НаСервере
Процедура ДоговорЭквайрингаПриИзмененииНаСервере()
	
	МассивДляУдаления = Новый Массив;
	
	Для каждого СтрокаЭквайринг Из Объект.ЭквайринговыеОперации Цикл
		
		Если СтрокаЭквайринг.ЭквайринговыйТерминал.Договор <> ДоговорЭквайринга Тогда
			МассивДляУдаления.Добавить(СтрокаЭквайринг);
		КонецЕсли; 
		
	КонецЦикла;
	
	Для каждого СтрокаДляУдаления Из МассивДляУдаления Цикл
		Объект.ЭквайринговыеОперации.Удалить(СтрокаДляУдаления);
	КонецЦикла;
	
	ФормыДокументовДеньги.УстановитьВидимостьЭлементовВЗависимостиОтВидаОперации(ЭтаФорма);
	ФормыДокументовДеньги.УстановитьВидимостьОтПользовательскихНастроек(ЭтаФорма);
КонецПроцедуры

// Обработка события "ПриУдалении" строки табличной части "ЭквайринговыеОперации"
//
&НаКлиенте
Процедура ЭквайринговыеОперацииПослеУдаления(Элемент)
	
	РаспределитьСуммуКомиссии();
	
	Для каждого СтрокаЭквайринга Из Объект.ЭквайринговыеОперации Цикл
		ПересчитатьСуммуПлатежаИКомиссииЭквайринга(СтрокаЭквайринга);
	КонецЦикла; 
	
КонецПроцедуры

// Процедура распределяет сумму комиссии по всем строкам табличной части, когда в комиссия рассчитывается в отчете эквайера
//
&НаСервере
Процедура РаспределитьСуммуКомиссии()
	
	//Необходимо перераспределить комиссию по всем строкам табличной части
	Если ДоговорЭквайринга.РасчетКомиссииВОтчетеЭквайера И ДоговорЭквайринга.КонтрольВзаиморасчетовЭквайринг Тогда
		Если Объект.ЭквайринговыеОперации.Количество() > 0 Тогда
			ТаблицаЭквайринга = Объект.ЭквайринговыеОперации.Выгрузить();
			МассивКоэффициентов = ТаблицаЭквайринга.ВыгрузитьКолонку("СуммаРасчетов");
			МассивСумм = ЭквайринговыеОперацииСервер.РаспределитьПропорционально(Объект.СуммаКомиссииДокумента, МассивКоэффициентов);
			Если МассивСумм <> Неопределено Тогда
				ТаблицаЭквайринга.ЗагрузитьКолонку(МассивСумм, "СуммаРасчетовКомиссии");
				ТаблицаЭквайринга.ЗагрузитьКолонку(МассивСумм, "СуммаКомиссииИтогоПоСтроке");
				Объект.ЭквайринговыеОперации.Загрузить(ТаблицаЭквайринга);
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеТерминалаВШапке(Элемент)
	
	ПараметрыЭквайринговогоТерминала = ПолучитьПараметрыЭквайринговогоТерминалаПриИзменении(Объект.ЭквайринговыйТерминал);
	ТипСчетаЗатратШапка = ПараметрыЭквайринговогоТерминала.ТипСчетаЗатрат;
	ЭквайринговыйТерминалПриИзмененииНаСервере();
	Объект.НаправлениеДеятельностиЗатраты = ПараметрыЭквайринговогоТерминала.НаправлениеДеятельности;
	Объект.ПодразделениеЗатраты = ПараметрыЭквайринговогоТерминала.Подразделение;
	Если Объект.БанковскийСчет <> ПараметрыЭквайринговогоТерминала.БанковскийСчетЭквайринг И ЗначениеЗаполнено(ПараметрыЭквайринговогоТерминала.БанковскийСчетЭквайринг) Тогда
		Объект.БанковскийСчет = ПараметрыЭквайринговогоТерминала.БанковскийСчетЭквайринг;
		БанковскийСчетПриИзменении(Элемент);
	КонецЕсли;
	
	ЭквайринговыйТерминал = Объект.ЭквайринговыйТерминал;
	
	МассивДляУдаления = Новый Массив;
	Для каждого СтрокаОпераций Из Объект.ЭквайринговыеОперации Цикл
		Если ЗначениеЗаполнено(СтрокаОпераций.ЭквайринговыйТерминал) И СтрокаОпераций.ЭквайринговыйТерминал <> ЭквайринговыйТерминал Тогда
			 МассивДляУдаления.Добавить(СтрокаОпераций);
		Иначе
			СтрокаОпераций.ЭквайринговыйТерминал = ЭквайринговыйТерминал;
		КонецЕсли; 
	КонецЦикла; 
	
	Для каждого СтрокаДляУдаления Из МассивДляУдаления Цикл
		Объект.ЭквайринговыеОперации.Удалить(СтрокаДляУдаления);
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеТерминалаВТЧ(ВыбраннаяСтрока)
	
	НалоговыйУчетТерминала = ПолучитьНастройкиНалоговогоУчетаТерминала(ВыбраннаяСтрока.ЭквайринговыйТерминал);
	
	Если НалоговыйУчетТерминала.СобственныеНастройкиНалоговогоУчета Тогда
		
		СтрокаЭквайринга = Объект.ЭквайринговыеОперации.НайтиПоИдентификатору(ВыбраннаяСтрока.ПолучитьИдентификатор()); 
		ЗаполнитьЗначенияСвойств(СтрокаЭквайринга, НалоговыйУчетТерминала);
		ВыбраннаяСтрока.НалоговыйУчет = ПолучитьПредставлениеНалоговогоУчетаСтроки(СтрокаЭквайринга, ВидимостьУчитыватьВНУ, ВидимостьПатент);
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеРасчеты

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДоговорЗаймаСотрудникуПриИзменении(Элемент)
	
	ОбработатьИзменениеДоговораКредитаИлиЗайма();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКредитаПриИзменении(Элемент)
	
	ОбработатьИзменениеДоговораКредитаИлиЗайма();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорСАгентомНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.ВидОперации) Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Сначала выберите операцию.'"));
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Сначала выберите контрагента.'"));
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.РасшифровкаПлатежа.Количество() = 0) Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Ошибка заполнения документа. Проверьте заполнение организации, операции, контрагента.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = ПолучитьПараметрыФормыВыбораДоговора(Объект.Ссылка, Объект.Организация, Объект.Контрагент, Объект.РасшифровкаПлатежа[0].Договор, Объект.ВидОперации);
	Если ПараметрыФормы.КонтролироватьВыборДоговора Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаВыбора", ПараметрыФормы, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеДоговораКредитаИлиЗайма()
	
	ЗаполнитьИнформациюПоКредитуЗаймуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнформациюПоКредитуЗаймуНаСервере()
	
	ФормыДокументовДеньги.ЗаполнитьИнформациюПоКредитуЗаймуНаСервере(ЭтаФорма);
	
	СтруктураДанные = РасчетыРаботаСФормамиВызовСервера.ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(
			Объект.Дата,
			Объект.ДоговорКредитаЗайма
		);
		
	Для Каждого СтрокаТабличнойЧасти Из Объект.РасшифровкаПлатежа Цикл
		СтрокаТабличнойЧасти.Курс = ?(
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс = 0,
			1,
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс
		);
		СтрокаТабличнойЧасти.Кратность = ?(
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность = 0,
			1,
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность
		);
		
		СтрокаТабличнойЧасти.СуммаРасчетов = ВалютыУНФКлиентСервер.Пересчитать(СтрокаТабличнойЧасти.СуммаПлатежа, Курс,
			СтрокаТабличнойЧасти.Курс, Кратность, СтрокаТабличнойЧасти.Кратность);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	
	СтруктураДанные = ПолучитьДанныеСотрудникПриИзменении(Объект.Подотчетник, Объект.Дата, Объект.Организация);
	Объект.ДоговорКредитаЗайма = СтруктураДанные.ДоговорКредитаЗайма;
	ОбработатьИзменениеДоговораКредитаИлиЗайма();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеСотрудникПриИзменении(Сотрудник, Дата, Организация)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить("ДоговорКредитаЗайма",
		Документы.ДоговорКредитаИЗайма.ПолучитьДоговорКредитаИЗаймаПоУмолчаниюПоОрганизацииВидуДоговора(Сотрудник,
		Организация));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеПодотчетникПриИзменении()

&НаКлиенте
Процедура РасчетыПрочиеКорреспонденцияПриИзменении(Элемент)
	
	Если Корреспонденция <> Объект.Корреспонденция Тогда
		УстановитьВидимостьРеквизитовВЗависимостиОтКорреспонденции(Истина);
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Корреспонденция = Объект.Корреспонденция;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УчитыватьВНУПриИзменении(Элемент)
	Если Объект.УчитыватьВНУ Тогда
		Объект.Патент = ПредопределенноеЗначение("Справочник.Патенты.ПустаяСсылка");
	КонецЕсли;
	Элементы.Патент.Доступность = Не Объект.УчитыватьВНУ;
	УстановитьНалоговыйСпецРежим();
КонецПроцедуры

&НаКлиенте
Процедура ПатентПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.Патент) Тогда
		Объект.УчитыватьВНУ = Ложь;
	КонецЕсли;
	УстановитьНалоговыйСпецРежим();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНалоговыйСпецРежим()
	Если ЗначениеЗаполнено(Объект.Патент) Тогда
		Объект.СпециальныйНалоговыйРежим = ПредопределенноеЗначение("Перечисление.СпециальныеНалоговыеРежимы.ПСН");
	Иначе
		Если Объект.СпециальныйНалоговыйРежим = ПредопределенноеЗначение("Перечисление.СпециальныеНалоговыеРежимы.ПСН") Тогда
			Объект.СпециальныйНалоговыйРежим = ПредопределенноеЗначение("Перечисление.СпециальныеНалоговыеРежимы.НеПрименяется");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидНалогаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если СтрДлина(Текст) > 13 Тогда
		
		СтрокаПоискаПоКБК = СтрЗаменить(ПараметрыПолученияДанных.СтрокаПоиска, " ", "");
		
		Если СтрДлина(СтрокаПоискаПоКБК) <= 20 И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрокаПоискаПоКБК) Тогда
			
			// Строка поиска похожа на КБК. Код включает код подвида доходов.
			ДанныеВыбора = ДвиженияДенежныхСредствВызовСервера.ДанныеВыбораНалогаПоКБК(СтрокаПоискаПоКБК);
			
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРасшифровкаПлатежа

&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыСуммаРасчетовПриИзменении(Элемент)
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуПлатежаНаКлиенте(ЭтотОбъект, Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыПередУдалением(Элемент, Отказ)
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыКурсПриИзменении(Элемент)
		
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуПлатежаНаКлиенте(ЭтотОбъект, Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыКурсНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РасчетыРаботаСФормамиКлиент.ПредоплатаКурсНачалоВыбора(ЭтаФорма, Элемент, ДанныеВыбора, СтандартнаяОбработка, "РасшифровкаПлатежаПрочиеРасчеты");
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыКратностьПриИзменении(Элемент)
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуПлатежаНаКлиенте(ЭтотОбъект, Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыСуммаПлатежаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные;
	
	СтрокаТабличнойЧасти.Курс = ?(
		СтрокаТабличнойЧасти.Курс = 0,
		1,
		СтрокаТабличнойЧасти.Курс
	);
	СтрокаТабличнойЧасти.Кратность = ?(
		СтрокаТабличнойЧасти.Кратность = 0,
		1,
		СтрокаТабличнойЧасти.Кратность
	);
	
	СтрокаТабличнойЧасти.Курс = ?(
		СтрокаТабличнойЧасти.СуммаРасчетов = 0,
		1,
		СтрокаТабличнойЧасти.СуммаПлатежа / СтрокаТабличнойЧасти.СуммаРасчетов * Курс
	);
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
		СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
	КонецЕсли;
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыДоговорНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.Контрагент.Пустая() Тогда
		СтандартнаяОбработка = Ложь;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Сначала выберите контрагента'");
		Сообщение.Поле = "Объект.Контрагент";
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;
	
	ОбработатьНачалоВыбораДоговораКонтрагентаПрочиеРасчеты(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыДоговорПриИзменении(Элемент)
	
	ОбработатьИзменениеДоговораКонтрагентаПрочиеРасчеты();
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные;
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);

КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамСуммаРасчетовПриИзменении(Элемент)
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуПлатежаНаКлиенте(ЭтотОбъект, Элементы.РасшифровкаПлатежаРасчетыПоКредитам.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамКурсПриИзменении(Элемент)
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуПлатежаНаКлиенте(ЭтотОбъект, Элементы.РасшифровкаПлатежаРасчетыПоКредитам.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамКурсНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РасчетыРаботаСФормамиКлиент.ПредоплатаКурсНачалоВыбора(ЭтаФорма, Элемент, ДанныеВыбора, СтандартнаяОбработка, "РасшифровкаПлатежаРасчетыПоКредитам");
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамКратностьПриИзменении(Элемент)
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуПлатежаНаКлиенте(ЭтотОбъект, Элементы.РасшифровкаПлатежаРасчетыПоКредитам.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежаРасчетыПоКредитам.ТекущиеДанные;
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамСуммаПлатежаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежаРасчетыПоКредитам.ТекущиеДанные;
	
	РасчетыРаботаСФормамиКлиент.РасшифровкаПлатежаРасчетыПоКредитамСуммаПлатежаПриИзменении(ЭтотОбъект, СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамПередУдалением(Элемент, Отказ)
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетыСПодотчетникомРасшифровкаПлатежаДокументНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.Подотчетник.Пустая() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Сначала нужно выбрать подотчетника'"),,
			"Объект.Подотчетник");
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетыСПодотчетникомРасшифровкаПлатежаСуммаПлатежаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.РасчетыСПодотчетникомРасшифровкаПлатежа.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда
		ТекущаяСтрока.СуммаРасчетов = ТекущаяСтрока.СуммаПлатежа;
	КонецЕсли;
	ПересчитатьИтогиПриИзмененииАвансаНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Заполняет сумму документа остатком суммы кредита, которую еще не получили от банка.
//
&НаСервере
Процедура ЗаполнитьПоДоговоруКредитаНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетыПоКредитамИЗаймамОбороты.ДоговорКредитаЗайма.ВалютаРасчетов КАК ВалютаРасчетов,
	|	РасчетыПоКредитамИЗаймамОбороты.ОсновнойДолгВалРасход
	|ПОМЕСТИТЬ ВременнаяТаблицаВыданныеРанееСуммы
	|ИЗ
	|	РегистрНакопления.РасчетыПоКредитамИЗаймам.Обороты(
	|			,
	|			,
	|			,
	|			ДоговорКредитаЗайма = &ДоговорЗаймаСотруднику
	|				И Организация = &Организация) КАК РасчетыПоКредитамИЗаймамОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыПоКредитамИЗаймам.ДоговорКредитаЗайма.ВалютаРасчетов,
	|	ВЫБОР
	|		КОГДА РасчетыПоКредитамИЗаймам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -РасчетыПоКредитамИЗаймам.ОсновнойДолгВал
	|		ИНАЧЕ РасчетыПоКредитамИЗаймам.ОсновнойДолгВал
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.РасчетыПоКредитамИЗаймам КАК РасчетыПоКредитамИЗаймам
	|ГДЕ
	|	РасчетыПоКредитамИЗаймам.Регистратор = &Ссылка
	|	И РасчетыПоКредитамИЗаймам.ДоговорКредитаЗайма = &ДоговорЗаймаСотруднику
	|	И РасчетыПоКредитамИЗаймам.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаВыданныеРанееСуммы.ВалютаРасчетов,
	|	СУММА(ВременнаяТаблицаВыданныеРанееСуммы.ОсновнойДолгВалРасход) КАК ОсновнойДолгВалРасход,
	|	ДоговорЗаймаСотруднику.СуммаДокумента
	|ИЗ
	|	ВременнаяТаблицаВыданныеРанееСуммы КАК ВременнаяТаблицаВыданныеРанееСуммы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДоговорКредитаИЗайма КАК ДоговорЗаймаСотруднику
	|		ПО ВременнаяТаблицаВыданныеРанееСуммы.ВалютаРасчетов = ДоговорЗаймаСотруднику.ВалютаРасчетов
	|ГДЕ
	|	ДоговорЗаймаСотруднику.Ссылка = &ДоговорЗаймаСотруднику
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаВыданныеРанееСуммы.ВалютаРасчетов,
	|	ДоговорЗаймаСотруднику.СуммаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(, Валюта = &ВалютаРасчетов) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(, Валюта = &ВалютаДенежныхСредств) КАК КурсыВалютСрезПоследних";
	
	Запрос.УстановитьПараметр("ДоговорЗаймаСотруднику", Объект.ДоговорКредитаЗайма);
	Запрос.УстановитьПараметр("Организация", Компания);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("ВалютаРасчетов", Объект.ДоговорКредитаЗайма.ВалютаРасчетов);
	Запрос.УстановитьПараметр("ВалютаДенежныхСредств", Объект.ВалютаДенежныхСредств);
	
	МРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = МРезультатов[1].Выбрать();
	
	ВыборкаВалютаДоговора = МРезультатов[2].Выбрать();
	Если ВыборкаВалютаДоговора.Следующий() Тогда
		КурсДоговора = ВыборкаВалютаДоговора.Курс;
		КратностьДоговора = ВыборкаВалютаДоговора.Кратность;
	Иначе
		КурсДоговора = 1;
		КратностьДоговора = 1;
	КонецЕсли;
	
	ВыборкаВалютыДС = МРезультатов[3].Выбрать();
	Если ВыборкаВалютыДС.Следующий() Тогда
		КурсДокумента = ВыборкаВалютыДС.Курс;
		КратностьДокумента = ВыборкаВалютыДС.Кратность;
	Иначе
		КурсДокумента = 1;
		КратностьДокумента = 1;
	КонецЕсли;
	
	Если Выборка.Следующий() Тогда
		ТекстСообщения = "";
		Если Выборка.СуммаДокумента < Выборка.ОсновнойДолгВалРасход Тогда
			ТекстСообщения = "По договору кредита уже выдано "+Выборка.ОсновнойДолгВалРасход+" ("+Выборка.ВалютаРасчетов+").";
		ИначеЕсли Выборка.СуммаДокумента = Выборка.ОсновнойДолгВалРасход Тогда
			ТекстСообщения = "По договору кредита уже выдана вся сумма: "+Выборка.ОсновнойДолгВалРасход+" ("+Выборка.ВалютаРасчетов+").";
		Иначе
			Объект.СуммаДокумента = (Выборка.СуммаДокумента - Выборка.ОсновнойДолгВалРасход) * КурсДоговора * КратностьДокумента / (КратностьДоговора * КурсДокумента);
		КонецЕсли;
		
		Если ТекстСообщения <> "" Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТекстСообщения;
			Сообщение.Поле = "ДоговорЗаймаСотруднику";
			Сообщение.Сообщить();
		КонецЕсли;
	Иначе
		Объект.СуммаДокумента = Объект.ДоговорКредитаЗайма.СуммаДокумента * КурсДоговора * КратностьДокумента / (КратностьДоговора * КурсДокумента);
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДоговоруКредита(Команда)
	
	Если Объект.ДоговорКредитаЗайма.Пустая() Тогда
		ПоказатьПредупреждение(Неопределено, "Выберите договор");
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоДоговоруКредитаНаСервере();
	СуммаДокументаПриИзменении(Элементы.СуммаДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДоговоруЗайма(Команда)
	
	Если Объект.ДоговорКредитаЗайма.Пустая() Тогда
		ПоказатьПредупреждение(Неопределено, "Выберите договор");
		Возврат;
	КонецЕсли;
	
	АдресРасшифровкиПлатежаВХранилище = ПоместитьРасшифровкаПлатежаВХранилище();
	ПараметрыОтбора = Новый Структура("
		|АдресРасшифровкиПлатежаВХранилище,
		|Организация,
		|Регистратор,
		|ИдентификаторФормыДокумента,
		|ВидОперации,
		|Дата,
		|Валюта,
		|ДоговорКредитаЗайма,
		|СуммаДокумента,
		|Контрагент,
		|СтавкаНДСПоУмолчанию,
		|СуммаПлатежа,
		|Курс,
		|Кратность,
		|Сотрудник",
		АдресРасшифровкиПлатежаВХранилище,
		Объект.Организация,
		Объект.Ссылка,
		УникальныйИдентификатор,
		Объект.ВидОперации,
		Объект.Дата,
		Объект.ВалютаДенежныхСредств,
		Объект.ДоговорКредитаЗайма,
		Объект.СуммаДокумента,
		Объект.Контрагент,
		СтавкаНДСПоУмолчанию,
		Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа"),
		Курс,
		Кратность,
		Объект.Подотчетник);
	
	ОткрытьФорму("ОбщаяФорма.ФормаЗаполненияРасшифровкиПлатежаПоКредитамИЗаймам", 
						ПараметрыОтбора,
						ЭтаФорма,,,,Новый ОписаниеОповещения("ЗаполнитьПоДоговоруЗаймаЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДоговоруЗаймаЗавершение(РезультатЗаполнения, ПараметрыЗавершения) Экспорт
	ЗаполнитьСуммуДокумента = Ложь;
	Если ТипЗнч(РезультатЗаполнения) = Тип("Структура") Тогда
		Если РезультатЗаполнения.Свойство("ОчищатьТабличнуюЧастьПриЗаполнении") И РезультатЗаполнения.ОчищатьТабличнуюЧастьПриЗаполнении Тогда
			Объект.РасшифровкаПлатежа.Очистить();
			ЗаполнитьСуммуДокумента = Истина;
		КонецЕсли;
		Если РезультатЗаполнения.Свойство("АдресРасшифровкиПлатежаВХранилище") Тогда
			ПолучитьРасшифровкаПлатежаИзХранилища(РезультатЗаполнения.АдресРасшифровкиПлатежаВХранилище, Ложь);
			
			Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
				Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
			КонецЕсли;
		КонецЕсли;
		Если ЗаполнитьСуммуДокумента Тогда
			Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
			СуммаДокументаПриИзменении(Элементы.СуммаДокумента);
		КонецЕсли;
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШапкаТабличнаяЧасть(Команда)
	
	ФормыДокументовДеньгиКлиент.ШапкаТабличнаяЧасть(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ШапкаТабличнаяЧастьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.БылиВнесеныИзменения Тогда
		ШапкаТабличнаяЧастьЗавершениеНаСервере(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ШапкаТабличнаяЧастьЗавершениеНаСервере(Знач Результат)
	
	ФормыДокументовДеньги.ШапкаТабличнаяЧастьЗавершение(ЭтаФорма, Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьИзменениеДоговораКонтрагентаПрочиеРасчеты()
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Договор) Тогда
		СтруктураДанные = РасчетыРаботаСФормамиВызовСервера.ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(
			Объект.Дата,
			СтрокаТабличнойЧасти.Договор,
			СтрокаТабличнойЧасти.ДокументПланирования,
			СтрокаТабличнойЧасти.СтатьяДДС
		);
		СтрокаТабличнойЧасти.Курс = ?(
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс = 0,
			1,
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс
		);
		СтрокаТабличнойЧасти.Кратность = ?(
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность = 0,
			1,
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность
		);
		
		СтрокаТабличнойЧасти.СтатьяДДС = СтруктураДанные.СтатьяДДСПоУмолчанию;
	Иначе
		СтрокаТабличнойЧасти.СтатьяДДС = ДвиженияДенежныхСредствВызовСервера.ПолучитьСтатьюДДСПоУмолчаниюДляСтрокиРасшифровки(
			СтрокаТабличнойЧасти.Договор, СтрокаТабличнойЧасти.ДокументПланирования, СтрокаТабличнойЧасти.СтатьяДДС);
	КонецЕсли;
	
	СтрокаТабличнойЧасти.СуммаРасчетов = ВалютыУНФКлиентСервер.Пересчитать(СтрокаТабличнойЧасти.СуммаПлатежа, Курс,
		СтрокаТабличнойЧасти.Курс, Кратность, СтрокаТабличнойЧасти.Кратность);
	
КонецПроцедуры // ОбработатьИзменениеДоговораКонтрагента()

&НаКлиенте
Процедура ОбработатьНачалоВыбораДоговораКонтрагентаПрочиеРасчеты(Элемент, СтандартнаяОбработка)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = ПолучитьПараметрыФормыВыбора(Объект.Ссылка, Объект.Организация, Объект.Контрагент, СтрокаТабличнойЧасти.Договор, Объект.ВидОперации);
	Если ПараметрыФормы.КонтролироватьВыборДоговора Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаВыбора", ПараметрыФормы, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры // ОбработатьИзменениеДоговораКонтрагента()

&НаСервере
Процедура УстановитьВидимостьРеквизитовВЗависимостиОтКорреспонденции(ОчиститьАналитику = Ложь)
	
	ФормыДокументовДеньги.УстановитьВидимостьРеквизитовВЗависимостиОтКорреспонденции(ЭтаФорма, ОчиститьАналитику);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДоговорКредитаЗаймаПоУмолчанию(Документ, Контрагент, Организация, ВидОперации)
	
	МенеджерДокумента = Документы.ДоговорКредитаИЗайма;
	
	СписокВидовДоговоров = Новый СписокЗначений;
	СписокВидовДоговоров.Добавить(?(ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.РасчетыПоКредитам, 
		Перечисления.ВидыДоговоровКредитаИЗайма.КредитПолученный,
		Перечисления.ВидыДоговоровКредитаИЗайма.ДоговорЗаймаСотруднику));
	
	ДоговорКредитаЗаймаПоУмолчанию = МенеджерДокумента.ПолучитьДоговорКредитаИЗаймаПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, СписокВидовДоговоров);
	
	Возврат ДоговорКредитаЗаймаПоУмолчанию;
	
КонецФункции

&НаСервере
Процедура УстановитьПараметрыВыбораПоУчетуПрочихРасчетовНаСервереДляЭлементаСчета()

	Элемент = Элементы.Корреспонденция;
	
	ПараметрыВыбораЭлемента = Новый Массив;
	ОтборПоТипуСчета = Новый Массив;
	
	Для Каждого Параметр Из Элемент.ПараметрыВыбора Цикл
		Если Параметр.Имя = "Отбор.ТипСчета" Тогда
			ОтборПоТипуСчета.Добавить(Перечисления.ТипыСчетов.Дебиторы);
			ОтборПоТипуСчета.Добавить(Перечисления.ТипыСчетов.Кредиторы);
			
			ПараметрыВыбораЭлемента.Добавить(Новый ПараметрВыбора("Отбор.ТипСчета", Новый ФиксированныйМассив(ОтборПоТипуСчета)));
		Иначе
			ПараметрыВыбораЭлемента.Добавить(Параметр);
		КонецЕсли;
	КонецЦикла;
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораЭлемента);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораПоМетаданнымДляЭлементаСчета()

	Элемент = Элементы.Корреспонденция;
	
	ПараметрыВыбораЭлемента = Новый Массив;
	
	ПараметрыВыбораИзМетаданных = Объект.Ссылка.Метаданные().Реквизиты.Корреспонденция.ПараметрыВыбора;
	Для Каждого Параметр Из ПараметрыВыбораИзМетаданных Цикл
		ПараметрыВыбораЭлемента.Добавить(Параметр);
	КонецЦикла;
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораЭлемента);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкиУчетаВНалогообложении()
	
	ФормыДокументовДеньги.ОпределитьВидимостьНастроекУчетаВНалогообложении(ЭтаФорма);
	
	Если ЗначениеЗаполнено(Объект.Статья) Тогда
		Объект.УчитыватьВНУ = РегламентированнаяОтчетностьУСН.НужноУчитыватьВНУ(Объект.Статья, Объект.ВидОперации, Объект.Организация, Объект.Дата);
	КонецЕсли;
	Объект.СпециальныйНалоговыйРежим = НалогиУНФ.СпециальныйНалоговыйРежим(Объект.Организация,,Объект.Дата);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайринговыеОперацииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		СтрокаЭквайринг = Элементы.ЭквайринговыеОперации.ТекущиеДанные;
		СтрокаЭквайринг.НалогообложениеНДС = Объект.НалогообложениеНДС;
		СтрокаЭквайринг.УчитыватьВНУ 	   = Объект.УчитыватьВНУ;
		СтрокаЭквайринг.Патент 			   = Объект.Патент;
		СтрокаЭквайринг.НалоговыйУчет = ПолучитьПредставлениеНалоговогоУчетаСтроки(СтрокаЭквайринг, ВидимостьУчитыватьВНУ, ВидимостьПатент);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область АвтоподборКонтактов

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	АвтоподборКонтактовКлиент.Подключаемый_ОбработкаВыбора(ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_АвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	АвтоподборКонтактовКлиент.Подключаемый_АвтоПодбор(ЭтаФорма, Элемент, Текст, ДанныеВыбора, Параметры, Ожидание,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
//@skip-warning
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено,
	СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма);
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
&НаКлиенте
Процедура ЗагрузкаИзФайлаРасчетыСАгентом(Команда)
	
	НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Документ.ПоступлениеНаСчет.ТабличнаяЧасть.РасшифровкаПлатежаОтАгента";
	
	НастройкиЗагрузкиДанных.Вставить("ПолноеИмяТабличнойЧасти", "ПоступлениеНаСчет.РасшифровкаПлатежаОтАгента");
	НастройкиЗагрузкиДанных.Вставить("ИмяМакетаСШаблоном", "ЗагрузкаИзФайлаРасшифровкаПлатежаОтАгента");
	НастройкиЗагрузкиДанных.Вставить("Заголовок", НСтр("ru = 'Загрузка оплат из файла'"));
	НастройкиЗагрузкиДанных.Вставить("Организация", Объект.Организация);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата", ЭтотОбъект, НастройкиЗагрузкиДанных);
	
	ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.ПоказатьФормуЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных, ОписаниеОповещения, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата(РезультатЗагрузки, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗагрузки) = Тип("Структура") Тогда
		
		Если РезультатЗагрузки.ОписаниеДействия = "ИзменитьСпособЗагрузкиДанныхИзВнешнегоИсточника" Тогда
		
			ЗагрузкаДанныхИзВнешнегоИсточника.ИзменитьСпособЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных.ИмяФормыЗагрузкиДанныхИзВнешнихИсточников);
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата", ЭтотОбъект, НастройкиЗагрузкиДанных);
			ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.ПоказатьФормуЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных, ОписаниеОповещения, ЭтотОбъект);
			
		ИначеЕсли РезультатЗагрузки.ОписаниеДействия = "ОбработатьПодготовленныеДанные" Тогда
			
			ОбработатьПодготовленныеДанные(РезультатЗагрузки);
			
		КонецЕсли;
		
	Иначе
		
		ЗагрузитьИзФайлаЗавершение(РезультатЗагрузки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаЗавершение(АдресЗагруженныхДанных) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ЗагрузитьИзФайлаНаСервере(АдресЗагруженныхДанных);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИзФайлаНаСервере(АдресЗагруженныхДанных)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	СтрокиДобавлены = Ложь;
	Для каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Заказ) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаРасшифровка = Объект.РасшифровкаПлатежаОтАгента.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаРасшифровка, СтрокаТаблицы);
		НоваяСтрокаРасшифровка.СуммаПлатежа = НоваяСтрокаРасшифровка.ПолученоОтКлиента - НоваяСтрокаРасшифровка.УдержаноАгентом;
		Если НЕ ЗначениеЗаполнено(НоваяСтрокаРасшифровка.Документ) Тогда
			ЗаполнитьДокументРасчетов(НоваяСтрокаРасшифровка);
		КонецЕсли; 
		СтрокиДобавлены = Истина;
		
	КонецЦикла;
	
	Если СтрокиДобавлены Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеВТабличноеПолеРасшифровкаПлатежаОтАгента(ТаблицаСопоставленияДанных)
	
	НачатьТранзакцию();
	
	Попытка
		
		Для Каждого СтрокаТаблицы Из ТаблицаСопоставленияДанных Цикл
			
			ЗагрузкаВПриложениеВозможна = СтрокаТаблицы[ЗагрузкаДанныхИзВнешнегоИсточника.ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна()];
			Если ЗагрузкаВПриложениеВозможна Тогда
				
				НоваяСтрока = Объект.РасшифровкаПлатежаОтАгента.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				НоваяСтрока.СуммаПлатежа = НоваяСтрока.ПолученоОтКлиента - НоваяСтрока.УдержаноАгентом;
				Если НЕ ЗначениеЗаполнено(НоваяСтрока.Документ) Тогда
					ЗаполнитьДокументРасчетов(НоваяСтрока);
				КонецЕсли; 
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ИмяСобытия = НСтр("ru='Загрузка данных'", ОбщегоНазначения.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.ПоступлениеНаСчет,, ПредставлениеОшибки);
		
		ВызватьИсключение ПредставлениеОшибки;
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокументРасчетов(СтрокаРасчетов)
	
	ЗаказРасчетов = СтрокаРасчетов.Заказ;
	Если НЕ ЗначениеЗаполнено(ЗаказРасчетов) Тогда
		Возврат;
	КонецЕсли; 
	СуммаРасчетов = СтрокаРасчетов.ПолученоОтКлиента;
	Если НЕ ЗначениеЗаполнено(СуммаРасчетов) Тогда
		Возврат;
	КонецЕсли;
	Если Объект.ВалютаДенежныхСредств <> ВалютаУчета Тогда
		СтруктураКурсы = ЦенообразованиеСервер.ПолучитьКурсыВалют(ВалютаУчета, Объект.ВалютаДенежныхСредств, Объект.Дата);
		СуммаРасчетов = ЦенообразованиеСервер.ПересчитатьИзВалютыВВалюту(СуммаРасчетов, СтруктураКурсы.КурсНач, СтруктураКурсы.Курс, СтруктураКурсы.КратностьНач, СтруктураКурсы.Кратность);
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЗаказРасчетов", ЗаказРасчетов);
	Запрос.УстановитьПараметр("СуммаРасчетов", СуммаРасчетов);
	Запрос.УстановитьПараметр("МоментРасчетов", ?(Объект.Проведен, Объект.Дата - 1, КонецДня(Объект.Дата)));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетыСПокупателямиОстатки.Заказ КАК Заказ,
	|	РасчетыСПокупателямиОстатки.Документ КАК Документ
	|ИЗ
	|	РегистрНакопления.РасчетыСПокупателями.Остатки(&МоментРасчетов, ) КАК РасчетыСПокупателямиОстатки
	|ГДЕ
	|	РасчетыСПокупателямиОстатки.Заказ = &ЗаказРасчетов
	|	И РасчетыСПокупателямиОстатки.СуммаОстаток = &СуммаРасчетов";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтрокаРасчетов.Документ = Выборка.Документ;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПодготовленныеДанные(РезультатЗагрузки)
	
	ТаблицаСопоставленияДанных = РезультатЗагрузки.ТаблицаСопоставленияДанных;
	ЗагрузитьДанныеВТабличноеПолеРасшифровкаПлатежаОтАгента(ТаблицаСопоставленияДанных);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника

#КонецОбласти

#КонецОбласти
