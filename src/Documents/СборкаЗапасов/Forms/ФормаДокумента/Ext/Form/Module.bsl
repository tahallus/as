
#Область ОписаниеПеременных

&НаКлиенте
Перем СтароеКоличествоПродукции;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
	ЗагрузкаДанныхИзВнешнегоИсточника.ПриСозданииНаСервере(Метаданные.Документы.ПриходнаяНакладная.ТабличныеЧасти.Запасы, НастройкиЗагрузкиДанных, ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = УправлениеСвойствамиУНФ.ЗаполнитьДополнительныеПараметры(Объект,
		"ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	УстановитьУсловноеОформлениеФормы();
	Если Объект.Ссылка.Пустая() Тогда
		ЗаполнитьКэшЗначений();
		// Прослеживаемость
		ОбновитьОтображениеПрослеживаемости();
		// Конец Прослеживаемость
	КонецЕсли; 
	
	ПечатьДокументовУНФ.УстановитьОтображениеПодменюПечати(Элементы.ПодменюПечать);
	
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДатаСеанса();
	КонецЕсли;
	ЭтотОбъект.ОснованиеСоздания = Параметры.Основание;
	ТипСтруктурнойЕдиницы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СтруктурнаяЕдиница,
		"ТипСтруктурнойЕдиницы");
	
	Компания = Константы.УчетПоКомпании.Компания(Объект.Организация);
	ОбновитьСпособРаспределенияПоУмолчанию();
	ФОРезервированиеЗапасов = ПолучитьФункциональнуюОпцию("РезервированиеЗапасов");
	ФОРозница = ПолучитьФункциональнуюОпцию("УчетРозничныхПродаж");
	ФОИспользоватьЯчейки = ПолучитьФункциональнуюОпцию("УчетПоЯчейкам");
	
	// ГТД и РНПТ
	ИменаТЧИПолей = ГрузовыеТаможенныеДекларацииСервер.ИменаТЧИПолейДляТаблицыЗапасы_БазовыеПоляСПрослеживаемостью();
	ИменаТЧИПолей = ГрузовыеТаможенныеДекларацииСервер.ИменаТЧИПолейДляТаблицыЗапасы_БазовыеПоляСПрослеживаемостью(ИменаТЧИПолей, "Продукция");
	
	ГрузовыеТаможенныеДекларацииСервер.ПриСозданииНаСервереДляДокументаСПрослеживаемостью(ЭтаФорма, ИменаТЧИПолей, КэшЗначений);
	// Конец ГТД и РНПТ
	
	КэшЗначений.Вставить("ВОРазборка", Перечисления.ВидыОперацийСборкаЗапасов.Разборка);
	КэшЗначений.Вставить("ВОСборка", Перечисления.ВидыОперацийСборкаЗапасов.Сборка);
	
	// Ячейки
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		КэшЗначений.Вставить("УчетПоЯчейкам", ПолучитьФункциональнуюОпцию("УчетПоЯчейкам"));
		ОбновитьДоступностьЯчеек();
		ЗаполнитьПризнакиДоступностиЯчеек(ЭтотОбъект);
	КонецЕсли; 
	// Конец Ячейки
	
	// Установка видимости реквизитов от пользовательских настроек
	УстановитьВидимостьОтПользовательскихНастроек(ЭтотОбъект); 
	
	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	// ПодключаемоеОборудование.
	ИспользоватьПодключаемоеОборудование = УправлениеНебольшойФирмойПовтИсп.ИспользоватьПодключаемоеОборудование();
	СписокЭлектронныхВесов = МенеджерОборудованияВызовСервера.ОборудованиеПоПараметрам("ЭлектронныеВесы", ,
		МенеджерОборудованияВызовСервера.ПолучитьРабочееМестоКлиента());
	Если СписокЭлектронныхВесов.Количество() = 0 Тогда
		// Нет подключенных весов.
		Элементы.ЗапасыПолучитьВес.Видимость = Ложь;
	КонецЕсли;
	Элементы.ЗапасыЗагрузитьДанныеИзТСД.Видимость = ИспользоватьПодключаемоеОборудование;
	Элементы.ПродукцияЗагрузитьДанныеИзТСД.Видимость = ИспользоватьПодключаемоеОборудование;
	// Конец ПодключаемоеОборудование
	
	УстановитьВидимостьИДоступностьМобильноеПриложение();
	Если МобильноеПриложениеВызовСервера.НужноОграничитьФункциональность() Тогда
		ЗначениеЗаполненияПодразделениеПоУмолчанию = Справочники.СтруктурныеЕдиницы.ОсновнойСклад;
	Иначе
		ЗначениеЗаполненияПодразделениеПоУмолчанию = Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаПродукции) И НЕ ТолькоПросмотр Тогда
		Объект.СтруктурнаяЕдиницаПродукции = ЗначениеЗаполненияПодразделениеПоУмолчанию;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаЗапасов) И НЕ ТолькоПросмотр Тогда
		Объект.СтруктурнаяЕдиницаЗапасов = ЗначениеЗаполненияПодразделениеПоУмолчанию;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаОтходов) И НЕ ТолькоПросмотр Тогда
		Объект.СтруктурнаяЕдиницаОтходов = ЗначениеЗаполненияПодразделениеПоУмолчанию;
	КонецЕсли;
	
	// КопированиеСтрокТабличныхЧастей
	КопированиеТабличнойЧастиСервер.ПриСозданииНаСевере(Элементы, "Продукция");
	КопированиеТабличнойЧастиСервер.ПриСозданииНаСевере(Элементы, "Запасы");
	КопированиеТабличнойЧастиСервер.ПриСозданииНаСевере(Элементы, "Отходы");
	
	// Серии номенклатуры
	ИспользоватьСерииНоменклатурыОстатки = СерииНоменклатурыУНФ.ИспользоватьСерииНоменклатурыОстатки();
	
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	Элементы.ЗапасыГруппаСерииНоменклатуры.Видимость = ИспользоватьСерииНоменклатуры;
	Элементы.ПродукцияГруппаСерииНоменклатуры.Видимость = ИспользоватьСерииНоменклатуры;
	
	// Характеристики
	НоменклатураВДокументахСервер.ОбновитьУсловноеОформлениеТабличнойЧастиДляХарактеристик(ЭтаФорма);
	Если Параметры.Ключ.Пустая() 
		Тогда
		НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект, Истина);
	КонецЕсли;
	
	ТекущаяПродукция = -1;
	УстановитьКартинкиЗакладок(ЭтотОбъект);
	
	РучноеРаспределение = Объект.РучноеРаспределение;

	Если Объект.РучноеРаспределение Тогда
		ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	КонецЕсли; 
	
	// ЭтапыПроизводства
	Если Параметры.Ключ.Пустая() Тогда
		ЗаполнитьПризнакиИспользованияЭтапов();
		ОбновитьЭтапыНаФорме(ЭтотОбъект);
	КонецЕсли;
	// Конец ЭтапыПроизводства
	
	Элементы.ДокументОснованиеНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(
		Объект.ДокументОснование);	
	
	УправлениеФормой(ЭтотОбъект);
	УстановитьРежимИСписокВыбора();
	
	// ИнтеграцияГосИС
	СобытияФормИС.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка, ДополнительныеПараметры);
	ШтрихкодированиеИС.ИнициализироватьКэшМаркируемойПродукции(ЭтаФорма);
	// Конец ИнтеграцияГосИС
	
	// МобильныйКлиент
	УстановитьВидимостьЭлементовДляМобильногоКлиента();
	// Конец МобильныйКлиент
	
	РазрешитьСкладыВТабличныхЧастях = ПолучитьФункциональнуюОпцию("РазрешитьСкладыВТабличныхЧастях");
	СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЗаполнитьКэшЗначений();
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменений
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменений
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// Прослеживаемость
	ОбновитьОтображениеПрослеживаемости(); 
	// Конец Прослеживаемость
	
	// ИнтеграцияГосИС
	СобытияФормИС.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец ИнтеграцияГосИС
	
	// Ячейки
	Если ТипЗнч(КэшЗначений) <> Тип("Структура") Тогда
		КэшЗначений = Новый Структура;
	КонецЕсли;
	КэшЗначений.Вставить("УчетПоЯчейкам", ПолучитьФункциональнуюОпцию("УчетПоЯчейкам"));
	ОбновитьДоступностьЯчеек();
	ЗаполнитьПризнакиДоступностиЯчеек(ЭтотОбъект);
	// Конец Ячейки
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	// ЭтапыПроизводства
	ЗаполнитьПризнакиИспользованияЭтапов();
	ОбновитьЭтапыНаФорме(ЭтотОбъект);
	// Конец ЭтапыПроизводства
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
	СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	СтатистикаИспользованияФормКлиент.ПроверитьЗаписатьСтатистикуКоманды(
		"СоздатьНаОсновании.СборкаЗапасов",
		ЭтотОбъект.ВладелецФормы,
		ЭтотОбъект.ОснованиеСоздания);
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект, "СканерШтрихкода");
	// Конец ПодключаемоеОборудование
	
	ОбновитьСпискиВыбораРаспределение();
	ОбновитьПодсказкуРаспределение();
	ОбновитьОтображениеКолонокВРазрезеЗапасов();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ОценкаПроизводительностиКлиент.ЗамерВремени("Проведение"
			+ РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	КонецЕсли;
	// СтандартныеПодсистемы.ОценкаПроизводительности

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// Обсуждения
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Модифицированность",Модифицированность);
	// Конец Обсуждения
	
	// Характеристики и Партии таб. часть "РаспределениеЗапасов"
	ПроверитьЗаполнениеХарактеристикРаспределениеЗапасов(Отказ);
	
	//ВЕТИС
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПодконтрольныхТоваровВЕТИС") Тогда
		Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			Отказ = ПроверитьСоответствиеРеквизитовДокументаВЕТИС();
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение И РежимОстаткиИРезервы Тогда
		ПроверитьПоложениеСкладаПриПроведении(ТекущийОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ОповеститьОбИзменении(Объект.ДокументОснование);
	КонецЕсли;
	
	ОбновитьОтображениеКолонокВРазрезеЗапасов();
	
	// Прослеживаемость
	Для каждого СтрокаЗапасы Из Объект.Запасы Цикл
		ПрослеживаемостьФормыКлиентСерверУНФ.ЗаполнитьПредставлениеРНПТ(ЭтотОбъект, СтрокаЗапасы);
	КонецЦикла; 
	Для каждого СтрокаЗапасы Из Объект.Продукция Цикл
		ПрослеживаемостьФормыКлиентСерверУНФ.ЗаполнитьПредставлениеРНПТ(ЭтотОбъект, СтрокаЗапасы);
	КонецЦикла; 
	// Конец Прослеживаемость
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	ЗаполнитьПризнакиИспользованияЭтапов();
	ОбновитьЭтапыНаФорме(ЭтотОбъект);
	ОбновитьДоступностьЯчеек();
	ЗаполнитьПризнакиДоступностиЯчеек(ЭтотОбъект);
	
	//Обсуждения
	ОбсужденияУНФ.ПослеЗаписиНаСервере(ТекущийОбъект);	
	// Конец Обсуждения
	
	// ИнтеграцияГосИС
	СобытияФормИС.ПослеЗаписиНаСервере(ЭтаФорма);
	// Конец ИнтеграцияГосИС
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			Данные = МенеджерОборудованияУНФКлиент.ПреобразоватьДанныеСоСканераВМассив(Параметр);
			ПолученыШтрихкоды(Данные);
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	Если ИмяСобытия = "ПодборПроизведен" 
		И ЗначениеЗаполнено(Параметр) 
		// Проверка на владельца формы
		И Источник <> Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000")
		И Источник = УникальныйИдентификатор
		Тогда
		
		АдресЗапасовВХранилище	= Параметр;
		
		Если МаркерПодбора = "Продукция" Тогда
			
			ИмяТабличнойЧасти = "Продукция";
			
		ИначеЕсли МаркерПодбора = "Запасы" Тогда
			
			ИмяТабличнойЧасти = "Запасы";
			
		ИначеЕсли МаркерПодбора = "Отходы" Тогда
			
			ИмяТабличнойЧасти = "Отходы";
			
		КонецЕсли;
		
		ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, ИмяТабличнойЧасти, Истина, Истина);
		
		Если ИмяТабличнойЧасти="Продукция" Тогда
			УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
			ОбновитьСпискиВыбораРаспределение();
		ИначеЕсли ИмяТабличнойЧасти="Запасы" Тогда
			ВывестиОтметкиКонтроля(ЭтотОбъект);
		КонецЕсли; 
		
	ИначеЕсли ИмяСобытия = "ПодборСерий"
		И ЗначениеЗаполнено(Параметр) 
		// Проверка на владельца формы
		И Источник <> Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000")
		И Источник = УникальныйИдентификатор
		Тогда
		
		Если Элементы.Страницы.ТекущаяСтраница = Элементы.ТЧПродукция Тогда
			ПолучитьСерииНоменклатурыПродукцииИзХранилища(Параметр.АдресВоВременномХранилище, Параметр.КлючСтроки);
		Иначе
			ПолучитьСерииНоменклатурыЗапасыИзХранилища(Параметр.АдресВоВременномХранилище, Параметр.КлючСтроки);
		КонецЕсли;
		
		
	КонецЕсли;
	
	// КопированиеСтрокТабличныхЧастей
	Если ИмяСобытия = "БуферОбменаТабличнаяЧастьКопированиеСтрок" Тогда
		КопированиеТабличнойЧастиКлиент.ОбработкаОповещения(Элементы, "Продукция");
		КопированиеТабличнойЧастиКлиент.ОбработкаОповещения(Элементы, "Запасы");
		КопированиеТабличнойЧастиКлиент.ОбработкаОповещения(Элементы, "Отходы");
	КонецЕсли;
	
	// Обсуждения
	ОбсужденияУНФКлиент.ОбработкаОповещения(ИмяСобытия, Параметр, Источник, ЭтотОбъект, Объект.Ссылка);
	// Конец Обсуждения
	
	// ИнтеграцияГосИС
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КэшированныеЗначения", Неопределено);
	ДополнительныеПараметры.Вставить("СтандартнаяОбработка", Истина);
	ДополнительныеПараметры.Вставить("ТребуетсяСерверныйВызов", Ложь);
	СобытияФормИСКлиент.ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник, ДополнительныеПараметры);
	// Конец ИнтеграцияГосИС

КонецПроцедуры // ОбработкаОповещения()

&НаКлиенте
Процедура ПриЗакрытии()
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьИДоступностьМобильноеПриложение()
	
	// МобильноеПриложение
	Если МобильноеПриложениеВызовСервера.НужноОграничитьФункциональность() Тогда
		Элементы.ТЧОтходы.Видимость = Ложь;
		Элементы.ЛеваяКолонка.Видимость = Ложь;
		Элементы.ВидОперации.Видимость = Ложь;
		Элементы.Организация.Видимость = Ложь;
		Элементы.ГруппаСкладПродукцияСборка.Видимость = Ложь;
		Элементы.ГруппаСкладПродукцияРазборка.Видимость = Ложь;
		Элементы.ПродукцияПродукцияПодбор.Видимость = Ложь;
		Элементы.ПродукцияГруппаКопированиеСтрок.Видимость = Ложь;
		Элементы.ПродукцияХарактеристика.Видимость = Ложь;
		Элементы.ГруппаСкладЗапасыСборка.Видимость = Ложь;
		Элементы.ГруппаСкладЗапасыРазборка.Видимость = Ложь;
		Элементы.ГруппаТорговоеОборудование.Видимость = Ложь;
		Элементы.ЗапасыГруппаКопированиеСтрок.Видимость = Ложь;
		Элементы.ЗапасыПодбор.Видимость = Ложь;
		Элементы.ЗапасыЗаполнитьПоСпецификации.Видимость = Ложь;
		Элементы.ЗапасыИзменитьРезерв.Видимость = Ложь;
		Элементы.ЗапасыХарактеристика.Видимость = Ложь;
		Элементы.ФормаЗаписать.Видимость = Ложь;
		Элементы.ФормаПровести.Видимость = Ложь;
		Элементы.ФормаОтменаПроведения.Видимость = Ложь;
		Элементы.ФормаПоказатьВСписке.Видимость = Ложь;
		Элементы.ГруппаГлобальныеКоманды.Видимость = Ложь;
		Элементы.ДокументОснованиеНадпись.Видимость = Ложь;
		Элементы.ФормаПровестиИЗакрыть.Заголовок = НСтр("ru = 'Готово'");
		Элементы.ФормаШапкаТабличнаяЧасть.Видимость = Ложь;
	КонецЕсли;
	// Конец МобильноеПриложение
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	// ИнтеграцияГосИС
	СобытияФормИСКлиент.ОбработкаНавигационнойСсылки(ЭтаФорма, НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	// Конец ИнтеграцияГосИС
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ДатаПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода Организация.
// В процедуре осуществляется очистка номера документа,
// а также производится установка параметров функциональных опций формы.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	// Обработка события изменения организации.
	Объект.Номер = "";
	СтруктураДанные = ПолучитьДанныеОрганизацияПриИзменении(Объект.Организация);
	Компания = СтруктураДанные.Компания;
	
	Объект.ПодписьРуководителя = СтруктураДанные.ПодписьРуководителя;
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
	ОбновитьНастройкиФормыВыбораНоменклатурыПриИзмененииРеквизита();
	ЗаполнитьПризнакиИспользованияСерийПриИзмененииКлючевыхРеквизитов();
	
КонецПроцедуры // ОрганизацияПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода ДокументОснование.
//
&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры // ДокументОснованиеПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода ЗаказПокупателя.
//
&НаКлиенте
Процедура ЗаказПокупателяПриИзменении(Элемент)
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		СтрокаТабличнойЧасти.ЗаказПокупателя = Объект.ЗаказПокупателя;
	КонецЦикла; 
	Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
		СтрокаТабличнойЧасти.ЗаказПокупателя = Объект.ЗаказПокупателя;
	КонецЦикла;
	Если Объект.РучноеРаспределение Тогда
		Для каждого СтрокаТабличнойЧасти Из Объект.РаспределениеЗапасов Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = Объект.ЗаказПокупателя;
		КонецЦикла; 
	КонецЕсли; 
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка") Тогда
		
		Элементы.ПродукцияРезерв.Видимость = ЗначениеЗаполнено(Объект.ЗаказПокупателя);
		
		Для каждого СтрокаПродукция Из Объект.Продукция Цикл
			СтрокаПродукция.Резерв = 0;
		КонецЦикла;
		
	Иначе
		
		Элементы.ЗапасыРезерв.Видимость = ЗначениеЗаполнено(Объект.ЗаказПокупателя);
		Элементы.ЗапасыИзменитьРезерв.Видимость = ЗначениеЗаполнено(Объект.ЗаказПокупателя);
		
		Для каждого СтрокаЗапасы Из Объект.Запасы Цикл
			СтрокаЗапасы.Резерв = 0;
		КонецЦикла;
		
		ИспользуетсяРезервирование = ЗначениеЗаполнено(Объект.ЗаказПокупателя);
		
	КонецЕсли;
	
	УправлениеВидимостьюЭтапов(ЭтотОбъект);
	ОбновитьЗаголовокКомандыЗаполнитьПоОстаткам(ЭтотОбъект);
	
КонецПроцедуры // ЗаказПокупателяПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода ВидОперации.
//
&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	ВидОперацииПриИзмененииСервер();
	
	УправлениеФормой(ЭтотОбъект);
	
	ИмяТабличнойЧасти = ?(Объект.ВидОперации = КэшЗначений.ВОСборка, "Продукция", "Запасы");
	Для каждого СтрокаТабличнойЧасти Из Объект[ИмяТабличнойЧасти] Цикл
		
		СтрокаТабличнойЧасти["СтранаПроисхождения"] = Неопределено;
		СтрокаТабличнойЧасти["НомерГТД"] = Неопределено;
		СтрокаТабличнойЧасти["Резерв"] = 0;
		
	КонецЦикла;
	
	Если Объект.ВидОперации<>ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Сборка") Тогда
		
		Если РежимОстаткиИРезервы Тогда
			РежимОстаткиИРезервы = Ложь;
			Элементы.ОстаткиИРезервы.Пометка = Ложь;
			ОбновитьОтображениеКолонокВРазрезеЗапасов();
		КонецЕсли;
		
		Если КэшЗначений.ИспользоватьЭтапыПроизводства Тогда
			УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
			Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
				СтрокаТабличнойЧасти.Этапы = "";
			КонецЦикла; 
			Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
				СтрокаТабличнойЧасти.Этап = ПредопределенноеЗначение("Справочник.ЭтапыПроизводства.ПустаяСсылка");
			КонецЦикла; 
			Для каждого СтрокаТабличнойЧасти Из Объект.РаспределениеЗапасов Цикл
				СтрокаТабличнойЧасти.Этап = ПредопределенноеЗначение("Справочник.ЭтапыПроизводства.ПустаяСсылка");
			КонецЦикла; 
		КонецЕсли; 
		Объект.ВыполненныеЭтапы.Очистить();
	КонецЕсли;
	
	УстановитьВидимостьОтПользовательскихНастроек(ЭтотОбъект);
	
	ОбновитьСкладВТЧ();
	
КонецПроцедуры // ВидОперацииПриИзменении()

&НаСервере
Процедура ВидОперацииПриИзмененииСервер()
	
	Если Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка") Тогда
		ИмяТЧ = "Запасы";
	Иначе
		ИмяТЧ = "Продукция";
	КонецЕсли;
	Для каждого СтрокаПродукция Из Объект[ИмяТЧ] Цикл
		Если ЗначениеЗаполнено(СтрокаПродукция.Партия)
			И СтрокаПродукция.Партия.Статус = ПредопределенноеЗначение("Перечисление.СтатусыПартий.ДавальческоеСырье") Тогда
			СтрокаПродукция.Партия = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьПризнакиДоступностиЯчеек(ЭтотОбъект);
	
	// Прослеживаемость
	ОбновитьПризнакПрослеживаемости();
	
КонецПроцедуры

#Область СтруктурнаяЕдиницаИзготовитель

// Процедура - обработчик события ПриИзменении поля ввода СтруктурнаяЕдиница.
//
&НаКлиенте
Процедура СтруктурнаяЕдиницаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница) Тогда
	
		СтруктураДанные = Новый Структура();
		СтруктураДанные.Вставить("Подразделение", Объект.СтруктурнаяЕдиница);
		СтруктураДанные.Вставить("ВидОперации", Объект.ВидОперации);
		
		СтруктураДанные = ПолучитьДанныеСтруктурнаяЕдиницаПриИзменении(СтруктураДанные);
		
		ТипСтруктурнойЕдиницы = СтруктураДанные.ТипСтруктурнойЕдиницы;
		Объект.ПодписьКонтролера = СтруктураДанные.ПодписьКонтролера;
		
		Элементы.Ячейка.Доступность = НЕ СтруктураДанные.ОрдерныйСклад И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница);
		
		Если ЗначениеЗаполнено(СтруктураДанные.СтруктурнаяЕдиницаПродукции) Тогда
			
			Объект.СтруктурнаяЕдиницаПродукции = СтруктураДанные.СтруктурнаяЕдиницаПродукции;
			Объект.ЯчейкаПродукции = СтруктураДанные.ЯчейкаПродукции;
			Элементы.ЯчейкаПродукцииСборка.Доступность = НЕ СтруктураДанные.ОрдерныйСкладПродукции И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаПродукции);
			Элементы.ЯчейкаПродукцииРазборка.Доступность = НЕ СтруктураДанные.ОрдерныйСкладПродукции И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаПродукции);
			
		Иначе
			
			Объект.СтруктурнаяЕдиницаПродукции = Объект.СтруктурнаяЕдиница;
			Объект.ЯчейкаПродукции = Объект.Ячейка;
			Элементы.ЯчейкаПродукцииСборка.Доступность = НЕ СтруктураДанные.ОрдерныйСклад И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница);
			Элементы.ЯчейкаПродукцииРазборка.Доступность = НЕ СтруктураДанные.ОрдерныйСклад И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтруктураДанные.СтруктурнаяЕдиницаЗапасов) Тогда
			
			СтруктурнаяЕдиницаЗапасов = СтруктураДанные.СтруктурнаяЕдиницаЗапасов;
			ЯчейкаЗапасов = СтруктураДанные.ЯчейкаЗапасов;
			Элементы.ЯчейкаЗапасовСборка.Доступность = НЕ СтруктураДанные.ОрдерныйСкладЗапасов И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаЗапасов);
			Элементы.ЯчейкаЗапасовРазборка.Доступность = НЕ СтруктураДанные.ОрдерныйСкладЗапасов И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаЗапасов);
			
		Иначе
			
			СтруктурнаяЕдиницаЗапасов = Объект.СтруктурнаяЕдиница;
			ЯчейкаЗапасов = Объект.Ячейка;
			Элементы.ЯчейкаЗапасовСборка.Доступность = НЕ СтруктураДанные.ОрдерныйСклад И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница);
			Элементы.ЯчейкаЗапасовРазборка.Доступность = НЕ СтруктураДанные.ОрдерныйСклад И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница);
			
		КонецЕсли;
		
		Объект.СтруктурнаяЕдиницаЗапасов = СтруктурнаяЕдиницаЗапасов;
		Объект.ЯчейкаЗапасов = ЯчейкаЗапасов;
		ОбновитьСкладВТЧ(); 
		
		Если ЗначениеЗаполнено(СтруктураДанные.СтруктурнаяЕдиницаОтходов) Тогда
			
			Объект.СтруктурнаяЕдиницаОтходов = СтруктураДанные.СтруктурнаяЕдиницаОтходов;
			Объект.ЯчейкаОтходов = СтруктураДанные.ЯчейкаОтходов;
			Элементы.ЯчейкаОтходов.Доступность = НЕ СтруктураДанные.ОрдерныйСкладОтходов И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаОтходов);
			
		Иначе
			
			Объект.СтруктурнаяЕдиницаОтходов = Объект.СтруктурнаяЕдиница;
			Объект.ЯчейкаОтходов = Объект.Ячейка;
			Элементы.ЯчейкаОтходов.Доступность = НЕ СтруктураДанные.ОрдерныйСклад И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница);
			
		КонецЕсли;
		
		Объект.ПодписьКладовщикаОтправил = ?(СтруктураДанные.ЭтоСборка, СтруктураДанные.СтруктурнаяЕдиницаЗапасовПодписьМОЛ, СтруктураДанные.СтруктурнаяЕдиницаПродукцииПодписьМОЛ);
		Объект.ПодписьКладовщикаПолучил = ?(СтруктураДанные.ЭтоСборка, СтруктураДанные.СтруктурнаяЕдиницаПродукцииПодписьМОЛ, СтруктураДанные.СтруктурнаяЕдиницаЗапасовПодписьМОЛ);
		
		// Этапы
		Если ТипСтруктурнойЕдиницы = ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение") 
			И НЕ КэшЗначений.ВыполнениеЭтаповРазнымиПодразделениями Тогда 
			Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
				СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = Объект.СтруктурнаяЕдиница;
			КонецЦикла;
		КонецЕсли; 
	
	Иначе
		
		Элементы.Ячейка.Доступность = Ложь;
		
		Объект.ПодписьКонтролера = Неопределено;
		Объект.ПодписьКладовщикаОтправил = Неопределено;
		Объект.ПодписьКладовщикаПолучил = Неопределено;
		ТипСтруктурнойЕдиницы = ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.ПустаяСсылка");
		
	КонецЕсли;
	
	УправлениеВидимостьюЭтапов(ЭтотОбъект);
	ЗаполнитьПризнакиИспользованияСерийПриИзмененииКлючевыхРеквизитов();
	
КонецПроцедуры // СтруктурнаяЕдиницаПриИзменении()

// Процедура - обработчик события Открытие поля СтруктурнаяЕдиница.
//
&НаКлиенте
Процедура СтруктурнаяЕдиницаОткрытие(Элемент, СтандартнаяОбработка)
	
	Если Элементы.СтруктурнаяЕдиница.РежимВыбораИзСписка
		И НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница) Тогда
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // СтруктурнаяЕдиницаОткрытие()

// Процедура - обработчик события ПриИзменении поля ввода Ячейка.
//
&НаКлиенте
Процедура ЯчейкаПриИзменении(Элемент)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиница);
	СтруктураДанные.Вставить("Ячейка", Объект.Ячейка);
	СтруктураДанные.Вставить("СтруктурнаяЕдиницаПродукции", Объект.СтруктурнаяЕдиницаПродукции);
	СтруктураДанные.Вставить("ЯчейкаПродукции", Объект.ЯчейкаПродукции);
	СтруктураДанные.Вставить("СтруктурнаяЕдиницаЗапасов", Объект.СтруктурнаяЕдиницаЗапасов);
	СтруктураДанные.Вставить("ЯчейкаЗапасов", Объект.ЯчейкаЗапасов);
	СтруктураДанные.Вставить("СтруктурнаяЕдиницаОтходов", Объект.СтруктурнаяЕдиницаОтходов);
	СтруктураДанные.Вставить("ЯчейкаОтходов", Объект.ЯчейкаОтходов);
	
	СтруктураДанные = ПолучитьДанныеЯчейкаПриИзменении(СтруктураДанные);
	
	Если СтруктураДанные.Свойство("НоваяЯчейкаПродукции") Тогда
		Объект.ЯчейкаПродукции = СтруктураДанные.НоваяЯчейкаПродукции;
	КонецЕсли;
	
	Если СтруктураДанные.Свойство("НоваяЯчейкаЗапасов") Тогда
		Объект.ЯчейкаЗапасов = СтруктураДанные.НоваяЯчейкаЗапасов;
	КонецЕсли;
	
	Если СтруктураДанные.Свойство("НоваяЯчейкаОтходов") Тогда
		Объект.ЯчейкаОтходов = СтруктураДанные.НоваяЯчейкаОтходов;
	КонецЕсли;
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли
	
КонецПроцедуры // ЯчейкаПриИзменении()

#КонецОбласти

&НаКлиенте
Процедура РучноеРаспределениеПриИзменении(Элемент)
	
	Объект.РучноеРаспределение = РучноеРаспределение;
	Модифицированность = Истина;
	
	УправлениеФормой(ЭтотОбъект);
	Если Объект.РучноеРаспределение Тогда
		РаспределитьФрагмент();
		ОбновитьСпискиВыбораРаспределение();
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовПодвалаФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
		
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "удалить" Тогда
		Объект.ДокументОснование = Неопределено;
		Элементы.ДокументОснованиеНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(Неопределено);
		Модифицированность = Истина;
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "заполнить" Тогда
		ЗаполнитьПоОснованиюНачало();		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "выбрать" Тогда
		
		СписокВыбора = Новый СписокЗначений;
		Если НЕ ФОРезервированиеЗапасов Тогда
			СписокВыбора.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"), НСтр("ru = 'Заказ покупателя'"));
		КонецЕсли;
		СписокВыбора.Добавить(Тип("ДокументСсылка.СборкаЗапасов"), НСтр("ru = 'Производство'"));
		Если ФОРозница Тогда
			СписокВыбора.Добавить(Тип("ДокументСсылка.ОтчетОРозничныхПродажах"), НСтр("ru = 'Отчет о розничных продажах'"));
		КонецЕсли;
		
		Если СписокВыбора.Количество()=1 Тогда
			ДокументОснованиеВыборТипаДокумента(СписокВыбора[0]);
		Иначе
			Оповещение = Новый ОписаниеОповещения("ДокументОснованиеВыборТипаДокумента", ЭтотОбъект);
			ПоказатьВыборИзМеню(Оповещение, СписокВыбора, Элемент);
		КонецЕсли; 
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "открыть" Тогда
		
		РаботаСФормойДокументаКлиент.ОткрытьФормуДокументаПоСсылке(Объект.ДокументОснование);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеВыборТипаДокумента(ВыбранноеЗначение, ДополнительныеДанные = Неопределено) Экспорт
	
	Если ВыбранноеЗначение=Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	// Выбрать основание
	Если ВыбранноеЗначение.Значение=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		СтруктураПараметровОтбора = Новый Структура();
		_ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыбратьОснованиеЗавершение", ЭтотОбъект);
		ОткрытьФорму("Документ.ЗаказПокупателя.ФормаВыбора", СтруктураПараметровОтбора, ЭтотОбъект, ,,,_ОповещениеОЗакрытии);
	ИначеЕсли ВыбранноеЗначение.Значение=Тип("ДокументСсылка.СборкаЗапасов") Тогда
		СтруктураПараметровОтбора = Новый Структура();
		_ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыбратьОснованиеЗавершение", ЭтотОбъект);
		ОткрытьФорму("Документ.СборкаЗапасов.ФормаВыбора", СтруктураПараметровОтбора, ЭтотОбъект, ,,,_ОповещениеОЗакрытии);
	ИначеЕсли ВыбранноеЗначение.Значение=Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		СтруктураПараметровОтбора = Новый Структура();
		_ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыбратьОснованиеЗавершение", ЭтотОбъект);
		ОткрытьФорму("Документ.ОтчетОРозничныхПродажах.ФормаВыбора", СтруктураПараметровОтбора, ЭтотОбъект, ,,,_ОповещениеОЗакрытии);
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОснованиеЗавершение(ВыбранноеЗначение, Параметры) Экспорт

	Если ВыбранноеЗначение <> Неопределено Тогда
		Если Объект.ДокументОснование <> ВыбранноеЗначение
			И (ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.СборкаЗапасов")
			ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах")) Тогда
			Объект.ЗаказНаПроизводство = ПредопределенноеЗначение("Документ.ЗаказНаПроизводство.ПустаяСсылка");
		КонецЕсли; 
		Объект.ДокументОснование = ВыбранноеЗначение;
		Элементы.ДокументОснованиеНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(ВыбранноеЗначение);
		Модифицированность = Истина;
		
		ЗаполнитьПоОснованиюНачало();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОснованиюНачало() Экспорт

	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПоДокументОснованиеЗавершение", ЭтотОбъект);
	ПоказатьВопрос(
		ОписаниеОповещения, 
		НСтр("ru = 'Заполнить документ по выбранному основанию?'"), 
		РежимДиалогаВопрос.ДаНет, 0);		

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДокументОснованиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Ответ = Результат;
    Если Ответ = КодВозвратаДиалога.Да Тогда
        ЗаполнитьПоДокументу("ДокументОснование");
		ОбновитьСпискиВыбораРаспределение();
    КонецЕсли;

КонецПроцедуры // ЗаполнитьПоДокументОснование()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПродукция

&НаКлиенте
Процедура ПродукцияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТабличнойЧасти = Объект.Продукция.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	// Прослеживаемость
	Если Поле = Элементы.ПродукцияРНПТ И СтрокаТабличнойЧасти.ПрослеживаемыйТовар Тогда
		ИмяТабличнойЧасти = "Продукция";
		ПрослеживаемостьФормыКлиентУНФ.ОткрытьФормуПодбораРНПТ(ЭтаФорма, "Продукция", Ложь);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	// Конец Прослеживаемость
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПриАктивизацииЯчейки(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти = Неопределено Тогда 
		Возврат 
	КонецЕсли;
	
	// Прослеживаемость
	Если ЭтоНоваяСтрокаПродукция
		И Элементы.Продукция.ТекущийЭлемент = Элементы.ПродукцияРНПТ 
		И Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.РНПТ) Тогда
		ИмяТабличнойЧасти = "Продукция";
		ПрослеживаемостьФормыКлиентУНФ.ОткрытьФормуПодбораРНПТ(ЭтаФорма, "Продукция", Ложь);
	КонецЕсли;
	// Конец Прослеживаемость
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущаяСтрока = Элемент.ТекущиеДанные;
	
	Если НоваяСтрока И Копирование Тогда
		ТекущаяСтрока.КлючСвязи = 0;
		ТекущаяСтрока.СерииНоменклатуры = "";
		ТекущаяСтрока.ИдентификаторСтроки = "";
		ТекущаяСтрока.РНПТ = "";
		СерииНоменклатурыУНФКлиентСервер.ОбновитьСтатусСерииВСтроке(Объект, Элемент.ТекущиеДанные, "СерииНоменклатурыПродукция");
	КонецЕсли;	
	
	Если Элемент.ТекущийЭлемент.Имя = "ПродукцияСерииНоменклатуры" Тогда
		ОткрытьПодборСерииНоменклатуры("Продукция","СерииНоменклатурыПродукция");
	КонецЕсли;
	
	ИмяТабличнойЧасти = "Продукция";
	
	Если НоваяСтрока ИЛИ ТекущаяСтрока.КлючСвязи=0 Тогда
		ТабличныеЧастиУНФКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект);
	КонецЕсли;
	
	Если НоваяСтрока И НЕ Копирование Тогда
		ЗаполнитьДанныеШапки(Объект, ТекущаяСтрока, "Продукция"); 
	КонецЕсли; 
	
	СтароеКоличествоПродукции = ТекущаяСтрока.Количество;
	
	Если НоваяСтрока Тогда
		ТекущаяСтрока.Этапы = "";
	КонецЕсли; 
	
	ЭтоНоваяСтрокаПродукция = НоваяСтрока;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ЭтоНоваяСтрокаПродукция = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПередУдалением(Элемент, Отказ)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Для каждого ВыделеннаяСтрока Из Элементы.Продукция.ВыделенныеСтроки Цикл
		ТекущиеДанныеСтроки = Элементы.Продукция.ДанныеСтроки(ВыделеннаяСтрока);
		СерииНоменклатурыУНФКлиентСервер.УдалитьСерииНоменклатурыПоКлючуСвязи(Объект.СерииНоменклатурыПродукция, ТекущиеДанныеСтроки,
			ИспользоватьСерииНоменклатурыОстатки);
	КонецЦикла;
	
	ЕстьСпецификации = Ложь;
	Для каждого ВыделеннаяСтрока Из Элемент.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элемент.ДанныеСтроки(ВыделеннаяСтрока);
		Если ЗначениеЗаполнено(ДанныеСтроки.Спецификация) Тогда
			ЕстьСпецификации = Истина;
		КонецЕсли;
		Если Объект.РучноеРаспределение Тогда
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("КлючСвязиПродукция", ДанныеСтроки.КлючСвязи);
			Строки = Объект.РаспределениеЗапасов.НайтиСтроки(СтруктураОтбора);
			Если Строки.Количество() > 0 Тогда
				ЗапасыНеРаспределены = Истина;
			КонецЕсли;
			Для каждого СтрокаРаспределения Из Строки Цикл
				СтрокаРаспределения.КлючСвязиПродукция = 0;
			КонецЦикла; 
		КонецЕсли;
		Если ДанныеСтроки.ИспользоватьЭтапыПроизводства Тогда
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("КлючСвязи", ДанныеСтроки.КлючСвязи);
			Строки = Объект.ВыполненныеЭтапы.НайтиСтроки(СтруктураОтбора);
			Для каждого СтрокаЭтапа Из Строки Цикл
				Объект.ВыполненныеЭтапы.Удалить(СтрокаЭтапа);
			КонецЦикла;
		КонецЕсли;
		// Прослеживаемость
		УдалитьСвязанныеЗаписи(ДанныеСтроки);
	КонецЦикла;
	
	Если Объект.Запасы.Количество() > 0 И ЕстьСпецификации Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПослеУдаления(Элемент)
	
	ОбновитьСпискиВыбораРаспределение();
	ОбновитьЗаголовокКомандыЗаполнитьПоОстаткам(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияНоменклатураПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
	СтруктураДанные.Вставить("Партия", СтрокаТабличнойЧасти.Партия);
	
	СтатусПартии = Новый СписокЗначений;
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
	
	СтруктураДанные.Вставить("СтатусПартии", СтатусПартии);
	
	СерииНоменклатурыУНФКлиентСервер.ДополнитьСтруктуруДаннымиДляПолученияСерийНоменклатуры(Объект, СтруктураДанные);
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
	
	Если Объект.ВидОперации = КэшЗначений.ВОРазборка Тогда
		
		СтрокаТабличнойЧасти.СтранаПроисхождения = СтруктураДанные.СтранаПроисхождения;
		СтрокаТабличнойЧасти.НомерГТД = Неопределено;
		
	КонецЕсли;
	
	СтрокаТабличнойЧасти.Количество = 1;
	СтрокаТабличнойЧасти.Спецификация = СтруктураДанные.Спецификация;
	СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства = СтруктураДанные.ИспользоватьЭтапыПроизводства;
	Если СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства Тогда
		СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = Объект.СтруктурнаяЕдиница;
	Иначе
		СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка");
	КонецЕсли; 
	
	Если Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка") Тогда
		Если НЕ СкладВШапке Тогда
			ЗаполнениеОбъектовУНФКлиент.ЗаполнитьСкладВСтрокеТЧЗапасы(СтрокаТабличнойЧасти, Объект, СтруктураДанные);
		Иначе
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаЗапасов;
			СтрокаТабличнойЧасти.Ячейка = Объект.ЯчейкаЗапасов;
		КонецЕсли;
		СтрокаТабличнойЧасти.ЯчейкаДоступна = ЯчейкаДоступна(СтрокаТабличнойЧасти.СтруктурнаяЕдиница);
	КонецЕсли; 
	
	// Серии номенклатуры
	СерииНоменклатурыУНФКлиентСервер.УдалитьСерииНоменклатурыПоКлючуСвязи(Объект.СерииНоменклатурыПродукция, СтрокаТабличнойЧасти,
		ИспользоватьСерииНоменклатурыОстатки);
	
	СтрокаТабличнойЧасти.ИспользоватьХарактеристики = СтруктураДанные.ИспользоватьХарактеристики;
	СтрокаТабличнойЧасти.ПроверятьЗаполнениеХарактеристики = СтруктураДанные.ПроверятьЗаполнениеХарактеристики;
	СтрокаТабличнойЧасти.ЗаполнениеХарактеристикиПроверено = Истина;
	
	Если СтруктураДанные.ИспользоватьХарактеристики Тогда
		Если ПодборНоменклатурыИзСписка И ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтрокаТабличнойЧасти.Характеристика = СтруктураВыбораНоменклатуры.Характеристика;
			СтруктураВыбораНоменклатуры.Характеристика = Неопределено;
		Иначе
			СтрокаТабличнойЧасти.Характеристика = СтруктураДанные.Характеристика;
		КонецЕсли;
	КонецЕсли;
	
	//Партии
	СтрокаТабличнойЧасти.ИспользоватьПартии = СтруктураДанные.ИспользоватьПартии;
	СтрокаТабличнойЧасти.ПроверятьЗаполнениеПартий = СтруктураДанные.ПроверятьЗаполнениеПартий;
	
	Если СтруктураДанные.ИспользоватьПартии Тогда
		Если ПодборНоменклатурыИзСписка И ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтрокаТабличнойЧасти.Партия = СтруктураВыбораНоменклатуры.Партия;
			СтруктураВыбораНоменклатуры.Партия = Неопределено;
		Иначе
			СтрокаТабличнойЧасти.Партия = СтруктураДанные.Партия;
		КонецЕсли;
	КонецЕсли;
	// Конец Партии
	
	СтрокаТабличнойЧасти.СтатусыСерийНоменклатуры = СтруктураДанные.СтатусыСерийНоменклатуры;
	
	// Прослеживаемость
	Если КэшЗначений.ВестиУчетПрослеживаемыхТоваров И Объект.ВидОперации = КэшЗначений.ВОРазборка Тогда
		СтрокаТабличнойЧасти.ПрослеживаемыйТовар = СтруктураДанные.ПрослеживаемыйТовар;
	Иначе
		СтрокаТабличнойЧасти.ПрослеживаемыйТовар = Ложь;
	КонецЕсли; 
	// Конец Прослеживаемость
		
	ПодборНоменклатурыИзСписка = Ложь;
	
	// Прослеживаемость
	УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти);
	// Конец Прослеживаемость
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) И Объект.Запасы.Количество()>0 Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли; 
	ОбновитьСпискиВыбораРаспределение();
	
	ОчиститьВыполненныеЭтапы(СтрокаТабличнойЧасти);
	УправлениеВидимостьюЭтапов(ЭтотОбъект);

КонецПроцедуры // ПродукцияНоменклатураПриИзменении()

&НаКлиенте
Процедура ПродукцияНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыбранноеЗначение);

		ПодборНоменклатурыИзСписка = ВыбранноеЗначение.Свойство("Ячейка");
		
		Если НЕ ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
		КонецЕсли;
		
		СтруктураВыбораНоменклатуры.Характеристика = ВыбранноеЗначение.Характеристика;
		СтруктураВыбораНоменклатуры.Партия = ВыбранноеЗначение.Партия;
		
		Если ВыбранноеЗначение.Свойство("СтруктураНастроек") Тогда
			НоменклатураВДокументахКлиентСервер.ЗаполнитьТаблицуНастроекФормыВыбораНоменклатуры(ЭтаФорма, "СборкаЗапасовОприходование", НастройкиФормыВыбораНоменклатуры, Истина, "Продукция", ВыбранноеЗначение.СтруктураНастроек);
			НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(ЭтаФорма, "Продукция");
		КонецЕсли;
		ВыбранноеЗначение = ВыбранноеЗначение.Номенклатура;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияХарактеристикаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
	
	СтруктураДанные = ПолучитьДанныеХарактеристикаПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.Спецификация = СтруктураДанные.Спецификация;
	СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства = СтруктураДанные.ИспользоватьЭтапыПроизводства;
	Если СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства Тогда
		СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = Объект.СтруктурнаяЕдиница;
	Иначе
		СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка");
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) И Объект.Запасы.Количество()>0 Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли; 
	ОбновитьСпискиВыбораРаспределение();
	
	ОчиститьВыполненныеЭтапы(СтрокаТабличнойЧасти);
	УправлениеВидимостьюЭтапов(ЭтотОбъект);
	
	// Прослеживаемость
	УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти);
	// Конец Прослеживаемость
	
КонецПроцедуры // ПродукцияХарактеристикаПриИзменении()

&НаКлиенте
Процедура ПродукцияПартияПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	
	// Прослеживаемость
	УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти);
	// Конец Прослеживаемость
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияКоличествоПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	
	// Серии номенклатуры
	Если ИспользоватьСерииНоменклатурыОстатки <> Неопределено Тогда
		СерииНоменклатурыУНФКлиентСервер.ОбновитьСерииНоменклатурыКоличество(Объект, СтрокаТабличнойЧасти, "СерииНоменклатурыПродукция");
	КонецЕсли;
		
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли; 
	
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		Возврат
	КонецЕсли;
	НоменклатураВДокументахКлиент.КонтрольЗначенияРезерваВТекущейСтроке(СтрокаТабличнойЧасти);
	
	СерииНоменклатурыУНФКлиентСервер.ОбновитьСтатусСерииВСтроке(Объект, СтрокаТабличнойЧасти, "СерииНоменклатурыПродукция");
	
	// Прослеживаемость
	УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти);
	// Конец Прослеживаемость
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияНомерГТДПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	Если СтрокаТабличнойЧасти <> Неопределено
		И ЗначениеЗаполнено(СтрокаТабличнойЧасти.НомерГТД) Тогда
		
		ДатаГТД = ГрузовыеТаможенныеДекларацииКлиент.ВыделитьДатуИзНомераГТД(Строка(СтрокаТабличнойЧасти.НомерГТД));
		Если ДатаГТД > КонецДня(Объект.Дата) Тогда
			
			ТекстСообщения = НСтр("ru ='Внимание: выбранная ГТД датирована более поздней датой, чем текущий документ.'");
			КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.Продукция",
				СтрокаТабличнойЧасти.НомерСтроки, "НомерГТД");
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , КонтекстноеПоле);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияСтранаПроисхожденияПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	
	Если КэшЗначений.УчетГТД Тогда
		
		СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтранаПроисхождения)
			ИЛИ СтрокаТабличнойЧасти.СтранаПроисхождения = КэшЗначений.Россия Тогда
			
			СтрокаТабличнойЧасти.НомерГТД = Неопределено;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Прослеживаемость
	УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти);
	// Конец Прослеживаемость
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияРНПТНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИмяТабличнойЧасти = "Продукция";
	ПрослеживаемостьФормыКлиентУНФ.ОткрытьФормуПодбораРНПТ(ЭтаФорма, "Продукция", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияРНПТОчистка(Элемент, СтандартнаяОбработка)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти <> Неопределено Тогда
		
		Если СтрокаТабличнойЧасти.ПрослеживаемыйТовар Тогда
			УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти);
		Иначе
			СтрокаТабличнойЧасти.НомерГТД = Неопределено;
		КонецЕсли;
		
		ПрослеживаемостьФормыКлиентСерверУНФ.ЗаполнитьПредставлениеРНПТ(ЭтаФорма, СтрокаТабличнойЧасти);
		
	КонецЕсли;
	
КонецПроцедуры

#Область СтруктурнаяЕдиницаПродукцияПолучательСборка

// Процедура - обработчик события ПриИзменении поля ввода СтруктурнаяЕдиницаПродукцииСборка.
//
&НаКлиенте
Процедура СтруктурнаяЕдиницаПродукцииСборкаПриИзменении(Элемент)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Организация",	Объект.Организация);
	СтруктураДанные.Вставить("Склад",		Объект.СтруктурнаяЕдиницаПродукции);
	
	СтруктураДанные = ПолучитьДанныеСтруктурнаяЕдиницаПродукцияЗапасыОтходыПриИзменении(СтруктураДанные);
	Объект.ПодписьКладовщикаПолучил = СтруктураДанные.ПодписьМОЛ;
	
	Элементы.ЯчейкаПродукцииСборка.Доступность = (НЕ СтруктураДанные.ОрдерныйСклад И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаПродукции));
	
КонецПроцедуры // СтруктурнаяЕдиницаПродукцииСборкаПриИзменении()

// Процедура - обработчик события Открытие поля СтруктурнаяЕдиницаПродукцииСборка.
//
&НаКлиенте
Процедура СтруктурнаяЕдиницаПродукцииСборкаОткрытие(Элемент, СтандартнаяОбработка)
	
	Если Элементы.СтруктурнаяЕдиницаПродукцииСборка.РежимВыбораИзСписка
		И НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаПродукции) Тогда
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // СтруктурнаяЕдиницаПродукцииСборкаОткрытие()

#КонецОбласти

#Область СтруктурнаяЕдиницаПродукцияСписатьИзРазборка

// Процедура - обработчик события ПриИзменении поля ввода СтруктурнаяЕдиницаПродукцииРазборка.
//
&НаКлиенте
Процедура СтруктурнаяЕдиницаПродукцииРазборкаПриИзменении(Элемент)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Организация", Объект.Организация);
	СтруктураДанные.Вставить("Склад",		Объект.СтруктурнаяЕдиницаЗапасов);
	
	СтруктураДанные = ПолучитьДанныеСтруктурнаяЕдиницаПродукцияЗапасыОтходыПриИзменении(СтруктураДанные);
	Объект.ПодписьКладовщикаОтправил = СтруктураДанные.ПодписьМОЛ;
	
	Элементы.ЯчейкаПродукцииРазборка.Доступность = (НЕ СтруктураДанные.ОрдерныйСклад И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаЗапасов));
	
	ОбновитьСкладВТЧ();
	
КонецПроцедуры // СтруктурнаяЕдиницаПродукцииРазборкаПриИзменении()

// Процедура - обработчик события Открытие поля СтруктурнаяЕдиницаПродукцииРазборка.
//
&НаКлиенте
Процедура СтруктурнаяЕдиницаПродукцииРазборкаОткрытие(Элемент, СтандартнаяОбработка)
	
	Если Элементы.СтруктурнаяЕдиницаПродукцииРазборка.РежимВыбораИзСписка
		И НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаЗапасов) Тогда
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // СтруктурнаяЕдиницаПродукцииРазборкаОткрытие()

#КонецОбласти

&НаКлиенте
Процедура ПродукцияЗаказПокупателяПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя) Тогда
		СтрокаТабличнойЧасти.Резерв = 0;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) И Объект.Запасы.Количество()>0 Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли; 
	ОбновитьСпискиВыбораРаспределение();
	
	Если КэшЗначений.ИспользоватьЭтапыПроизводства 
		И НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя)
		И НЕ ЗначениеЗаполнено(Объект.ЗаказНаПроизводство) Тогда
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСвязи", СтрокаТабличнойЧасти.КлючСвязи);
		СтрокиВыполненныеЭтапы = Объект.ВыполненныеЭтапы.НайтиСтроки(СтруктураОтбора);
		Если СтрокиВыполненныеЭтапы.Количество()>0 Тогда
			Для каждого СтрокаВыполненныйЭтап Из СтрокиВыполненныеЭтапы Цикл
				Объект.ВыполненныеЭтапы.Удалить(СтрокаВыполненныйЭтап);
			КонецЦикла; 
			УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
			ОбновитьЭтапыНаФорме(ЭтотОбъект);
		КонецЕсли; 
	КонецЕсли; 
	
	УправлениеВидимостьюЭтапов(ЭтотОбъект);
	ОбновитьЗаголовокКомандыЗаполнитьПоОстаткам(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияЕдиницаИзмеренияПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли;
	
	Если СтрокаТабличнойЧасти = Неопределено Тогда 
		Возврат 
	КонецЕсли;
	
	СерииНоменклатурыУНФКлиентСервер.ОбновитьСтатусСерииВСтроке(Объект, СтрокаТабличнойЧасти, "СерииНоменклатурыПродукция");
	
	// Прослеживаемость
	УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти);
	// Конец Прослеживаемость
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияСпецификацияПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Спецификация", СтрокаТабличнойЧасти.Спецификация);
	СтруктураДанные = ПолучитьДанныеСпецификацияПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства = СтруктураДанные.ИспользоватьЭтапыПроизводства;
	Если СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства Тогда
		СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = Объект.СтруктурнаяЕдиница;
	Иначе
		СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка");
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) И Объект.Запасы.Количество()>0 Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли; 
	ОбновитьСпискиВыбораРаспределение();
	
	ОчиститьВыполненныеЭтапы(СтрокаТабличнойЧасти);
	УправлениеВидимостьюЭтапов(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПродукцияЭтапыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущаяСтрока = Элементы.Продукция.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.Спецификация)
		ИЛИ (НЕ ЗначениеЗаполнено(Объект.ЗаказНаПроизводство) И НЕ ПроизводствоПодЗаказ(ТекущаяСтрока)) Тогда
		ТекстСообщения = НСтр("ru = 'При изготовлении %1 не используются этапы производства.'");
	    ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(ТекстСообщения, ТекущаяСтрока.Номенклатура));
		Возврат;
	КонецЕсли;
	
	КлючСвязи = ТекущаяСтрока.КлючСвязи;
	Если КлючСвязи=0 Тогда
		ИмяТабличнойЧасти = "Продукция";
		ТабличныеЧастиУНФКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект, ТекущаяСтрока);
		КлючСвязи = ТекущаяСтрока.КлючСвязи;
	КонецЕсли; 
	
	МассивЭтапов = Новый Массив;
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("КлючСвязи", КлючСвязи);
	Строки = Объект.ВыполненныеЭтапы.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаТабличнойЧасти Из Строки Цикл
		МассивЭтапов.Добавить(СтрокаТабличнойЧасти.Этап);
	КонецЦикла;
	
	СтруктураОткрытия = Новый Структура;
	СтруктураОткрытия.Вставить("КлючСвязи", КлючСвязи);
	СтруктураОткрытия.Вставить("ВыполненныеЭтапы", МассивЭтапов);
	СтруктураОткрытия.Вставить("Спецификация", ТекущаяСтрока.Спецификация);
	ОткрытьФорму("Документ.СборкаЗапасов.Форма.ВыполненныеЭтапы", СтруктураОткрытия, Элемент, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияЭтапыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение)<>Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ВыбранноеЗначение.Свойство("КлючСвязи")
		ИЛИ НЕ ВыбранноеЗначение.Свойство("ВыполненныеЭтапы") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("КлючСвязи", ВыбранноеЗначение.КлючСвязи);
	Строки = Объект.ВыполненныеЭтапы.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаТабличнойЧасти Из Строки Цикл
		Объект.ВыполненныеЭтапы.Удалить(СтрокаТабличнойЧасти);
	КонецЦикла; 
	
	Для каждого Этап Из ВыбранноеЗначение.ВыполненныеЭтапы Цикл
		НоваяСтрока = Объект.ВыполненныеЭтапы.Добавить();
		НоваяСтрока.КлючСвязи = ВыбранноеЗначение.КлючСвязи;
		НоваяСтрока.Этап = Этап;
	КонецЦикла;
	
	ОбновитьЭтапыНаФорме(ЭтотОбъект);
	УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияСтруктурнаяЕдиницаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Продукция.ТекущиеДанные;
	СтрокаТабличнойЧасти.ЯчейкаДоступна = ЯчейкаДоступна(СтрокаТабличнойЧасти.СтруктурнаяЕдиница);
	ОбновитьСтатусыСерийНоменклатурыВТабличнойЧасти(СтрокаТабличнойЧасти.НомерСтроки, "Продукция", "СерииНоменклатурыПродукция");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗапасы

&НаКлиенте
Процедура ЗапасыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущаяСтрока = Элемент.ТекущиеДанные;
	
	Если НоваяСтрока И Копирование Тогда
		ТекущаяСтрока.КлючСвязи = 0;
		ТекущаяСтрока.СерииНоменклатуры = "";
		ТекущаяСтрока.ИдентификаторСтроки = "";
		ТекущаяСтрока.РНПТ = "";
		СерииНоменклатурыУНФКлиентСервер.ОбновитьСтатусСерииВСтроке(Объект, Элемент.ТекущиеДанные);
	КонецЕсли;	
	
	Если Элемент.ТекущийЭлемент.Имя = "ЗапасыСерииНоменклатуры" Тогда
		ОткрытьПодборСерииНоменклатуры("Запасы", "СерииНоменклатуры");
	КонецЕсли;
	
	ИмяТабличнойЧасти = "Запасы";
	
	Если НоваяСтрока ИЛИ ТекущаяСтрока.КлючСвязи=0 Тогда
		ТабличныеЧастиУНФКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект);
	КонецЕсли; 
	
	Если НоваяСтрока Тогда
		СтарыеДанныеСтроки = Неопределено;
		ЗаполнитьДанныеШапки(Объект, ТекущаяСтрока);
	Иначе
		СтруктураПолей = Новый Структура(ИменаКолонокЗапасов(Объект));
		ЗаполнитьЗначенияСвойств(СтруктураПолей, ТекущаяСтрока);
		СтарыеДанныеСтроки = Новый ФиксированнаяСтруктура(СтруктураПолей);
	КонецЕсли;
	
	ЭтоНоваяСтрокаПродукция = НоваяСтрока;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьКэшКонтроляПриИзмененииДанныхСтроки(Элемент.ТекущиеДанные);
	Если НЕ ОтменаРедактирования Тогда
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
	ЭтоНоваяСтрокаПродукция = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	// Запасы и Остатки
	ТекущиеДанные = Элементы.Запасы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда Возврат КонецЕсли;
	
	Номенклатура = ТекущиеДанные.Номенклатура;
	Характеристика = ТекущиеДанные.Характеристика;
	Партия = ТекущиеДанные.Партия;
	Склад = ?(ЗначениеЗаполнено(ТекущиеДанные.СтруктурнаяЕдиница), ТекущиеДанные.СтруктурнаяЕдиница, Объект.СтруктурнаяЕдиница);
	
	СтруктураПараметров = Новый Структура("ОрганизацияОбъекта, Номенклатура, Характеристика, Партия, Склад, ЕдиницаИзмерения, ЗаказПокупателя, НомерВариантаКП");
	
	СтруктураПараметров.Номенклатура = Номенклатура;
	СтруктураПараметров.Характеристика = Характеристика;
	СтруктураПараметров.Партия = Партия;
	СтруктураПараметров.Склад = Склад;
	СтруктураПараметров.ЕдиницаИзмерения = ТекущиеДанные.ЕдиницаИзмерения;
	СтруктураПараметров.ЗаказПокупателя = ?(ЗначениеЗаполнено(ТекущиеДанные.ЗаказПокупателя), ТекущиеДанные.ЗаказПокупателя, Объект.ЗаказПокупателя);
	СтруктураПараметров.НомерВариантаКП = 0;
	СтруктураПараметров.ОрганизацияОбъекта = Объект.Организация;
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		
		Если Элемент.ТекущийЭлемент = Элементы.ЗапасыОстатокОбщий 
			Или Элемент.ТекущийЭлемент = Элементы.ЗапасыСтруктурнаяЕдиницаРезерв Тогда
			
			ЗначениеПоля = ТекущиеДанные.ОстатокОбщий;
			
			СписокВыбораСклад = Элементы.ЗапасыСтруктурнаяЕдиницаРезерв.СписокВыбора;
			СписокВыбораСклад.Очистить();
			
			Если ЗначениеЗаполнено(ЗначениеПоля) Тогда
				
				ПараметрыОповещения = Новый Структура("ВыбранноеПоле", "ЗапасыОстатокОбщий");
				Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзВыпадающегоСпискаТабличнойЧасти", ЭтаФорма, ПараметрыОповещения);
				
				СписокДокументов = СписокДокументовПоЗаказу("ОбщиеЗапасы", СтруктураПараметров);
				
				Для Каждого ЭлементСписка Из СписокДокументов Цикл
					СписокВыбораСклад.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление,, ЭлементСписка.Картинка);
				КонецЦикла;
				
				Если СписокДокументов.Количество() Тогда
					СписокДокументов.Добавить("ОтчетОстаткиНаСкладах", НСтр("ru = 'Остатки на складах (отчет)'"),, БиблиотекаКартинок.Отчет);
					СписокВыбораСклад.Добавить("ОтчетОстаткиНаСкладах", НСтр("ru = 'Остатки на складах (отчет)'"),, БиблиотекаКартинок.Отчет);
				КонецЕсли;
				
				Если Элемент.ТекущийЭлемент = Элементы.ЗапасыОстатокОбщий Тогда
					ПоказатьВыборИзМеню(Оповещение, СписокДокументов, Элемент.ТекущийЭлемент);
				КонецЕсли;
					
			КонецЕсли;
			
		КонецЕсли;
		
		
		Если Элемент.ТекущийЭлемент = Элементы.ЗапасыВРезервеВсего 
			Или Элемент.ТекущийЭлемент = Элементы.ЗапасыЗарезервировано Тогда
			
			ЗначениеПоля = ?(Элемент.ТекущийЭлемент = Элементы.ЗапасыВРезервеВсего, ТекущиеДанные.ВРезерве, ТекущиеДанные.ВРезервеТекСклад);
			
			Если ЗначениеЗаполнено(ЗначениеПоля) Тогда
				
				ПараметрыОповещения = Новый Структура("ВыбранноеПоле", "ЗапасыВРезерве");
				Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзВыпадающегоСпискаТабличнойЧасти", ЭтаФорма, ПараметрыОповещения);
				
				СтруктураПараметров.Склад = ?(Элемент.ТекущийЭлемент = Элементы.ЗапасыВРезервеВсего, Неопределено, СтруктураПараметров.Склад);
				
				СписокДокументов = СписокДокументовПоЗаказу("Остаток", СтруктураПараметров);
				
				ПоказатьВыборИзМеню(Оповещение, СписокДокументов, Элемент.ТекущийЭлемент);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Прослеживаемость
	Если Поле = Элементы.ЗапасыРНПТ И ТекущиеДанные.ПрослеживаемыйТовар Тогда
		ИмяТабличнойЧасти = "Запасы";
		ПрослеживаемостьФормыКлиентУНФ.ОткрытьФормуПодбораРНПТ(ЭтаФорма, "Запасы", Ложь);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	// Конец Прослеживаемость
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПриАктивизацииЯчейки(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти = Неопределено Тогда 
		Возврат 
	КонецЕсли;
	
	// Прослеживаемость
	Если ЭтоНоваяСтрокаЗапасы
		И Элементы.Запасы.ТекущийЭлемент = Элементы.ЗапасыРНПТ 
		И Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.РНПТ) Тогда
		ИмяТабличнойЧасти = "Запасы";
		ПрослеживаемостьФормыКлиентУНФ.ОткрытьФормуПодбораРНПТ(ЭтаФорма, "Запасы", Ложь);
	КонецЕсли;
	// Конец Прослеживаемость
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Копирование И РежимОстаткиИРезервы Тогда
		
		СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
		
		Отказ = Истина;
		
		НоваяСтрока = Объект.Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		
		НоваяСтрока.Резерв = 0;
		
		СтруктураДанные = Новый Структура;
		СтруктураДанные.Вставить("Организация", Компания);
		СтруктураДанные.Вставить("Номенклатура", 	НоваяСтрока.Номенклатура);
		СтруктураДанные.Вставить("Характеристика", 	НоваяСтрока.Характеристика);
		СтруктураДанные.Вставить("Партия", 	НоваяСтрока.Партия);
		СтруктураДанные.Вставить("ИспользоватьХарактеристики", НоваяСтрока.ИспользоватьХарактеристики);
		СтруктураДанные.Вставить("ИспользоватьПартии", НоваяСтрока.ИспользоватьПартии);
		
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере(СтруктураДанные);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПередУдалением(Элемент, Отказ)
	
	Если Отказ Тогда
		СтарыеДанныеСтроки = Неопределено;
		Возврат;
	КонецЕсли; 
	
	// Подчиненные табличные части
	Для каждого ВыделеннаяСтрока Из Элементы.Запасы.ВыделенныеСтроки Цикл
		ТекущиеДанныеСтроки = Элементы.Запасы.ДанныеСтроки(ВыделеннаяСтрока);
		// Серии номенклатуры
		СерииНоменклатурыУНФКлиентСервер.УдалитьСерииНоменклатурыПоКлючуСвязи(Объект.СерииНоменклатуры, ТекущиеДанныеСтроки,
			ИспользоватьСерииНоменклатурыОстатки);
		// Прослеживаемость
		УдалитьСвязанныеЗаписи(ТекущиеДанныеСтроки);
	КонецЦикла;
	
	Если НЕ Объект.РучноеРаспределение Тогда
		СтарыеДанныеСтроки = Неопределено;
	ИначеЕсли Элементы.Запасы.ВыделенныеСтроки.Количество()>1 Тогда
		СохраняемыеДанные = Новый Массив;
		Для каждого ВыделеннаяСтрока Из Элементы.Запасы.ВыделенныеСтроки Цикл
			ТекущиеДанныеСтроки = Элементы.Запасы.ДанныеСтроки(ВыделеннаяСтрока);
			СтруктураПолей = Новый Структура(ИменаКолонокЗапасов(Объект));
			ЗаполнитьЗначенияСвойств(СтруктураПолей, ТекущиеДанныеСтроки);
			СохраняемыеДанные.Добавить(Новый ФиксированнаяСтруктура(СтруктураПолей));
		КонецЦикла;
		СтарыеДанныеСтроки = Новый ФиксированныйМассив(СохраняемыеДанные);
	ИначеЕсли Элементы.Запасы.ВыделенныеСтроки.Количество()=1 Тогда
		СтруктураПолей = Новый Структура(ИменаКолонокЗапасов(Объект));
		ЗаполнитьЗначенияСвойств(СтруктураПолей, Элемент.ТекущиеДанные);
		СтарыеДанныеСтроки = Новый ФиксированнаяСтруктура(СтруктураПолей);
	Иначе
		СтарыеДанныеСтроки = Неопределено;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПослеУдаления(Элемент)
	
	Если НЕ Объект.РучноеРаспределение Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект);
	КонецЕсли;
	Если ТипЗнч(СтарыеДанныеСтроки)=Тип("ФиксированныйМассив") Тогда
		Значения = Новый Массив(СтарыеДанныеСтроки);
		Для каждого Значение Из Значения Цикл
			СтарыеДанныеСтроки = Значение;
			ОбновитьКэшКонтроляПриИзмененииДанныхСтроки();
		КонецЦикла; 
	Иначе
		ОбновитьКэшКонтроляПриИзмененииДанныхСтроки();
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
	
	Если РежимОстаткиИРезервы Тогда
		Если ЗначениеЗаполнено(НоменклатураУдаленнойСтроки) Тогда
			ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере(,НоменклатураУдаленнойСтроки);
		Иначе
			ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыНоменклатураПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
	СтруктураДанные.Вставить("Партия", СтрокаТабличнойЧасти.Партия);
	
	СтатусПартии = Новый СписокЗначений;
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ДавальческоеСырье"));
	
	СтруктураДанные.Вставить("СтатусПартии", СтатусПартии);
	
	Если СкладВШапке Тогда
		СтруктураДанные.Вставить("Склад", Объект.СтруктурнаяЕдиница);
		СтруктураДанные.Вставить("Ячейка", Объект.ЯчейкаЗапасов);
	ИначеЕсли ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтруктурнаяЕдиница) И ПодборНоменклатурыИзСписка Тогда
		СтруктураДанные.Вставить("Склад", СтрокаТабличнойЧасти.СтруктурнаяЕдиница);
		СтруктураДанные.Вставить("Ячейка", СтрокаТабличнойЧасти.Ячейка);
	КонецЕсли;

	СерииНоменклатурыУНФКлиентСервер.ДополнитьСтруктуруДаннымиДляПолученияСерийНоменклатуры(Объект, СтруктураДанные);
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
	
	Если Объект.ВидОперации = КэшЗначений.ВОСборка Тогда
		
		СтрокаТабличнойЧасти.СтранаПроисхождения = СтруктураДанные.СтранаПроисхождения;
		СтрокаТабличнойЧасти.НомерГТД = Неопределено;
		
	КонецЕсли;
	
	СтрокаТабличнойЧасти.Спецификация = СтруктураДанные.Спецификация;
	СтрокаТабличнойЧасти.Количество = 1;
	СтрокаТабличнойЧасти.ДоляСтоимости = 1;
	
	Если Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Сборка") Тогда
		Если НЕ СкладВШапке Тогда
			ЗаполнениеОбъектовУНФКлиент.ЗаполнитьСкладВСтрокеТЧЗапасы(СтрокаТабличнойЧасти, Объект, СтруктураДанные);
		Иначе
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаЗапасов;
			СтрокаТабличнойЧасти.Ячейка = Объект.ЯчейкаЗапасов;
		КонецЕсли;
		СтрокаТабличнойЧасти.ЯчейкаДоступна = ЯчейкаДоступна(СтрокаТабличнойЧасти.СтруктурнаяЕдиница);
	КонецЕсли; 
	
	// Серии номенклатуры
	СерииНоменклатурыУНФКлиентСервер.УдалитьСерииНоменклатурыПоКлючуСвязи(Объект.СерииНоменклатуры, СтрокаТабличнойЧасти,
		ИспользоватьСерииНоменклатурыОстатки);
	
	СтрокаТабличнойЧасти.ИспользоватьХарактеристики = СтруктураДанные.ИспользоватьХарактеристики;
	СтрокаТабличнойЧасти.ПроверятьЗаполнениеХарактеристики = СтруктураДанные.ПроверятьЗаполнениеХарактеристики;
	СтрокаТабличнойЧасти.ЗаполнениеХарактеристикиПроверено = Истина;
	
	Если СтруктураДанные.ИспользоватьХарактеристики Тогда
		Если ПодборНоменклатурыИзСписка И ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтрокаТабличнойЧасти.Характеристика = СтруктураВыбораНоменклатуры.Характеристика;
			СтруктураВыбораНоменклатуры.Характеристика = Неопределено;
		Иначе
			СтрокаТабличнойЧасти.Характеристика = СтруктураДанные.Характеристика;
		КонецЕсли;
	КонецЕсли;
	
	//Партии
	СтрокаТабличнойЧасти.ИспользоватьПартии = СтруктураДанные.ИспользоватьПартии;
	СтрокаТабличнойЧасти.ПроверятьЗаполнениеПартий = СтруктураДанные.ПроверятьЗаполнениеПартий;
	
	Если СтруктураДанные.ИспользоватьПартии Тогда
		Если ПодборНоменклатурыИзСписка И ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтрокаТабличнойЧасти.Партия = СтруктураВыбораНоменклатуры.Партия;
			СтруктураВыбораНоменклатуры.Партия = Неопределено;
		Иначе
			СтрокаТабличнойЧасти.Партия = СтруктураДанные.Партия;
		КонецЕсли;
	КонецЕсли;
	// Конец Партии
	
	СтрокаТабличнойЧасти.СтатусыСерийНоменклатуры = СтруктураДанные.СтатусыСерийНоменклатуры;
	
	// Прослеживаемость
	Если КэшЗначений.ВестиУчетПрослеживаемыхТоваров И Объект.ВидОперации = КэшЗначений.ВОСборка Тогда
		СтрокаТабличнойЧасти.ПрослеживаемыйТовар = СтруктураДанные.ПрослеживаемыйТовар;
	Иначе
		СтрокаТабличнойЧасти.ПрослеживаемыйТовар = Ложь;
	КонецЕсли; 
	// Конец Прослеживаемость
	
	ПодборНоменклатурыИзСписка = Ложь;
	
	// Прослеживаемость
	УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти);
	// Конец Прослеживаемость
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере( , СтрокаТабличнойЧасти.Номенклатура);
	КонецЕсли;
	
КонецПроцедуры // ЗапасыНоменклатураПриИзменении()

&НаКлиенте
Процедура ЗапасыНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
		Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		
		СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
		
		ПодборНоменклатурыИзСписка = ВыбранноеЗначение.Свойство("Ячейка");
		
		Если НЕ ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
		КонецЕсли;
		
		СтруктураВыбораНоменклатуры.Характеристика = ВыбранноеЗначение.Характеристика;
		СтруктураВыбораНоменклатуры.Партия = ВыбранноеЗначение.Партия;
		
		Если ВыбранноеЗначение.Свойство("СтруктураНастроек") Тогда
			НоменклатураВДокументахКлиентСервер.ЗаполнитьТаблицуНастроекФормыВыбораНоменклатуры(ЭтаФорма, "СборкаЗапасовСписание", НастройкиФормыВыбораНоменклатуры, Истина, "Запасы", ВыбранноеЗначение.СтруктураНастроек);
		КонецЕсли;
			
		Если РазрешитьСкладыВТабличныхЧастях Тогда
			Если Объект.ПоложениеСклада = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти")
				Или Объект.СтруктурнаяЕдиницаЗапасов = ВыбранноеЗначение.Склад
				Или Не ЗначениеЗаполнено(ВыбранноеЗначение.Склад) Тогда
				
				ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыбранноеЗначение);
				
				Если СтрокаТабличнойЧасти.Свойство("СтруктурнаяЕдиница") Тогда
					СтрокаТабличнойЧасти.СтруктурнаяЕдиница = ВыбранноеЗначение.Склад;
				КонецЕсли;
				
				НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(ЭтаФорма, "Запасы");
				
			ИначеЕсли ЗначениеЗаполнено(ВыбранноеЗначение.Склад) И Не Объект.СтруктурнаяЕдиницаЗапасов = ВыбранноеЗначение.Склад Тогда
				Режим = РежимДиалогаВопрос.ДаНет;
				ПараметрыОповещения = ВыбранноеЗначение;
				Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаСкладВТЧ", ЭтотОбъект, ПараметрыОповещения);
				ПоказатьВопрос(Оповещение, НСтр("ru = 'Выбранный склад отличен от склада в шапке. Отобразить склад в табличной части?'"), Режим, 0);
			Иначе
				ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыбранноеЗначение);
				НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(ЭтаФорма, "Запасы");
			КонецЕсли;
		Иначе
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыбранноеЗначение);
			НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(ЭтаФорма, "Запасы");
		КонецЕсли;
		
		ВыбранноеЗначение = ВыбранноеЗначение.Номенклатура;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаСкладВТЧ(Результат, ПараметрыОповещения) Экспорт
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ПараметрыОповещения);
		ЗапасыНоменклатураПриИзменении(Неопределено);
		НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(ЭтаФорма, "Запасы");
		Возврат;
	КонецЕсли;
	
	Объект.ПоложениеСклада = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти");
	
	УстановитьВидимостьОтПользовательскихНастроек(ЭтотОбъект);
	
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ПараметрыОповещения);
	
	Если СтрокаТабличнойЧасти.Свойство("СтруктурнаяЕдиница") Тогда
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = ПараметрыОповещения.Склад;
	КонецЕсли;
	
	ЗапасыНоменклатураПриИзменении(Неопределено);
	НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(ЭтаФорма, "Запасы");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыХарактеристикаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
	
	СтруктураДанные = ПолучитьДанныеХарактеристикаПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.Спецификация = СтруктураДанные.Спецификация;
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере( ,СтрокаТабличнойЧасти.Номенклатура);
	КонецЕсли;
	
	// Прослеживаемость
	УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти);
	// Конец Прослеживаемость
	
КонецПроцедуры // ЗапасыХарактеристикаПриИзменении()

&НаКлиенте
Процедура ЗапасыПартияПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	Если РежимОстаткиИРезервы Тогда
		Если СтрокаТабличнойЧасти = Неопределено Тогда
			Возврат
		КонецЕсли;
		
		ЗаполнитьЗначенияОстатковПоНоменклатуре(СтрокаТабличнойЧасти)
	КонецЕсли;
	
	// Прослеживаемость
	УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти);
	// Конец Прослеживаемость
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыКоличествоПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	// Серии номенклатуры
	Если ИспользоватьСерииНоменклатурыОстатки<>Неопределено Тогда
		СерииНоменклатурыУНФКлиентСервер.ОбновитьСерииНоменклатурыКоличество(Объект, СтрокаТабличнойЧасти);
	КонецЕсли;
	
	Если СтрокаТабличнойЧасти = Неопределено Тогда Возврат КонецЕсли;
	НоменклатураВДокументахКлиент.КонтрольЗначенияРезерваВТекущейСтроке(СтрокаТабличнойЧасти);
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковПоНоменклатуре(СтрокаТабличнойЧасти)
	КонецЕсли;
	
	СерииНоменклатурыУНФКлиентСервер.ОбновитьСтатусСерииВСтроке(Объект, СтрокаТабличнойЧасти);
	
	// Прослеживаемость
	УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти);
	// Конец Прослеживаемость
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыЕдиницаИзмеренияПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда 
		Возврат 
	КонецЕсли;
	
	СерииНоменклатурыУНФКлиентСервер.ОбновитьСтатусСерииВСтроке(Объект, СтрокаТабличнойЧасти);
	
	// Прослеживаемость
	УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти);
	// Конец Прослеживаемость
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыРезервПриИзменении(Элемент)
		СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда Возврат КонецЕсли;
	
	НоменклатураВДокументахКлиент.КонтрольЗначенияРезерваВТекущейСтроке(СтрокаТабличнойЧасти, "ПриИзмененииРезерваПроверитьКоличество", Объект);
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковПоНоменклатуре(СтрокаТабличнойЧасти);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыНомерГТДПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	Если СтрокаТабличнойЧасти <> Неопределено
		И ЗначениеЗаполнено(СтрокаТабличнойЧасти.НомерГТД) Тогда
		
		ДатаГТД = ГрузовыеТаможенныеДекларацииКлиент.ВыделитьДатуИзНомераГТД(Строка(СтрокаТабличнойЧасти.НомерГТД));
		Если ДатаГТД > КонецДня(Объект.Дата) Тогда
			
			ТекстСообщения = НСтр("ru ='Внимание: выбранная ГТД датирована более поздней датой, чем текущий документ.'");
			КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.Запасы",
				СтрокаТабличнойЧасти.НомерСтроки, "НомерГТД");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , КонтекстноеПоле);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыСтранаПроисхожденияПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	Если КэшЗначений.УчетГТД Тогда
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтранаПроисхождения)
			ИЛИ СтрокаТабличнойЧасти.СтранаПроисхождения = КэшЗначений.Россия Тогда
			
			СтрокаТабличнойЧасти.НомерГТД = Неопределено;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Прослеживаемость
	УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти);
	// Конец Прослеживаемость
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыРНПТНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИмяТабличнойЧасти = "Запасы";
	ПрослеживаемостьФормыКлиентУНФ.ОткрытьФормуПодбораРНПТ(ЭтаФорма, "Запасы", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыРНПТОчистка(Элемент, СтандартнаяОбработка)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти <> Неопределено Тогда
		
		Если СтрокаТабличнойЧасти.ПрослеживаемыйТовар Тогда
			УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти);
		Иначе
			СтрокаТабличнойЧасти.НомерГТД = Неопределено;
		КонецЕсли;
		
		ПрослеживаемостьФормыКлиентСерверУНФ.ЗаполнитьПредставлениеРНПТ(ЭтаФорма, СтрокаТабличнойЧасти);
		
	КонецЕсли;

КонецПроцедуры

#Область СтруктурнаяЕдиницаЗапасыСписатьИзСборка

&НаКлиенте
Процедура СтруктурнаяЕдиницаЗапасовСборкаПриИзменении(Элемент)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Организация", Объект.Организация);
	СтруктураДанные.Вставить("Склад",		Объект.СтруктурнаяЕдиницаЗапасов);
	
	СтруктураДанные = ПолучитьДанныеСтруктурнаяЕдиницаПродукцияЗапасыОтходыПриИзменении(СтруктураДанные);
	Объект.ПодписьКладовщикаОтправил = СтруктураДанные.ПодписьМОЛ;
	
	Элементы.ЯчейкаЗапасовСборка.Доступность = (НЕ СтруктураДанные.ОрдерныйСклад И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаЗапасов));
	
	ОбновитьСкладВТЧ();
	УстановитьВидимостьОтПользовательскихНастроек(ЭтотОбъект);
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
	ОбновитьНастройкиФормыВыбораНоменклатурыПриИзмененииРеквизита();
	
КонецПроцедуры // СтруктурнаяЕдиницаЗапасовСборкаПриИзменении()

&НаКлиенте
Процедура ЯчейкаЗапасовСборкаПриИзменении(Элемент)
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
		
		Если СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаЗапасов Тогда
			СтрокаТабличнойЧасти.Ячейка = Объект.ЯчейкаЗапасов;
		КонецЕсли;
		
	КонецЦикла;
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктурнаяЕдиницаЗапасовСборкаОткрытие(Элемент, СтандартнаяОбработка)
	
	Если Элементы.СтруктурнаяЕдиницаЗапасовСборка.РежимВыбораИзСписка
		И НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаЗапасов) Тогда
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // СтруктурнаяЕдиницаЗапасовСборкаОткрытие()

#КонецОбласти

#Область СтруктурнаяЕдиницаЗапасыПолучательРазборка

// Процедура - обработчик события ПриИзменении поля ввода СтруктурнаяЕдиницаЗапасовРазборка.
//
&НаКлиенте
Процедура СтруктурнаяЕдиницаЗапасовРазборкаПриИзменении(Элемент)
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Организация", Объект.Организация);
	СтруктураДанные.Вставить("Склад",		Объект.СтруктурнаяЕдиницаПродукции);
	
	СтруктураДанные = ПолучитьДанныеСтруктурнаяЕдиницаПродукцияЗапасыОтходыПриИзменении(СтруктураДанные);
	Объект.ПодписьКладовщикаПолучил = СтруктураДанные.ПодписьМОЛ;
	
	Элементы.ЯчейкаЗапасовРазборка.Доступность = (НЕ СтруктураДанные.ОрдерныйСклад И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаПродукции));
	
КонецПроцедуры // СтруктурнаяЕдиницаЗапасовРазборкаПриИзменении()

// Процедура - обработчик события Открытие поля СтруктурнаяЕдиницаЗапасовРазборка.
//
&НаКлиенте
Процедура СтруктурнаяЕдиницаЗапасовРазборкаОткрытие(Элемент, СтандартнаяОбработка)
	
	Если Элементы.СтруктурнаяЕдиницаЗапасовРазборка.РежимВыбораИзСписка
		И НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаПродукции) Тогда
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // СтруктурнаяЕдиницаЗапасовРазборкаОткрытие()

#КонецОбласти

&НаКлиенте
Процедура ЗапасыЗаказПокупателяПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя) Тогда
		СтрокаТабличнойЧасти.Резерв = 0;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПредупреждениеЗапасыЗакрытьНажатие(Элемент)
	
	УстановитьКартинкиЗакладок(ЭтотОбъект, Истина);		
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыЭтапНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыЭтапАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МассивЭтапов = Новый Массив;
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		Если НЕ СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства Тогда
			МассивЭтапов.Добавить(ПредопределенноеЗначение("Справочник.ЭтапыПроизводства.ПустаяСсылка"));
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	Для каждого СтрокаТабличнойЧасти Из Объект.ВыполненныеЭтапы Цикл
		Если МассивЭтапов.Найти(СтрокаТабличнойЧасти.Этап)=Неопределено Тогда
			МассивЭтапов.Добавить(СтрокаТабличнойЧасти.Этап);
		КонецЕсли; 
	КонецЦикла; 
	ДанныеВыбора = Новый СписокЗначений;
	ДанныеВыбора.ЗагрузитьЗначения(МассивЭтапов);
	ОбновитьПредставлениеПустогоЭтапа(ДанныеВыбора);

КонецПроцедуры
 
&НаКлиенте
Процедура ЗапасыСтруктурнаяЕдиницаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	СтрокаТабличнойЧасти.ЯчейкаДоступна = ЯчейкаДоступна(СтрокаТабличнойЧасти.СтруктурнаяЕдиница);
	
	ОбновитьСтатусыСерийНоменклатурыВТабличнойЧасти(СтрокаТабличнойЧасти.НомерСтроки);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатусыСерийНоменклатурыВТабличнойЧасти(НомерСтроки, ИмяТаблицы = "Запасы", ИмяТаблицыСерии = "СерииНоменклатуры")
	ДополнительныеСвойства = Новый Структура("НомерСтроки", НомерСтроки);
	СерииНоменклатурыУНФ.ОбновитьСтатусыСерийНоменклатурыВТабличнойЧасти(Объект, ИмяТаблицы, ДополнительныеСвойства, ИмяТаблицыСерии);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаИзФайлаЗапасы(Команда)
	
	НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Документ.СборкаЗапасов.ТабличнаяЧасть.Запасы";
	
	НастройкиЗагрузкиДанных.Вставить("ПолноеИмяТабличнойЧасти", "СборкаЗапасов.Запасы");
	НастройкиЗагрузкиДанных.Вставить("ИмяМакетаСШаблоном","ЗагрузкаИзФайлаЗапасы");
	НастройкиЗагрузкиДанных.Вставить("Заголовок", НСтр("ru = 'Загрузка запасов из файла'"));
	НастройкиЗагрузкиДанных.Вставить("СодержаниеВидимо", Истина);
	НастройкиЗагрузкиДанных.Вставить("ПоложениеСклада", Объект.ПоложениеСклада);
	НастройкиЗагрузкиДанных.Вставить("ПоложениеЗаказаПокупателя", Объект.ПоложениеЗаказаПокупателя);
	НастройкиЗагрузкиДанных.Вставить("ЗаказЗаполнен", ЗаказПокупателяЗаполнен(Объект));
	НастройкиЗагрузкиДанных.Вставить("ВидОперации", Объект.ВидОперации);
	НастройкиЗагрузкиДанных.Вставить("ЗагрузкаТабличнойЧастиУслуги", Ложь);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата", ЭтотОбъект, НастройкиЗагрузкиДанных);
	
	ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.ПоказатьФормуЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных, ОписаниеОповещения, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРаспределениеЗапасов

&НаКлиенте
Процедура РаспределениеЗапасовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ТекущаяСтрока = Элемент.ТекущиеДанные;
		ТекущаяСтрока.НомерСтроки = РаспределениеЗапасов.Количество();
		СтарыеДанныеСтроки = Неопределено;
		Если ЗначениеЗаполнено(ТекущаяПродукция) Тогда
			ТекущаяСтрока.КлючСвязиПродукция = ТекущаяПродукция;
			СтрокаПродукции = СтрокаПоКлючу(Объект.Продукция, ТекущаяПродукция);
			ЗаполнитьДанныеПродукцииВСтрокеРаспределения(ТекущаяСтрока, СтрокаПродукции);
		КонецЕсли; 
		ЗаполнитьДанныеШапки(Объект, Элемент.ТекущиеДанные);
	Иначе
		СтруктураПолей = Новый Структура(ИменаКолонокЗапасов(Объект));
		ЗаполнитьЗначенияСвойств(СтруктураПолей, Элемент.ТекущиеДанные);
		СтарыеДанныеСтроки = Новый ФиксированнаяСтруктура(СтруктураПолей);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьДанныеТЧРаспределениеЗапасов(ЭтотОбъект);
	ОбновитьКэшКонтроляПриИзмененииДанныхСтроки(Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	ДанныеПеретаскивания = Новый Структура;
	ДанныеПеретаскивания.Вставить("Событие", "ПерераспределениеМатериалов");
	ДанныеПеретаскивания.Вставить("Строка", Элемент.ТекущаяСтрока);
	
	ПараметрыПеретаскивания.Значение = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеПеретаскивания);
	
КонецПроцедуры                                                                                                                           

&НаКлиенте
Процедура РаспределениеЗапасовПередУдалением(Элемент, Отказ)
	
	Если Отказ Тогда
		СтарыеДанныеСтроки = Неопределено;
	Иначе
		СтруктураПолей = Новый Структура(ИменаКолонокЗапасов(Объект));
		ЗаполнитьЗначенияСвойств(СтруктураПолей, Элемент.ТекущиеДанные);
		СтарыеДанныеСтроки = Новый ФиксированнаяСтруктура(СтруктураПолей);
	КонецЕсли; 	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПродукцииПриАктивизацииСтроки(Элемент)
	
	ЭлементСписка = Элементы.СписокПродукции.ТекущиеДанные;
	Если ЭлементСписка=Неопределено ИЛИ ЭлементСписка.Значение=ТекущаяПродукция Тогда
		Возврат;
	КонецЕсли; 
	
	ОбновитьРаспределениеЗапасовНаФорме(); 
	
	// МобильныйКлиент
	Если ЭтоМобильныйКлиент Тогда
		Если ЗначениеЗаполнено(ЭлементСписка.Значение) Тогда
			Элементы.ПраваяПанель.Заголовок = НСтр("ru = 'Отборы (установлены)'");
		Иначе
			Элементы.ПраваяПанель.Заголовок = НСтр("ru = 'Отборы'");
		КонецЕсли;
	КонецЕсли; 
	// Конец МобильныйКлиент	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПродукцииПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли; 
	
	Если Строка=Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение)<>Тип("Массив") ИЛИ ПараметрыПеретаскивания.Значение.Количество()<>1 Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеСобытия = ПараметрыПеретаскивания.Значение[0];
	
	Если ТипЗнч(ОписаниеСобытия)<>Тип("Структура") ИЛИ НЕ ОписаниеСобытия.Свойство("Событие") ИЛИ ОписаниеСобытия.Событие<>"ПерераспределениеМатериалов" Тогда
		Возврат;
	КонецЕсли; 
	
	ЭлементСписка = СписокПродукции.НайтиПоИдентификатору(Строка);
	СтрокаРаспределения = РаспределениеЗапасов.НайтиПоИдентификатору(ОписаниеСобытия.Строка);
	Если ЭлементСписка=Неопределено ИЛИ ЭлементСписка.Значение=0 ИЛИ СтрокаРаспределения.КлючСвязиПродукция=ЭлементСписка.Значение Тогда
		Возврат;
	КонецЕсли; 
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПродукцииПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли; 
	
	ЭлементСписка = СписокПродукции.НайтиПоИдентификатору(Строка);
	Если ЭлементСписка=Неопределено ИЛИ ЭлементСписка.Значение=0 Тогда
		Возврат;
	КонецЕсли; 
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение)<>Тип("Массив") ИЛИ ПараметрыПеретаскивания.Значение.Количество()<>1 Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеСобытия = ПараметрыПеретаскивания.Значение[0];
	
	СтрокаРаспределения = РаспределениеЗапасов.НайтиПоИдентификатору(ОписаниеСобытия.Строка);
	Если СтрокаРаспределения=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПродукции = СтрокаПоКлючу(Объект.Продукция, ЭлементСписка.Значение);
	Если СтрокаПродукции=Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("СтрокаПродукции", СтрокаПродукции);
	СтруктураПараметров.Вставить("СтрокаРаспределения", СтрокаРаспределения);
	Оповещение = Новый ОписаниеОповещения("СписокПродукцииПеретаскиваниеЗавершение", ЭтотОбъект, СтруктураПараметров);
	ПоказатьВводЧисла(Оповещение, СтрокаРаспределения.Количество, НСтр("ru = 'Количество'"), 15, 3);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПродукцииПеретаскиваниеЗавершение(Количество, СтруктураПараметров) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Количество) ИЛИ ТипЗнч(Количество)<>Тип("Число") Тогда
		Возврат;
	КонецЕсли; 	
	
	СтрокаПродукции = СтруктураПараметров.СтрокаПродукции;
	СтрокаРаспределения = СтруктураПараметров.СтрокаРаспределения;
	
	Если СтрокаПродукции.ЗаказПокупателя<>СтрокаРаспределения.ЗаказПокупателя Тогда
		// Предварительно требуется замена идентификатора запасов
		СтрокаРаспределения.ЗаказПокупателя = СтрокаПродукции.ЗаказПокупателя;
	КонецЕсли; 
	
	Если Количество>СтрокаРаспределения.Количество Тогда
		Количество = СтрокаРаспределения.Количество;
	КонецЕсли;
	
	Если ТекущаяПродукция=0 Тогда
		// Отображается полный список распределенных запасов
		НоваяСтрока = НайтиСуществующуюСтрокуРаспределения(РаспределениеЗапасов, СтрокаПродукции.КлючСвязи, СтрокаРаспределения);
		Если Количество=СтрокаРаспределения.Количество Тогда
			// Переносятся все материалы из строки распределения
			Если НоваяСтрока=Неопределено Тогда
				ЗаполнитьДанныеПродукцииВСтрокеРаспределения(СтрокаРаспределения, СтрокаПродукции);
			Иначе
				НоваяСтрока.Количество = НоваяСтрока.Количество+Количество;
				РаспределениеЗапасов.Удалить(СтрокаРаспределения);
			КонецЕсли; 
		Иначе
			// Переносится часть материалов
			Если НоваяСтрока=Неопределено Тогда
				НоваяСтрока = РаспределениеЗапасов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРаспределения);
				НоваяСтрока.Количество = Количество;
				ЗаполнитьДанныеПродукцииВСтрокеРаспределения(НоваяСтрока, СтрокаПродукции);
				НоваяСтрока.ЯчейкаДоступна = ЯчейкаДоступна(НоваяСтрока.СтруктурнаяЕдиница);
			Иначе
				НоваяСтрока.Количество = НоваяСтрока.Количество+Количество;
			КонецЕсли; 
			СтрокаРаспределения.Количество = СтрокаРаспределения.Количество-Количество;
		КонецЕсли;
		ОбновитьДанныеТЧРаспределениеЗапасов(ЭтотОбъект);
	Иначе
		// Отображается список распределенных запасов с отбором по изделию
		СтараяСтрока = НайтиСуществующуюСтрокуРаспределения(Объект.РаспределениеЗапасов, СтрокаРаспределения.КлючСвязиПродукция, СтрокаРаспределения);
		НоваяСтрока = НайтиСуществующуюСтрокуРаспределения(Объект.РаспределениеЗапасов, СтрокаПродукции.КлючСвязи, СтараяСтрока);
		Если Количество=СтрокаРаспределения.Количество Тогда
			// Переносятся все материалы из строки распределения
			Если НоваяСтрока=Неопределено Тогда
				СтараяСтрока.КлючСвязиПродукция = СтрокаПродукции.КлючСвязи;
			Иначе
				НоваяСтрока.Количество = НоваяСтрока.Количество+Количество;
				Объект.РаспределениеЗапасов.Удалить(СтараяСтрока);
			КонецЕсли; 
			РаспределениеЗапасов.Удалить(СтрокаРаспределения);
		Иначе
			// Переносится часть материалов
			Если НоваяСтрока=Неопределено Тогда
				НоваяСтрока = Объект.РаспределениеЗапасов.Добавить();
				ЗаполнитьДанныеЗапасовВСтрокеРаспределения(Объект, НоваяСтрока, СтрокаРаспределения);
				НоваяСтрока.КлючСвязиПродукция = СтрокаПродукции.КлючСвязи;
				НоваяСтрока.Количество = Количество;
			Иначе
				НоваяСтрока.Количество = НоваяСтрока.Количество+Количество;
			КонецЕсли;
			СтараяСтрока.Количество = СтараяСтрока.Количество-Количество;
			СтрокаРаспределения.Количество = СтрокаРаспределения.Количество-Количество;
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовНоменклатураПродукцияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение)<>Тип("Число") Тогда
		Возврат;
	КонецЕсли; 	
	
	СтрокаПродукция = СтрокаПоКлючу(Объект.Продукция, ВыбранноеЗначение);
	Если СтрокаПродукция=Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ТекущаяСтрока = Элементы.РаспределениеЗапасов.ТекущиеДанные;
	
	ЗаполнитьДанныеПродукцииВСтрокеРаспределения(ТекущаяСтрока, СтрокаПродукция);
	Если Элементы.Найти("РаспределениеЗапасовНоменклатура")<>Неопределено Тогда
		Элементы.РаспределениеЗапасов.ТекущийЭлемент = Элементы.РаспределениеЗапасовНоменклатура;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовНоменклатураПриИзменении(Элемент)
	
	ТекСтр = Элементы.РаспределениеЗапасов.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", ТекСтр.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", ТекСтр.Характеристика);
	СтруктураДанные.Вставить("Партия", ТекСтр.Партия);
	
	Если Не СкладВШапке И ЗначениеЗаполнено(ТекСтр.СтруктурнаяЕдиница) И ПодборНоменклатурыИзСписка Тогда
		СтруктураДанные.Вставить("Склад", ТекСтр.Склад);
		СтруктураДанные.Вставить("Ячейка", ТекСтр.Ячейка);
	КонецЕсли;
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	ТекСтр.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
	ТекСтр.Количество = 1;
	ТекСтр.Спецификация = СтруктураДанные.Спецификация;
	
	//Характеристики
	ТекСтр.ИспользоватьХарактеристики = СтруктураДанные.ИспользоватьХарактеристики;
	ТекСтр.ПроверятьЗаполнениеХарактеристики = СтруктураДанные.ПроверятьЗаполнениеХарактеристики;
	ТекСтр.ЗаполнениеХарактеристикиПроверено = Истина;
	
	Если СтруктураДанные.ИспользоватьХарактеристики Тогда
		Если ПодборНоменклатурыИзСписка И ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			ТекСтр.Характеристика = СтруктураВыбораНоменклатуры.Характеристика;
			СтруктураВыбораНоменклатуры.Характеристика = Неопределено;
		Иначе
			ТекСтр.Характеристика = СтруктураДанные.Характеристика;
		КонецЕсли;
	КонецЕсли;
	//Конец Характеристики
	
	//Партии
	ТекСтр.ИспользоватьПартии = СтруктураДанные.ИспользоватьПартии;
	ТекСтр.ПроверятьЗаполнениеПартий = СтруктураДанные.ПроверятьЗаполнениеПартий;
	
	Если СтруктураДанные.ИспользоватьПартии Тогда
		Если ПодборНоменклатурыИзСписка И ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			ТекСтр.Партия = СтруктураВыбораНоменклатуры.Партия;
			СтруктураВыбораНоменклатуры.Партия = Неопределено;
		Иначе
			ТекСтр.Партия = СтруктураДанные.Партия;
		КонецЕсли;
	КонецЕсли;
	// Конец Партии
	
	ПодборНоменклатурыИзСписка = Ложь;
	
	Если Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Сборка") Тогда
		Если НЕ СкладВШапке Тогда
			ЗаполнениеОбъектовУНФКлиент.ЗаполнитьСкладВСтрокеТЧЗапасы(ТекСтр, Объект, СтруктураДанные);
		Иначе
			ТекСтр.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаЗапасов;
			ТекСтр.Ячейка = Объект.ЯчейкаЗапасов;
			ТекСтр.ЯчейкаДоступна = ЯчейкаДоступна(ТекСтр.СтруктурнаяЕдиница);
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		
		СтрокаТабличнойЧасти = Элементы.РаспределениеЗапасов.ТекущиеДанные;
		
		ПодборНоменклатурыИзСписка = ВыбранноеЗначение.Свойство("Ячейка");
		
		Если НЕ ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
		КонецЕсли;
		
		СтруктураВыбораНоменклатуры.Характеристика = ВыбранноеЗначение.Характеристика;
		СтруктураВыбораНоменклатуры.Партия = ВыбранноеЗначение.Партия;
		
		Если РазрешитьСкладыВТабличныхЧастях Тогда
			Если Объект.ПоложениеСклада = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
				
				ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыбранноеЗначение);
				
				Если СтрокаТабличнойЧасти.Свойство("СтруктурнаяЕдиница") Тогда
					СтрокаТабличнойЧасти.СтруктурнаяЕдиница = ВыбранноеЗначение.Склад;
				КонецЕсли;
				
			Иначе
				Режим = РежимДиалогаВопрос.ДаНет;
				ПараметрыОповещения = ВыбранноеЗначение;
				Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаСкладВТЧРаспределениеЗапасов", ЭтотОбъект, ПараметрыОповещения);
				ПоказатьВопрос(Оповещение, НСтр("ru = 'Выбранный склад отличен от склада в шапке. Отобразить склад в табличной части?'"), Режим, 0);
			КонецЕсли;
		КонецЕсли;
		
		ВыбранноеЗначение = ВыбранноеЗначение.Номенклатура;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаСкладВТЧРаспределениеЗапасов(Результат, ПараметрыОповещения) Экспорт
	
	СтрокаТабличнойЧасти = Элементы.РаспределениеЗапасов.ТекущиеДанные;
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ПараметрыОповещения);
		РаспределениеЗапасовНоменклатураПриИзменении(Неопределено);
		Возврат;
	КонецЕсли;
	
	Объект.ПоложениеСклада = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти");
	
	УстановитьВидимостьОтПользовательскихНастроек(ЭтотОбъект);
	
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ПараметрыОповещения);
	
	Если СтрокаТабличнойЧасти.Свойство("СтруктурнаяЕдиница") Тогда
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = ПараметрыОповещения.Склад;
	КонецЕсли;
	
	РаспределениеЗапасовНоменклатураПриИзменении(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовХарактеристикаПриИзменении(Элемент)
	
	ТекСтр = Элементы.РаспределениеЗапасов.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", ТекСтр.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", ТекСтр.Характеристика);
	
	СтруктураДанные = ПолучитьДанныеХарактеристикаПриИзменении(СтруктураДанные);
	
	ТекСтр.Спецификация = СтруктураДанные.Спецификация;
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьРазвернутьПанель(Элемент)
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфо.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость);

КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовПриАктивизацииСтроки(Элемент)
	
	ОбновитьТекстПодсказкиРаспределения();	
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовПослеУдаления(Элемент)
	
	ОбновитьДанныеТЧРаспределениеЗапасов(ЭтотОбъект);
	ОбновитьКэшКонтроляПриИзмененииДанныхСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура РучноеРаспределениеРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("СтрокаПоиска", НСтр("ru = 'Способ распределения материалов'"));
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриИзмененииСпособаРаспределенияПоУмолчанию", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.НастройкаПрограммы.Форма",
			ПараметрыОткрытияФормы,
			ЭтотОбъект,
			Ложь,,,
			ОписаниеОповещения
	);
	
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовЭтапНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовЭтапАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаТабличнойЧасти = Элементы.РаспределениеЗапасов.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СпецификацияПродукция) ИЛИ СтрокаТабличнойЧасти.КлючСвязиПродукция=0 Тогда
		Возврат;
	КонецЕсли; 
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("КлючСвязи", СтрокаТабличнойЧасти.КлючСвязиПродукция);
	СтрокиЭтапы = Объект.ВыполненныеЭтапы.НайтиСтроки(СтруктураОтбора);
	СтрокиПродукция = Объект.Продукция.НайтиСтроки(СтруктураОтбора);
	Если СтрокиПродукция.Количество()=0 Тогда
		Возврат;
	КонецЕсли; 
	СтрокаПродукция = СтрокиПродукция[0];
	ДанныеВыбора = Новый СписокЗначений;
	Если НЕ СтрокаПродукция.ИспользоватьЭтапыПроизводства Тогда
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Справочник.ЭтапыПроизводства.ПустаяСсылка"));
	Иначе
		Для каждого СтрокаЭтап Из СтрокиЭтапы Цикл
			ДанныеВыбора.Добавить(СтрокаЭтап.Этап);
		КонецЦикла; 
	КонецЕсли; 
	ОбновитьПредставлениеПустогоЭтапа(ДанныеВыбора); 
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеЗапасовСтруктурнаяЕдиницаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РаспределениеЗапасов.ТекущиеДанные;
	СтрокаТабличнойЧасти.ЯчейкаДоступна = ЯчейкаДоступна(СтрокаТабличнойЧасти.СтруктурнаяЕдиница);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтходы

// Процедура - обработчик события ПриИзменении поля ввода Номенклатура.
//
&НаКлиенте
Процедура ОтходыНоменклатураПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Отходы.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
	СтруктураДанные.Вставить("Партия", СтрокаТабличнойЧасти.Партия);
	
	СтатусПартии = Новый СписокЗначений;
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ДавальческоеСырье"));
	
	СтруктураДанные.Вставить("СтатусПартии", СтатусПартии);
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
	СтрокаТабличнойЧасти.Количество = 1;
	
	СтрокаТабличнойЧасти.ИспользоватьХарактеристики = СтруктураДанные.ИспользоватьХарактеристики;
	СтрокаТабличнойЧасти.ПроверятьЗаполнениеХарактеристики = СтруктураДанные.ПроверятьЗаполнениеХарактеристики;
	СтрокаТабличнойЧасти.ЗаполнениеХарактеристикиПроверено = Истина;
	
	Если СтруктураДанные.ИспользоватьХарактеристики Тогда
		Если ПодборНоменклатурыИзСписка И ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтрокаТабличнойЧасти.Характеристика = СтруктураВыбораНоменклатуры.Характеристика;
			СтруктураВыбораНоменклатуры.Характеристика = Неопределено;
		Иначе
			СтрокаТабличнойЧасти.Характеристика = СтруктураДанные.Характеристика;
		КонецЕсли;
	КонецЕсли;
	
	//Партии
	СтрокаТабличнойЧасти.ИспользоватьПартии = СтруктураДанные.ИспользоватьПартии;
	СтрокаТабличнойЧасти.ПроверятьЗаполнениеПартий = СтруктураДанные.ПроверятьЗаполнениеПартий;
	
	Если СтруктураДанные.ИспользоватьПартии Тогда
		Если ПодборНоменклатурыИзСписка И ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтрокаТабличнойЧасти.Партия = СтруктураВыбораНоменклатуры.Партия;
			СтруктураВыбораНоменклатуры.Партия = Неопределено;
		Иначе
			СтрокаТабличнойЧасти.Партия = СтруктураДанные.Партия;
		КонецЕсли;
	КонецЕсли;
	// Конец Партии
	
	ПодборНоменклатурыИзСписка = Ложь;
	
КонецПроцедуры // ОтходыНоменклатураПриИзменении()

&НаКлиенте
Процедура ОтходыНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		СтрокаТабличнойЧасти = Элементы.Отходы.ТекущиеДанные;
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыбранноеЗначение);
		
		ПодборНоменклатурыИзСписка = ВыбранноеЗначение.Свойство("Ячейка");
		
		Если НЕ ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
		КонецЕсли;
		
		СтруктураВыбораНоменклатуры.Характеристика = ВыбранноеЗначение.Характеристика;
		СтруктураВыбораНоменклатуры.Партия = ВыбранноеЗначение.Партия;
		
		Если ВыбранноеЗначение.Свойство("СтруктураНастроек") Тогда
			НоменклатураВДокументахКлиентСервер.ЗаполнитьТаблицуНастроекФормыВыбораНоменклатуры(ЭтаФорма, "СборкаЗапасовОприходование", НастройкиФормыВыбораНоменклатуры, Истина, "Отходы", ВыбранноеЗначение.СтруктураНастроек);
			НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(ЭтаФорма, "Отходы");
		КонецЕсли;
		ВыбранноеЗначение = ВыбранноеЗначение.Номенклатура;
	КонецЕсли;

КонецПроцедуры

#Область СтруктурнаяЕдиницаПолучательОтходов

// Процедура - обработчик события ПриИзменении поля ввода СтруктурнаяЕдиницаОтходов.
//
&НаКлиенте
Процедура СтруктурнаяЕдиницаОтходовПриИзменении(Элемент)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Организация", Объект.Организация);
	СтруктураДанные.Вставить("Склад", Объект.СтруктурнаяЕдиницаОтходов);
	
	СтруктураДанные = ПолучитьДанныеСтруктурнаяЕдиницаПродукцияЗапасыОтходыПриИзменении(СтруктураДанные);
	
	Элементы.ЯчейкаОтходов.Доступность = (НЕ СтруктураДанные.ОрдерныйСклад И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаОтходов));
	
КонецПроцедуры // СтруктурнаяЕдиницаОтходовПриИзменении()

// Процедура - обработчик события Открытие поля СтруктурнаяЕдиницаОтходов.
//
&НаКлиенте
Процедура СтруктурнаяЕдиницаОтходовОткрытие(Элемент, СтандартнаяОбработка)
	
	Если Элементы.СтруктурнаяЕдиницаОтходов.РежимВыбораИзСписка
		И НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаОтходов) Тогда
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // СтруктурнаяЕдиницаОтходовОткрытие()

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДекорацияПечатьНажатие(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьИзмененияРеквизитовПечати", ЭтотОбъект);
	ОткрытьФорму("Обработка.РеквизитыПечати.Форма.РеквизитыПечатиПроизводство", Новый Структура("КонтекстПечати", Объект), ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку ЗаполнитьПоОснованию.
//
&НаКлиенте
Процедура ЗаполнитьПоОснованию(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоОснованиюЗавершение", ЭтотОбъект), НСтр(
		"ru = 'Документ будет полностью перезаполнен по ""Основанию"". Продолжить операцию?'"),
		РежимДиалогаВопрос.ДаНет, 0);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОснованиюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ЗаполнитьПоДокументу("ЗаказНаПроизводство");
		
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка") Тогда
			
			Если НЕ ЗначениеЗаполнено(Объект.ЗаказПокупателя) 
				И Объект.ПоложениеЗаказаПокупателя<>ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
				
				Для Каждого СтрокаЗапасы Из Объект.Продукция Цикл
					СтрокаЗапасы.Резерв = 0;
				КонецЦикла;
				Элементы.ПродукцияРезерв.Видимость = Ложь;
				
			Иначе
				
				Если Элементы.ПродукцияРезерв.Видимость = Ложь Тогда
					Элементы.ПродукцияРезерв.Видимость = Истина;
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			Если НЕ ЗначениеЗаполнено(Объект.ЗаказПокупателя) 
				И Объект.ПоложениеЗаказаПокупателя<>ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
				
				Для Каждого СтрокаЗапасы Из Объект.Запасы Цикл
					СтрокаЗапасы.Резерв = 0;
				КонецЦикла;
				Элементы.ЗапасыРезерв.Видимость = Ложь;
				Элементы.ЗапасыИзменитьРезерв.Видимость = Ложь;
				ИспользуетсяРезервирование = Ложь;
				
			Иначе
				
				Если Элементы.ЗапасыРезерв.Видимость = Ложь Тогда
					Элементы.ЗапасыРезерв.Видимость = Истина;
					Элементы.ЗапасыИзменитьРезерв.Видимость = Истина;
					ИспользуетсяРезервирование = Истина;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ОбновитьСпискиВыбораРаспределение();
		ЗапасыНеЗаполнены = Ложь;
		ВывестиОтметкиКонтроля(ЭтотОбъект);
		
	КонецЕсли;
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
КонецПроцедуры  // ЗаполнитьПоОснованию()

// Процедура - обработчик нажатия на кнопку ЗаполнитьПоЗаказуПокупателя.
//
&НаКлиенте
Процедура ЗаполнитьПоЗаказуПокупателя(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоЗаказуПокупателяЗавершение", ЭтотОбъект), НСтр(
		"ru = 'Документ будет полностью перезаполнен по ""Заказу покупателя"". Продолжить операцию?'"),
		РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоЗаказуПокупателяЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Ответ = Результат;

	Если Ответ = КодВозвратаДиалога.Да Тогда

		ЗаполнитьПоДокументу("ЗаказПокупателя");

		ОбновитьСпискиВыбораРаспределение();
		ЗапасыНеЗаполнены = Ложь;
		ВывестиОтметкиКонтроля(ЭтотОбъект);

	КонецЕсли;

	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;

КонецПроцедуры // ЗаполнитьПоЗаказуПокупателя()

// Процедура - обработчик команды ЗаполнитьПоРезервам подменю ИзменитьРезерв.
//
&НаКлиенте
Процедура ИзменитьРезервЗаполнитьПоРезервам(Команда)
	
	Если Объект.Запасы.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Табличная часть ""Запасы и услуги"" не заполнена.'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ЗаполнитьКолонкуРезервПоРезервамНаСервере();
	
КонецПроцедуры // ИзменитьРезервЗаполнитьПоРезервам()

// Процедура - обработчик команды ОчиститьРезерв подменю ИзменитьРезерв.
//
&НаКлиенте
Процедура ИзменитьРезервОчиститьРезерв(Команда)
	
	Если Объект.Запасы.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Табличная часть ""Запасы и услуги"" не заполнена.'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
		СтрокаТабличнойЧасти.Резерв = 0;
	КонецЦикла;
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
КонецПроцедуры // ИзменитьРезервЗаполнитьПоОстаткам()

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ПечатьТТН.Форма.ДанныеПечати" Тогда
		
		Для каждого ЗначенияРеквизита Из ВыбранноеЗначение Цикл
			Если Объект.Свойство(ЗначенияРеквизита.Ключ) Тогда
				Объект[ЗначенияРеквизита.Ключ] = ЗначенияРеквизита.Значение;
				Модифицированность = Истина;
			КонецЕсли; 
		КонецЦикла;
		
	// Прослеживаемость
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Справочник.НомераГТД.Форма.ФормаПодбораРНПТ" Тогда
	
		ОбработкаВыбораРНПТННаКлиенте(ВыбранноеЗначение);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Справочник.НомераГТД.Форма.ФормаВыбора" Тогда
		
		ОбработкаВыбораГТДНаКлиенте(ВыбранноеЗначение);
	
	// Конец Прослеживаемость
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШапкаТабличнаяЧасть(Команда)
	
	ЗаполнениеОбъектовУНФКлиент.ЗаполнитьПустыеПоложения(Объект, "ПоложениеСклада, ПоложениеЗаказаПокупателя");
	
	ПараметрыДиалога = Новый Структура;
	ПараметрыДиалога.Вставить("ПоложениеСкладаВДокументахПроизводства", Объект.ПоложениеСклада);
	ПараметрыДиалога.Вставить("ПоложениеЗаказаПокупателяВДокументахПроизводства", Объект.ПоложениеЗаказаПокупателя);
	ПараметрыДиалога.Вставить("БылиВнесеныИзменения", Ложь);
	ПараметрыДиалога.Вставить("Доступность", Новый Структура("ПоложениеСкладаВДокументахПроизводства", НЕ РежимОстаткиИРезервы));
	
	ОткрытьФорму(
	"ОбщаяФорма.ШапкаТабличнаяЧасть",
	ПараметрыДиалога,,,,,
	Новый ОписаниеОповещения("ШапкаТабличнаяЧастьЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ШапкаТабличнаяЧастьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.БылиВнесеныИзменения Тогда
		ШапкаТабличнаяЧастьЗавершениеНаСервере(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ШапкаТабличнаяЧастьЗавершениеНаСервере(Знач Результат)
	
	Если Объект.ПоложениеСклада <> Результат.ПоложениеСкладаВДокументахПроизводства Тогда
		Объект.ПоложениеСклада = Результат.ПоложениеСкладаВДокументахПроизводства;
		Если Объект.ВидОперации = Перечисления.ВидыОперацийСборкаЗапасов.Разборка Тогда
			ИмяТЧ = "Продукция";
		Иначе
			ИмяТЧ = "Запасы";
		КонецЕсли;
		Если НЕ РежимОстаткиИРезервы Тогда
			Если Объект.ПоложениеСклада <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
				СтруктураПолей = ЗаполнениеОбъектовУНФ.СтруктурнаяЕдиницаИЯчейкаДляШапки(Объект[ИмяТЧ]);
				Объект.СтруктурнаяЕдиницаЗапасов = СтруктураПолей.СтруктурнаяЕдиница;
				Объект.ЯчейкаЗапасов = СтруктураПолей.Ячейка;
				Если НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаЗапасов) Тогда
					СтруктураДанные = Новый Структура;
					СтруктураДанные.Вставить("Подразделение", Объект.СтруктурнаяЕдиница);
					СтруктураДанные.Вставить("ВидОперации", Объект.ВидОперации);
					СтруктураДанные = ПолучитьДанныеСтруктурнаяЕдиницаПриИзменении(СтруктураДанные);
					Объект.СтруктурнаяЕдиницаЗапасов = СтруктураДанные.СтруктурнаяЕдиницаЗапасов;
					Объект.ЯчейкаЗапасов = СтруктураДанные.ЯчейкаЗапасов;
				КонецЕсли; 
				Если НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаЗапасов) Тогда
					Объект.СтруктурнаяЕдиницаЗапасов = Объект.СтруктурнаяЕдиница;
					Объект.ЯчейкаЗапасов = Справочники.Ячейки.ПустаяСсылка();
				КонецЕсли; 
			КонецЕсли; 
			ОрдерныйСклад = Объект.СтруктурнаяЕдиницаЗапасов.ОрдерныйСклад;
			Для каждого СтрокаТабличнойЧасти Из Объект[ИмяТЧ] Цикл
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаЗапасов;
				СтрокаТабличнойЧасти.Ячейка = Объект.ЯчейкаЗапасов;
				СтрокаТабличнойЧасти.ЯчейкаДоступна = НЕ ОрдерныйСклад И ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтруктурнаяЕдиница);
			КонецЦикла;
		КонецЕсли;
		Если РучноеРаспределение Тогда
			Если Объект.ВидОперации = Перечисления.ВидыОперацийСборкаЗапасов.Разборка Тогда
				Для каждого СтрокаТабличнойЧасти Из Объект.РаспределениеЗапасов Цикл
					СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаПродукции;
					СтрокаТабличнойЧасти.Ячейка = Объект.ЯчейкаПродукции;
				КонецЦикла;
			ИначеЕсли Объект.ПоложениеСклада <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда 
				Для каждого СтрокаТабличнойЧасти Из Объект.РаспределениеЗапасов Цикл
					СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаЗапасов;
					СтрокаТабличнойЧасти.Ячейка = Объект.ЯчейкаЗапасов;
				КонецЦикла;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	
	Если Объект.ПоложениеЗаказаПокупателя <> Результат.ПоложениеЗаказаПокупателяВДокументахПроизводства Тогда
		Объект.ПоложениеЗаказаПокупателя = Результат.ПоложениеЗаказаПокупателяВДокументахПроизводства;
		Если Объект.ПоложениеЗаказаПокупателя <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			Объект.ЗаказПокупателя = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(Объект.Продукция, "ЗаказПокупателя");
		КонецЕсли; 
		Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = Объект.ЗаказПокупателя;
		КонецЦикла; 
		Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = Объект.ЗаказПокупателя;
		КонецЦикла;
		Если Объект.РучноеРаспределение Тогда
			Для каждого СтрокаТабличнойЧасти Из Объект.РаспределениеЗапасов Цикл
				СтрокаТабличнойЧасти.ЗаказПокупателя = Объект.ЗаказПокупателя;
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли; 
	
	УстановитьВидимостьОтПользовательскихНастроек(ЭтотОбъект);
	
	Если Объект.РучноеРаспределение Тогда
		ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
		ВывестиОтметкиКонтроля(ЭтотОбъект);
	КонецЕсли;
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
	ЗаполнитьПризнакиИспользованияСерийПриИзмененииКлючевыхРеквизитов();
	
КонецПроцедуры

&НаКлиенте
Процедура Распределить(Команда)

	Если Объект.РаспределениеЗапасов.Количество() <> 0 Тогда

		ПоказатьВопрос(Новый ОписаниеОповещения("РаспределитьЗавершение", ЭтотОбъект), НСтр(
			"ru = 'Табличная часть ""Распределение"" будет перезаполнена. Продолжить операцию?'"),
			РежимДиалогаВопрос.ДаНет, 0);
		Возврат;

	КонецЕсли;

	РаспределитьФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура РаспределитьЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Ответ = Результат;

	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	РаспределитьФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура РаспределитьФрагмент()

	РаспределитьСервер();
	
	ОбновитьРаспределениеЗапасовНаФорме();

КонецПроцедуры

// Процедура - обработчик команды командной панели табличной части.
//
&НаКлиенте
Процедура ЗаполнитьПоСпецификации(Команда)
	
	Если Объект.Запасы.Количество() <> 0 Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоСпецификацииЗавершение", ЭтотОбъект), НСтр("ru = 'Табличная часть ""Материалы"" будет перезаполнена. Продолжить выполнение операции?'"), 
		РежимДиалогаВопрос.ДаНет, 0);
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьПоСпецификацииФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСпецификацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоСпецификацииФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСпецификацииФрагмент()
	
	ЗаполнитьПоСпецификацииНаСервере();
	
КонецПроцедуры // КомандаЗаполнитьПоСпецификации()

&НаКлиенте
Процедура ЗаполнитьПоРаспределению(Команда)
	
	Если Объект.Запасы.Количество() <> 0 Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоРаспределениюЗавершение", ЭтотОбъект), НСтр("ru = 'Табличная часть ""Материалы"" будет перезаполнена. Продолжить выполнение операции?'"), 
							РежимДиалогаВопрос.ДаНет, 0);
        Возврат;
		
	КонецЕсли;
	
	ЗаполнитьПоРаспределениюФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоРаспределениюЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Ответ = Результат;
    
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    ЗаполнитьПоРаспределениюФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоРаспределениюФрагмент()
    
    ЗаполнитьПоРаспределениюСервер();
	
КонецПроцедуры // ЗаполнитьПоРаспределению()

&НаКлиенте
Процедура ЗаполнитьПоОстаткам(Команда)
	
	Если Объект.Запасы.Количество() <> 0 Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоОстаткамЗавершение", ЭтотОбъект), НСтр("ru = 'Табличная часть ""Материалы"" будет перезаполнена. Продолжить выполнение операции?'"), 
							РежимДиалогаВопрос.ДаНет, 0);
        Возврат;
		
	КонецЕсли;
	
	ЗаполнитьПоОстаткамФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткамЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Ответ = Результат;
    
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    ЗаполнитьПоОстаткамФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткамФрагмент()
    
    ЗаполнитьПоОстаткамСервер();
	
КонецПроцедуры // ЗаполнитьПоРаспределению()

&НаКлиенте
Процедура ДобавитьИзЗаказов(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ДобавитьИзЗаказовЗавершение", ЭтотОбъект);
	
	СтруктураОткрытия = Новый Структура;
	СтруктураОткрытия.Вставить("РежимВыбора", Истина);
	СтруктураОткрытия.Вставить("МножественныйВыбор", Истина);
	СтруктураОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
	ОткрытьФорму("Документ.ЗаказПокупателя.ФормаВыбора", СтруктураОткрытия, ЭтотОбъект, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзЗаказовЗавершение(Заказы, ДополнительныеДанные) Экспорт
	
	Если Заказы=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТабличнойЧасти = "Продукция";
	
	КоличествоСтрок = Объект.Продукция.Количество();
	
	ДобавитьПродукциюИзЗаказов(Заказы);
	
	ЕстьСпецификации = Ложь;
	Для ии = КоличествоСтрок + 1 По Объект.Продукция.Количество() Цикл
		СтрокаТабличнойЧасти = Объект.Продукция[ии - 1];
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
			ЕстьСпецификации = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла;  
	
	Если ЕстьСпецификации Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
	КонецЕсли; 
	ОбновитьСпискиВыбораРаспределение();
	ОбновитьЗаголовокКомандыЗаполнитьПоОстаткам(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура РежимГТД(Команда)
	
	Если РежимОстаткиИРезервы Тогда
		
		Элементы.ОстаткиИРезервы.Пометка = Ложь;
		РежимОстаткиИРезервы = Ложь;
		ОбновитьОтображениеКолонокВРазрезеЗапасов();
		
	КонецЕсли;
	
	ЭтоТЧМатериалы = (Команда.Имя = "РежимГТДМатериалы");
	Если ЭтоТЧМатериалы Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РежимГТДМатериалы", "Пометка", НЕ Элементы.РежимГТДМатериалы.Пометка);
		
	КонецЕсли;
	
	ЭтоТЧПродукция = (Команда.Имя = "РежимГТДПродукция");
	Если ЭтоТЧПродукция Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РежимГТДПродукция", "Пометка", НЕ Элементы.РежимГТДПродукция.Пометка);
		
	КонецЕсли;
	
	ВключитьРежимГТД = ?(ЭтоТЧМатериалы, Элементы.РежимГТДМатериалы.Пометка, Элементы.РежимГТДПродукция.Пометка);
	ИзменитьРежимРаботаГТД(ВключитьРежимГТД, ЭтоТЧМатериалы, ЭтоТЧПродукция);
	
КонецПроцедуры

&НаКлиенте
Процедура НомераГТДЗаполнитьПоФактическимОстаткам(Команда)
	
	ЭтоТЧПродукция	= (Команда.Имя = "НомераГТДЗаполнитьПоФактическимОстаткамПродукция");
	ЭтоТЧМатериалы	= (Команда.Имя = "НомераГТДЗаполнитьПоФактическимОстаткамМатериалы");
	
	НомераГТДЗаполнитьПоФактическимОстаткамНаСервере(ЭтоТЧПродукция, ЭтоТЧМатериалы);
	
КонецПроцедуры

&НаКлиенте
Процедура НомераГТДПодобрать(Команда)
	
	ЭтоТЧМатериалы = (Команда.Имя = "НомераГТДПодобратьМатериалы");
	ЭтоТЧПродукция = (Команда.Имя = "НомераГТДПодобратьПродукция");
	
	НомераГТДПодобратьНаСервере(ЭтоТЧПродукция, ЭтоТЧМатериалы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьНомераГТДИСтраныПроисхождения(Команда)
	
	ЭтоТЧМатериалы	= (Команда.Имя = "ОчиститьНомераГТДИСтраныПроисхожденияМатериалы");
	
	ИмяТЧ = ?(ЭтоТЧМатериалы = Истина, "Запасы", "Продукция");
	
	НомераГТДОчиститьНомераИСтраныПроисхожденияНаСервере(ИмяТЧ);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДополнительныйРеквизит(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущийНаборСвойств", ПредопределенноеЗначение("Справочник.НаборыДополнительныхРеквизитовИСведений.Документ_СборкаЗапасов"));
	ПараметрыФормы.Вставить("ЭтоДополнительноеСведение", Ложь);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта", ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#Область ИнтеграцияИС

// ИнтеграцияИС

// @skip-warning
&НаКлиенте
Процедура Подключаемый_ПолеИнтеграцииИСОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ИнтеграцияИСКлиент.ОбработкаНавигационнойСсылкиВФормеДокументаОснования(
		ЭтотОбъект,
		Объект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры
// Конец ИнтеграцияИС

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПризнакиИспользованияСерийПриИзмененииКлючевыхРеквизитов()
	ДополнительныеСвойства = Новый Структура("ИзменениеКлючевыхРеквизитов", Истина);
	СерииНоменклатурыУНФ.ЗаполнитьПризнакиИспользованияСерий(Объект, ДополнительныеСвойства);
КонецПроцедуры

&НаСервере
Процедура ОбновитьНастройкиФормыВыбораНоменклатурыПриИзмененииРеквизита()
	НоменклатураВДокументахСервер.ОбновитьНастройкиФормыВыбораНоменклатурыПриИзмененииРеквизита(ЭтотОбъект,
		"СборкаЗапасовСписание", "Запасы", НастройкиФормыВыбораНоменклатуры);
	НоменклатураВДокументахСервер.ОбновитьНастройкиФормыВыбораНоменклатурыПриИзмененииРеквизита(ЭтотОбъект,
		"СборкаЗапасовОприходование", "Продукция", НастройкиФормыВыбораНоменклатуры);
	НоменклатураВДокументахСервер.ОбновитьНастройкиФормыВыбораНоменклатурыПриИзмененииРеквизита(ЭтотОбъект,
		"СборкаЗапасовОприходование", "Отходы", НастройкиФормыВыбораНоменклатуры);
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзмененияРеквизитовПечати(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		ПечатьДокументовУНФКлиент.ОбновитьЗначенияРеквизитовПечати(ЭтотОбъект, Результат.ИзмененныеРеквизиты);
		
		Если Результат.Свойство("Команда") Тогда
			
			Подключаемый_ВыполнитьКоманду(Результат.Команда);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет Запасы по спецификации.
//
&НаСервере
Процедура ЗаполнитьПоСпецификацииНаСервере()
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ЗаполнитьТабличнуюЧастьПоСпецификации();
	ЗначениеВРеквизитФормы(Документ, "Объект");
	
	// Прослеживаемость
	ОбновитьОтображениеПрослеживаемости(); 
	// Конец Прослеживаемость
	
	ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	ЗаполнитьПризнакиИспользованияЭтапов();
	
	ЗапасыНеЗаполнены = Ложь;
	ВывестиОтметкиКонтроля(ЭтотОбъект);
	ОбновитьЭтапыНаФорме(ЭтотОбъект);
	ОбновитьДоступностьЯчеек();
	ЗаполнитьПризнакиДоступностиЯчеек(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет Запасы по остаткам.
//
&НаСервере
Процедура ЗаполнитьПоОстаткамСервер()
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ЗаполнитьТабличнуюЧастьПоОстаткам();
	ЗначениеВРеквизитФормы(Документ, "Объект");
	
	// Прослеживаемость
	ОбновитьОтображениеПрослеживаемости(); 
	// Конец Прослеживаемость
	
	ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	ЗаполнитьПризнакиИспользованияЭтапов();
	
	ЗапасыНеЗаполнены = Ложь;
	ВывестиОтметкиКонтроля(ЭтотОбъект);
	ОбновитьЭтапыНаФорме(ЭтотОбъект);
	ОбновитьДоступностьЯчеек();
	ЗаполнитьПризнакиДоступностиЯчеек(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

// Получает набор данных с сервера для процедуры ДатаПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеДатаПриИзменении(ДокументСсылка, ДатаНовая, ДатаПередИзменением)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("РазностьДат", ДокументыУНФ.ПроверитьНомерДокумента(ДокументСсылка, ДатаНовая, ДатаПередИзменением));
	
	// Прослеживаемость
	КэшЗначений.Вставить("ВестиУчетПрослеживаемыхТоваров", ПрослеживаемостьУНФ.ВедетсяУчетПрослеживаемыхТоваров(Объект.Дата));
	ОбновитьПризнакПрослеживаемости();
	// Конец Прослеживаемость
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДатаПриИзменении()

// Получает набор данных с сервера.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеОрганизацияПриИзменении(Организация)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Компания", Константы.УчетПоКомпании.Компания(Организация));
	СтруктураДанные.Вставить("ПодписьРуководителя", Организация.ПодписьРуководителя);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеОрганизацияПриИзменении()

// Получает набор данных с сервера для процедуры НоменклатураПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные)
	
	СтруктураДанные.Вставить("ЕдиницаИзмерения", СтруктураДанные.Номенклатура.ЕдиницаИзмерения);
	СтруктураДанные.Вставить("СтранаПроисхождения", СтруктураДанные.Номенклатура.СтранаПроисхождения);
	// Наборы
	СтруктураДанные.Вставить("ЭтоНабор", СтруктураДанные.Номенклатура.ЭтоНабор);
	
	Если Не СтруктураДанные.Свойство("Склад") Тогда
		СтруктураДанные.Вставить("Склад", СтруктураДанные.Номенклатура.Склад);
		СтруктураДанные.Вставить("Ячейка", СтруктураДанные.Номенклатура.Ячейка);
	КонецЕсли;

	// Характеристики
	СтруктураДанные.Вставить("ИспользоватьХарактеристики",Ложь);
	СтруктураДанные.Вставить("ПроверятьЗаполнениеХарактеристики",Ложь);
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура) 
		И СтруктураДанные.Номенклатура.ИспользоватьХарактеристики Тогда
		ЗначенияПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура);
		
		Если Не ЗначенияПоУмолчанию = Неопределено Тогда
			ХарактеристикаПоУмолчанию = ЗначенияПоУмолчанию;
		КонецЕсли;
		
		Если НЕ СтруктураДанные.Свойство("Характеристика") Тогда
			СтруктураДанные.Вставить("Характеристика",ХарактеристикаПоУмолчанию);
		Иначе
			СтруктураДанные.Характеристика = ?(ЗначениеЗаполнено(СтруктураДанные.Характеристика), СтруктураДанные.Характеристика, ХарактеристикаПоУмолчанию);
		КонецЕсли;
		
		СтруктураДанные.Вставить("ИспользоватьХарактеристики",Истина);
		СтруктураДанные.Вставить("ПроверятьЗаполнениеХарактеристики",СтруктураДанные.Номенклатура.ПроверятьЗаполнениеХарактеристики);
	КонецЕсли; 
	// Конец Характеристики
	
	//Партии
	СтруктураДанные.Вставить("ИспользоватьПартии",Ложь);
	СтруктураДанные.Вставить("ПроверятьЗаполнениеПартий",Ложь);
	
	Если НЕ СтруктураДанные.Свойство("Партия") Тогда
		СтруктураДанные.Вставить("Партия", Неопределено);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура) 
		И СтруктураДанные.Номенклатура.ИспользоватьПартии Тогда
		
		Если СтруктураДанные.Свойство("СтатусПартии") Тогда
			ЗначенияПартииПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияПартийНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура,СтруктураДанные.СтатусПартии);
		Иначе
			Если Не СтруктураДанные.Свойство("ВидОперации") Тогда
				ВидОперации = Неопределено
			Иначе
				ВидОперации = СтруктураДанные.ВидОперации
			КонецЕсли;
			
			СтатусПартии = НоменклатураВДокументахСервер.СоответствиеВидаОперацииИлиХозОперацииСтатусуПартии(, ВидОперации);
			ЗначенияПартииПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияПартийНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура,СтатусПартии);
		КонецЕсли;
		
		ПартияПоУмолчанию = Справочники.ПартииНоменклатуры.ПустаяСсылка();
		
		Если Не ЗначенияПартииПоУмолчанию = Неопределено Тогда
			ПартияПоУмолчанию = ЗначенияПартииПоУмолчанию;
		КонецЕсли;
		
		СтруктураДанные.Партия = ?(ЗначениеЗаполнено(СтруктураДанные.Партия), СтруктураДанные.Партия, ПартияПоУмолчанию);
		
		СтруктураДанные.ПроверятьЗаполнениеПартий = СтруктураДанные.Номенклатура.ПроверятьЗаполнениеПартий;
		СтруктураДанные.ИспользоватьПартии = Истина;
		
	КонецЕсли;
	// Конец Партии
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура) И СтруктураДанные.Номенклатура.ИспользоватьСерииНоменклатуры Тогда
		СтруктураДанные.Вставить("СтатусыСерийНоменклатуры", СерииНоменклатурыУНФ.СтатусСерийНоменклатурыПриИзменении(СтруктураДанные));
	Иначе
		СтруктураДанные.Вставить("СтатусыСерийНоменклатуры", 0);
	КонецЕсли;
	
	Если СтруктураДанные.Свойство("Характеристика") Тогда
		СтруктураДанные.Вставить("Спецификация", Справочники.Спецификации.СпецификацияПоУмолчанию(
			СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	Иначе
		СтруктураДанные.Вставить("Спецификация", Справочники.Спецификации.СпецификацияПоУмолчанию(
			СтруктураДанные.Номенклатура));
	КонецЕсли;
	СтруктураДанные = ПолучитьДанныеСпецификацияПриИзменении(СтруктураДанные);
	
	// Прослеживаемость
	СтруктураДанные.Вставить("ПрослеживаемыйТовар", СтруктураДанные.Номенклатура.ПрослеживаемыйТовар 
		И СтруктураДанные.Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Запас);
	СтруктураДанные.Вставить("НомерГТД", Неопределено);
	// Конец Прослеживаемость
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеНоменклатураПриИзменении()

// Получает набор данных с сервера для процедуры ХарактеристикаПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеХарактеристикаПриИзменении(СтруктураДанные)
	
	СтруктураДанные.Вставить("Спецификация", Справочники.Спецификации.СпецификацияПоУмолчанию(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	СтруктураДанные = ПолучитьДанныеСпецификацияПриИзменении(СтруктураДанные);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеХарактеристикаПриИзменении()

// Получает набор данных с сервера для процедуры СтруктурнаяЕдиницаПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеСтруктурнаяЕдиницаПриИзменении(СтруктураДанные)
	
	Если СтруктураДанные.Подразделение.ПолучательПеремещения.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Склад
		ИЛИ СтруктураДанные.Подразделение.ПолучательПеремещения.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Подразделение Тогда
		
		СтруктураДанные.Вставить("СтруктурнаяЕдиницаПродукции", СтруктураДанные.Подразделение.ПолучательПеремещения);
		СтруктураДанные.Вставить("ЯчейкаПродукции", СтруктураДанные.Подразделение.ЯчейкаПолучателяПеремещения);
		СтруктураДанные.Вставить("СтруктурнаяЕдиницаПродукцииПодписьМОЛ", СтруктураДанные.Подразделение.ПолучательПеремещения.ПодписьМОЛ);
		
	Иначе
		
		СтруктураДанные.Вставить("СтруктурнаяЕдиницаПродукции", Неопределено);
		СтруктураДанные.Вставить("ЯчейкаПродукции", Неопределено);
		СтруктураДанные.Вставить("СтруктурнаяЕдиницаПродукцииПодписьМОЛ", СтруктураДанные.Подразделение.ПодписьМОЛ);
		
	КонецЕсли;
	
	Если СтруктураДанные.Подразделение.ИсточникПеремещения.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Склад
		ИЛИ СтруктураДанные.Подразделение.ИсточникПеремещения.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Подразделение Тогда
		
		СтруктураДанные.Вставить("СтруктурнаяЕдиницаЗапасов", СтруктураДанные.Подразделение.ИсточникПеремещения);
		СтруктураДанные.Вставить("ЯчейкаЗапасов", СтруктураДанные.Подразделение.ЯчейкаИсточникаПеремещения);
		СтруктураДанные.Вставить("СтруктурнаяЕдиницаЗапасовПодписьМОЛ", СтруктураДанные.Подразделение.ИсточникПеремещения.ПодписьМОЛ);
		
	Иначе
		
		СтруктураДанные.Вставить("СтруктурнаяЕдиницаЗапасов", Неопределено);
		СтруктураДанные.Вставить("ЯчейкаЗапасов", Неопределено);
		СтруктураДанные.Вставить("СтруктурнаяЕдиницаЗапасовПодписьМОЛ", СтруктураДанные.Подразделение.ПодписьМОЛ);
		
	КонецЕсли;
	
	СтруктураДанные.Вставить("СтруктурнаяЕдиницаОтходов", СтруктураДанные.Подразделение.ПолучательОтходов);
	СтруктураДанные.Вставить("ЯчейкаОтходов", СтруктураДанные.Подразделение.ЯчейкаПолучателяОтходов);
	
	СтруктураДанные.Вставить("ОрдерныйСклад", СтруктураДанные.Подразделение.ОрдерныйСклад 
	И СтруктураДанные.Подразделение.ТипСтруктурнойЕдиницы=Перечисления.ТипыСтруктурныхЕдиниц.Склад);
	СтруктураДанные.Вставить("ОрдерныйСкладПродукции", СтруктураДанные.Подразделение.ПолучательПеремещения.ОрдерныйСклад 
	И СтруктураДанные.Подразделение.ПолучательПеремещения.ТипСтруктурнойЕдиницы=Перечисления.ТипыСтруктурныхЕдиниц.Склад);
	СтруктураДанные.Вставить("ОрдерныйСкладОтходов", СтруктураДанные.Подразделение.ПолучательОтходов.ОрдерныйСклад 
	И СтруктураДанные.Подразделение.ПолучательОтходов.ТипСтруктурнойЕдиницы=Перечисления.ТипыСтруктурныхЕдиниц.Склад);
	СтруктураДанные.Вставить("ОрдерныйСкладЗапасов", СтруктураДанные.Подразделение.ИсточникПеремещения.ОрдерныйСклад 
	И СтруктураДанные.Подразделение.ИсточникПеремещения.ТипСтруктурнойЕдиницы=Перечисления.ТипыСтруктурныхЕдиниц.Склад);
	СтруктураДанные.Вставить("ЭтоСборка", (СтруктураДанные.ВидОперации = Перечисления.ВидыОперацийСборкаЗапасов.Сборка));
	СтруктураДанные.Вставить("ПодписьКонтролера", СтруктураДанные.Подразделение.ПодписьМОЛ);
	СтруктураДанные.Вставить("ТипСтруктурнойЕдиницы", СтруктураДанные.Подразделение.ТипСтруктурнойЕдиницы);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеСтруктурнаяЕдиницаПриИзменении()

// Получает набор данных с сервера для процедуры ЯчейкаПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеЯчейкаПриИзменении(СтруктураДанные)
	
	Если СтруктураДанные.СтруктурнаяЕдиница = СтруктураДанные.СтруктурнаяЕдиницаПродукции Тогда
		
		Если СтруктураДанные.СтруктурнаяЕдиница.ПолучательПеремещения <> СтруктураДанные.СтруктурнаяЕдиницаПродукции
			ИЛИ СтруктураДанные.СтруктурнаяЕдиница.ЯчейкаПолучателяПеремещения <> СтруктураДанные.ЯчейкаПродукции Тогда
			
			СтруктураДанные.Вставить("НоваяЯчейкаПродукции", СтруктураДанные.Ячейка);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтруктураДанные.СтруктурнаяЕдиница = СтруктураДанные.СтруктурнаяЕдиницаЗапасов Тогда
		
		Если СтруктураДанные.СтруктурнаяЕдиница.ИсточникПеремещения <> СтруктураДанные.СтруктурнаяЕдиницаЗапасов
			ИЛИ СтруктураДанные.СтруктурнаяЕдиница.ЯчейкаИсточникаПеремещения <> СтруктураДанные.ЯчейкаЗапасов Тогда
			
			СтруктураДанные.Вставить("НоваяЯчейкаЗапасов", СтруктураДанные.Ячейка);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтруктураДанные.СтруктурнаяЕдиница = СтруктураДанные.СтруктурнаяЕдиницаОтходов Тогда
		
		Если СтруктураДанные.СтруктурнаяЕдиница.ПолучательОтходов <> СтруктураДанные.СтруктурнаяЕдиницаОтходов
			ИЛИ СтруктураДанные.СтруктурнаяЕдиница.ЯчейкаПолучателяОтходов <> СтруктураДанные.ЯчейкаОтходов Тогда
			
			СтруктураДанные.Вставить("НоваяЯчейкаОтходов", СтруктураДанные.Ячейка);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеЯчейкаПриИзменении()

// Получает набор данных с сервера для процедуры СтруктурнаяЕдиницаПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеСтруктурнаяЕдиницаПродукцияЗапасыОтходыПриИзменении(СтруктураДанные)
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтруктураДанные.Склад, "ОрдерныйСклад, ТипСтруктурнойЕдиницы, ПодписьМОЛ");
	
	СтруктураДанные.Вставить("ОрдерныйСклад", ЗначенияРеквизитов.ОрдерныйСклад = Истина 
	И ЗначенияРеквизитов.ТипСтруктурнойЕдиницы=Перечисления.ТипыСтруктурныхЕдиниц.Склад);
	СтруктураДанные.Вставить("ПодписьМОЛ", ЗначенияРеквизитов.ПодписьМОЛ);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеСтруктурнаяЕдиницаПродукцияЗапасыОтходыПриИзменении()

// Процедура вызывает обработку заполнения документа по основанию.
//
&НаСервере
Процедура ЗаполнитьПоДокументу(РеквизитОснование = "ДокументОснование")
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Заполнить(Объект[РеквизитОснование]);
	ЗначениеВРеквизитФормы(Документ, "Объект");
	
	// Прослеживаемость
	ОбновитьОтображениеПрослеживаемости(); 
	// Конец Прослеживаемость
	
	ТипСтруктурнойЕдиницы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СтруктурнаяЕдиница, "ТипСтруктурнойЕдиницы");
	РучноеРаспределение = Объект.РучноеРаспределение;

	УправлениеФормой(ЭтотОбъект);
	
	УстановитьВидимостьОтПользовательскихНастроек(ЭтотОбъект);
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	ЗаполнитьПризнакиИспользованияЭтапов();
	
	Если Объект.РучноеРаспределение Тогда
		ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	КонецЕсли; 
	ОбновитьЭтапыНаФорме(ЭтотОбъект);
	ОбновитьДоступностьЯчеек();
	ЗаполнитьПризнакиДоступностиЯчеек(ЭтотОбъект);
	
КонецПроцедуры // ЗаполнитьПоДокументу()

// ПодключаемоеОборудование
// Процедура получает данные по штрихкодам.
//
&НаСервереБезКонтекста
Процедура ПолучитьДанныеПоШтрихКодам(СтруктураДанные)
	
	// Преобразование весовых штрихкодов.
	Для каждого ТекШтрихкод Из СтруктураДанные.МассивШтрихкодов Цикл
		
		РегистрыСведений.ШтрихкодыНоменклатуры.ПреобразоватьВесовойШтрихкод(ТекШтрихкод);
		
	КонецЦикла;
	
	ДанныеПоШтрихКодам = РегистрыСведений.ШтрихкодыНоменклатуры.ПолучитьДанныеПоШтрихкодамВМассиве(СтруктураДанные.МассивШтрихкодов);
	
	Для каждого ТекШтрихкод Из СтруктураДанные.МассивШтрихкодов Цикл
		
		МассивДанныхШтрихкода = ДанныеПоШтрихкодам[ТекШтрихкод.Штрихкод];
		
		Если МассивДанныхШтрихкода <> Неопределено
			И МассивДанныхШтрихкода.Количество() <> 0 Тогда
			
			Для Каждого ДанныеШтрихкода Из МассивДанныхШтрихкода Цикл
				
				СтруктураДанныеНоменклатуры = Новый Структура();
				СтруктураДанныеНоменклатуры.Вставить("Номенклатура", ДанныеШтрихкода.Номенклатура);
				СтруктураДанныеНоменклатуры.Вставить("ТипНоменклатуры", ДанныеШтрихкода.Номенклатура.ТипНоменклатуры);
				СтруктураДанныеНоменклатуры.Вставить("Характеристика", ДанныеШтрихкода.Характеристика);
				ДанныеШтрихкода.Вставить("СтруктураДанныеНоменклатуры", ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанныеНоменклатуры));
				
				Если НЕ ЗначениеЗаполнено(ДанныеШтрихкода.ЕдиницаИзмерения) Тогда
					ДанныеШтрихкода.ЕдиницаИзмерения  = ДанныеШтрихкода.Номенклатура.ЕдиницаИзмерения;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураДанные.Вставить("ДанныеПоШтрихКодам", ДанныеПоШтрихКодам);
	
	Для каждого парам Из Метаданные.Документы.СборкаЗапасов.ТабличныеЧасти.Запасы.Реквизиты.Номенклатура.ПараметрыВыбора Цикл
		Если парам.Имя = "Отбор.ТипНоменклатуры" Тогда
			Если ТипЗнч(парам.Значение)=Тип("ФиксированныйМассив") Тогда
				СтруктураДанные.Вставить("ОтборТипНоменклатуры", парам.Значение);
			Иначе
			    МассивТипов = Новый Массив;
				МассивТипов.Добавить(парам.Значение);
				СтруктураДанные.Вставить("ОтборТипНоменклатуры", МассивТипов);
			КонецЕсли;
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПолучитьДанныеПоШтрихКодам()

&НаКлиенте
Функция ЗаполнитьПоДаннымШтрихкодов(ДанныеШтрихкодов)
	
	ИмяТекущейСтраницы = Элементы.Страницы.ТекущаяСтраница.Имя;
	Если ИмяТекущейСтраницы = "ТЧПродукция" Тогда
		ИмяТабличнойЧасти = "Продукция";
		ИмяТабличнойЧастиСерииНоменклатуры = "СерииНоменклатурыПродукция";
	ИначеЕсли ИмяТекущейСтраницы = "ТЧЗапасы" Тогда
		ИмяТабличнойЧасти = "Запасы";
		ИмяТабличнойЧастиСерииНоменклатуры = "СерииНоменклатуры";
	Иначе
		Возврат Новый Структура("НеизвестныеШтрихкоды, ШтрихкодыНекорректногоТипа", Новый Массив, Новый Массив);
	КонецЕсли;
	
	НеизвестныеШтрихкоды = Новый Массив;
	ШтрихкодыНекорректногоТипа = Новый Массив;
	
	Если ТипЗнч(ДанныеШтрихкодов) = Тип("Массив") Тогда
		МассивШтрихкодов = ДанныеШтрихкодов;
	Иначе
		МассивШтрихкодов = Новый Массив;
		МассивШтрихкодов.Добавить(ДанныеШтрихкодов);
	КонецЕсли;
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("МассивШтрихкодов", МассивШтрихкодов);
	ПолучитьДанныеПоШтрихКодам(СтруктураДанные);
	
	Для каждого ТекШтрихкод Из СтруктураДанные.МассивШтрихкодов Цикл
		ДанныеШтрихкода = СтруктураДанные.ДанныеПоШтрихкодам[ТекШтрихкод.Штрихкод];
		
		Если ДанныеШтрихкода.Количество() > 1 Тогда
			
			ПараметрыОткрытия = Новый Структура("МассивНоменклатуры, ТекШтрихкод", ДанныеШтрихкода, ТекШтрихкод);
			ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗаполнитьПоДаннымШтрихкодовЗавершение"
			, ЭтотОбъект, Новый Структура("СтруктураДанные, ИмяТабличнойЧастиСерииНоменклатуры", СтруктураДанные, ИмяТабличнойЧастиСерииНоменклатуры));
			
			ОткрытьФорму("РегистрСведений.ШтрихкодыНоменклатуры.Форма.ДублиНоменклатурыПоШтрихКодам", ПараметрыОткрытия, ЭтаФорма,,,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
			Продолжить;
		КонецЕсли;
		
		Если ДанныеШтрихкода <> Неопределено
		   И ДанныеШтрихкода.Количество() = 0 Тогда
			НеизвестныеШтрихкоды.Добавить(ТекШтрихкод);
		ИначеЕсли СтруктураДанные.ОтборТипНоменклатуры.Найти(ДанныеШтрихкода[0].СтруктураДанныеНоменклатуры.ТипНоменклатуры) = Неопределено 
			ИЛИ ДанныеШтрихкода[0].СтруктураДанныеНоменклатуры.ЭтоНабор Тогда
			ШтрихкодыНекорректногоТипа.Добавить(Новый Структура("Штрихкод,Номенклатура,ТипНоменклатуры,ЭтоНабор"
			, ТекШтрихкод.Штрихкод, ДанныеШтрихкода[0].Номенклатура, ДанныеШтрихкода[0].СтруктураДанныеНоменклатуры.ТипНоменклатуры, ДанныеШтрихкода[0].СтруктураДанныеНоменклатуры.ЭтоНабор));
		Иначе
			
			ДанныеШтрихкода = ДанныеШтрихкода[0];
			
			МассивСтрокТЧ = Объект[ИмяТабличнойЧасти].НайтиСтроки(Новый Структура("Номенклатура,Характеристика,ЕдиницаИзмерения",ДанныеШтрихкода.Номенклатура,ДанныеШтрихкода.Характеристика,ДанныеШтрихкода.ЕдиницаИзмерения));
			Если МассивСтрокТЧ.Количество() = 0 Тогда
				НоваяСтрока = Объект[ИмяТабличнойЧасти].Добавить();
				ЗаполнитьДанныеШапки(Объект, НоваяСтрока, ИмяТабличнойЧасти);
				НоваяСтрока.Номенклатура = ДанныеШтрихкода.Номенклатура;
				НоваяСтрока.Характеристика = ДанныеШтрихкода.Характеристика;
				НоваяСтрока.Партия = ДанныеШтрихкода.Партия;
				НоваяСтрока.Количество = ТекШтрихкод.Количество;
				НоваяСтрока.ЕдиницаИзмерения = ?(ЗначениеЗаполнено(ДанныеШтрихкода.ЕдиницаИзмерения), ДанныеШтрихкода.ЕдиницаИзмерения, ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ЕдиницаИзмерения);
				Если НоваяСтрока.Свойство("Спецификация") Тогда
					НоваяСтрока.Спецификация = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.Спецификация;
				КонецЕсли;
				Если НоваяСтрока.Свойство("ИспользоватьЭтапыПроизводства") Тогда
					НоваяСтрока.ИспользоватьЭтапыПроизводства = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ИспользоватьЭтапыПроизводства;
					Если НоваяСтрока.Свойство("ПодразделениеЗавершающегоЭтапа")
						И НоваяСтрока.ИспользоватьЭтапыПроизводства 
						И ТипСтруктурнойЕдиницы = ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение") Тогда
						НоваяСтрока.ПодразделениеЗавершающегоЭтапа = Объект.СтруктурнаяЕдиница;
					Иначе
						НоваяСтрока.ПодразделениеЗавершающегоЭтапа = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка");
					КонецЕсли;
				КонецЕсли;
				
				// Прослеживаемость
				НоваяСтрока.СтранаПроисхождения = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.СтранаПроисхождения;
				Если КэшЗначений.ВестиУчетПрослеживаемыхТоваров Тогда
					НоваяСтрока.ПрослеживаемыйТовар = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ПрослеживаемыйТовар;
					ПрослеживаемостьФормыКлиентСерверУНФ.ЗаполнитьПредставлениеРНПТ(ЭтотОбъект, НоваяСтрока);
				Иначе
					НоваяСтрока.ПрослеживаемыйТовар = Ложь;
				КонецЕсли;
				Если НЕ НоваяСтрока.ПрослеживаемыйТовар Тогда
					ДанныеШтрихкода.СтруктураДанныеНоменклатуры.Свойство("НомерГТД", НоваяСтрока.НомерГТД);
				КонецЕсли; 
				// Конец Прослеживаемость
				
				ТабличныеЧастиУНФКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект, НоваяСтрока);
				Элементы[ИмяТабличнойЧасти].ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
				Если ИмяТабличнойЧасти = "Запасы" Тогда
					НоваяСтрока.ДоляСтоимости = 1;
				КонецЕсли; 
			Иначе
				НоваяСтрока = МассивСтрокТЧ[0];
				НоваяСтрока.Количество = НоваяСтрока.Количество + ТекШтрихкод.Количество;
				Элементы[ИмяТабличнойЧасти].ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
			КонецЕсли;
			
			Если ДанныеШтрихкода.Свойство("Серия") И ЗначениеЗаполнено(ДанныеШтрихкода.Серия) Тогда
				СерииНоменклатурыУНФКлиентСервер.ДобавитьСерияВСтроку(НоваяСтрока, ДанныеШтрихкода.Серия, Объект, ИмяТабличнойЧастиСерииНоменклатуры, ИмяТабличнойЧасти);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый Структура("НеизвестныеШтрихкоды, ШтрихкодыНекорректногоТипа",НеизвестныеШтрихкоды, ШтрихкодыНекорректногоТипа);
	
КонецФункции // ЗаполнитьПоДаннымШтрихкодов()

&НаКлиенте
Процедура ЗаполнитьПоДаннымШтрихкодовЗавершение(СтруктураНоменклатуры, Параметры) Экспорт
	
	Если СтруктураНоменклатуры = Неопределено Тогда Возврат КонецЕсли;
	
	ДанныеШтрихкода = СтруктураНоменклатуры.МассивНоменклатуры[0];
	ТекШтрихкод = СтруктураНоменклатуры.ТекШтрихкод;
	ШтрихкодыНекорректногоТипа = Новый Массив;
	СтруктураДанные = Параметры.СтруктураДанные;
	ИмяТабличнойЧастиСерииНоменклатуры = Параметры.ИмяТабличнойЧастиСерииНоменклатуры;
	
	Если СтруктураДанные.ОтборТипНоменклатуры.Найти(ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ТипНоменклатуры) = Неопределено ИЛИ ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ЭтоНабор Тогда
		ШтрихкодыНекорректногоТипа.Добавить(Новый Структура("Штрихкод,Номенклатура,ТипНоменклатуры,ЭтоНабор", ТекШтрихкод.Штрихкод, ДанныеШтрихкода.Номенклатура, ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ТипНоменклатуры, ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ЭтоНабор));
	Иначе
		МассивСтрокТЧ = Объект[ИмяТабличнойЧасти].НайтиСтроки(Новый Структура("Номенклатура,Характеристика,ЕдиницаИзмерения",ДанныеШтрихкода.Номенклатура,ДанныеШтрихкода.Характеристика,ДанныеШтрихкода.ЕдиницаИзмерения));
		Если МассивСтрокТЧ.Количество() = 0 Тогда
			НоваяСтрока = Объект[ИмяТабличнойЧасти].Добавить();
			ЗаполнитьДанныеШапки(Объект, НоваяСтрока, ИмяТабличнойЧасти);
			НоваяСтрока.Номенклатура = ДанныеШтрихкода.Номенклатура;
			НоваяСтрока.Характеристика = ДанныеШтрихкода.Характеристика;
			НоваяСтрока.Партия = ДанныеШтрихкода.Партия;
			НоваяСтрока.Количество = ТекШтрихкод.Количество;
			НоваяСтрока.ЕдиницаИзмерения = ?(ЗначениеЗаполнено(ДанныеШтрихкода.ЕдиницаИзмерения), ДанныеШтрихкода.ЕдиницаИзмерения, ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ЕдиницаИзмерения);
			Если НоваяСтрока.Свойство("Спецификация") Тогда
				НоваяСтрока.Спецификация = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.Спецификация;
			КонецЕсли;
			Если НоваяСтрока.Свойство("ИспользоватьЭтапыПроизводства") Тогда
				НоваяСтрока.ИспользоватьЭтапыПроизводства = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ИспользоватьЭтапыПроизводства;
			КонецЕсли; 
			
			// Прослеживаемость
			НоваяСтрока.СтранаПроисхождения = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.СтранаПроисхождения;
			Если КэшЗначений.ВестиУчетПрослеживаемыхТоваров Тогда
				НоваяСтрока.ПрослеживаемыйТовар = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ПрослеживаемыйТовар;
				ПрослеживаемостьФормыКлиентСерверУНФ.ЗаполнитьПредставлениеРНПТ(ЭтотОбъект, НоваяСтрока);
			Иначе
				НоваяСтрока.ПрослеживаемыйТовар = Ложь;
			КонецЕсли;
			Если НЕ НоваяСтрока.ПрослеживаемыйТовар Тогда
				ДанныеШтрихкода.СтруктураДанныеНоменклатуры.Свойство("НомерГТД", НоваяСтрока.НомерГТД);
			КонецЕсли; 
			// Конец Прослеживаемость
			
			ТабличныеЧастиУНФКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект, НоваяСтрока);
			Элементы[ИмяТабличнойЧасти].ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
			Если ИмяТабличнойЧасти = "Запасы" Тогда
				НоваяСтрока.ДоляСтоимости = 1;
			КонецЕсли; 
		Иначе
			НоваяСтрока = МассивСтрокТЧ[0];
			НоваяСтрока.Количество = НоваяСтрока.Количество + ТекШтрихкод.Количество;
			Элементы[ИмяТабличнойЧасти].ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		КонецЕсли;
		
		Если ДанныеШтрихкода.Свойство("Серия") И ЗначениеЗаполнено(ДанныеШтрихкода.Серия) Тогда
			СерииНоменклатурыУНФКлиентСервер.ДобавитьСерияВСтроку(НоваяСтрока, ДанныеШтрихкода.Серия, Объект, ИмяТабличнойЧастиСерииНоменклатуры, ИмяТабличнойЧасти);
		КонецЕсли;
		
	КонецЕсли;
	
	ПолученыШтрихкодыНекорректногоТипа(ШтрихкодыНекорректногоТипа);
	ЗаполнитьПризнакиИспользованияХарактеристикСервер();;
	
КонецПроцедуры

// Процедура обрабатывает полученные штрихкоды.
//
&НаКлиенте
Процедура ПолученыШтрихкоды(ДанныеШтрихкодов) Экспорт
	
	Модифицированность = Истина;
	
	НеДобавленныеШтрихкоды		= ЗаполнитьПоДаннымШтрихкодов(ДанныеШтрихкодов);
	НеизвестныеШтрихкоды		= НеДобавленныеШтрихкоды.НеизвестныеШтрихкоды;
	ШтрихкодыНекорректногоТипа	= НеДобавленныеШтрихкоды.ШтрихкодыНекорректногоТипа;
	
	ПолученыШтрихкодыНекорректногоТипа(ШтрихкодыНекорректногоТипа);
	
	Если НеизвестныеШтрихкоды.Количество() > 0 Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПолученыШтрихкодыЗавершение", ЭтотОбъект, НеизвестныеШтрихкоды);
		
		ОткрытьФорму(
			"РегистрСведений.ШтрихкодыНоменклатуры.Форма.РегистрацияШтрихкодовНоменклатуры",
			Новый Структура("НеизвестныеШтрихкоды", НеизвестныеШтрихкоды), ЭтотОбъект,,,,Оповещение
		);
		
		Возврат;
		
	КонецЕсли;
	
	ПолученыШтрихкодыФрагмент(НеизвестныеШтрихкоды);
	
КонецПроцедуры // ПолученыШтрихкоды()

&НаКлиенте
Процедура ПолученыШтрихкодыЗавершение(ВозвращаемыеПараметры, Параметры) Экспорт
	
	НеизвестныеШтрихкоды = Параметры;
	
	Если ВозвращаемыеПараметры <> Неопределено Тогда
		
		МассивШтрихкодов = Новый Массив;
		
		Для каждого ЭлементМассива Из ВозвращаемыеПараметры.ЗарегистрированныеШтрихкоды Цикл
			МассивШтрихкодов.Добавить(ЭлементМассива);
		КонецЦикла;
		
		Для каждого ЭлементМассива Из ВозвращаемыеПараметры.ПолученыНовыеШтрихкоды Цикл
			МассивШтрихкодов.Добавить(ЭлементМассива);
		КонецЦикла;
		
		НеДобавленныеШтрихкоды		= ЗаполнитьПоДаннымШтрихкодов(МассивШтрихкодов);
		НеизвестныеШтрихкоды		= НеДобавленныеШтрихкоды.НеизвестныеШтрихкоды;
		ШтрихкодыНекорректногоТипа	= НеДобавленныеШтрихкоды.ШтрихкодыНекорректногоТипа;
		ПолученыШтрихкодыНекорректногоТипа(ШтрихкодыНекорректногоТипа);
	КонецЕсли;
	
	ПолученыШтрихкодыФрагмент(НеизвестныеШтрихкоды);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолученыШтрихкодыФрагмент(НеизвестныеШтрихкоды) Экспорт
	
	Для каждого ТекНеизвестныйШтрихкод Из НеизвестныеШтрихкоды Цикл
		
		СтрокаСообщения = НСтр("ru = 'Данные по штрихкоду не найдены: %1%; количество: %2%'");
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1%", ТекНеизвестныйШтрихкод.Штрихкод);
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%2%", ТекНеизвестныйШтрихкод.Количество);
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрокаСообщения);
		
	КонецЦикла;
	
	ЗаполнитьПризнакиИспользованияХарактеристикСервер();

	Если ИмяТабличнойЧасти="Продукция" Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
		ОбновитьСпискиВыбораРаспределение();
	ИначеЕсли ИмяТабличнойЧасти="Запасы" Тогда
		ВывестиОтметкиКонтроля(ЭтотОбъект);
	КонецЕсли;
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

// Проверяет,в зависимости от использования, заполнение характеристик номенклатуры
//
&НаСервере
Процедура ПроверитьЗаполнениеХарактеристикРаспределениеЗапасов(Отказ)
	
ТаблицаНоменклатуры = РаспределениеЗапасов.Выгрузить(,"Номенклатура");
ТаблицаНоменклатуры.Свернуть("Номенклатура");

СписокНоменклатуры = СписокНоменклатурыСХарактеристикамиРаспределениеЗапасов(ТаблицаНоменклатуры);
СписокНоменклатурыСПартиями = СписокНоменклатурыСПартиямиРаспределениеЗапасов(ТаблицаНоменклатуры);

Для Каждого СтрокаЗапасы Из РаспределениеЗапасов Цикл
	
	Если Не СписокНоменклатуры.НайтиПоЗначению(СтрокаЗапасы.Номенклатура) = Неопределено
		И Не ЗначениеЗаполнено(СтрокаЗапасы.Характеристика) Тогда
			ТекстСообщения = СтрШаблон(НСтр(
				"ru = 'В таблице ""РаспределениеЗапасов"" для номенклатуры %1 в строке %2, заполнение поля ""Характеристика"" является обязательным.'"),
				СтрокаЗапасы.Номенклатура, СтрокаЗапасы.НомерСтроки);
			КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("РаспределениеЗапасов",
				СтрокаЗапасы.НомерСтроки, "Характеристика");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , КонтекстноеПоле , , Отказ);
	КонецЕсли;
	
	Если Не СписокНоменклатурыСПартиями.НайтиПоЗначению(СтрокаЗапасы.Номенклатура) = Неопределено
		И Не ЗначениеЗаполнено(СтрокаЗапасы.Партия) Тогда
			ТекстСообщения = СтрШаблон(НСтр(
				"ru = 'В таблице ""РаспределениеЗапасов"" для номенклатуры %1 в строке %2, заполнение поля ""Партия"" является обязательным.'"),
				СтрокаЗапасы.Номенклатура, СтрокаЗапасы.НомерСтроки);
			КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("РаспределениеЗапасов",
				СтрокаЗапасы.НомерСтроки, "Партия");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , КонтекстноеПоле , , Отказ);
	КонецЕсли;
	
КонецЦикла;
	
КонецПроцедуры

// Получает список номенклатуры для которой ведется учет по характеристикам таб. части "РаспределениеЗапасов"
//
&НаСервере
Функция СписокНоменклатурыСХарактеристикамиРаспределениеЗапасов(ТаблицаНоменклатуры)
	
	СписокНоменклатуры = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры", ТаблицаНоменклатуры);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(ТаблицаНоменклатуры.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	&ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|ГДЕ
	|	ТаблицаНоменклатуры.Номенклатура.ИспользоватьХарактеристики
	|	И ТаблицаНоменклатуры.Номенклатура.ПроверятьЗаполнениеХарактеристики";
	
	МассивНоменклатуры = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Номенклатура");
	СписокНоменклатуры.ЗагрузитьЗначения(МассивНоменклатуры);
	
	Возврат СписокНоменклатуры;
	
КонецФункции

// Получает список номенклатуры для которой ведется учет по партиям таб. части "РаспределениеЗапасов"
//
&НаСервере
Функция СписокНоменклатурыСПартиямиРаспределениеЗапасов(ТаблицаНоменклатуры)
	
	СписокНоменклатуры = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры", ТаблицаНоменклатуры);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(ТаблицаНоменклатуры.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	&ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|ГДЕ
	|	ТаблицаНоменклатуры.Номенклатура.ИспользоватьПартии 
	|	И ТаблицаНоменклатуры.Номенклатура.ПроверятьЗаполнениеПартий";
	
	МассивНоменклатуры = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Номенклатура");
	СписокНоменклатуры.ЗагрузитьЗначения(МассивНоменклатуры);
	
	Возврат СписокНоменклатуры;

КонецФункции

&НаКлиенте
Процедура ПолученыШтрихкодыНекорректногоТипа(ШтрихкодыНекорректногоТипа) Экспорт
	
	Для каждого ТекНекорректныйШтрихкод Из ШтрихкодыНекорректногоТипа Цикл
		
		СтрокаСообщения = НСтр("ru = 'Найденная по штрихкоду %1% номенклатура -%2%- имеет тип %3%, который не подходит для этой табличной части'");
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1%", ТекНекорректныйШтрихкод.Штрихкод);
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%2%", ТекНекорректныйШтрихкод.Номенклатура);
		Если ТекНекорректныйШтрихкод.Свойство("ЭтоНабор") И ТекНекорректныйШтрихкод.ЭтоНабор Тогда
			// Наборы
			СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%3%", НСтр("ru = 'Набор'"));
		Иначе
			СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%3%", ТекНекорректныйШтрихкод.ТипНоменклатуры);
		КонецЕсли; 
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрокаСообщения);
		
	КонецЦикла;
	
КонецПроцедуры

// Конец ПодключаемоеОборудование

// Процедура заполняет колонку Резерв по резервам под заказ.
//
&НаСервере
Процедура ЗаполнитьКолонкуРезервПоРезервамНаСервере()
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ЗаполнитьКолонкуРезервПоРезервам();
	ЗначениеВРеквизитФормы(Документ, "Объект");
	
	// Прослеживаемость
	ОбновитьОтображениеПрослеживаемости(); 
	// Конец Прослеживаемость
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	ЗаполнитьПризнакиИспользованияЭтапов();
	
	Если Объект.РучноеРаспределение Тогда
		ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	КонецЕсли; 
	ОбновитьЭтапыНаФорме(ЭтотОбъект);
	ОбновитьДоступностьЯчеек();
	ЗаполнитьПризнакиДоступностиЯчеек(ЭтотОбъект);
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьКолонкуРезервПоРезервамНаСервере()

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыТЧПоШапке(Объект)

	// Заполнение склада
	Если Объект.ПоложениеСклада<>ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		ТЧ = ?(Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка"), Объект.Продукция, Объект.Запасы);
		Для каждого СтрокаТабличнойЧасти Из ТЧ Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаЗапасов;
			СтрокаТабличнойЧасти.Ячейка = Объект.ЯчейкаЗапасов;
		КонецЦикла; 
		ТЧ = ?(Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка"), Объект.Запасы, Объект.Продукция);
		Для каждого СтрокаТабличнойЧасти Из ТЧ Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаПродукции;
			СтрокаТабличнойЧасти.Ячейка = Объект.ЯчейкаПродукции;
		КонецЦикла; 
	КонецЕсли;
	// Заполнение заказа покупателя
	Если Объект.ПоложениеЗаказаПокупателя<>ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = Объект.ЗаказПокупателя;
		КонецЦикла; 
		Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = Объект.ЗаказПокупателя;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаПоКлючу(Таблица, КлючСвязи, ИмяПоля = "КлючСвязи")
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить(ИмяПоля, КлючСвязи);
	Строки = Таблица.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат Неопределено;
	Иначе
		Возврат Строки[0];
	КонецЕсли; 
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПризнакиИспользованияХарактеристикСервер()
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	Если Объект.РучноеРаспределение Тогда
		ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПродукциюИзЗаказов(Заказы)
	
	ИмяТабличнойЧасти = "Продукция";
	СоответствиеКлючейЗапасы = Новый Соответствие;
	СоответствиеКлючейМатериалы = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Заказы", Заказы);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяЗапасы.Характеристика КАК Характеристика,
	|	ЗаказПокупателяЗапасы.СерииНоменклатуры КАК СерииНоменклатуры,
	|	ЗаказПокупателяЗапасы.Количество - ЗаказПокупателяЗапасы.Резерв КАК Количество,
	|	ЗаказПокупателяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПокупателяЗапасы.Спецификация КАК Спецификация,
	|	ЗаказПокупателяЗапасы.Партия КАК Партия,
	|	ЗаказПокупателяЗапасы.Ссылка КАК ЗаказПокупателя,
	|	ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ЗаказПокупателяЗапасы.КлючСвязи КАК КлючСвязи,
	|	ИСТИНА КАК ЭтоЗапасы
	|ПОМЕСТИТЬ Запасы
	|ИЗ
	|	Документ.ЗаказПокупателя.Запасы КАК ЗаказПокупателяЗапасы
	|ГДЕ
	|	ЗаказПокупателяЗапасы.Количество - ЗаказПокупателяЗапасы.Резерв > 0
	|	И (ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ИЛИ ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|	И (ЗаказПокупателяЗапасы.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|			ИЛИ ЗаказПокупателяЗапасы.Номенклатура.СпособПополнения = ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство))
	|	И ЗаказПокупателяЗапасы.Ссылка В(&Заказы)
	|	И ЗаказПокупателяЗапасы.Ссылка.ОсновнойВариантКП = ЗаказПокупателяЗапасы.НомерВариантаКП
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказПокупателяМатериалы.Номенклатура,
	|	ЗаказПокупателяМатериалы.Характеристика,
	|	ЗаказПокупателяМатериалы.СерииНоменклатуры,
	|	ЗаказПокупателяМатериалы.Количество - ЗаказПокупателяМатериалы.Резерв,
	|	ЗаказПокупателяМатериалы.ЕдиницаИзмерения,
	|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка),
	|	ЗаказПокупателяМатериалы.Партия,
	|	ЗаказПокупателяМатериалы.Ссылка,
	|	ЗаказПокупателяМатериалы.Номенклатура.ТипНоменклатуры,
	|	ЗаказПокупателяМатериалы.КлючСвязи,
	|	ЛОЖЬ
	|ИЗ
	|	Документ.ЗаказПокупателя.Материалы КАК ЗаказПокупателяМатериалы
	|ГДЕ
	|	ЗаказПокупателяМатериалы.Количество - ЗаказПокупателяМатериалы.Резерв > 0
	|	И (ЗаказПокупателяМатериалы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ИЛИ ЗаказПокупателяМатериалы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|	И ЗаказПокупателяМатериалы.Номенклатура.СпособПополнения = ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство)
	|	И ЗаказПокупателяМатериалы.Ссылка В(&Заказы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Запасы.Номенклатура КАК Номенклатура,
	|	Запасы.Характеристика КАК Характеристика,
	|	Запасы.СерииНоменклатуры КАК СерииНоменклатуры,
	|	Запасы.Количество КАК Количество,
	|	Запасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Запасы.Спецификация КАК Спецификация,
	|	Запасы.Партия КАК Партия,
	|	Запасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	Запасы.ТипНоменклатуры КАК ТипНоменклатуры,
	|	Запасы.КлючСвязи КАК КлючСвязи,
	|	Запасы.ЭтоЗапасы КАК ЭтоЗапасы
	|ИЗ
	|	Запасы КАК Запасы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказПокупателяСерииНоменклатуры.Серия КАК Серия,
	|	ЗаказПокупателяСерииНоменклатуры.КлючСвязи КАК КлючСвязи
	|ИЗ
	|	Документ.ЗаказПокупателя.СерииНоменклатуры КАК ЗаказПокупателяСерииНоменклатуры
	|ГДЕ
	|	ЗаказПокупателяСерииНоменклатуры.КлючСвязи В
	|			(ВЫБРАТЬ
	|				Запасы.КлючСвязи
	|			ИЗ
	|				Запасы
	|			ГДЕ
	|				Запасы.ЭтоЗапасы)
	|	И ЗаказПокупателяСерииНоменклатуры.Ссылка В(&Заказы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказПокупателяСерииНоменклатурыМатериалы.Серия КАК Серия,
	|	ЗаказПокупателяСерииНоменклатурыМатериалы.КлючСвязи КАК КлючСвязи
	|ИЗ
	|	Документ.ЗаказПокупателя.СерииНоменклатурыМатериалы КАК ЗаказПокупателяСерииНоменклатурыМатериалы
	|ГДЕ
	|	ЗаказПокупателяСерииНоменклатурыМатериалы.КлючСвязи В
	|			(ВЫБРАТЬ
	|				Запасы.КлючСвязи
	|			ИЗ
	|				Запасы
	|			ГДЕ
	|				НЕ Запасы.ЭтоЗапасы)
	|	И ЗаказПокупателяСерииНоменклатурыМатериалы.Ссылка В(&Заказы)";
	Результат = Запрос.ВыполнитьПакет();
	
	МаксимумКлючСвязи = 0;
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		МаксимумКлючСвязи = Макс(МаксимумКлючСвязи, СтрокаТабличнойЧасти.КлючСвязи);
	КонецЦикла; 
	
	Выборка = Результат[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Продукция.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка, , "КлючСвязи");
		МаксимумКлючСвязи = МаксимумКлючСвязи + 1;
		НоваяСтрока.КлючСвязи = МаксимумКлючСвязи;
		Если Выборка.ЭтоЗапасы Тогда
			СоответствиеКлючейЗапасы.Вставить(Выборка.КлючСвязи, НоваяСтрока.КлючСвязи);
		Иначе
			СоответствиеКлючейМатериалы.Вставить(Выборка.КлючСвязи, НоваяСтрока.КлючСвязи);
		КонецЕсли; 
	КонецЦикла;
	
	// Серийный номера
	Выборка = Результат[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.СерииНоменклатурыПродукция.Добавить();
		НоваяСтрока.Серия = Выборка.Серия;
		НоваяСтрока.КлючСвязи = СоответствиеКлючейЗапасы.Получить(Выборка.КлючСвязи);
	КонецЦикла; 
	Выборка = Результат[3].Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.СерииНоменклатурыПродукция.Добавить();
		НоваяСтрока.Серия = Выборка.Серия;
		НоваяСтрока.КлючСвязи = СоответствиеКлючейМатериалы.Получить(Выборка.КлючСвязи);
	КонецЦикла; 
	
	ЗаполнитьПризнакиИспользованияХарактеристикСервер();
	ЗаполнитьПризнакиИспользованияЭтапов();

КонецПроцедуры

&НаСервере
Функция ПроверитьСоответствиеРеквизитовДокументаВЕТИС()
	
	МетаданныеОбъекта = Объект.Ссылка.Метаданные();
	
	СоответствиеПроверяемыхРеквизитов = Новый Соответствие;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийСборкаЗапасов.Сборка
		Тогда
		СоответствиеПроверяемыхРеквизитов.Вставить("Продукция", "СтруктурнаяЕдиницаПродукции" );
		СоответствиеПроверяемыхРеквизитов.Вставить("ПредставлениеПоляПродукция", "Получатель");
		СоответствиеПроверяемыхРеквизитов.Вставить("Запасы", "СтруктурнаяЕдиницаЗапасов");
		СоответствиеПроверяемыхРеквизитов.Вставить("ПредставлениеПоляЗапасы", "Списать из");
	Иначе
		СоответствиеПроверяемыхРеквизитов.Вставить("Продукция", "СтруктурнаяЕдиницаЗапасов");
		СоответствиеПроверяемыхРеквизитов.Вставить("ПредставлениеПоляПродукция", "Списать из");
		СоответствиеПроверяемыхРеквизитов.Вставить("Запасы", "СтруктурнаяЕдиницаПродукции");
		СоответствиеПроверяемыхРеквизитов.Вставить("ПредставлениеПоляЗапасы", "Списать из");
	КонецЕсли;
	
	СоответствиеПроверяемыхРеквизитов.Вставить("Отходы", "СтруктурнаяЕдиницаОтходов");
	СоответствиеПроверяемыхРеквизитов.Вставить("ПредставлениеПоляОтходы", "Получатель отходов");
	
	Для Каждого ТабличнаяЧасть Из МетаданныеОбъекта.ТабличныеЧасти Цикл
		
		ИмяТЧ = ТабличнаяЧасть.Имя;
		
		Если НЕ Объект[ИмяТЧ].Количество()
			Или ТабличнаяЧасть.Реквизиты.Найти("Номенклатура") = Неопределено
			Или ИмяТЧ = "РаспределениеЗапасов" Тогда
			Продолжить
		КонецЕсли;
		
		ЕстьНоменклатураВЕТИС = Ложь;
		
		Для Каждого СтрокаТЧ Из Объект[ИмяТЧ] Цикл
			
			ЕстьНоменклатураВЕТИС = СтрокаТЧ.Номенклатура.ПодконтрольнаяПродукцияВЕТИС;
			
			Если ЕстьНоменклатураВЕТИС Тогда 
				
				ИмяПроверяемогоРеквизита = СоответствиеПроверяемыхРеквизитов.Получить(ИмяТЧ);
				
				Если Не Объект.СтруктурнаяЕдиница = Объект[ИмяПроверяемогоРеквизита] Тогда
					
				ПредставлениеПоля = СоответствиеПроверяемыхРеквизитов.Получить("ПредставлениеПоля"+ИмяТЧ);
					
				ТекстСообщения = НСтр("ru = '(ВЕТИС)В шапке табличного поля %Таблица%, значение поля %ИмяПоля% должно совпадать с значением поля ""Изготовитель"".'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Таблица%", ИмяТЧ);
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяПоля%", ПредставлениеПоля);
				
				СообщениеОбОшибке = Новый СообщениеПользователю;
				
								
				СообщениеОбОшибке.Текст = ТекстСообщения;
				СообщениеОбОшибке.Поле = "Объект."+ИмяПроверяемогоРеквизита;
				
				СообщениеОбОшибке.УстановитьДанные(Объект);
				
				СообщениеОбОшибке.Сообщить();
				
				Возврат Истина;
					
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьКэшЗначений()
	
	Если ТипЗнч(КэшЗначений)<>Тип("Структура") Тогда
		КэшЗначений = Новый Структура;
	КонецЕсли; 
	КэшЗначений.Вставить("ИспользоватьЭтапыПроизводства", ПолучитьФункциональнуюОпцию("ИспользоватьЭтапыПроизводства"));
	КэшЗначений.Вставить("ВыполнениеЭтаповРазнымиПодразделениями", ПолучитьФункциональнуюОпцию("ВыполнениеЭтаповРазнымиПодразделениями"));
	КэшЗначений.Вставить("ВОРазборка", Перечисления.ВидыОперацийСборкаЗапасов.Разборка);
	КэшЗначений.Вставить("ВОСборка", Перечисления.ВидыОперацийСборкаЗапасов.Сборка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеСпецификацияПриИзменении(СтруктураДанные)
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЭтапыПроизводства")
		И ЗначениеЗаполнено(СтруктураДанные.Спецификация)
		И ТипЗнч(СтруктураДанные.Спецификация)=Тип("СправочникСсылка.Спецификации") Тогда
		ВидПроизводства = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктураДанные.Спецификация, "ВидПроизводства");
		СтруктураДанные.Вставить("ИспользоватьЭтапыПроизводства", ЗначениеЗаполнено(ВидПроизводства));
	Иначе
		СтруктураДанные.Вставить("ИспользоватьЭтапыПроизводства", Ложь);
	КонецЕсли; 
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеХарактеристикаПриИзменении()

&НаКлиенте
Функция ПроизводствоПодЗаказ(СтрокаТабличнойЧасти)
	
	Если Объект.ПоложениеЗаказаПокупателя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") И ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя) Тогда
		Возврат Истина;
	ИначеЕсли Объект.ПоложениеЗаказаПокупателя<>ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") И ЗначениеЗаполнено(Объект.ЗаказПокупателя) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте 
Процедура ИзменитьРежимРаботаГТД(ВключитьРежимГТД, ЭтоТЧМатериалы, ЭтоТЧПродукция)
	
	ИмяСвойства = ?(ЭтоТЧМатериалы, "ОбрабатываемыеРеквизитыМатериалы", "ОбрабатываемыеРеквизитыПродукция");
	Если НЕ КэшЗначений.Свойство(ИмяСвойства) Тогда
		
		КэшЗначений.Вставить(ИмяСвойства, Новый Массив);
		
	КонецЕсли;
	
	Если ВключитьРежимГТД Тогда
		
		
		ВключитьРежимРаботаГТД(ИмяСвойства, ЭтоТЧМатериалы, ЭтоТЧПродукция);
		
	Иначе
		
		ВыключитьРежимРаботаГТД(ИмяСвойства, ЭтоТЧМатериалы, ЭтоТЧПродукция);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьРежимРаботаГТД(ИмяСвойства, ЭтоТЧМатериалы, ЭтоТЧПродукция)
	
	ИмяТЧ = ?(ЭтоТЧМатериалы = Истина, "Запасы", "Продукция");
	
	НеизменяемыеРеквизиты = Новый Массив;
	НеизменяемыеРеквизиты.Добавить(ИмяТЧ + "Пометка");
	НеизменяемыеРеквизиты.Добавить(ИмяТЧ + "НомерСтроки");
	НеизменяемыеРеквизиты.Добавить(ИмяТЧ + "Номенклатура");
	НеизменяемыеРеквизиты.Добавить(ИмяТЧ + "Характеристика");
	НеизменяемыеРеквизиты.Добавить(ИмяТЧ + "Партия");
	НеизменяемыеРеквизиты.Добавить(ИмяТЧ + "Количество");
	НеизменяемыеРеквизиты.Добавить(ИмяТЧ + "ЕдиницаИзмерения");
	НеизменяемыеРеквизиты.Добавить(ИмяТЧ + "СтранаПроисхождения");
	НеизменяемыеРеквизиты.Добавить(ИмяТЧ + "НомерГТД");
	ПрослеживаемостьФормыКлиентСерверУНФ.ДобавитьНеизменяемыеРеквизитыДляРежимаГТД(НеизменяемыеРеквизиты, ИмяТЧ);
	
	Для каждого ЭлементФормы Из Элементы[ИмяТЧ].ПодчиненныеЭлементы Цикл
		
		Если НеизменяемыеРеквизиты.Найти(ЭлементФормы.Имя) = Неопределено
			И ЭлементФормы.Видимость = Истина Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ЭлементФормы.Имя, "Видимость", Ложь);
			КэшЗначений[ИмяСвойства].Добавить(ЭлементФормы.Имя);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ИмяТЧ + "ЕдиницаИзмерения", "Доступность", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыключитьРежимРаботаГТД(ИмяСвойства, ЭтоТЧМатериалы, ЭтоТЧПродукция)
	
	ИмяТЧ = ?(ЭтоТЧМатериалы = Истина, "Запасы", "Продукция");
	
	Для каждого ИмяЭлементаФормы Из КэшЗначений[ИмяСвойства] Цикл
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ИмяЭлементаФормы, "Видимость", Истина);
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ИмяТЧ + "ЕдиницаИзмерения", "Доступность", Истина);
	
	КэшЗначений[ИмяСвойства] = Новый Массив;
	
КонецПроцедуры

&НаСервере
Процедура НомераГТДЗаполнитьПоФактическимОстаткамНаСервере(ЭтоТЧПродукция, ЭтоТЧМатериалы)
	
	ИмяТЧ = ?(ЭтоТЧМатериалы = Истина, "Запасы", "Продукция");
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("ТаблицаЗапасы", Неопределено);
	ПараметрыПодбора.Вставить("ОстаткиПоГТД", Неопределено);
	ПараметрыПодбора.Вставить("Дата", Объект.Дата);
	ПараметрыПодбора.Вставить("Ссылка", Объект.Ссылка);
	ПараметрыПодбора.Вставить("Организация", Объект.Организация);
	ПараметрыПодбора.Вставить("ИндексТекущейСтроки", ?(ЗначениеЗаполнено(Элементы[ИмяТЧ].ТекущаяСтрока), Объект[ИмяТЧ].Индекс(Объект[ИмяТЧ].НайтиПоИдентификатору(Элементы[ИмяТЧ].ТекущаяСтрока)), 0));
	ПараметрыПодбора.Вставить("ЕстьКлючСвязиАвтоматическихСкидок", Неопределено);
	
	МассивИменПолей = Новый Массив;
	МассивИменПолей.Добавить("Резерв");
	ПараметрыПодбора.Вставить("ИменаПолей", МассивИменПолей);
	
	ГрузовыеТаможенныеДекларацииСервер.ПодготовитьТаблицуЗапасовИзТаблицыФормы(ПараметрыПодбора, Объект[ИмяТЧ]);
	ГрузовыеТаможенныеДекларацииСервер.СформироватьОстаткиНомеровГТД(ПараметрыПодбора);
	ГрузовыеТаможенныеДекларацииСервер.ПеренестиОстаткиНомеровГТДВТаблицуФормы(Объект[ИмяТЧ], ПараметрыПодбора);
	
	Если ПараметрыПодбора.ИндексТекущейСтроки <> -1 Тогда
		
		СтрокаКоллекции = Объект[ИмяТЧ].Получить(ПараметрыПодбора.ИндексТекущейСтроки);
		Элементы[ИмяТЧ].ТекущаяСтрока = СтрокаКоллекции.ПолучитьИдентификатор();
		
	КонецЕсли;
	
	// Прослеживаемость
	ОбновитьОтображениеПрослеживаемости();
	
КонецПроцедуры

&НаСервере
Процедура НомераГТДПодобратьНаСервере(ЭтоТЧПродукция, ЭтоТЧМатериалы);
	
	ИмяТЧ = ?(ЭтоТЧМатериалы = Истина, "Запасы", "Продукция");
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("ТаблицаЗапасы", Неопределено);
	ПараметрыПодбора.Вставить("Организация", Объект.Организация);
	
	ГрузовыеТаможенныеДекларацииСервер.ПодготовитьТаблицуЗапасовИзТаблицыФормы(ПараметрыПодбора, Объект[ИмяТЧ]);
	ГрузовыеТаможенныеДекларацииСервер.ПодобратьНомераГТДПоПредыдущимПоступлениям(ПараметрыПодбора);
	ГрузовыеТаможенныеДекларацииСервер.ПеренестиНомераГТДВТаблицуФормы(Объект[ИмяТЧ], ПараметрыПодбора);
	
	// Прослеживаемость
	ОбновитьОтображениеПрослеживаемости();
	
КонецПроцедуры

&НаСервере
Процедура НомераГТДОчиститьНомераИСтраныПроисхожденияНаСервере(ИмяТЧ)
	
	ГрузовыеТаможенныеДекларацииСервер.ОчиститьНомераГТДИСтраныПроисхождения(Объект[ИмяТЧ], -1, Ложь);
	
	// Прослеживаемость
	Объект.СведенияПрослеживаемости.Очистить();
	ОбновитьОтображениеПрослеживаемости();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСкладВТЧ()
	
	Если Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка") Тогда
		ИмяТЧ = "Продукция";
		ИмяТЧОчистка = "Запасы";
	Иначе
		ИмяТЧ = "Запасы";
		ИмяТЧОчистка = "Продукция";
	КонецЕсли;
	Для каждого СтрокаТабличнойЧасти Из Объект[ИмяТЧОчистка] Цикл
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка");
		СтрокаТабличнойЧасти.Ячейка = ПредопределенноеЗначение("Справочник.Ячейки.ПустаяСсылка");
		СтрокаТабличнойЧасти.ЯчейкаДоступна = Ложь;
	КонецЦикла; 
	Для каждого СтрокаТабличнойЧасти Из Объект[ИмяТЧ] Цикл
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаЗапасов;
		СтрокаТабличнойЧасти.Ячейка = Объект.ЯчейкаЗапасов;
		СтрокаТабличнойЧасти.ЯчейкаДоступна = ЯчейкаДоступна(СтрокаТабличнойЧасти.СтруктурнаяЕдиница);
	КонецЦикла; 
	Если Объект.РучноеРаспределение Тогда
		Для каждого СтрокаТабличнойЧасти Из Объект.РаспределениеЗапасов Цикл
			Если Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка") Тогда
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка");
				СтрокаТабличнойЧасти.Ячейка = ПредопределенноеЗначение("Справочник.Ячейки.ПустаяСсылка");
			Иначе
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаЗапасов;
				СтрокаТабличнойЧасти.Ячейка = Объект.ЯчейкаЗапасов;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДанныеШапки(Объект, СтрокаТЧ, ИмяТЧ = "Запасы")
	
	ЭтоСборка = (Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Сборка"));
	ЭтоРазборка = (Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка"));
	
	СтруктураПолей = Новый Структура;
	Если Объект.ПоложениеЗаказаПокупателя<>ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		СтруктураПолей.Вставить("ЗаказПокупателя", Объект.ЗаказПокупателя);
	КонецЕсли;
	Если Объект.ПоложениеСклада<>ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") 
		И ЭтоСборка Тогда
		Если ИмяТЧ="Продукция" Тогда
			СтруктураПолей.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиницаПродукции);
			СтруктураПолей.Вставить("Ячейка", Объект.ЯчейкаПродукции);
		Иначе
			СтруктураПолей.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиницаЗапасов);
			СтруктураПолей.Вставить("Ячейка", Объект.ЯчейкаЗапасов);
		КонецЕсли; 
	КонецЕсли; 
	Если Объект.ПоложениеСклада<>ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") 
		И ЭтоРазборка Тогда
		Если ИмяТЧ="Продукция" Тогда
			СтруктураПолей.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиницаЗапасов);
			СтруктураПолей.Вставить("Ячейка", Объект.ЯчейкаЗапасов);
		Иначе
			СтруктураПолей.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиницаПродукции);
			СтруктураПолей.Вставить("Ячейка", Объект.ЯчейкаПродукции);
		КонецЕсли; 
	КонецЕсли;
	Если СтруктураПолей.Количество()>0 Тогда
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтруктураПолей);
	КонецЕсли; 
	
КонецПроцедуры

// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла
&НаКлиенте
Процедура ЗагрузитьИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныйПараметр)
	
	Если АдресЗагруженныхДанных = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ЗагрузитьИзФайлаНаСервере(АдресЗагруженныхДанных, ДополнительныйПараметр);
	Для каждого СтрокаТаблицы Из Объект.Запасы Цикл 
		СтрокаТаблицы.ЯчейкаДоступна = ЯчейкаДоступна(СтрокаТаблицы.СтруктурнаяЕдиница);
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИзФайлаНаСервере(АдресЗагруженныхДанных, ТипЗагрузки)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	ТоварыДобавлены = Ложь;
	Для каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТаблицы.Номенклатура.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Запас Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрокаТовары = Объект.Запасы.Добавить();
		НоваяСтрокаТовары.Характеристика = СтрокаТаблицы.Характеристика;
		НоваяСтрокаТовары.Партия = СтрокаТаблицы.Партия;
		ЗаполнениеОбъектовУНФ.ЗаполнитьСтрокуПоШапке(НоваяСтрокаТовары, Объект, "СтруктурнаяЕдиница", "ПоложениеСклада");
		ЗаполнениеОбъектовУНФ.ЗаполнитьСтрокуПоШапке(НоваяСтрокаТовары, Объект, "Ячейка", "ПоложениеСклада");
		ЗаполнениеОбъектовУНФ.ЗаполнитьСтрокуПоШапке(НоваяСтрокаТовары, Объект, "Заказ", "ПоложениеЗаказаПоставщику");
		
		НоваяСтрокаТовары.Номенклатура = СтрокаТаблицы.Номенклатура;
		НоваяСтрокаТовары.Количество = СтрокаТаблицы.Количество;
		НоваяСтрокаТовары.ЕдиницаИзмерения = СтрокаТаблицы.ЕдиницаИзмерения;
		
		ТоварыДобавлены = Истина;
		
	КонецЦикла;
	
	Если ТоварыДобавлены Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата(РезультатЗагрузки, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗагрузки) = Тип("Структура") Тогда
		
		Если РезультатЗагрузки.ОписаниеДействия = "ИзменитьСпособЗагрузкиДанныхИзВнешнегоИсточника" Тогда
		
			ЗагрузкаДанныхИзВнешнегоИсточника.ИзменитьСпособЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных.ИмяФормыЗагрузкиДанныхИзВнешнихИсточников);
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата", ЭтотОбъект, НастройкиЗагрузкиДанных);
			ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.ПоказатьФормуЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных, ОписаниеОповещения, ЭтотОбъект);
			
		ИначеЕсли РезультатЗагрузки.ОписаниеДействия = "ОбработатьПодготовленныеДанные" Тогда
			
			ДлительнаяОперация = ОбработатьПодготовленныеДанныеНоменклатуры(РезультатЗагрузки);
			
			СтруктураПараметров = Новый Структура;
			Если РезультатЗагрузки.НастройкиЗагрузкиДанных.Свойство("ТекстОшибки") Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатЗагрузки.НастройкиЗагрузкиДанных.ТекстОшибки);
			КонецЕсли;
			
			ПроверитьНеобходимостьПодключенияОбработчикаОжидания(ДлительнаяОперация, СтруктураПараметров);
			
			Если РезультатЗагрузки.НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Документ.СборкаЗапасов.ТабличнаяЧасть.Запасы" Тогда
				Для каждого СтрокаТаблицы Из Объект.Запасы Цикл
					СтрокаТаблицы.ЯчейкаДоступна = ЯчейкаДоступна(СтрокаТаблицы.СтруктурнаяЕдиница);
				КонецЦикла; 
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		ТипЗагрузки = "Запасы";
		ЗагрузитьИзФайлаЗавершение(РезультатЗагрузки, ТипЗагрузки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ИменаСвойств()
	
	ИменаСвойств = "Номенклатура, Количество, ЕдиницаИзмерения";
	Если ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
		
		ИменаСвойств = ИменаСвойств + ", Характеристика";
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартии") Тогда
		
		ИменаСвойств = ИменаСвойств + ", Партия";
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("УчетПоНесколькимСкладам") Тогда
		
		ИменаСвойств = ИменаСвойств + ", СтруктурнаяЕдиница";
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("УчетГТД") Тогда
		
		ИменаСвойств = ИменаСвойств + ", СтранаПроисхождения, НомерГТД";
		
	КонецЕсли;
	
	Возврат ИменаСвойств;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьДанныеВТабличноеПолеЗапасы(ТаблицаСопоставленияДанных)
	
	ИменаСвойств = ИменаСвойств();
	
	НачатьТранзакцию();
	
	Попытка
		
		Для Каждого СтрокаТаблицы Из ТаблицаСопоставленияДанных Цикл
			
			ЗагрузкаВПриложениеВозможна = СтрокаТаблицы[ЗагрузкаДанныхИзВнешнегоИсточника.ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна()];
			Если ЗагрузкаВПриложениеВозможна Тогда
				
				НоваяСтрока = Объект.Запасы.Добавить();
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ИменаСвойств);
				ЗаполнениеОбъектовУНФ.ЗаполнитьСтрокуПоШапке(НоваяСтрока, Объект, "СтруктурнаяЕдиница", "ПоложениеСклада");
				ЗаполнениеОбъектовУНФ.ЗаполнитьСтрокуПоШапке(НоваяСтрока, Объект, "Ячейка", "ПоложениеСклада");
				ЗаполнениеОбъектовУНФ.ЗаполнитьСтрокуПоШапке(НоваяСтрока, Объект, "ЗаказПокупателя", "ПоложениеЗаказаПокупателя");
			
				НоваяСтрока.ЗаказПокупателя = СтрокаТаблицы.ЗаказПокупателя;
				
				Если ЗаказПокупателяЗаполнен(Объект) И НЕ Объект.ВидОперации = Перечисления.ВидыОперацийСборкаЗапасов.Разборка Тогда
					НоваяСтрока.Резерв = СтрокаТаблицы.Резерв;
				КонецЕсли;
				
				Если Объект.ВидОперации = Перечисления.ВидыОперацийСборкаЗапасов.Разборка Тогда
					НоваяСтрока.ДоляСтоимости = СтрокаТаблицы.ДоляСтоимости;
				КонецЕсли;				
								
				Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "СтранаПроисхождения")
					И ЗначениеЗаполнено(СтрокаТаблицы.СтранаПроисхождения)
					И СтрокаТаблицы.СтранаПроисхождения <> Справочники.СтраныМира.Россия
					И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "НомерГТД")
					И НЕ ЗначениеЗаполнено(СтрокаТаблицы.НомерГТД)
					И НЕ ПустаяСтрока(СтрокаТаблицы.НомерГТД_ВходящиеДанные)
					Тогда
					
					СправочникОбъект = Справочники.НомераГТД.СоздатьЭлемент();
					СправочникОбъект.Код = СтрокаТаблицы.НомерГТД_ВходящиеДанные;
					СправочникОбъект.ДопускаетсяЗаписьСОшибкой = Ложь;
					
					ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект, Истина, Истина);
					
					НоваяСтрока.НомерГТД = СправочникОбъект.Ссылка;
					
					СтрокиСНомеромГТД = ТаблицаСопоставленияДанных.НайтиСтроки(Новый Структура("НомерГТД_ВходящиеДанные", СтрокаТаблицы.НомерГТД_ВходящиеДанные));
					Для каждого СтрокаТаблицы Из СтрокиСНомеромГТД Цикл
						
						СтрокаТаблицы.НомерГТД = СправочникОбъект.Ссылка;
						
					КонецЦикла;
					
				КонецЕсли;
				
				// Заполнить Серии номенклатуры
				Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "Серия_ВходящиеДанные") 
					И ЗначениеЗаполнено(СтрокаТаблицы.Серия_ВходящиеДанные) 
					И НоваяСтрока.Номенклатура.ИспользоватьСерииНоменклатуры Тогда
					
						Если СтрНайти(СтрокаТаблицы.Серия_ВходящиеДанные, Символы.ПС) > 0 Тогда
							Разделитель = Символы.ПС;
						ИначеЕсли СтрНайти(СтрокаТаблицы.Серия_ВходящиеДанные, ",") > 0 Тогда
							Разделитель = ",";
						ИначеЕсли СтрНайти(СтрокаТаблицы.Серия_ВходящиеДанные, ";") > 0 Тогда
							Разделитель = ";";
						ИначеЕсли СтрНайти(СтрокаТаблицы.Серия_ВходящиеДанные, " ") > 0 Тогда
							Разделитель = " ";
						КонецЕсли;
					    МассивСерийных = СтрРазделить(СтрокаТаблицы.Серия_ВходящиеДанные, Разделитель);
						
						Для каждого серийный Из МассивСерийных Цикл
							серийныйСсылка = Справочники.СерииНоменклатуры.НайтиПоНаименованию(СокрЛП(серийный), Истина, , НоваяСтрока.Номенклатура);
							Если НЕ ЗначениеЗаполнено(серийныйСсылка) Тогда
								
								серийныйОбъект = Справочники.СерииНоменклатуры.СоздатьЭлемент();
								серийныйОбъект.Владелец = НоваяСтрока.Номенклатура;
								серийныйОбъект.Наименование	= СокрЛП(серийный);
								серийныйОбъект.Записать();
								серийныйСсылка = серийныйОбъект.Ссылка;
							КонецЕсли;
							
							СерииНоменклатурыУНФКлиентСервер.ДобавитьСерияВСтроку(НоваяСтрока, серийныйСсылка, Объект); 
						
						КонецЦикла;
				
				КонецЕсли; 
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(НСтр("ru='Загрузка данных'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.Номенклатура,, ПредставлениеОшибки);
		
		ВызватьИсключение ПредставлениеОшибки;
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПодготовленныеДанные(АдресРезультатаЗагрузки)
	
	Если АдресРезультатаЗагрузки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РезультатЗагрузки = ПолучитьИзВременногоХранилища(АдресРезультатаЗагрузки);
	
	Если РезультатЗагрузки = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ТаблицаСопоставленияДанных = РезультатЗагрузки.ТаблицаСопоставленияДанных;
	Если РезультатЗагрузки.НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Документ.СборкаЗапасов.ТабличнаяЧасть.Запасы" Тогда
		
		ЗагрузитьДанныеВТабличноеПолеЗапасы(ТаблицаСопоставленияДанных);
		ОбновитьОтображениеПрослеживаемости();
		
	КонецЕсли;
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработатьПодготовленныеДанныеНоменклатуры(РезультатЗагрузки)
	
	ПараметрыВызоваСервера = Новый Структура;
	ПараметрыВызоваСервера.Вставить("НастройкиЗагрузкиДанных", РезультатЗагрузки.НастройкиЗагрузкиДанных);
	ПараметрыВызоваСервера.Вставить("ТаблицаСопоставленияДанных", ДанныеФормыВЗначение(РезультатЗагрузки.ТаблицаСопоставленияДанных, Тип("ТаблицаЗначений")));
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Подсистема ЗагрузкаДанныхИзВнешнегоИсточника: Выполнение серверного метода загрузки результата'");
	ПараметрыВыполнения.ЗапуститьНеВФоне    = Ложь;
	ПараметрыВыполнения.ЗапуститьВФоне      = Истина;	
	
	ИмяМетода = "Справочники.Номенклатура.ОбработатьПодготовленныеДанные";
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыВызоваСервера, ПараметрыВыполнения);
	
КонецФункции
// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла


#КонецОбласти

#Область РаботаСПодбором

// Процедура - обработчик события Действие команды Подбор
//
&НаКлиенте
Процедура Подбор(Команда)
	
	ИмяТабличнойЧасти 	= "Запасы";
	МаркерПодбора = "Запасы";
	ПодборНоменклатурыВДокументахКлиент.ОткрытьФормуПодбораНоменклатуры(ЭтотОбъект, ИмяТабличнойЧасти);

КонецПроцедуры // Подбор()

// Процедура - обработчик события Действие команды Подбор ТЧ Продукция.
//
&НаКлиенте
Процедура ПодборПродукция(Команда)
	
	ИмяТабличнойЧасти = "Продукция";
	МаркерПодбора = "Продукция";
	ПодборНоменклатурыВДокументахКлиент.ОткрытьФормуПодбораНоменклатуры(ЭтотОбъект, ИмяТабличнойЧасти);
	
КонецПроцедуры // ПодборПродукция()

// Процедура - обработчик события Действие команды Подбор
//
&НаКлиенте
Процедура ОтходыПодбор(Команда)
	
	ИмяТабличнойЧасти 	= "Отходы";
	МаркерПодбора = "Отходы";
	ПодборНоменклатурыВДокументахКлиент.ОткрытьФормуПодбораНоменклатуры(ЭтотОбъект, ИмяТабличнойЧасти);
	
КонецПроцедуры // ПодборВыполнить()

// Функция получает список товаров из временного хранилища
//
&НаСервере
Процедура ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, ИмяТЧ, ЕстьХарактеристики, ЕстьПартии)
	
	ТаблицаДляЗагрузки = ПолучитьИзВременногоХранилища(АдресЗапасовВХранилище);
	
	Для каждого СтрокаЗагрузки Из ТаблицаДляЗагрузки Цикл
		
		НоваяСтрока = Объект[ИмяТЧ].Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗагрузки);
		ЗаполнитьДанныеШапки(Объект, НоваяСтрока, ИмяТЧ);
		
		// Характеристики
		СтруктураДанныеНоменклатуры = Новый Структура();
		СтруктураДанныеНоменклатуры.Вставить("Номенклатура", СтрокаЗагрузки.Номенклатура);
		СтруктураДанныеНоменклатуры.Вставить("Характеристика", СтрокаЗагрузки.Характеристика);
		
		Если ИмяТабличнойЧасти = "Продукция"
			Тогда
			СтатусПартии = Новый СписокЗначений;
			СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
			СтруктураДанныеНоменклатуры.Вставить("СтатусПартии", СтатусПартии)
		ИначеЕсли ИмяТабличнойЧасти = "Запасы" Или ИмяТабличнойЧасти = "Отходы"
			Тогда
			СтатусПартии = Новый СписокЗначений;
			СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
			СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ДавальческоеСырье"));
			СтруктураДанныеНоменклатуры.Вставить("СтатусПартии", СтатусПартии)
		КонецЕсли;
		
		СерииНоменклатурыУНФКлиентСервер.ДополнитьСтруктуруДаннымиДляПолученияСерийНоменклатуры(Объект, СтруктураДанныеНоменклатуры);
		СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанныеНоменклатуры);
		
		НоваяСтрока.ИспользоватьХарактеристики = СтруктураДанные.ИспользоватьХарактеристики;
		НоваяСтрока.ПроверятьЗаполнениеХарактеристики = СтруктураДанные.ПроверятьЗаполнениеХарактеристики;
		НоваяСтрока.ЗаполнениеХарактеристикиПроверено = Истина;
		
		Если СтруктураДанные.ИспользоватьХарактеристики
			Тогда
			НоваяСтрока.Характеристика = СтруктураДанные.Характеристика;
		КонецЕсли;
		// Конец Характеристики
		
		//Партии
		НоваяСтрока.ИспользоватьПартии = СтруктураДанные.ИспользоватьПартии;
		НоваяСтрока.ПроверятьЗаполнениеПартий = СтруктураДанные.ПроверятьЗаполнениеПартий;
		
		НоваяСтрока.Партия = ?(ЗначениеЗаполнено(СтрокаЗагрузки.Партия), СтрокаЗагрузки.Партия, СтруктураДанные.Партия);
		// Конец Партии
		
		Если ИмяТабличнойЧасти = "Запасы" Или ИмяТабличнойЧасти = "Продукция" Тогда
			НоваяСтрока.СтатусыСерийНоменклатуры = СтруктураДанные.СтатусыСерийНоменклатуры;
			ТабличныеЧастиУНФКлиентСервер.ЗаполнитьКлючСвязи(Объект[ИмяТабличнойЧасти], НоваяСтрока, "КлючСвязи");
		КонецЕсли;
		
		Если НоваяСтрока.Свойство("Спецификация") Тогда
			НоваяСтрока.Спецификация = СтруктураДанные.Спецификация;
		КонецЕсли;
		Если НоваяСтрока.Свойство("ИспользоватьЭтапыПроизводства") Тогда
			НоваяСтрока.ИспользоватьЭтапыПроизводства = СтруктураДанные.ИспользоватьЭтапыПроизводства;
			Если НоваяСтрока.Свойство("ПодразделениеЗавершающегоЭтапа")
				И НоваяСтрока.ИспользоватьЭтапыПроизводства 
				И ТипСтруктурнойЕдиницы = ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение") Тогда
				НоваяСтрока.ПодразделениеЗавершающегоЭтапа = Объект.СтруктурнаяЕдиница;
			Иначе
				НоваяСтрока.ПодразделениеЗавершающегоЭтапа = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка");
			КонецЕсли;
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(НоваяСтрока, "КлючСвязи") Тогда
			НоваяСтрока.КлючСвязи = ТабличныеЧастиУНФКлиентСервер.СоздатьНовыйКлючСвязи(ЭтотОбъект);
		КонецЕсли; 
		
		// Прослеживаемость
		Если ИмяТабличнойЧасти = "Запасы" ИЛИ ИмяТабличнойЧасти = "Продукция" Тогда
			НоваяСтрока.СтранаПроисхождения = СтруктураДанные.СтранаПроисхождения;
			Если КэшЗначений.ВестиУчетПрослеживаемыхТоваров Тогда
				НоваяСтрока.ПрослеживаемыйТовар = СтруктураДанные.ПрослеживаемыйТовар;
				ПрослеживаемостьФормыКлиентСерверУНФ.ЗаполнитьПредставлениеРНПТ(ЭтотОбъект, НоваяСтрока);
			Иначе
				НоваяСтрока.ПрослеживаемыйТовар = Ложь;
			КонецЕсли;
			Если НЕ НоваяСтрока.ПрослеживаемыйТовар Тогда
				НоваяСтрока.НомерГТД = СтруктураДанные.НомерГТД;
			КонецЕсли; 
		КонецЕсли; 
		// Конец Прослеживаемость
		
	КонецЦикла;
	
	Если ИмяТабличнойЧасти = "Запасы" Тогда
		ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	КонецЕсли;
	ОбновитьДоступностьЯчеек();
	ЗаполнитьПризнакиДоступностиЯчеек(ЭтотОбъект);
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
КонецПроцедуры // ПолучитьЗапасыИзХранилища()

// ПодключаемоеОборудование
// Процедура - обработчик команды командной панели табличной части.
//
&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	ТекШтрихкод = "";
	
	ПараметрыОбработкиОповещения = Новый Структура("ТекШтрихкод", ТекШтрихкод);
	ОбработкаЗавершения = Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", ЭтотОбъект,
		ПараметрыОбработкиОповещения);
	
	#Если МобильныйКлиент Тогда
		ОткрытьФорму("ОбщаяФорма.ФормаПоискаПоШтрихкоду", , , , , , ОбработкаЗавершения);
	#Иначе
		ПоказатьВводЗначения(ОбработкаЗавершения, ТекШтрихкод, НСтр("ru = 'Введите штрихкод'"));
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкодуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ТекШтрихкод = ?(Результат = Неопределено, СокрЛП(ДополнительныеПараметры.ТекШтрихкод), СокрЛП(Результат));
	
	Если НЕ ПустаяСтрока(ТекШтрихкод) Тогда
		ПолученыШтрихкоды(Новый Структура("Штрихкод, Количество, ДоляСтоимости", ТекШтрихкод, 1, 1));
	КонецЕсли;
	
КонецПроцедуры // ПоискПоШтрихкоду()

// Процедура - обработчик события Действие команды ПолучитьВес
//
&НаКлиенте
Процедура ПолучитьВес(Команда)
	
	ПодключаемоеОборудованиеУНФКлиент.ПолучениеВесаСЭлектронныхВесовДляТабличнойЧасти(ЭтотОбъект, "Запасы", Ложь);
	
КонецПроцедуры // ПолучитьВес()

// Процедура - обработчик команды ЗагрузитьДанныеИзТСД.
//
&НаКлиенте
Процедура ЗагрузитьДанныеИзТСД(Команда)
	
	ПодключаемоеОборудованиеУНФКлиент.ПолучитьДанныеИзТСД(ЭтотОбъект);
	
КонецПроцедуры // ЗагрузитьДанныеИзТСД()

&НаКлиенте
Процедура ВыгрузитьДанныеВТСД(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.ТЧПродукция Тогда
		ИмяТаблицыВыборки = "Продукция";
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ТЧЗапасы Тогда
		ИмяТаблицыВыборки = "Запасы";
	КонецЕсли;
	ДополнительныеПараметры.Вставить("ИмяТаблицыВыборки", ИмяТаблицыВыборки);
	
	ПодключаемоеОборудованиеУНФКлиент.ВыгрузитьДокументВТСД(ЭтотОбъект, Ложь, ДополнительныеПараметры);
	
КонецПроцедуры

// Конец ПодключаемоеОборудование

#КонецОбласти

#Область УправлениеВнешнимВидомФормы

// Процедура устанавливает режим выбора и список выбора для элементов формы.
//
// Параметры:
//  Нет.
//
&НаСервере
Процедура УстановитьРежимИСписокВыбора()
	
	Если НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница)
		ИЛИ Объект.СтруктурнаяЕдиница.ОрдерныйСклад
		ИЛИ Объект.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы<>Перечисления.ТипыСтруктурныхЕдиниц.Склад Тогда
		Элементы.Ячейка.Доступность = Ложь;
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаПродукции)
		ИЛИ Объект.СтруктурнаяЕдиницаПродукции.ОрдерныйСклад
		ИЛИ Объект.СтруктурнаяЕдиницаПродукции.ТипСтруктурнойЕдиницы<>Перечисления.ТипыСтруктурныхЕдиниц.Склад Тогда
		Элементы.ЯчейкаПродукцииСборка.Доступность = Ложь;
		Элементы.ЯчейкаЗапасовРазборка.Доступность = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаЗапасов)
		ИЛИ Объект.СтруктурнаяЕдиницаЗапасов.ОрдерныйСклад
		ИЛИ Объект.СтруктурнаяЕдиницаЗапасов.ТипСтруктурнойЕдиницы<>Перечисления.ТипыСтруктурныхЕдиниц.Склад Тогда
		Элементы.ЯчейкаЗапасовСборка.Доступность = Ложь;
		Элементы.ЯчейкаПродукцииРазборка.Доступность = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаОтходов)
		ИЛИ Объект.СтруктурнаяЕдиницаОтходов.ОрдерныйСклад
		ИЛИ Объект.СтруктурнаяЕдиницаОтходов.ТипСтруктурнойЕдиницы<>Перечисления.ТипыСтруктурныхЕдиниц.Склад Тогда
		Элементы.ЯчейкаОтходов.Доступность = Ложь;
	КонецЕсли;
	
	Если НЕ Константы.ФункциональнаяОпцияУчетПоНесколькимПодразделениям.Получить()
		И НЕ Константы.ФункциональнаяОпцияУчетПоНесколькимСкладам.Получить() Тогда
		
		Элементы.СтруктурнаяЕдиница.РежимВыбораИзСписка = Истина;
		Элементы.СтруктурнаяЕдиница.КнопкаСоздания = Ложь;
		Элементы.СтруктурнаяЕдиница.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		Элементы.СтруктурнаяЕдиница.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		
		Элементы.СтруктурнаяЕдиницаПродукцииСборка.РежимВыбораИзСписка = Истина;
		Элементы.СтруктурнаяЕдиницаПродукцииСборка.КнопкаСоздания = Ложь;
		Элементы.СтруктурнаяЕдиницаПродукцииСборка.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		Элементы.СтруктурнаяЕдиницаПродукцииСборка.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		
		Элементы.СтруктурнаяЕдиницаПродукцииРазборка.РежимВыбораИзСписка = Истина;
		Элементы.СтруктурнаяЕдиницаПродукцииРазборка.КнопкаСоздания = Ложь;
		Элементы.СтруктурнаяЕдиницаПродукцииРазборка.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		Элементы.СтруктурнаяЕдиницаПродукцииРазборка.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		
		Элементы.СтруктурнаяЕдиницаЗапасовСборка.РежимВыбораИзСписка = Истина;
		Элементы.СтруктурнаяЕдиницаЗапасовСборка.КнопкаСоздания = Ложь;
		Элементы.СтруктурнаяЕдиницаЗапасовСборка.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		Элементы.СтруктурнаяЕдиницаЗапасовСборка.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		
		Элементы.СтруктурнаяЕдиницаЗапасовРазборка.РежимВыбораИзСписка = Истина;
		Элементы.СтруктурнаяЕдиницаЗапасовРазборка.КнопкаСоздания = Ложь;
		Элементы.СтруктурнаяЕдиницаЗапасовРазборка.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		Элементы.СтруктурнаяЕдиницаЗапасовРазборка.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		
		Элементы.СтруктурнаяЕдиницаОтходов.РежимВыбораИзСписка = Истина;
		Элементы.СтруктурнаяЕдиницаОтходов.КнопкаСоздания = Ложь;
		Элементы.СтруктурнаяЕдиницаОтходов.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		Элементы.СтруктурнаяЕдиницаОтходов.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		
		Элементы.ПродукцияСтруктурнаяЕдиница.РежимВыбораИзСписка = Истина;
		Элементы.ПродукцияСтруктурнаяЕдиница.КнопкаСоздания = Ложь;
		Элементы.ПродукцияСтруктурнаяЕдиница.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		Элементы.ПродукцияСтруктурнаяЕдиница.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		
		Элементы.ЗапасыСтруктурнаяЕдиница.РежимВыбораИзСписка = Истина;
		Элементы.ЗапасыСтруктурнаяЕдиница.КнопкаСоздания = Ложь;
		Элементы.ЗапасыСтруктурнаяЕдиница.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		Элементы.ЗапасыСтруктурнаяЕдиница.СписокВыбора.Добавить(Справочники.СтруктурныеЕдиницы.ОсновнойСклад);
		
	КонецЕсли;
	
КонецПроцедуры // УстановитьРежимИСписокВыбора()

&НаКлиентеНаСервереБезКонтекста
Функция ЗаказПокупателяЗаполнен(Объект)
	
	Возврат ЗначениеЗаполнено(Объект.ЗаказПокупателя) 
	ИЛИ Объект.ПоложениеЗаказаПокупателя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти");	
	
КонецФункции 

&НаСервере
Процедура УстановитьУсловноеОформлениеФормы()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Колонка "Резерв" ТЧ "Продукция"
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.ЗаказПокупателя", Документы.ЗаказПокупателя.ПустаяСсылка());
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.ПоложениеЗаказаПокупателя", Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ПродукцияРезерв.Имя);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Доступность", Ложь);
	
	// Колонка "Резерв" ТЧ "Запасы"
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.ЗаказПокупателя", Документы.ЗаказПокупателя.ПустаяСсылка());
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.ПоложениеЗаказаПокупателя", Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыРезерв.Имя);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Доступность", Ложь);
	
	// Этапы производства
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.ИспользоватьЭтапыПроизводства", Ложь);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ПродукцияПодразделениеЗавершающегоЭтапа");
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ПродукцияЭтапы");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Доступность", Ложь);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.ИспользоватьЭтапыПроизводства", Истина);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.ПродукцияПодразделениеЗавершающегоЭтапа", Справочники.СтруктурныеЕдиницы.ПустаяСсылка());
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ПродукцияПодразделениеЗавершающегоЭтапа");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Истина);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.ИспользоватьЭтапыПроизводства", Истина);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.Этапы", "");
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ПродукцияЭтапы");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Истина);
	
	// Ячейки
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Продукция.ЯчейкаДоступна", Ложь);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ПродукцияЯчейка");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Доступность", Ложь);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.ЯчейкаДоступна", Ложь);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ЗапасыЯчейка");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Доступность", Ложь);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.ЯчейкаДоступна", Ложь);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ЗапасыЯчейкаДляРежимаОстатки");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Доступность", Ложь);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.ЯчейкаДоступна", Ложь);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ЗапасыОстатокВЯчейке");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Текст", "");
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "РаспределениеЗапасов.ЯчейкаДоступна", Ложь);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "РаспределениеЗапасовЯчейка");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Доступность", Ложь);
	
	НоменклатураВДокументахСервер.УстановитьУсловноеОформлениеЗапасыИРезервы(ЭтаФорма);
	
	// Прослеживаемость
	ПрослеживаемостьФормыУНФ.ОбновитьУсловноеОформлениеТабличнойЧастиДляПрослеживаемости(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьОтПользовательскихНастроек(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	ЭтоРазборка	= (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка"));
	СкладВТЧ = (Объект.ПоложениеСклада=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти"));
	ЗаказВТЧ = (Объект.ПоложениеЗаказаПокупателя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти"));
	
	// Склад
	Если СкладВТЧ Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСкладПродукцияСборка", "Видимость", НЕ ЭтоРазборка);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСкладПродукцияРазборка", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСкладЗапасыСборка", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСкладЗапасыРазборка", "Видимость", ЭтоРазборка);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияСтруктурнаяЕдиница", "Видимость", ЭтоРазборка);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияЯчейка", "Видимость", ЭтоРазборка);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыСтруктурнаяЕдиница", "Видимость", НЕ ЭтоРазборка);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыЯчейка", "Видимость", НЕ ЭтоРазборка);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РаспределениеЗапасовСтруктурнаяЕдиница", "Видимость", НЕ ЭтоРазборка);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РаспределениеЗапасовЯчейка", "Видимость", НЕ ЭтоРазборка);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыЗаполнитьПоОстаткамИРезервамВсеСклады", "Видимость", Истина);
		Форма.СкладВШапке = Ложь;
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСкладПродукцияСборка", "Видимость", НЕ ЭтоРазборка);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСкладПродукцияРазборка", "Видимость", ЭтоРазборка);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСкладЗапасыСборка", "Видимость", НЕ ЭтоРазборка);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСкладЗапасыРазборка", "Видимость", ЭтоРазборка);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияСтруктурнаяЕдиница", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияЯчейка", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыСтруктурнаяЕдиница", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыЯчейка", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РаспределениеЗапасовСтруктурнаяЕдиница", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РаспределениеЗапасовЯчейка", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыЗаполнитьПоОстаткамИРезервамВсеСклады", "Видимость", Форма.РежимОстаткиИРезервы);
		Форма.СкладВШапке = Истина;
	КонецЕсли;
	
	// Заполнение по остаткам
	СкладЗаполнен = (НЕ СкладВТЧ И ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаЗапасов));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыЗаполнитьПоОстаткам", "Доступность", СкладЗаполнен);
	ОбновитьЗаголовокКомандыЗаполнитьПоОстаткам(Форма);
	
	// Заказ покупателя
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияЗаказПокупателя", "Видимость", ЗаказВТЧ);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыЗаказПокупателя", "Видимость", ЗаказВТЧ);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РаспределениеЗапасовЗаказПокупателя", "Видимость", ЗаказВТЧ);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаЗаказПокупателя", "Видимость", НЕ ЗаказВТЧ);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияДобавитьИзЗаказов", "Видимость", НЕ ЭтоРазборка И ЗаказВТЧ);
	
КонецПроцедуры // УстановитьВидимостьОтПользовательскихНастроек()

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ЭтоРазборка	= (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка"));
	ЭтоСборка	= (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Сборка"));
	ЗаказПокупателяЗаполнен = ЗаказПокупателяЗаполнен(Объект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыЗаполнитьПоОстаткам", "Видимость", НЕ ЭтоРазборка);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтруктурнаяЕдиницаЗапасовСборка", "АвтоОтметкаНезаполненного", Объект.Запасы.Количество()>0);
	Если Объект.Запасы.Количество()=0 Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтруктурнаяЕдиницаЗапасовСборка", "ОтметкаНезаполненного", Ложь);
	КонецЕсли; 
	
	// Резерв.
	Элементы.ЗапасыРезерв.Видимость = Не Форма.РежимОстаткиИРезервы И ЗаказПокупателяЗаполнен И ЭтоСборка;
	Элементы.ЗапасыИзменитьРезерв.Видимость = ЗаказПокупателяЗаполнен И ЭтоСборка;
	Элементы.ПродукцияРезерв.Видимость = ЗаказПокупателяЗаполнен И ЭтоРазборка;
	Элементы.ЗапасыДоляСтоимости.Видимость = ЭтоРазборка;
	
	Элементы.ЗаказПокупателя.ТолькоПросмотр = ЗначениеЗаполнено(Объект.ЗаказНаПроизводство);
	Элементы.ПродукцияЗаказПокупателя.ТолькоПросмотр = ЗначениеЗаполнено(Объект.ЗаказНаПроизводство);
	Элементы.ЗапасыЗаказПокупателя.ТолькоПросмотр = ЗначениеЗаполнено(Объект.ЗаказНаПроизводство);
	
	Элементы.РаспределениеЗапасов.ТолькоПросмотр = Форма.ТолькоПросмотр;
	Элементы.РучноеРаспределение.ТолькоПросмотр = Форма.ТолькоПросмотр;
	
	// Статус партии.
	
	МассивСтатусовДляВыбораНоменклатуры = Новый Массив;
	
	Если ЭтоРазборка Тогда
		
		ДоступныеСтатусы = Новый Массив;
		ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
		ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ДавальческоеСырье"));
		МассивЗапасРабота = Новый ФиксированныйМассив(ДоступныеСтатусы);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Статус", МассивЗапасРабота));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Дополнительно.ОграничениеСтатуса", МассивЗапасРабота));
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.ПродукцияПартия.ПараметрыВыбора = НовыеПараметры;
		
		МассивПараметров = Новый Массив;
		Для каждого ЭлементМассива Из Элементы.ПродукцияНоменклатура.ПараметрыВыбора Цикл
		Если ЭлементМассива.Имя="Дополнительно.СтатусыПартий" Тогда 
			Продолжить;
		КонецЕсли;
			МассивПараметров.Добавить(ЭлементМассива);
		КонецЦикла;
		МассивПараметров.Добавить(Новый ПараметрВыбора("Дополнительно.СтатусыПартий", МассивЗапасРабота));
		Элементы.ПродукцияНоменклатура.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
		
		НовыйМассив = Новый Массив;
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Статус", ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы")));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Дополнительно.ОграничениеСтатуса", ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы")));
		МассивСтатусовДляВыбораНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
		
		МассивЗапасРабота = Новый ФиксированныйМассив(МассивСтатусовДляВыбораНоменклатуры);
		
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.ЗапасыПартия.ПараметрыВыбора = НовыеПараметры;
		Элементы.РаспределениеЗапасовПартия.ПараметрыВыбора = НовыеПараметры;
		
	Иначе
		
		НовыйМассив = Новый Массив;
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Статус", ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы")));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Дополнительно.ОграничениеСтатуса", ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы")));
		МассивСтатусовДляВыбораНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.ПродукцияПартия.ПараметрыВыбора = НовыеПараметры;
		
		МассивПараметров = Новый Массив;
		МассивЗапасРабота = Новый ФиксированныйМассив(МассивСтатусовДляВыбораНоменклатуры);
		Для каждого ЭлементМассива Из Элементы.ПродукцияНоменклатура.ПараметрыВыбора Цикл
			Если ЭлементМассива.Имя="Дополнительно.СтатусыПартий" Тогда 
				Продолжить;
			КонецЕсли;
			МассивПараметров.Добавить(ЭлементМассива);
		КонецЦикла;
		МассивПараметров.Добавить(Новый ПараметрВыбора("Дополнительно.СтатусыПартий", МассивЗапасРабота));
		Элементы.ПродукцияНоменклатура.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
		
		ДоступныеСтатусы = Новый Массив;
		ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
		ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ДавальческоеСырье"));
		
		МассивЗапасРабота = Новый ФиксированныйМассив(ДоступныеСтатусы);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Статус", МассивЗапасРабота));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Дополнительно.ОграничениеСтатуса", МассивЗапасРабота));
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.ЗапасыПартия.ПараметрыВыбора = НовыеПараметры;
		Элементы.РаспределениеЗапасовПартия.ПараметрыВыбора = НовыеПараметры;
		
	КонецЕсли;
	
	МассивПараметров = Новый Массив;
	Для каждого ЭлементМассива Из Элементы.ЗапасыНоменклатура.ПараметрыВыбора Цикл
		Если ЭлементМассива.Имя="Дополнительно.СтатусыПартий" Тогда 
			Продолжить;
		КонецЕсли;
		МассивПараметров.Добавить(ЭлементМассива);
	КонецЦикла;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Дополнительно.СтатусыПартий", МассивЗапасРабота));
	Элементы.ЗапасыНоменклатура.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	МассивПараметров = Новый Массив;
	Для каждого ЭлементМассива Из Элементы.РаспределениеЗапасовНоменклатура.ПараметрыВыбора Цикл
		Если ЭлементМассива.Имя="Дополнительно.СтатусыПартий" Тогда 
			Продолжить;
		КонецЕсли;
		МассивПараметров.Добавить(ЭлементМассива);
	КонецЦикла;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Дополнительно.СтатусыПартий", МассивЗапасРабота));
	Элементы.РаспределениеЗапасовНоменклатура.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Распределение запасов
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТЧРаспределениеЗапасов", 		    "Видимость", Объект.РучноеРаспределение);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыЗаполнитьПоРаспределению",    "Видимость", Объект.РучноеРаспределение);
	
	УправлениеВидимостьюЭтапов(Форма);
	
	НоменклатураВДокументахКлиентСервер.ЗаполнитьТаблицуНастроекФормыВыбораНоменклатуры(Форма, "СборкаЗапасовСписание", Форма.НастройкиФормыВыбораНоменклатуры);
	НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(Форма, "Запасы");
	НоменклатураВДокументахКлиентСервер.ЗаполнитьТаблицуНастроекФормыВыбораНоменклатуры(Форма, "СборкаЗапасовОприходование", Форма.НастройкиФормыВыбораНоменклатуры,,"Продукция");
	НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(Форма, "Продукция");
	НоменклатураВДокументахКлиентСервер.ЗаполнитьТаблицуНастроекФормыВыбораНоменклатуры(Форма, "СборкаЗапасовОприходование", Форма.НастройкиФормыВыбораНоменклатуры,,"Отходы");
	НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(Форма, "Отходы");
	
	// Прослеживаемость
	ПрослеживаемостьФормыКлиентСерверУНФ.НастроитьВидимостьКолонокРНПТ(
		Форма, , , Истина); //Объект.ВидОперации = Форма.КэшЗначений.ВОСборка);
	ПрослеживаемостьФормыКлиентСерверУНФ.НастроитьВидимостьКолонокРНПТ(
		Форма, "ПродукцияГруппаРНПТ", "ПродукцияПодсказкаРНПТ", Истина); //Объект.ВидОперации = Форма.КэшЗначений.ВОРазборка);
	// Конец Прослеживаемость
	
КонецПроцедуры // УстановитьВидимостьИДоступность()

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЗаголовокКомандыЗаполнитьПоОстаткам(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	ЗаказВТЧ = (Объект.ПоложениеЗаказаПокупателя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти"));
	Если ЗаказВТЧ Тогда
		ЗаказЗаполнен = Ложь;
		Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя) Тогда
				ЗаказЗаполнен = Истина;
				Прервать;
			КонецЕсли; 
		КонецЦикла; 
	Иначе
		ЗаказЗаполнен = ЗначениеЗаполнено(Объект.ЗаказПокупателя);
	КонецЕсли;
	
	ЗаголовокКоманды = ?(ЗаказЗаполнен И Форма.ФОРезервированиеЗапасов, НСтр("ru = 'По остаткам и резервам'"), НСтр("ru = 'По остаткам'"));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыЗаполнитьПоОстаткам", "Заголовок", ЗаголовокКоманды);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовДляМобильногоКлиента()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	ЭтоМобильныйКлиент = Истина;
	РаботаСОтборами.НастроитьПанельОтборовМобильныйКлиент(ЭтотОбъект,,,,,Истина);
	МобильныйКлиентУНФ.НастроитьФормуОбъектаМобильныйКлиент(Элементы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеВидимостьюЭтапов(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ОтображатьЭтапы = ПроизводствоСЭтапами(Форма);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияЭтапы", 				    		"Видимость", ОтображатьЭтапы);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыЭтап", 		    					"Видимость", ОтображатьЭтапы);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РаспределениеЗапасовЭтап", 		    		"Видимость", ОтображатьЭтапы);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПродукцияПодразделениеЗавершающегоЭтапа",	"Видимость", ОтображатьЭтапы);
	
	Если НЕ ОтображатьЭтапы И НЕ Форма.ТолькоПросмотр Тогда
		Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
			СтрокаТабличнойЧасти.Этапы = "";
		КонецЦикла; 
		Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
			СтрокаТабличнойЧасти.Этап = ПредопределенноеЗначение("Справочник.ЭтапыПроизводства.ПустаяСсылка");
		КонецЦикла; 
		Для каждого СтрокаТабличнойЧасти Из Объект.РаспределениеЗапасов Цикл
			СтрокаТабличнойЧасти.Этап = ПредопределенноеЗначение("Справочник.ЭтапыПроизводства.ПустаяСсылка");
		КонецЦикла; 
		Объект.ВыполненныеЭтапы.Очистить();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Функция ЯчейкаДоступна(СтруктурнаяЕдиница)
	
	Если НЕ КэшЗначений.УчетПоЯчейкам Тогда
		Возврат Ложь;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(СтруктурнаяЕдиница) ИЛИ ТипЗнч(СтруктурнаяЕдиница)<>Тип("СправочникСсылка.СтруктурныеЕдиницы") Тогда
		Возврат Ложь;
	КонецЕсли; 
	Если НЕ КэшЗначений.Свойство("ДоступностьЯчеек") Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Результат = КэшЗначений.ДоступностьЯчеек.Получить(СтруктурнаяЕдиница);
	Если Результат = Неопределено Тогда
		МассивСтруктурныхЕдиниц = Новый Массив;
		МассивСтруктурныхЕдиниц.Добавить(СтруктурнаяЕдиница);
		ДополнитьДоступностьЯчеек(МассивСтруктурныхЕдиниц, КэшЗначений.ДоступностьЯчеек);
		Результат = КэшЗначений.ДоступностьЯчеек.Получить(СтруктурнаяЕдиница);
		Если Результат=Неопределено Тогда
			Возврат Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОбновитьДоступностьЯчеек()
	
	Если НЕ КэшЗначений.УчетПоЯчейкам Тогда
		Возврат;
	КонецЕсли; 	
	
	Если НЕ КэшЗначений.Свойство("ДоступностьЯчеек") Тогда
		КэшЗначений.Вставить("ДоступностьЯчеек", Новый ФиксированноеСоответствие(Новый Соответствие));
	КонецЕсли; 
	
	МассивСтруктурныхЕдиниц = Новый Массив;
	МассивСтруктурныхЕдиниц.Добавить(Объект.СтруктурнаяЕдиница);
	МассивСтруктурныхЕдиниц.Добавить(Объект.СтруктурнаяЕдиницаПродукции);
	Если Объект.ПоложениеСклада=Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Если Объект.ВидОперации=Перечисления.ВидыОперацийСборкаЗапасов.Разборка Тогда
			ИмяТЧ = "Продукция";
		Иначе
			ИмяТЧ = "Запасы";
		КонецЕсли; 
		Для каждого СтрокаТабличнойЧасти Из Объект[ИмяТч] Цикл
			МассивСтруктурныхЕдиниц.Добавить(СтрокаТабличнойЧасти.СтруктурнаяЕдиница);
		КонецЦикла; 
	Иначе
		МассивСтруктурныхЕдиниц.Добавить(Объект.СтруктурнаяЕдиницаЗапасов);
	КонецЕсли;
	МассивСтруктурныхЕдиниц = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивСтруктурныхЕдиниц);
	ДополнитьДоступностьЯчеек(МассивСтруктурныхЕдиниц, КэшЗначений.ДоступностьЯчеек);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДополнитьДоступностьЯчеек(СтруктурныеЕдиницы, СоответствиеСтруктурныхЕдиниц)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтруктурныеЕдиницы", СтруктурныеЕдиницы);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтруктурныеЕдиницы.Ссылка КАК Ссылка,
	|	СтруктурныеЕдиницы.ОрдерныйСклад КАК ОрдерныйСклад,
	|	СтруктурныеЕдиницы.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы
	|ИЗ
	|	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
	|ГДЕ
	|	СтруктурныеЕдиницы.Ссылка В(&СтруктурныеЕдиницы)";
	Выборка = Запрос.Выполнить().Выбрать();
	Соответствие = Новый Соответствие(СоответствиеСтруктурныхЕдиниц);
	Пока Выборка.Следующий() Цикл
		Соответствие.Вставить(Выборка.Ссылка, НЕ Выборка.ОрдерныйСклад И Выборка.ТипСтруктурнойЕдиницы=Перечисления.ТипыСтруктурныхЕдиниц.Склад);	
	КонецЦикла; 
	СоответствиеСтруктурныхЕдиниц = Новый ФиксированноеСоответствие(Соответствие);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПризнакиДоступностиЯчеек(Форма)

	Объект = Форма.Объект;
	КэшЗначений = Форма.КэшЗначений;
	Если НЕ КэшЗначений.УчетПоЯчейкам Тогда
		Возврат;
	КонецЕсли;
	Если Объект.ПоложениеСклада<>ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		Возврат;
	КонецЕсли; 
	 
	Если Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка") Тогда
		ИмяТЧ = "Продукция";
	Иначе
		ИмяТЧ = "Запасы";
	КонецЕсли;
	Для каждого СтрокаТабличнойЧасти Из Объект[ИмяТч] Цикл
		Результат = КэшЗначений.ДоступностьЯчеек.Получить(СтрокаТабличнойЧасти.СтруктурнаяЕдиница);
		Если Результат=Неопределено Тогда
			Результат = Ложь;
		КонецЕсли; 
		СтрокаТабличнойЧасти.ЯчейкаДоступна = Результат И ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтруктурнаяЕдиница);
	КонецЦикла; 
	Для каждого СтрокаТабличнойЧасти Из Форма.РаспределениеЗапасов Цикл
		Результат = КэшЗначений.ДоступностьЯчеек.Получить(СтрокаТабличнойЧасти.СтруктурнаяЕдиница);
		Если Результат=Неопределено Тогда
			Результат = Ложь;
		КонецЕсли; 
		СтрокаТабличнойЧасти.ЯчейкаДоступна = Результат И ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтруктурнаяЕдиница);
	КонецЦикла; 
	
КонецПроцедуры 

#КонецОбласти

#Область ЗаполнениеОбъектов

&НаКлиенте
Процедура СохранитьДокументКакШаблон(Параметр) Экспорт
	
	ЗаполнениеОбъектовУНФКлиент.СохранитьДокументКакШаблон(Объект, ОтображаемыеРеквизиты(), Параметр);
	
КонецПроцедуры

&НаСервере
Функция ОтображаемыеРеквизиты()
	
	Возврат ЗаполнениеОбъектовУНФ.ОтображаемыеРеквизиты(ЭтотОбъект);
	
КонецФункции

#КонецОбласти

#Область КопированиеСтрокТабличныхЧастей

// Процедура - обработчик нажатия кнопки Копировать строки в ТЧ Запасы.
//
&НаКлиенте
Процедура ЗапасыКопироватьСтроки(Команда)
	
	КопироватьСтроки("Запасы");
	
КонецПроцедуры

// Процедура - обработчик нажатия кнопки Вставить строки в ТЧ Запасы.
//
&НаКлиенте
Процедура ЗапасыВставитьСтроки(Команда)
	
	ИмяТабличнойЧасти = "Запасы";
	ВставитьСтроки(ИмяТабличнойЧасти);
	
КонецПроцедуры

// Процедура - обработчик нажатия кнопки Копировать строки в ТЧ Продукция.
//
&НаКлиенте
Процедура ПродукцияКопироватьСтроки(Команда)
	
	КопироватьСтроки("Продукция");
	
КонецПроцедуры

// Процедура - обработчик нажатия кнопки Вставить строки в ТЧ Продукция.
//
&НаКлиенте
Процедура ПродукцияВставитьСтроки(Команда)
	
	ИмяТабличнойЧасти = "Продукция";
	ВставитьСтроки(ИмяТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтходыКопироватьСтроки(Команда)
	
	КопироватьСтроки("Отходы");
	
КонецПроцедуры

// Процедура - обработчик нажатия кнопки Вставить строки в ТЧ Отходы.
//
&НаКлиенте
Процедура ОтходыВставитьСтроки(Команда)
	
	ВставитьСтроки("Отходы");
	
КонецПроцедуры

// Вызывает процедуру копирования строк и оповещает пользователя о количестве скопированных.
//
&НаКлиенте
Процедура КопироватьСтроки(ИмяТЧ)
	
	Если КопированиеТабличнойЧастиКлиент.МожноКопироватьСтроки(Объект[ИмяТЧ], Элементы[ИмяТЧ].ТекущиеДанные) Тогда
		КоличествоСкопированных = 0;
		КопироватьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных);
		КопированиеТабличнойЧастиКлиент.ОповеститьПользователяОКопированииСтрок(КоличествоСкопированных);
	КонецЕсли;
	
КонецПроцедуры

// Вызывает процедуру вставки строк и оповещает пользователя о количестве вставленных.
//
&НаКлиенте
Процедура ВставитьСтроки(ИмяТЧ)
	
	КоличествоСкопированных = 0;
	КоличествоВставленных = 0;
	ВставитьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных, КоличествоВставленных);
	ОбработатьВставленныеСтроки(ИмяТЧ, КоличествоВставленных);
	КопированиеТабличнойЧастиКлиент.ОповеститьПользователяОВставкеСтрок(КоличествоСкопированных, КоличествоВставленных);
	
КонецПроцедуры

// Выполняет копирование выделенных строк в буфер обмена.
//
&НаСервере
Процедура КопироватьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных)
	
	КопированиеТабличнойЧастиСервер.Копировать(Объект[ИмяТЧ], Элементы[ИмяТЧ].ВыделенныеСтроки, КоличествоСкопированных);
	
КонецПроцедуры

// Вставляет скопированные строки из буфера обмена в выбранную табличную часть.
//
&НаСервере
Процедура ВставитьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных, КоличествоВставленных)
	
	КопированиеТабличнойЧастиСервер.Вставить(Объект, ИмяТЧ, Элементы, КоличествоСкопированных, КоличествоВставленных);
	ОбработатьВставленныеСтрокиНаСервере(ИмяТЧ, КоличествоВставленных);
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	ЗаполнитьПризнакиИспользованияЭтапов();
	
	Если Объект.РучноеРаспределение И ИмяТЧ="Запасы" Тогда
		ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	КонецЕсли;
	
	Если РежимОстаткиИРезервы И ИмяТЧ="Запасы" Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает вставленные строки.
//
&НаСервере
Процедура ОбработатьВставленныеСтрокиНаСервере(ИмяТЧ, КоличествоВставленных)
	
	Количество = Объект[ИмяТЧ].Количество();
	Для Итератор = 1 По КоличествоВставленных Цикл
		Строка = Объект[ИмяТЧ][Количество - Итератор];
		
		СтруктураДанные = Новый Структура;
		СтруктураДанные.Вставить("Номенклатура", Строка.Номенклатура);
		СтруктураДанные.Вставить("Характеристика", Строка.Характеристика);
		
		СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
		
		Если ИмяТЧ = "Продукция" ИЛИ ИмяТЧ = "Запасы" Тогда
			Если НЕ ЗначениеЗаполнено(Строка.Спецификация) Тогда
				Строка.Спецификация = СтруктураДанные.Спецификация;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.ЕдиницаИзмерения) Тогда
			Строка.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВставленныеСтроки(ИмяТЧ, КоличествоВставленных)
	
	Количество = Объект[ИмяТЧ].Количество();
	
	Если ИмяТЧ="Продукция" ИЛИ ИмяТЧ="Запасы" Тогда
		
		ЕстьСпецификации = Ложь;
		Для Итератор = 1 По КоличествоВставленных Цикл
			Строка = Объект[ИмяТЧ][Количество - Итератор];
			ТабличныеЧастиУНФКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект, Строка);
			Если ЗначениеЗаполнено(Строка.Спецификация) Тогда
				ЕстьСпецификации = Истина;
			КонецЕсли; 
		КонецЦикла;
		
		Если ИмяТЧ="Продукция" Тогда
			Если ЕстьСпецификации Тогда
				ЗапасыНеЗаполнены = Истина;
			КонецЕсли; 
			УстановитьКартинкиЗакладок(ЭтотОбъект);
			ОбновитьСпискиВыбораРаспределение();
		ИначеЕсли ИмяТЧ="Запасы" Тогда
			ВывестиОтметкиКонтроля(ЭтотОбъект);
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область РаботаССериямиНоменклатуры

&НаКлиенте
Процедура ЗапасыСерииНоменклатурыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ОткрытьПодборСерииНоменклатуры("Запасы", "СерииНоменклатуры");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодборСерииНоменклатуры(ИмяТЧЗапасы, ИмяТЧСерииНоменклатуры)
		
	ТекущиеДанныеИдентификатор = Элементы[ИмяТЧЗапасы].ТекущиеДанные.ПолучитьИдентификатор();
	ПараметрыСерийНоменклатуры = ПараметрыПодбораСерийНоменклатуры(ТекущиеДанныеИдентификатор, ИмяТЧЗапасы, ИмяТЧСерииНоменклатуры);
	// Для подбора СН используем поле СтруктурнаяЕдиницаЗапасов
	Если Объект.ПоложениеСклада = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти")
		И ((Объект.ВидОперации = КэшЗначений.ВОСборка И ИмяТЧЗапасы = "Запасы")
		ИЛИ (Объект.ВидОперации = КэшЗначений.ВОРазборка И ИмяТЧЗапасы = "Продукция"))Тогда
		ПараметрыСерийНоменклатуры.Вставить("СтруктурнаяЕдиница", Элементы[ИмяТЧЗапасы].ТекущиеДанные.СтруктурнаяЕдиница);
		ПараметрыСерийНоменклатуры.Вставить("Ячейка", Элементы[ИмяТЧЗапасы].ТекущиеДанные.Ячейка);
	Иначе
		ПараметрыСерийНоменклатуры.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиницаЗапасов);
		ПараметрыСерийНоменклатуры.Вставить("Ячейка", Объект.ЯчейкаЗапасов);
	КонецЕсли;
	ОткрытьФорму("Обработка.ПодборСерийНоменклатуры.Форма", ПараметрыСерийНоменклатуры, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПродукцияСерииНоменклатурыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	ОткрытьПодборСерииНоменклатуры("Продукция","СерииНоменклатурыПродукция");
	
КонецПроцедуры

Функция ПолучитьСерииНоменклатурыЗапасыИзХранилища(АдресВоВременномХранилище, КлючСтроки)
	
	ПараметрыИменаПолей = Новый Структура;
	ПараметрыИменаПолей.Вставить("ИмяТЧЗапасы", "Запасы");
	ПараметрыИменаПолей.Вставить("ИмяТЧСерииНоменклатуры", "СерииНоменклатуры");
	
	Возврат СерииНоменклатурыУНФ.ПолучитьСерииНоменклатурыИзХранилища(Объект, АдресВоВременномХранилище, КлючСтроки,
		ПараметрыИменаПолей);
	
КонецФункции

Функция ПолучитьСерииНоменклатурыПродукцииИзХранилища(АдресВоВременномХранилище, КлючСтроки)
	
	ПараметрыИменаПолей = Новый Структура;
	ПараметрыИменаПолей.Вставить("ИмяТЧЗапасы", "Продукция");
	ПараметрыИменаПолей.Вставить("ИмяТЧСерииНоменклатуры", "СерииНоменклатурыПродукция");
	
	Возврат СерииНоменклатурыУНФ.ПолучитьСерииНоменклатурыИзХранилища(Объект, АдресВоВременномХранилище, КлючСтроки,
		ПараметрыИменаПолей);
	
КонецФункции

Функция ПараметрыПодбораСерийНоменклатуры(ТекущиеДанныеИдентификатор, ИмяТЧ, ИмяТЧСерииНоменклатуры)
	
	Если ИмяТЧ = "Запасы" И Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Сборка") Тогда
	    РежимПодбора = Истина;
	ИначеЕсли ИмяТЧ = "Продукция" И Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка") Тогда
		РежимПодбора = Истина;
	Иначе
		РежимПодбора = Ложь;
	КонецЕсли;
	
	Возврат СерииНоменклатурыУНФ.ПараметрыПодбораСерийНоменклатуры(Объект, ЭтотОбъект.УникальныйИдентификатор, ТекущиеДанныеИдентификатор, РежимПодбора, ИмяТЧ, ИмяТЧСерииНоменклатуры);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
// @skip-warning
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// ИнтеграцияГосИС
// @skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормИСКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда, Неопределено);
	
КонецПроцедуры
// Конец ИнтеграцияГосИС

#КонецОбласти

#Область РаспределениеЗапасов

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКартинкиЗакладок(Форма, Заполнено = Неопределено, Распределено = Неопределено)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если Заполнено <> (НЕ Форма.ЗапасыНеЗаполнены) Тогда
		Если Заполнено <> Неопределено Тогда
			Форма.ЗапасыНеЗаполнены = НЕ Заполнено;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТЧЗапасы", "Картинка", ?(Форма.ЗапасыНеЗаполнены, БиблиотекаКартинок.ВниманиеВВидеТреугольника, Новый Картинка));
	КонецЕсли; 	
	
	Если НЕ Объект.РучноеРаспределение Тогда
		Форма.ЗапасыНеРаспределены = Ложь;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТЧРаспределениеЗапасов", "Картинка", Новый Картинка);
	ИначеЕсли Распределено <> (НЕ Форма.ЗапасыНеРаспределены) Тогда
		Если Распределено <> Неопределено Тогда
			Форма.ЗапасыНеРаспределены = НЕ Распределено;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТЧРаспределениеЗапасов", "Картинка", ?(Форма.ЗапасыНеРаспределены, БиблиотекаКартинок.ВниманиеВВидеТреугольника, Новый Картинка));
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПредупрежденияЗапасы", "Видимость", Форма.ЗапасыНеЗаполнены);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПредупрежденияРаспределение", "Видимость", Форма.ЗапасыНеРаспределены);
	
КонецПроцедуры

&НаСервере
Процедура РаспределитьСервер()
	
	Если НЕ Объект.РучноеРаспределение Тогда
		Возврат;
	КонецЕсли; 
	
	ЗаполнитьРеквизитыТЧПоШапке(Объект);
	ПроизводствоСервер.РаспределитьМатериалы(Объект.Продукция, Объект.Запасы, Объект.РаспределениеЗапасов, Объект.ВыполненныеЭтапы, Объект.ЗаказНаПроизводство, Объект.ВидОперации);
	ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	УстановитьКартинкиЗакладок(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСпискиВыбораРаспределение()
	
	ТекущаяСтрока = Элементы.СписокПродукции.ТекущиеДанные;
	Если ТекущаяСтрока=Неопределено Тогда
		КлючСвязи = 0;
	Иначе
		КлючСвязи = ТекущаяСтрока.Значение;
	КонецЕсли;
	
	ИмяТабличнойЧасти = "Продукция";
	
	СписокПродукции.Очистить();
	Элементы.РаспределениеЗапасовНоменклатураПродукция.СписокВыбора.Очистить();
	Элементы.ЗапасыЗаказПокупателя.СписокВыбора.Очистить();
	
	СписокПродукции.Добавить(0, НСтр("ru = 'Вся продукция'"));
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		Если Объект.РучноеРаспределение Тогда
			Если СтрокаТабличнойЧасти.КлючСвязи=0 И НЕ ТолькоПросмотр Тогда
				СтрокаТабличнойЧасти.КлючСвязи = ТабличныеЧастиУНФКлиентСервер.СоздатьНовыйКлючСвязи(ЭтотОбъект);
			КонецЕсли; 
			Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;
			ПредставлениеПродукции = Представление(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика, СтрокаТабличнойЧасти.Спецификация);
			СписокПродукции.Добавить(СтрокаТабличнойЧасти.КлючСвязи, ПредставлениеПродукции);
			Элементы.РаспределениеЗапасовНоменклатураПродукция.СписокВыбора.Добавить(СтрокаТабличнойЧасти.КлючСвязи, ПредставлениеПродукции);
		КонецЕсли;
		Если Элементы.ЗапасыЗаказПокупателя.СписокВыбора.НайтиПоЗначению(СтрокаТабличнойЧасти.ЗаказПокупателя)=Неопределено Тогда
			ПредставлениеЗаказа = ?(ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя), Строка(СтрокаТабличнойЧасти.ЗаказПокупателя), НСтр("ru = '<Не указан>'"));
			Элементы.ЗапасыЗаказПокупателя.СписокВыбора.Добавить(СтрокаТабличнойЧасти.ЗаказПокупателя, ПредставлениеЗаказа);
		КонецЕсли; 
	КонецЦикла;
	
	Элементы.ЗапасыЗаказПокупателя.СписокВыбора.СортироватьПоПредставлению();
	
	ТекущаяСтрока = СписокПродукции.НайтиПоЗначению(КлючСвязи);
	Если ТекущаяСтрока=Неопределено Тогда
		ТекущаяСтрока = СписокПродукции[0];
	КонецЕсли; 
	Элементы.СписокПродукции.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
			
	Если Объект.РучноеРаспределение Тогда
		ОбновитьРаспределениеЗапасовНаФорме();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРаспределениеЗапасовНаФорме()
	
	Если НЕ Объект.РучноеРаспределение Тогда
		Возврат;
	КонецЕсли; 
	
	ТекущаяСтрока = Элементы.СписокПродукции.ТекущиеДанные;
	БезОтбора = (ТекущаяСтрока=Неопределено ИЛИ ТекущаяСтрока.Значение=0);
	Элементы.РаспределениеЗапасовГруппаПродукция.Видимость = БезОтбора;
	
	Если БезОтбора Тогда
		Строки = Объект.РаспределениеЗапасов;
		ТекущаяПродукция = 0;
	Иначе
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСвязиПродукция", ТекущаяСтрока.Значение);
		Строки = Объект.РаспределениеЗапасов.НайтиСтроки(СтруктураОтбора);
		ТекущаяПродукция = ТекущаяСтрока.Значение;
	КонецЕсли; 
	
	СоответствияПродукции = Новый Соответствие;
	ИмяТабличнойЧасти = "Продукция";
	Для каждого СтрокаПродукция Из Объект.Продукция Цикл
		Если СтрокаПродукция.КлючСвязи=0 Тогда
			СтрокаПродукция.КлючСвязи = ТабличныеЧастиУНФКлиентСервер.СоздатьНовыйКлючСвязи(ЭтотОбъект);
		КонецЕсли; 
		СоответствияПродукции.Вставить(СтрокаПродукция.КлючСвязи, СтрокаПродукция);
	КонецЦикла; 
	
	РаспределениеЗапасов.Очистить();
	Для каждого СтрокаТабличнойЧасти Из Строки Цикл
		Если НЕ БезОтбора И ТекущаяСтрока.Значение<>СтрокаТабличнойЧасти.КлючСвязиПродукция Тогда
			Продолжить;
		КонецЕсли; 
		СтрокаПродукция = СоответствияПродукции.Получить(СтрокаТабличнойЧасти.КлючСвязиПродукция);
		НоваяСтрока = РаспределениеЗапасов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		ЗаполнитьДанныеПродукцииВСтрокеРаспределения(НоваяСтрока, СтрокаПродукция);
		НоваяСтрока.ЯчейкаДоступна = ЯчейкаДоступна(НоваяСтрока.СтруктурнаяЕдиница);
	КонецЦикла; 
	
	ПеренумероватьРаспределение(РаспределениеЗапасов);
	ВывестиОтметкиКонтроля(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция Представление(Значение1 = Неопределено, Значение2 = Неопределено, Значение3 = Неопределено)
	
	Результат = "";
	МассивЗначений = Новый Массив;
	МассивЗначений.Добавить(Значение1);
	МассивЗначений.Добавить(Значение2);
	МассивЗначений.Добавить(Значение3);
	
	Для каждого Значение Из МассивЗначений Цикл
		Если НЕ ЗначениеЗаполнено(Значение) Тогда
			Продолжить;
		КонецЕсли;
		Результат = Результат + ?(ПустаяСтрока(Результат), "", ", ")+Строка(Значение);
	КонецЦикла; 
	
	Возврат Результат; 
	
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДанныеТЧРаспределениеЗапасов(Форма)
	
	Если НЕ Форма.Объект.РучноеРаспределение Тогда
		Возврат;
	КонецЕсли; 
	
	РаспределениеНаФорме = Форма.РаспределениеЗапасов;
	РаспределениеТЧ = Форма.Объект.РаспределениеЗапасов;
	ТекущаяПродукция = Форма.ТекущаяПродукция;
	
	Если ТекущаяПродукция>0 Тогда
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСвязиПродукция", ТекущаяПродукция);
		Строки = РаспределениеТЧ.НайтиСтроки(СтруктураОтбора);
		Если Строки.Количество()=0 Тогда
			ИндексВставки = РаспределениеТЧ.Количество();
		Иначе
			ИндексВставки = РаспределениеТЧ.Индекс(Строки[0]);
		КонецЕсли; 
	Иначе
		Строки = Новый Массив;
		РаспределениеТЧ.Очистить();
		ИндексВставки = 0;
	КонецЕсли; 
	Для каждого СтрокаФормы Из РаспределениеНаФорме Цикл
		Если СтрокаФормы.Количество=0 Тогда
			Продолжить;
		КонецЕсли; 
		Если ИндексВставки>=РаспределениеТЧ.Количество() Тогда
			НоваяСтрока = РаспределениеТЧ.Добавить();
		Иначе
			НоваяСтрока = РаспределениеТЧ.Вставить(ИндексВставки);
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаФормы);
		ИндексВставки = ИндексВставки + 1;
	КонецЦикла; 
	Для каждого СтрокаТабличнойЧасти Из Строки Цикл
		РаспределениеТЧ.Удалить(СтрокаТабличнойЧасти);
	КонецЦикла; 
	
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДанныеПродукцииВСтрокеРаспределения(СтрокаРаспределения, СтрокаПродукции)
	
	Если СтрокаПродукции=Неопределено Тогда
		СтрокаРаспределения.НоменклатураПродукция = Неопределено;
		СтрокаРаспределения.ХарактеристикаПродукция = Неопределено;
		СтрокаРаспределения.СпецификацияПродукция = Неопределено;
		СтрокаРаспределения.ЕдиницаИзмеренияПродукция = Неопределено;
		СтрокаРаспределения.КоличествоПродукция = 0;
		СтрокаРаспределения.РезервПродукция = 0;
		СтрокаРаспределения.ПартияПродукция = Неопределено;
		СтрокаРаспределения.СтруктурнаяЕдиницаПродукция = Неопределено;
		СтрокаРаспределения.ЯчейкаПродукция = Неопределено;
		СтрокаРаспределения.КлючСвязиПродукция = 0;
		СтрокаРаспределения.ЗаказПокупателя = Неопределено; 
	Иначе
		СтрокаРаспределения.НоменклатураПродукция = СтрокаПродукции.Номенклатура;
		СтрокаРаспределения.ХарактеристикаПродукция = СтрокаПродукции.Характеристика;
		СтрокаРаспределения.СпецификацияПродукция = СтрокаПродукции.Спецификация;
		СтрокаРаспределения.ЕдиницаИзмеренияПродукция = СтрокаПродукции.ЕдиницаИзмерения;
		СтрокаРаспределения.КоличествоПродукция = СтрокаПродукции.Количество;
		СтрокаРаспределения.РезервПродукция = СтрокаПродукции.Резерв;
		СтрокаРаспределения.ПартияПродукция = СтрокаПродукции.Партия;
		СтрокаРаспределения.СтруктурнаяЕдиницаПродукция = СтрокаПродукции.СтруктурнаяЕдиница;
		СтрокаРаспределения.ЯчейкаПродукция = СтрокаПродукции.Ячейка;
		СтрокаРаспределения.КлючСвязиПродукция = СтрокаПродукции.КлючСвязи;
		СтрокаРаспределения.ЗаказПокупателя = СтрокаПродукции.ЗаказПокупателя; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НайтиСуществующуюСтрокуРаспределения(Таблица, КлючСвязиПродукция, ДанныеЗапасов)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("КлючСвязиПродукция", КлючСвязиПродукция);
	СтруктураОтбора.Вставить("Номенклатура", ДанныеЗапасов.Номенклатура);
	СтруктураОтбора.Вставить("Характеристика", ДанныеЗапасов.Характеристика);
	СтруктураОтбора.Вставить("Партия", ДанныеЗапасов.Партия);
	СтруктураОтбора.Вставить("Спецификация", ДанныеЗапасов.Спецификация);
	СтруктураОтбора.Вставить("ЕдиницаИзмерения", ДанныеЗапасов.ЕдиницаИзмерения);
	СтруктураОтбора.Вставить("СтруктурнаяЕдиница", ДанныеЗапасов.СтруктурнаяЕдиница);
	СтруктураОтбора.Вставить("Ячейка", ДанныеЗапасов.Ячейка);
	СтруктураОтбора.Вставить("ЗаказПокупателя", ДанныеЗапасов.ЗаказПокупателя);
	Строки = Таблица.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат Неопределено;
	Иначе
		Возврат Строки[0];
	КонецЕсли; 
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПеренумероватьРаспределение(РаспределениеЗапасов)
	
	Ном = 1;
	Для каждого СтрокаТабличнойЧасти Из РаспределениеЗапасов Цикл
		СтрокаТабличнойЧасти.НомерСтроки = Ном;
		Ном = Ном+1;
	КонецЦикла; 	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоРаспределениюСервер()
	
	Если НЕ Объект.РучноеРаспределение Тогда
		Возврат;
	КонецЕсли; 
	
	ПроизводствоСервер.ЗаполнитьПоРаспределению(Объект.Запасы, Объект.РаспределениеЗапасов);
	ПроизводствоСервер.ЗаполнитьКэшКонтроляРаспределения(Объект, КэшКонтроляРаспределения);
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	ЗапасыНеЗаполнены = Ложь;
	ВывестиОтметкиКонтроля(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДанныеЗапасовВСтрокеРаспределения(Объект, СтрокаРаспределения, СтрокаЗапасов)
	
	ЗаполнитьЗначенияСвойств(СтрокаРаспределения, СтрокаЗапасов, ИменаКолонокЗапасов(Объект))
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиОтметкиКонтроля(Форма)
	
	Объект = Форма.Объект;
	Если НЕ Объект.РучноеРаспределение Тогда
		УстановитьКартинкиЗакладок(Форма);
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаРаспределения Из Форма.РаспределениеЗапасов Цикл
		СтрокаРаспределения.ОшибкаКоличество = Ложь;
	КонецЦикла;
	
	ЕстьОшибкиРаспределения = Ложь;
	Для каждого СтрокаКонтроля Из Форма.КэшКонтроляРаспределения Цикл
		Если СтрокаКонтроля.КоличествоЗапасы=СтрокаКонтроля.КоличествоРаспределение Тогда
			Продолжить;
		КонецЕсли;
		ЕстьОшибкиРаспределения = Истина;
		СтруктураОтбора = Новый Структура(ИменаКолонокЗапасов(Форма.Объект));
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаКонтроля);
		СтрокиРаспределения = Форма.РаспределениеЗапасов.НайтиСтроки(СтруктураОтбора);
		Для каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
			Если (СтрокаКонтроля.КоличествоЗапасы<>СтрокаКонтроля.КоличествоРаспределение) Тогда
				СтрокаРаспределения.ОшибкаКоличество = Истина;
			КонецЕсли; 
		КонецЦикла;  
	КонецЦикла;
	
	УстановитьКартинкиЗакладок(Форма, , НЕ ЕстьОшибкиРаспределения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКэшКонтроляПриИзмененииДанныхСтроки(НовыеДанныеСтроки = Неопределено)
	
	Если НЕ Объект.РучноеРаспределение Тогда
		Возврат;
	КонецЕсли; 
	
	Для ии = 1 По 2 Цикл
		Если ии=1 Тогда
			Если ТипЗнч(СтарыеДанныеСтроки)<>Тип("ФиксированнаяСтруктура") Тогда
				Продолжить;
			КонецЕсли; 
			СтруктураОтбора = Новый Структура(СтарыеДанныеСтроки);
			СтарыеДанныеСтроки = Неопределено;
		Иначе
			Если НовыеДанныеСтроки=Неопределено Тогда
				Продолжить;
			КонецЕсли; 
			СтруктураОтбора = Новый Структура(ИменаКолонокЗапасов(Объект));
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, НовыеДанныеСтроки);
		КонецЕсли;
		СтруктураИтогов = ИтогиПоЗапасам(СтруктураОтбора);
		СтрокиКонтроля = КэшКонтроляРаспределения.НайтиСтроки(СтруктураОтбора);
		Если СтрокиКонтроля.Количество()=0 Тогда
			СтрокаКонтроля = КэшКонтроляРаспределения.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаКонтроля, СтруктураОтбора);
		Иначе
			СтрокаКонтроля = СтрокиКонтроля[0];
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтрокаКонтроля, СтруктураИтогов);
		ЕстьОшибки = (СтрокаКонтроля.КоличествоЗапасы<>СтрокаКонтроля.КоличествоРаспределение);
		СтрокиНаФорме = РаспределениеЗапасов.НайтиСтроки(СтруктураОтбора);
		Для каждого СтрокаНаФорме Из СтрокиНаФорме Цикл
			СтрокаНаФорме.ОшибкаКоличество = ЕстьОшибки;
		КонецЦикла; 
	КонецЦикла;
	
	ЕстьОшибкиРаспределения = Ложь;
	Для каждого СтрокаКонтроля Из КэшКонтроляРаспределения Цикл
		Если СтрокаКонтроля.КоличествоЗапасы<>СтрокаКонтроля.КоличествоРаспределение Тогда
			ЕстьОшибкиРаспределения = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	УстановитьКартинкиЗакладок(ЭтотОбъект, , НЕ ЕстьОшибкиРаспределения);
	ОбновитьТекстПодсказкиРаспределения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТекстПодсказкиРаспределения()
	
	ПоясняющийТекстОшибкиРаспределение = "";
	
	ТекущаяСтрока = Элементы.РаспределениеЗапасов.ТекущиеДанные;
	Если ТекущаяСтрока=Неопределено Тогда
		ПоясняющийТекстОшибкиРаспределение = НСтр("ru = 'Результат распределения не соответствует данным о материалах и/или продукции.'");
		Возврат;
	ИначеЕсли ЗначениеЗаполнено(ТекущаяСтрока.НоменклатураПродукция) И НЕ ТекущаяСтрока.ОшибкаКоличество Тогда
		ПоясняющийТекстОшибкиРаспределение = НСтр("ru = 'Результат распределения не соответствует данным о материалах и/или продукции. Для подробной информации выделите строку с предупреждением.'");
		Возврат;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура(ИменаКолонокЗапасов(Объект));
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, ТекущаяСтрока);
	СтруктураИтогов = ИтогиПоЗапасам(СтруктураОтбора);
	ЕдиницаИзмерения = Строка(СтруктураОтбора.ЕдиницаИзмерения);
	
	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.НоменклатураПродукция) Тогда
		ПоясняющийТекстОшибкиРаспределение = ПоясняющийТекстОшибкиРаспределение 
		+ НСтр("ru = 'Не указана продукция распределения. '"); 
	КонецЕсли; 
	
	Если ТекущаяСтрока.ОшибкаКоличество Тогда
		ПоясняющийТекстОшибкиРаспределение = ПоясняющийТекстОшибкиРаспределение 
		+ СтрШаблон(НСтр("ru = 'Отличается количество материалов (%1 %2) и распределения (%3 %4). '"), СтруктураИтогов.КоличествоЗапасы, ЕдиницаИзмерения, СтруктураИтогов.КоличествоРаспределение, ЕдиницаИзмерения); 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Функция ИтогиПоЗапасам(СтруктураОтбора)
	
	СтруктураИтогов = Новый Структура("КоличествоЗапасы, КоличествоРаспределение", 0, 0, 0, 0);
	СтрокиЗапасы = Объект.Запасы.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаЗапасы Из СтрокиЗапасы Цикл
		СтруктураИтогов.КоличествоЗапасы = СтруктураИтогов.КоличествоЗапасы + СтрокаЗапасы.Количество;
	КонецЦикла; 
	СтрокиРаспределения = Объект.РаспределениеЗапасов.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
		СтруктураИтогов.КоличествоРаспределение = СтруктураИтогов.КоличествоРаспределение + СтрокаРаспределения.Количество;
	КонецЦикла;
	Возврат СтруктураИтогов;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИменаКолонокЗапасов(Объект)
	
	Возврат "Этап, Номенклатура, Характеристика, Партия, Спецификация, ЕдиницаИзмерения"
	+?(Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Сборка") 
	И Объект.ПоложениеСклада=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти"), ", СтруктурнаяЕдиница, Ячейка", "")
	+?(Объект.ПоложениеЗаказаПокупателя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти"), ", ЗаказПокупателя", "");	
	
КонецФункции 

&НаКлиенте
Процедура ОбновитьПодсказкуРаспределение(ПрочитатьЗначение = Ложь)
	
	Если ПрочитатьЗначение Тогда
		ОбновитьСпособРаспределенияПоУмолчанию();
	КонецЕсли; 
	
	ЭлементыЗаголовка = Новый Массив;
	ЭлементыЗаголовка.Добавить(
	СтрШаблон(НСтр("ru = 'Правила автоматического распределения материалов: по спецификациям и (или) 
	|пропорционально количеству выпускаемой продукции. Для ручного режима первичное 
	|распределение выполняется по тем же правилам, но есть возможность откорректировать
	|результат. Режим по умолчанию: %1 '"),
	?(РучноеРаспределениеПоУмолчанию, НСтр("ru = 'ручное распределение'"), НСтр("ru = 'автоматическое распределение'"))));
	ЭлементыЗаголовка.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'изменить'"), , , , "ИзменитьРежимПоУмолчанию"));
	Элементы.РучноеРаспределениеРасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(ЭлементыЗаголовка);	// АПК:1356 Используются предварительно локализованные строки с гиперссылками
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСпособРаспределенияПоУмолчанию()
	
	РучноеРаспределениеПоУмолчанию = (Константы.ИспользоватьРучноеРаспределениеМатериаловПоУмолчанию.Получить()=Перечисления.ДаНет.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСпособаРаспределенияПоУмолчанию(Значение, ДополнительныеПараметры) Экспорт
	
	ОбновитьПодсказкуРаспределение(Истина);	
	
КонецПроцедуры

#КонецОбласти

#Область ЭтапыПроизводства

&НаКлиентеНаСервереБезКонтекста
Функция ПроизводствоСЭтапами(Форма)
	
	Объект = Форма.Объект;
	КэшЗначений = Форма.КэшЗначений;
	
	Если НЕ КэшЗначений.ИспользоватьЭтапыПроизводства Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Разборка") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница) Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Форма.ТипСтруктурнойЕдиницы) И Форма.ТипСтруктурнойЕдиницы <> ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение") Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Если Объект.ПоложениеЗаказаПокупателя=ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
		ПодЗаказ = Ложь;
		Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя) Тогда
				ПодЗаказ = Истина;
				Прервать;
			КонецЕсли; 
		КонецЦикла;
	Иначе
		ПодЗаказ = ЗначениеЗаполнено(Объект.ЗаказПокупателя);
	КонецЕсли;
	
	Если НЕ ПодЗаказ И НЕ ЗначениеЗаполнено(Объект.ЗаказНаПроизводство) Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Если Объект.ВыполненныеЭтапы.Количество()>0 Тогда
		Возврат Истина;
	КонецЕсли; 
	
	ЕстьЭтапы = Ложь;
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		Если СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства Тогда
			ЕстьЭтапы = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат ЕстьЭтапы;		
	
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЭтапыНаФорме(Форма)
	
	КэшЗначений = Форма.КэшЗначений;
	Если НЕ КэшЗначений.ИспользоватьЭтапыПроизводства Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	Если Объект.ВидОперации<>ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Сборка") Тогда
		Возврат;
	КонецЕсли; 
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСвязи", СтрокаТабличнойЧасти.КлючСвязи);
		СтрокиЭтапы = Объект.ВыполненныеЭтапы.НайтиСтроки(СтруктураОтбора);
		СтрокаТабличнойЧасти.Этапы = "";
		Для каждого СтрокаЭтап Из СтрокиЭтапы Цикл
			СтрокаТабличнойЧасти.Этапы = СтрокаТабличнойЧасти.Этапы + ?(ПустаяСтрока(СтрокаТабличнойЧасти.Этапы), "", "; ") + Строка(СтрокаЭтап.Этап);
		КонецЦикла;
	КонецЦикла; 	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПредставлениеПустогоЭтапа(Список)
	
	Если Список.Количество()>0 И НЕ ЗначениеЗаполнено(Список[0].Значение) Тогда
		Список[0].Представление = НСтр("ru = '<Без этапов>'");
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВыполненныеЭтапы(СтрокаТабличнойЧасти)
	
	Если НЕ КэшЗначений.ИспользоватьЭтапыПроизводства Тогда
		Возврат;
	КонецЕсли; 	
	
	Если СтрокаТабличнойЧасти.КлючСвязи=0 Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("КлючСвязи", СтрокаТабличнойЧасти.КлючСвязи);
	СтрокиВыполненныеЭтапы = Объект.ВыполненныеЭтапы.НайтиСтроки(СтруктураОтбора);
	
	ЕстьЭтапы = СтрокиВыполненныеЭтапы.Количество()>0;
	
	Для каждого СтрокаЭтап Из СтрокиВыполненныеЭтапы Цикл
		Объект.ВыполненныеЭтапы.Удалить(СтрокаЭтап);
	КонецЦикла; 
	
	Если ЕстьЭтапы Тогда
		УстановитьКартинкиЗакладок(ЭтотОбъект, Ложь);
		ОбновитьЭтапыНаФорме(ЭтотОбъект);
	КонецЕсли; 
	
	Если НЕ СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства
		И ЗначениеЗаполнено(СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа) Тогда
		СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = Неопределено;
	КонецЕсли;
	
КонецПроцедуры
 
&НаСервере
Процедура ЗаполнитьПризнакиИспользованияЭтапов()
	
	Если НЕ КэшЗначений.ИспользоватьЭтапыПроизводства Тогда
		Возврат;
	КонецЕсли;
	Если Объект.ВидОперации=Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка Тогда
		Возврат;
	КонецЕсли;
	
	МассивСпецификаций = Новый Массив;
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства = Ложь;
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) И МассивСпецификаций.Найти(СтрокаТабличнойЧасти.Спецификация)=Неопределено Тогда
			МассивСпецификаций.Добавить(СтрокаТабличнойЧасти.Спецификация);
		КонецЕсли; 
	КонецЦикла;
	
	Если МассивСпецификаций.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	СпецификацииСПоэтапнымПроизводством = ПроизводствоСервер.СпецификацииСПоэтапнымПроизводством(МассивСпецификаций);
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
			Продолжить;
		КонецЕсли;
		СтрокаТабличнойЧасти.ИспользоватьЭтапыПроизводства = (СпецификацииСПоэтапнымПроизводством.Найти(СтрокаТабличнойЧасти.Спецификация)<>Неопределено);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти 

#Область ОстаткиИРезервы

&НаКлиенте
Процедура ДекорацияОтборПоДублямОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Элементы.Запасы.ОтборСтрок = Неопределено;
	Элементы.ЗапасыКонтекстноеМенюПоказатьДублиСтрок.Пометка = Ложь;
	Элементы.ДекорацияОтборПоДублям.Видимость = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыРазбитьСтроку(Команда)
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда Возврат КонецЕсли;
	
	Если СтрокаТабличнойЧасти.Количество = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Количество в текущей строке равно нулю.'");
		Сообщение.КлючДанных = Объект.Ссылка;
		Сообщение.Сообщить();
		Возврат
	КонецЕсли;
	
	
	ПараметрыОповещения = Новый Структура("СтрокаТабличнойЧасти", СтрокаТабличнойЧасти);
	Оповещение = Новый ОписаниеОповещения("РазбитьСтрокуТабличнойЧастиЗавершение", ЭтаФорма, ПараметрыОповещения);
	
	ПоказатьВводЧисла(Оповещение, СтрокаТабличнойЧасти.Количество, "Введите количество в новой строке", 15, 3);
КонецПроцедуры

&НаКлиенте
Процедура РазбитьСтрокуТабличнойЧастиЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда Возврат КонецЕсли;
	
	СтрокаТабличнойЧасти = Параметры.СтрокаТабличнойЧасти;
	
	Если Результат <= 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Количество в новой строке должно быть больше нуля.'");
		Сообщение.КлючДанных = Объект.Ссылка;
		Сообщение.Сообщить();
		Возврат
	КонецЕсли;
	
	Если Результат >= СтрокаТабличнойЧасти.Количество Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Количество в новой строке должно быть меньше чем в текущей.'");
		Сообщение.КлючДанных = Объект.Ссылка;
		Сообщение.Сообщить();
		Возврат
	КонецЕсли;

	НоваяСтрока = Объект.Запасы.Вставить(СтрокаТабличнойЧасти.НомерСтроки);
	ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТабличнойЧасти);
	
	Если СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Резерв Тогда
		СтрокаТабличнойЧасти.Резерв = СтрокаТабличнойЧасти.Резерв - Результат;
		НоваяСтрока.Резерв = Результат;
	Иначе
		НоваяСтрока.Резерв = 0;
	КонецЕсли;
	
	СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество - Результат;
	НоваяСтрока.Количество = Результат;
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковПоНоменклатуре(СтрокаТабличнойЧасти)
	КонецЕсли;
	
	Элементы.Запасы.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыОбновить(Команда)
	ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиИРезервы(Команда)
	
	РежимОстаткиИРезервы = Не РежимОстаткиИРезервы;
	Элементы.ОстаткиИРезервы.Пометка = Не Элементы.ОстаткиИРезервы.Пометка;
	
	ОбновитьОтображениеКолонокВРазрезеЗапасов();
	
	Если РежимОстаткиИРезервы Тогда
		Элементы.Запасы.ВысотаШапки = 2;
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	Иначе
		ПроверитьПоложениеСклада();
		Элементы.Запасы.ВысотаШапки = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеКолонокВРазрезеЗапасов()
	
	Элементы.ЗапасыРезерв.Видимость = Не РежимОстаткиИРезервы И ЗаказПокупателяЗаполнен(Объект) И Объект.ВидОперации=ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Сборка");
	Элементы.ЗапасыКонтекстноеМенюГруппаРаботаССтрокамиОбновить.Видимость = РежимОстаткиИРезервы;
	ЭтоРазборка	= (Объект.ВидОперации = КэшЗначений.ВОРазборка);
	ЭтоСборка	= (Объект.ВидОперации = КэшЗначений.ВОСборка);
	
	Если РежимОстаткиИРезервы Тогда
		
		Элементы.ЗапасыОстатки.Видимость = Истина;
		Элементы.ЗапасыСтруктурнаяЕдиница.Видимость = Ложь;
		
		Элементы.ЗапасыРезервДляРежимаОстатки.Видимость = Объект.Проведен;
		Элементы.ЗапасыВРезервеВсего.Видимость = Объект.Проведен;
		
		Элементы.ЗапасыРезервДляРежимаОстатки.Видимость = ФОРезервированиеЗапасов;
		Элементы.ЗапасыВРезервеВсего.Видимость = ФОРезервированиеЗапасов;
		Элементы.ЗапасыЗарезервировано.Видимость = ФОРезервированиеЗапасов;
		Элементы.ЗапасыИзменитьРезервЗаполнитьПоРезервам.Видимость = ФОРезервированиеЗапасов;
		
		Элементы.ЗапасыСтруктурнаяЕдиницаРезерв.Видимость = РазрешитьСкладыВТабличныхЧастях И НЕ ЭтоРазборка;
		
		Если ФОИспользоватьЯчейки И Элементы.ЗапасыЯчейка.Видимость Тогда
			Элементы.ЗапасыЯчейка.Видимость = Ложь;
			Элементы.ЗапасыЯчейкаДляРежимаОстатки.Видимость = РазрешитьСкладыВТабличныхЧастях И НЕ ЭтоРазборка;
		Иначе
			Элементы.ЗапасыЯчейкаДляРежимаОстатки.Видимость = Ложь;
		КонецЕсли;
		
		Если Элементы.РежимГТДМатериалы.Пометка Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РежимГТДПродукция", "Пометка", НЕ Элементы.РежимГТДПродукция.Пометка И ЭтоРазборка);
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РежимГТДМатериалы", "Пометка", НЕ Элементы.РежимГТДМатериалы.Пометка И ЭтоСборка);
			
			ВключитьРежимГТД = ?(ЭтоРазборка, Элементы.РежимГТДПродукция.Пометка, Элементы.РежимГТДМатериалы.Пометка);
			ИзменитьРежимРаботаГТД(ВключитьРежимГТД, ЭтоРазборка, ЭтоСборка);
		КонецЕсли;
		
	Иначе
		
		Элементы.ЗапасыСтруктурнаяЕдиница.Видимость = НЕ СкладВШапке И НЕ ЭтоРазборка;
		Элементы.ЗапасыЯчейка.Видимость = НЕ СкладВШапке И ФОИспользоватьЯчейки И НЕ ЭтоРазборка;
		Элементы.ЗапасыОстатки.Видимость = Ложь;
		Элементы.ЗапасыИзменитьРезервЗаполнитьПоРезервам.Видимость = Ложь;
		
		Если ФОИспользоватьЯчейки И Элементы.ЗапасыЯчейкаДляРежимаОстатки.Видимость Тогда
			Элементы.ЗапасыЯчейкаДляРежимаОстатки.Видимость = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ЗапасыЗаполнитьПоОстаткамИРезервамВсеСклады.Видимость = Не СкладВШапке Или РежимОстаткиИРезервы;
	Элементы.ЗапасыОстатокВЯчейке.Видимость = Элементы.ЗапасыЯчейкаДляРежимаОстатки.Видимость;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПоложениеСклада()
	
	Если Объект.ПоложениеСклада = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке") И РазрешитьСкладыВТабличныхЧастях Тогда
		
		ЗначениеСклада = Объект.СтруктурнаяЕдиницаЗапасов;
		
		Для Каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
			
			Если НЕ СтрокаТабличнойЧасти.СтруктурнаяЕдиница = ЗначениеСклада Тогда
				Объект.ПоложениеСклада = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти");
				УстановитьВидимостьОтПользовательскихНастроек(ЭтаФорма);
				Возврат;
			КонецЕсли;
			
			ЗначениеСклада = СтрокаТабличнойЧасти.СтруктурнаяЕдиница;
			
		КонецЦикла;
	
	КонецЕсли;
	
	УстановитьВидимостьОтПользовательскихНастроек(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьПоложениеСкладаПриПроведении(ТекущийОбъект)
	
	Если ТекущийОбъект.ПоложениеСклада = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке") И РазрешитьСкладыВТабличныхЧастях Тогда
		
		ЗначениеСклада = ТекущийОбъект.СтруктурнаяЕдиницаЗапасов;
		
		Для Каждого СтрокаТабличнойЧасти Из ТекущийОбъект.Запасы Цикл
			
			Если НЕ СтрокаТабличнойЧасти.СтруктурнаяЕдиница = ЗначениеСклада Тогда
				ТекущийОбъект.ПоложениеСклада = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти");
				Элементы.СтруктурнаяЕдиницаЗапасовСборка.Видимость = Ложь;
				Элементы.ЯчейкаЗапасовСборка.Видимость = Ложь;
				Возврат;
			КонецЕсли;
			
			ЗначениеСклада = СтрокаТабличнойЧасти.СтруктурнаяЕдиница;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере(СтруктураДанные = Неопределено, НоменклатураОтбора = Неопределено)
	
	УчетПоЯчейкам = ПолучитьФункциональнуюОпцию("УчетПоЯчейкам");
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
		СтрокаТабличнойЧасти.ЯчейкаДоступна = УчетПоЯчейкам И Не СтрокаТабличнойЧасти.СтруктурнаяЕдиница.ОрдерныйСклад;
	КонецЦикла;
	
	НоменклатураВДокументахСервер.ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧасти(Объект, СтруктураДанные, НоменклатураОтбора,,"Производство")
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыЯчейкаДляРежимаОстаткиПриИзменении(Элемент)
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗначенияОстатковПоНоменклатуре(ТекущиеДанные)
	
	Если РежимОстаткиИРезервы Тогда
		СтруктураДанные = Новый Структура;
		СтруктураДанные.Вставить("Организация", Компания);
		СтруктураДанные.Вставить("Номенклатура", 	ТекущиеДанные.Номенклатура);
		СтруктураДанные.Вставить("Характеристика", 	ТекущиеДанные.Характеристика);
		СтруктураДанные.Вставить("Партия", 	ТекущиеДанные.Партия);
		СтруктураДанные.Вставить("ИспользоватьХарактеристики", ТекущиеДанные.ИспользоватьХарактеристики);
		СтруктураДанные.Вставить("ИспользоватьПартии", ТекущиеДанные.ИспользоватьПартии);
		
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере(СтруктураДанные, ТекущиеДанные.Номенклатура);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДублиСтрок(Команда)
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда Возврат КонецЕсли;
	
	Если Элементы.Запасы.ОтборСтрок = Неопределено Тогда
		
		ПараметрыОтбора = Новый Структура("Номенклатура, Характеристика, Партия"
		, СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика, СтрокаТабличнойЧасти.Партия);
		
		НайденныеСтроки = Объект.Запасы.НайтиСтроки(ПараметрыОтбора);
		
		Если НайденныеСтроки.Количество() <=1 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Дубли строки не найдены.'");
			Сообщение.КлючДанных = Объект.Ссылка;
			Сообщение.Сообщить();
			Возврат
		КонецЕсли;
		
		ПараметрыОтбора = Новый ФиксированнаяСтруктура("Номенклатура, Характеристика, Партия"
		, СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика, СтрокаТабличнойЧасти.Партия);
		
		Элементы.Запасы.ОтборСтрок = ПараметрыОтбора;
		Элементы.ЗапасыКонтекстноеМенюПоказатьДублиСтрок.Пометка = Истина;
		
		Элементы.ДекорацияОтборПоДублям.Заголовок = ИнформацияОбОтборе(Строка(СтрокаТабличнойЧасти.НомерСтроки));
		Элементы.ДекорацияОтборПоДублям.Видимость = Истина;
		
	Иначе
		Элементы.Запасы.ОтборСтрок = Неопределено;
		Элементы.ЗапасыКонтекстноеМенюПоказатьДублиСтрок.Пометка = Ложь;
		Элементы.ДекорацияОтборПоДублям.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ИнформацияОбОтборе(НомерСтрокиПредставление)
	ТекстСообщения = НСтр("ru = 'Дубли строки '");
	Возврат НоменклатураВДокументахСервер.ИнформацияОбОтборе(ТекстСообщения + НомерСтрокиПредставление);
КонецФункции

&НаСервере
Функция СписокДокументовПоЗаказу(ТипОперации, СтруктураПараметров)
	Возврат НоменклатураВДокументахСервер.СписокДокументовПоЗаказу(Объект, ТипОперации, СтруктураПараметров);
КонецФункции

&НаКлиенте
Процедура ЗапасыСтруктурнаяЕдиницаРезервОчистка(Элемент, СтандартнаяОбработка)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда Возврат КонецЕсли;
	ЗаполнитьЗначенияОстатковПоНоменклатуре(СтрокаТабличнойЧасти);

КонецПроцедуры

&НаКлиенте
Процедура ЗапасыСтруктурнаяЕдиницаРезервОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если ВыбранноеЗначение = Неопределено Тогда Возврат КонецЕсли;
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда Возврат КонецЕсли;
	
	Если ВыбранноеЗначение = "ОтчетОстаткиНаСкладах" Тогда
		
		ТекущиеДанные = Элементы.Запасы.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено ИЛИ Не ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда Возврат КонецЕсли;
		
		Вариант = РаботаСФормойДокументаКлиентСервер.ВариантОтчета("ОстаткиТоваров", "ПоМестамХранения");
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("СформироватьПриОткрытии", Истина);
		ОтборРасшифровки = Новый Соответствие;
		ОтборРасшифровки.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
		ОтборРасшифровки.Вставить("Характеристика", ТекущиеДанные.Характеристика);
		ОтборРасшифровки.Вставить("СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы", ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Склад"));
		ПараметрыОткрытия.Вставить("ОтборРасшифровки", ОтборРасшифровки);
		
		ВариантыОтчетовКлиент.ОткрытьФормуОтчета(, Вариант, ПараметрыОткрытия);
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("Структура") И ВыбранноеЗначение.Свойство("Склад") Тогда
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = ВыбранноеЗначение.Склад;
		СтрокаТабличнойЧасти.Ячейка = ПредопределенноеЗначение("Справочник.Ячейки.ПустаяСсылка");
	Иначе
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = ВыбранноеЗначение;
		СтрокаТабличнойЧасти.Ячейка = ПредопределенноеЗначение("Справочник.Ячейки.ПустаяСсылка");
	КонецЕсли;
	СтрокаТабличнойЧасти.ЯчейкаДоступна = ЯчейкаДоступна(СтрокаТабличнойЧасти.СтруктурнаяЕдиница);
	
	ЗаполнитьЗначенияОстатковПоНоменклатуре(СтрокаТабличнойЧасти);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораИзВыпадающегоСпискаТабличнойЧасти(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда Возврат КонецЕсли;
	
	ТекущиеДанные = Элементы.Запасы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда Возврат КонецЕсли;
	
	Если Параметры.ВыбранноеПоле = "ЗапасыВРезерве" И Результат.Значение = Объект.Ссылка Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.ЗапасыРезервДляРежимаОстатки;
		Возврат;
	КонецЕсли;
	
	Если Параметры.ВыбранноеПоле = "ЗапасыОстатокОбщий" ИЛИ Параметры.ВыбранноеПоле = "ЗапасыВРезерве" Тогда
		
		Если Результат.Значение = "ОтчетОстаткиНаСкладах" Тогда
			
			Если ТекущиеДанные = Неопределено ИЛИ Не ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда Возврат КонецЕсли;
			
			Вариант = РаботаСФормойДокументаКлиентСервер.ВариантОтчета("ОстаткиТоваров", "ПоМестамХранения");
			
			ПараметрыОткрытия = Новый Структура;
			ПараметрыОткрытия.Вставить("СформироватьПриОткрытии", Истина);
			ОтборРасшифровки = Новый Соответствие;
			ОтборРасшифровки.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
			ОтборРасшифровки.Вставить("Характеристика", ТекущиеДанные.Характеристика);
			ОтборРасшифровки.Вставить("СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы", ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Склад"));
			ПараметрыОткрытия.Вставить("ОтборРасшифровки", ОтборРасшифровки);
			
			ВариантыОтчетовКлиент.ОткрытьФормуОтчета(, Вариант, ПараметрыОткрытия);
			
		Иначе
			
			Если ТипЗнч(Результат.Значение) = Тип("Структура") И Результат.Значение.Свойство("Склад") 
				И Не ТекущиеДанные.СтруктурнаяЕдиница = Результат.Значение.Склад Тогда
				
				Если ЗначениеЗаполнено(ТекущиеДанные.НеХватает) И Не ТекущиеДанные.Количество = ТекущиеДанные.НеХватает Тогда
					
					НоваяСтрока = Объект.Запасы.Вставить(ТекущиеДанные.НомерСтроки);
					ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекущиеДанные);
					
					НоваяСтрока.СтруктурнаяЕдиница = Результат.Значение.Склад;
					
					СтруктураОстатков = СвободныйОстатокПоСтруктурнойЕдинице(НоваяСтрока.СтруктурнаяЕдиница, НоваяСтрока.Номенклатура
					, НоваяСтрока.Характеристика, НоваяСтрока.Партия, НоваяСтрока.ЕдиницаИзмерения);
					
					НоваяСтрока.Количество = ТекущиеДанные.НеХватает;
					
					ТекущиеДанные.Количество = ТекущиеДанные.Количество - ТекущиеДанные.НеХватает;
					
					НоваяСтрока.Резерв = СтруктураОстатков.КоличествоРезерв;
					
					ЗаполнитьЗначенияОстатковПоНоменклатуре(НоваяСтрока);
					
					Элементы.Запасы.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
					
				Иначе
					
					ТекущиеДанные.СтруктурнаяЕдиница = Результат.Значение.Склад;
					
					СтруктураОстатков = СвободныйОстатокПоСтруктурнойЕдинице(ТекущиеДанные.СтруктурнаяЕдиница, ТекущиеДанные.Номенклатура
										, ТекущиеДанные.Характеристика, ТекущиеДанные.Партия, ТекущиеДанные.ЕдиницаИзмерения);
					
					ТекущиеДанные.Резерв = СтруктураОстатков.КоличествоРезерв;
					ТекущиеДанные.ОстатокСвободно = СтруктураОстатков.КоличествоСвободно;
					
					ЗаполнитьЗначенияОстатковПоНоменклатуре(ТекущиеДанные);
					
				КонецЕсли;
				
			Иначе
				
				Если Не ТипЗнч(Результат.Значение) = Тип("Структура") Тогда
					ПоказатьЗначение(,Результат.Значение);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	ПоказатьЗначение(,Результат.Значение);
		
КонецПроцедуры

&НаСервере
Функция СвободныйОстатокПоСтруктурнойЕдинице(СтруктурнаяЕдиница, Номенклатура, Характеристика, Партия, ЕдиницаИзмерения)
	
	Коэффициент = ?(ТипЗнч(ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения"), ЕдиницаИзмерения.Коэффициент, 1);
	
	Организация = Константы.УчетПоКомпании.Компания(Объект.Организация);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Партия", Партия);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	
	ЕстьЗаказ = ЗначениеЗаполнено(Объект.ЗаказПокупателя);
	
	Если ЕстьЗаказ Тогда
		Запрос.УстановитьПараметр("Заказ", Объект.ЗаказПокупателя);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ЗапасыОстаткиСвободные.КоличествоОстаток) КАК КоличествоСвободно,
		|	СУММА(ЗапасыОстаткиРезерв.КоличествоОстаток) КАК КоличествоРезерв
		|ИЗ
		|	РегистрНакопления.Запасы.Остатки(
		|			,
		|			Номенклатура = &Номенклатура
		|				И ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|				И Организация = &Организация
		|				И Характеристика = &Характеристика
		|				И Партия = &Партия
		|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК ЗапасыОстаткиСвободные,
		|	РегистрНакопления.Запасы.Остатки(
		|			,
		|			Номенклатура = &Номенклатура
		|				И ЗаказПокупателя = &Заказ
		|				И Организация = &Организация
		|				И Характеристика = &Характеристика
		|				И Партия = &Партия
		|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК ЗапасыОстаткиРезерв
		|
		|УПОРЯДОЧИТЬ ПО
		|	КоличествоСвободно УБЫВ";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ЗапасыОстаткиСвободные.КоличествоОстаток) КАК КоличествоСвободно
		|ИЗ
		|	РегистрНакопления.Запасы.Остатки(
		|			,
		|			Номенклатура = &Номенклатура
		|				И ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|				И Организация = &Организация
		|				И Характеристика = &Характеристика
		|				И Партия = &Партия
		|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК ЗапасыОстаткиСвободные
		|
		|УПОРЯДОЧИТЬ ПО
		|	КоличествоСвободно УБЫВ";
	КонецЕсли;
	
	Результат = Запрос.Выполнить().Выбрать();
	
	СтруктураВозврата = Новый Структура("КоличествоСвободно, КоличествоРезерв");
	
	Пока Результат.Следующий() Цикл
		СтруктураВозврата.КоличествоСвободно = Результат.КоличествоСвободно/Коэффициент;
		СтруктураВозврата.КоличествоРезерв = ?(ЕстьЗаказ, Результат.КоличествоРезерв/Коэффициент, 0);
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьПоОстаткамИРезервамВсеСклады(Команда)
	
	Если Объект.Запасы.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Табличная часть ""Товары, услуги"" не заполнена.'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьТабличнуюЧастьПоОстаткамИРезервамНаСервере();
	КонецЕсли;
	
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧастьПоОстаткамИРезервамНаСервере()
	
	Если СкладВШапке Тогда
		ЗначениеСклада = ?(Не ЗначениеЗаполнено(Объект.СтруктурнаяЕдиницаЗапасов), Неопределено, Объект.СтруктурнаяЕдиницаЗапасов);
			Для Каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = ЗначениеСклада;
			КонецЦикла;
	КонецЕсли;
	
	НоменклатураВДокументахСервер.ЗаполнитьКоличествоПоОстаткамИРезервамПроизводство(Объект);
	
	//Ячейки
	ОбновитьДоступностьЯчеек();
	ЗаполнитьПризнакиДоступностиЯчеек(ЭтотОбъект);
	// Конец Ячейки
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	Если РежимОстаткиИРезервы Тогда
		ЗаполнитьЗначенияОстатковНоменклатурыВТабличнойЧастиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеОбработчики

&НаКлиенте
Процедура ПослеВыполненияФоновогоЗадания(Прогресс, ДополнительныеПараметры) Экспорт
	
	Если НЕ Прогресс = Неопределено Тогда
		ОбработатьПодготовленныеДанные(Прогресс.АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНеобходимостьПодключенияОбработчикаОжидания(ДлительнаяОперация, СтруктураПараметров = Неопределено)
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ПослеВыполненияФоновогоЗадания(ДлительнаяОперация, СтруктураПараметров);
		
	Иначе
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеВыполненияФоновогоЗадания", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Идет загрузка данных.'");
		ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прослеживаемость

&НаКлиенте
Процедура ОбработкаВыбораРНПТННаКлиенте(ВыбранноеЗначение)
	
	СтрокаТабличнойЧасти = Элементы[ИмяТабличнойЧасти].ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти <> Неопределено Тогда
		Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ИдентификаторСтроки) Тогда
			СтрокаТабличнойЧасти.ИдентификаторСтроки = Новый УникальныйИдентификатор;
		КонецЕсли;
		ОбработкаВыбораРНПТННаСервере(СтрокаТабличнойЧасти.ИдентификаторСтроки, ВыбранноеЗначение);
		ПрослеживаемостьФормыКлиентСерверУНФ.ЗаполнитьПредставлениеРНПТ(ЭтаФорма, СтрокаТабличнойЧасти);
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте 
Процедура ОбработкаВыбораГТДНаКлиенте(ВыбранноеЗначение)
	
	СтрокаТабличнойЧасти = Элементы[ИмяТабличнойЧасти].ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти <> Неопределено Тогда
		
		// Если в результате выбора пришло только значение,
		// то переложим его в структуру.
		Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.НомераГТД") Тогда
			ВыбранноеЗначение = Новый Структура("НомерГТД", ВыбранноеЗначение);
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыбранноеЗначение);
		
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.НомерГТД) Тогда
			
			ДатаГТД = ГрузовыеТаможенныеДекларацииКлиент.ВыделитьДатуИзНомераГТД(Строка(СтрокаТабличнойЧасти.НомерГТД));
			Если ДатаГТД > КонецДня(Объект.Дата) Тогда
				
				ТекстСообщения = НСтр("ru ='Выбранная ГТД датирована более поздней датой, чем текущий документ'");
				ИмяПоля = СтрШаблон("Объект.%1[%2].РНПТ", ИмяТабличнойЧасти, СтрокаТабличнойЧасти.ПолучитьИдентификатор());
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , ИмяПоля);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПрослеживаемостьФормыКлиентСерверУНФ.ЗаполнитьПредставлениеРНПТ(ЭтаФорма, СтрокаТабличнойЧасти);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораРНПТННаСервере(ИдентификаторСтроки, ВыбранноеЗначение)
	
	ПрослеживаемостьФормыУНФ.ОбработкаВыбораРНПТН(ЭтотОбъект, ИдентификаторСтроки, ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСвязанныеЗаписи(СтрокаТабличнойЧасти)
	
	СтруктураОтбора = Новый Структура("ИдентификаторСтроки", СтрокаТабличнойЧасти.ИдентификаторСтроки);
	МассивСтрокСведенияПрослеживаемости = Объект.СведенияПрослеживаемости.НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаТЧ Из МассивСтрокСведенияПрослеживаемости Цикл
		Объект.СведенияПрослеживаемости.Удалить(СтрокаТЧ);
	КонецЦикла;
	ПрослеживаемостьФормыКлиентСерверУНФ.ЗаполнитьПредставлениеРНПТ(ЭтаФорма, СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПризнакПрослеживаемости()
	
	ИспользуетсяПрослеживаемость = КэшЗначений.ВестиУчетПрослеживаемыхТоваров;
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		СтрокаТабличнойЧасти.ПрослеживаемыйТовар = Ложь;
	КонецЦикла; 
	Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
		СтрокаТабличнойЧасти.ПрослеживаемыйТовар = Ложь;
	КонецЦикла;
	
	Если Объект.ВидОперации = КэшЗначений.ВОРазборка Тогда
		ПрослеживаемостьФормыУНФ.ОбновитьПризнакПрослеживаемости(Объект.Запасы, Объект.СведенияПрослеживаемости, Ложь);
		ПрослеживаемостьФормыУНФ.ОбновитьПризнакПрослеживаемости(Объект.Продукция, Объект.СведенияПрослеживаемости, ИспользуетсяПрослеживаемость);
	Иначе
		ПрослеживаемостьФормыУНФ.ОбновитьПризнакПрослеживаемости(Объект.Продукция, Объект.СведенияПрослеживаемости, Ложь);
		ПрослеживаемостьФормыУНФ.ОбновитьПризнакПрослеживаемости(Объект.Запасы, Объект.СведенияПрослеживаемости, ИспользуетсяПрослеживаемость);
	КонецЕсли;
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		ПрослеживаемостьФормыКлиентСерверУНФ.ЗаполнитьПредставлениеРНПТ(ЭтотОбъект, СтрокаТабличнойЧасти, Объект.ВидОперации = КэшЗначений.ВОРазборка);
	КонецЦикла; 
	Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
		ПрослеживаемостьФормыКлиентСерверУНФ.ЗаполнитьПредставлениеРНПТ(ЭтотОбъект, СтрокаТабличнойЧасти, Объект.ВидОперации = КэшЗначений.ВОСборка);
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеПрослеживаемости()
	
	ПрослеживаемостьФормыУНФ.ПриСозданииФормы(ЭтотОбъект, КэшЗначений);
	Для каждого СтрокаТабличнойЧасти Из Объект.Продукция Цикл
		ПрослеживаемостьФормыКлиентСерверУНФ.ЗаполнитьПредставлениеРНПТ(ЭтотОбъект, СтрокаТабличнойЧасти, Объект.ВидОперации = КэшЗначений.ВОРазборка);
	КонецЦикла; 
	Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
		ПрослеживаемостьФормыКлиентСерверУНФ.ЗаполнитьПредставлениеРНПТ(ЭтотОбъект, СтрокаТабличнойЧасти, Объект.ВидОперации = КэшЗначений.ВОСборка);
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти 

#Область Инициализация

ЭтоНоваяСтрокаЗапасы = Ложь;
ЭтоНоваяСтрокаПродукция = Ложь;

#КонецОбласти