
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьЭтапыПроизводства") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	Если НЕ Параметры.Свойство("Спецификация") 
		ИЛИ НЕ Параметры.Свойство("ВыполненныеЭтапы")
		ИЛИ НЕ Параметры.Свойство("КлючСвязи", КлючСвязи) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Спецификация", Параметры.Спецификация);
	Запрос.УстановитьПараметр("ВыполненныеЭтапы", Параметры.ВыполненныеЭтапы);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыПроизводстваЭтапы.НомерСтроки КАК НомерСтроки,
	|	ВидыПроизводстваЭтапы.Этап КАК Этап,
	|	ВЫБОР
	|		КОГДА ВидыПроизводстваЭтапы.Этап В (&ВыполненныеЭтапы)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Пометка
	|ИЗ
	|	Справочник.Спецификации КАК Спецификации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыПроизводства.Этапы КАК ВидыПроизводстваЭтапы
	|		ПО Спецификации.ВидПроизводства = ВидыПроизводстваЭтапы.Ссылка
	|ГДЕ
	|	Спецификации.Ссылка = &Спецификация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	ЭтапыПроизводства.Ссылка,
	|	ИСТИНА
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	НЕ ЭтапыПроизводства.Ссылка В
	|				(ВЫБРАТЬ
	|					ВидыПроизводстваЭтапы.Этап
	|				ИЗ
	|					Справочник.Спецификации КАК Спецификации
	|						ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыПроизводства.Этапы КАК ВидыПроизводстваЭтапы
	|						ПО
	|							Спецификации.ВидПроизводства = ВидыПроизводстваЭтапы.Ссылка
	|				ГДЕ
	|					Спецификации.Ссылка = &Спецификация)
	|	И ЭтапыПроизводства.Ссылка В(&ВыполненныеЭтапы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ВыполненныеЭтапы.Добавить(Выборка.Этап, , Выборка.Пометка);	
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	МассивЭтапов = Новый Массив;
	Для каждого Элемент Из ВыполненныеЭтапы Цикл
		Если НЕ Элемент.Пометка Тогда
			Продолжить;
		КонецЕсли;
		Если МассивЭтапов.Найти(Элемент.Значение)<>Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		МассивЭтапов.Добавить(Элемент.Значение);
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("КлючСвязи", КлючСвязи);
	Результат.Вставить("ВыполненныеЭтапы", МассивЭтапов);
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

#КонецОбласти 
 

