
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаВажныеКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДата();
	КонецЕсли;
	
	Компания = Константы.УчетПоКомпании.Компания(Объект.Организация);
	Контрагент = Объект.Контрагент;
	Договор = Объект.Договор;
	Заказ = Объект.ЗаказПоставщику;
	ВалютаРасчетов = Объект.Договор.ВалютаРасчетов;
	НациональнаяВалюта = Константы.НациональнаяВалюта.Получить();
	ОтборНациональнаяВалюта = Новый Структура("Валюта", НациональнаяВалюта);
	СтруктураПоВалюте = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Объект.Дата, ОтборНациональнаяВалюта);
	КурсНациональнаяВалюта = СтруктураПоВалюте.Курс;
	КратностьНациональнаяВалюта = СтруктураПоВалюте.Кратность;
	СчетУчетаНДСПоУмолчанию = ПланыСчетов.Управленческий.Налоги;
	СкладПоУмолчанию = Справочники.СтруктурныеЕдиницы.ОсновнойСклад;
	
	// Сформируем надпись цены и валюты.
	УчетВалютныхОпераций = Константы.ФункциональнаяУчетВалютныхОпераций.Получить();
	СтруктураНадписи = Новый Структура;
	СтруктураНадписи.Вставить("ВалютаДокумента", Объект.ВалютаДокумента);
	СтруктураНадписи.Вставить("ВалютаРасчетов", ВалютаРасчетов);
	СтруктураНадписи.Вставить("Курс", Объект.Курс);
	СтруктураНадписи.Вставить("КурсНациональнаяВалюта", КурсНациональнаяВалюта);
	СтруктураНадписи.Вставить("СуммаВключаетНДС", Объект.СуммаВключаетНДС);
	СтруктураНадписи.Вставить("УчетВалютныхОпераций", УчетВалютныхОпераций);
	СтруктураНадписи.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
	ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НаправлениеДеятельности", "Видимость",
		Объект.ТаможенныйШтраф <> 0);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СчетУчетаЗатрат", "Видимость",
		Объект.ТаможенныйШтраф <> 0);
	
	ОбменСБухгалтериейНастроен = УправлениеНебольшойФирмойПовтИсп.ОбменСБухгалтериейНастроен();
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыДокументПартии", "Видимость", ОбменСБухгалтериейНастроен);
	
	// Установка видимости договора.
	УстановитьВидимостьДоговора();
	
	// КопированиеСтрокТабличныхЧастей
	КопированиеТабличнойЧастиСервер.ПриСозданииНаСевере(Элементы, "Запасы");
	
	// ГрупповоеИзменениеСтрок
	ИнициализироватьГрупповоеИзменениеСтрок();
	// Конец ГрупповоеИзменениеСтрок
	
	АвтоподборКонтактов.ПодключитьОбработчикиСобытияАвтоподбор(ЭтотОбъект);
	
	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	
	УсловноеОформлениеТабличныхЧастей();
	
	// Характеристики
	НоменклатураВДокументахСервер.ОбновитьУсловноеОформлениеТабличнойЧастиДляХарактеристик(ЭтаФорма);
	Если Параметры.Ключ.Пустая() Тогда
		НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект, Истина)
	КонецЕсли;
	
	СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
	
	ЗаполнитьНомерРазделаПоНомеруСтроки();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ПроверитьЗаполнениеТНВЭД();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НЕ ЗавершениеРаботы Тогда
		// ГрупповоеИзменениеСтрок
		СохранитьТекущееДействиеИзмененияСтрок();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Контрагент" 
		И ЗначениеЗаполнено(Параметр)
		И Объект.Контрагент = Параметр Тогда
		
		УстановитьВидимостьДоговора();
		
	КонецЕсли;
	
	// КопированиеСтрокТабличныхЧастей
	Если ИмяСобытия = "БуферОбменаТабличнаяЧастьКопированиеСтрок" Тогда
		КопированиеТабличнойЧастиКлиент.ОбработкаОповещения(Элементы, "Запасы");
	КонецЕсли;
	
	// Обсуждения
	ОбсужденияУНФКлиент.ОбработкаОповещения(ИмяСобытия, Параметр, Источник, ЭтаФорма, Объект.Ссылка);
	// Конец Обсуждения
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Объект.ТаможенныйШтраф <> 0
		И НЕ (ЗначениеЗаполнено(Объект.НаправлениеДеятельности)
				И ЗначениеЗаполнено(Объект.СчетУчетаЗатрат)) Тогда
				
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОсновныеСведения;
		ТекстОшибки = НСтр("ru ='Проверьте заполнение направления деятельности и счета затрат'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , , , Отказ);
		
	Иначе
		
		// СтандартныеПодсистемы.ОценкаПроизводительности
		Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			ОценкаПроизводительностиКлиент.ЗамерВремени("Проведение"
			+ РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
		КонецЕсли;
		// СтандартныеПодсистемы.ОценкаПроизводительности
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ТекстСообщения = "";
		Справочники.ДоговорыКонтрагентов.ПроверитьСоответствиеДоговораУсловиямДокумента(ТекстСообщения, Объект.Договор, Объект.Ссылка, Объект.Организация, Объект.Контрагент, Отказ);
		
		Если ТекстСообщения <> "" Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ?(Отказ, НСтр("ru = 'Документ не проведен! '") + ТекстСообщения, ТекстСообщения);
			
			Если Отказ Тогда
				Сообщение.ПутьКДанным = "Объект";
				Сообщение.Поле = "Договор";
				Сообщение.Сообщить();
				Возврат;
			Иначе
				Сообщение.Сообщить();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Модифицированность",Модифицированность);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	
	Оповестить("ОповещениеОбИзмененииДолга");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект,
		"Объект.Комментарий");
		
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Элемент.ТекущиеДанные.Пометка = Истина;
	
	ТекущиеДанные = Элементы.Разделы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		НомерРаздела = 1;
	Иначе
		НомерРаздела = Элементы.Разделы.ТекущиеДанные.НомерСтроки;
	КонецЕсли;
		
	Элемент.ТекущиеДанные.НомерРаздела = НомерРаздела;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыНоменклатураПриИзменении(Элемент)
	
	СтрокаРаздела = Элементы.Разделы.ТекущиеДанные;
	СтрокаЗапасы = Элементы.Запасы.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Организация", Объект.Организация);
	СтруктураДанные.Вставить("Номенклатура", СтрокаЗапасы.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаЗапасы.Характеристика);
	СтруктураДанные.Вставить("Партия", СтрокаЗапасы.Партия);

	СтруктураДанные.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
	СтруктураДанные.Вставить("ДатаОбработки", Объект.Дата);
	
	СтатусПартии = Новый СписокЗначений;
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
	
	СтруктураДанные.Вставить("СтатусПартии", СтатусПартии);
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	СтрокаЗапасы.Количество			= 1;
	СтрокаЗапасы.НомерРаздела		= СтрокаРаздела.НомерСтроки;
	СтрокаЗапасы.СуммаПошлины		= 0;
	СтрокаЗапасы.СуммаНДС			= 0;
	СтрокаЗапасы.СтранаПроисхождения= СтруктураДанные.СтранаПроисхождения;
	
	СтрокаЗапасы.ИспользоватьХарактеристики = СтруктураДанные.ИспользоватьХарактеристики;
	СтрокаЗапасы.ПроверятьЗаполнениеХарактеристики = СтруктураДанные.ПроверятьЗаполнениеХарактеристики;
	СтрокаЗапасы.ЗаполнениеХарактеристикиПроверено = Истина;
	
	Если СтруктураДанные.ИспользоватьХарактеристики Тогда
		Если ПодборНоменклатурыИзСписка И ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтрокаЗапасы.Характеристика = СтруктураВыбораНоменклатуры.Характеристика;
			СтруктураВыбораНоменклатуры.Характеристика = Неопределено;
		Иначе
			СтрокаЗапасы.Характеристика = СтруктураДанные.Характеристика;
		КонецЕсли;
	КонецЕсли;
	
	//Партии
	СтрокаЗапасы.ИспользоватьПартии = СтруктураДанные.ИспользоватьПартии;
	СтрокаЗапасы.ПроверятьЗаполнениеПартий = СтруктураДанные.ПроверятьЗаполнениеПартий;
	
	Если СтруктураДанные.ИспользоватьПартии Тогда
		Если ПодборНоменклатурыИзСписка И ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтрокаЗапасы.Партия = СтруктураВыбораНоменклатуры.Партия;
			СтруктураВыбораНоменклатуры.Партия = Неопределено;
		Иначе
			СтрокаЗапасы.Партия = СтруктураДанные.Партия;
		КонецЕсли;
	КонецЕсли;
	// Конец Партии
	
	ПодборНоменклатурыИзСписка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		
		СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
		
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыбранноеЗначение);
		
		ПодборНоменклатурыИзСписка = ВыбранноеЗначение.Свойство("Ячейка");
		
		Если НЕ ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
		КонецЕсли;
		
		СтруктураВыбораНоменклатуры.Характеристика = ВыбранноеЗначение.Характеристика;
		СтруктураВыбораНоменклатуры.Партия = ВыбранноеЗначение.Партия;
		
		ВыбранноеЗначение = ВыбранноеЗначение.Номенклатура;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(Объект.Ссылка, Объект.Дата, ДатаПередИзменением,
			ВалютаРасчетов);
			
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВалютаРасчетов) Тогда
			ПересчитатьКурсКратностьВалютыРасчетов(СтруктураДанные);
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Объект.Номер = "";
	СтруктураДанные = ПолучитьДанныеОрганизацияПриИзменении(Объект.Организация);
	Компания = СтруктураДанные.Компания;
	
	Объект.Договор = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Объект.Контрагент, Объект.Организация);
	ОбработатьИзменениеДоговора();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	КонтрагентПередИзменением = Контрагент;
	КонтрагентВестиРасчетыПоЗаказамПередИзменением = КонтрагентВестиРасчетыПоЗаказам;
	Контрагент = Объект.Контрагент;
	
	Если КонтрагентПередИзменением <> Объект.Контрагент Тогда
		
		СтруктураДанных = ПолучитьДанныеКонтрагентПриИзменении(Объект.Дата, Объект.ВалютаДокумента, Объект.Контрагент,
			Объект.Организация);
		Объект.Договор = СтруктураДанных.Договор;
		
		СтруктураДанных.Вставить("КонтрагентПередИзменением", КонтрагентПередИзменением);
		СтруктураДанных.Вставить("КонтрагентВестиРасчетыПоЗаказамПередИзменением", КонтрагентВестиРасчетыПоЗаказамПередИзменением);
		
		ОбработатьИзменениеДоговора(СтруктураДанных);
		
	Иначе
		
		Объект.Договор = Договор; // Восстанавливаем автоматически очищенный договор.
		Объект.ЗаказПоставщику = Заказ;
		
	КонецЕсли;
	
	Заказ = Объект.ЗаказПоставщику;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	
	ОбработатьИзменениеДоговора();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = ПолучитьПараметрыФормыВыбора(Объект.Ссылка, Объект.Организация, Объект.Контрагент, Объект.Договор);
	Если ПараметрыФормы.КонтролироватьВыборДоговора Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаВыбора", ПараметрыФормы, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОстатокВзаиморасчетовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСФормойДокументаКлиент.ОткрытьОтчетВзаиморасчетыСКонтрагентом(Объект.Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыТаможеннаяСтоимостьПриИзменении(Элемент)
	
	НомерРаздела = Объект.Разделы.Индекс(Элементы.Разделы.ТекущиеДанные) + 1;
	ПриИзмененииСтоимостиСтавкиПошлины(НомерРаздела);
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПроцентПошлиныПриИзменении(Элемент)
	
	ДанныеТекущейСтроки = Элементы.Разделы.ТекущиеДанные;
	Если ДанныеТекущейСтроки <> Неопределено Тогда
		
		НомерРаздела = Объект.Разделы.Индекс(ДанныеТекущейСтроки) + 1;
		ПриИзмененииСтоимостиСтавкиПошлины(НомерРаздела);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыСуммаПошлиныПриИзменении(Элемент)
	
	ПриИзмененииСуммыПошлиныСтавкиНДС(Элементы.Разделы.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыСтавкаНДСПриИзменении(Элемент)
	
	ПриИзмененииСуммыПошлиныСтавкиНДС(Элементы.Разделы.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыСуммаНДСПриИзменении(Элемент)
	
	ВычислитьСуммуДокумента();
	РаспределитьНДСИПошлинуПоРазделуСервер(Объект.Разделы.Индекс(Элементы.Разделы.ТекущиеДанные) + 1);
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ДанныеТекущейСтроки = Элементы.Разделы.ТекущиеДанные;
	Если НЕ ДанныеТекущейСтроки = Неопределено Тогда
		
		ИдентификаторСтроки = ДанныеТекущейСтроки.ПолучитьИдентификатор();
		ЗаполнитьСведенияОРазделе(ИдентификаторСтроки);
		
		Если Объект.Разделы.Количество() > 1 Тогда
			
			СписокВыбораЭлемента = Элементы.ЗапасыИзменениеСтрокДействие.СписокВыбора;
			Если СписокВыбораЭлемента.НайтиПоЗначению(ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьИсходноеМесто")) = Неопределено Тогда
				
				СписокВыбораЭлемента.Добавить(ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьИсходноеМесто"), НСтр("ru ='Перенести строки в другой раздел'"));
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока
		ИЛИ НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.СчетУчетаНДС) Тогда
		
		Элемент.ТекущиеДанные.СчетУчетаНДС = СчетУчетаНДСПоУмолчанию;
		
	КонецЕсли;
	
	Если Копирование Тогда
		
		Элемент.ТекущиеДанные.ТаможеннаяСтоимость = 0;
		Элемент.ТекущиеДанные.СуммаПошлины = 0;
		Элемент.ТекущиеДанные.СуммаНДС= 0;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПередУдалением(Элемент, Отказ)
	
	НомерРаздела = Объект.Разделы.Индекс(Элементы.Разделы.ТекущиеДанные) + 1;
	УдалитьТоварыРаздела(НомерРаздела);
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПослеУдаления(Элемент)
	
	Если Объект.Разделы.Количество() < 2 Тогда
		
		СписокВыбораЭлемента = Элементы.ЗапасыИзменениеСтрокДействие.СписокВыбора;
		
		ЭлементСписка = СписокВыбораЭлемента.НайтиПоЗначению(ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьИсходноеМесто"));
		Если ЭлементСписка <> Неопределено Тогда
			
			СписокВыбораЭлемента.Удалить(ЭлементСписка);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыФактурнаяСтоимостьПриИзменении(Элемент)
	
	ПересчитатьИтогиРаздела(Объект.Разделы.Индекс(Элементы.Разделы.ТекущиеДанные) + 1, "ТаможеннаяСтоимость");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыСуммаПошлиныПриИзменении(Элемент)
	
	ПересчитатьИтогиРаздела(Объект.Разделы.Индекс(Элементы.Разделы.ТекущиеДанные) + 1, "СуммаПошлины");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыСуммаНДСПриИзменении(Элемент)
	
	ПересчитатьИтогиРаздела(Объект.Разделы.Индекс(Элементы.Разделы.ТекущиеДанные) + 1, "СуммаНДС");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Элементы.Разделы.ТекущиеДанные = Неопределено Тогда
		
		ТекстОшибки = НСтр("ru ='Необходимо выделить раздел, для которого добавляются запасы'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "Объект.Разделы", , Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПослеУдаления(Элемент)
	
	ПересчитатьИтогиРаздела(Объект.Разделы.Индекс(Элементы.Разделы.ТекущиеДанные) + 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаможенныйСборПриИзменении(Элемент)
	
	ВычислитьСуммуДокумента();
	
	РаспределитьСуммуСбораПоЗапасам();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаможенныйШтрафПриИзменении(Элемент)
	
	СуммаЗаполнена = Объект.ТаможенныйШтраф <> 0;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НаправлениеДеятельности", "Видимость", СуммаЗаполнена);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СчетУчетаЗатрат", "Видимость", СуммаЗаполнена);
	
	Если СуммаЗаполнена Тогда
		
		Объект.НаправлениеДеятельности = Неопределено;
		Объект.СчетУчетаЗатрат = Неопределено;
		
	КонецЕсли;
	
	ВычислитьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПриИзменении(Элемент)
	
	РаспределитьСуммуСбораПоЗапасам();
	
	СтрокаРаздела = Элементы.Разделы.ТекущиеДанные;
	Если СтрокаРаздела <> Неопределено Тогда
		
		ЗаполнитьСведенияОРазделе(СтрокаРаздела.ПолучитьИдентификатор());
		
	КонецЕсли;
	
	ПроверитьЗаполнениеТНВЭД();
	
	ДанныеТекущейСтроки = Элементы.Запасы.ТекущиеДанные;
	Если ДанныеТекущейСтроки <> Неопределено Тогда
		
		Если НЕ ЗначениеЗаполнено(ДанныеТекущейСтроки.СтруктурнаяЕдиница) Тогда
			
			ДанныеТекущейСтроки.СтруктурнаяЕдиница = СкладПоУмолчанию;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#Область АвтоподборКонтактов

// @skip-warning
&НаКлиенте
Процедура Подключаемый_ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	АвтоподборКонтактовКлиент.Подключаемый_ОбработкаВыбора(ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_АвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	АвтоподборКонтактовКлиент.Подключаемый_АвтоПодбор(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, Параметры, Ожидание,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область РеквизитыКопированиеСтрок

&НаКлиенте
Процедура ЗапасыИзменениеСтрокЗначениеПриИзменении(Элемент)
	
	НастроитьОформлениеПанелиРедактирования(3);
	
КонецПроцедуры

#КонецОбласти

#Область РеквизитыГрупповоеИзменение

&НаКлиенте
Процедура ЗапасыПометкаПриИзменении(Элемент)
	
	ГрупповоеИзменениеСтрокКлиент.СтрокаПометкаПриИзменении(Объект.Запасы, Элементы.ЗапасыУстановитьФлажки, Элементы.ЗапасыСнятьФлажки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыИзменениеСтрокДействиеПриИзменении(Элемент)
	
	НастроитьОформлениеПанелиРедактирования(2);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗапасыВставитьСтроки(Команда)
	
	СтрокаРаздела = Элементы.Разделы.ТекущиеДанные;
	Если СтрокаРаздела = Неопределено Тогда
		
		ТекстОшибки = НСтр("ru ='Необходимо выделить раздел, для которого добавляются запасы'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "Объект.Разделы");
		Возврат;
		
	КонецЕсли;
	
	ВставитьСтроки(СтрокаРаздела, "Запасы");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыВыполнитьДействие(Команда)
	
	ОбработатьТаблицу();
	НастроитьОформлениеПанелиРедактирования(4);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыИзменитьСтроки(Команда)
	
	ПоказатьСкрытьПанельРедактирования(Истина);
	
	ЗапасыИзменениеСтрокДействие = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыКопироватьСтроки(Команда)
	
	КопироватьСтроки("Запасы");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыОтменитьИзменения(Команда)
	
	ПоказатьСкрытьПанельРедактирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыСнятьФлажки(Команда)
	
	УстановитьПометку(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыУстановитьФлажки(Команда)
	
	УстановитьПометку(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоНакладной(Команда)
	
	Если (Объект.Разделы.Количество() + Объект.Запасы.Количество()) > 0 Тогда
		
		ТекстВопроса = НСтр("ru ='Текущие данные табличных частей разделы и запасы будут очищены.
								|Продолжить?'");
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПоНакладнойПродолжение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Возврат;
		
	КонецЕсли;
	
	ВыбратьПриходнуюНакладную();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоНакладнойПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ВыбратьПриходнуюНакладную();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьДокументКакШаблон(Команда) Экспорт
	
	ЗаполнениеОбъектовУНФКлиент.СохранитьДокументКакШаблон(Объект, ОтображаемыеРеквизиты(), Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьЦеныИВалюту(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьИзмененияПоКнопкеЦеныИВалюты(Объект.ВалютаДокумента);
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНомерРазделаПоНомеруСтроки()
	
	Для Каждого СтрокаРазделов Из Объект.Разделы Цикл
		СтрокаРазделов.НомерРаздела = СтрокаРазделов.НомерСтроки;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНомерРазделаПоНомеруСтрокиКлиент()
	
	Для Каждого СтрокаРазделов Из Объект.Разделы Цикл
		СтрокаРазделов.НомерРаздела = СтрокаРазделов.НомерСтроки;
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Функция РаспределитьПропорционально(Знач ИсхСумма, СоответствиеКоэфф, Знач Точность = 2)
	
	Результат = Новый Соответствие;
	
	Если СоответствиеКоэфф.Количество() = 0
		Или ИсхСумма = 0
		Или ИсхСумма = Null Тогда
		
		Возврат Результат;
		
	КонецЕсли;
	
	ИндексМакс = 0;
	МаксЗнач = 0;
	РаспрСумма = 0;
	СуммаКоэфф = 0;
	
	Для Каждого ЭлементСоответствия Из СоответствиеКоэфф Цикл
		
		МодульЧисла = ?(ЭлементСоответствия.Значение > 0, ЭлементСоответствия.Значение, - ЭлементСоответствия.Значение);
		
		Если МаксЗнач < МодульЧисла Тогда
			
			МаксЗнач = МодульЧисла;
			ИндексМакс = ЭлементСоответствия.Ключ;
			
		КонецЕсли;
		
		СуммаКоэфф = СуммаКоэфф + ЭлементСоответствия.Значение;
		
	КонецЦикла;
	
	Если СуммаКоэфф = 0 Тогда
		
		Возврат Результат;
		
	КонецЕсли;
	
	Для Каждого ЭлементСоответствия Из СоответствиеКоэфф Цикл
		
		НоваяСумма = Окр(ИсхСумма * ЭлементСоответствия.Значение / СуммаКоэфф, Точность, 1);
		Результат.Вставить(ЭлементСоответствия.Ключ, НоваяСумма);
		РаспрСумма = РаспрСумма + НоваяСумма;
		
	КонецЦикла;
	
	// Погрешности округления отнесем на коэффициент с максимальным весом
	Если Не РаспрСумма = ИсхСумма Тогда
		
		ЗначениеЭлемента = Результат.Получить(ИндексМакс);
		ЗначениеЭлемента = ЗначениеЭлемента + (ИсхСумма - РаспрСумма);
		
		Результат.Вставить(ИндексМакс, ЗначениеЭлемента);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура РаспределитьСуммуТаможенногоСбораПоКолонкамТаблицыЗапасов(РаспределяемаяСумма, ТаблицаЗапасов, ИмяКолонки, ИмяКолонкиБазы)
	
	Если РаспределяемаяСумма <> 0 Тогда
		
		СоответствиеСтарыхСумм = Новый Соответствие;
		Для каждого СтрокаЗапасов Из ТаблицаЗапасов Цикл
			
			СоответствиеСтарыхСумм.Вставить(СтрокаЗапасов.ПолучитьИдентификатор(), СтрокаЗапасов[?(ПустаяСтрока(ИмяКолонкиБазы), ИмяКолонки, ИмяКолонкиБазы)]);
			
		КонецЦикла;
		
		СоответствиеНовыхСумм = РаспределитьПропорционально(РаспределяемаяСумма, СоответствиеСтарыхСумм);
		
		Для каждого ЭлементСоответствия Из СоответствиеНовыхСумм Цикл
			
			СтрокаТаблицы = ТаблицаЗапасов.НайтиПоИдентификатору(ЭлементСоответствия.Ключ);
			СтрокаТаблицы[ИмяКолонки] = ЭлементСоответствия.Значение;
			
		КонецЦикла;
		
	Иначе
		
		Для каждого СтрокаЗапасов Из ТаблицаЗапасов Цикл
			
			СтрокаЗапасов[ИмяКолонки] = 0;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеДоговора(ДанныеДоговора = Неопределено)
	
	ДоговорПередИзменением = Договор;
	Договор = Объект.Договор;
	
	Если ДоговорПередИзменением <> Объект.Договор Тогда
		
		ПараметрыДокумента = Новый Структура;
		Если ДанныеДоговора = Неопределено Тогда
			
			ДанныеДоговора = ПолучитьДанныеДоговорПриИзменении(Объект.Дата, Объект.ВалютаДокумента, Объект.Договор);
			
		Иначе
			
			ПараметрыДокумента.Вставить("КонтрагентПередИзменением", ДанныеДоговора.КонтрагентПередИзменением);
			ПараметрыДокумента.Вставить("КонтрагентВестиРасчетыПоЗаказамПередИзменением", ДанныеДоговора.КонтрагентВестиРасчетыПоЗаказамПередИзменением);
			ПараметрыДокумента.Вставить("ВидимостьДоговораПередИзменением", Элементы.Договор.Видимость);
			
		КонецЕсли;
		
		ВалютаРасчетовПередИзменением = ВалютаРасчетов;
		ВалютаРасчетов = ДанныеДоговора.ВалютаРасчетов;
		
		НовыйДоговорИВалютаРасчетов = ЗначениеЗаполнено(Объект.Договор) И ЗначениеЗаполнено(ВалютаРасчетов) 
			И Объект.Договор <> ДоговорПередИзменением И ВалютаРасчетовПередИзменением <> ДанныеДоговора.ВалютаРасчетов;
		
		ОткрытьФормуЦеныИВалюты = НовыйДоговорИВалютаРасчетов И Объект.ВалютаДокумента <> ДанныеДоговора.ВалютаРасчетов
			И (Объект.Запасы.Количество() > 0 ИЛИ Объект.Разделы.Количество() > 0);
		
		ПараметрыДокумента.Вставить("ДоговорПередИзменением", ДоговорПередИзменением);
		ПараметрыДокумента.Вставить("ВалютаРасчетовПередИзменением", ВалютаРасчетовПередИзменением);
		ПараметрыДокумента.Вставить("ДанныеДоговора", ДанныеДоговора);
		ПараметрыДокумента.Вставить("ОткрытьФормуЦеныИВалюты", ОткрытьФормуЦеныИВалюты);
		
		ОбработатьИзменениеДоговораИВалютыРасчетов(ПараметрыДокумента);
		
	Иначе
		
		Объект.ЗаказПоставщику = Заказ;
		
	КонецЕсли;
	
	Заказ = Объект.ЗаказПоставщику;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуНДС(СтрокаТабличнойЧасти)
	
	Если Объект.ВалютаДокумента = НациональнаяВалюта Тогда
		
		СуммаПошлины = СтрокаТабличнойЧасти.СуммаПошлины;
		
	Иначе
		
		ПараметрыТекущегоКурса = Новый Структура;
		ПараметрыТекущегоКурса.Вставить("Валюта",	Объект.ВалютаДокумента);
		ПараметрыТекущегоКурса.Вставить("Курс",		Объект.Курс);
		ПараметрыТекущегоКурса.Вставить("Кратность",Объект.Кратность);
		
		ПараметрыНовогоКурса = Новый Структура;
		ПараметрыНовогоКурса.Вставить("Валюта",		НациональнаяВалюта);
		ПараметрыНовогоКурса.Вставить("Курс",		1);
		ПараметрыНовогоКурса.Вставить("Кратность",	1);
		
		СуммаПошлины = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(СтрокаТабличнойЧасти.СуммаПошлины, ПараметрыТекущегоКурса, ПараметрыНовогоКурса);
		
	КонецЕсли;
	
	СтавкаНДС 		= УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
	БазаРасчетаНДС	= СтрокаТабличнойЧасти.ТаможеннаяСтоимость + СуммаПошлины;
	
	СтрокаТабличнойЧасти.СуммаНДС = БазаРасчетаНДС * СтавкаНДС / 100;
	
	ВычислитьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьКурсКратностьВалютыРасчетов(СтруктураДанные)
	
	КурсНовый = ?(СтруктураДанные.ВалютаКурсКратность.Курс = 0, 1, СтруктураДанные.ВалютаКурсКратность.Курс);
	КратностьНовый = ?(СтруктураДанные.ВалютаКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаКурсКратность.Кратность);
	
	Если Объект.Курс <> КурсНовый
		ИЛИ Объект.Кратность <> КратностьНовый Тогда
		
		КурсВалютыСтрокой = Строка(Объект.Кратность) + " " + СокрЛП(ВалютаРасчетов) + " = " + Строка(Объект.Курс) + " " + СокрЛП(НациональнаяВалюта);
		КурсНовыйВалютыСтрокой = Строка(КратностьНовый) + " " + СокрЛП(ВалютаРасчетов) + " = " + Строка(КурсНовый) + " " + СокрЛП(НациональнаяВалюта);
		
		ТекстСообщения = НСтр("ru = 'На дату документа у валюты расчетов (" + КурсВалютыСтрокой + ") был задан курс.
									|Установить курс расчетов (" + КурсНовыйВалютыСтрокой + ") в соответствии с курсом валюты?'");
		
		Режим = РежимДиалогаВопрос.ДаНет;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ПересчитатьКурсКратностьВалютыРасчетовЗавершение", ЭтотОбъект, Новый Структура("КратностьНовый, КурсНовый", КратностьНовый, КурсНовый)), ТекстСообщения, Режим, 0);
		Возврат;
		
	КонецЕсли;
	
	// Сформируем надпись цены и валюты.
	ПересчитатьКурсКратностьВалютыРасчетовФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьКурсКратностьВалютыРасчетовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КратностьНовый = ДополнительныеПараметры.КратностьНовый;
	КурсНовый = ДополнительныеПараметры.КурсНовый;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Объект.Курс = КурсНовый;
		Объект.Кратность = КратностьНовый;
		
	КонецЕсли;
	
	ПересчитатьКурсКратностьВалютыРасчетовФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьКурсКратностьВалютыРасчетовФрагмент()
	
	Перем СтруктураНадписи;
	
	СтруктураНадписи = Новый Структура("ВалютаДокумента, ВалютаРасчетов, Курс, КурсНациональнаяВалюта, СуммаВключаетНДС, УчетВалютныхОпераций, НалогообложениеНДС", Объект.ВалютаДокумента, ВалютаРасчетов, Объект.Курс, КурсНациональнаяВалюта, Объект.СуммаВключаетНДС, УчетВалютныхОпераций, Объект.НалогообложениеНДС);
	ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалюты(Знач ВалютаРасчетовПередИзменением, ПересчитатьЦены = Ложь, ТекстПредупреждения = "")
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ТолькоПросмотр", 		ЭтотОбъект.ТолькоПросмотр);
	СтруктураПараметров.Вставить("ВалютаДокумента",		Объект.ВалютаДокумента);
	СтруктураПараметров.Вставить("Курс",				Объект.Курс);
	СтруктураПараметров.Вставить("Кратность",			Объект.Кратность);
	СтруктураПараметров.Вставить("НалогообложениеНДС",	Объект.НалогообложениеНДС);
	СтруктураПараметров.Вставить("СуммаВключаетНДС",	Объект.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("НДСВключатьВСтоимость",Объект.НДСВключатьВСтоимость);
	СтруктураПараметров.Вставить("Контрагент",			Объект.Контрагент);
	СтруктураПараметров.Вставить("Договор",				Объект.Договор);
	СтруктураПараметров.Вставить("Организация",			Компания);
	СтруктураПараметров.Вставить("ДатаДокумента",		Объект.Дата);
	СтруктураПараметров.Вставить("ПерезаполнитьЦены",	Ложь);
	СтруктураПараметров.Вставить("ПересчитатьЦены",		ПересчитатьЦены);
	СтруктураПараметров.Вставить("БылиВнесеныИзменения",Ложь);
	СтруктураПараметров.Вставить("ТекстПредупреждения",	ТекстПредупреждения);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуЦеныИВалютаЗавершение", ЭтотОбъект, Новый Структура("ВалютаРасчетовПередИзменением", ВалютаРасчетовПередИзменением));
	ОткрытьФорму("ОбщаяФорма.ФормаЦеныИВалюта", СтруктураПараметров, ЭтаФорма, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеДоговораИВалютыРасчетов(ПараметрыДокумента)
	
	ВалютаРасчетовПередИзменением = ПараметрыДокумента.ВалютаРасчетовПередИзменением;
	ДанныеДоговора = ПараметрыДокумента.ДанныеДоговора;
	ОткрытьФормуЦеныИВалюты = ПараметрыДокумента.ОткрытьФормуЦеныИВалюты;
	
	Если НЕ ДанныеДоговора.СуммаВключаетНДС = Неопределено Тогда
		
		Объект.СуммаВключаетНДС = ДанныеДоговора.СуммаВключаетНДС;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Договор) Тогда 
		
		Объект.Курс	  = ?(ДанныеДоговора.ВалютаРасчетовКурсКратность.Курс = 0, 1, ДанныеДоговора.ВалютаРасчетовКурсКратность.Курс);
		Объект.Кратность = ?(ДанныеДоговора.ВалютаРасчетовКурсКратность.Кратность = 0, 1, ДанныеДоговора.ВалютаРасчетовКурсКратность.Кратность);
		
	КонецЕсли;
	
	Объект.ВалютаДокумента = ДанныеДоговора.ВалютаРасчетов;
	Заказ = Объект.ЗаказПоставщику;
	
	Если ОткрытьФормуЦеныИВалюты Тогда
		
		ТекстПредупреждение = НСтр("ru = 'Изменилась валюта расчетов по договору с контрагентом!
										|Необходимо проверить валюту документа!'");
										
		ОбработатьИзмененияПоКнопкеЦеныИВалюты(ВалютаРасчетовПередИзменением, Истина, ТекстПредупреждение);
		
	Иначе
		
		СтруктураНадписи = Новый Структура("ВалютаДокумента, ВалютаРасчетов, Курс, КурсНациональнаяВалюта, СуммаВключаетНДС, УчетВалютныхОпераций, НалогообложениеНДС", 
			Объект.ВалютаДокумента, 
			ВалютаРасчетов, 
			Объект.Курс, 
			КурсНациональнаяВалюта, 
			Объект.СуммаВключаетНДС, 
			УчетВалютныхОпераций, 
			Объект.НалогообложениеНДС);
			
		ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
		
	КонецЕсли;
	
	Если ПараметрыДокумента.Свойство("ИзменитьПеременнуюВалютаРасчетов") Тогда
		
		ВалютаРасчетов = ДанныеДоговора.ВалютаРасчетов;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЦеныИВалютаЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") 
		И РезультатЗакрытия.БылиВнесеныИзменения Тогда
		
		Объект.ВалютаДокумента = РезультатЗакрытия.ВалютаДокумента;
		Объект.Курс = РезультатЗакрытия.КурсРасчетов;
		Объект.Кратность = РезультатЗакрытия.КратностьРасчетов;
		Объект.НалогообложениеНДС = РезультатЗакрытия.НалогообложениеНДС;
		Объект.СуммаВключаетНДС = РезультатЗакрытия.СуммаВключаетНДС;
		Объект.НДСВключатьВСтоимость = РезультатЗакрытия.НДСВключатьВСтоимость;
		ВалютаРасчетовПередИзменением = ДополнительныеПараметры.ВалютаРасчетовПередИзменением;
		
		// Пересчитываем цены по валюте.
		Если РезультатЗакрытия.ПересчитатьЦены Тогда
			
			ЦенообразованиеКлиент.ПересчитатьЦеныТабличнойЧастиПоВалюте(ЭтаФорма, ВалютаРасчетовПередИзменением, "Запасы", РезультатЗакрытия.КурсПересчетаЦен);
			ЦенообразованиеКлиент.ПересчитатьЦеныТабличнойЧастиПоВалюте(ЭтаФорма, ВалютаРасчетовПередИзменением, "Разделы", РезультатЗакрытия.КурсПересчетаЦен);
			
		КонецЕсли;
		
		// Пересчитываем сумму если изменился признак "Сумма включает НДС".
		Если РезультатЗакрытия.СуммаВключаетНДС <> РезультатЗакрытия.ПредСуммаВключаетНДС Тогда
			
			ЦенообразованиеКлиент.ПересчитаемСуммуТабличнойЧастиПоФлагуСуммаВключаетНДС(ЭтаФорма, "Разделы");
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураНадписи = Новый Структура("ВалютаДокумента, ВалютаРасчетов, Курс, КурсНациональнаяВалюта, СуммаВключаетНДС, УчетВалютныхОпераций, НалогообложениеНДС",
		Объект.ВалютаДокумента,
		ВалютаРасчетов,
		Объект.Курс,
		КурсНациональнаяВалюта,
		Объект.СуммаВключаетНДС,
		УчетВалютныхОпераций,
		Объект.НалогообложениеНДС);
		
	ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
	
КонецПроцедуры

&НаКлиенте
Функция РассчитатьСуммуПошлиныГТД(Знач ДанныеСтроки, Знач ДанныеОбъекта, Знач ВалютаРеглУчета)

	Если НЕ ЗначениеЗаполнено(ДанныеОбъекта.ВалютаДокумента) Тогда
		
		ТаможеннаяСтоимостьВал = ДанныеСтроки.ТаможеннаяСтоимость;
		
	Иначе
		
		ПараметрыТекущегоКурса = Новый Структура;
		ПараметрыТекущегоКурса.Вставить("Валюта",	ДанныеОбъекта.ВалютаДокумента);
		ПараметрыТекущегоКурса.Вставить("Курс",		ДанныеОбъекта.КурсДокумента);
		ПараметрыТекущегоКурса.Вставить("Кратность",ДанныеОбъекта.КратностьДокумента);
		
		ПараметрыНовогоКурса = Новый Структура;
		ПараметрыНовогоКурса.Вставить("Валюта",		ДанныеОбъекта.ВалютаРегламентированногоУчета);
		ПараметрыНовогоКурса.Вставить("Курс",		1);
		ПараметрыНовогоКурса.Вставить("Кратность",	1);
		
		ТаможеннаяСтоимостьВал = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(ДанныеСтроки.ТаможеннаяСтоимость, ПараметрыТекущегоКурса, ПараметрыНовогоКурса);
		
	КонецЕсли;

	Возврат ТаможеннаяСтоимостьВал * ДанныеСтроки.СтавкаПошлины / 100;

КонецФункции

&НаКлиенте
Процедура ПриИзмененииСтоимостиСтавкиПошлины(НомерРаздела)
	
	СтрокаРаздела = Объект.Разделы.Получить(НомерРаздела - 1);
	
	ДанныеОбъекта = Новый Структура("ВалютаДокумента,КурсВзаиморасчетов,КратностьВзаиморасчетов");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	ДанныеОбъекта.Вставить("Ссылка", Объект.Ссылка);
	ДанныеОбъекта.Вставить("ВалютаРасчетов", ВалютаРасчетов);
	ДанныеОбъекта.Вставить("ВалютаРегламентированногоУчета", НациональнаяВалюта);
	ДанныеОбъекта.Вставить("КурсДокумента", Объект.Курс);
	ДанныеОбъекта.Вставить("КратностьДокумента", Объект.Кратность);
	
	ДанныеСтроки = Новый Структура("ТаможеннаяСтоимость, СтавкаПошлины, СуммаПошлины, СтавкаНДС, СуммаНДС, НомерСтроки");
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, СтрокаРаздела);
	
	СтрокаРаздела.СуммаПошлины = РассчитатьСуммуПошлиныГТД(ДанныеСтроки, ДанныеОбъекта, НациональнаяВалюта);
	ДанныеСтроки.СуммаПошлины  = СтрокаРаздела.СуммаПошлины;
	РассчитатьСуммуНДС(СтрокаРаздела);
	
	ВычислитьСуммуДокумента();
	
	РаспределитьНДСИПошлинуПоРазделуСервер(НомерРаздела);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСуммыПошлиныСтавкиНДС(СтрокаРаздела)
	
	РассчитатьСуммуНДС(СтрокаРаздела);
	РаспределитьНДСИПошлинуПоРазделуСервер(Объект.Разделы.Индекс(СтрокаРаздела) + 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСведенияОРазделе(ИдентификаторСтроки)
	
	Если ИдентификаторСтроки <> Неопределено Тогда
		
		ДанныеСтрокиРаздела = Объект.Разделы.НайтиПоИдентификатору(ИдентификаторСтроки);
		
		КодРаздела = ?(ПустаяСтрока(СокрЛП(ДанныеСтрокиРаздела.КодТНВЭД)), "0000000000", СокрЛП(ДанныеСтрокиРаздела.КодТНВЭД));
		
		НомерРаздела = ДанныеСтрокиРаздела.НомерСтроки;
		ТоварыВРазделе = Объект.Запасы.НайтиСтроки(Новый Структура("НомерРаздела", НомерРаздела));
		КоличествоТоваровВРазделе = ?(ТипЗнч(ТоварыВРазделе) = Тип("Массив"), ТоварыВРазделе.Количество(), 0);
		
		СтрокаСведений = НСтр("ru ='Код товаров: %1, строк в разделе: %2'");
		ДанныеСтрокиРаздела.СведенияОРазделеГТД = СтрШаблон(СтрокаСведений, КодРаздела, КоличествоТоваровВРазделе);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьИтогиРаздела(НомерРаздела, ИмяКолонки = "")
	
	Если НомерРаздела = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ДанныеРаздела = Объект.Разделы.Получить(НомерРаздела - 1);
	СтараяТаможеннаяСтоимость = ДанныеРаздела.ТаможеннаяСтоимость;
	
	Итоги = Новый Структура("СуммаПошлины,СуммаНДС,ТаможеннаяСтоимость", 0, 0, 0);
	
	ТоварыРаздела = Объект.Запасы.НайтиСтроки(Новый Структура("НомерРаздела", НомерРаздела));
	Для Каждого СтрокаТаблицы Из ТоварыРаздела Цикл
		
		Итоги.СуммаПошлины			= Итоги.СуммаПошлины + СтрокаТаблицы.СуммаПошлины;
		Итоги.СуммаНДС				= Итоги.СуммаНДС + СтрокаТаблицы.СуммаНДС;
		Итоги.ТаможеннаяСтоимость	= Итоги.ТаможеннаяСтоимость + СтрокаТаблицы.ФактурнаяСтоимость;
		
	КонецЦикла;
	
	Если ИмяКолонки = "" Тогда
		
		ЗаполнитьЗначенияСвойств(ДанныеРаздела, Итоги);
		
	Иначе
		
		ДанныеРаздела[ИмяКолонки] = Итоги[ИмяКолонки];
		
	КонецЕсли; 
	
	Если СтараяТаможеннаяСтоимость <> ДанныеРаздела.ТаможеннаяСтоимость Тогда
		
		ПриИзмененииСтоимостиСтавкиПошлины(НомерРаздела);
		
	КонецЕсли;
	
	ВычислитьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ВычислитьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Разделы.Итог("СуммаНДС") + Объект.Разделы.Итог("СуммаПошлины") + Объект.ТаможенныйСбор + Объект.ТаможенныйШтраф;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПриходнуюНакладную()
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("МножественныйВыбор", Ложь);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораПриходнойНакладной", ЭтотОбъект);
	
	ОткрытьФорму("Документ.ПриходнаяНакладная.ФормаВыбора", ПараметрыОткрытия, ЭтаФорма, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПриходнойНакладной(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат)
		И ТипЗнч(Результат) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
		
		ЗаполнитьПоНакладнойНаСевере(Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьСуммуСбораПоЗапасам()
	
	Если Объект.Запасы.Количество() < 1 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Элементы.ЗапасыСуммаСбора.Доступность Тогда // Если включено уточнение
		
		Возврат;
		
	КонецЕсли;
	
	РаспределитьСуммуТаможенногоСбораПоКолонкамТаблицыЗапасов(Объект.ТаможенныйСбор, Объект.Запасы, "СуммаСбора", "ФактурнаяСтоимость");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьНадписьЦеныИВалюта(СтруктураНадписи)
	
	ТекстНадписи = "";
	
	// Валюта.
	Если СтруктураНадписи.УчетВалютныхОпераций Тогда
		Если ЗначениеЗаполнено(СтруктураНадписи.ВалютаДокумента) Тогда
			ТекстНадписи = НСтр("ru = '%Валюта%'");
			Если СтруктураНадписи.ВалютаДокумента <> УправлениеНебольшойФирмойПовтИсп.ПолучитьНациональнуюВалюту() Тогда
				ТекстНадписи = ТекстНадписи + НСтр("ru = ' %Курс%'");
				ТекстНадписи = СтрЗаменить(ТекстНадписи, "%Валюта%", СокрЛП(УправлениеНебольшойФирмойПовтИсп.ПолучитьСимвольноеПредставлениеВалюты(СтруктураНадписи.ВалютаДокумента)));
				ТекстНадписи = СтрЗаменить(ТекстНадписи, "%Курс%", СокрЛП(Строка(СтруктураНадписи.Курс)));
			Иначе
				ТекстНадписи = СтрЗаменить(ТекстНадписи, "%Валюта%", СокрЛП(Строка(СтруктураНадписи.ВалютаДокумента)));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Налогообложение НДС.
	Если ЗначениеЗаполнено(СтруктураНадписи.НалогообложениеНДС) Тогда
		
		Если ПустаяСтрока(ТекстНадписи) Тогда
			
			ТекстНадписи = ТекстНадписи + НСтр("ru = '%НалогообложениеНДС%'");
			
		Иначе
			
			ТекстНадписи = ТекстНадписи + НСтр("ru = ' • %НалогообложениеНДС%'");
			
		КонецЕсли;
		
		ТекстНадписи = СтрЗаменить(ТекстНадписи, "%НалогообложениеНДС%", РаботаСФормойДокументаКлиентСервер.КраткоеПредставлениеТипаНалогообложенияНДС(СтруктураНадписи.НалогообложениеНДС));
		
	КонецЕсли;
	
	// Флаг сумма включает НДС.
	ТекстНадписи = ТекстНадписи 
		+ ?(ПустаяСтрока(ТекстНадписи), "", " • ")
		+ ?(СтруктураНадписи.СуммаВключаетНДС, НСтр("ru = 'Сумма включает НДС'"), НСтр("ru = 'Сумма не включает НДС'"));
	
	Возврат ТекстНадписи;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеДатаПриИзменении(ДокументСсылка, ДатаНовая, ДатаПередИзменением, ВалютаРасчетов)
	
	РазностьДат = ДокументыУНФ.ПроверитьНомерДокумента(ДокументСсылка, ДатаНовая, ДатаПередИзменением);
	ВалютаКурсКратность = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ДатаНовая, Новый Структура("Валюта", ВалютаРасчетов));
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"РазностьДат",
		РазностьДат
	);
	СтруктураДанные.Вставить(
		"ВалютаКурсКратность",
		ВалютаКурсКратность
	);
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеОрганизацияПриИзменении(Организация)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить("Компания", Константы.УчетПоКомпании.Компания(Организация));
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные)
	
	СтруктураДанные.Вставить("СтранаПроисхождения", СтруктураДанные.Номенклатура.СтранаПроисхождения);
	СтруктураДанные.Вставить("СтавкаНДС", Справочники.СтавкиНДС.СтавкаНДС(СтруктураДанные.Номенклатура.ВидСтавкиНДС, ?(ЗначениеЗаполнено(СтруктураДанные.ДатаОбработки), СтруктураДанные.ДатаОбработки, ТекущаяДатаСеанса())));
	
	// Характеристики
	СтруктураДанные.Вставить("ИспользоватьХарактеристики",Ложь);
	СтруктураДанные.Вставить("ПроверятьЗаполнениеХарактеристики",Ложь);
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура) И СтруктураДанные.Номенклатура.ИспользоватьХарактеристики
		Тогда
		ЗначенияПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура);
		
		Если Не ЗначенияПоУмолчанию = Неопределено
			Тогда
			ХарактеристикаПоУмолчанию = ЗначенияПоУмолчанию;
		КонецЕсли;
		
		Если НЕ СтруктураДанные.Свойство("Характеристика") Тогда
			СтруктураДанные.Вставить("Характеристика",ХарактеристикаПоУмолчанию);
		Иначе
			СтруктураДанные.Характеристика = ?(ЗначениеЗаполнено(СтруктураДанные.Характеристика), СтруктураДанные.Характеристика, ХарактеристикаПоУмолчанию);
		КонецЕсли;
		
		СтруктураДанные.Вставить("ИспользоватьХарактеристики",Истина);
		СтруктураДанные.Вставить("ПроверятьЗаполнениеХарактеристики",СтруктураДанные.Номенклатура.ПроверятьЗаполнениеХарактеристики);
	КонецЕсли; 
	// Конец Характеристики
	
	//Партии
	СтруктураДанные.Вставить("ИспользоватьПартии",Ложь);
	СтруктураДанные.Вставить("ПроверятьЗаполнениеПартий",Ложь);
	
	Если НЕ СтруктураДанные.Свойство("Партия") Тогда
		СтруктураДанные.Вставить("Партия", Неопределено);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура) И СтруктураДанные.Номенклатура.ИспользоватьПартии
		Тогда
		
		Если СтруктураДанные.Свойство("СтатусПартии")
			Тогда
			ЗначенияПартииПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияПартийНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура,СтруктураДанные.СтатусПартии);
		Иначе
			Если Не СтруктураДанные.Свойство("ВидОперации")
				Тогда
				ВидОперации = Неопределено
			Иначе
				ВидОперации = СтруктураДанные.ВидОперации
			КонецЕсли;
			
			СтатусПартии = НоменклатураВДокументахСервер.СоответствиеВидаОперацииИлиХозОперацииСтатусуПартии(, ВидОперации);
			ЗначенияПартииПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияПартийНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура,СтатусПартии);
		КонецЕсли;
		
		ПартияПоУмолчанию = Справочники.ПартииНоменклатуры.ПустаяСсылка();
		
		Если Не ЗначенияПартииПоУмолчанию = Неопределено
			Тогда
			ПартияПоУмолчанию = ЗначенияПартииПоУмолчанию;
		КонецЕсли;
		
		СтруктураДанные.Партия = ?(ЗначениеЗаполнено(СтруктураДанные.Партия), СтруктураДанные.Партия, ПартияПоУмолчанию);
		
		СтруктураДанные.ПроверятьЗаполнениеПартий = СтруктураДанные.Номенклатура.ПроверятьЗаполнениеПартий;
		СтруктураДанные.ИспользоватьПартии = Истина;
		
	КонецЕсли;
	// Конец Партии
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеДоговорПриИзменении(Дата, ВалютаДокумента, Договор)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"ВалютаРасчетов",
		Договор.ВалютаРасчетов
	);
	
	СтруктураДанные.Вставить(
		"ВалютаРасчетовКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Договор.ВалютаРасчетов))
	);
	
	СтруктураДанные.Вставить(
		"РасчетыВУсловныхЕдиницах",
		Договор.РасчетыВУсловныхЕдиницах
	);
	
	СтруктураДанные.Вставить(
		"СуммаВключаетНДС",
		?(ЗначениеЗаполнено(Договор.ВидЦен), Договор.ВидЦен.ЦенаВключаетНДС, Неопределено)
	);
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПараметрыФормыВыбора(Документ, Организация, Контрагент, Договор)
	
	СписокВидовДоговоров = Справочники.ДоговорыКонтрагентов.ПолучитьСписокВидовДоговораДляДокумента(Документ);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КонтролироватьВыборДоговора", Контрагент.ВестиРасчетыПоДоговорам);
	ПараметрыФормы.Вставить("Контрагент", Контрагент);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("ВидыДоговоров", СписокВидовДоговоров);
	ПараметрыФормы.Вставить("ТекущаяСтрока", Договор);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация)
	
	Если Не Контрагент.ВестиРасчетыПоДоговорам Тогда
		Возврат Справочники.ДоговорыКонтрагентов.ДоговорПоУмолчанию(Контрагент);
	КонецЕсли;
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, СписокВидовДоговоров);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

&НаСервере
Процедура УсловноеОформлениеТабличныхЧастей()
	
	ОшибкаПроверкиТНВЭД = УсловноеОформление.Элементы.Добавить();
	ОшибкаПроверкиТНВЭД.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(255,200,200));
	
	ЭлементОтбора = ОшибкаПроверкиТНВЭД.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Запасы.ОшибкаЗаполненияТНВЭД");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Истина;
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ОшибкаПроверкиТНВЭД.Поля, "ЗапасыНоменклатураТоварнаяНоменклатураВЭД");
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗаполнениеТНВЭД()
	
	ПараметрыОтбора = Новый Структура("НомерРаздела");
	
	Для каждого СтрокаТаблицыРазделы Из Объект.Разделы Цикл
		
		ТНВЭД = Неопределено;
		
		ПараметрыОтбора.НомерРаздела = СтрокаТаблицыРазделы.НомерСтроки;
		НайденныеСтроки = Объект.Запасы.НайтиСтроки(ПараметрыОтбора);
		
		Для каждого СтрокаТаблицыЗапасов Из НайденныеСтроки Цикл
			
			Если ЗначениеЗаполнено(СтрокаТаблицыЗапасов.Номенклатура.ТоварнаяНоменклатураВЭД) Тогда
				
				Если ТНВЭД = Неопределено Тогда
					
					ТНВЭД = СтрокаТаблицыЗапасов.Номенклатура.ТоварнаяНоменклатураВЭД;
					Продолжить;
					
				КонецЕсли;
				
				СтрокаТаблицыЗапасов.ОшибкаЗаполненияТНВЭД = (ТНВЭД <> СтрокаТаблицыЗапасов.Номенклатура.ТоварнаяНоменклатураВЭД);
				
			Иначе
				
				СтрокаТаблицыЗапасов.ОшибкаЗаполненияТНВЭД = Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОтображаемыеРеквизиты()
	
	Возврат ЗаполнениеОбъектовУНФ.ОтображаемыеРеквизиты(ЭтаФорма);
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеКонтрагентПриИзменении(Дата, ВалютаДокумента, Контрагент, Организация)
	
	ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Контрагент, Организация);
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"Договор",
		ДоговорПоУмолчанию
	);
		
	СтруктураДанные.Вставить(
		"ВалютаРасчетов",
		ДоговорПоУмолчанию.ВалютаРасчетов
	);
	
	СтруктураДанные.Вставить(
		"ВалютаРасчетовКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", ДоговорПоУмолчанию.ВалютаРасчетов))
	);
	
	СтруктураДанные.Вставить(
		"РасчетыВУсловныхЕдиницах",
		ДоговорПоУмолчанию.РасчетыВУсловныхЕдиницах
	);
	
	СтруктураДанные.Вставить(
		"СуммаВключаетНДС",
		?(ЗначениеЗаполнено(ДоговорПоУмолчанию.ВидЦен), Договор.ВидЦен.ЦенаВключаетНДС, Неопределено)
	);
	
	УстановитьВидимостьДоговора();
	
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьДоговора()
	
	КонтрагентВестиРасчетыПоЗаказам = Объект.Контрагент.ВестиРасчетыПоЗаказам;
	Элементы.Договор.Видимость = Объект.Контрагент.ВестиРасчетыПоДоговорам;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьИОстаткиВзаиморасчетов()
	
	Элементы.ОстатокВзаиморасчетов.Видимость = НЕ ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.Контрагент);
	Элементы.Контрагент.АвтоМаксимальнаяШирина = НЕ Элементы.ОстатокВзаиморасчетов.Видимость;
	
	Если НЕ Элементы.ОстатокВзаиморасчетов.Видимость Тогда
		// Только для новых объектов
		Возврат;
	КонецЕсли;
	
	Элементы.ОстатокВзаиморасчетов.Заголовок = РаботаСКонтрагентамиУНФ.ЗаголовокНадписиВзаиморасчетов(Объект.Контрагент);
	
КонецПроцедуры

&НаСервере
Процедура РаспределитьНДСИПошлинуПоРазделуСервер(НомерРаздела)
	
	СтрокаРаздела 		= Объект.Разделы.Получить(НомерРаздела - 1);
	СуммаПошлины		= СтрокаРаздела.СуммаПошлины;
	СуммаНДС			= СтрокаРаздела.СуммаНДС;
	
	МассивСтрок  = Объект.Запасы.НайтиСтроки(Новый Структура("НомерРаздела", НомерРаздела));
	БазисРаспределения = Новый Массив;
	
	ВсегоСтоимость = 0;
	Для Каждого ЭлементМассива Из МассивСтрок Цикл
		
		ВсегоСтоимость = ВсегоСтоимость + ЭлементМассива.ФактурнаяСтоимость;
		БазисРаспределения.Добавить(ЭлементМассива.ФактурнаяСтоимость);
		
	КонецЦикла;
	
	Если ВсегоСтоимость = 0 Тогда
		
		Если МассивСтрок.Количество() > 0 Тогда
			
			ТекстСообщения = НСтр("ru='Общая сумма фактурной стоимости раздела %1 нулевая!
									|Распределение невозможно.'");
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, НомерРаздела);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Товары", "Объект");
			
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Если НЕ СуммаПошлины = 0 Тогда
		
		МассивРезультатаРаспределения_Пошлина = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СуммаПошлины,
			БазисРаспределения);
		
	КонецЕсли;
	
	Если НЕ СуммаНДС = 0 Тогда
		
		МассивРезультатаРаспределения_НДС = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СуммаНДС,
			БазисРаспределения);
		
	КонецЕсли;
	
	Для ИндексСтроки = 0 По МассивСтрок.Количество() - 1 Цикл
		
		СтрокаМассива				= МассивСтрок[ИндексСтроки];
		СтрокаМассива.СуммаПошлины	= ?(СуммаПошлины = 0,	0, МассивРезультатаРаспределения_Пошлина[ИндексСтроки]);
		СтрокаМассива.СуммаНДС		= ?(СуммаНДС = 0,		0, МассивРезультатаРаспределения_НДС[ИндексСтроки]);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьТоварыРаздела(НомерРаздела)
	
	МассивСтрок = Объект.Запасы.НайтиСтроки(Новый Структура("НомерРаздела", НомерРаздела));
	Для Каждого СтрокаРаздела Из МассивСтрок Цикл
		
		Объект.Запасы.Удалить(СтрокаРаздела);
		
	КонецЦикла;
	
	Если Объект.Разделы.Количество() > НомерРаздела Тогда
		
		ПересчитатьНомераРазделов(НомерРаздела);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьНомераРазделов(НомерУдаленногоРаздела)
	
	Для каждого СтрокаТовары Из Объект.Запасы Цикл
		
		Если СтрокаТовары.НомерРаздела > НомерУдаленногоРаздела Тогда
			
			СтрокаТовары.НомерРаздела = СтрокаТовары.НомерРаздела - 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоНакладнойНаСевере(ДокументСсылка)
	
	ТекущийДокументОбъект = РеквизитФормыВЗначение("Объект");
	ТекущийДокументОбъект.Заполнить(ДокументСсылка);
	ЗначениеВРеквизитФормы(ТекущийДокументОбъект, "Объект");
	
	Модифицированность = Истина;
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	ЗаполнитьНомерРазделаПоНомеруСтроки();
	
КонецПроцедуры

#Область СлужебныеКопированиеСтрок

&НаКлиенте
Процедура ВставитьСтроки(СтрокаРаздела, ИмяТЧ)
	
	КоличествоСкопированных = 0;
	КоличествоВставленных = 0;
	ВставитьСтрокиНаСервере(СтрокаРаздела.НомерСтроки, ИмяТЧ, КоличествоСкопированных, КоличествоВставленных);
	
	ЗаполнитьСведенияОРазделе(СтрокаРаздела.ПолучитьИдентификатор());
	
	КопированиеТабличнойЧастиКлиент.ОповеститьПользователяОВставкеСтрок(КоличествоСкопированных, КоличествоВставленных);
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьСтроки(ИмяТЧ)
	
	Если КопированиеТабличнойЧастиКлиент.МожноКопироватьСтроки(Объект[ИмяТЧ], Элементы[ИмяТЧ].ТекущиеДанные) Тогда
		КоличествоСкопированных = 0;
		КопироватьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных);
		КопированиеТабличнойЧастиКлиент.ОповеститьПользователяОКопированииСтрок(КоличествоСкопированных);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеРезервнымиКопиямиТаблицы(СостояниеПерехода, ИзменяетДанные)
	
	ГрупповоеИзменениеСтрокСервер.УправлениеРезервнымиКопиями(
		ЭтотОбъект,
		Объект.Запасы,
		ЗапасыРезервнаяКопияТаблицыАдрес,
		СостояниеПерехода,
		ИзменяетДанные
	);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПометку(Пометка)
	
	Элементы.ЗапасыСнятьФлажки.Видимость = НЕ Элементы.ЗапасыСнятьФлажки.Видимость;
	Элементы.ЗапасыУстановитьФлажки.Видимость = НЕ Элементы.ЗапасыУстановитьФлажки.Видимость;
	
	Для каждого Строка Из Объект.Запасы Цикл
		Строка.Пометка = Пометка;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьТекущееДействиеИзмененияСтрок()
	
	Если ЗапасыИзменениеСтрокДействие = ЗапасыИзменениеСтрокДействиеПриОткрытии Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьТекущееДействиеИзмененияСтрокСервер();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьТекущееДействиеИзмененияСтрокСервер()
	
	ГрупповоеИзменениеСтрокСервер.СохранитьНастройки(НаборЭлементовГрупповогоИзмененияСтрокСервер());
	
КонецПроцедуры

&НаСервере
Процедура КопироватьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных)
	
	КопированиеТабличнойЧастиСервер.Копировать(Объект[ИмяТЧ], Элементы[ИмяТЧ].ВыделенныеСтроки, КоличествоСкопированных);
	
КонецПроцедуры

&НаСервере
Процедура ВставитьСтрокиНаСервере(НомерСтрокиРаздела, ИмяТЧ, КоличествоСкопированных, КоличествоВставленных)
	
	КопированиеТабличнойЧастиСервер.Вставить(Объект, ИмяТЧ, Элементы, КоличествоСкопированных, КоличествоВставленных);
	ОбработатьВставленныеСтрокиНаСервере(НомерСтрокиРаздела, ИмяТЧ, КоличествоВставленных);
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВставленныеСтрокиНаСервере(НомерСтрокиРаздела, ИмяТЧ, КоличествоВставленных)
	
	Количество = Объект[ИмяТЧ].Количество();
	
	Для Итератор = 1 По КоличествоВставленных Цикл
		
		Строка = Объект[ИмяТЧ][Количество - Итератор];
		
		СтруктураДанные = Новый Структура;
		СтруктураДанные.Вставить("Организация", Объект.Организация);
		СтруктураДанные.Вставить("Номенклатура", Строка.Номенклатура);
		СтруктураДанные.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
		СтруктураДанные.Вставить("ДатаОбработки", Объект.Дата);
		
		СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
		
		Строка.НомерРаздела = НомерСтрокиРаздела;
		Строка.СуммаНДС = 0;
		
		Если НЕ ЗначениеЗаполнено(Строка.СтранаПроисхождения) Тогда
			
			Строка.СтранаПроисхождения = Строка.Номенклатура.СтранаПроисхождения;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеГрупповоеИзменение

&НаКлиенте
Процедура ОбработатьТаблицу()
	Перем Ошибки, ИзмененныеРазделы;
	
	СтрокаРаздела = Элементы.Разделы.ТекущиеДанные;
	Если СтрокаРаздела = Неопределено Тогда
		
		ТекстОшибки = НСтр("ru ='Укажите раздел ГТД, в который необходимо добавить товары'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Разделы", ТекстОшибки, "");
		
	Иначе
		
		ОбработатьТаблицуНаСервере(СтрокаРаздела.ПолучитьИдентификатор(), ИзмененныеРазделы, Ошибки);
		
	КонецЕсли;
	
	Если НЕ Ошибки = Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		Возврат;
		
	КонецЕсли;
	
	Если ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокумента")
		ИЛИ ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокументаИмпортныеТовары")
		ИЛИ ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ПодобратьНомераГТД")
		ИЛИ ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УдалитьСтроки")
		Тогда
		
		ПересчитатьИтогиРаздела(СтрокаРаздела.НомерСтроки);
		ЗаполнитьСведенияОРазделе(СтрокаРаздела.ПолучитьИдентификатор());
		
		РаспределитьСуммуСбораПоЗапасам();
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоСуммам") 
		И НЕ Элементы.ЗапасыСуммаСбора.Доступность Тогда
		
		РаспределитьСуммуСбораПоЗапасам();
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьИсходноеМесто") Тогда
		
		Если ИзмененныеРазделы <> Неопределено Тогда
			
			Для каждого СтрокаРаздела Из Объект.Разделы Цикл
				
				Если ИзмененныеРазделы.Найти(СтрокаРаздела.НомерСтроки) = Неопределено Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				ПересчитатьИтогиРаздела(СтрокаРаздела.НомерСтроки, "ТаможеннаяСтоимость");
				ЗаполнитьСведенияОРазделе(СтрокаРаздела.ПолучитьИдентификатор());
				
			КонецЦикла;
			
			РаспределитьСуммуСбораПоЗапасам();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОформлениеПанелиРедактирования(Состояние, СохранитьИзменения = Неопределено)
	
	ГрупповоеИзменениеСтрокКлиент.НастроитьОформлениеПанелиРедактирования(
		ЭтотОбъект,
		НаборЭлементовГрупповогоИзмененияСтрокКлиент(),
		Состояние,
		ЗапасыИзменениеСтрокЗначение
	);
	
	ПриИзмененииТекущегоДействия();
	
КонецПроцедуры

&НаКлиенте
Функция НаборЭлементовГрупповогоИзмененияСтрокКлиент()
	
	НаборЭлементов = Новый Структура();
	НаборЭлементов.Вставить("ИмяТЧ", "Запасы");
	НаборЭлементов.Вставить("ДокументСсылка", Объект.Ссылка);
	НаборЭлементов.Вставить("ПанельРедактирования", Элементы.ГруппаЗапасыИзменениеСтрок);
	НаборЭлементов.Вставить("ГруппаДействие", Элементы.ГруппаЗапасыВыборДействия);
	НаборЭлементов.Вставить("ГруппаЗначение", Элементы.ГруппаЗапасыВыборЗначения);
	НаборЭлементов.Вставить("ГруппаВыполнить", Элементы.ГруппаЗапасыВыполнитьДействие);
	НаборЭлементов.Вставить("КнопкаВыполнитьДействие", Элементы.ЗапасыВыполнитьДействие);
	НаборЭлементов.Вставить("КолонкаПометка", Элементы.ЗапасыПометка);
	НаборЭлементов.Вставить("КолонкаНомерСтроки", Элементы.ЗапасыНомерСтроки);
	НаборЭлементов.Вставить("Действие", ЭтаФорма.ЗапасыИзменениеСтрокДействие);
	НаборЭлементов.Вставить("ДействиеЭлемент", Элементы.ЗапасыИзменениеСтрокДействие);
	НаборЭлементов.Вставить("Значение", ЭтаФорма.ЗапасыИзменениеСтрокЗначение);
	НаборЭлементов.Вставить("ЗначениеЭлемент", Элементы.ЗапасыИзменениеСтрокЗначение);
	НаборЭлементов.Вставить("ОбъектИзменений", ""); 				// Неактуально
	НаборЭлементов.Вставить("КолонкаОбъектИзменений", Неопределено);// Неактуально
	Возврат НаборЭлементов;
	
КонецФункции

&НаСервере
Процедура ИнициализироватьГрупповоеИзменениеСтрок()
	
	ЗаполнитьСписокДействий();
	ГрупповоеИзменениеСтрокСервер.ПриСозданииНаСервере(НаборЭлементовГрупповогоИзмененияСтрокСервер(), ЭтаФорма.ЗапасыИзменениеСтрокДействие);
	ЗапасыИзменениеСтрокДействиеПриОткрытии = ЗапасыИзменениеСтрокДействие;
	УстановитьПометку(Истина);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьТаблицуНаСервере(ИдентификаторРаздела, ИзмененныеРазделы, Ошибки)
	
	Если ЗапасыИзменениеСтрокДействие = Перечисления.ДействияГрупповогоИзмененияСтрок.ПодобратьДокументыПартий Тогда
		
		ПодобратьДокументыПартий(ИдентификаторРаздела, Ошибки);
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = Перечисления.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокумента Тогда
		
		ДобавитьСтрокиВРазделИзДокумента(ИдентификаторРаздела, Ложь, Ошибки);
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = Перечисления.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокументаИмпортныеТовары Тогда
		
		ДобавитьСтрокиВРазделИзДокумента(ИдентификаторРаздела, Истина, Ошибки);
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = Перечисления.ДействияГрупповогоИзмененияСтрок.ПодобратьНомераГТД Тогда
		
		ДобавитьСтрокиВРазделИзДокументовПриходнаяНакладная(ИдентификаторРаздела, Ошибки);
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = Перечисления.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоСуммам Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыСуммаСбора", "Доступность", НЕ Элементы.ЗапасыСуммаСбора.Доступность);
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = Перечисления.ДействияГрупповогоИзмененияСтрок.УдалитьСтроки Тогда
		
		ПомеченныеСтроки = Объект.Запасы.НайтиСтроки(Новый Структура("Пометка", Истина));
		Для каждого СтрокаТаблицы Из ПомеченныеСтроки Цикл
			
			Объект.Запасы.Удалить(СтрокаТаблицы);
			
		КонецЦикла;
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = Перечисления.ДействияГрупповогоИзмененияСтрок.УстановитьИсходноеМесто Тогда
		
		ИзмененныеРазделы = Новый Массив;
		ИзмененныеРазделы.Добавить(ЗапасыИзменениеСтрокЗначение);
		
		ПомеченныеСтроки = Объект.Запасы.НайтиСтроки(Новый Структура("Пометка", Истина));
		Для каждого СтрокаТаблицы Из ПомеченныеСтроки Цикл
			
			Если ИзмененныеРазделы.Найти(СтрокаТаблицы.НомерРаздела) = Неопределено Тогда
				
				ИзмененныеРазделы.Добавить(СтрокаТаблицы.НомерРаздела);
				
			КонецЕсли;
			
			СтрокаТаблицы.НомерРаздела = ЗапасыИзменениеСтрокЗначение;
			
		КонецЦикла;
		
		Если ИзмененныеРазделы.Количество() = 1 Тогда
			
			ИзмененныеРазделы = Неопределено;
			
		КонецЕсли;
		
	КонецЕсли;
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииТекущегоДействия()
	
	МассивТипов = Новый Массив;
	
	Если ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение(
		"Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьИсходноеМесто") Тогда
		
		МассивТипов.Добавить(Тип("Число"));
		
		ДанныеТекущегоРаздела = Элементы.Разделы.ТекущиеДанные;
		
		Элементы.ЗапасыИзменениеСтрокЗначение.СписокВыбора.Очистить();
		Для СчетчикРазделов = 1 По Объект.Разделы.Количество() Цикл
			
			Если НЕ ДанныеТекущегоРаздела.НомерСтроки = СчетчикРазделов Тогда
				
				Элементы.ЗапасыИзменениеСтрокЗначение.СписокВыбора.Добавить(СчетчикРазделов, СтрШаблон(НСтр(
					"ru = 'Раздел %1'"), СчетчикРазделов));
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если Элементы.ЗапасыИзменениеСтрокЗначение.СписокВыбора.Количество() = 1 Тогда
			
			ЗапасыИзменениеСтрокЗначение = Элементы.ЗапасыИзменениеСтрокЗначение.СписокВыбора[0].Значение;
			
		КонецЕсли;
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокумента")
			ИЛИ ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокументаИмпортныеТовары") Тогда
		
		МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ПриходнаяНакладная"));
		МассивТипов.Добавить(Тип("ДокументСсылка.СчетНаОплатуПоставщика"));
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоСуммам") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗапасыИзменениеСтрокЗначение", "Видимость", Ложь);
		
	КонецЕсли;
	
	Элементы.ЗапасыИзменениеСтрокЗначение.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьСкрытьПанельРедактирования(ИзменяетДанные = Неопределено)
	Перем СостояниеПерехода;
	
	ГрупповоеИзменениеСтрокСервер.ПоказатьСкрытьПанельРедактирования(
		ЭтотОбъект,
		НаборЭлементовГрупповогоИзмененияСтрокСервер(),
		СостояниеПерехода,
		ИзменяетДанные
	);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаРазделы", "Доступность", НЕ Элементы.ГруппаЗапасыИзменениеСтрок.Видимость);
	
	УправлениеРезервнымиКопиямиТаблицы(СостояниеПерехода, ИзменяетДанные);
	
КонецПроцедуры

&НаСервере
Функция НаборЭлементовГрупповогоИзмененияСтрокСервер()
	
	НаборЭлементов = Новый Структура;
	НаборЭлементов.Вставить("ИмяТЧ", "Запасы");
	НаборЭлементов.Вставить("ДокументСсылка", Объект.Ссылка);
	НаборЭлементов.Вставить("ПанельРедактирования", Элементы.ГруппаЗапасыИзменениеСтрок);
	НаборЭлементов.Вставить("ГруппаДействие", Элементы.ГруппаЗапасыВыборДействия);
	НаборЭлементов.Вставить("ГруппаЗначение", Элементы.ГруппаЗапасыВыборЗначения);
	НаборЭлементов.Вставить("ГруппаВыполнить", Элементы.ГруппаЗапасыВыполнитьДействие);
	НаборЭлементов.Вставить("КнопкаВыполнитьДействие", Элементы.ЗапасыВыполнитьДействие);
	НаборЭлементов.Вставить("КнопкаИзменитьСтроки", Элементы.ЗапасыИзменитьСтроки);
	НаборЭлементов.Вставить("КолонкаПометка", Элементы.ЗапасыПометка);
	НаборЭлементов.Вставить("КолонкаНомерСтроки", Элементы.ЗапасыНомерСтроки);
	НаборЭлементов.Вставить("Действие", ЭтаФорма.ЗапасыИзменениеСтрокДействие);
	НаборЭлементов.Вставить("ДействиеЭлемент", Элементы.ЗапасыИзменениеСтрокДействие);
	НаборЭлементов.Вставить("Значение", ЭтаФорма.ЗапасыИзменениеСтрокЗначение);
	НаборЭлементов.Вставить("ЗначениеЭлемент", Элементы.ЗапасыИзменениеСтрокЗначение);
	НаборЭлементов.Вставить("ОбъектИзменений", "");					//Неактуально
	НаборЭлементов.Вставить("КолонкаОбъектИзменений", Неопределено);//Неактуально
	Возврат НаборЭлементов;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокДействий()
	
	СписокВыбораДействий = Элементы.ЗапасыИзменениеСтрокДействие.СписокВыбора;
	
	СписокВыбораДействий.Очистить();
	
	ОбменСБухгалтериейНастроен = УправлениеНебольшойФирмойПовтИсп.ОбменСБухгалтериейНастроен();
	Если ОбменСБухгалтериейНастроен Тогда
		
		СписокВыбораДействий.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ПодобратьДокументыПартий,		НСтр("ru ='Подобрать документы партий'"));
		
	КонецЕсли;
	
	СписокВыбораДействий.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокумента,				НСтр("ru ='Добавить из документа'"));
	СписокВыбораДействий.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокументаИмпортныеТовары, НСтр("ru ='Добавить из документа (импортные товары)'"));
	СписокВыбораДействий.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ПодобратьНомераГТД,					НСтр("ru ='Добавить строки по номеру ГТД из приходных накладных'"));
	СписокВыбораДействий.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоСуммам,			НСтр("ru ='Уточнить суммы распределения таможенного сбора'"));
	СписокВыбораДействий.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.УдалитьСтроки, 						НСтр("ru ='Удалить строки'"));
	
	Если Объект.Разделы.Количество() > 1 Тогда
		
		СписокВыбораДействий.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.УстановитьИсходноеМесто,	НСтр("ru ='Перенести строки в другой раздел'"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодобратьДокументыПартий(ИдентификаторРаздела, Ошибки)
	
	СтрокаРаздела = Объект.Разделы.НайтиПоИдентификатору(ИдентификаторРаздела);
	Если СтрокаРаздела = Неопределено Тогда
		
		ТекстОшибки = НСтр("ru ='Укажите раздел ГТД, в который необходимо добавить товары'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Разделы", ТекстОшибки, "");
		
	КонецЕсли;
	
	Если НЕ Ошибки = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	МассивСтрок = Объект.Запасы.НайтиСтроки(Новый Структура("НомерРаздела", СтрокаРаздела.НомерСтроки));
	ТаблицаЗапасов = Объект.Запасы.Выгрузить(МассивСтрок);
	
	Если ТаблицаЗапасов.Количество() < 1 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекстаЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаЗапасов.НомерСтроки КАК НомерСтроки
	|	,ТаблицаЗапасов.Номенклатура КАК Номенклатура
	|	,ТаблицаЗапасов.Характеристика КАК Характеристика
	|	,ТаблицаЗапасов.Партия КАК Партия
	|	,ТаблицаЗапасов.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ДанныеПоДокументам
	|ИЗ &ТаблицаЗапасов КАК ТаблицаЗапасов
	|;
	|
	|ВЫБРАТЬ
	|	ДанныеПоДокументам.НомерСтроки КАК НомерСтроки
	|	,ДанныеПоДокументам.Номенклатура КАК Номенклатура
	|	,ДанныеПоДокументам.Характеристика КАК Характеристика
	|	,ДанныеПоДокументам.Партия КАК Партия
	|	,ДанныеПоДокументам.СтранаПроисхождения КАК СтранаПроисхождения
	|	,Максимум(ТабличнаяЧастьДокумента.Ссылка.Дата) КАК Период
	|ПОМЕСТИТЬ ДанныеПоПериодам
	|ИЗ ДанныеПоДокументам КАК ДанныеПоДокументам
	|	СОЕДИНЕНИЕ Документ.ПриходнаяНакладная.Запасы КАК ТабличнаяЧастьДокумента
	|	ПО ДанныеПоДокументам.Номенклатура = ТабличнаяЧастьДокумента.Номенклатура
	|		И ДанныеПоДокументам.Характеристика = ТабличнаяЧастьДокумента.Характеристика
	|		И ДанныеПоДокументам.Партия = ТабличнаяЧастьДокумента.Партия
	|		И ДанныеПоДокументам.СтранаПроисхождения = ТабличнаяЧастьДокумента.СтранаПроисхождения
	|		И ТабличнаяЧастьДокумента.НомерГТД = &НомерГТД
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПоДокументам.НомерСтроки
	|	,ДанныеПоДокументам.Номенклатура
	|	,ДанныеПоДокументам.Характеристика
	|	,ДанныеПоДокументам.Партия
	|	,ДанныеПоДокументам.СтранаПроисхождения
	|;
	|
	|ВЫБРАТЬ
	|	ДанныеПоПериодам.НомерСтроки КАК НомерСтроки
	|	,ДанныеПоПериодам.Номенклатура КАК Номенклатура
	|	,ДанныеПоПериодам.Характеристика КАК Характеристика
	|	,ДанныеПоПериодам.Партия КАК Партия
	|	,ДанныеПоПериодам.СтранаПроисхождения КАК СтранаПроисхождения
	|	,ТабличнаяЧастьДокумента.Ссылка КАК ДокументПартии
	|ИЗ ДанныеПоПериодам КАК ДанныеПоПериодам
	|	СОЕДИНЕНИЕ Документ.ПриходнаяНакладная.Запасы КАК ТабличнаяЧастьДокумента
	|	ПО ДанныеПоПериодам.Номенклатура = ТабличнаяЧастьДокумента.Номенклатура
	|		И ДанныеПоПериодам.Характеристика = ТабличнаяЧастьДокумента.Характеристика
	|		И ДанныеПоПериодам.Партия = ТабличнаяЧастьДокумента.Партия
	|		И ДанныеПоПериодам.СтранаПроисхождения = ТабличнаяЧастьДокумента.СтранаПроисхождения
	|		И ДанныеПоПериодам.Период = ТабличнаяЧастьДокумента.Ссылка.Дата
	|";
	
	Запрос = Новый Запрос(ТекстаЗапроса);
	Запрос.УстановитьПараметр("ТаблицаЗапасов", ТаблицаЗапасов);
	Запрос.УстановитьПараметр("НомерГТД", Объект.НомерГТД);
	ТаблицаДокументовПартий = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаЗапасов Из МассивСтрок Цикл
		
		НайденныеСтроки = ТаблицаДокументовПартий.НайтиСтроки(Новый Структура("НомерСтроки", СтрокаЗапасов.НомерСтроки));
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			ЗаполнитьЗначенияСвойств(СтрокаЗапасов, НайденныеСтроки[0], "ДокументПартии");
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокиВРазделИзДокумента(ИдентификаторРаздела, ТолькоИмпортные, Ошибки)
	
	Если НЕ ЗначениеЗаполнено(ЗапасыИзменениеСтрокЗначение) Тогда
		
		ТекстОшибки = НСтр("ru ='Для заполнения необходимо выбрать документ'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ЗапасыИзменениеСтрокЗначение", ТекстОшибки, "");
		
	КонецЕсли;
	
	СтрокаРаздела = Объект.Разделы.НайтиПоИдентификатору(ИдентификаторРаздела);
	Если СтрокаРаздела = Неопределено Тогда
		
		ТекстОшибки = НСтр("ru ='Укажите раздел ГТД, в который необходимо добавить товары'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Разделы", ТекстОшибки, "");
		
	КонецЕсли;
	
	Если НЕ Ошибки = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ШаблонТекстаЗапроса = 
	"ВЫБРАТЬ
	|	ТабличнаяЧастьДокумента.Номенклатура КАК Номенклатура
	|	,ТабличнаяЧастьДокумента.Характеристика КАК Характеристика
	|	,&ИмяПоляПартия КАК Партия
	|	,&ИмяПоляСтранаПроисхождения КАК СтранаПроисхождения
	|	,ТабличнаяЧастьДокумента.Количество КАК Количество
	|	,ТабличнаяЧастьДокумента.Сумма КАК Сумма
	|	,ТабличнаяЧастьДокумента.СуммаНДС КАК СуммаНДС
	|	,ТабличнаяЧастьДокумента.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС
	|ИЗ
	|	&ИмяТаблицыДокумента КАК ТабличнаяЧастьДокумента
	|ГДЕ
	|	ТабличнаяЧастьДокумента.Ссылка = &Ссылка И &ТолькоИмпортные";
	
	Если ТипЗнч(ЗапасыИзменениеСтрокЗначение) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&ИмяПоляПартия", "NULL");
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&ИмяПоляСтранаПроисхождения", "ТабличнаяЧастьДокумента.Номенклатура.СтранаПроисхождения");
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&ИмяТаблицыДокумента", "Документ.ЗаказПоставщику.Запасы");
		
		ОписаниеУсловий = ?(ТолькоИмпортные,
		"
		|	И ТабличнаяЧастьДокумента.Номенклатура.СтранаПроисхождения <> Значение(Справочник.СтраныМира.ПустаяСсылка)
		|	И ТабличнаяЧастьДокумента.Номенклатура.СтранаПроисхождения <> Значение(Справочник.СтраныМира.Россия)",
		"");
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, " И &ТолькоИмпортные", ОписаниеУсловий);
		
	ИначеЕсли ТипЗнч(ЗапасыИзменениеСтрокЗначение) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
		
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&ИмяПоляПартия", "ТабличнаяЧастьДокумента.Партия");
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&ИмяПоляСтранаПроисхождения", "ТабличнаяЧастьДокумента.СтранаПроисхождения");
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&ИмяТаблицыДокумента", "Документ.ПриходнаяНакладная.Запасы");
		
		ОписаниеУсловий = ?(ТолькоИмпортные,
		"
		|	И ТабличнаяЧастьДокумента.СтранаПроисхождения <> Значение(Справочник.СтраныМира.ПустаяСсылка)
		|	И ТабличнаяЧастьДокумента.СтранаПроисхождения <> Значение(Справочник.СтраныМира.Россия)",
		"");
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, " И &ТолькоИмпортные", ОписаниеУсловий);
		
	ИначеЕсли ТипЗнч(ЗапасыИзменениеСтрокЗначение) = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда
		
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&ИмяПоляПартия", "ТабличнаяЧастьДокумента.Партия");
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&ИмяПоляСтранаПроисхождения", "ТабличнаяЧастьДокумента.Номенклатура.СтранаПроисхождения");
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&ИмяТаблицыДокумента", "Документ.СчетНаОплатуПоставщика.Запасы");
		
		ОписаниеУсловий = ?(ТолькоИмпортные,
		"
		|	И ТабличнаяЧастьДокумента.Номенклатура.СтранаПроисхождения <> Значение(Справочник.СтраныМира.ПустаяСсылка)
		|	И ТабличнаяЧастьДокумента.Номенклатура.СтранаПроисхождения <> Значение(Справочник.СтраныМира.Россия)",
		"");
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, " И &ТолькоИмпортные", ОписаниеУсловий);
		
	КонецЕсли;
	
	Запрос = Новый Запрос(ШаблонТекстаЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ЗапасыИзменениеСтрокЗначение);
	Запрос.УстановитьПараметр("НомерГТД", Объект.НомерГТД);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОблагаетсяНДС = ?(ЗапасыИзменениеСтрокЗначение.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС, Истина, Ложь);
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Объект.Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка, "Номенклатура, Характеристика, Партия, СтранаПроисхождения, Количество");
		
		НоваяСтрока.Пометка = Истина;
		НоваяСтрока.НомерРаздела = СтрокаРаздела.НомерСтроки;
		НоваяСтрока.ФактурнаяСтоимость = ?(Выборка.СуммаВключаетНДС И ОблагаетсяНДС, Выборка.Сумма - Выборка.СуммаНДС, Выборка.Сумма);
		
		Если ТипЗнч(ЗапасыИзменениеСтрокЗначение) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
			
			НоваяСтрока.ДокументПартии = ЗапасыИзменениеСтрокЗначение;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокиВРазделИзДокументовПриходнаяНакладная(ИдентификаторРаздела, Ошибки)
	
	СтрокаРаздела = Объект.Разделы.НайтиПоИдентификатору(ИдентификаторРаздела);
	Если СтрокаРаздела = Неопределено Тогда
		
		ТекстОшибки = НСтр("ru ='Укажите раздел ГТД, в который необходимо добавить товары'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Разделы", ТекстОшибки, "");
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.НомерГТД) Тогда
		
		ТекстОшибки = НСтр("ru ='Укажите номер ГТД'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Разделы", ТекстОшибки, "");
		
	КонецЕсли;
	
	Если НЕ Ошибки = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокументаЗапасы.Ссылка КАК ДокументПартии
	|	,ТаблицаДокументаЗапасы.Номенклатура КАК Номенклатура
	|	,ТаблицаДокументаЗапасы.Характеристика КАК Характеристика
	|	,ТаблицаДокументаЗапасы.Партия КАК Партия
	|	,ТаблицаДокументаЗапасы.СтранаПроисхождения КАК СтранаПроисхождения
	|	,ТаблицаДокументаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
	|	,ТаблицаДокументаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя
	|	,Выбор КОГДА ТаблицаДокументаЗапасы.ЕдиницаИзмерения Ссылка Справочник.КлассификаторЕдиницИзмерения
	|		ТОГДА ТаблицаДокументаЗапасы.Количество
	|		ИНАЧЕ ТаблицаДокументаЗапасы.Количество * ТаблицаДокументаЗапасы.ЕдиницаИзмерения.Коэффициент КОНЕЦ КАК Количество
	|	,Выбор КОГДА СуммыРегУчет.Всего ЕСТЬ NULL
	|		ТОГДА
	|			ВЫБОР КОГДА ТаблицаДокументаЗапасы.Ссылка.СуммаВключаетНДС
	|				ТОГДА ТаблицаДокументаЗапасы.Сумма - ТаблицаДокументаЗапасы.СуммаНДС
	|				ИНАЧЕ ТаблицаДокументаЗапасы.Сумма КОНЕЦ
	|		ИНАЧЕ ВЫРАЗИТЬ(СуммыРегУчет.Всего КАК Число(15,2)) - ВЫРАЗИТЬ(СуммыРегУчет.НДС КАК Число(15,2)) КОНЕЦ КАК СуммаБезНДС
	|ПОМЕСТИТЬ СтрокиВВалютеТекущегоДокумента
	|ИЗ
	|	Документ.ПриходнаяНакладная.Запасы КАК ТаблицаДокументаЗапасы
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовРегламентированныйУчет КАК СуммыРегУчет
	|		ПО ТаблицаДокументаЗапасы.Ссылка = СуммыРегУчет.Регистратор
	|			И СуммыРегУчет.ТабличнаяЧастьДокумента = Значение(Перечисление.ТабличныеЧастиДокументов.Запасы)
	|			И СуммыРегУчет.НомерСтрокиДокумента = ТаблицаДокументаЗапасы.НомерСтроки
	|ГДЕ
	|	ТаблицаДокументаЗапасы.НомерГТД = &НомерГТД
	|	И ТаблицаДокументаЗапасы.Ссылка.Проведен = ИСТИНА
	|
	|;Выбрать
	|	СтрокиПриходныхНакладный.Номенклатура КАК Номенклатура
	|	,СтрокиПриходныхНакладный.Характеристика КАК Характеристика
	|	,СтрокиПриходныхНакладный.Партия КАК Партия
	|	,СтрокиПриходныхНакладный.СтранаПроисхождения КАК СтранаПроисхождения
	|	,СтрокиПриходныхНакладный.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
	|	,СтрокиПриходныхНакладный.ЗаказПокупателя КАК ЗаказПокупателя
	|	,СтрокиПриходныхНакладный.ДокументПартии КАК ДокументПартии
	|	,Сумма(СтрокиПриходныхНакладный.Количество) КАК Количество
	|	,Сумма(СтрокиПриходныхНакладный.СуммаБезНДС) КАК ФактурнаяСтоимость
	|ИЗ СтрокиВВалютеТекущегоДокумента КАК СтрокиПриходныхНакладный
	|СГРУППИРОВАТЬ ПО
	|	СтрокиПриходныхНакладный.Номенклатура
	|	,СтрокиПриходныхНакладный.Характеристика
	|	,СтрокиПриходныхНакладный.Партия
	|	,СтрокиПриходныхНакладный.СтранаПроисхождения
	|	,СтрокиПриходныхНакладный.СтруктурнаяЕдиница
	|	,СтрокиПриходныхНакладный.ЗаказПокупателя
	|	,СтрокиПриходныхНакладный.ДокументПартии";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НомерГТД", 			Объект.НомерГТД);
	Запрос.УстановитьПараметр("ДатаДокумента", 		Объект.Дата);
	Запрос.УстановитьПараметр("ВалютаДокумента", 	Объект.ВалютаДокумента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Объект.Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка, "Номенклатура, Характеристика, Партия, СтранаПроисхождения, СтруктурнаяЕдиница, ЗаказПокупателя, ДокументПартии, Количество, ФактурнаяСтоимость");
		
		НоваяСтрока.НомерРаздела = СтрокаРаздела.НомерСтроки;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Разделы.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.НомерРаздела)
		И Не СтрокаТабличнойЧасти.НомерСтроки = СтрокаТабличнойЧасти.НомерРаздела Тогда
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("НомерРаздела");
		
		СоответствиеСтрокРазделам = Новый Соответствие;
		
		Для Каждого СтрокаРазделов Из Объект.Разделы Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаРазделов.НомерРаздела) Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаРазделов.НомерСтроки = СтрокаРазделов.НомерРаздела Тогда
				Продолжить
			КонецЕсли;
			
			СтруктураОтбора.НомерРаздела = СтрокаРазделов.НомерРаздела;
			НайденныеСтроки = Объект.Запасы.НайтиСтроки(СтруктураОтбора);
			
			СоответствиеСтрокРазделам.Вставить(СтрокаРазделов.НомерСтроки, НайденныеСтроки);
			
		КонецЦикла;
		
		Для Каждого СтрокаСоответствия Из СоответствиеСтрокРазделам Цикл
			
			Для Каждого НайденнаяСтрока Из СтрокаСоответствия.Значение Цикл
				НайденнаяСтрока.НомерРаздела = СтрокаСоответствия.Ключ;
			КонецЦикла;
			
		КонецЦикла;
		
		ЗаполнитьНомерРазделаПоНомеруСтрокиКлиент();
		
		Элементы.Запасы.ОтборСтрок = Новый ФиксированнаяСтруктура("НомерРаздела", СтрокаТабличнойЧасти.НомерСтроки);
		
	КонецЕсли;

	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПриАктивизацииСтроки(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Разделы.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НомерРаздела = Объект.Разделы.Индекс(СтрокаТабличнойЧасти) + 1;
	
	Элементы.Запасы.ОтборСтрок = Новый ФиксированнаяСтруктура("НомерРаздела", НомерРаздела);
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	НоваяСтрока = Объект.Разделы.Добавить();
	
	Если Копирование Тогда
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Родитель);
	КонецЕсли;
	
	НоваяСтрока.НомерРаздела = НоваяСтрока.НомерСтроки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
// @skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
