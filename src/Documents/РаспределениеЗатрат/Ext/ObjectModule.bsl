#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура заполняет табличную часть по спецификации.
//
// Параметры:
//  СпецификацияЗаполнения		 - 	СправочникСсылка.Спецификации - Спецификация для заполнения
//  КоличествоЗаполнения		 - 	Число - Количество продукции
//  ЕдиницаИзмеренияЗаполнения	 - 	СправочникСсылка.ЕдиницаИзмерения, 
//		СправочникСсылка.КлассификаторЕдиницИзмерения - Единица измерения продукции
//  ЗаказЗаполнения				 - 	ДокументСсылка.ЗаказПокупателя - Заказ для заполнения табличной части
//
Процедура ЗаполнитьТабличнуюЧастьПоСпецификации(СпецификацияЗаполнения, КоличествоЗаполнения, ЕдиницаИзмеренияЗаполнения, 
	ЗаказЗаполнения) Экспорт
    
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(СпецификацииСостав.НомерСтроки) КАК СпецификацииСоставНомерСтроки,
	|	СпецификацииСостав.Номенклатура КАК Номенклатура,
	|	СпецификацииСостав.ТипСтрокиСостава КАК ТипСтрокиСостава,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА СпецификацииСостав.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	СпецификацииСостав.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СпецификацииСостав.Спецификация КАК Спецификация,
	|	СУММА(ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(&ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|				ТОГДА СпецификацииСостав.Количество / СпецификацииСостав.КоличествоПродукции * &Количество
	|			ИНАЧЕ СпецификацииСостав.Количество / СпецификацииСостав.КоличествоПродукции * &Коэффициент * &Количество
	|		КОНЕЦ) КАК Количество,
	|	&ЗаказПокупателя КАК ЗаказПокупателя
	|ИЗ
	|	Справочник.Спецификации.Состав КАК СпецификацииСостав
	|ГДЕ
	|	СпецификацииСостав.Ссылка = &Спецификация
	|	И СпецификацииСостав.Номенклатура.ТипНоменклатуры = &ТипНоменклатуры
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацииСостав.Номенклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА СпецификацииСостав.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	СпецификацииСостав.ЕдиницаИзмерения,
	|	СпецификацииСостав.Спецификация,
	|	СпецификацииСостав.ТипСтрокиСостава,
	|	СпецификацииСостав.ДоляСтоимости
	|
	|УПОРЯДОЧИТЬ ПО
	|	СпецификацииСоставНомерСтроки");
	
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики",
		Константы.ФункциональнаяОпцияИспользоватьХарактеристики.Получить());
	Запрос.УстановитьПараметр("ДатаОбработки", КонецДня(Дата));
	
	Запрос.УстановитьПараметр("ЗаказПокупателя", ЗаказЗаполнения);
	Запрос.УстановитьПараметр("Спецификация", СпецификацияЗаполнения);
	Запрос.УстановитьПараметр("Количество", КоличествоЗаполнения);
	
	Запрос.УстановитьПараметр("ЕдиницаИзмерения", ЕдиницаИзмеренияЗаполнения);
	
	Если ТипЗнч(ЕдиницаИзмеренияЗаполнения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
    	Запрос.УстановитьПараметр("Коэффициент", 1);
	Иначе
		Запрос.УстановитьПараметр("Коэффициент", ЕдиницаИзмеренияЗаполнения.Коэффициент);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Запас);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ТипСтрокиСостава = Перечисления.ТипыСтрокСоставаСпецификации.Узел Тогда
			
			ЗаполнитьТабличнуюЧастьПоСпецификации(Выборка.Спецификация, Выборка.Количество, 
				Выборка.ЕдиницаИзмерения, ЗаказЗаполнения);
			
		Иначе
			
	    	НоваяСтрока = Запасы.Добавить();
	    	ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
		КонецЕсли;
		
   КонецЦикла;

КонецПроцедуры // ЗаполнитьТабличнуюЧастьПоСпецификации()

// Процедура выполняет заполнение запасов по нормативам.
//
Процедура ВыполнитьЗаполнениеЗапасовПоНормативам() Экспорт
	
	Запасы.Очистить();
	РаспределениеЗапасов.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РаспределениеЗатратПродукция.Спецификация КАК Спецификация,
	|	РаспределениеЗатратПродукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	РаспределениеЗатратПродукция.Количество КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблицаПродукция
	|ИЗ
	|	&РаспределениеЗатратПродукция КАК РаспределениеЗатратПродукция
	|ГДЕ
	|	РаспределениеЗатратПродукция.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(СпецификацииСостав.НомерСтроки) КАК СпецификацииСоставНомерСтроки,
	|	СпецификацииСостав.ТипСтрокиСостава КАК ТипСтрокиСостава,
	|	СпецификацииСостав.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА СпецификацииСостав.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	СпецификацииСостав.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СпецификацииСостав.Спецификация КАК Спецификация,
	|	ВременнаяТаблицаПродукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СУММА(СпецификацииСостав.Количество / СпецификацииСостав.КоличествоПродукции * ВременнаяТаблицаПродукция.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ВременнаяТаблицаПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Спецификации.Состав КАК СпецификацииСостав
	|		ПО ВременнаяТаблицаПродукция.Спецификация = СпецификацииСостав.Ссылка
	|ГДЕ
	|	СпецификацииСостав.Номенклатура.ТипНоменклатуры = &ТипНоменклатуры
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацииСостав.Номенклатура,
	|	СпецификацииСостав.Спецификация,
	|	СпецификацииСостав.ТипСтрокиСостава,
	|	ВременнаяТаблицаПродукция.ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА СпецификацииСостав.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	СпецификацииСостав.ЕдиницаИзмерения
	|
	|УПОРЯДОЧИТЬ ПО
	|	СпецификацииСоставНомерСтроки";
	
	ИспользоватьХарактеристики = Константы.ФункциональнаяОпцияИспользоватьХарактеристики.Получить(); 
	
	ВременнаяТаблицаПродукция = Продукция.Выгрузить();
	
	Для каждого СтрокаВТ Из ВременнаяТаблицаПродукция Цикл
		Если ТипЗнч(СтрокаВТ.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
			СтрокаВТ.Количество = СтрокаВТ.Количество * СтрокаВТ.ЕдиницаИзмерения.Коэффициент;
		КонецЕсли;	
	КонецЦикла;	
	
	Запрос.УстановитьПараметр("РаспределениеЗатратПродукция", ВременнаяТаблицаПродукция);
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики", 	ИспользоватьХарактеристики);
	
	Запрос.УстановитьПараметр("ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Запас);
	
	ТаблицаЗапасы = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТЗ Из ТаблицаЗапасы Цикл
		
		Если СтрокаТЗ.ТипСтрокиСостава = Перечисления.ТипыСтрокСоставаСпецификации.Узел Тогда
			
			ЗаполнитьТабличнуюЧастьПоСпецификации(СтрокаТЗ.Спецификация, СтрокаТЗ.Количество, 
				СтрокаТЗ.ЕдиницаИзмерения, СтрокаТЗ.ЗаказПокупателя);
									
		Иначе	
			
			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
						
		КонецЕсли;	
		
	КонецЦикла;	
	
	Запасы.Свернуть("Номенклатура, Характеристика, ЕдиницаИзмерения, ЗаказПокупателя, Спецификация", "Количество");
	
	КлючСвязи = 0;
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		СтрокаТабличнойЧасти.КлючСвязи = КлючСвязи;
		КлючСвязи = КлючСвязи + 1;
	КонецЦикла;	
	
КонецПроцедуры // ВыполнитьЗаполнениеЗапасовПоНормативам()

// Процедура выполняет заполнение запасов по нормативам.
//
Процедура ВыполнитьЗаполнениеЗапасовПоОстаткам() Экспорт
	
	Запасы.Очистить();
	РаспределениеЗапасов.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(ЗапасыОстатки.КоличествоОстаток) КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА ЗапасыОстатки.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|				ТОГДА ЗапасыОстатки.КоличествоОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Резерв,
	|	ЕСТЬNULL(ЗапасыОстатки.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	|	ЗапасыОстатки.Характеристика КАК Характеристика,
	|	ЗапасыОстатки.Партия КАК Партия,
	|	ЗапасыОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ЗапасыОстатки.Организация КАК Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
	|ИЗ
	|	РегистрНакопления.Запасы.Остатки(&ДатаОбработки, ЗаказПокупателя В (&ЗаказПокупателя)) КАК ЗапасыОстатки
	|ГДЕ
	|	ЗапасыОстатки.КоличествоОстаток > 0
	|	И ЗапасыОстатки.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ЗапасыОстатки.Организация = &Организация
	|	И ЗапасыОстатки.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыОстатки.Партия,
	|	ЗапасыОстатки.Номенклатура,
	|	ЗапасыОстатки.Характеристика,
	|	ЗапасыОстатки.Номенклатура.ЕдиницаИзмерения,
	|	ЗапасыОстатки.Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница,
	|	ЗапасыОстатки.ЗаказПокупателя";
	
	МассивЗаказов = Продукция.ВыгрузитьКолонку("ЗаказПокупателя");
	МассивЗаказов.Добавить(Документы.ЗаказПокупателя.ПустаяСсылка());
	Запрос.УстановитьПараметр("ЗаказПокупателя", МассивЗаказов);
	Запрос.УстановитьПараметр("ДатаОбработки", КонецДня(Дата));
	Запрос.УстановитьПараметр("Организация", Константы.УчетПоКомпании.Компания(Организация));
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	
	КлючСвязи = 0;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
        НоваяСтрока.КлючСвязи = КлючСвязи;
		
		КлючСвязи = КлючСвязи + 1;
		
	КонецЦикла;	
	
КонецПроцедуры // ВыполнитьЗаполнениеЗапасовПоОстаткам()

// Процедура выполняет распределение запасов по количеству.
//
Процедура ВыполнитьРаспределениеЗапасовПоНормативам() Экспорт
	
	ОписаниеТиповКоличество = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3,
		ДопустимыйЗнак.Неотрицательный));
	ОписаниеТиповДоля = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5, 3,
		ДопустимыйЗнак.Неотрицательный));
		
	РаспределениеЗапасов.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =	
	"ВЫБРАТЬ
	|	РаспределениеЗатратПродукция.Номенклатура КАК Продукция,
	|	РаспределениеЗатратПродукция.Характеристика КАК ХарактеристикаПродукции,
	|	РаспределениеЗатратПродукция.Партия КАК ПартияПродукция,
	|	РаспределениеЗатратПродукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	РаспределениеЗатратПродукция.Спецификация КАК Спецификация,
	|	РаспределениеЗатратПродукция.Количество КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблицаПродукция
	|ИЗ
	|	&РаспределениеЗатратПродукция КАК РаспределениеЗатратПродукция
	|ГДЕ
	|	РаспределениеЗатратПродукция.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаПродукция.Продукция КАК Продукция,
	|	ВременнаяТаблицаПродукция.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	ВременнаяТаблицаПродукция.ПартияПродукция КАК ПартияПродукция,
	|	ВременнаяТаблицаПродукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВременнаяТаблицаПродукция.Спецификация КАК СпецификацияПродукции,
	|	СпецификацииСостав.ТипСтрокиСостава КАК ТипСтрокиСостава,
	|	СпецификацииСостав.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА СпецификацииСостав.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	СпецификацииСостав.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СпецификацииСостав.Спецификация КАК Спецификация,
	|	СУММА(СпецификацииСостав.Количество / СпецификацииСостав.КоличествоПродукции * ВременнаяТаблицаПродукция.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ВременнаяТаблицаПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Спецификации.Состав КАК СпецификацииСостав
	|		ПО ВременнаяТаблицаПродукция.Спецификация = СпецификацииСостав.Ссылка
	|ГДЕ
	|	СпецификацииСостав.Номенклатура.ТипНоменклатуры = &ТипНоменклатуры
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаПродукция.Продукция,
	|	ВременнаяТаблицаПродукция.ХарактеристикаПродукции,
	|	ВременнаяТаблицаПродукция.ПартияПродукция,
	|	ВременнаяТаблицаПродукция.Спецификация,
	|	СпецификацииСостав.Номенклатура,
	|	СпецификацииСостав.Спецификация,
	|	СпецификацииСостав.ТипСтрокиСостава,
	|	ВременнаяТаблицаПродукция.ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА СпецификацииСостав.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	СпецификацииСостав.ЕдиницаИзмерения";
	
	ИспользоватьХарактеристики = Константы.ФункциональнаяОпцияИспользоватьХарактеристики.Получить(); 
	
	ВременнаяТаблицаПродукция = Продукция.Выгрузить();
	
	Для каждого СтрокаВТ Из ВременнаяТаблицаПродукция Цикл
		Если ТипЗнч(СтрокаВТ.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
			СтрокаВТ.Количество = СтрокаВТ.Количество * СтрокаВТ.ЕдиницаИзмерения.Коэффициент;
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("РаспределениеЗатратПродукция", ВременнаяТаблицаПродукция);
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики", 	ИспользоватьХарактеристики);
	
	Запрос.УстановитьПараметр("ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Запас);
	
	ВременнаяТаблицаРаспределение = Запрос.Выполнить().Выгрузить();
	
	Для н = 0 По ВременнаяТаблицаРаспределение.Количество() - 1 Цикл
		
		СтрокаТЗ = ВременнаяТаблицаРаспределение[н];
		
		Если СтрокаТЗ.ТипСтрокиСостава = Перечисления.ТипыСтрокСоставаСпецификации.Узел Тогда
			
			РаспределитьТабличнуюЧастьПоСпецификации(СтрокаТЗ, ВременнаяТаблицаРаспределение, 
				СтрокаТЗ.СпецификацияПродукции);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаЗапасыПоЗаказамНормативы = ВременнаяТаблицаРаспределение.Скопировать( ,
		"Номенклатура, Характеристика, ЕдиницаИзмерения, ЗаказПокупателя, Количество");
	ТаблицаЗапасыПоЗаказамНормативы.Свернуть("Номенклатура, Характеристика, ЕдиницаИзмерения, ЗаказПокупателя", 
		"Количество");
	ТаблицаЗапасыНормативы = ТаблицаЗапасыПоЗаказамНормативы.Скопировать( ,
		"Номенклатура, Характеристика, ЕдиницаИзмерения, Количество");
	ТаблицаЗапасыНормативы.Свернуть("Номенклатура, Характеристика, ЕдиницаИзмерения", 
		"Количество");
		
    // Получим процент каждой строки запасов в общем количестве по нормативам
	ВременнаяТаблицаРаспределение.Колонки.Добавить("ДоляВОбщемКоличестве", ОписаниеТиповДоля);
	ВременнаяТаблицаРаспределение.Колонки.Добавить("ВсегоКоличество", ОписаниеТиповКоличество);
	ВременнаяТаблицаРаспределение.Колонки.Добавить("ДоляВКоличествеПоЗаказу", ОписаниеТиповДоля);
	ВременнаяТаблицаРаспределение.Колонки.Добавить("КоличествоПоЗаказу", ОписаниеТиповКоличество);
	Для каждого СтрокаТабличнойЧасти Из ТаблицаЗапасыПоЗаказамНормативы Цикл
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Номенклатура", 		СтрокаТабличнойЧасти.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", 		СтрокаТабличнойЧасти.Характеристика);
		СтруктураПоиска.Вставить("ЕдиницаИзмерения", 	СтрокаТабличнойЧасти.ЕдиницаИзмерения);
		СтруктураПоиска.Вставить("ЗаказПокупателя", 	СтрокаТабличнойЧасти.ЗаказПокупателя);
		МассивПоиска = ВременнаяТаблицаРаспределение.НайтиСтроки(СтруктураПоиска);
		Для каждого СтрокаМассива Из МассивПоиска Цикл
			СтрокаМассива.КоличествоПоЗаказу = СтрокаТабличнойЧасти.Количество;
			СтрокаМассива.ДоляВКоличествеПоЗаказу = СтрокаМассива.Количество / СтрокаТабличнойЧасти.Количество;
		КонецЦикла;
	КонецЦикла;
	Для каждого СтрокаТабличнойЧасти Из ТаблицаЗапасыНормативы Цикл
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Номенклатура", 		СтрокаТабличнойЧасти.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", 		СтрокаТабличнойЧасти.Характеристика);
		СтруктураПоиска.Вставить("ЕдиницаИзмерения", 	СтрокаТабличнойЧасти.ЕдиницаИзмерения);
		МассивПоиска = ВременнаяТаблицаРаспределение.НайтиСтроки(СтруктураПоиска);
		Для каждого СтрокаМассива Из МассивПоиска Цикл
			СтрокаМассива.ВсегоКоличество = СтрокаТабличнойЧасти.Количество;
			СтрокаМассива.ДоляВОбщемКоличестве = СтрокаМассива.Количество / СтрокаТабличнойЧасти.Количество;
		КонецЦикла;
	КонецЦикла;

    // Получим процент каждой строки продукции в общем количестве продукции
	ВременнаяТаблицаРаспределениеПоПродукции = ВременнаяТаблицаПродукция.Скопировать();
	ВременнаяТаблицаРаспределениеПоПродукции.Колонки.Добавить("ДоляВОбщемКоличестве", ОписаниеТиповДоля);
	ВременнаяТаблицаРаспределениеПоПродукции.Колонки.Добавить("ВсегоКоличество", ОписаниеТиповКоличество);
	Для каждого СтрокаТабличнойЧасти Из ВременнаяТаблицаРаспределениеПоПродукции Цикл
		СтрокаТабличнойЧасти.ВсегоКоличество = ВременнаяТаблицаРаспределениеПоПродукции.Итог("Количество");
		СтрокаТабличнойЧасти.ДоляВОбщемКоличестве = СтрокаТабличнойЧасти.Количество
			/ СтрокаТабличнойЧасти.ВсегоКоличество;
	КонецЦикла;
	
	// Распределим фактическое количество запасов по продукции
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		
		КоличествоКРаспределению = СтрокаТабличнойЧасти.Количество;
		РезервКРаспределению = СтрокаТабличнойЧасти.Резерв;
		НоваяСтрока = Неопределено;
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Номенклатура", 		СтрокаТабличнойЧасти.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", 		СтрокаТабличнойЧасти.Характеристика);
		СтруктураПоиска.Вставить("ЕдиницаИзмерения", 	СтрокаТабличнойЧасти.ЕдиницаИзмерения);
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя) Тогда
			СтруктураПоиска.Вставить("ЗаказПокупателя", 	СтрокаТабличнойЧасти.ЗаказПокупателя);
		КонецЕсли;
		СтруктураПоиска.Вставить("Спецификация", 		СтрокаТабличнойЧасти.Спецификация);
		МассивПоиска = ВременнаяТаблицаРаспределение.НайтиСтроки(СтруктураПоиска);
		Если МассивПоиска.Количество() = 0 И СтруктураПоиска.Свойство("ЗаказПокупателя") Тогда
			СтруктураПоиска.Удалить("ЗаказПокупателя");
			МассивПоиска = ВременнаяТаблицаРаспределение.НайтиСтроки(СтруктураПоиска);
		КонецЕсли;
		Для каждого СтрокаМассива Из МассивПоиска Цикл
			
			Если КоличествоКРаспределению > 0 Тогда
				
				Если СтруктураПоиска.Свойство("ЗаказПокупателя") Тогда
					// Распределение по заказу покупателя
					КРаспределению = Окр(СтрокаТабличнойЧасти.Количество * СтрокаМассива.Количество
						/ СтрокаМассива.КоличествоПоЗаказу, 3);
				Иначе
					КРаспределению = Окр(СтрокаТабличнойЧасти.Количество * СтрокаМассива.Количество
						/ СтрокаМассива.ВсегоКоличество, 3);
				КонецЕсли;
				СтрокаКоличество = Мин(КРаспределению, КоличествоКРаспределению);
				КоличествоКРаспределению = КоличествоКРаспределению - СтрокаКоличество;
				
				СтрокаРезерв = Мин(КРаспределению, РезервКРаспределению);
				РезервКРаспределению = РезервКРаспределению - СтрокаРезерв;
				
				НоваяСтрока = РаспределениеЗапасов.Добавить();
				НоваяСтрока.Количество 		= СтрокаКоличество;
				НоваяСтрока.Резерв 			= СтрокаРезерв;
				НоваяСтрока.Номенклатура	= СтрокаМассива.Продукция;
				НоваяСтрока.Характеристика 	= СтрокаМассива.ХарактеристикаПродукции;
				НоваяСтрока.Партия 			= СтрокаМассива.ПартияПродукция;
				НоваяСтрока.ЗаказПокупателя	= СтрокаМассива.ЗаказПокупателя;
				НоваяСтрока.Спецификация	= СтрокаМассива.СпецификацияПродукции;
				НоваяСтрока.КлючСвязи		= СтрокаТабличнойЧасти.КлючСвязи;
				
			Иначе
				
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Если запасы не указаны в спецификации на продукцию, 
		// распределим пропорционально количеству продукции
		Если МассивПоиска.Количество() = 0 Тогда
			
			ВсегоКоличествоПродукции = ВременнаяТаблицаПродукция.Итог("Количество");
			
			Для каждого СтрокаМассива Из ВременнаяТаблицаПродукция Цикл
				
				Если КоличествоКРаспределению > 0 Тогда
					
					СтрокаКоличество = Мин(Окр(СтрокаТабличнойЧасти.Количество * СтрокаМассива.Количество
						/ ВсегоКоличествоПродукции, 3), КоличествоКРаспределению);
					КоличествоКРаспределению = КоличествоКРаспределению - СтрокаКоличество;
					
					СтрокаРезерв = МИН(СтрокаКоличество, РезервКРаспределению);
					РезервКРаспределению = РезервКРаспределению - СтрокаРезерв;
					
					НоваяСтрока = РаспределениеЗапасов.Добавить();
					НоваяСтрока.Количество 		= СтрокаКоличество;
					НоваяСтрока.Резерв 			= СтрокаРезерв;
					НоваяСтрока.Номенклатура	= СтрокаМассива.Номенклатура;
					НоваяСтрока.Характеристика 	= СтрокаМассива.Характеристика;
					НоваяСтрока.Партия 			= СтрокаМассива.Партия;
					НоваяСтрока.ЗаказПокупателя	= СтрокаМассива.ЗаказПокупателя;
					НоваяСтрока.Спецификация	= СтрокаМассива.Спецификация;
					НоваяСтрока.КлючСвязи		= СтрокаТабличнойЧасти.КлючСвязи;
					
				Иначе
					
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
		
		КонецЕсли; 
		
		// Неточности округления
		Если КоличествоКРаспределению <> 0 И НоваяСтрока <> Неопределено Тогда
			НоваяСтрока.Количество = НоваяСтрока.Количество + КоличествоКРаспределению;
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры // ВыполнитьРаспределениеЗапасовПоНормативам()

// Процедура выполняет распределение запасов по количеству.
//
Процедура ВыполнитьРаспределениеЗапасовПоКоличеству() Экспорт
	
	РаспределениеЗапасов.Очистить();
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗапасы.КлючСвязи КАК КлючСвязи,
	|	ТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаЗапасы.Количество КАК Количество,
	|	ТаблицаЗапасы.Резерв КАК Резерв
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасы
	|ИЗ
	|	&ТаблицаЗапасы КАК ТаблицаЗапасы";
	
	Запрос.УстановитьПараметр("ТаблицаЗапасы", Запасы.Выгрузить());
	
	Запрос.Выполнить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПродукция.Номенклатура КАК Номенклатура,
	|	ТаблицаПродукция.Характеристика КАК Характеристика,
	|	ТаблицаПродукция.Партия КАК Партия,
	|	ТаблицаПродукция.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаПродукция.Спецификация КАК Спецификация,
	|	ТаблицаПродукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаПродукция.Количество КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблицаПродукция
	|ИЗ
	|	&ТаблицаПродукция КАК ТаблицаПродукция";
	
	Запрос.УстановитьПараметр("ТаблицаПродукция", Продукция.Выгрузить());
	
	Запрос.Выполнить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаПродукция.Номенклатура КАК Номенклатура,
	|	ВременнаяТаблицаПродукция.Характеристика КАК Характеристика,
	|	ВременнаяТаблицаПродукция.Партия КАК Партия,
	|	ВременнаяТаблицаПродукция.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВременнаяТаблицаПродукция.Спецификация КАК Спецификация,
	|	ВременнаяТаблицаПродукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВременнаяТаблицаЗапасы.КлючСвязи КАК КлючСвязи,
	|	ISNULL(ВременнаяТаблицаПродукция.Количество, 0) КАК Количество,
	|	ВременнаяТаблицаЗапасы.Количество КАК ВсегоКоличество,
	|	ВременнаяТаблицаЗапасы.Резерв КАК ВсегоРезерв
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ВременнаяТаблицаЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаПродукция КАК ВременнаяТаблицаПродукция
	|		ПО ВременнаяТаблицаЗапасы.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		ИЛИ ВременнаяТаблицаЗапасы.ЗаказПокупателя = ВременнаяТаблицаПродукция.ЗаказПокупателя
	|		ИЛИ НЕ ВременнаяТаблицаЗапасы.ЗаказПокупателя В
	|			(ВЫБРАТЬ
	|				ЗаказПокупателя
	|			ИЗ
	|				ВременнаяТаблицаПродукция)
	|ИТОГИ
	|	СУММА(Количество)
	|ПО
	|	КлючСвязи";
	
	ВыборкаКлючСвязи = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "КлючСвязи");
	Пока ВыборкаКлючСвязи.Следующий() Цикл
		
		ИсхКоличество = 0;
		ИсхРезерв = 0;
		БазаРаспределенияКоличество = ВыборкаКлючСвязи.Количество;
		БазаРаспределенияРезерв = ВыборкаКлючСвязи.Количество;
		ВыборкаДетализация = ВыборкаКлючСвязи.Выбрать();
		Пока ВыборкаДетализация.Следующий() Цикл
			
			НоваяСтрока = РаспределениеЗапасов.Добавить();
			НоваяСтрока.КлючСвязи = ВыборкаДетализация.КлючСвязи;
			НоваяСтрока.Номенклатура = ВыборкаДетализация.Номенклатура;
			НоваяСтрока.Характеристика = ВыборкаДетализация.Характеристика;
			НоваяСтрока.Партия = ВыборкаДетализация.Партия;
			НоваяСтрока.ЗаказПокупателя = ВыборкаДетализация.ЗаказПокупателя;
			НоваяСтрока.Спецификация = ВыборкаДетализация.Спецификация;
			
			Если БазаРаспределенияКоличество = 0 Тогда
				НоваяСтрока.Количество = 0;
			Иначе
				НоваяСтрока.Количество = Окр((ВыборкаДетализация.ВсегоКоличество - ИсхКоличество)
					* ВыборкаДетализация.Количество / БазаРаспределенияКоличество, 3, 1);
			КонецЕсли;
			БазаРаспределенияКоличество = БазаРаспределенияКоличество - ВыборкаДетализация.Количество;
			ИсхКоличество = ИсхКоличество + НоваяСтрока.Количество;
			
			Если БазаРаспределенияРезерв = 0 Тогда
				НоваяСтрока.Резерв = 0;
			Иначе
				НоваяСтрока.Резерв = Окр((ВыборкаДетализация.ВсегоРезерв - ИсхРезерв) * ВыборкаДетализация.Количество
					/ БазаРаспределенияРезерв, 3, 1);
			КонецЕсли;
			БазаРаспределенияРезерв = БазаРаспределенияРезерв - ВыборкаДетализация.Количество;
			ИсхРезерв = ИсхРезерв + НоваяСтрока.Резерв;
			
		КонецЦикла;
		
	КонецЦикла;
	
	МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ВыполнитьРаспределениеЗатратПоКоличеству()

// Процедура выполняет заполнение расходов по остаткам.
//
Процедура ВыполнитьЗаполнениеРасходовПоОстаткам() Экспорт
	
	Затраты.Очистить();
	РаспределениеЗатрат.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(ЗапасыОстатки.СуммаОстаток) КАК Сумма,
	|	ЗапасыОстатки.СчетУчета КАК СчетЗатрат,
	|	ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя
	|ИЗ
	|	РегистрНакопления.Запасы.Остатки(
	|			&ДатаОбработки,
	|			Организация = &Организация
	|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|				И Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				И СчетУчета.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.НезавершенноеПроизводство)
	|				И ЗаказПокупателя В (&ЗаказПокупателя)) КАК ЗапасыОстатки
	|ГДЕ
	|	ЗапасыОстатки.СуммаОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыОстатки.СчетУчета,
	|	ЗапасыОстатки.ЗаказПокупателя";
	
	МассивЗаказов = Продукция.ВыгрузитьКолонку("ЗаказПокупателя");
	МассивЗаказов.Добавить(Документы.ЗаказПокупателя.ПустаяСсылка());
	Запрос.УстановитьПараметр("ЗаказПокупателя", МассивЗаказов);
	Запрос.УстановитьПараметр("ДатаОбработки", КонецДня(Дата));
	Запрос.УстановитьПараметр("Организация", Константы.УчетПоКомпании.Компания(Организация));
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	
	КлючСвязи = 0;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Затраты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
        НоваяСтрока.КлючСвязи = КлючСвязи;
		
		КлючСвязи = КлючСвязи + 1;
		
	КонецЦикла;
	
КонецПроцедуры // ВыполнитьЗаполнениеРасходовПоОстаткам()

// Процедура выполняет распределение расходов по количеству.
//
Процедура ВыполнитьРаспределениеЗатратПоКоличеству() Экспорт
	
	РаспределениеЗатрат.Очистить();
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗатраты.КлючСвязи КАК КлючСвязи,
	|	ТаблицаЗатраты.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаЗатраты.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВременнаяТаблицаЗатраты
	|ИЗ
	|	&ТаблицаЗатраты КАК ТаблицаЗатраты";
	
	Запрос.УстановитьПараметр("ТаблицаЗатраты", Затраты.Выгрузить());
	Запрос.Выполнить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПродукция.Номенклатура КАК Номенклатура,
	|	ТаблицаПродукция.Характеристика КАК Характеристика,
	|	ТаблицаПродукция.Партия КАК Партия,
	|	ТаблицаПродукция.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаПродукция.Спецификация КАК Спецификация,
	|	ТаблицаПродукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаПродукция.Количество КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблицаПродукция
	|ИЗ
	|	&ТаблицаПродукция КАК ТаблицаПродукция";
	
	Запрос.УстановитьПараметр("ТаблицаПродукция", Продукция.Выгрузить());
	Запрос.Выполнить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаПродукция.Номенклатура КАК Номенклатура,
	|	ВременнаяТаблицаПродукция.Характеристика КАК Характеристика,
	|	ВременнаяТаблицаПродукция.Партия КАК Партия,
	|	ВременнаяТаблицаПродукция.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВременнаяТаблицаПродукция.Спецификация КАК Спецификация,
	|	ВременнаяТаблицаПродукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВременнаяТаблицаЗатраты.КлючСвязи КАК КлючСвязи,
	|	ВременнаяТаблицаПродукция.Количество КАК Количество,
	|	ВременнаяТаблицаЗатраты.Сумма КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаЗатраты КАК ВременнаяТаблицаЗатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаПродукция КАК ВременнаяТаблицаПродукция
	|		ПО ВременнаяТаблицаЗатраты.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		ИЛИ ВременнаяТаблицаЗатраты.ЗаказПокупателя = ВременнаяТаблицаПродукция.ЗаказПокупателя
	|		ИЛИ НЕ ВременнаяТаблицаЗатраты.ЗаказПокупателя В
	|			(ВЫБРАТЬ
	|				ЗаказПокупателя
	|			ИЗ
	|				ВременнаяТаблицаПродукция)
	|ИТОГИ
	|	СУММА(Количество)
	|ПО
	|	КлючСвязи";
	
	ВыборкаКлючСвязи = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "КлючСвязи");
	Пока ВыборкаКлючСвязи.Следующий() Цикл
		
		ИсхСумма = 0;
		БазаРаспределения = ВыборкаКлючСвязи.Количество;
		ВыборкаДетализация = ВыборкаКлючСвязи.Выбрать();
		Пока ВыборкаДетализация.Следующий() Цикл
			
			НоваяСтрока = РаспределениеЗатрат.Добавить();
			НоваяСтрока.КлючСвязи = ВыборкаДетализация.КлючСвязи;
			НоваяСтрока.Номенклатура = ВыборкаДетализация.Номенклатура;
			НоваяСтрока.Характеристика = ВыборкаДетализация.Характеристика;
			НоваяСтрока.Партия = ВыборкаДетализация.Партия;
			НоваяСтрока.ЗаказПокупателя = ВыборкаДетализация.ЗаказПокупателя;
			НоваяСтрока.Спецификация = ВыборкаДетализация.Спецификация;
			
			Если БазаРаспределения = 0 Тогда
				НоваяСтрока.Сумма = 0;
			Иначе
				НоваяСтрока.Сумма = Окр((ВыборкаДетализация.Сумма - ИсхСумма) * ВыборкаДетализация.Количество
					/ БазаРаспределения, 2, 1);
			КонецЕсли;
			БазаРаспределения = БазаРаспределения - ВыборкаДетализация.Количество;
			ИсхСумма = ИсхСумма + НоваяСтрока.Сумма;
			
		КонецЦикла;
		
	КонецЦикла;
	
	МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ВыполнитьРаспределениеЗатратПоКоличеству()

// Процедура выполняет заполнение продукции по выпуску.
//
Процедура ВыполнитьЗаполнениеПродукцииПоВыпуску() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыпускПродукцииОбороты.Номенклатура КАК Номенклатура,
	|	ВыпускПродукцииОбороты.Характеристика КАК Характеристика,
	|	ВыпускПродукцииОбороты.Партия КАК Партия,
	|	ВыпускПродукцииОбороты.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВыпускПродукцииОбороты.Спецификация КАК Спецификация,
	|	ВЫБОР
	|		КОГДА &РезервированиеЗапасов
	|			ТОГДА ВыпускПродукцииОбороты.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	СУММА(ВыпускПродукцииОбороты.КоличествоОборот) КАК Количество
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции.Обороты(
	|			&ДатаНачала,
	|			&ДатаОкончания,
	|			,
	|			Организация = &Организация
	|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|				И Номенклатура.ТипНоменклатуры <> &ТипНоменклатурыУслуга) КАК ВыпускПродукцииОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыпускПродукцииОбороты.Характеристика,
	|	ВыпускПродукцииОбороты.Спецификация,
	|	ВыпускПродукцииОбороты.Партия,
	|	ВыпускПродукцииОбороты.Номенклатура,
	|	ВыпускПродукцииОбороты.ЗаказПокупателя,
	|	ВыпускПродукцииОбороты.Номенклатура.ЕдиницаИзмерения";
	
	Запрос.УстановитьПараметр("РезервированиеЗапасов", ПолучитьФункциональнуюОпцию("РезервированиеЗапасов"));
	Запрос.УстановитьПараметр("Организация", Константы.УчетПоКомпании.Компания(Организация));
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	Запрос.УстановитьПараметр("ТипНоменклатурыУслуга", Перечисления.ТипыНоменклатуры.Услуга);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачалаПериода);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(Дата));
	
	Продукция.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры // ВыполнитьЗаполнениеПродукцииПоВыпуску()	

// Обработчик заполнения на основании документа СборкаЗапасов.
//
// Параметры:
//  ДокументСсылкаСборкаЗапасов	 - ДокументСсылка.СборкаЗапасов.
//
Процедура ЗаполнитьПоСборкеЗапасов(ДокументСсылкаСборкаЗапасов) Экспорт
	
	Продукция.Очистить();
	Запасы.Очистить();
	РаспределениеЗапасов.Очистить();
	РаспределениеЗатрат.Очистить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СборкаЗапасов.Ссылка КАК ДокументОснование,
	|	СборкаЗапасов.ВидОперации КАК ВидОперации,
	|	СборкаЗапасов.Организация КАК Организация,
	|	СборкаЗапасов.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	СборкаЗапасов.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
	|	СборкаЗапасов.Ячейка КАК Ячейка,
	|	СборкаЗапасов.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СборкаЗапасов.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	СборкаЗапасов.Продукция.(
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Партия КАК Партия,
	|		Количество КАК Количество,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Спецификация КАК Спецификация
	|	) КАК Продукция,
	|	СборкаЗапасов.Запасы.(
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Партия КАК Партия,
	|		Количество КАК Количество,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Спецификация КАК Спецификация
	|	) КАК Запасы
	|ИЗ
	|	Документ.СборкаЗапасов КАК СборкаЗапасов
	|ГДЕ
	|	СборкаЗапасов.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаСборкаЗапасов);
	
	ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	Если ВыборкаИзРезультатаЗапроса.Следующий() Тогда
		
		Если ВыборкаИзРезультатаЗапроса.ВидОперации = Перечисления.ВидыОперацийСборкаЗапасов.Разборка Тогда
			ИмяТабличнойЧасти = "Запасы";
		Иначе
			ИмяТабличнойЧасти = "Продукция";
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаИзРезультатаЗапроса);
		
		Если ВыборкаИзРезультатаЗапроса.ТипСтруктурнойЕдиницы<>Перечисления.ТипыСтруктурныхЕдиниц.Подразделение Тогда
			Если ПолучитьФункциональнуюОпцию("УчетПоНесколькимПодразделениям") Тогда
				СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
			Иначе
				СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение;
			КонецЕсли; 
		КонецЕсли; 
		
		ВыборкаПродукция = ВыборкаИзРезультатаЗапроса[ИмяТабличнойЧасти].Выбрать();
		Пока ВыборкаПродукция.Следующий() Цикл
			НоваяСтрока = Продукция.Добавить();
			НоваяСтрока.Номенклатура 		= ВыборкаПродукция.Номенклатура;
			НоваяСтрока.Характеристика 		= ВыборкаПродукция.Характеристика;
			НоваяСтрока.Партия 				= ВыборкаПродукция.Партия;
			НоваяСтрока.ЕдиницаИзмерения 	= ВыборкаПродукция.ЕдиницаИзмерения;
			НоваяСтрока.Количество 			= ВыборкаПродукция.Количество;
			НоваяСтрока.Спецификация 		= ВыборкаПродукция.Спецификация;
			НоваяСтрока.ЗаказПокупателя		= ВыборкаИзРезультатаЗапроса.ЗаказПокупателя;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик заполнения на основании документа ОтчетПереработчика.
//
// Параметры:
//  ДокументСсылкаОтчетПереработчика	 - ДокументСсылка.ОтчетПереработчика.
//
Процедура ЗаполнитьПоОтчетуПереработчика(ДокументСсылкаОтчетПереработчика) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОтчетПереработчика.Ссылка КАК ДокументОснование,
	|	ОтчетПереработчика.Организация КАК Организация,
	|	ОтчетПереработчика.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ОтчетПереработчика.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
	|	ОтчетПереработчика.Ячейка КАК Ячейка,
	|	ОтчетПереработчика.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ОтчетПереработчика.Продукция.(
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Партия КАК Партия,
	|		Количество КАК Количество,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Спецификация КАК Спецификация) КАК Продукция
	|ИЗ
	|	Документ.ОтчетПереработчика КАК ОтчетПереработчика
	|ГДЕ
	|	ОтчетПереработчика.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	
	ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	Если ВыборкаИзРезультатаЗапроса.Следующий() Тогда
		
		Если ВыборкаИзРезультатаЗапроса.ТипСтруктурнойЕдиницы <> Перечисления.ТипыСтруктурныхЕдиниц.Подразделение Тогда
			ТекстОшибки = НСтр("ru='Распределение затрат доступно только на подразделении. Тип структурной единицы в документе %1 - %2.'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, ДокументСсылкаОтчетПереработчика,
				ВыборкаИзРезультатаЗапроса.ТипСтруктурнойЕдиницы);
			ВызватьИсключение ТекстОшибки;
		КонецЕсли; 
		
		Продукция.Очистить();
		Запасы.Очистить();
		РаспределениеЗапасов.Очистить();
		РаспределениеЗатрат.Очистить();
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаИзРезультатаЗапроса);
		
		Если НЕ ПолучитьФункциональнуюОпцию("УчетПоНесколькимПодразделениям") Тогда
			СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение;
		КонецЕсли; 
		
		ВыборкаПродукция = ВыборкаИзРезультатаЗапроса.Продукция.Выбрать();
		Пока ВыборкаПродукция.Следующий() Цикл
			НоваяСтрока = Продукция.Добавить();
			НоваяСтрока.Номенклатура 		= ВыборкаПродукция.Номенклатура;
			НоваяСтрока.Характеристика 		= ВыборкаПродукция.Характеристика;
			НоваяСтрока.Партия 				= ВыборкаПродукция.Партия;
			НоваяСтрока.ЕдиницаИзмерения 	= ВыборкаПродукция.ЕдиницаИзмерения;
			НоваяСтрока.Количество 			= ВыборкаПродукция.Количество;
			НоваяСтрока.Спецификация 		= ВыборкаПродукция.Спецификация;
			НоваяСтрока.ЗаказПокупателя		= ВыборкаИзРезультатаЗапроса.ЗаказПокупателя;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик заполнения на основании документа ЗаказНаПроизводство.
//
// Параметры:
//  ДокументСсылкаЗаказНаПроизводство	 - ДокументСсылка.ЗаказНаПроизводство.
//
Процедура ЗаполнитьПоЗаказуНаПроизводство(ДокументСсылкаЗаказНаПроизводство) Экспорт
	
	Продукция.Очистить();
	Запасы.Очистить();
	РаспределениеЗапасов.Очистить();
	РаспределениеЗатрат.Очистить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗаказНаПроизводство.Ссылка КАК ДокументОснование,
	|	ЗаказНаПроизводство.Организация КАК Организация,
	|	ЗаказНаПроизводство.ВидОперации КАК ВидОперации,
	|	ЗаказНаПроизводство.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗаказНаПроизводство.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
	|	ЗаказНаПроизводство.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ЗаказНаПроизводство.Ссылка КАК ЗаказНаПроизводство,
	|	ЗаказНаПроизводство.Продукция.(
	|		Номенклатура КАК Номенклатура,
	|		Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|		Характеристика КАК Характеристика,
	|		Количество КАК Количество,
	|		Резерв КАК Резерв,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Спецификация КАК Спецификация,
	|		Партия КАК Партия
	|	),
	|	ЗаказНаПроизводство.Запасы.(
	|		Номенклатура КАК Номенклатура,
	|		Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|		Характеристика КАК Характеристика,
	|		Количество КАК Количество,
	|		Резерв КАК Резерв,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Спецификация КАК Спецификация,
	|		Партия КАК Партия
	|	)
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|ГДЕ
	|	ЗаказНаПроизводство.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗаказНаПроизводство);
	Запрос.УстановитьПараметр("ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Услуга);
	
	ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	Если ВыборкаИзРезультатаЗапроса.Следующий() Тогда
		
		Если ВыборкаИзРезультатаЗапроса.ВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка Тогда
			ИмяТЧПродукция = "Запасы";
			ИмяТЧЗапасы = "Продукция";
		Иначе
			ИмяТЧПродукция = "Продукция";
			ИмяТЧЗапасы = "Запасы";
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаИзРезультатаЗапроса);
		
		Если ВыборкаИзРезультатаЗапроса.ТипСтруктурнойЕдиницы <> Перечисления.ТипыСтруктурныхЕдиниц.Подразделение Тогда
			Если ПолучитьФункциональнуюОпцию("УчетПоНесколькимПодразделениям") Тогда
				СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
			Иначе
				СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение;
			КонецЕсли; 
		КонецЕсли;
		
		ВыборкаПродукция = ВыборкаИзРезультатаЗапроса[ИмяТЧПродукция].Выбрать();
		Пока ВыборкаПродукция.Следующий() Цикл
			Если ВыборкаПродукция.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = Продукция.Добавить();
			НоваяСтрока.Номенклатура 		= ВыборкаПродукция.Номенклатура;
			НоваяСтрока.Характеристика 		= ВыборкаПродукция.Характеристика;
			НоваяСтрока.Партия 				= ВыборкаПродукция.Партия;
			НоваяСтрока.ЕдиницаИзмерения 	= ВыборкаПродукция.ЕдиницаИзмерения;
			НоваяСтрока.Количество 			= ВыборкаПродукция.Количество;
			НоваяСтрока.Спецификация 		= ВыборкаПродукция.Спецификация;
			НоваяСтрока.ЗаказПокупателя		= ВыборкаИзРезультатаЗапроса.ЗаказПокупателя;
		КонецЦикла;
		
		КлючСвязи = 0;
		ВыборкаЗапасы = ВыборкаИзРезультатаЗапроса[ИмяТЧЗапасы].Выбрать();
		Пока ВыборкаЗапасы.Следующий() Цикл
			Если ВыборкаПродукция.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Запас Тогда
				НоваяСтрока = Запасы.Добавить();
				НоваяСтрока.Номенклатура 		= ВыборкаЗапасы.Номенклатура;
				НоваяСтрока.Характеристика 		= ВыборкаЗапасы.Характеристика;
				НоваяСтрока.Партия 				= ВыборкаЗапасы.Партия;
				НоваяСтрока.ЕдиницаИзмерения 	= ВыборкаЗапасы.ЕдиницаИзмерения;
				НоваяСтрока.Количество 			= ВыборкаЗапасы.Количество;
				НоваяСтрока.Резерв 				= ВыборкаЗапасы.Резерв;
				НоваяСтрока.Спецификация 		= ВыборкаЗапасы.Спецификация;
				НоваяСтрока.ЗаказПокупателя		= ВыборкаИзРезультатаЗапроса.ЗаказПокупателя;
				НоваяСтрока.КлючСвязи			= КлючСвязи;
				КлючСвязи = КлючСвязи + 1;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик заполнения на основании документа ЗаказПокупателя.
//
// Параметры:
//  ДокументСсылкаЗаказПокупателя	 - ДокументСсылка.ЗаказПокупателя.
//
Процедура ЗаполнитьПоЗаказуПокупателя(ДокументСсылкаЗаказПокупателя) Экспорт
	
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылкаЗаказПокупателя, "ВидОперации, СостояниеЗаказа, Проведен");
	
	Если РеквизитыОснования.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаПродажу Тогда
		ЗаполнитьПоЗаказуПокупателяНаПродажу(ДокументСсылкаЗаказПокупателя);
	КонецЕсли;
	
	Если РеквизитыОснования.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаПереработку Тогда
		ЗаполнитьПоЗаказуПокупателяНаПереработку(ДокументСсылкаЗаказПокупателя);
	КонецЕсли;
	
	Если РеквизитыОснования.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд Тогда
		Если НЕ РеквизитыОснования.Проведен Тогда
			ВызватьИсключение НСтр("ru = 'Нельзя ввести распределение затрат на основании непроведенного заказ-наряда.'");
		КонецЕсли;
		Если РеквизитыОснования.СостояниеЗаказа <> Справочники.СостоянияЗаказНарядов.Завершен Тогда
			ВызватьИсключение НСтр("ru = 'Нельзя ввести распределение затрат на основании незавершенного заказ-наряда.'");
		КонецЕсли;
		ЗаполнитьПоЗаказНаряду(ДокументСсылкаЗаказПокупателя);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	// Прослеживаемость
	СведенияПрослеживаемости.Очистить();
	// Конец Прослеживаемость
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("ДокументСсылка.СборкаЗапасов")] = "ЗаполнитьПоСборкеЗапасов";
	СтратегияЗаполнения[Тип("ДокументСсылка.ЗаказНаПроизводство")] = "ЗаполнитьПоЗаказуНаПроизводство";
	СтратегияЗаполнения[Тип("ДокументСсылка.ЗаказПокупателя")] = "ЗаполнитьПоЗаказуПокупателя";
	СтратегияЗаполнения[Тип("ДокументСсылка.ОтчетПереработчика")] = "ЗаполнитьПоОтчетуПереработчика";
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения, ?(ЗначениеЗаполнено(ДанныеЗаполнения), "СтруктурнаяЕдиница", ""));
	
	// Прослеживаемость
	ПрослеживаемостьУНФ.ОбновитьПризнакПрослеживаемости(Запасы, Дата);
	
КонецПроцедуры // ОбработкаЗаполнения()

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Запасы.Итог("Количество") <> РаспределениеЗапасов.Итог("Количество") Тогда

		ТекстСообщения = НСтр("ru = 'Количество запасов не соответствует количеству распределения.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Запасы", , Отказ);
		
	КонецЕсли;
	
	Если Затраты.Итог("Сумма") <> РаспределениеЗатрат.Итог("Сумма") Тогда

		ТекстСообщения = НСтр("ru = 'Сумма расходов не соответствует сумме распределения.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Затраты", , Отказ);
		
	КонецЕсли;

	Если ПолучитьФункциональнуюОпцию("РезервированиеЗапасов") Тогда
		
		Для каждого СтрокаЗапасы Из Запасы Цикл
			
			Если СтрокаЗапасы.Резерв > СтрокаЗапасы.Количество Тогда
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'В строке №%1 табл. части ""Запасы"" количество позиций к списанию из резерва превышает общее количество запасов.'"),
					СтрокаЗапасы.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки,
					"Резерв");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
			КонецЕсли;
			
		КонецЦикла;
		
		Для каждого СтрокаРаспределениеЗапасов Из РаспределениеЗапасов Цикл
			
			Если СтрокаРаспределениеЗапасов.Резерв > СтрокаРаспределениеЗапасов.Количество Тогда
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'В строке №%1 табл. части ""Распределение запасов"" количество позиций к списанию из резерва превышает общее количество запасов.'"),
					СтрокаРаспределениеЗапасов.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("РаспределениеЗапасов",
					СтрокаРаспределениеЗапасов.НомерСтроки, "Резерв");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	НоменклатураВДокументахСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, Отказ, Истина);
	
	// Прослеживаемость
	ВестиУчетПрослеживаемыхТоваров = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеКонстанты("ВестиУчетПрослеживаемыхТоваров");
	МассивСтрокПрослеживаемыхТоваров = Запасы.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ЕстьПрослеживаемыйТовар = МассивСтрокПрослеживаемыхТоваров.Количество() > 0;
	Если ЕстьПрослеживаемыйТовар И Не ЗначениеЗаполнено(КодОперацииПрослеживаемости)
		И ВестиУчетПрослеживаемыхТоваров Тогда
		ТекстОшибки = НСтр("ru='Не выбрана причина списания в прослеживаемости'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка, "КодОперацииПрослеживаемости", , Отказ);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.РаспределениеЗатрат.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ПроведениеДокументовУНФ.ОтразитьДвижения("Запасы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыНаСкладах", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыВРазрезеГТД", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыИАгентскиеУслугиПринятые", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ПотребностьВЗапасах", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьУправленческий(ДополнительныеСвойства, Движения, Отказ);
	
	// СерииНоменклатуры
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатурыГарантии", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДвиженияСерииНоменклатуры", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатуры", ТаблицыДляДвижений, Движения, Отказ);
	
	// Прослеживаемость
	УправлениеНебольшойФирмойСервер.ОтразитьПрослеживаемыеТовары(ДополнительныеСвойства, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ОперацииСПрослеживаемымиТоварами", ТаблицыДляДвижений, Движения, Отказ);
	
	// Запись наборов записей.
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль
	Документы.РаспределениеЗатрат.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для удаления проведения документа.
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей.
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль
	Документы.РаспределениеЗатрат.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ, Истина);
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// Прослеживаемость
	ПрослеживаемыйТовар = Запасы.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ЕстьПрослеживаемыйТовар = ПрослеживаемыйТовар.Количество() <> 0;
	Если ЕстьПрослеживаемыйТовар Тогда
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			ПрослеживаемостьУНФ.ПодобратьРНПТОстатки(ЭтотОбъект, Отказ);
		КонецЕсли; 
	Иначе
		СведенияПрослеживаемости.Очистить();
	КонецЕсли;
	
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		Если СтрокаТабличнойЧасти.ПрослеживаемыйТовар Тогда
			СтрокаТабличнойЧасти.НомерГТД = Ложь;
		КонецЕсли;
	КонецЦикла;
	// Конец Прослеживаемость
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	// Прослеживаемость
	СведенияПрослеживаемости.Очистить();
	ПрослеживаемостьУНФ.ОбновитьПризнакПрослеживаемости(Запасы, Дата);
	// Конец Прослеживаемость
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура РаспределитьТабличнуюЧастьПоСпецификации(ПоСтроке, ВременнаяТаблицаРаспределение, СпецификацияПродукции)
    
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МАКСИМУМ(СпецификацииСостав.НомерСтроки) КАК СпецификацииСоставНомерСтроки,
	|	СпецификацииСостав.Номенклатура КАК Номенклатура,
	|	СпецификацииСостав.ТипСтрокиСостава КАК ТипСтрокиСостава,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА СпецификацииСостав.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	СпецификацииСостав.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СпецификацииСостав.Спецификация КАК Спецификация,
	|	СУММА(ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(&ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|				ТОГДА СпецификацииСостав.Количество / СпецификацииСостав.КоличествоПродукции * &Количество
	|			ИНАЧЕ СпецификацииСостав.Количество / СпецификацииСостав.КоличествоПродукции * &Коэффициент * &Количество
	|		КОНЕЦ) КАК Количество,
	|	&Продукция КАК Продукция,
	|	&ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	&ПартияПродукция КАК ПартияПродукция,
	|	&ЗаказПокупателя КАК ЗаказПокупателя
	|ИЗ
	|	Справочник.Спецификации.Состав КАК СпецификацииСостав
	|ГДЕ
	|	СпецификацииСостав.Ссылка = &Спецификация
	|	И СпецификацииСостав.Номенклатура.ТипНоменклатуры = &ТипНоменклатуры
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацииСостав.Номенклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА СпецификацииСостав.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	СпецификацииСостав.ЕдиницаИзмерения,
	|	СпецификацииСостав.Спецификация,
	|	СпецификацииСостав.ТипСтрокиСостава,
	|	СпецификацииСостав.ДоляСтоимости
	|
	|УПОРЯДОЧИТЬ ПО
	|	СпецификацииСоставНомерСтроки");
	
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики",
		Константы.ФункциональнаяОпцияИспользоватьХарактеристики.Получить());
	Запрос.УстановитьПараметр("ДатаОбработки", КонецДня(Дата));
	
	Запрос.УстановитьПараметр("Продукция", 					ПоСтроке.Продукция);
	Запрос.УстановитьПараметр("ХарактеристикаПродукции", 	ПоСтроке.ХарактеристикаПродукции);
	Запрос.УстановитьПараметр("ПартияПродукция", 			ПоСтроке.ПартияПродукция);
	Запрос.УстановитьПараметр("ЗаказПокупателя", 			ПоСтроке.ЗаказПокупателя);
	    	
	Запрос.УстановитьПараметр("Спецификация", ПоСтроке.Спецификация);
	Запрос.УстановитьПараметр("Количество", ПоСтроке.Количество);
	
	Запрос.УстановитьПараметр("ЕдиницаИзмерения", ПоСтроке.ЕдиницаИзмерения);
	
	Если ТипЗнч(ПоСтроке.ЕдиницаИзмерения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
    	Запрос.УстановитьПараметр("Коэффициент", 1);
	Иначе
		Запрос.УстановитьПараметр("Коэффициент", ПоСтроке.ЕдиницаИзмерения.Коэффициент);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Запас);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ТипСтрокиСостава = Перечисления.ТипыСтрокСоставаСпецификации.Узел Тогда
			
			РаспределитьТабличнуюЧастьПоСпецификации(Выборка, ВременнаяТаблицаРаспределение, СпецификацияПродукции);
			
		Иначе
			
	    	НоваяСтрока = ВременнаяТаблицаРаспределение.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.СпецификацияПродукции = СпецификацияПродукции;
			
		КонецЕсли;
		
   КонецЦикла;

КонецПроцедуры // РаспределитьТабличнуюЧастьПоСпецификации()

Процедура ЗаполнитьПоЗаказуПокупателяНаПродажу(ДокументСсылкаЗаказПокупателя)
	
	Продукция.Очистить();
	Запасы.Очистить();
	РаспределениеЗапасов.Очистить();
	РаспределениеЗатрат.Очистить();
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказПокупателя.Ссылка КАК ДокументОснование,
	|	ЗаказПокупателя.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.СтруктурнаяЕдиницаПродажи.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗаказПокупателя.СтруктурнаяЕдиницаПродажи
	|		КОГДА &УчетПоНесколькимПодразделениям
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ОсновноеПодразделение)
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ЗаказПокупателя.ОжидаетсяВыборВариантаКП КАК ОжидаетсяВыборВариантаКП
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказПокупателяЗапасы.Ссылка КАК ЗаказПокупателя,
	|	ЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ЗаказПокупателяЗапасы.Характеристика КАК Характеристика,
	|	ЗаказПокупателяЗапасы.Количество КАК Количество,
	|	ЗаказПокупателяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПокупателяЗапасы.Спецификация КАК Спецификация
	|ИЗ
	|	ВТЗаказПокупателяЗапасы КАК ЗаказПокупателяЗапасы";
	
	Документы.ЗаказПокупателя.ДобавитьТаблицуЗапасыВМенеджерВременныхТаблиц(ДокументСсылкаЗаказПокупателя, Запрос.МенеджерВременныхТаблиц, Истина);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗаказПокупателя);
	Запрос.УстановитьПараметр("УчетПоНесколькимПодразделениям", ПолучитьФункциональнуюОпцию("УчетПоНесколькимПодразделениям"));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Если РезультатЗапроса[0].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаШапка = РезультатЗапроса[0].Выбрать();
	ВыборкаЗапасы = РезультатЗапроса[1].Выбрать();
	
	ВыборкаШапка.Следующий();
	
	Документы.ЗаказПокупателя.ПроверитьВозможностьВводаНаОснованииЗаказаПокупателя(
		ДокументСсылкаЗаказПокупателя, Новый Структура("ОжидаетсяВыборВариантаКП", ВыборкаШапка.ОжидаетсяВыборВариантаКП));
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	
	Пока ВыборкаЗапасы.Следующий() Цикл
		
		Если ВыборкаЗапасы.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Услуга Тогда
			
			НоваяСтрока = Продукция.Добавить();
			НоваяСтрока.Номенклатура 		= ВыборкаЗапасы.Номенклатура;
			НоваяСтрока.Характеристика 		= ВыборкаЗапасы.Характеристика;
			НоваяСтрока.ЕдиницаИзмерения 	= ВыборкаЗапасы.ЕдиницаИзмерения;
			НоваяСтрока.Количество 			= ВыборкаЗапасы.Количество;
			НоваяСтрока.Спецификация		= ВыборкаЗапасы.Спецификация;
			НоваяСтрока.ЗаказПокупателя		= ВыборкаЗапасы.ЗаказПокупателя;
			
			ЗаполнитьТабличнуюЧастьПоСпецификации(ВыборкаЗапасы.Спецификация, ВыборкаЗапасы.Количество, ВыборкаЗапасы.ЕдиницаИзмерения, ВыборкаЗапасы.ЗаказПокупателя);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запасы.Свернуть("Номенклатура, Характеристика, ЕдиницаИзмерения, ЗаказПокупателя, Спецификация", "Количество");
	
	КлючСвязи = 0;
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		СтрокаТабличнойЧасти.КлючСвязи = КлючСвязи;
		КлючСвязи = КлючСвязи + 1;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуПокупателяНаПереработку(ДокументСсылкаЗаказПокупателя)
	
	Продукция.Очистить();
	Запасы.Очистить();
	РаспределениеЗапасов.Очистить();
	РаспределениеЗатрат.Очистить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗаказПокупателя.Ссылка КАК ДокументОснование,
	|	ЗаказПокупателя.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.СтруктурнаяЕдиницаПродажи.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗаказПокупателя.СтруктурнаяЕдиницаПродажи
	|		КОГДА &УчетПоНесколькимПодразделениям
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ОсновноеПодразделение)
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ЗаказПокупателя.Запасы.(
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Количество КАК Количество,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Спецификация КАК Спецификация,
	|		Ссылка КАК ЗаказПокупателя,
	|		Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры
	|	) КАК Запасы,
	|	ЗаказПокупателя.МатериалыЗаказчика.(
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Количество КАК Количество,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Ссылка КАК ЗаказПокупателя
	|	) КАК МатериалыЗаказчика
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗаказПокупателя);
	Запрос.УстановитьПараметр("УчетПоНесколькимПодразделениям", ПолучитьФункциональнуюОпцию("УчетПоНесколькимПодразделениям"));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаИзРезультатаЗапроса);
	
	ВыборкаМатериалы = ВыборкаИзРезультатаЗапроса.МатериалыЗаказчика.Выбрать();
	
	ВыборкаЗапасы = ВыборкаИзРезультатаЗапроса.Запасы.Выбрать();
	Пока ВыборкаЗапасы.Следующий() Цикл
		
		Если ВыборкаЗапасы.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Услуга Тогда
			
			НоваяСтрока = Продукция.Добавить();
			НоваяСтрока.Номенклатура 		= ВыборкаЗапасы.Номенклатура;
			НоваяСтрока.Характеристика 		= ВыборкаЗапасы.Характеристика;
			НоваяСтрока.ЕдиницаИзмерения 	= ВыборкаЗапасы.ЕдиницаИзмерения;
			НоваяСтрока.Количество 			= ВыборкаЗапасы.Количество;
			НоваяСтрока.Спецификация		= ВыборкаЗапасы.Спецификация;
			НоваяСтрока.ЗаказПокупателя		= ВыборкаЗапасы.ЗаказПокупателя;
			
			Если ВыборкаМатериалы.Количество() = 0 Тогда
				ЗаполнитьТабличнуюЧастьПоСпецификации(ВыборкаЗапасы.Спецификация, ВыборкаЗапасы.Количество, ВыборкаЗапасы.ЕдиницаИзмерения, ВыборкаЗапасы.ЗаказПокупателя);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Пока ВыборкаМатериалы.Следующий() Цикл
		
		НоваяСтрока = Запасы.Добавить();
		НоваяСтрока.Номенклатура 		= ВыборкаМатериалы.Номенклатура;
		НоваяСтрока.Характеристика 		= ВыборкаМатериалы.Характеристика;
		НоваяСтрока.ЕдиницаИзмерения 	= ВыборкаМатериалы.ЕдиницаИзмерения;
		НоваяСтрока.Количество 			= ВыборкаМатериалы.Количество;
		НоваяСтрока.ЗаказПокупателя		= ВыборкаМатериалы.ЗаказПокупателя;
		
	КонецЦикла;
	
	Запасы.Свернуть("Номенклатура, Характеристика, ЕдиницаИзмерения, ЗаказПокупателя, Спецификация", "Количество");
	
	КлючСвязи = 0;
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		СтрокаТабличнойЧасти.КлючСвязи = КлючСвязи;
		КлючСвязи = КлючСвязи + 1;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказНаряду(ДокументСсылкаЗаказПокупателя)
	
	Продукция.Очистить();
	Запасы.Очистить();
	РаспределениеЗапасов.Очистить();
	РаспределениеЗатрат.Очистить();
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказПокупателя.Ссылка КАК ДокументОснование,
	|	ЗаказПокупателя.Проведен КАК Проведен,
	|	ЗаказПокупателя.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА
	|			ЗаказПокупателя.СтруктурнаяЕдиницаПродажи.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗаказПокупателя.СтруктурнаяЕдиницаПродажи
	|		КОГДА &УчетПоНесколькимПодразделениям
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ОсновноеПодразделение)
	|	КОНЕЦ КАК СтруктурнаяЕдиница
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказПокупателяРаботы.Ссылка КАК ЗаказПокупателя,
	|	ЗаказПокупателяРаботы.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяРаботы.Характеристика КАК Характеристика,
	|	ЗаказПокупателяРаботы.Количество * ЗаказПокупателяРаботы.Кратность * ЗаказПокупателяРаботы.Коэффициент КАК
	|		Количество,
	|	ЗаказПокупателяРаботы.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПокупателяРаботы.Спецификация КАК Спецификация
	|ИЗ
	|	Документ.ЗаказПокупателя.Работы КАК ЗаказПокупателяРаботы
	|ГДЕ
	|	ЗаказПокупателяРаботы.Ссылка = &Ссылка
	|	И ЗаказПокупателяРаботы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И ЗаказПокупателяРаботы.Количество * ЗаказПокупателяРаботы.Кратность * ЗаказПокупателяРаботы.Коэффициент > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПотребностьВЗапасахОстатки.ЗаказПокупателя,
	|	ПотребностьВЗапасахОстатки.Номенклатура,
	|	ПотребностьВЗапасахОстатки.Характеристика,
	|	ПотребностьВЗапасахОстатки.КоличествоОстаток КАК Количество,
	|	ПотребностьВЗапасахОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	РегистрНакопления.ПотребностьВЗапасах.Остатки(, ЗаказПокупателя = &Ссылка
	|	И ТипДвижения = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженийЗапасов.Отгрузка)) КАК ПотребностьВЗапасахОстатки";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗаказПокупателя);
	Запрос.УстановитьПараметр("УчетПоНесколькимПодразделениям", ПолучитьФункциональнуюОпцию("УчетПоНесколькимПодразделениям"));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Если РезультатЗапроса[0].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаШапка = РезультатЗапроса[0].Выбрать();
	ВыборкаРаботы = РезультатЗапроса[1].Выбрать();
	ВыборкаМатериалы = РезультатЗапроса[2].Выбрать();
	
	ВыборкаШапка.Следующий();
	
	Документы.ЗаказПокупателя.ПроверитьВозможностьВводаНаОснованииЗаказаПокупателя(
		ДокументСсылкаЗаказПокупателя, Новый Структура("Проведен", ВыборкаШапка.Проведен));
		
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка, "Организация, СтруктурнаяЕдиница");
	
	Пока ВыборкаРаботы.Следующий() Цикл
		
		НоваяСтрока = Продукция.Добавить();
		НоваяСтрока.Номенклатура 		= ВыборкаРаботы.Номенклатура;
		НоваяСтрока.Характеристика 		= ВыборкаРаботы.Характеристика;
		НоваяСтрока.ЕдиницаИзмерения 	= ВыборкаРаботы.ЕдиницаИзмерения;
		НоваяСтрока.Количество 			= ВыборкаРаботы.Количество;
		НоваяСтрока.Спецификация		= ВыборкаРаботы.Спецификация;
		НоваяСтрока.ЗаказПокупателя		= ВыборкаРаботы.ЗаказПокупателя;
		
	КонецЦикла;
	
	Пока ВыборкаМатериалы.Следующий() Цикл
		
		НоваяСтрока = Запасы.Добавить();
		НоваяСтрока.Номенклатура 		= ВыборкаМатериалы.Номенклатура;
		НоваяСтрока.Характеристика 		= ВыборкаМатериалы.Характеристика;
		НоваяСтрока.ЕдиницаИзмерения 	= ВыборкаМатериалы.ЕдиницаИзмерения;
		НоваяСтрока.Количество 			= ВыборкаМатериалы.Количество;
		НоваяСтрока.ЗаказПокупателя		= ВыборкаМатериалы.ЗаказПокупателя;
		
	КонецЦикла;
	
	КлючСвязи = 0;
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		СтрокаТабличнойЧасти.КлючСвязи = КлючСвязи;
		КлючСвязи = КлючСвязи + 1;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти 

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли