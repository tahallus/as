
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ТекСтрокаПолучатели = ПараметрыВыполненияКоманды.Источник.Элементы.Получатели.ТекущиеДанные;
	
	Если ТекСтрокаПолучатели = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭлектроннаяПочтаУНФКлиент.КонтрагентУжеСоздан(
		ТекСтрокаПолучатели.Контакт,
		НСтр("ru = 'Контрагент уже создан.'"),
		ПараметрыВыполненияКоманды.Источник) Тогда
		Возврат;
	КонецЕсли;
	
	КИКонтактнойФормы = ПодготовитьКонтактнуюИнформациюПоДаннымКФ(ПараметрКоманды, ТекСтрокаПолучатели.КакСвязаться);
	Если КИКонтактнойФормы <> Неопределено Тогда
		ПараметрыФормы = ПараметрыФормыПоКФ(ПараметрКоманды, КИКонтактнойФормы, ТекСтрокаПолучатели.Контакт);
		ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник.Элементы.Получатели);
		Возврат;
	КонецЕсли;
		
	Если Не ЭлектроннаяПочтаУНФКлиент.АдресЭлектроннойПочтыЗаполнен(ПараметрыВыполненияКоманды.Источник, ТекСтрокаПолучатели.КакСвязаться) Тогда
		Возврат;
	КонецЕсли;

	ПараметрыФормы = ПараметрыФормыСозданияКонтакта(
	ПараметрКоманды,
	ТекСтрокаПолучатели.Контакт,
	ТекСтрокаПолучатели.КакСвязаться);
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта",
	ПараметрыФормы,
	ПараметрыВыполненияКоманды.Источник.Элементы.Получатели);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПараметрыФормыПоКФ(ПараметрКоманды, КИКонтактнойФормы, Контакт)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Контакт", Контакт);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ИсточникПривлечения = ПолучитьЗначениеИсточникаПривлечения(ПараметрКоманды);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура);
	ПараметрыФормы.ЗначенияЗаполнения.Вставить("Покупатель", Истина);
	ПараметрыФормы.ЗначенияЗаполнения.Вставить("ИсточникПривлечения", ИсточникПривлечения);
	ПараметрыФормы.Вставить("ДанныеИзКонтактнойФормы", КИКонтактнойФормы);
	Возврат ПараметрыФормы;
КонецФункции

&НаСервере
Функция ПодготовитьКонтактнуюИнформациюПоДаннымКФ(Событие, КакСвязаться)
	Возврат Справочники.КонтактныеФормыGoogle.ПодготовитьКонтактнуюИнформациюПоДаннымКФ(Событие, КакСвязаться);
КонецФункции

&НаСервере
Функция ПараметрыФормыСозданияКонтакта(ПараметрКоманды, Контакт, КакСвязаться)
	
	Результат = Новый Структура;
	Результат.Вставить("РежимВыбора", Истина);
	Результат.Вставить("ЗначенияЗаполнения", Новый Структура);
	Результат.ЗначенияЗаполнения.Вставить("Покупатель", Истина);
	
	ИсточникПривлечения = ПолучитьЗначениеИсточникаПривлечения(ПараметрКоманды);
	Результат.ЗначенияЗаполнения.Вставить("ИсточникПривлеченияПокупателя", ИсточникПривлечения);
	
	Если ТипЗнч(Контакт) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
		Результат.Вставить("Контакт", Контакт);
		Возврат Результат;
	КонецЕсли;
	
	Результат.Вставить("КонтактКакСвязаться", Новый Структура);
	Результат.КонтактКакСвязаться.Вставить("ВидКонтакта", "Контрагент");
	Результат.КонтактКакСвязаться.Вставить("Контакт", Контакт);
	Результат.КонтактКакСвязаться.Вставить("КакСвязаться", КакСвязаться);
	Результат.КонтактКакСвязаться.Вставить("ТипКИ", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция КонтрагентУжеСоздан(Контакт, ТекстСообщения, Форма) Экспорт
	
	Если ТипЗнч(Контакт) <> Тип("СправочникСсылка.Контрагенты") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИндексСтроки = Форма.Объект.Участники.Индекс(Форма.Элементы.Получатели.ТекущиеДанные);
	ОбщегоНазначенияКлиент.СообщитьПользователю(
	ТекстСообщения,,
	СтрШаблон("Объект.Участники[%1].Контакт", Формат(ИндексСтроки, "ЧГ=")));
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ПолучитьЗначениеИсточникаПривлечения(Событие)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Событие, "ИсточникПривлечения");
	
КонецФункции

#КонецОбласти
