#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	// Взаиморасчеты
	СоответствиеТабличныхЧастейИРеквизитаЗаказ = Новый Соответствие;
	СоответствиеТабличныхЧастейИРеквизитаЗаказ.Вставить("Запасы", "Заказ");
	РасчетыПроведениеДокументов.ИнициализироватьДополнительныеСвойстваДляПроведения(ЭтотОбъект, ДополнительныеСвойства,
		Отказ, Истина, СоответствиеТабличныхЧастейИРеквизитаЗаказ);
	// Конец Взаиморасчеты
	
	// Инициализация данных документа.
	Документы.ПередачаТоваровМеждуОрганизациями.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Взаиморасчеты
	// Проверим, можно ли продолжать и не было ли отказа в процедурах
	// формирования движений по взаиморасчетам.
	Отказ = ДополнительныеСвойства.Отказ;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	// Конец Взаиморасчеты
	
	// Подготовка наборов записей.
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ПроведениеДокументовУНФ.ОтразитьДвижения("Запасы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыВРазрезеГТД", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("Закупки", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("Продажи", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыНаСкладах", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходыКассовыйМетод", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходыНераспределенные", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходыОтложенные", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("РасчетыСПоставщиками", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("РасчетыСПокупателями", ТаблицыДляДвижений, Движения, Отказ);
	
	// СерииНоменклатуры
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатурыГарантии", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДвиженияСерииНоменклатуры", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатуры", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатурыКРасходу", ТаблицыДляДвижений, Движения, Отказ);
	
	ПроведениеДокументовУНФ.ОтразитьУправленческий(ДополнительныеСвойства, Движения, Отказ);
	
	// Суммы документов для регламентированного учета
	ПроведениеДокументовУНФ.ОтразитьДвижения("СуммыДокументовРегламентированныйУчет",
		ДополнительныеСвойства.ТаблицыДляДвижений, Движения, Отказ);
	
	ПроведениеДокументовУНФ.ОтразитьДвижения("СуммыДокументовРегламентированныйУчет", ТаблицыДляДвижений, Движения,
		Отказ);
	
	// Взаиморасчеты
	ПроведениеДокументовУНФ.ОтразитьДвижения("ОплатаДокументов", ТаблицыДляДвижений, Движения, Отказ);
	// Конец Взаиморасчеты
	
	// Интеркампани
	ПроведениеДокументовУНФ.ОтразитьДвижения("РезервыТоваровОрганизаций", ТаблицыДляДвижений, Движения, Отказ);
	// Конец Интеркампани
	
	// Запись наборов записей.
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	Если ДополнительныеСвойства.Свойство("ТаблицаДокументовДляИзменения")
		И ДополнительныеСвойства.ТаблицаДокументовДляИзменения.Количество() > 0
		Тогда
		РасчетыПроведениеДокументов.ОбработатьТаблицуДокументовДляИзмененияПриОтгрузке(ДополнительныеСвойства, Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Контроль возникновения отрицательного остатка.
	Документы.ПередачаТоваровМеждуОрганизациями.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("ДокументСсылка.ЗаказПокупателя")] = "ЗаполнитьПоЗаказуПокупателя";
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения, "ВидЦен");
	
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		СтруктураДанные = Новый Структура;
		СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
		СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
		СтруктураДанные.Вставить("Партия", СтрокаТабличнойЧасти.Партия);
		СтруктураДанные.Вставить("НалогообложениеНДС", НалогообложениеНДС);
		СтруктураДанные.Вставить("ДатаОбработки", Дата);
		СтруктураДанные.Вставить("ВалютаДокумента", ВалютаДокумента);
		СтруктураДанные.Вставить("СуммаВключаетНДС", СуммаВключаетНДС);
		СтруктураДанные.Вставить("ВидЦен", ВидЦен);
		СтруктураДанные.Вставить("Коэффициент", 1);
		СтрокаТабличнойЧасти.Цена = ЦенообразованиеСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураДанные);
		РассчитатьСуммыВСтрокеТЧ(СтрокаТабличнойЧасти);
	КонецЦикла; 
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	УправлениеНебольшойФирмойЭлектронныеДокументыСервер.ОчиститьДатуНомерВходящегоДокумента(ЭтотОбъект);
	
	Предоплата.Очистить();
	ПредоплатаПолучатель.Очистить();
	
	СпособПродажиГИСМ = "";
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Добавить("Подразделение");
	
	ПроверитьЗаполненностьСтруктурнойЕдиницы(Отказ, ПроверяемыеРеквизиты);
	
	РасчетыРаботаСФормамиВызовСервера.ПроверитьЗаполнениеДокументаПредоплаты(КонтрагентПолучатель, ПроверяемыеРеквизиты);
	
	// Серии номенклатуры
	СерииНоменклатурыУНФ.ПроверкаЗаполненияСерийНоменклатуры(Отказ, Запасы, СерииНоменклатуры, СтруктурнаяЕдиница, ЭтотОбъект);
	
	ПроверитьЗаполнениеНоменклатурыПоДоговоруОбслуживания(Отказ);
	
	// Наборы
	НаборыСервер.ПроверитьТабличнуюЧасть(ЭтотОбъект, "Запасы", Отказ);
	// КонецНаборы
	
	НоменклатураВДокументахСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, Отказ, Истина);
	
	ГрузовыеТаможенныеДекларацииСервер.ПриОбработкеПроверкиЗаполнения(Отказ, ЭтотОбъект);
	
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		Если СтрокаТабличнойЧасти.Количество = 0 И СтрокаТабличнойЧасти.КоличествоПлан = 0 Тогда
			ТекстСообщения = СтрШаблон(НСтр(
				"ru = 'В таблице ""Запасы"" для номенклатуры %2 в строке %3 не указано плановое и фактическое количество передачи.'"),
				СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.НомерСтроки);
			КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаТабличнойЧасти.НомерСтроки, "Количество");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = Запасы.Итог("Всего") ;
	
	
	// Положение склада
	Если ПоложениеСклада<>Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = СтруктурнаяЕдиница;
			СтрокаТабличнойЧасти.Ячейка = Ячейка;
		КонецЦикла;
	Иначе
		СтруктураПолей = ЗаполнениеОбъектовУНФ.СтруктурнаяЕдиницаИЯчейкаДляШапки(Запасы);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураПолей);
	КонецЕсли;
	
	// МобильноеПриложение
	Если МобильноеПриложениеВызовСервера.НужноОграничитьФункциональность()
		И НЕ ПометкаУдаления Тогда
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	// Конец МобильноеПриложение
	
	// ИнтеграцияГИСМ
	ЕстьМаркируемаяПродукцияГИСМ = ИнтеграцияГИСМУНФ.ЕстьМаркируемаяПродукцияГИСМ(Запасы);
	// Конец ИнтеграцияГИСМ
	
	// Интеграция ВЕТИС
	ЕстьПодконтрольнаяПродукцияВЕТИС = ИнтеграцияВЕТИСУНФ.ЕстьПодконтрольнаяПродукцияВЕТИС(Запасы);
	// Конец ИнтеграцияВЕТИС
	
	РасчетыПроведениеДокументов.ПередЗаписьюНакладной(ЭтотОбъект);
	
	// До включения автоматических скидок будем считать, что скидки рассчитаны.
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическиеСкидкиНаценки") Тогда
		СкидкиРассчитаны = Истина;
	КонецЕсли;
	
	Если ЭтоНовый() И НЕ ЗначениеЗаполнено(Номер) Тогда
		УстановитьНовыйНомер();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СчетаФактурыУНФ.ПриЗаписиДокументаОснованияСчетаФактуры(Ссылка, ДополнительныеСвойства, Ложь);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
		// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	// Взаиморасчеты
	СоответствиеТабличныхЧастейИРеквизитаЗаказ = Новый Соответствие;
	СоответствиеТабличныхЧастейИРеквизитаЗаказ.Вставить("Запасы", "Заказ");
	РасчетыПроведениеДокументов.ИнициализироватьДополнительныеСвойстваДляПроведения(ЭтотОбъект, ДополнительныеСвойства, Отказ, Ложь, СоответствиеТабличныхЧастейИРеквизитаЗаказ);
	// Конец Взаиморасчеты
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	Если ДополнительныеСвойства.Свойство("ТаблицаДокументовДляИзменения")
		И ДополнительныеСвойства.ТаблицаДокументовДляИзменения.Количество() > 0
		Тогда
		РасчетыПроведениеДокументов.ОбработатьТаблицуДокументовДляИзмененияПриОтгрузке(ДополнительныеСвойства, Отказ);
	КонецЕсли;
	
	// Подчиненный счет-фактура
	Если Не Отказ Тогда
		СчетаФактурыУНФ.ОтменитьПроведениеПодчиненногоСчетаФактуры(Ссылка, Номер, Дата, ДополнительныеСвойства, Ложь);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроцедурыЗаполненияДокумента

// Процедура заполняет авансы.
//
Процедура ЗаполнитьПредоплату(ИмяТЧ) Экспорт
	
	ЭтотОбъект[ИмяТЧ].Очистить();
	
	ОстатокРасчетов = ЦенообразованиеСервер.ПересчитатьИзВалютыВВалюту(
		Запасы.Итог("Всего"),
		?(Договор.ВалютаРасчетов = ВалютаДокумента, Курс, 1),
		Курс,
		?(Договор.ВалютаРасчетов = ВалютаДокумента, Кратность, 1),
		Кратность);	
	
	ПоПоставщикам = (ИмяТЧ = "ПредоплатаПолучатель");
	Запрос = НовыйЗапросРасшифровкиПредоплаты(ПоПоставщикам);
	
	ВыборкаРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаРезультатаЗапроса.Следующий() Цикл
		
		Если ОстатокРасчетов = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Если ВыборкаРезультатаЗапроса.СуммаРасчетов <= ОстатокРасчетов Тогда // сумма остатка меньше или равна чем осталось распределить
			
			НоваяСтрока = ЭтотОбъект[ИмяТЧ].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаРезультатаЗапроса);
			ОстатокРасчетов = ОстатокРасчетов - ВыборкаРезультатаЗапроса.СуммаРасчетов;
			
		Иначе // сумма остатка больше чем нужно распределить
			
			НоваяСтрока = ЭтотОбъект[ИмяТЧ].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаРезультатаЗапроса);
			НоваяСтрока.СуммаРасчетов = ОстатокРасчетов;
			НоваяСтрока.СуммаПлатежа = ЦенообразованиеСервер.ПересчитатьИзВалютыВВалюту(
				НоваяСтрока.СуммаРасчетов,
				ВыборкаРезультатаЗапроса.Курс,
				ВыборкаРезультатаЗапроса.КурсыВалютыДокументаКурс,
				ВыборкаРезультатаЗапроса.Кратность,
				ВыборкаРезультатаЗапроса.КурсыВалютыДокументаКратность);
			ОстатокРасчетов = 0;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуПокупателя(ДокументСсылкаЗаказ) Экспорт
	
	Запрос = Новый Запрос;
	Документы.ЗаказПокупателя.ДобавитьТаблицуЗапасыВМенеджерВременныхТаблиц(ДокументСсылкаЗаказ, Запрос.МенеджерВременныхТаблиц, Ложь);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗаказ);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Ссылка КАК ДокументОснование,
	|	ТаблицаДокумента.Организация КАК ОрганизацияПолучатель,
	|	ТаблицаДокумента.СтруктурнаяЕдиницаРезерв КАК СтруктурнаяЕдиница,
	|	ТаблицаДокумента.Ячейка КАК Ячейка,
	|	НастройкаПередачиТоваровМеждуОрганизациями.ВидЦены КАК ВидЦен,
	|	НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияВладелец КАК Организация,
	|	НастройкаПередачиТоваровМеждуОрганизациями.КонтрагентВладельца КАК КонтрагентВладелец,
	|	НастройкаПередачиТоваровМеждуОрганизациями.КонтрагентПродавец КАК КонтрагентПолучатель,
	|	НастройкаПередачиТоваровМеждуОрганизациями.Договор КАК Договор,
	|	ТаблицаДокумента.ПоложениеСклада КАК ПоложениеСклада
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК НастройкаПередачиТоваровМеждуОрганизациями
	|		ПО ТаблицаДокумента.Организация = НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияПродавец
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧастьЗапасы.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧастьЗапасы.Номенклатура.ВидСтавкиНДС КАК ВидСтавкиНДС,
	|	ТабличнаяЧастьЗапасы.ТипНоменклатурыЗапас КАК ТипНоменклатурыЗапас,
	|	ТабличнаяЧастьЗапасы.Характеристика КАК Характеристика,
	|	ТабличнаяЧастьЗапасы.Партия КАК Партия,
	|	ТабличнаяЧастьЗапасы.СерииНоменклатуры КАК СерииНоменклатуры,
	|	ТабличнаяЧастьЗапасы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТабличнаяЧастьЗапасы.НомерГТД КАК НомерГТД,
	|	ТабличнаяЧастьЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТабличнаяЧастьЗапасы.КлючСвязи КАК КлючСвязи,
	|	ТабличнаяЧастьЗапасы.Количество КАК Количество,
	|	ТабличнаяЧастьЗапасы.Вес КАК Вес,
	|	ТабличнаяЧастьЗапасы.Объем КАК Объем,
	|	ТабличнаяЧастьЗапасы.СтруктурнаяЕдиницаРезерв КАК СтруктурнаяЕдиница,
	|	ТабличнаяЧастьЗапасы.Ячейка КАК Ячейка
	|ИЗ
	|	ВТЗаказПокупателяЗапасы КАК ТабличнаяЧастьЗапасы
	|ГДЕ
	|	ТабличнаяЧастьЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТабличнаяЧастьЗапасы.НомерСтроки";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	Если РезультатыЗапроса[0].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаШапка = РезультатыЗапроса[0].Выбрать();
	ВыборкаШапка.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	ВидОперации = Перечисления.ВидыОперацийПередачаТоваровМеждуОрганизациями.РучноеЗаполнение;
	НалогообложениеНДС = НалогиУНФ.НалогообложениеНДС(Организация, , Дата);
	
	Запасы.Очистить();
	ВыборкаЗапасы = РезультатыЗапроса[1].Выбрать();
	Пока ВыборкаЗапасы.Следующий() Цикл
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЗапасы);
		Если НалогообложениеНДС <> ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДС") Тогда
			Если НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.НеОблагаетсяНДС") Тогда
				НоваяСтрока.СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
			Иначе
				НоваяСтрока.СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(ВыборкаЗапасы.ВидСтавкиНДС) Тогда
			НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(ВыборкаЗапасы.ВидСтавкиНДС);
		Иначе
			НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(Организация.ВидСтавкиНДСПоУмолчанию);
		КонецЕсли;
		СтруктураДанные = Новый Структура;
		СтруктураДанные.Вставить("Номенклатура", НоваяСтрока.Номенклатура);
		СтруктураДанные.Вставить("Характеристика", НоваяСтрока.Характеристика);
		СтруктураДанные.Вставить("Партия", НоваяСтрока.Партия);
		СтруктураДанные.Вставить("НалогообложениеНДС", НалогообложениеНДС);
		СтруктураДанные.Вставить("ДатаОбработки", Дата);
		СтруктураДанные.Вставить("ВалютаДокумента", ВалютаДокумента);
		СтруктураДанные.Вставить("СуммаВключаетНДС", СуммаВключаетНДС);
		СтруктураДанные.Вставить("ВидЦен", ВидЦен);
		СтруктураДанные.Вставить("Коэффициент", ?(ТипЗнч(НоваяСтрока.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения"), 
			НоваяСтрока.ЕдиницаИзмерения.Коэффициент, 1));
		НоваяСтрока.Цена = ЦенообразованиеСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураДанные);
		НоваяСтрока.Сумма = НоваяСтрока.Цена * НоваяСтрока.Количество;
		СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(НоваяСтрока.СтавкаНДС);
		Если СуммаВключаетНДС Тогда
			НоваяСтрока.СуммаНДС = НоваяСтрока.Сумма - (НоваяСтрока.Сумма) / ((СтавкаНДС + 100) / 100);
			НоваяСтрока.Всего = НоваяСтрока.Сумма;
		Иначе
			НоваяСтрока.СуммаНДС = НоваяСтрока.Сумма * СтавкаНДС / 100;
			НоваяСтрока.Всего = НоваяСтрока.Сумма + НоваяСтрока.СуммаНДС;
		КонецЕсли; 
	КонецЦикла; 
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") Тогда
		
		СерииНоменклатуры.Очистить();
		
		ТаблицаСерииНоменклатуры = ДокументСсылкаЗаказ.СерииНоменклатуры.Выгрузить();
		Для Каждого СтрокаСерия Из ТаблицаСерииНоменклатуры Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаСерия.КлючСвязи) Тогда
				Продолжить;
			КонецЕсли;
			СтрокиЗапасы = ТабличныеЧастиУНФКлиентСервер.СтрокиПоКлючуСвязи(Запасы, СтрокаСерия.КлючСвязи);
			Если СтрокиЗапасы.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = СерииНоменклатуры.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСерия);
		КонецЦикла;
		
	КонецЕсли;
	
	СерииНоменклатурыУНФ.УдалитьСерииНоменклатурыВТабличнойЧастиВЗависимостиОтПолитики(ЭтотОбъект);
	
КонецПроцедуры

Процедура ЗаполнитьПоПлануПередачи() Экспорт
	
	ОбновитьПланПередачи();
	
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.КоличествоПлан;
		РассчитатьСуммыВСтрокеТЧ(СтрокаТабличнойЧасти);
	КонецЦикла;
	
	УдалитьПустыеСтроки();
	
КонецПроцедуры

Процедура ЗаполнитьПоФактическойНехватке() Экспорт
	
	СкладВТЧ = (ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти);
	
	ОбновитьПланПередачи();
	
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		СтрокаТабличнойЧасти.Количество = 0;
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаОстатков", КонецДня(Дата) + 1);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Организация", ОрганизацияПолучатель);
	Если СкладВТЧ Тогда
		Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Неопределено);
		Запрос.УстановитьПараметр("Ячейка", Неопределено);
	Иначе
		Если ПолучитьФункциональнуюОпцию("УчетПоНесколькимСкладам") Тогда
			Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
		Иначе
			Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Неопределено);
		КонецЕсли; 
		Если ПолучитьФункциональнуюОпцию("УчетПоЯчейкам") Тогда
			Запрос.УстановитьПараметр("Ячейка", Ячейка);
		Иначе
			Запрос.УстановитьПараметр("Ячейка", Неопределено);
		КонецЕсли; 
	КонецЕсли; 
	Запрос.Текст = ТекстЗапросаФактическиеОстатки();
	Результат = Запрос.ВыполнитьПакет();
	
	// Заполнение по учету запасов на складах
	Выборка = Результат[Результат.Количество() - 3].Выбрать();
	СтруктураПоиска = Новый Структура;
	Пока Выборка.Следующий() Цикл
		СтруктураПоиска.Очистить();
		СтруктураПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", Выборка.Характеристика);
		СтруктураПоиска.Вставить("Партия", Выборка.Партия);
		Если СкладВТЧ Тогда
			СтруктураПоиска.Вставить("СтруктурнаяЕдиница", Выборка.СтруктурнаяЕдиница);
			СтруктураПоиска.Вставить("Ячейка", Выборка.Ячейка);
		КонецЕсли; 
		Строки = Запасы.НайтиСтроки(СтруктураПоиска);
		Если Строки.Количество() = 0 Тогда
			СтрокаТабличнойЧасти = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
			ЗаполнитьЦенуВСтрокеТЧ(СтрокаТабличнойЧасти);
		Иначе
			Коэффициент = ?(ТипЗнч(СтрокаТабличнойЧасти.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения"), 
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТабличнойЧасти.ЕдиницаИзмерения, "Коэффициент", Истина), 1);
			СтрокаТабличнойЧасти = Строки[0];
			СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество + Выборка.Количество / ?(Коэффициент = 0, 1, Коэффициент);
			СтрокаТабличнойЧасти.Вес = СтрокаТабличнойЧасти.Вес + Выборка.Вес;
			СтрокаТабличнойЧасти.Объем = СтрокаТабличнойЧасти.Объем + Выборка.Объем;
		КонецЕсли;
		РассчитатьСуммыВСтрокеТЧ(СтрокаТабличнойЧасти);
	КонецЦикла;
	
	Выборка = Результат[Результат.Количество() - 2].Выбрать();
	КорректировкаПоУчетуЗапасов(Выборка);
	Выборка = Результат[Результат.Количество() - 1].Выбрать();
	КорректировкаПоУчетуГТД(Выборка);
	УдалитьПустыеСтроки();
	
КонецПроцедуры

Процедура ОбновитьПланПередачи()
	
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		СтрокаТабличнойЧасти.КоличествоПлан = 0;
	КонецЦикла; 
	
	Если НЕ ЗначениеЗаполнено(ВидОперации)
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПередачаТоваровМеждуОрганизациями.РучноеЗаполнение Тогда
		Возврат;
	КонецЕсли;
	
	СкладВТЧ = (ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Дата));
	Запрос.УстановитьПараметр("Организация", ОрганизацияПолучатель);
	Запрос.УстановитьПараметр("КорОрганизация", Организация);
	Если СкладВТЧ Тогда
		Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Неопределено);
		Запрос.УстановитьПараметр("Ячейка", Неопределено);
	Иначе
		Если ПолучитьФункциональнуюОпцию("УчетПоНесколькимСкладам") Тогда
			Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
		Иначе
			Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Неопределено);
		КонецЕсли; 
		Если ПолучитьФункциональнуюОпцию("УчетПоЯчейкам") Тогда
			Запрос.УстановитьПараметр("Ячейка", Ячейка);
		Иначе
			Запрос.УстановитьПараметр("Ячейка", Неопределено);
		КонецЕсли; 
	КонецЕсли; 
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РезервыТоваровОрганизаций.Номенклатура КАК Номенклатура,
	|	РезервыТоваровОрганизаций.Характеристика КАК Характеристика,
	|	РезервыТоваровОрганизаций.Партия КАК Партия,
	|	РезервыТоваровОрганизаций.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	РезервыТоваровОрганизаций.Ячейка КАК Ячейка,
	|	СУММА(РезервыТоваровОрганизаций.Количество) КАК КоличествоПлан,
	|	РезервыТоваровОрганизаций.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|ГДЕ
	|	РезервыТоваровОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И РезервыТоваровОрганизаций.Организация = &Организация
	|	И РезервыТоваровОрганизаций.КорОрганизация = &КорОрганизация
	|	И РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И (&СтруктурнаяЕдиница = НЕОПРЕДЕЛЕНО
	|			ИЛИ РезервыТоваровОрганизаций.СтруктурнаяЕдиница = &СтруктурнаяЕдиница)
	|	И (&Ячейка = НЕОПРЕДЕЛЕНО
	|			ИЛИ РезервыТоваровОрганизаций.Ячейка = &Ячейка)
	|	И РезервыТоваровОрганизаций.Регистратор <> &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РезервыТоваровОрганизаций.Характеристика,
	|	РезервыТоваровОрганизаций.Ячейка,
	|	РезервыТоваровОрганизаций.Номенклатура,
	|	РезервыТоваровОрганизаций.Партия,
	|	РезервыТоваровОрганизаций.СтруктурнаяЕдиница,
	|	РезервыТоваровОрганизаций.Номенклатура.ЕдиницаИзмерения
	|
	|ИМЕЮЩИЕ
	|	СУММА(РезервыТоваровОрганизаций.Количество) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Характеристика";
	Выборка = Запрос.Выполнить().Выбрать();
	СтруктураПоиска = Новый Структура;
	Пока Выборка.Следующий() Цикл
		СтруктураПоиска.Очистить();
		СтруктураПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", Выборка.Характеристика);
		СтруктураПоиска.Вставить("Партия", Выборка.Партия);
		Если СкладВТЧ Тогда
			СтруктураПоиска.Вставить("СтруктурнаяЕдиница", Выборка.СтруктурнаяЕдиница);
			СтруктураПоиска.Вставить("Ячейка", Выборка.Ячейка);
		КонецЕсли; 
		Строки = Запасы.НайтиСтроки(СтруктураПоиска);
		Если Строки.Количество() = 0 Тогда
			СтрокаТабличнойЧасти = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
			СтруктураДанные = Новый Структура;
			СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
			СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
			СтруктураДанные.Вставить("Партия", СтрокаТабличнойЧасти.Партия);
			СтруктураДанные.Вставить("НалогообложениеНДС", НалогообложениеНДС);
			СтруктураДанные.Вставить("ДатаОбработки", Дата);
			СтруктураДанные.Вставить("ВалютаДокумента", ВалютаДокумента);
			СтруктураДанные.Вставить("СуммаВключаетНДС", СуммаВключаетНДС);
			СтруктураДанные.Вставить("ВидЦен", ВидЦен);
			СтруктураДанные.Вставить("Коэффициент", 1);
			СтрокаТабличнойЧасти.Цена = ЦенообразованиеСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураДанные);
		Иначе
			Коэффициент = ?(ТипЗнч(СтрокаТабличнойЧасти.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения"), 
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТабличнойЧасти.ЕдиницаИзмерения, "Коэффициент", Истина), 1);
			СтрокаТабличнойЧасти = Строки[0];
			СтрокаТабличнойЧасти.КоличествоПлан = Выборка.КоличествоПлан / ?(Коэффициент = 0, 1, Коэффициент);
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

Процедура УдалитьПустыеСтроки()
		
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Количество", 0);
	СтруктураПоиска.Вставить("КоличествоПлан", 0);
	Строки = Запасы.НайтиСтроки(СтруктураПоиска);
	Для каждого СтрокаТабличнойЧасти Из Строки Цикл
		Запасы.Удалить(СтрокаТабличнойЧасти);
	КонецЦикла;
	
КонецПроцедуры

Функция НовыйЗапросРасшифровкиПредоплаты(ПоПоставщикам)
	
	Результат = Новый Запрос;
	Если ПоПоставщикам Тогда
		Результат.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РасчетыСПоставщикамиОстатки.Документ КАК Документ,
		|	РасчетыСПоставщикамиОстатки.Заказ КАК Заказ,
		|	РасчетыСПоставщикамиОстатки.ДокументДата КАК ДокументДата,
		|	РасчетыСПоставщикамиОстатки.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
		|	СУММА(РасчетыСПоставщикамиОстатки.СуммаОстаток) КАК СуммаОстаток,
		|	СУММА(РасчетыСПоставщикамиОстатки.СуммаВалОстаток) КАК СуммаВалОстаток,
		|	СУММА(РасчетыСПоставщикамиОстатки.СуммаРегОстаток) КАК СуммаРегОстаток
		|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПоставщикамиОстатки
		|ИЗ
		|	(ВЫБРАТЬ
		|		РасчетыСПоставщикамиОстатки.Договор КАК Договор,
		|		РасчетыСПоставщикамиОстатки.Документ КАК Документ,
		|		РасчетыСПоставщикамиОстатки.Документ.Дата КАК ДокументДата,
		|		РасчетыСПоставщикамиОстатки.Заказ КАК Заказ,
		|		РасчетыСПоставщикамиОстатки.СуммаОстаток КАК СуммаОстаток,
		|		РасчетыСПоставщикамиОстатки.СуммаВалОстаток КАК СуммаВалОстаток,
		|		РасчетыСПоставщикамиОстатки.СуммаРегОстаток КАК СуммаРегОстаток
		|	ИЗ
		|		РегистрНакопления.РасчетыСПоставщиками.Остатки(
		|				,
		|				Организация = &Организация
		|					И Контрагент = &Контрагент
		|					И Договор = &Договор
		|					И Заказ В (&Заказ)
		|					И ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК РасчетыСПоставщикамиОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДвиженияДокументаРасчетыСПоставщиками.Договор,
		|		ДвиженияДокументаРасчетыСПоставщиками.Документ,
		|		ДвиженияДокументаРасчетыСПоставщиками.Документ.Дата,
		|		ДвиженияДокументаРасчетыСПоставщиками.Заказ,
		|		ВЫБОР
		|			КОГДА ДвиженияДокументаРасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА -ЕСТЬNULL(ДвиженияДокументаРасчетыСПоставщиками.Сумма, 0)
		|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаРасчетыСПоставщиками.Сумма, 0)
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ДвиженияДокументаРасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА -ЕСТЬNULL(ДвиженияДокументаРасчетыСПоставщиками.СуммаВал, 0)
		|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаРасчетыСПоставщиками.СуммаВал, 0)
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ДвиженияДокументаРасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА -ЕСТЬNULL(ДвиженияДокументаРасчетыСПоставщиками.СуммаРег, 0)
		|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаРасчетыСПоставщиками.СуммаРег, 0)
		|		КОНЕЦ
		|	ИЗ
		|		РегистрНакопления.РасчетыСПоставщиками КАК ДвиженияДокументаРасчетыСПоставщиками
		|	ГДЕ
		|		ДвиженияДокументаРасчетыСПоставщиками.Регистратор = &Ссылка
		|		И ДвиженияДокументаРасчетыСПоставщиками.Период <= &Период
		|		И ДвиженияДокументаРасчетыСПоставщиками.Организация = &Организация
		|		И ДвиженияДокументаРасчетыСПоставщиками.Контрагент = &Контрагент
		|		И ДвиженияДокументаРасчетыСПоставщиками.Договор = &Договор
		|		И ДвиженияДокументаРасчетыСПоставщиками.Заказ В(&Заказ)
		|		И ДвиженияДокументаРасчетыСПоставщиками.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК РасчетыСПоставщикамиОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыСПоставщикамиОстатки.Документ,
		|	РасчетыСПоставщикамиОстатки.Заказ,
		|	РасчетыСПоставщикамиОстатки.ДокументДата,
		|	РасчетыСПоставщикамиОстатки.Договор.ВалютаРасчетов
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетыСПоставщикамиОстатки.СуммаВалОстаток) < 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РасчетыСПоставщикамиОстатки.Документ КАК Документ,
		|	РасчетыСПоставщикамиОстатки.Заказ КАК Заказ,
		|	РасчетыСПоставщикамиОстатки.ДокументДата КАК ДокументДата,
		|	РасчетыСПоставщикамиОстатки.ВалютаРасчетов КАК ВалютаРасчетов,
		|	-СУММА(РасчетыСПоставщикамиОстатки.СуммаУчета) КАК СуммаУчета,
		|	-СУММА(РасчетыСПоставщикамиОстатки.СуммаРасчетов) КАК СуммаРасчетов,
		|	-СУММА(РасчетыСПоставщикамиОстатки.СуммаПлатежа) КАК СуммаПлатежа,
		|	СУММА(ВЫБОР
		|			КОГДА ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаРег, 0) = 0
		|				ТОГДА РасчетыСПоставщикамиОстатки.СуммаУчета / ВЫБОР
		|						КОГДА ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаРасчетов, 0) <> 0
		|							ТОГДА РасчетыСПоставщикамиОстатки.СуммаРасчетов
		|						ИНАЧЕ 1
		|					КОНЕЦ * (РасчетыСПоставщикамиОстатки.КурсыВалютыУчетаКурс / РасчетыСПоставщикамиОстатки.КурсыВалютыУчетаКратность)
		|			ИНАЧЕ РасчетыСПоставщикамиОстатки.СуммаРег / ВЫБОР
		|					КОГДА ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаРасчетов, 0) <> 0
		|						ТОГДА РасчетыСПоставщикамиОстатки.СуммаРасчетов
		|					ИНАЧЕ 1
		|				КОНЕЦ
		|		КОНЕЦ) КАК Курс,
		|	1 КАК Кратность,
		|	РасчетыСПоставщикамиОстатки.КурсыВалютыДокументаКурс КАК КурсыВалютыДокументаКурс,
		|	РасчетыСПоставщикамиОстатки.КурсыВалютыДокументаКратность КАК КурсыВалютыДокументаКратность
		|ИЗ
		|	(ВЫБРАТЬ
		|		РасчетыСПоставщикамиОстатки.ВалютаРасчетов КАК ВалютаРасчетов,
		|		РасчетыСПоставщикамиОстатки.Документ КАК Документ,
		|		РасчетыСПоставщикамиОстатки.ДокументДата КАК ДокументДата,
		|		РасчетыСПоставщикамиОстатки.Заказ КАК Заказ,
		|		ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаОстаток, 0) КАК СуммаУчета,
		|		ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) КАК СуммаРасчетов,
		|		ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаОстаток, 0) * КурсыВалютыУчета.Курс * &КратностьВалютыДокумента / (&КурсВалютыДокумента * КурсыВалютыУчета.Кратность) КАК СуммаПлатежа,
		|		ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаРегОстаток, 0) КАК СуммаРег,
		|		КурсыВалютыУчета.Курс КАК КурсыВалютыУчетаКурс,
		|		КурсыВалютыУчета.Кратность КАК КурсыВалютыУчетаКратность,
		|		&КурсВалютыДокумента КАК КурсыВалютыДокументаКурс,
		|		&КратностьВалютыДокумента КАК КурсыВалютыДокументаКратность
		|	ИЗ
		|		ВременнаяТаблицаРасчетыСПоставщикамиОстатки КАК РасчетыСПоставщикамиОстатки
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта = &ВалютаУчета) КАК КурсыВалютыУчета
		|			ПО (ИСТИНА)) КАК РасчетыСПоставщикамиОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыСПоставщикамиОстатки.Документ,
		|	РасчетыСПоставщикамиОстатки.Заказ,
		|	РасчетыСПоставщикамиОстатки.ДокументДата,
		|	РасчетыСПоставщикамиОстатки.ВалютаРасчетов,
		|	РасчетыСПоставщикамиОстатки.КурсыВалютыУчетаКурс,
		|	РасчетыСПоставщикамиОстатки.КурсыВалютыУчетаКратность,
		|	РасчетыСПоставщикамиОстатки.КурсыВалютыДокументаКурс,
		|	РасчетыСПоставщикамиОстатки.КурсыВалютыДокументаКратность
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетыСПоставщикамиОстатки.СуммаРасчетов) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДокументДата";
	Иначе
		Результат.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РасчетыСПокупателямиОстатки.Документ КАК Документ,
		|	РасчетыСПокупателямиОстатки.Заказ КАК Заказ,
		|	РасчетыСПокупателямиОстатки.ДокументДата КАК ДокументДата,
		|	РасчетыСПокупателямиОстатки.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
		|	СУММА(РасчетыСПокупателямиОстатки.СуммаОстаток) КАК СуммаОстаток,
		|	СУММА(РасчетыСПокупателямиОстатки.СуммаВалОстаток) КАК СуммаВалОстаток,
		|	СУММА(РасчетыСПокупателямиОстатки.СуммаРегОстаток) КАК СуммаРегОстаток
		|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПокупателямиОстатки
		|ИЗ
		|	(ВЫБРАТЬ
		|		РасчетыСПокупателямиОстатки.Договор КАК Договор,
		|		РасчетыСПокупателямиОстатки.Документ КАК Документ,
		|		РасчетыСПокупателямиОстатки.Документ.Дата КАК ДокументДата,
		|		РасчетыСПокупателямиОстатки.Заказ КАК Заказ,
		|		ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|		ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
		|		ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаРегОстаток, 0) КАК СуммаРегОстаток
		|	ИЗ
		|		РегистрНакопления.РасчетыСПокупателями.Остатки(
		|				,
		|				Организация = &Организация
		|					И Контрагент = &Контрагент
		|					И Договор = &Договор
		|					И Заказ В (&Заказ)
		|					И ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК РасчетыСПокупателямиОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДвиженияДокументаРасчетыСПокупателями.Договор,
		|		ДвиженияДокументаРасчетыСПокупателями.Документ,
		|		ДвиженияДокументаРасчетыСПокупателями.Документ.Дата,
		|		ДвиженияДокументаРасчетыСПокупателями.Заказ,
		|		ВЫБОР
		|			КОГДА ДвиженияДокументаРасчетыСПокупателями.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА -ЕСТЬNULL(ДвиженияДокументаРасчетыСПокупателями.Сумма, 0)
		|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаРасчетыСПокупателями.Сумма, 0)
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ДвиженияДокументаРасчетыСПокупателями.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА -ЕСТЬNULL(ДвиженияДокументаРасчетыСПокупателями.СуммаВал, 0)
		|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаРасчетыСПокупателями.СуммаВал, 0)
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ДвиженияДокументаРасчетыСПокупателями.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА -ЕСТЬNULL(ДвиженияДокументаРасчетыСПокупателями.СуммаРег, 0)
		|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаРасчетыСПокупателями.СуммаРег, 0)
		|		КОНЕЦ
		|	ИЗ
		|		РегистрНакопления.РасчетыСПокупателями КАК ДвиженияДокументаРасчетыСПокупателями
		|	ГДЕ
		|		ДвиженияДокументаРасчетыСПокупателями.Регистратор = &Ссылка
		|		И ДвиженияДокументаРасчетыСПокупателями.Период <= &Период
		|		И ДвиженияДокументаРасчетыСПокупателями.Организация = &Организация
		|		И ДвиженияДокументаРасчетыСПокупателями.Контрагент = &Контрагент
		|		И ДвиженияДокументаРасчетыСПокупателями.Договор = &Договор
		|		И ДвиженияДокументаРасчетыСПокупателями.Заказ В(&Заказ)
		|		И ДвиженияДокументаРасчетыСПокупателями.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК РасчетыСПокупателямиОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыСПокупателямиОстатки.Документ,
		|	РасчетыСПокупателямиОстатки.Заказ,
		|	РасчетыСПокупателямиОстатки.ДокументДата,
		|	РасчетыСПокупателямиОстатки.Договор.ВалютаРасчетов
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетыСПокупателямиОстатки.СуммаВалОстаток) < 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РасчетыСПокупателямиОстатки.Документ КАК Документ,
		|	РасчетыСПокупателямиОстатки.Заказ КАК Заказ,
		|	РасчетыСПокупателямиОстатки.ДокументДата КАК ДокументДата,
		|	РасчетыСПокупателямиОстатки.ВалютаРасчетов КАК ВалютаРасчетов,
		|	-СУММА(РасчетыСПокупателямиОстатки.СуммаУчета) КАК СуммаУчета,
		|	-СУММА(РасчетыСПокупателямиОстатки.СуммаРасчетов) КАК СуммаРасчетов,
		|	-СУММА(РасчетыСПокупателямиОстатки.СуммаПлатежа) КАК СуммаПлатежа,
		|	СУММА(ВЫБОР
		|			КОГДА ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаРег, 0) = 0
		|				ТОГДА РасчетыСПокупателямиОстатки.СуммаУчета / ВЫБОР
		|						КОГДА ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаРасчетов, 0) <> 0
		|							ТОГДА РасчетыСПокупателямиОстатки.СуммаРасчетов
		|						ИНАЧЕ 1
		|					КОНЕЦ * (РасчетыСПокупателямиОстатки.КурсыВалютыУчетаКурс / РасчетыСПокупателямиОстатки.КурсыВалютыУчетаКратность)
		|			ИНАЧЕ РасчетыСПокупателямиОстатки.СуммаРег / ВЫБОР
		|					КОГДА ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаРасчетов, 0) <> 0
		|						ТОГДА РасчетыСПокупателямиОстатки.СуммаРасчетов
		|					ИНАЧЕ 1
		|				КОНЕЦ
		|		КОНЕЦ) КАК Курс,
		|	1 КАК Кратность,
		|	РасчетыСПокупателямиОстатки.КурсыВалютыДокументаКурс КАК КурсыВалютыДокументаКурс,
		|	РасчетыСПокупателямиОстатки.КурсыВалютыДокументаКратность КАК КурсыВалютыДокументаКратность
		|ИЗ
		|	(ВЫБРАТЬ
		|		РасчетыСПокупателямиОстатки.ВалютаРасчетов КАК ВалютаРасчетов,
		|		РасчетыСПокупателямиОстатки.Документ КАК Документ,
		|		РасчетыСПокупателямиОстатки.ДокументДата КАК ДокументДата,
		|		РасчетыСПокупателямиОстатки.Заказ КАК Заказ,
		|		ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаОстаток, 0) КАК СуммаУчета,
		|		ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) КАК СуммаРасчетов,
		|		ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаОстаток, 0) * КурсыВалютыУчета.Курс * &КратностьВалютыДокумента / (&КурсВалютыДокумента * КурсыВалютыУчета.Кратность) КАК СуммаПлатежа,
		|		ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаРегОстаток, 0) КАК СуммаРег,
		|		КурсыВалютыУчета.Курс КАК КурсыВалютыУчетаКурс,
		|		КурсыВалютыУчета.Кратность КАК КурсыВалютыУчетаКратность,
		|		&КурсВалютыДокумента КАК КурсыВалютыДокументаКурс,
		|		&КратностьВалютыДокумента КАК КурсыВалютыДокументаКратность
		|	ИЗ
		|		ВременнаяТаблицаРасчетыСПокупателямиОстатки КАК РасчетыСПокупателямиОстатки
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта = &ВалютаУчета) КАК КурсыВалютыУчета
		|			ПО (ИСТИНА)) КАК РасчетыСПокупателямиОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыСПокупателямиОстатки.Документ,
		|	РасчетыСПокупателямиОстатки.Заказ,
		|	РасчетыСПокупателямиОстатки.ДокументДата,
		|	РасчетыСПокупателямиОстатки.ВалютаРасчетов,
		|	РасчетыСПокупателямиОстатки.КурсыВалютыУчетаКурс,
		|	РасчетыСПокупателямиОстатки.КурсыВалютыУчетаКратность,
		|	РасчетыСПокупателямиОстатки.КурсыВалютыДокументаКурс,
		|	РасчетыСПокупателямиОстатки.КурсыВалютыДокументаКратность
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетыСПокупателямиОстатки.СуммаРасчетов) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДокументДата";
	КонецЕсли;
	
	Если ПоПоставщикам Тогда
		Результат.УстановитьПараметр("Заказ", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Документы.ЗаказПоставщику.ПустаяСсылка()));
		Результат.УстановитьПараметр("Организация", ОрганизацияПолучатель);
		Результат.УстановитьПараметр("Контрагент", КонтрагентВладелец);
	Иначе
		Результат.УстановитьПараметр("Заказ", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Документы.ЗаказПокупателя.ПустаяСсылка()));
		Результат.УстановитьПараметр("Организация", Организация);
		Результат.УстановитьПараметр("Контрагент", КонтрагентПолучатель);
	КонецЕсли;
	Результат.УстановитьПараметр("Договор", Договор);
	Результат.УстановитьПараметр("Период", Дата);
	Результат.УстановитьПараметр("ВалютаДокумента", ВалютаДокумента);
	Результат.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Если Договор.ВалютаРасчетов = ВалютаДокумента Тогда
		Результат.УстановитьПараметр("КурсВалютыДокумента", Курс);
		Результат.УстановитьПараметр("КратностьВалютыДокумента", Кратность);
	Иначе
		Результат.УстановитьПараметр("КурсВалютыДокумента", 1);
		Результат.УстановитьПараметр("КратностьВалютыДокумента", 1);
	КонецЕсли;
	Результат.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаФактическиеОстатки()
	
	Возврат 
	"ВЫБРАТЬ
	|	ЗапасыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыНаСкладахОстатки.Характеристика КАК Характеристика,
	|	ЗапасыНаСкладахОстатки.Партия КАК Партия,
	|	ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗапасыНаСкладахОстатки.Ячейка КАК Ячейка,
	|	ЗапасыНаСкладахОстатки.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ОстаткиСклад
	|ИЗ
	|	РегистрНакопления.ЗапасыНаСкладах.Остатки(
	|			&ДатаОстатков,
	|			Организация = &Организация
	|				И (&СтруктурнаяЕдиница = НЕОПРЕДЕЛЕНО
	|					ИЛИ СтруктурнаяЕдиница = &СтруктурнаяЕдиница)
	|				И (&Ячейка = НЕОПРЕДЕЛЕНО
	|					ИЛИ Ячейка = &Ячейка)) КАК ЗапасыНаСкладахОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗапасыНаСкладах.Номенклатура,
	|	ЗапасыНаСкладах.Характеристика,
	|	ЗапасыНаСкладах.Партия,
	|	ЗапасыНаСкладах.СтруктурнаяЕдиница,
	|	ЗапасыНаСкладах.Ячейка,
	|	-ЗапасыНаСкладах.Количество
	|ИЗ
	|	РегистрНакопления.ЗапасыНаСкладах КАК ЗапасыНаСкладах
	|ГДЕ
	|	ЗапасыНаСкладах.Регистратор = &Ссылка
	|	И ЗапасыНаСкладах.Период < &ДатаОстатков
	|	И ЗапасыНаСкладах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ЗапасыНаСкладах.Организация = &Организация
	|	И (&СтруктурнаяЕдиница = НЕОПРЕДЕЛЕНО
	|			ИЛИ ЗапасыНаСкладах.СтруктурнаяЕдиница = &СтруктурнаяЕдиница)
	|	И (&Ячейка = НЕОПРЕДЕЛЕНО
	|			ИЛИ ЗапасыНаСкладах.Ячейка = &Ячейка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыОстатки.Характеристика КАК Характеристика,
	|	ЗапасыОстатки.Партия КАК Партия,
	|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗапасыОстатки.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ОстаткиЗапасы
	|ИЗ
	|	РегистрНакопления.Запасы.Остатки(
	|			&ДатаОстатков,
	|			Организация = &Организация
	|				И (&СтруктурнаяЕдиница = НЕОПРЕДЕЛЕНО
	|					ИЛИ СтруктурнаяЕдиница = &СтруктурнаяЕдиница)
	|				И ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)) КАК ЗапасыОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Запасы.Номенклатура,
	|	Запасы.Характеристика,
	|	Запасы.Партия,
	|	Запасы.СтруктурнаяЕдиница,
	|	-Запасы.Количество
	|ИЗ
	|	РегистрНакопления.Запасы КАК Запасы
	|ГДЕ
	|	Запасы.Регистратор = &Ссылка
	|	И Запасы.Период < &ДатаОстатков
	|	И Запасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И Запасы.Организация = &Организация
	|	И (&СтруктурнаяЕдиница = НЕОПРЕДЕЛЕНО
	|			ИЛИ Запасы.СтруктурнаяЕдиница = &СтруктурнаяЕдиница)
	|	И Запасы.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыВРазрезеГТДОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыВРазрезеГТДОстатки.Характеристика КАК Характеристика,
	|	ЗапасыВРазрезеГТДОстатки.Партия КАК Партия,
	|	ЗапасыВРазрезеГТДОстатки.НомерГТД КАК НомерГТД,
	|	ЗапасыВРазрезеГТДОстатки.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЗапасыВРазрезеГТДОстатки.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ОстаткиГТД
	|ИЗ
	|	РегистрНакопления.ЗапасыВРазрезеГТД.Остатки(&ДатаОстатков, Организация = &Организация) КАК ЗапасыВРазрезеГТДОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗапасыВРазрезеГТД.Номенклатура,
	|	ЗапасыВРазрезеГТД.Характеристика,
	|	ЗапасыВРазрезеГТД.Партия,
	|	ЗапасыВРазрезеГТД.НомерГТД,
	|	ЗапасыВРазрезеГТД.СтранаПроисхождения,
	|	-ЗапасыВРазрезеГТД.Количество
	|ИЗ
	|	РегистрНакопления.ЗапасыВРазрезеГТД КАК ЗапасыВРазрезеГТД
	|ГДЕ
	|	ЗапасыВРазрезеГТД.Регистратор = &Ссылка
	|	И ЗапасыВРазрезеГТД.Период < &ДатаОстатков
	|	И ЗапасыВРазрезеГТД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ЗапасыВРазрезеГТД.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиСклад.Номенклатура КАК Номенклатура,
	|	ОстаткиСклад.Характеристика КАК Характеристика,
	|	ОстаткиСклад.Партия КАК Партия,
	|	ОстаткиСклад.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ОстаткиСклад.Ячейка КАК Ячейка,
	|	СУММА(-ОстаткиСклад.Количество) КАК Количество,
	|	ОстаткиСклад.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ОстаткиСклад.Номенклатура.Вес * СУММА(-ОстаткиСклад.Количество) КАК Вес,
	|	ОстаткиСклад.Номенклатура.Объем * СУММА(-ОстаткиСклад.Количество) КАК Объем
	|ИЗ
	|	ОстаткиСклад КАК ОстаткиСклад
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиСклад.Партия,
	|	ОстаткиСклад.Ячейка,
	|	ОстаткиСклад.Характеристика,
	|	ОстаткиСклад.Номенклатура,
	|	ОстаткиСклад.СтруктурнаяЕдиница,
	|	ОстаткиСклад.Номенклатура.ЕдиницаИзмерения,
	|	ОстаткиСклад.Номенклатура.Вес,
	|	ОстаткиСклад.Номенклатура.Объем
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОстаткиСклад.Количество) < 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиЗапасы.Номенклатура КАК Номенклатура,
	|	ОстаткиЗапасы.Характеристика КАК Характеристика,
	|	ОстаткиЗапасы.Партия КАК Партия,
	|	ОстаткиЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	СУММА(-ОстаткиЗапасы.Количество) КАК Количество,
	|	ОстаткиЗапасы.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ОстаткиЗапасы.Номенклатура.Вес * СУММА(-ОстаткиЗапасы.Количество) КАК Вес,
	|	ОстаткиЗапасы.Номенклатура.Объем * СУММА(-ОстаткиЗапасы.Количество) КАК Объем
	|ИЗ
	|	ОстаткиЗапасы КАК ОстаткиЗапасы
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиЗапасы.Партия,
	|	ОстаткиЗапасы.Характеристика,
	|	ОстаткиЗапасы.Номенклатура,
	|	ОстаткиЗапасы.СтруктурнаяЕдиница,
	|	ОстаткиЗапасы.Номенклатура.ЕдиницаИзмерения,
	|	ОстаткиЗапасы.Номенклатура.Вес,
	|	ОстаткиЗапасы.Номенклатура.Объем
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОстаткиЗапасы.Количество) < 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиГТД.Номенклатура КАК Номенклатура,
	|	ОстаткиГТД.Характеристика КАК Характеристика,
	|	ОстаткиГТД.Партия КАК Партия,
	|	ОстаткиГТД.НомерГТД КАК НомерГТД,
	|	ОстаткиГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(-ОстаткиГТД.Количество) КАК Количество,
	|	ОстаткиГТД.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ОстаткиГТД.Номенклатура.Вес * СУММА(-ОстаткиГТД.Количество) КАК Вес,
	|	ОстаткиГТД.Номенклатура.Объем * СУММА(-ОстаткиГТД.Количество) КАК Объем
	|ИЗ
	|	ОстаткиГТД КАК ОстаткиГТД
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиГТД.Партия,
	|	ОстаткиГТД.Характеристика,
	|	ОстаткиГТД.Номенклатура,
	|	ОстаткиГТД.НомерГТД,
	|	ОстаткиГТД.СтранаПроисхождения,
	|	ОстаткиГТД.Номенклатура.ЕдиницаИзмерения,
	|	ОстаткиГТД.Номенклатура.Вес,
	|	ОстаткиГТД.Номенклатура.Объем
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОстаткиГТД.Количество) < 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Характеристика";	
	
КонецФункции

Процедура КорректировкаПоУчетуЗапасов(Выборка)
	
	Если НЕ ПолучитьФункциональнуюОпцию("РезервированиеЗапасов") Тогда
		Возврат;
	КонецЕсли;
	СкладВТЧ = (ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти);
	
	СтруктураПоиска = Новый Структура;
	Пока Выборка.Следующий() Цикл
		СтруктураПоиска.Очистить();
		СтруктураПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", Выборка.Характеристика);
		СтруктураПоиска.Вставить("Партия", Выборка.Партия);
		Если СкладВТЧ Тогда
			СтруктураПоиска.Вставить("СтруктурнаяЕдиница", Выборка.СтруктурнаяЕдиница);
		КонецЕсли; 
		Строки = Запасы.НайтиСтроки(СтруктураПоиска);
		Если Строки.Количество() = 0 Тогда
			СтрокаТабличнойЧасти = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
			ЗаполнитьЦенуВСтрокеТЧ(СтрокаТабличнойЧасти);
		Иначе
			НехваткаПоСкладу = 0;
			Для каждого СтрокаТабличнойЧасти Из Строки Цикл
				Коэффициент = ?(ТипЗнч(СтрокаТабличнойЧасти.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения"), 
					ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТабличнойЧасти.ЕдиницаИзмерения, "Коэффициент", Истина), 1);
				НехваткаПоСкладу = НехваткаПоСкладу + СтрокаТабличнойЧасти.Количество * ?(Коэффициент = 0, 1, Коэффициент);
			КонецЦикла;
			Если НехваткаПоСкладу >= Выборка.Количество Тогда
				Продолжить;
			КонецЕсли;
			Разница = Выборка.Количество - НехваткаПоСкладу;
			Если СкладВТЧ Тогда
				СтруктураПоиска.Вставить("Ячейка", Справочники.Ячейки.ПустаяСсылка());
				Строки = Запасы.НайтиСтроки(СтруктураПоиска);
			КонецЕсли;
			Если Строки.Количество() = 0 Тогда
				СтрокаТабличнойЧасти = Запасы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
				ЗаполнитьЦенуВСтрокеТЧ(СтрокаТабличнойЧасти);
				СтрокаТабличнойЧасти.Количество = 0;
			Иначе
				СтрокаТабличнойЧасти = Строки[0];
			КонецЕсли; 
			Коэффициент = ?(ТипЗнч(СтрокаТабличнойЧасти.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения"), 
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТабличнойЧасти.ЕдиницаИзмерения, "Коэффициент", Истина), 1);
			КоличествоЗапасов = Разница / ?(Коэффициент = 0, 1, Коэффициент);
			СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество + Разница;
			СтрокаТабличнойЧасти.Вес = СтрокаТабличнойЧасти.Вес + Выборка.Вес * Разница / КоличествоЗапасов;
			СтрокаТабличнойЧасти.Объем = СтрокаТабличнойЧасти.Объем + Выборка.Объем * Разница / КоличествоЗапасов;
		КонецЕсли; 
		РассчитатьСуммыВСтрокеТЧ(СтрокаТабличнойЧасти);
	КонецЦикла;
	
КонецПроцедуры
 
Процедура КорректировкаПоУчетуГТД(Выборка)
	
	Если НЕ ПолучитьФункциональнуюОпцию("УчетГТД") Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураПоиска = Новый Структура;
	Пока Выборка.Следующий() Цикл
		СтруктураПоиска.Очистить();
		СтруктураПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", Выборка.Характеристика);
		СтруктураПоиска.Вставить("Партия", Выборка.Партия);
		СтруктураПоиска.Вставить("НомерГТД", Выборка.НомерГТД);
		СтруктураПоиска.Вставить("СтранаПроисхождения", Выборка.СтранаПроисхождения);
		Строки = Запасы.НайтиСтроки(СтруктураПоиска);
		Разница = 0;
		Если Строки.Количество() = 0 Тогда
			СтрокаТабличнойЧасти = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
			ЗаполнитьЦенуВСтрокеТЧ(СтрокаТабличнойЧасти);
			Разница = Выборка.Количество;
		Иначе
			СтрокаТабличнойЧасти = Строки[0];
			Коэффициент = ?(ТипЗнч(СтрокаТабличнойЧасти.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения"), 
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТабличнойЧасти.ЕдиницаИзмерения, "Коэффициент", Истина), 1);
			КоличествоГТД = Выборка.Количество / ?(Коэффициент = 0, 1, Коэффициент);
			Если СтрокаТабличнойЧасти.Количество >= КоличествоГТД Тогда
				Продолжить;
			КонецЕсли; 
			Разница = КоличествоГТД - СтрокаТабличнойЧасти.Количество;
			СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество + Разница;
			СтрокаТабличнойЧасти.Вес = СтрокаТабличнойЧасти.Вес + Выборка.Вес * Разница / КоличествоГТД;
			СтрокаТабличнойЧасти.Объем = СтрокаТабличнойЧасти.Объем + Выборка.Объем * Разница / КоличествоГТД;
			Разница = Разница * ?(Коэффициент = 0, 1, Коэффициент);
		КонецЕсли;
		Если Разница <= 0 Тогда
			Продолжить;
		КонецЕсли;
		РассчитатьСуммыВСтрокеТЧ(СтрокаТабличнойЧасти);
		// Уменьшение передачи по пустому ГТД
		СтруктураПоиска.Вставить("НомерГТД", Справочники.НомераГТД.ПустаяСсылка());
		СтруктураПоиска.Вставить("СтранаПроисхождения", Справочники.СтраныМира.ПустаяСсылка());
		Строки = Запасы.НайтиСтроки(СтруктураПоиска);
		Для каждого СтрокаТабличнойЧасти Из Строки Цикл
			Коэффициент = ?(ТипЗнч(СтрокаТабличнойЧасти.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения"), 
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТабличнойЧасти.ЕдиницаИзмерения, "Коэффициент", Истина), 1);
			КоличествоПоСтроке = СтрокаТабличнойЧасти.Количество * ?(Коэффициент = 0, 1, Коэффициент);
			СнятьПоСтроке = Мин(Разница, КоличествоПоСтроке);
			Если СнятьПоСтроке <= 0 Тогда
				Продолжить;
			КонецЕсли;
			Разница = Разница - СнятьПоСтроке;
			СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество - СнятьПоСтроке / ?(Коэффициент = 0, 1, Коэффициент);
			Если Разница <= 0 Тогда
				Прервать;
			КонецЕсли; 
		КонецЦикла; 
	КонецЦикла;
	
КонецПроцедуры
	
#КонецОбласти

Процедура ПроверитьЗаполненностьСтруктурнойЕдиницы(Отказ, ПроверяемыеРеквизиты)
	
	Если ПоложениеСклада <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		ПроверяемыеРеквизиты.Добавить("СтруктурнаяЕдиница");
		Возврат;
	КонецЕсли;
		
	Для Каждого ТекСтрокаТЧ Из Запасы Цикл
		Если Не ТекСтрокаТЧ.ТипНоменклатурыЗапас Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(ТекСтрокаТЧ.СтруктурнаяЕдиница) Тогда
			Продолжить;
		КонецЕсли;
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнена колонка ""Склад"" в строке %1 списка ""Запасы""'"),
			ТекСтрокаТЧ.НомерСтроки);
		КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", ТекСтрокаТЧ.НомерСтроки,
			"СтруктурнаяЕдиница");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеНоменклатурыПоДоговоруОбслуживания(Отказ)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьБиллинг") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Договор) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор, "ЭтоДоговорОбслуживания, ДоговорОбслуживанияТарифныйПлан");
	
	Если Не ДанныеДоговора.ЭтоДоговорОбслуживания Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаЗапасы Из Запасы Цикл
		РазрешенаПродажаПозиции = Справочники.ДоговорыКонтрагентов.РазрешенаПродажаНоменклатурыПоДоговоруОбслуживания(
			Договор, СтрокаЗапасы.Номенклатура, СтрокаЗапасы.Характеристика);
		Если РазрешенаПродажаПозиции Тогда
			Продолжить;
		КонецЕсли;
		ТекстСообщения = НСтр("ru = 'Запрещено проводить незапланированные товары/услуги по текущему договору обслуживания.'");
		ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки, "Номенклатура");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ДанныеДоговора.ДоговорОбслуживанияТарифныйПлан,
			ПутьКДанным, , Отказ);
	КонецЦикла;
	
КонецПроцедуры

Процедура РассчитатьСуммыВСтрокеТЧ(СтрокаТабличнойЧасти)
	
	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
	Если СуммаВключаетНДС Тогда
		СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.Сумма - (СтрокаТабличнойЧасти.Сумма) / ((СтавкаНДС + 100) / 100);
	Иначе
		СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.Сумма * СтавкаНДС / 100;
	КонецЕсли; 
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
КонецПроцедуры

Процедура ЗаполнитьЦенуВСтрокеТЧ(СтрокаТабличнойЧасти)
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
	СтруктураДанные.Вставить("Партия", СтрокаТабличнойЧасти.Партия);
	СтруктураДанные.Вставить("НалогообложениеНДС", НалогообложениеНДС);
	СтруктураДанные.Вставить("ДатаОбработки", Дата);
	СтруктураДанные.Вставить("ВалютаДокумента", ВалютаДокумента);
	СтруктураДанные.Вставить("СуммаВключаетНДС", СуммаВключаетНДС);
	СтруктураДанные.Вставить("ВидЦен", ВидЦен);
	СтруктураДанные.Вставить("Коэффициент", 1);
	СтрокаТабличнойЧасти.Цена = ЦенообразованиеСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураДанные);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли