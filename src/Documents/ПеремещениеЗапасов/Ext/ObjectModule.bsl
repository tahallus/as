#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ПоложениеЗаказаПокупателя <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = ЗаказПокупателя;
		КонецЦикла;	
	Иначе
		ЗаказПокупателя = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(Запасы, "ЗаказПокупателя");
	КонецЕсли;	
	
	Если НЕ Константы.ФункциональнаяОпцияУчетПоНесколькимНаправлениямДеятельности.Получить() Тогда
		
		Если СчетЗатрат.ТипСчета = Перечисления.ТипыСчетов.Расходы
			И (ВидОперации = Перечисления.ВидыОперацийПеремещениеЗапасов.СписаниеНаРасходы
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПеремещениеЗапасов.ПередачаВЭксплуатацию) Тогда
			
			НаправлениеДеятельности = Справочники.НаправленияДеятельности.ОсновноеНаправление;
			
		КонецЕсли;	
		
	КонецЕсли;
	
	Если ВидОперации <> Перечисления.ВидыОперацийПеремещениеЗапасов.ПередачаВЭксплуатацию
		И ВидОперации <> Перечисления.ВидыОперацийПеремещениеЗапасов.ВозвратИзЭксплуатации Тогда
		Спецоснастка.Очистить();
		ИнвентарьИХозяйственныеПринадлежности.Очистить();
	КонецЕсли; 
	
	// Прослеживаемость
	ТребуетсяПодобратьРНПТ = ПрослеживаемостьУНФ.ТребуетсяПодобратьРНПТ(Запасы, Дата, ПрослеживаемаяОперация());
	Если ТребуетсяПодобратьРНПТ Тогда
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			Если ВидОперации = Перечисления.ВидыОперацийПеремещениеЗапасов.ВозвратИзЭксплуатации Тогда
				ПрослеживаемостьУНФ.ПодобратьРНПТВозвратИзЭксплуатации(ЭтотОбъект, Отказ);
			Иначе
				ПрослеживаемостьУНФ.ПодобратьРНПТОстатки(ЭтотОбъект, Отказ);
			КонецЕсли; 
		КонецЕсли; 
	Иначе
		СведенияПрослеживаемости.Очистить();
		Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
			СтрокаТабличнойЧасти.ПрослеживаемыйТовар = Ложь;
		КонецЦикла; 
	КонецЕсли;
	
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		Если СтрокаТабличнойЧасти.ПрослеживаемыйТовар Тогда
			СтрокаТабличнойЧасти.НомерГТД = Ложь;
		КонецЕсли;
	КонецЦикла;
	// Конец Прослеживаемость
	
КонецПроцедуры // ПередЗаписью()

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	// Прослеживаемость
	СведенияПрослеживаемости.Очистить();
	// Конец Прослеживаемость
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
		ОснованиеПоложениеСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "ПоложениеСклада");
		Если ОснованиеПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			ТекстСообщения = НСтр("ru = 'Ввод перемещения недоступен при расположении склада в табличной части.'");
			ВызватьИсключение ТекстСообщения;
		КонецЕсли; 
	КонецЕсли; 
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("ДокументСсылка.АвансовыйОтчет")] = "ЗаполнитьПоАвансовомуОтчету";
	СтратегияЗаполнения[Тип("ДокументСсылка.ЗаказНаПроизводство")] = "ЗаполнитьПоЗаказуНаПроизводство";
	СтратегияЗаполнения[Тип("ДокументСсылка.ЗаказПокупателя")] = "ЗаполнитьПоЗаказуПокупателя";
	СтратегияЗаполнения[Тип("ДокументСсылка.ПеремещениеПоЯчейкам")] = "ЗаполнитьПоПеремещениюПоЯчейкам";
	СтратегияЗаполнения[Тип("ДокументСсылка.ПриходнаяНакладная")] = "ЗаполнитьПоПриходнойНакладной";
	СтратегияЗаполнения[Тип("ДокументСсылка.ПриходныйОрдер")] = "ЗаполнитьПоПриходномуОрдеру";
	СтратегияЗаполнения[Тип("ДокументСсылка.СборкаЗапасов")] = "ЗаполнитьПоСборкеЗапасов";
	СтратегияЗаполнения[Тип("ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС")] = "ЗаполнитьПоВходящейТранспортнойОперации";
	СтратегияЗаполнения[Тип("ДокументСсылка.ИсходящаяТранспортнаяОперацияВЕТИС")] = "ЗаполнитьПоИсходящейТранспортнойОперации";
	СтратегияЗаполнения[Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")] = "ЗаполнитьПоПередачеТоваровМеждуОрганизациями";
	СтратегияЗаполнения[Тип("ДокументСсылка.ЗаказНаПеремещение")] = "ЗаполнитьПоЗаказНаПеремещение";
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения, "ЗаказПокупателя");
	
	ЗаполнитьПодписиКладовщиков();
	
	// Прослеживаемость
	Если ПрослеживаемаяОперация() Тогда
		ПрослеживаемостьУНФ.ОбновитьПризнакПрослеживаемости(Запасы, Дата);
	Иначе
		ПрослеживаемостьУНФ.ОчиститьДанныеПрослеживаемости(Запасы, СведенияПрослеживаемости);
	КонецЕсли;
	// Конец Прослеживаемость 
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Проверка наличия розничной цены.
	ПроверитьНаличиеРозничнойЦены(Отказ);
	
	Если Запасы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийПеремещениеЗапасов.ПередачаВЭксплуатацию
	 ИЛИ ВидОперации = Перечисления.ВидыОперацийПеремещениеЗапасов.СписаниеНаРасходы Тогда
		ПроверяемыеРеквизиты.Добавить("СчетЗатрат");
	КонецЕсли;
	
	УказаноКоличествоРезерваНоНеУказанЗаказ = НСтр("ru = 'В строке указано количество резерва, но не указан заказ.'");

	Если ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		
		Для каждого СтрокаЗапасы Из Запасы Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаЗапасы.ЗаказПокупателя) И СтрокаЗапасы.Резерв > 0 Тогда
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки,
					"Резерв");
				ОбщегоНазначения.СообщитьПользователю(УказаноКоличествоРезерваНоНеУказанЗаказ, ЭтотОбъект,
					КонтекстноеПоле, , Отказ);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
		
		Для каждого СтрокаЗапасы Из Запасы Цикл
			
			Если НЕ ЗначениеЗаполнено(ЗаказПокупателя) И СтрокаЗапасы.Резерв > 0 Тогда
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки,
					"Резерв");
				ОбщегоНазначения.СообщитьПользователю(УказаноКоличествоРезерваНоНеУказанЗаказ, ЭтотОбъект,
					КонтекстноеПоле, , Отказ);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Константы.ФункциональнаяОпцияРезервированиеЗапасов.Получить() 
		И (ВидОперации = Перечисления.ВидыОперацийПеремещениеЗапасов.Перемещение
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПеремещениеЗапасов.СписаниеНаРасходы) Тогда
		
		Для каждого СтрокаЗапасы Из Запасы Цикл
			
			Если СтрокаЗапасы.Резерв > СтрокаЗапасы.Количество Тогда
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'В строке №%1 табл. части ""Запасы"" количество передаваемых в резерв позиций превышает общее количество запасов.'"),
					СтрокаЗапасы.НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Серии номенклатуры
	Если НЕ (ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница,"ОрдерныйСклад") = Истина 
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиницаПолучатель,"ОрдерныйСклад") = Истина) Тогда
		СерииНоменклатурыУНФ.ПроверкаЗаполненияСерийНоменклатуры(Отказ, Запасы, СерииНоменклатуры, СтруктурнаяЕдиница, ЭтотОбъект);
		СерииНоменклатурыУНФ.ПроверкаЗаполненияСерийНоменклатуры(Отказ, Запасы, СерииНоменклатуры, СтруктурнаяЕдиницаПолучатель, ЭтотОбъект);
	КонецЕсли;
	
	НоменклатураВДокументахСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, Отказ, Истина);
	
	// Подарочные сертификаты
	РаботаСПодарочнымиСертификатами.ПроверитьВозможностьИспользованияСертификатов(Отказ, ЭтотОбъект, "Запасы", "СтруктурнаяЕдиницаПолучатель", Истина);
	
	Если НЕ ВидОперации = Перечисления.ВидыОперацийПеремещениеЗапасов.Перемещение Тогда
		
		ИменаТабличныхЧастей = Новый Массив;
		ИменаТабличныхЧастей.Добавить("Запасы");
		ИменаТабличныхЧастей.Добавить("Спецоснастка");
		ИменаТабличныхЧастей.Добавить("ИнвентарьИХозяйственныеПринадлежности");
		
		ГрузовыеТаможенныеДекларацииСервер.ПриОбработкеПроверкиЗаполнения(Отказ, ЭтотОбъект, ИменаТабличныхЧастей);
		
	КонецЕсли;
	
	// Прослеживаемость
	Если ВидОперации = Перечисления.ВидыОперацийПеремещениеЗапасов.СписаниеНаРасходы Тогда
		ВестиУчетПрослеживаемыхТоваров = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеКонстанты("ВестиУчетПрослеживаемыхТоваров");
		МассивСтрокПрослеживаемыхТоваров = Запасы.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
		ЕстьПрослеживаемыйТовар = МассивСтрокПрослеживаемыхТоваров.Количество() > 0;
		Если ЕстьПрослеживаемыйТовар И Не ЗначениеЗаполнено(КодОперацииПрослеживаемости)
			И ВестиУчетПрослеживаемыхТоваров Тогда
			ТекстОшибки = НСтр("ru='Не выбрана причина списания в прослеживаемости'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка, "КодОперацииПрослеживаемости", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.ПеремещениеЗапасов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыКРасходуСоСкладов", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыКПоступлениюНаСклады", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыНаСкладах", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("Запасы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыВРазрезеГТД", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("СуммовойУчетВРознице", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗаказыНаПеремещение", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("РазмещениеЗаказов", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьУправленческий(ДополнительныеСвойства, Движения, Отказ);

	// СерииНоменклатуры
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатурыГарантии", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДвиженияСерииНоменклатуры", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатуры", ТаблицыДляДвижений, Движения, Отказ);
	
	// Прослеживаемость
	УправлениеНебольшойФирмойСервер.ОтразитьПрослеживаемыеТовары(ДополнительныеСвойства, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ОперацииСПрослеживаемымиТоварами", ТаблицыДляДвижений, Движения, Отказ);
	
	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	// Контроль
	Документы.ПеремещениеЗапасов.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	// Контроль
	Документы.ПеремещениеЗапасов.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ, Истина);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	// Прослеживаемость
	СведенияПрослеживаемости.Очистить();
	Если ПрослеживаемаяОперация() Тогда
		ПрослеживаемостьУНФ.ОбновитьПризнакПрослеживаемости(Запасы, Дата);
	Иначе
		ПрослеживаемостьУНФ.ОчиститьДанныеПрослеживаемости(Запасы, СведенияПрослеживаемости);
	КонецЕсли;
	// Конец Прослеживаемость
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// АПК:299-выкл процедуры вызываются см. ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент

Процедура ЗаполнитьПоАвансовомуОтчету(АвансовыйОтчет) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	АвансовыйОтчетЗапасы.Ссылка КАК ДокументОснование,
	|	АвансовыйОтчетЗапасы.Ссылка.Организация КАК Организация,
	|	АвансовыйОтчетЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	АвансовыйОтчетЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	АвансовыйОтчетЗапасы.Ячейка КАК Ячейка
	|ИЗ
	|	Документ.АвансовыйОтчет.Запасы КАК АвансовыйОтчетЗапасы
	|ГДЕ
	|	АвансовыйОтчетЗапасы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АвансовыйОтчетЗапасы.Ссылка КАК Ссылка,
	|	АвансовыйОтчетЗапасы.НомерСтроки КАК НомерСтроки,
	|	АвансовыйОтчетЗапасы.Номенклатура КАК Номенклатура,
	|	АвансовыйОтчетЗапасы.Характеристика КАК Характеристика,
	|	АвансовыйОтчетЗапасы.Партия КАК Партия,
	|	АвансовыйОтчетЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	АвансовыйОтчетЗапасы.Количество КАК Количество,
	|	АвансовыйОтчетЗапасы.Сумма КАК Сумма,
	|	АвансовыйОтчетЗапасы.ЗаказПокупателя КАК ЗаказПокупателя
	|ИЗ
	|	Документ.АвансовыйОтчет.Запасы КАК АвансовыйОтчетЗапасы
	|ГДЕ
	|	АвансовыйОтчетЗапасы.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", АвансовыйОтчет);
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = РезультатыЗапроса[0].Выбрать();
	ВыборкаШапка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	
	Запасы.Загрузить(РезультатыЗапроса[1].Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуНаПроизводство(ДанныеЗаполнения) Экспорт
	
	ДанныеЗаполненияВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "ВидОперации");
	
	Если ДанныеЗаполненияВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Сборка Тогда
		ЗаполнитьПоЗаказуНаПроизводствоСборка(ДанныеЗаполнения);
	КонецЕсли;
	
	Если ДанныеЗаполненияВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка Тогда
		ЗаполнитьПоЗаказуНаПроизводствоРазборка(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуПокупателя(ДокументСсылкаЗаказ) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	&Ссылка КАК ДокументОснование,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.СтруктурнаяЕдиницаРезерв КАК СтруктурнаяЕдиница,
	|	ТаблицаДокумента.Ячейка КАК ЯчейкаПолучатель,
	|	ТаблицаДокумента.ОжидаетсяВыборВариантаКП КАК ОжидаетсяВыборВариантаКП,
	|	ТаблицаДокумента.Грузоотправитель КАК Грузоотправитель,
	|	ТаблицаДокумента.Грузополучатель КАК Грузополучатель,
	|	ТаблицаДокумента.АдресДоставки КАК АдресДоставки,
	|	ТаблицаДокумента.Проект КАК Проект
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧастьЗапасы.Ссылка КАК ЗаказПокупателя,
	|	ТабличнаяЧастьЗапасы.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧастьЗапасы.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧастьЗапасы.Характеристика КАК Характеристика,
	|	ТабличнаяЧастьЗапасы.Партия КАК Партия,
	|	ТабличнаяЧастьЗапасы.СерииНоменклатуры КАК СерииНоменклатуры,
	|	ТабличнаяЧастьЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТабличнаяЧастьЗапасы.Количество КАК Количество,
	|	ТабличнаяЧастьЗапасы.Резерв КАК Резерв,
	|	ТабличнаяЧастьЗапасы.КлючСвязи КАК КлючСвязи,
	|	ТабличнаяЧастьЗапасы.Содержание КАК Содержание
	|ИЗ
	|	ВТЗаказПокупателяЗапасы КАК ТабличнаяЧастьЗапасы
	|ГДЕ
	|	ТабличнаяЧастьЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗаказ);
	
	Документы.ЗаказПокупателя.ДобавитьТаблицуЗапасыВМенеджерВременныхТаблиц(ДокументСсылкаЗаказ, Запрос.МенеджерВременныхТаблиц, Ложь);
	
	Если ПолучитьФункциональнуюОпцию("РезервированиеЗапасов") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Ссылка КАК ДокументОснование", "&Ссылка КАК ЗаказПокупателя");
	КонецЕсли;
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	Если РезультатыЗапроса[0].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаШапка = РезультатыЗапроса[0].Выбрать();
	ВыборкаШапка.Следующий();
	
	ЗначенияРеквизитов = Новый Структура("ОжидаетсяВыборВариантаКП", ВыборкаШапка.ОжидаетсяВыборВариантаКП);
	Документы.ЗаказПокупателя.ПроверитьВозможностьВводаНаОснованииЗаказаПокупателя(ДокументСсылкаЗаказ, ЗначенияРеквизитов);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
	
	Запасы.Загрузить(РезультатыЗапроса[1].Выгрузить());
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") Тогда
		
		СерииНоменклатуры.Очистить();
		
		ТаблицаСерииНоменклатуры = ДокументСсылкаЗаказ.СерииНоменклатуры.Выгрузить();
		Для Каждого СтрокаСерия Из ТаблицаСерииНоменклатуры Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаСерия.КлючСвязи) Тогда
				Продолжить;
			КонецЕсли;
			СтрокиЗапасы = ТабличныеЧастиУНФКлиентСервер.СтрокиПоКлючуСвязи(Запасы, СтрокаСерия.КлючСвязи);
			Если СтрокиЗапасы.Количество()=0 Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = СерииНоменклатуры.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСерия);
		КонецЦикла;
		
		СерииНоменклатурыУНФ.УдалитьСерииНоменклатурыВТабличнойЧастиВЗависимостиОтПолитики(ЭтотОбъект);
		
	КонецЕсли;
	
	Если ПоложениеПроекта = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для каждого СтрокаЗапасов Из Запасы Цикл
			СтрокаЗапасов.Проект = Проект;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоПеремещениюПоЯчейкам(ДанныеЗаполнения) Экспорт
	
	Запрос = Новый Запрос( 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПеремещениеПоЯчейкам.Ссылка КАК ДокументОснование,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПеремещениеЗапасов.Перемещение) КАК ВидОперации,
	|	ПеремещениеПоЯчейкам.Организация КАК Организация,
	|	ПеремещениеПоЯчейкам.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ПеремещениеПоЯчейкам.Ячейка КАК Ячейка,
	|	ВЫБОР
	|		КОГДА ПеремещениеПоЯчейкам.СтруктурнаяЕдиница.ПолучательПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|				ИЛИ ПеремещениеПоЯчейкам.СтруктурнаяЕдиница.ПолучательПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ПеремещениеПоЯчейкам.СтруктурнаяЕдиница.ПолучательПеремещения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК СтруктурнаяЕдиницаПолучатель,
	|	ВЫБОР
	|		КОГДА ПеремещениеПоЯчейкам.СтруктурнаяЕдиница.ПолучательПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|				ИЛИ ПеремещениеПоЯчейкам.СтруктурнаяЕдиница.ПолучательПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ПеремещениеПоЯчейкам.СтруктурнаяЕдиница.ЯчейкаПолучателяПеремещения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|	КОНЕЦ КАК ЯчейкаПолучатель,
	|	ПеремещениеПоЯчейкам.Запасы.(
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Партия КАК Партия,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество КАК Количество,
	|		СерииНоменклатуры КАК СерииНоменклатуры,
	|		КлючСвязи КАК КлючСвязи
	|	) КАК Запасы
	|ИЗ
	|	Документ.ПеремещениеПоЯчейкам КАК ПеремещениеПоЯчейкам
	|ГДЕ
	|	ПеремещениеПоЯчейкам.Ссылка = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
		ВыборкаИзРезультатаЗапроса.Следующий();
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаИзРезультатаЗапроса);
		Запасы.Загрузить(ВыборкаИзРезультатаЗапроса.Запасы.Выгрузить());
		ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "Запасы", "ЗаказПокупателя", "ПоложениеЗаказаПокупателя");
		
	КонецЕсли;

	СерииНоменклатурыУНФ.ЗаполнитьТЧСерииНоменклатурыПоКлючуСвязи(ЭтотОбъект, ДанныеЗаполнения);
	СерииНоменклатурыУНФ.УдалитьСерииНоменклатурыВТабличнойЧастиВЗависимостиОтПолитики(ЭтотОбъект);
	
КонецПроцедуры

Процедура ЗаполнитьПоПриходнойНакладной(ДокументСсылкаПриходнаяНакладная) Экспорт
	
	Запрос = Новый Запрос();
	
	Запрос.УстановитьПараметр("РезервированиеЗапасов", ПолучитьФункциональнуюОпцию("РезервированиеЗапасов"));
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриходнаяНакладная);
	Запрос.УстановитьПараметр("Дата", ТекущаяДатаСеанса());
	
	// Заполним данные шапки документа.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Ссылка КАК ДокументОснование,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Номер КАК НомерВходящегоДокумента,
	|	ТаблицаДокумента.Дата КАК ДатаВходящегоДокумента,
	|	ТаблицаДокумента.ВалютаДокумента КАК ВалютаДенежныхСредств,
	|	ТаблицаДокумента.Контрагент КАК Контрагент,
	|	ТаблицаДокумента.Договор КАК Договор,
	|	ТаблицаДокумента.СуммаДокумента КАК СуммаДокумента,
	|	ТаблицаДокумента.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаДокумента.Ячейка КАК Ячейка,
	|	ТаблицаДокумента.СведенияПрослеживаемости.(
	|		НомерСтроки КАК НомерСтроки,
	|		РНПТ КАК РНПТ,
	|		Количество КАК Количество,
	|		КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|		ИдентификаторСтроки КАК ИдентификаторСтроки
	|	) КАК СведенияПрослеживаемости,
	|	ТаблицаДокумента.Запасы.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Партия КАК Партия,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество КАК Количество,
	|		Цена КАК Цена,
	|		Сумма КАК Сумма,
	|		СтавкаНДС КАК СтавкаНДС,
	|		СуммаНДС КАК СуммаНДС,
	|		ВЫБОР
	|			КОГДА НЕ &РезервированиеЗапасов
	|				ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			КОГДА ТаблицаДокумента.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию))
	|				ТОГДА ТаблицаДокумента.Запасы.ЗаказПокупателя
	|			ИНАЧЕ ТаблицаДокумента.Запасы.Заказ
	|		КОНЕЦ КАК ЗаказПокупателя,
	|		Всего КАК Всего,
	|		Себестоимость КАК Себестоимость,
	|		СуммаРасходов КАК СуммаРасходов,
	|		Содержание КАК Содержание,
	|		СерииНоменклатуры КАК СерииНоменклатуры,
	|		ИдентификаторСтроки КАК ИдентификаторСтроки,
	|		КлючСвязи КАК КлючСвязи
	|	) КАК Запасы
	|ИЗ
	|	Документ.ПриходнаяНакладная КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	Запасы.Загрузить(Выборка.Запасы.Выгрузить());
	
	Если НЕ ЗначениеЗаполнено(ПоложениеЗаказаПокупателя) Тогда
		ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
	КонецЕсли; 
	Если ПолучитьФункциональнуюОпцию("РезервированиеЗапасов") И ПоложениеЗаказаПокупателя=Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
		Если Запасы.Количество() > 0 Тогда
			ЗаказПокупателя = Запасы[0].ЗаказПокупателя;
		КонецЕсли; 
		Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
			Если СтрокаТабличнойЧасти.ЗаказПокупателя<>ЗаказПокупателя Тогда
				ЗаказПокупателя = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(Запасы, "ЗаказПокупателя");
				ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя) Тогда
				СтрокаТабличнойЧасти.Резерв = СтрокаТабличнойЧасти.Количество;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
	// Прослеживаемость
	Если ПрослеживаемаяОперация()
		И ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")	Тогда
		СведенияПрослеживаемости.Загрузить(Выборка.СведенияПрослеживаемости.Выгрузить());
	Иначе
		СведенияПрослеживаемости.Очистить();
	КонецЕсли;
	// Конец Прослеживаемость
	
	СерииНоменклатурыУНФ.ЗаполнитьТЧСерииНоменклатурыПоКлючуСвязи(ЭтотОбъект, ДокументСсылкаПриходнаяНакладная);
	СерииНоменклатурыУНФ.УдалитьСерииНоменклатурыВТабличнойЧастиВЗависимостиОтПолитики(ЭтотОбъект);
	
КонецПроцедуры // ЗаполнитьПоПриходнаяНакладная()

Процедура ЗаполнитьПоПриходномуОрдеру(ДанныеЗаполнения) Экспорт
	
	Организация = ДанныеЗаполнения.Организация;
	ВидОперации = Перечисления.ВидыОперацийПеремещениеЗапасов.Перемещение;
	ДокументОснование = ДанныеЗаполнения.Ссылка;
	СтруктурнаяЕдиницаПолучатель = ДанныеЗаполнения.СтруктурнаяЕдиница;
	ЯчейкаПолучатель = ДанныеЗаполнения.Ячейка;
	
	Запасы.Очистить();
	Для каждого ТекСтрокаЗапасы Из ДанныеЗаполнения.Запасы Цикл
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаЗапасы);
	КонецЦикла;
	ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "Запасы", "ЗаказПокупателя", "ПоложениеЗаказаПокупателя");

	СерииНоменклатурыУНФ.ЗаполнитьТЧСерииНоменклатурыПоКлючуСвязи(ЭтотОбъект, ДанныеЗаполнения);
	СерииНоменклатурыУНФ.УдалитьСерииНоменклатурыВТабличнойЧастиВЗависимостиОтПолитики(ЭтотОбъект);
	
КонецПроцедуры

Процедура ЗаполнитьПоСборкеЗапасов(ДанныеЗаполнения) Экспорт
	
	ДанныеЗаполненияВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "ВидОперации");
	
	Если ДанныеЗаполненияВидОперации = Перечисления.ВидыОперацийСборкаЗапасов.Сборка Тогда
		ЗаполнитьПоСборкеЗапасовСборка(ДанныеЗаполнения);
	КонецЕсли;
	
	Если ДанныеЗаполненияВидОперации = Перечисления.ВидыОперацийСборкаЗапасов.Разборка Тогда
		ЗаполнитьПоСборкеЗапасовРазборка(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоПередачеТоваровМеждуОрганизациями(ДанныеЗаполнения) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПередачаТоваровМеждуОрганизациями.Ссылка КАК ДокументОснование,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПеремещениеЗапасов.Перемещение) КАК ВидОперации,
	|	ПередачаТоваровМеждуОрганизациями.ОрганизацияПолучатель КАК Организация,
	|	ПередачаТоваровМеждуОрганизациями.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ПередачаТоваровМеждуОрганизациями.Ячейка КАК Ячейка,
	|	ПередачаТоваровМеждуОрганизациями.Запасы.(
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Партия КАК Партия,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество КАК Количество,
	|		КлючСвязи КАК КлючСвязи
	|	) КАК Запасы
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК ПередачаТоваровМеждуОрганизациями
	|ГДЕ
	|	ПередачаТоваровМеждуОрганизациями.Ссылка = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	Запасы.Очистить();
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
		ВыборкаИзРезультатаЗапроса.Следующий();
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаИзРезультатаЗапроса);
		
		ВыборкаЗапасы = ВыборкаИзРезультатаЗапроса.Запасы.Выбрать();
		Пока ВыборкаЗапасы.Следующий() Цикл
			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЗапасы);
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПоЗаказНаПеремещение(ДокументСсылкаЗаказ) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	&Ссылка КАК ДокументОснование,
	|	&Ссылка КАК ЗаказНаПеремещение,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.СтруктурнаяЕдиницаРезерв КАК СтруктурнаяЕдиница,
	|	ТаблицаДокумента.СтруктурнаяЕдиницаПолучатель КАК СтруктурнаяЕдиницаПолучатель,
	|	ТаблицаДокумента.СтруктурнаяЕдиницаПолучатель.ПодписьМОЛ КАК ПодписьКладовщика,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ТаблицаДокумента.ПоложениеЗаказаПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВШапке)
	|	КОНЕЦ КАК ПоложениеЗаказаПокупателя,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ТаблицаДокумента.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаказПокупателя
	|ИЗ
	|	Документ.ЗаказНаПеремещение КАК ТаблицаДокумента,
	|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧастьЗапасы.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧастьЗапасы.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧастьЗапасы.Характеристика КАК Характеристика,
	|	ТабличнаяЧастьЗапасы.Партия КАК Партия,
	|	ТабличнаяЧастьЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧастьЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|			ТОГДА ВЫРАЗИТЬ(ТабличнаяЧастьЗапасы.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Коэффициент,
	|	ТабличнаяЧастьЗапасы.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА 0
	|		КОГДА ТабличнаяЧастьЗапасы.Ссылка.НеРазмещатьЗаказыПокупателей
	|			ТОГДА ТабличнаяЧастьЗапасы.Количество
	|		ИНАЧЕ ТабличнаяЧастьЗапасы.Резерв
	|	КОНЕЦ КАК Резерв,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ТабличнаяЧастьЗапасы.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ТабличнаяЧастьЗапасы.Ссылка.СтруктурнаяЕдиницаРезерв КАК СтруктурнаяЕдиницаРезерв
	|ПОМЕСТИТЬ Запасы
	|ИЗ
	|	Документ.ЗаказНаПеремещение.Запасы КАК ТабличнаяЧастьЗапасы,
	|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
	|ГДЕ
	|	ТабличнаяЧастьЗапасы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Запасы.НомерСтроки КАК НомерСтроки,
	|	Запасы.Номенклатура КАК Номенклатура,
	|	Запасы.Характеристика КАК Характеристика,
	|	Запасы.Партия КАК Партия,
	|	Запасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Запасы.Коэффициент КАК Коэффициент,
	|	Запасы.Количество КАК Количество,
	|	Запасы.Резерв КАК Резерв,
	|	Запасы.ЗаказПокупателя КАК ЗаказПокупателя
	|ИЗ
	|	Запасы КАК Запасы
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	СУММА(ВложенныйЗапрос.Остаток) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказыНаПеремещениеОстатки.Номенклатура КАК Номенклатура,
	|		ЗаказыНаПеремещениеОстатки.Характеристика КАК Характеристика,
	|		ЗаказыНаПеремещениеОстатки.КоличествоОстаток КАК Остаток
	|	ИЗ
	|		РегистрНакопления.ЗаказыНаПеремещение.Остатки(
	|				,
	|				ТипДвижения = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженийЗапасов.Поступление)
	|					И ЗаказНаПеремещение = &Ссылка) КАК ЗаказыНаПеремещениеОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗаказыНаПеремещение.Номенклатура,
	|		ЗаказыНаПеремещение.Характеристика,
	|		ВЫБОР
	|			КОГДА ЗаказыНаПеремещение.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЗаказыНаПеремещение.Количество
	|			ИНАЧЕ -ЗаказыНаПеремещение.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ЗаказыНаПеремещение КАК ЗаказыНаПеремещение
	|	ГДЕ
	|		ЗаказыНаПеремещение.ТипДвижения = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженийЗапасов.Поступление)
	|		И ЗаказыНаПеремещение.ЗаказНаПеремещение = &Ссылка
	|		И ЗаказыНаПеремещение.Регистратор = &Регистратор) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Остаток) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	СУММА(ВложенныйЗапрос.Остаток) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		РазмещениеЗаказовОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|		РазмещениеЗаказовОстатки.Номенклатура КАК Номенклатура,
	|		РазмещениеЗаказовОстатки.Характеристика КАК Характеристика,
	|		РазмещениеЗаказовОстатки.КоличествоОстаток КАК Остаток
	|	ИЗ
	|		РегистрНакопления.РазмещениеЗаказов.Остатки(, ИсточникОбеспечения = &Ссылка) КАК РазмещениеЗаказовОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РазмещениеЗаказов.ЗаказПокупателя,
	|		РазмещениеЗаказов.Номенклатура,
	|		РазмещениеЗаказов.Характеристика,
	|		ВЫБОР
	|			КОГДА РазмещениеЗаказов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА РазмещениеЗаказов.Количество
	|			ИНАЧЕ -РазмещениеЗаказов.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РазмещениеЗаказов КАК РазмещениеЗаказов
	|	ГДЕ
	|		РазмещениеЗаказов.Регистратор = &Регистратор
	|		И РазмещениеЗаказов.ИсточникОбеспечения = &Ссылка) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.ЗаказПокупателя,
	|	ВложенныйЗапрос.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Остаток) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	ВложенныйЗапрос.Партия КАК Партия,
	|	ВложенныйЗапрос.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СУММА(ВложенныйЗапрос.Остаток) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыОстатки.Характеристика КАК Характеристика,
	|		ЗапасыОстатки.Партия КАК Партия,
	|		ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|		ЗапасыОстатки.КоличествоОстаток КАК Остаток
	|	ИЗ
	|		РегистрНакопления.Запасы.Остатки(
	|				,
	|				(СтруктурнаяЕдиница, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
	|					(ВЫБРАТЬ
	|						Запасы.СтруктурнаяЕдиницаРезерв,
	|						Запасы.Номенклатура,
	|						Запасы.Характеристика,
	|						Запасы.Партия,
	|						Запасы.ЗаказПокупателя
	|					ИЗ
	|						Запасы
	|					ГДЕ
	|						Запасы.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка))) КАК ЗапасыОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Запасы.Номенклатура,
	|		Запасы.Характеристика,
	|		Запасы.Партия,
	|		Запасы.ЗаказПокупателя,
	|		Запасы.Количество
	|	ИЗ
	|		РегистрНакопления.Запасы КАК Запасы
	|	ГДЕ
	|		Запасы.Регистратор = &Регистратор
	|		И (Запасы.СтруктурнаяЕдиница, Запасы.Номенклатура, Запасы.Характеристика, Запасы.Партия, Запасы.ЗаказПокупателя) В
	|				(ВЫБРАТЬ
	|					Запасы.СтруктурнаяЕдиницаРезерв,
	|					Запасы.Номенклатура,
	|					Запасы.Характеристика,
	|					Запасы.Партия,
	|					Запасы.ЗаказПокупателя
	|				ИЗ
	|					Запасы
	|				ГДЕ
	|					Запасы.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка))) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Партия,
	|	ВложенныйЗапрос.ЗаказПокупателя,
	|	ВложенныйЗапрос.Характеристика
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Остаток) > 0");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗаказ);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	Если РезультатыЗапроса[0].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаШапка = РезультатыЗапроса[0].Выбрать();
	ВыборкаШапка.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	
	МассивВладельцев = Новый Массив;
	МассивВладельцев.Добавить(СтруктурнаяЕдиницаПолучатель);
	Адреса = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(МассивВладельцев, , Справочники.ВидыКонтактнойИнформации.ФактАдресСтруктурнойЕдиницы);
	Если Адреса.Количество() > 0 Тогда
		АдресДоставки = Адреса[0].Представление;
	КонецЕсли;
	
	Запасы.Очистить();
	ВыборкаЗапасы = РезультатыЗапроса[2].Выбрать();
	ТаблицаОстатки = РезультатыЗапроса[3].Выгрузить();
	ТаблицаРазмещение = РезультатыЗапроса[4].Выгрузить();
	ТаблицаРезерв = РезультатыЗапроса[5].Выгрузить();
	
	СтруктураПоиска = Новый Структура;
	Пока ВыборкаЗапасы.Следующий() Цикл
		
		СтруктураПоиска.Очистить();
		СтруктураПоиска.Вставить("Номенклатура", ВыборкаЗапасы.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", ВыборкаЗапасы.Характеристика);
		СтрокиОстатки = ТаблицаОстатки.НайтиСтроки(СтруктураПоиска);
		Если ЗначениеЗаполнено(ВыборкаЗапасы.ЗаказПокупателя) Тогда
			СтруктураПоиска.Вставить("ЗаказПокупателя", ВыборкаЗапасы.ЗаказПокупателя);
			СтрокиРазмещение = ТаблицаРазмещение.НайтиСтроки(СтруктураПоиска);
		Иначе
			СтрокиРазмещение = Новый Массив;
		КонецЕсли;
		
		Доступно = 0;
		Для каждого СтрокаОстаток Из СтрокиОстатки Цикл
			Доступно = Доступно + СтрокаОстаток.Остаток;
		КонецЦикла; 
		КПеремещению = Мин(Доступно, ВыборкаЗапасы.Количество * ВыборкаЗапасы.Коэффициент);
		
		Размещено = 0;
		Для каждого СтрокаОстаток Из СтрокиРазмещение Цикл
			Размещено = Размещено + СтрокаОстаток.Остаток;
		КонецЦикла;
		
		Резерв = 0;
		Если ЗначениеЗаполнено(ВыборкаЗапасы.ЗаказПокупателя) Тогда
			СтруктураПоиска.Вставить("Партия", ВыборкаЗапасы.Партия);
			СтрокиРезерв = ТаблицаРезерв.НайтиСтроки(СтруктураПоиска);
			Для каждого СтрокаОстаток Из СтрокиРезерв Цикл
				Резерв = Резерв + СтрокаОстаток.Остаток;
			КонецЦикла; 
		КонецЕсли;
		
		Разница = КПеремещению - Резерв - Размещено;
		Если ЗначениеЗаполнено(ВыборкаЗапасы.ЗаказПокупателя) И КПеремещению > Резерв + Размещено
			И Разница > 0 Тогда
			
			// Следует разделить строку, часть запасов перемещается без резервирования
			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЗапасы);
			НоваяСтрока.ЗаказПокупателя = Неопределено;
			Если ПоложениеЗаказаПокупателя <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
				ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
			КонецЕсли;
			НоваяСтрока.Количество = Разница / ВыборкаЗапасы.Коэффициент;
			НоваяСтрока.Резерв = 0;
			КПеремещению = Резерв + Размещено;
			Зачтено = НоваяСтрока.Количество;
			УменьшитьОстатки(СтрокиОстатки, Зачтено);
		КонецЕсли;
		
		Если КПеремещению <= 0 Тогда
			Продолжить;
		КонецЕсли; 
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЗапасы);
		НоваяСтрока.Количество = КПеремещению / ВыборкаЗапасы.Коэффициент;
		НоваяСтрока.Резерв = Резерв / ВыборкаЗапасы.Коэффициент;
		
		Зачтено = КПеремещению;
		УменьшитьОстатки(СтрокиОстатки, Зачтено);
		Если ЗначениеЗаполнено(ВыборкаЗапасы.ЗаказПокупателя) И ВыборкаЗапасы.Резерв > 0 Тогда
			Зачтено = КПеремещению - Резерв;
			УменьшитьОстатки(СтрокиРазмещение, Зачтено);
			Зачтено = Резерв;
			УменьшитьОстатки(СтрокиРезерв, Зачтено);
		КонецЕсли;
		
	КонецЦикла; 
	
КонецПроцедуры 

// АПК:299-вкл

// Заполнение документа на основании входящей транспортной операции ВЕТИС.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//	
Процедура ЗаполнитьПоВходящейТранспортнойОперации(ДанныеЗаполнения) Экспорт
	
	Реквизиты = ИнтеграцияВЕТИСУНФ.ДанныеПрикладныхДокументовИзВходящейТранспортнойОперацииВЕТИС(ДанныеЗаполнения);
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты);
		СтруктурнаяЕдиница = Реквизиты.ГрузоотправительПредприятие;
		
	КонецЕсли;
	
	Товары = Реквизиты.Товары.Выгрузить();
	ТоварыУточнение = Реквизиты.ТоварыУточнение.Выгрузить();
	
	Товары.Индексы.Добавить("ИдентификаторСтроки");
	
	Для Каждого СтрокаТовары Из ТоварыУточнение Цикл 
		
		ИсходнаяСтрока = Товары.НайтиСтроки(Новый Структура("ИдентификаторСтроки", СтрокаТовары.ИдентификаторСтроки));
		Если ИсходнаяСтрока.Количество() = 0 Тогда 
			ИсходнаяСтрока = Новый Структура;
		Иначе 
			ИсходнаяСтрока = ИсходнаяСтрока[0];
			ИсходнаяСтрока.БылоУточнение = Истина;
		КонецЕсли;
		
		НоваяСтрокаЗапасы = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаЗапасы, ИсходнаяСтрока);
		ЗаполнитьЗначенияСвойств(НоваяСтрокаЗапасы, СтрокаТовары);
		
	КонецЦикла;
	
	Для Каждого СтрокаТовары Из Товары.НайтиСтроки(Новый Структура("БылоУточнение", Ложь)) Цикл
		
		НоваяСтрокаЗапасы = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаЗапасы, СтрокаТовары);
		
	КонецЦикла;
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "Запасы", "ЗаказПокупателя", "ПоложениеЗаказаПокупателя");
	
КонецПроцедуры

// Заполнение документа на основании исходящей транспортной операции ВЕТИС.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//	
Процедура ЗаполнитьПоИсходящейТранспортнойОперации(ДанныеЗаполнения) Экспорт
	
	Реквизиты = ИнтеграцияВЕТИСУНФ.ДанныеПрикладныхДокументовИзИсходящейТранспортнойОперацииВЕТИС(ДанныеЗаполнения);
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты);
		
	КонецЕсли;
	
	Товары = Реквизиты.Товары.Выгрузить();
	
	Запасы.Очистить();
	
	Для Каждого СтрокаТовары Из Товары Цикл
		
		НоваяСтрокаЗапасы = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаЗапасы, СтрокаТовары);
		
	КонецЦикла;
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "Запасы", "ЗаказПокупателя", "ПоложениеЗаказаПокупателя");
	
КонецПроцедуры

// Процедура заполняет табличную часть Запасы по остаткам на складе.
//
Процедура ЗаполнитьЗапасыПоОстаткамНаСкладе(ИмяТабличнойЧасти = "Запасы") Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗапасыНаСкладахОстатки.Организация КАК Организация,
	|	ЗапасыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыНаСкладахОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗапасыНаСкладахОстатки.Характеристика КАК Характеристика,
	|	ЗапасыНаСкладахОстатки.Партия КАК Партия,
	|	ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗапасыНаСкладахОстатки.Ячейка КАК Ячейка,
	|	СУММА(ЗапасыНаСкладахОстатки.КоличествоОстаток) КАК Количество
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыНаСкладах.Организация КАК Организация,
	|		ЗапасыНаСкладах.Номенклатура КАК Номенклатура,
	|		ЗапасыНаСкладах.Характеристика КАК Характеристика,
	|		ЗапасыНаСкладах.Партия КАК Партия,
	|		ЗапасыНаСкладах.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗапасыНаСкладах.Ячейка КАК Ячейка,
	|		ЗапасыНаСкладах.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.ЗапасыНаСкладах.Остатки(
	|				,
	|				Организация = &Организация
	|					И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|					И Ячейка = &Ячейка) КАК ЗапасыНаСкладах
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаЗапасыНаСкладах.Организация,
	|		ДвиженияДокументаЗапасыНаСкладах.Номенклатура,
	|		ДвиженияДокументаЗапасыНаСкладах.Характеристика,
	|		ДвиженияДокументаЗапасыНаСкладах.Партия,
	|		ДвиженияДокументаЗапасыНаСкладах.СтруктурнаяЕдиница,
	|		ДвиженияДокументаЗапасыНаСкладах.Ячейка,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаЗапасыНаСкладах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗапасыНаСкладах.Количество, 0)
	|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗапасыНаСкладах.Количество, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ЗапасыНаСкладах КАК ДвиженияДокументаЗапасыНаСкладах
	|	ГДЕ
	|		ДвиженияДокументаЗапасыНаСкладах.Регистратор = &Ссылка
	|		И ДвиженияДокументаЗапасыНаСкладах.Период <= &Период
	|		И ДвиженияДокументаЗапасыНаСкладах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)) КАК ЗапасыНаСкладахОстатки
	|ГДЕ
	|	ЗапасыНаСкладахОстатки.КоличествоОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыНаСкладахОстатки.Организация,
	|	ЗапасыНаСкладахОстатки.Номенклатура,
	|	ЗапасыНаСкладахОстатки.Характеристика,
	|	ЗапасыНаСкладахОстатки.Партия,
	|	ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница,
	|	ЗапасыНаСкладахОстатки.Ячейка,
	|	ЗапасыНаСкладахОстатки.Номенклатура.ЕдиницаИзмерения";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Организация", Константы.УчетПоКомпании.Компания(Организация));
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	Запрос.УстановитьПараметр("Ячейка", Ячейка);
	
	ЭтотОбъект[ИмяТабличнойЧасти].Загрузить(Запрос.Выполнить().Выгрузить());
	ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "Запасы", "ЗаказПокупателя", "ПоложениеЗаказаПокупателя");
	
КонецПроцедуры // ЗаполнитьЗапасыПоОстаткамНаСкладе()

// Процедура заполняет колонку Количество по резервам под заказ.
//
Процедура ЗаполнитьКолонкуРезервПоРезервам() Экспорт
	
	Запасы.ЗагрузитьКолонку(Новый Массив(Запасы.Количество()), "Резерв");
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ВЫБОР
	|		КОГДА &ЗаказВШапке
	|			ТОГДА &Заказ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаЗапасы.ЗаказПокупателя ССЫЛКА Документ.ЗаказПокупателя
	|						И ТаблицаЗапасы.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|					ТОГДА ТаблицаЗапасы.ЗаказПокупателя
	|				ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК ЗаказПокупателя
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасы
	|ИЗ
	|	&ТаблицаЗапасы КАК ТаблицаЗапасы";
	
	ЗаказВШапке = ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
	Запрос.УстановитьПараметр("ТаблицаЗапасы", Запасы.Выгрузить());
	Запрос.УстановитьПараметр("ЗаказВШапке", ЗаказВШапке);
	Запрос.УстановитьПараметр("Заказ", ?(ЗначениеЗаполнено(ЗаказПокупателя), ЗаказПокупателя, Документы.ЗаказПокупателя.ПустаяСсылка()));
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтруктурныеЕдиницы.Ссылка КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА СтруктурныеЕдиницы.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ТаблицаЗапасы.Номенклатура.СчетУчетаЗатрат
	|		ИНАЧЕ ТаблицаЗапасы.Номенклатура.СчетУчетаЗапасов
	|	КОНЕЦ КАК СчетУчета,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасыОтбор
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
	|		ПО (СтруктурныеЕдиницы.Ссылка = &СтруктурнаяЕдиница)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗапасыОстатки.Организация КАК Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗапасыОстатки.СчетУчета КАК СчетУчета,
	|	ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыОстатки.Характеристика КАК Характеристика,
	|	ЗапасыОстатки.Партия КАК Партия,
	|	СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыОстатки.Организация КАК Организация,
	|		ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗапасыОстатки.СчетУчета КАК СчетУчета,
	|		ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|		ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыОстатки.Характеристика КАК Характеристика,
	|		ЗапасыОстатки.Партия КАК Партия,
	|		ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.Запасы.Остатки(
	|				,
	|				(Организация, СтруктурнаяЕдиница, СчетУчета, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
	|					(ВЫБРАТЬ
	|						&Организация,
	|						ТаблицаЗапасы.СтруктурнаяЕдиница,
	|						ТаблицаЗапасы.СчетУчета,
	|						ТаблицаЗапасы.Номенклатура,
	|						ТаблицаЗапасы.Характеристика,
	|						ТаблицаЗапасы.Партия,
	|						ТаблицаЗапасы.ЗаказПокупателя
	|					ИЗ
	|						ВременнаяТаблицаЗапасыОтбор КАК ТаблицаЗапасы
	|					ГДЕ
	|						ТаблицаЗапасы.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка))) КАК ЗапасыОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаЗапасы.Организация,
	|		ДвиженияДокументаЗапасы.СтруктурнаяЕдиница,
	|		ДвиженияДокументаЗапасы.СчетУчета,
	|		ДвиженияДокументаЗапасы.ЗаказПокупателя,
	|		ДвиженияДокументаЗапасы.Номенклатура,
	|		ДвиженияДокументаЗапасы.Характеристика,
	|		ДвиженияДокументаЗапасы.Партия,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
	|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.Запасы КАК ДвиженияДокументаЗапасы
	|	ГДЕ
	|		ДвиженияДокументаЗапасы.Регистратор = &Ссылка
	|		И ДвиженияДокументаЗапасы.Период <= &Период
	|		И ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ДвиженияДокументаЗапасы.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)) КАК ЗапасыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыОстатки.Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница,
	|	ЗапасыОстатки.СчетУчета,
	|	ЗапасыОстатки.ЗаказПокупателя,
	|	ЗапасыОстатки.Номенклатура,
	|	ЗапасыОстатки.Характеристика,
	|	ЗапасыОстатки.Партия";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Организация", Константы.УчетПоКомпании.Компания(Организация));
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураДляПоиска = Новый Структура;
		Если НЕ ЗаказВШапке Тогда
			СтруктураДляПоиска.Вставить("ЗаказПокупателя", Выборка.ЗаказПокупателя);
		КонецЕсли;
		СтруктураДляПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
		СтруктураДляПоиска.Вставить("Характеристика", Выборка.Характеристика);
		СтруктураДляПоиска.Вставить("Партия", Выборка.Партия);
		
		ВсегоОстаток = Выборка.КоличествоОстаток;
		МассивСтрокЗапасы = Запасы.НайтиСтроки(СтруктураДляПоиска);
		Для каждого СтрокаЗапасы Из МассивСтрокЗапасы Цикл
			
			ВсегоОстаток = ?(ТипЗнч(СтрокаЗапасы.ЕдиницаИзмерения) = ТИП("СправочникСсылка.КлассификаторЕдиницИзмерения"), ВсегоОстаток, ВсегоОстаток / СтрокаЗапасы.ЕдиницаИзмерения.Коэффициент);
			Если СтрокаЗапасы.Количество >= ВсегоОстаток Тогда
				СтрокаЗапасы.Резерв = ВсегоОстаток;
				ВсегоОстаток = 0;
			Иначе
				СтрокаЗапасы.Резерв = СтрокаЗапасы.Количество;
				ВсегоОстаток = ВсегоОстаток - СтрокаЗапасы.Количество;
				ВсегоОстаток = ?(ТипЗнч(СтрокаЗапасы.ЕдиницаИзмерения) = ТИП("СправочникСсылка.КлассификаторЕдиницИзмерения"), ВсегоОстаток, ВсегоОстаток * СтрокаЗапасы.ЕдиницаИзмерения.Коэффициент);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьКолонкуРезервПоРезервам()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоЗаказуНаПроизводствоСборка(ДанныеЗаполнения)
	
	Запрос = Новый Запрос( 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказНаПроизводство.Ссылка КАК ДокументОснование,
	|	ЗаказНаПроизводство.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаПолучатель,
	|	ЗаказНаПроизводство.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПеремещениеЗапасов.Перемещение) КАК ВидОперации,
	|	ЗаказНаПроизводство.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ЗаказНаПроизводство.ПоложениеЗаказаПокупателя КАК ПоложениеЗаказаПокупателя,
	|	ВЫБОР
	|		КОГДА ЗаказНаПроизводство.СтруктурнаяЕдиницаРезерв.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|				ИЛИ ЗаказНаПроизводство.СтруктурнаяЕдиницаРезерв.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗаказНаПроизводство.СтруктурнаяЕдиницаРезерв
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ЗаказНаПроизводство.СтруктурнаяЕдиница.ИсточникПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|				ИЛИ ЗаказНаПроизводство.СтруктурнаяЕдиница.ИсточникПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗаказНаПроизводство.СтруктурнаяЕдиница.ЯчейкаИсточникаПеремещения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|	КОНЕЦ КАК Ячейка,
	|	ЗаказНаПроизводство.Запасы.(
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество КАК Количество,
	|		Резерв КАК Резерв,
	|		ЗаказПокупателя КАК ЗаказПокупателя,
	|		Партия КАК Партия
	|	) КАК Запасы
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|ГДЕ
	|	ЗаказНаПроизводство.Ссылка = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	Запасы.Очистить();
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
		ВыборкаИзРезультатаЗапроса.Следующий();
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаИзРезультатаЗапроса);
		
		ВыборкаЗапасы = ВыборкаИзРезультатаЗапроса.Запасы.Выбрать();
		Пока ВыборкаЗапасы.Следующий() Цикл
			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЗапасы);
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПоЗаказуНаПроизводствоРазборка(ДанныеЗаполнения)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказНаПроизводство.Ссылка КАК ДокументОснование,
	|	ЗаказНаПроизводство.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаПолучатель,
	|	ЗаказНаПроизводство.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПеремещениеЗапасов.Перемещение) КАК ВидОперации,
	|	ЗаказНаПроизводство.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ЗаказНаПроизводство.ПоложениеЗаказаПокупателя КАК ПоложениеЗаказаПокупателя,
	|	ВЫБОР
	|		КОГДА ЗаказНаПроизводство.СтруктурнаяЕдиницаРезерв.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|				ИЛИ ЗаказНаПроизводство.СтруктурнаяЕдиницаРезерв.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗаказНаПроизводство.СтруктурнаяЕдиницаРезерв
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ЗаказНаПроизводство.СтруктурнаяЕдиница.ИсточникПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|				ИЛИ ЗаказНаПроизводство.СтруктурнаяЕдиница.ИсточникПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗаказНаПроизводство.СтруктурнаяЕдиница.ЯчейкаИсточникаПеремещения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|	КОНЕЦ КАК Ячейка,
	|	ЗаказНаПроизводство.Продукция.(
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество КАК Количество,
	|		Резерв КАК Резерв,
	|		ЗаказПокупателя КАК ЗаказПокупателя,
	|		Партия КАК Партия
	|	) КАК Продукция
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|ГДЕ
	|	ЗаказНаПроизводство.Ссылка = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	Запасы.Очистить();
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
		ВыборкаИзРезультатаЗапроса.Следующий();
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаИзРезультатаЗапроса);
		
		ВыборкаПродукция = ВыборкаИзРезультатаЗапроса.Продукция.Выбрать();
		Пока ВыборкаПродукция.Следующий() Цикл
			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПродукция);
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПоСборкеЗапасовСборка(ДанныеЗаполнения)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СборкаЗапасов.Ссылка КАК ДокументОснование,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПеремещениеЗапасов.Перемещение) КАК ВидОперации,
	|	СборкаЗапасов.Организация КАК Организация,
	|	СборкаЗапасов.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СборкаЗапасов.ПоложениеЗаказаПокупателя КАК ПоложениеЗаказаПокупателя,
	|	СборкаЗапасов.СтруктурнаяЕдиницаПродукции КАК СтруктурнаяЕдиница,
	|	СборкаЗапасов.ЯчейкаПродукции КАК Ячейка,
	|	ВЫБОР
	|		КОГДА СборкаЗапасов.СтруктурнаяЕдиницаПродукции.ПолучательПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|				ИЛИ СборкаЗапасов.СтруктурнаяЕдиницаПродукции.ПолучательПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА СборкаЗапасов.СтруктурнаяЕдиницаПродукции.ПолучательПеремещения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК СтруктурнаяЕдиницаПолучатель,
	|	ВЫБОР
	|		КОГДА СборкаЗапасов.СтруктурнаяЕдиницаПродукции.ПолучательПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|				ИЛИ СборкаЗапасов.СтруктурнаяЕдиницаПродукции.ПолучательПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА СборкаЗапасов.СтруктурнаяЕдиницаПродукции.ЯчейкаПолучателяПеремещения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|	КОНЕЦ КАК ЯчейкаПолучатель,
	|	СборкаЗапасов.Продукция.(
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Партия КАК Партия,
	|		ЗаказПокупателя КАК ЗаказПокупателя,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество КАК Количество,
	|		ВЫБОР
	|			КОГДА СборкаЗапасов.Продукция.Ссылка.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ СборкаЗапасов.Продукция.Количество
	|		КОНЕЦ КАК Резерв,
	|		СерииНоменклатуры КАК СерииНоменклатуры,
	|		КлючСвязи КАК КлючСвязи
	|	) КАК Продукция
	|ИЗ
	|	Документ.СборкаЗапасов КАК СборкаЗапасов
	|ГДЕ
	|	СборкаЗапасов.Ссылка = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	Запасы.Очистить();
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
		ВыборкаИзРезультатаЗапроса.Следующий();
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаИзРезультатаЗапроса);
		
		ВыборкаПродукция = ВыборкаИзРезультатаЗапроса.Продукция.Выбрать();
		Пока ВыборкаПродукция.Следующий() Цикл
			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПродукция);
		КонецЦикла;
		
		СерииНоменклатурыУНФ.ЗаполнитьТЧСерииНоменклатурыПоКлючуСвязи(ЭтотОбъект, ДанныеЗаполнения, "Продукция", "СерииНоменклатурыПродукция");
		СерииНоменклатурыУНФ.УдалитьСерииНоменклатурыВТабличнойЧастиВЗависимостиОтПолитики(ЭтотОбъект);
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПоСборкеЗапасовРазборка(ДанныеЗаполнения)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СборкаЗапасов.Ссылка КАК ДокументОснование,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПеремещениеЗапасов.Перемещение) КАК ВидОперации,
	|	СборкаЗапасов.Организация КАК Организация,
	|	СборкаЗапасов.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СборкаЗапасов.ПоложениеЗаказаПокупателя КАК ПоложениеЗаказаПокупателя,
	|	СборкаЗапасов.СтруктурнаяЕдиницаПродукции КАК СтруктурнаяЕдиница,
	|	СборкаЗапасов.ЯчейкаПродукции КАК Ячейка,
	|	ВЫБОР
	|		КОГДА СборкаЗапасов.СтруктурнаяЕдиницаПродукции.ПолучательПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|				ИЛИ СборкаЗапасов.СтруктурнаяЕдиницаПродукции.ПолучательПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА СборкаЗапасов.СтруктурнаяЕдиницаПродукции.ПолучательПеремещения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК СтруктурнаяЕдиницаПолучатель,
	|	ВЫБОР
	|		КОГДА СборкаЗапасов.СтруктурнаяЕдиницаПродукции.ПолучательПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|				ИЛИ СборкаЗапасов.СтруктурнаяЕдиницаПродукции.ПолучательПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА СборкаЗапасов.СтруктурнаяЕдиницаПродукции.ЯчейкаПолучателяПеремещения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|	КОНЕЦ КАК ЯчейкаПолучатель,
	|	СборкаЗапасов.Запасы.(
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Партия КАК Партия,
	|		Количество КАК Количество,
	|		ВЫБОР
	|			КОГДА СборкаЗапасов.Запасы.Ссылка.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ СборкаЗапасов.Запасы.Количество
	|		КОНЕЦ КАК Резерв,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ЗаказПокупателя КАК ЗаказПокупателя,
	|		СерииНоменклатуры КАК СерииНоменклатуры,
	|		КлючСвязи КАК КлючСвязи
	|	) КАК Запасы
	|ИЗ
	|	Документ.СборкаЗапасов КАК СборкаЗапасов
	|ГДЕ
	|	СборкаЗапасов.Ссылка = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	Запасы.Очистить();
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
		ВыборкаИзРезультатаЗапроса.Следующий();
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаИзРезультатаЗапроса);
		
		ВыборкаЗапасы = ВыборкаИзРезультатаЗапроса.Запасы.Выбрать();
		Пока ВыборкаЗапасы.Следующий() Цикл
			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЗапасы);
		КонецЦикла;
		
		СерииНоменклатурыУНФ.ЗаполнитьТЧСерииНоменклатурыПоКлючуСвязи(ЭтотОбъект, ДанныеЗаполнения);
		СерииНоменклатурыУНФ.УдалитьСерииНоменклатурыВТабличнойЧастиВЗависимостиОтПолитики(ЭтотОбъект);
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПодписиКладовщиков()
	
	Если ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда
		
		ПодписьКладовщикаОтправил = СтруктурнаяЕдиница.ПодписьМОЛ;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктурнаяЕдиницаПолучатель) Тогда
		
		ПодписьКладовщикаПолучил = СтруктурнаяЕдиницаПолучатель.ПодписьМОЛ;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНаличиеРозничнойЦены(Отказ)
	
	Если СтруктурнаяЕдиницаПолучатель.ТипСтруктурнойЕдиницы <> Перечисления.ТипыСтруктурныхЕдиниц.Розница И
		СтруктурнаяЕдиницаПолучатель.ТипСтруктурнойЕдиницы <> Перечисления.ТипыСтруктурныхЕдиниц.РозницаСуммовойУчет Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ТаблицаДокумента", Запасы);
	Запрос.УстановитьПараметр("РозничныйВидЦен", СтруктурнаяЕдиницаПолучатель.РозничныйВидЦен);
	Запрос.УстановитьПараметр("СписокНоменклатура", Запасы.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("СписокХарактеристика", Запасы.ВыгрузитьКолонку("Характеристика"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Партия КАК Партия
	|ПОМЕСТИТЬ ПеремещениеЗапасовЗапасы
	|ИЗ
	|	&ТаблицаДокумента КАК ТаблицаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПеремещениеЗапасовЗапасы.НомерСтроки КАК НомерСтроки,
	|	ПеремещениеЗапасовЗапасы.Номенклатура КАК Номенклатура,
	|	ПеремещениеЗапасовЗапасы.Характеристика КАК Характеристика,
	|	ПеремещениеЗапасовЗапасы.Партия КАК Партия
	|ПОМЕСТИТЬ ЗапасыСФильтром
	|ИЗ
	|	ПеремещениеЗапасовЗапасы КАК ПеремещениеЗапасовЗапасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ПеремещениеЗапасовЗапасы.Номенклатура = СправочникНоменклатура.Ссылка
	|			И (СправочникНоменклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗапасыСФильтром.НомерСтроки КАК НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(ЗапасыСФильтром.Номенклатура) КАК НоменклатураПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ЗапасыСФильтром.Характеристика) КАК ХарактеристикаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ЗапасыСФильтром.Партия) КАК ПартияПредставление
	|ИЗ
	|	ЗапасыСФильтром КАК ЗапасыСФильтром
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|				&Дата,
	|				ВидЦен = &РозничныйВидЦен
	|					И Номенклатура В (&СписокНоменклатура)
	|					И Характеристика В (&СписокХарактеристика)) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ЗапасыСФильтром.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|			И ЗапасыСФильтром.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
	|ГДЕ
	|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) = 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаРезультатаЗапроса = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаРезультатаЗапроса.Следующий() Цикл
		
		ПредставлениеНоменклатуры = Справочники.Номенклатура.Представление(
			ВыборкаРезультатаЗапроса.НоменклатураПредставление,
			ВыборкаРезультатаЗапроса.ХарактеристикаПредставление,
			ВыборкаРезультатаЗапроса.ПартияПредставление);
		ТекстСообщения = СтрШаблон(НСтр(
			"ru = 'Для номенклатуры %1 в строке %2 списка ""Запасы"" не установлена розничная цена.'"),
			ПредставлениеНоменклатуры, ВыборкаРезультатаЗапроса.НомерСтроки);
		КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы",
			ВыборкаРезультатаЗапроса.НомерСтроки, "Номенклатура");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
		
	КонецЦикла;
	
КонецПроцедуры // ПроверитьНаличиеРозничнойЦены()

Процедура УменьшитьОстатки(СтрокиОстатков, Количество)
		
	Для каждого СтрокаОстаток Из СтрокиОстатков Цикл
		ЗачтеноСтрока = Мин(Количество, СтрокаОстаток.Остаток);
		СтрокаОстаток.Остаток = СтрокаОстаток.Остаток - ЗачтеноСтрока;
		Количество = Количество - ЗачтеноСтрока;
		Если Количество <= 0 Тогда
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Прослеживаемость

Функция ПрослеживаемаяОперация()
	
	Возврат (ВидОперации = Перечисления.ВидыОперацийПеремещениеЗапасов.СписаниеНаРасходы
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПеремещениеЗапасов.ПередачаВЭксплуатацию
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПеремещениеЗапасов.ВозвратИзЭксплуатации);
	
КонецФункции

#КонецОбласти 

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли