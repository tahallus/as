#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = НСтр("en='Invoice of';ru='Продажа от '") + Формат(Данные.Дата, "ДФ=dd.MM.yyyy");
	
КонецПроцедуры

#КонецОбласти

#Область Печать

Функция СформироватьПечатнуюФорму(Ссылка) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходТовараМП.Ссылка,
	|	РасходТовараМП.ВерсияДанных,
	|	РасходТовараМП.ПометкаУдаления,
	|	РасходТовараМП.Номер,
	|	РасходТовараМП.Дата,
	|	РасходТовараМП.Проведен,
	|	РасходТовараМП.Покупатель,
	|	РасходТовараМП.Основание,
	|	РасходТовараМП.СуммаДокумента,
	|	РасходТовараМП.Комментарий,
	|	РасходТовараМП.СуммаОплаты,
	|	РасходТовараМП.СуммаСкидки,
	|	РасходТовараМП.Товары.(
	|		Ссылка,
	|		НомерСтроки,
	|		Товар,
	|		Цена,
	|		Количество,
	|		Сумма
	|	)
	|ИЗ
	|	Документ.РасходТовараМП КАК РасходТовараМП
	|ГДЕ
	|	РасходТовараМП.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Док = Запрос.Выполнить().Выбрать();
	Док.Следующий();
	
	ТабличныйДокумент = Новый ТабличныйДокумент();
	Макет = Документы.РасходТовараМП.ПолучитьМакет("Накладная");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.ТекстЗаголовка =
		НСтр("ru='Расходная накладная №';en='INVOICE #'")
		+ Док.Номер
		+ НСтр("ru=' от ';en=' DATE '")
		+ Формат(Док.Дата, "ДЛФ=DD");
	
	ОбластьМакета.Параметры.Лого = Новый Картинка(Константы.ЛоготипМП.Получить().Получить());
	ОбластьМакета.Параметры.ПредставлениеПоставщика = Константы.НазваниеОрганизацииМП.Получить();
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	Если ЗначениеЗаполнено(Док.Покупатель) Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("Покупатель");
		ОбластьМакета.Параметры.ПредставлениеПокупателя = Док.Покупатель.Наименование + ?(ЗначениеЗаполнено(Док.Покупатель.НомерТелефона), НСтр("ru=' тел. ';en=' tel. '") + Док.Покупатель.НомерТелефона, "");
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ВыборкаСтрокТовары = Док.Товары.Выбрать();
	
	Количество = 0;
	
	Пока ВыборкаСтрокТовары.Следующий() Цикл
		Количество = Количество + 1;
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		ОбластьМакета.Параметры.НомерСтроки = Количество;
		ОбластьМакета.Параметры.Товар = ВыборкаСтрокТовары.Товар.Наименование;
		ОбластьМакета.Параметры.Количество = ВыборкаСтрокТовары.Количество;
		ОбластьМакета.Параметры.Цена = ВыборкаСтрокТовары.Цена;
		ОбластьМакета.Параметры.Сумма = ВыборкаСтрокТовары.Сумма;
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ПодвалТаблицы");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	Если Док.СуммаСкидки <> 0 Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ИтогоСкидка");
		ОбластьМакета.Параметры.ВсегоБезСкидки = Док.СуммаДокумента + Док.СуммаСкидки;
		ОбластьМакета.Параметры.СуммаСкидки = Док.СуммаСкидки;
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ИтогоКОплате");
	ОбластьМакета.Параметры.Итого = Док.СуммаДокумента;
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	Если НСтр("ru='ru';en='en'") = "ru" Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция СформироватьПечатнуюФормуИнвойс(Ссылка) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходТовараМП.Ссылка КАК Ссылка,
	|	РасходТовараМП.ВерсияДанных КАК ВерсияДанных,
	|	РасходТовараМП.ПометкаУдаления КАК ПометкаУдаления,
	|	РасходТовараМП.Номер КАК Номер,
	|	РасходТовараМП.Дата КАК Дата,
	|	РасходТовараМП.Проведен КАК Проведен,
	|	РасходТовараМП.Покупатель КАК Покупатель,
	|	РасходТовараМП.Покупатель.Наименование КАК ПокупательНаименование,
	|	РасходТовараМП.Основание КАК Основание,
	|	РасходТовараМП.СуммаДокумента КАК СуммаДокумента,
	|	РасходТовараМП.Комментарий КАК Комментарий,
	|	РасходТовараМП.СуммаОплаты КАК СуммаОплаты,
	|	РасходТовараМП.СуммаСкидки КАК СуммаСкидки,
	|	РасходТовараМП.Товары.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Товар КАК Товар,
	|		Цена КАК Цена,
	|		Количество КАК Количество,
	|		Сумма КАК Сумма,
	|		Товар.Наименование КАК ТоварНаименование
	|	) КАК Товары,
	|	РасходТовараМП.Покупатель.НомерТелефона КАК ПокупательНомерТелефона,
	|	РасходТовараМП.Покупатель.АдресЭП КАК ПокупательАдресЭП,
	|	РасходТовараМП.Покупатель.Адрес КАК ПокупательАдрес,
	|	РасходТовараМП.СуммаНалога КАК СуммаНалога,
	|	РасходТовараМП.НазваниеНалога КАК НазваниеНалога,
	|	РасходТовараМП.НастройкаНалога КАК НастройкаНалога,
	|	РасходТовараМП.НалогВСумме КАК НалогВСумме
	|ИЗ
	|	Документ.РасходТовараМП КАК РасходТовараМП
	|ГДЕ
	|	РасходТовараМП.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Док = Запрос.Выполнить().Выбрать();
	Док.Следующий();
	
	ТабличныйДокумент = Новый ТабличныйДокумент();
	Макет = Документы.РасходТовараМП.ПолучитьМакет("Инвойс");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	
	ОбластьМакета.Параметры.НазваниеОрганизации = Константы.НазваниеОрганизацииМП.Получить();
	ОбластьМакета.Параметры.Номер = НСтр("ru='Расходная накладная №';en='INVOICE #'") + Док.Номер;
	
	ИнформацияОбОрганизации = "";
	
	ДобавитьИнформациюОбОрганизации(ИнформацияОбОрганизации, НСтр("en = 'Address:'; ru = 'Адрес:'") + Символ(32), Константы.АдресОрганизацииМП.Получить());
	ДобавитьИнформациюОбОрганизации(ИнформацияОбОрганизации, НСтр("en = 'Tel.:'; ru = 'Телефон:'") + Символ(32), Константы.ТелефонОрганизацииМП.Получить());
	ДобавитьИнформациюОбОрганизации(ИнформацияОбОрганизации, НСтр("en = 'Email:'; ru = 'Email:'") + Символ(32), Константы.АдресЭПОрганизацииМП.Получить());
	
	ОбластьМакета.Параметры.ИнформацияОбОрганизации = ИнформацияОбОрганизации;
	
	ОбластьМакета.Параметры.Лого = Новый Картинка(Константы.ЛоготипМП.Получить().Получить());

	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаИнформацияОКлиентеЖирная");
	ОбластьМакета.Параметры.ИнформацияОКлиенте = Док.ПокупательНаименование;
	ОбластьМакета.Параметры.ЗаголовокРеквизита = НСтр("en = 'Date'; ru = 'Дата'");
	ОбластьМакета.Параметры.Реквизит = Формат(Док.Дата, "ДЛФ=DD");
	
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаИнформацияОКлиенте");
	
	ИнформацияОПокупателе = "";
	
	ДобавитьИнформациюОбОрганизации(ИнформацияОПокупателе, НСтр("en = 'Address:'; ru = 'Адрес:'") + Символ(32), Док.ПокупательАдрес);
	ДобавитьИнформациюОбОрганизации(ИнформацияОПокупателе, НСтр("en = 'Tel.:'; ru = 'Телефон:'") + Символ(32), Док.ПокупательНомерТелефона);
	ДобавитьИнформациюОбОрганизации(ИнформацияОПокупателе, НСтр("en = 'Email:'; ru = 'Email:'") + Символ(32), Док.ПокупательАдресЭП);

	ОбластьМакета.Параметры.ИнформацияОКлиенте = ИнформацияОПокупателе;

	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	НазваниеНалога = ""; 
	
	ЕстьНалогВТабличнойЧасти = Ложь;
	Если Док.НастройкаНалога = Перечисления.НастройкиНалогаМП.Построчно И Док.СуммаНалога <> 0 Тогда
		ЕстьНалогВТабличнойЧасти = Истина;
	КонецЕсли;
	
	Если ЕстьНалогВТабличнойЧасти Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицыСНалогом");
		ОбластьМакета.Параметры.НазваниеНалога = ВРег(Док.НазваниеНалога);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицыСНалогом");
	Иначе
		ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы");
	КонецЕсли;
	
	ВыборкаСтрокТовары = Док.Товары.Выбрать();
	
	Пока ВыборкаСтрокТовары.Следующий() Цикл
		ОбластьМакета.Параметры.Номенклатура = ВыборкаСтрокТовары.ТоварНаименование;
		ОбластьМакета.Параметры.Количество = ВыборкаСтрокТовары.Количество;
		ОбластьМакета.Параметры.Цена = Формат(ВыборкаСтрокТовары.Цена, "ЧДЦ=2");
		ОбластьМакета.Параметры.Сумма = Формат(ВыборкаСтрокТовары.Сумма, "ЧДЦ=2");
		Если ЕстьНалогВТабличнойЧасти Тогда
			
			СтавкаНалога = Справочники.ТоварыМП.ПолучитьСтавкуНалога(ВыборкаСтрокТовары.Товар);
			
			Если Док.НалогВСумме Тогда
				Подитог = Док.СуммаДокумента + Док.СуммаСкидки;
				ДоляСуммы = 100 * ВыборкаСтрокТовары.Сумма / Подитог;
				СкидкаНаСтроку = Док.СуммаСкидки * ДоляСуммы / 100;
				СуммаСоСкидкой = ВыборкаСтрокТовары.Сумма - СкидкаНаСтроку;
				СуммаНалога = СуммаСоСкидкой - СуммаСоСкидкой / (1 + СтавкаНалога / 100);
			Иначе
				СуммаНалога = ВыборкаСтрокТовары.Сумма * СтавкаНалога / 100;
			КонецЕсли;
			ОбластьМакета.Параметры.СуммаНалога = Формат(СуммаНалога, "ЧДЦ=2");
		КонецЕсли;
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЦикла;

	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаПодитог");
	Если Док.НалогВСумме Тогда
		Подитог = Док.СуммаДокумента + Док.СуммаСкидки;
	Иначе
		Подитог = Док.СуммаДокумента + Док.СуммаСкидки + Док.СуммаНалога;
	КонецЕсли;
	ОбластьМакета.Параметры.Подитог = Формат(Подитог, "ЧДЦ=2");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаПодитог");
	СтруктураРеквизитов = Новый Структура("ЗаголовокРеквизита, Реквизит", "", "");
	
	Если Подитог <> 0 Тогда
		Скидка = Окр(Док.СуммаСкидки / Подитог * 100, 0);
		ПроцентСкидки = Строка(Скидка) + "%";
	КонецЕсли;
	
	ДобавитьРеквизит(СтруктураРеквизитов, НСтр("en = 'Discont ('; ru = 'Скидка ('") + ПроцентСкидки + ")" ,  Формат(Док.СуммаСкидки, "ЧДЦ=2"));
	Если Док.НастройкаНалога = Перечисления.НастройкиНалогаМП.НаВесьДокумент Тогда
		НазваниеНалога = Док.НазваниеНалога + "(" + Док.СтавкаНалога + "%)";
	Иначе
		НазваниеНалога = Док.НазваниеНалога;
	КонецЕсли;
	ДобавитьРеквизит(СтруктураРеквизитов, НазваниеНалога,  Формат(Док.СуммаНалога, "ЧДЦ=2"));
	
	ОбластьМакета.Параметры.ЗаголовокРеквизита = СтруктураРеквизитов.ЗаголовокРеквизита;
	ОбластьМакета.Параметры.Реквизит = СтруктураРеквизитов.Реквизит;

	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаИтог");
	ОбластьМакета.Параметры.Итого = Формат(Док.СуммаДокумента, "ЧДЦ=2");
	ОбластьМакета.Параметры.ОстатокДолга = Формат(Док.СуммаДокумента - Док.СуммаОплаты, "ЧДЦ=2");

	ТабличныйДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("ПечатьПодпись");
	ОбластьМакета.Параметры.Факсимиле = Новый Картинка(Константы.ФаксимилеРуководителяМП.Получить().Получить());

	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция СформироватьПечатнуюФормуТОРГ12(Ссылка) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходТовараМП.Ссылка КАК Ссылка,
	|	РасходТовараМП.ВерсияДанных КАК ВерсияДанных,
	|	РасходТовараМП.ПометкаУдаления КАК ПометкаУдаления,
	|	РасходТовараМП.Номер КАК Номер,
	|	РасходТовараМП.Дата КАК Дата,
	|	РасходТовараМП.Проведен КАК Проведен,
	|	РасходТовараМП.Покупатель КАК Покупатель,
	|	РасходТовараМП.Покупатель.Наименование КАК ПокупательНаименование,
	|	РасходТовараМП.Основание КАК Основание,
	|	РасходТовараМП.СуммаДокумента КАК СуммаДокумента,
	|	РасходТовараМП.Комментарий КАК Комментарий,
	|	РасходТовараМП.СуммаОплаты КАК СуммаОплаты,
	|	РасходТовараМП.СуммаСкидки КАК СуммаСкидки,
	|	РасходТовараМП.Товары.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Товар КАК Товар,
	|		Цена КАК Цена,
	|		Количество КАК Количество,
	|		Сумма КАК Сумма,
	|		Товар.Наименование КАК ТоварНаименование
	|	) КАК Товары,
	|	РасходТовараМП.Покупатель.НомерТелефона КАК ПокупательНомерТелефона,
	|	РасходТовараМП.Покупатель.АдресЭП КАК ПокупательАдресЭП,
	|	РасходТовараМП.Покупатель.Адрес КАК ПокупательАдрес,
	|	РасходТовараМП.СуммаНалога КАК СуммаНалога,
	|	РасходТовараМП.НазваниеНалога КАК НазваниеНалога,
	|	РасходТовараМП.НастройкаНалога КАК НастройкаНалога,
	|	РасходТовараМП.НалогВСумме КАК НалогВСумме
	|ИЗ
	|	Документ.РасходТовараМП КАК РасходТовараМП
	|ГДЕ
	|	РасходТовараМП.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Док = Запрос.Выполнить().Выбрать();
	Док.Следующий();
	
	ТабличныйДокумент = Новый ТабличныйДокумент();
	Макет = Документы.РасходТовараМП.ПолучитьМакет("ТОРГ12");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	
	ОбластьМакета.Параметры.ПредставлениеГрузоотправителя = Константы.НазваниеОрганизацииМП.Получить();
	
	ТабличныйДокумент.Вывести(ОбластьМакета);

	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ДобавитьИнформациюОбОрганизации(ИнформацияОКомпании, Заголовок, Значение)
	
	Если ЗначениеЗаполнено(Значение) Тогда
		ИнформацияОКомпании = ИнформацияОКомпании + Заголовок + Значение + Символы.ПС;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьРеквизит(СтруктураПараметров, Заголовок, Значение)
	
	Если ЗначениеЗаполнено(Значение) Тогда
		СтруктураПараметров.ЗаголовокРеквизита = СтруктураПараметров.ЗаголовокРеквизита + Заголовок + Символы.ПС;
		СтруктураПараметров.Реквизит = СтруктураПараметров.Реквизит + Значение + Символы.ПС;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
