
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДата();
	КонецЕсли;   
	
	Если ТипЗнч(КэшЗначений) <> Тип("Структура") Тогда
		КэшЗначений = Новый Структура;
	КонецЕсли;
	
	КэшЗначений.Вставить("ИспользоватьМинимальныеЦены", ЦенообразованиеСерверПовтИсп.МинимальныеЦеныИспользуются());
	
	Компания = Константы.УчетПоКомпании.Компания(Объект.Организация);
	
	Если Объект.Работы.Количество() = 0 Тогда
		
		НоваяСтрока = Объект.Работы.Добавить();
		
		Если Объект.ПоложениеВидаРабот <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти
			И ЗначениеЗаполнено(Объект.ВидРабот) Тогда
			НоваяСтрока.ВидРабот = Объект.ВидРабот;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.Ссылка)
			И Не ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			Если Параметры.ЗначенияЗаполнения.Свойство("Заказчик")
				И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Заказчик) Тогда
				НоваяСтрока.Заказчик = Параметры.ЗначенияЗаполнения.Заказчик;
			КонецЕсли;
			Если Параметры.Свойство("ВремяНачала") Тогда 
				НоваяСтрока.ДатаНачала = Параметры.ВремяНачала;
			КонецЕсли;
			Если Параметры.Свойство("ВремяОкончания") Тогда 
				НоваяСтрока.ДатаОкончания = Параметры.ВремяОкончания;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Параметры.ЗначенияЗаполнения.Свойство("ДанныеЗаписиКалендаря") Тогда
		Параметры.ЗначенияЗаполнения.ДанныеЗаписиКалендаря.Свойство("КолонкаКалендаря", КолонкаКалендаря);
	КонецЕсли;
	
	Для Каждого Работа Из Объект.Работы Цикл
		РассчитатьПродолжительность(Работа);
	КонецЦикла;
	
	Если Параметры.Свойство("Сотрудник") Тогда // для заполнения из менеджера контактов.
		Объект.Сотрудник = Параметры.Сотрудник;
	КонецЕсли;
	
	Элементы.РедактироватьСписком.Пометка = Объект.Работы.Количество() > 1;
	
	СобытияУНФКлиентСервер.ЗаполнитьСписокВыбораВремени(Элементы.НачалоВремя, , '00010101000000', '00010101230000');
	СобытияУНФКлиентСервер.ЗаполнитьСписокВыбораВремени(Элементы.ОкончаниеВремя, , '00010101000000', '00010101230000');
	
	КопированиеТабличнойЧастиСервер.ПриСозданииНаСевере(Элементы, "Работы");

	ЦенообразованиеСервер.ДобавитьКолонкуИЗаполнитьМинимальныеЦеныВТЧ(ЭтотОбъект, "Работы", Истина);

	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПечатьДокументовУНФ.УстановитьОтображениеПодменюПечати(Элементы.ПодменюПечать);
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = УправлениеСвойствамиУНФ.ЗаполнитьДополнительныеПараметры(Объект, "ДополнительныеРеквизиты", , "КомандыОбычные");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// Характеристики
	НоменклатураВДокументахСервер.ОбновитьУсловноеОформлениеТабличнойЧастиДляХарактеристик(ЭтаФорма);
	Если Параметры.Ключ.Пустая()
		Тогда
		НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект, Истина)
	КонецЕсли;
	
	Если Объект.Работы.Количество() = 1 И ЗначениеЗаполнено(Объект.Работы[0].Номенклатура)
		Тогда
		Если Не Объект.Работы[0].ИспользоватьХарактеристики
			Тогда
			Элементы.РаботыСтрокаХарактеристика.Доступность = Ложь
		ИначеЕсли Объект.Работы[0].ПроверятьЗаполнениеХарактеристики И Не ЗначениеЗаполнено(Объект.Работы[0].Характеристика)
			Тогда
			Элементы.РаботыСтрокаХарактеристика.ОтметкаНезаполненного = Истина;
		КонецЕсли
	КонецЕсли;
	// Конец Характеристики  
	
	// минимальные цены   
	ИспользоватьМинимальныеЦены = ?(КэшЗначений.Свойство("ИспользоватьМинимальныеЦены"), КэшЗначений.ИспользоватьМинимальныеЦены, Ложь);
	ЦенообразованиеСервер.УстановитьУсловноеОформлениеМинимальнойЦены(УсловноеОформление, "Работы", ИспользоватьМинимальныеЦены);
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, 
		"Объект.Работы.Цена", 
		Новый ПолеКомпоновкиДанных("Объект.Работы.МинимальнаяЦена"), ВидСравненияКомпоновкиДанных.Меньше);
		
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "РаботыСтрокаЦена");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", WebЦвета.Красный);
	НовоеУсловноеОформление.Использование = ИспользоватьМинимальныеЦены; 
	// конец минимальные цены
	
	НоменклатураВДокументахКлиентСервер.ЗаполнитьТаблицуНастроекФормыВыбораНоменклатуры(ЭтаФорма, "ЗаданиеНаРаботу", НастройкиФормыВыбораНоменклатуры,, "Работы");
	НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(ЭтаФорма, "Работы");
	
	СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// КопированиеСтрокТабличныхЧастей
	Если ИмяСобытия = "БуферОбменаТабличнаяЧастьКопированиеСтрок" Тогда
		КопированиеТабличнойЧастиКлиент.ОбработкаОповещения(Элементы, "Работы");
	КонецЕсли;
	
	// Обсуждения
	ОбсужденияУНФКлиент.ОбработкаОповещения(ИмяСобытия, Параметр, Источник, ЭтотОбъект, Объект.Ссылка);
	// Конец Обсуждения
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменений
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменений
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	Если Объект.Работы.Количество()
		Тогда
		СтрокаТабличнойЧасти = Объект.Работы[0];
		
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура)
			Тогда
			Элементы.РаботыСтрокаХарактеристика.ОтметкаНезаполненного = НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Характеристика) И СтрокаТабличнойЧасти.ПроверятьЗаполнениеХарактеристики;
			Элементы.РаботыСтрокаХарактеристика.Доступность = СтрокаТабличнойЧасти.ИспользоватьХарактеристики;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ОценкаПроизводительностиКлиент.ЗамерВремени("Проведение"+ РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	КонецЕсли;
	// СтандартныеПодсистемы.ОценкаПроизводительности
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Записи календарей сотрудника
	ТекущийОбъект.ДополнительныеСвойства.Вставить("КолонкаКалендаря",КолонкаКалендаря);
	// Конец Записи календарей сотрудника
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// Обсуждения
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Модифицированность",Модифицированность);
	// Конец Обсуждения
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	
	Для Каждого Работа Из Объект.Работы Цикл
		РассчитатьПродолжительность(Работа);
	КонецЦикла;
	
	// УНФ.КалендарьСотрудника
	Оповестить("Запись_ИсточникЗаписейКалендаряСотрудника");
	// Конец УНФ.КалендарьСотрудника
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	НоменклатураВДокументахСервер.ЗаполнитьПризнакиИспользованияХарактеристик(Объект);
	
	ЦенообразованиеСервер.ДобавитьКолонкуИЗаполнитьМинимальныеЦеныВТЧ(ЭтотОбъект, "Работы", Истина);	
	
	//Обсуждения
	ОбсужденияУНФ.ПослеЗаписиНаСервере(ТекущийОбъект);	
	// Конец Обсуждения
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СтруктурнаяЕдиницаПриИзменении(Элемент)
	
	СтруктураДанные = ПолучитьДанныеПодразделенияПриИзменении(Объект.СтруктурнаяЕдиница);
	ЗаполнитьЗначенияСвойств(Объект, СтруктураДанные, "ПодписьМОЛ");
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Объект.Номер = "";
	СтруктураДанные = ПолучитьДанныеОрганизацияПриИзменении(Объект.Организация);
	Компания = СтруктураДанные.Компания;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	Для Каждого СтрокаТЧ Из Объект.Работы Цикл
		СтрокаТЧ.Цена = 0;
		СтрокаТЧ.Сумма = 0;
	КонецЦикла;
	
	УправлениеФормой();
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаданиеНаРаботу.Внутреннее") Тогда
		Для каждого ТекущаяСтрока Из Объект.Работы Цикл
			Если ТипЗнч(ТекущаяСтрока.Заказчик) <> Тип("ДокументСсылка.ЗаказПокупателя") Тогда
				ТекущаяСтрока.Заказчик = Неопределено;
				Модифицированность = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидРаботПриИзменении(Элемент)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("ДатаОбработки",	Объект.Дата);
	СтруктураДанные.Вставить("Номенклатура",	Объект.ВидРабот);
	СтруктураДанные.Вставить("ВидЦен",			Объект.ВидЦен);
	
	ЦенообразованиеКлиентСервер.ДополнитьСтруктуруДанныхНоменклатурыСтруктурнойЕдиницей(Объект, Неопределено, СтруктураДанные, ,Истина);
	
	СтруктураЦены = ПолучитьЦену(СтруктураДанные);
	Цена = СтруктураЦены.Цена;
	
	МинимальнаяЦена = 0;
	Если СтруктураЦены.Свойство("МинимальнаяЦена") Тогда
		МинимальнаяЦена = СтруктураЦены.МинимальнаяЦена;
	КонецЕсли; 
	
	Для каждого СтрокаТЧ Из Объект.Работы Цикл
		СтрокаТЧ.ВидРабот = Объект.ВидРабот;
		СтрокаТЧ.Цена = Цена;
		Если СтрокаТЧ.Свойство("МинимальнаяЦена") Тогда
			СтрокаТЧ.МинимальнаяЦена = МинимальнаяЦена;
		КонецЕсли;
		РассчитатьСумму(СтрокаТЧ);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЦенПриИзменении(Элемент)
	
	СтруктураДанных = Новый Структура;
	ТабличнаяЧастьДокумента = Новый Массив;
	
	СтруктураДанных.Вставить("Дата",				Объект.Дата);
	СтруктураДанных.Вставить("Организация",			Компания);
	СтруктураДанных.Вставить("ВидЦен",				Объект.ВидЦен);
	
	Если Объект.ПоложениеВидаРабот = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке") Тогда
		
		СтрокаТабличнойЧасти = Новый Структура();
		СтрокаТабличнойЧасти.Вставить("Номенклатура",		Объект.ВидРабот);
		СтрокаТабличнойЧасти.Вставить("Цена",				0);
		
		ТабличнаяЧастьДокумента.Добавить(СтрокаТабличнойЧасти);
		
	Иначе
		
		Для Каждого СтрокаТЧ Из Объект.Работы Цикл
			
			СтрокаТЧ.Цена = 0;
			
			СтрокаТабличнойЧасти = Новый Структура();
			СтрокаТабличнойЧасти.Вставить("Номенклатура",		СтрокаТЧ.ВидРабот);
			СтрокаТабличнойЧасти.Вставить("Цена",				0);
			
			ТабличнаяЧастьДокумента.Добавить(СтрокаТабличнойЧасти);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ПолучитьЦеныТабличнойЧастиПоВидуЦен(СтруктураДанных, ТабличнаяЧастьДокумента);
	
	Если Объект.ПоложениеВидаРабот = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке") Тогда
		
		Цена = ТабличнаяЧастьДокумента[0].Цена;
		
		Для Каждого СтрокаТЧ Из Объект.Работы Цикл
			СтрокаТЧ.Цена = Цена;
			РассчитатьСумму(СтрокаТЧ);
		КонецЦикла;
		
	Иначе
		
		Для Каждого СтрокаТЧ Из ТабличнаяЧастьДокумента Цикл
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("ВидРабот", СтрокаТЧ.Номенклатура);
			
			РезультатПоиска = Объект.Работы.НайтиСтроки(СтруктураПоиска);
			
			Для Каждого СтрокаРезультат Из РезультатПоиска Цикл
				СтрокаРезультат.Цена = СтрокаТЧ.Цена;
				РассчитатьСумму(СтрокаРезультат);
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыСтрокаНоменклатураПриИзменении(Элемент)
	
	Если Объект.Работы.Количество() <> 0 Тогда
		Объект.Работы[0].Характеристика = Неопределено;
	КонецЕсли;
	
	Если Объект.Работы.Количество() Тогда
		СтрокаТабличнойЧасти = Объект.Работы[0];
		
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура)
			Тогда	
			
			СтруктураДанные = Новый Структура;
			
			СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
			СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
			
			СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
			
			СтрокаТабличнойЧасти.ИспользоватьХарактеристики = СтруктураДанные.ИспользоватьХарактеристики;
			СтрокаТабличнойЧасти.ПроверятьЗаполнениеХарактеристики = СтруктураДанные.ПроверятьЗаполнениеХарактеристики;
			СтрокаТабличнойЧасти.ЗаполнениеХарактеристикиПроверено = Истина;
			
			Если СтруктураДанные.ИспользоватьХарактеристики Тогда
				Если ПодборНоменклатурыИзСписка И ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
					СтрокаТабличнойЧасти.Характеристика = СтруктураВыбораНоменклатуры.Характеристика;
					СтруктураВыбораНоменклатуры.Характеристика = Неопределено;
				Иначе
					СтрокаТабличнойЧасти.Характеристика = СтруктураДанные.Характеристика;
				КонецЕсли;
			КонецЕсли;
			
			Элементы.РаботыСтрокаХарактеристика.ОтметкаНезаполненного = НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Характеристика) И СтруктураДанные.ПроверятьЗаполнениеХарактеристики;
			
			Если Не СтрокаТабличнойЧасти.ИспользоватьХарактеристики
				Тогда
				СтрокаТабличнойЧасти.Характеристика = Неопределено;
				Элементы.РаботыСтрокаХарактеристика.Доступность = Ложь;
			Иначе
				Элементы.РаботыСтрокаХарактеристика.Доступность = Истина;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	ПодборНоменклатурыИзСписка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыСтрокаНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда

		ПодборНоменклатурыИзСписка = ВыбранноеЗначение.Свойство("Ячейка");
		
		Если НЕ ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
		КонецЕсли;
		
		СтруктураВыбораНоменклатуры.Характеристика = ВыбранноеЗначение.Характеристика;
		СтруктураВыбораНоменклатуры.Партия = ВыбранноеЗначение.Партия;
		
		Если ВыбранноеЗначение.Свойство("СтруктураНастроек") Тогда
			НоменклатураВДокументахКлиентСервер.ЗаполнитьТаблицуНастроекФормыВыбораНоменклатуры(ЭтаФорма, "ЗаданиеНаРаботу", НастройкиФормыВыбораНоменклатуры, Истина, "Работы", ВыбранноеЗначение.СтруктураНастроек);
			НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(ЭтаФорма, "Работы");
		КонецЕсли;
		
		ВыбранноеЗначение = ВыбранноеЗначение.Номенклатура;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыСтрокаХарактеристикаПриИзменении(Элемент)
	
	Если Объект.Работы.Количество() Тогда
		СтрокаТабличнойЧасти = Объект.Работы[0];
		Элементы.РаботыСтрокаХарактеристика.ОтметкаНезаполненного = НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Характеристика) И СтрокаТабличнойЧасти.ПроверятьЗаполнениеХарактеристики;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыСтрокаХарактеристикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	Если Объект.Работы.Количество() <> 0 Тогда
		ПараметрыФормы.Вставить("Отбор", Новый Структура("Владелец", Объект.Работы[0].Номенклатура));
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("РаботыСтрокаХарактеристикаНачалоВыбораЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.ХарактеристикиНоменклатуры.ФормаВыбора", ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыСтрокаХарактеристикаНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено
		ИЛИ ТипЗнч(Результат) <> Тип("СправочникСсылка.ХарактеристикиНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Работы.Количество() <> 0 Тогда
		Объект.Работы[0].Характеристика = Результат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРаботы

&НаКлиенте
Процедура РаботыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
		
		Если Объект.ПоложениеВидаРабот <> ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти") Тогда
			
			ТекущаяСтрока = Элементы.Работы.ТекущиеДанные;
			
			ЗаполнениеОбъектовУНФКлиент.ЗаполнитьСтрокуПоШапке(ТекущаяСтрока, Объект, "ВидРабот", "ПоложениеВидаРабот");
			
			СтруктураДанные = Новый Структура();
			СтруктураДанные.Вставить("ДатаОбработки",	Объект.Дата);
			СтруктураДанные.Вставить("Номенклатура",	Объект.ВидРабот);
			СтруктураДанные.Вставить("ВидЦен",			Объект.ВидЦен);
			
			ЦенообразованиеКлиентСервер.ДополнитьСтруктуруДанныхНоменклатурыСтруктурнойЕдиницей(Объект, Неопределено, СтруктураДанные,, Истина);
			
			ТекущаяСтрока.Цена = ПолучитьЦену(СтруктураДанные).Цена;
			
			РассчитатьСумму(ТекущаяСтрока);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыНачалоВремяПриИзменении(Элемент)
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ДанныеСтроки = Элементы.Работы.ТекущиеДанные;
	Иначе
		ДанныеСтроки = Объект.Работы[0];
	КонецЕсли;
	
	ДанныеСтроки.ДатаНачала = НачалоМинуты(ДанныеСтроки.ДатаНачала);
	
	РассчитатьТрудоемкость(ДанныеСтроки);
	РассчитатьПродолжительность(ДанныеСтроки);
	РассчитатьСумму(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыНачалоВремяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ДанныеСтроки = Элементы.Работы.ТекущиеДанные;
	Иначе
		ДанныеСтроки = Объект.Работы[0];
	КонецЕсли;
	
	ВыбранноеЗначение = НачалоДня(ДанныеСтроки.ДатаНачала) + (ВыбранноеЗначение - НачалоДня(ВыбранноеЗначение));
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыНачалоДатаПриИзменении(Элемент)
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ДанныеСтроки = Элементы.Работы.ТекущиеДанные;
	Иначе
		ДанныеСтроки = Объект.Работы[0];
	КонецЕсли;
	
	РассчитатьТрудоемкость(ДанныеСтроки);
	РассчитатьПродолжительность(ДанныеСтроки);
	РассчитатьСумму(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыОкончаниеВремяПриИзменении(Элемент)
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ДанныеСтроки = Элементы.Работы.ТекущиеДанные;
	Иначе
		ДанныеСтроки = Объект.Работы[0];
	КонецЕсли;
	
	ДанныеСтроки.ДатаОкончания = НачалоМинуты(ДанныеСтроки.ДатаОкончания);
	
	РассчитатьТрудоемкость(ДанныеСтроки);
	РассчитатьПродолжительность(ДанныеСтроки);
	РассчитатьСумму(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыОкончаниеВремяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ДанныеСтроки = Элементы.Работы.ТекущиеДанные;
	Иначе
		ДанныеСтроки = Объект.Работы[0];
	КонецЕсли;
	
	ВыбранноеЗначение = НачалоДня(ДанныеСтроки.ДатаОкончания) + (ВыбранноеЗначение - НачалоДня(ВыбранноеЗначение));
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыОкончаниеДатаПриИзменении(Элемент)
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ДанныеСтроки = Элементы.Работы.ТекущиеДанные;
	Иначе
		ДанныеСтроки = Объект.Работы[0];
	КонецЕсли;
	
	РассчитатьТрудоемкость(ДанныеСтроки);
	РассчитатьПродолжительность(ДанныеСтроки);
	РассчитатьСумму(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыПродолжительностьПриИзменении(Элемент)
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ДанныеСтроки = Элементы.Работы.ТекущиеДанные;
	Иначе
		ДанныеСтроки = Объект.Работы[0];
	КонецЕсли;
	
	РассчитыватьТрудоемкостьАвтоматически = Не ЗначениеЗаполнено(ДанныеСтроки.Трудоемкость)
		Или ДанныеСтроки.Трудоемкость = (ДанныеСтроки.ДатаОкончания - ДанныеСтроки.ДатаНачала) / 3600;
	
	ДанныеСтроки.ДатаОкончания = НачалоМинуты(ДанныеСтроки.ДатаНачала + ДанныеСтроки.Продолжительность * 3600);
	
	Если РассчитыватьТрудоемкостьАвтоматически Тогда
		ДанныеСтроки.Трудоемкость = (ДанныеСтроки.ДатаОкончания - ДанныеСтроки.ДатаНачала) / 3600;
	КонецЕсли;
	
	РассчитатьСумму(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыТрудоемкостьПриИзменении(Элемент)
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ДанныеСтроки = Элементы.Работы.ТекущиеДанные;
	Иначе
		ДанныеСтроки = Объект.Работы[0];
	КонецЕсли;
	
	РассчитатьСумму(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыВидРаботПриИзменении(Элемент)
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ДанныеСтроки = Элементы.Работы.ТекущиеДанные;
	Иначе
		ДанныеСтроки = Объект.Работы[0];
	КонецЕсли;
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("ДатаОбработки",	ДанныеСтроки.ДатаНачала);
	СтруктураДанные.Вставить("Номенклатура",	ДанныеСтроки.ВидРабот);
	СтруктураДанные.Вставить("ВидЦен",			Объект.ВидЦен);
	
	ЦенообразованиеКлиентСервер.ДополнитьСтруктуруДанныхНоменклатурыСтруктурнойЕдиницей("Объект", Неопределено, СтруктураДанные,, Истина);
	
	СтруктураЦены = ПолучитьЦену(СтруктураДанные);
		
	ДанныеСтроки.Цена = СтруктураЦены.Цена;
	
	Если СтруктураЦены.Свойство("МинимальнаяЦена") Тогда
		ДанныеСтроки.МинимальнаяЦена = СтруктураЦены.МинимальнаяЦена;
	КонецЕсли;
	
	РассчитатьСумму(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыЦенаПриИзменении(Элемент)
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ДанныеСтроки = Элементы.Работы.ТекущиеДанные;
	Иначе
		ДанныеСтроки = Объект.Работы[0];
	КонецЕсли;
	
	РассчитатьСумму(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыСуммаПриИзменении(Элемент)
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ДанныеСтроки = Элементы.Работы.ТекущиеДанные;
	Иначе
		ДанныеСтроки = Объект.Работы[0];
	КонецЕсли;
	
	ДанныеСтроки.Цена = ?(ДанныеСтроки.Трудоемкость = 0, 0, ДанныеСтроки.Сумма / ДанныеСтроки.Трудоемкость);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыЗаказчикОбработкаВыбораВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		
		СтандартнаяОбработка = Ложь;
		ВыбранныйДоговор = Неопределено;
		
		ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаВыбораСКонтрагентом",,,,,, Новый ОписаниеОповещения("РаботыЗаказчикОбработкаВыбораВыбораЗавершение", ЭтотОбъект));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыЗаказчикОбработкаВыбораВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВыбранныйДоговор = Результат;
	
	Если ТипЗнч(ВыбранныйДоговор) = Тип("СправочникСсылка.ДоговорыКонтрагентов")Тогда
		Если Элементы.РедактироватьСписком.Пометка Тогда
			Элементы.Работы.ТекущиеДанные.Заказчик = ВыбранныйДоговор;
		ИначеЕсли Объект.Работы.Количество() > 0 Тогда
			Объект.Работы[0].Заказчик = ВыбранныйДоговор;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыКомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ДанныеСтроки = Элементы.Работы.ТекущиеДанные;
	Иначе
		ДанныеСтроки = Объект.Работы[0];
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Текст, Заголовок", ДанныеСтроки.Комментарий, "Редактирование комментария");
	ВозвращаемыйКомментарий = Неопределено;
	
	ОткрытьФорму("ОбщаяФорма.РедактированиеТекста", ПараметрыФормы,,,,, Новый ОписаниеОповещения("РаботыКомментарийНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("ТекущиеДанные", ДанныеСтроки))); 
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыКомментарийНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
	
	ВозвращаемыйКомментарий = Результат;
	
	Если ТипЗнч(ВозвращаемыйКомментарий) = Тип("Строка") Тогда
		
		Если ТекущиеДанные.Комментарий <> ВозвращаемыйКомментарий Тогда
			Модифицированность = Истина;
		КонецЕсли;
		
		ТекущиеДанные.Комментарий = ВозвращаемыйКомментарий;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыНоменклатураПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Работы.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура)
		Тогда	
		
		СтруктураДанные = Новый Структура;
		
		СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
		СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
		
		СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
		
		СтрокаТабличнойЧасти.ИспользоватьХарактеристики = СтруктураДанные.ИспользоватьХарактеристики;
		СтрокаТабличнойЧасти.ПроверятьЗаполнениеХарактеристики = СтруктураДанные.ПроверятьЗаполнениеХарактеристики;
		СтрокаТабличнойЧасти.ЗаполнениеХарактеристикиПроверено = Истина;
		
		Если СтруктураДанные.ИспользоватьХарактеристики Тогда
			Если ПодборНоменклатурыИзСписка И ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
				СтрокаТабличнойЧасти.Характеристика = СтруктураВыбораНоменклатуры.Характеристика;
				СтруктураВыбораНоменклатуры.Характеристика = Неопределено;
			Иначе
				СтрокаТабличнойЧасти.Характеристика = СтруктураДанные.Характеристика;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда

		ПодборНоменклатурыИзСписка = ВыбранноеЗначение.Свойство("Ячейка");
		
		Если НЕ ТипЗнч(СтруктураВыбораНоменклатуры) = Тип("Структура") Тогда
			СтруктураВыбораНоменклатуры = Новый Структура("Характеристика, Партия");
		КонецЕсли;
		
		СтруктураВыбораНоменклатуры.Характеристика = ВыбранноеЗначение.Характеристика;
		СтруктураВыбораНоменклатуры.Партия = ВыбранноеЗначение.Партия;
		
		Если ВыбранноеЗначение.Свойство("СтруктураНастроек") Тогда
			НоменклатураВДокументахКлиентСервер.ЗаполнитьТаблицуНастроекФормыВыбораНоменклатуры(ЭтаФорма, "ЗаданиеНаРаботу", НастройкиФормыВыбораНоменклатуры, Истина, "Работы", ВыбранноеЗначение.СтруктураНастроек);
			НоменклатураВДокументахКлиентСервер.ОбновитьПараметрыОткрытияФормыВыбора(ЭтаФорма, "Работы");
		КонецЕсли;
		
		ВыбранноеЗначение = ВыбранноеЗначение.Номенклатура;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДекорацияПечатьНажатие(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьИзмененияРеквизитовПечати", ЭтотОбъект);
	ОткрытьФорму("Обработка.РеквизитыПечати.Форма.РеквизитыПечатиЗаданиеНаРаботу", Новый Структура("КонтекстПечати", Объект), ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьСписком(Команда)
	
	Если Элементы.РедактироватьСписком.Пометка И Объект.Работы.Количество() > 1 Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьВозможностьРедактированияСпискомЗавершение", ЭтотОбъект);
		
		ПоказатьВопрос(
			ОписаниеОповещения,
			НСтр("ru='Все строки кроме первой будут удалены. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет
		);
		Возврат;
	КонецЕсли;
	
	Элементы.РедактироватьСписком.Пометка = НЕ Элементы.РедактироватьСписком.Пометка;
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ШапкаТабличнаяЧасть(Команда)
	
	ЗаполнениеОбъектовУНФКлиент.ЗаполнитьПустыеПоложения(Объект, "ПоложениеВидаРабот");
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ПоложениеВидаРаботВЗаданииНаРаботу", Объект.ПоложениеВидаРабот);
	СтруктураПараметров.Вставить("БылиВнесеныИзменения", Ложь);
	
	ОткрытьФорму("ОбщаяФорма.ШапкаТабличнаяЧасть", СтруктураПараметров,,,,, Новый ОписаниеОповещения("ШапкаТабличнаяЧастьЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ШапкаТабличнаяЧастьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.БылиВнесеныИзменения Тогда
		
		Если Объект.ПоложениеВидаРабот<>Результат.ПоложениеВидаРаботВЗаданииНаРаботу Тогда
			Объект.ПоложениеВидаРабот = Результат.ПоложениеВидаРаботВЗаданииНаРаботу;
			ЗаполнениеОбъектовУНФКлиент.ОбработатьИзменениеПоложения(Объект, "Работы", "ВидРабот", "ПоложениеВидаРабот");
		КонецЕсли;
		
		УправлениеФормой();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДополнительныйРеквизит(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущийНаборСвойств", ПредопределенноеЗначение("Справочник.НаборыДополнительныхРеквизитовИСведений.Документ_ЗаданиеНаРаботу"));
	ПараметрыФормы.Вставить("ЭтоДополнительноеСведение", Ложь);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта", ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


&НаСервереБезКонтекста
Функция ПолучитьДанныеПодразделенияПриИзменении(СтруктурнаяЕдиница)
	
	СтруктураДанные = Новый Структура("ПодписьМОЛ");
	
	Если ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда
		
		СтруктураДанные.ПодписьМОЛ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "ПодписьМОЛ");
		
	КонецЕсли;
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьИзмененияРеквизитовПечати(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		ПечатьДокументовУНФКлиент.ОбновитьЗначенияРеквизитовПечати(ЭтотОбъект, Результат.ИзмененныеРеквизиты);
		
		Если Результат.Свойство("Команда") Тогда
			
			Подключаемый_ВыполнитьКоманду(Результат.Команда);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеДатаПриИзменении(ДокументСсылка, ДатаНовая, ДатаПередИзменением)
	
	СтруктураДанные = Новый Структура();
	РазностьДат = ДокументыУНФ.ПроверитьНомерДокумента(ДокументСсылка, ДатаНовая, ДатаПередИзменением);
	СтруктураДанные.Вставить("РазностьДат", РазностьДат);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДатаПриИзменении()

&НаСервереБезКонтекста
Функция ПолучитьДанныеОрганизацияПриИзменении(Организация)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Компания", Константы.УчетПоКомпании.Компания(Организация));
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаКлиенте
Процедура УстановитьВозможностьРедактированияСпискомЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Пока Объект.Работы.Количество() > 1 Цикл
		Объект.Работы.Удалить(Объект.Работы.Количество()-1);
	КонецЦикла;
	
	Элементы.РедактироватьСписком.Пометка = НЕ Элементы.РедактироватьСписком.Пометка;
	УправлениеФормой();
	
КонецПроцедуры

// Выполняем пересчет цены табличной части документа после изменений в форме 
// "Цены и валюта".
//
// Параметры:
//  СтруктураРеквизитов - Структура реквизитов, необходимых при пересчете
//  ТабличнаяЧастьДокумента - ДанныеФормыСтруктура, содержит табличную часть
//                 документа.
//
&НаСервереБезКонтекста
Процедура ПолучитьЦеныТабличнойЧастиПоВидуЦен(СтруктураДанных, ТабличнаяЧастьДокумента)
	
	// 1. Отбор по номенклатуре
	МассивНоменклатуры = Новый Массив;
	Для каждого СтрокаТЧ Из ТабличнаяЧастьДокумента Цикл
		
		МассивНоменклатуры.Добавить(СтрокаТЧ.Номенклатура);
		
	КонецЦикла;
	
	// 2. Заполним цены.
	ВидЦенПараметр = СтруктураДанных.ВидЦен;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	|	ЦеныНоменклатурыСрезПоследних.ВидЦен.ВалютаЦены КАК ЦеныНоменклатурыВалюта,
	|	ЦеныНоменклатурыСрезПоследних.ВидЦен.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ЦеныНоменклатурыСрезПоследних.ВидЦен.ПорядокОкругления КАК ПорядокОкругления,
	|	ЦеныНоменклатурыСрезПоследних.ВидЦен.ОкруглятьВБольшуюСторону КАК ОкруглятьВБольшуюСторону,
	|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Цена
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|			&ДатаОбработки,
	|			ВидЦен = &ВидЦен
	|				И Номенклатура В (&МассивНоменклатуры)) КАК ЦеныНоменклатурыСрезПоследних
	|ГДЕ
	|	ЦеныНоменклатурыСрезПоследних.Актуальность";
		
	Запрос.УстановитьПараметр("ДатаОбработки",	 СтруктураДанных.Дата);
	Запрос.УстановитьПараметр("ВидЦен",			 ВидЦенПараметр);
	Запрос.УстановитьПараметр("МассивНоменклатуры", МассивНоменклатуры);
	
	ТаблицаЦен = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТабЧасти Из ТабличнаяЧастьДокумента Цикл
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Номенклатура", СтрокаТабЧасти.Номенклатура);
		
		РезультатПоиска = ТаблицаЦен.НайтиСтроки(СтруктураПоиска);
		Если РезультатПоиска.Количество() > 0 Тогда
			
			Цена = РезультатПоиска[0].Цена;
			Если Цена = 0 Тогда
				
				СтрокаТабЧасти.Цена = Цена;
				
			Иначе
				
				ПорядокОкругления = РезультатПоиска[0].ПорядокОкругления;
				ОкруглятьВБольшуюСторону = РезультатПоиска[0].ОкруглятьВБольшуюСторону;
				
				СтрокаТабЧасти.Цена = ЦенообразованиеСервер.ОкруглитьЦену(Цена, ПорядокОкругления, ОкруглятьВБольшуюСторону); 
				
			КонецЕсли;
			
		Иначе
			
			СтрокаТабЧасти.Цена = 0;
		
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ПолучитьЦеныТабличнойЧастиПоВидуЦен()

&НаСервереБезКонтекста
Функция ПолучитьЦену(СтруктураДанные)
	
	СтруктураДанные.Вставить("Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	СтруктураДанные.Вставить("СуммаВключаетНДС", СтруктураДанные.ВидЦен.ЦенаВключаетНДС);
	СтруктураДанные.Вставить("ВалютаДокумента", СтруктураДанные.ВидЦен.ВалютаЦены);
	СтруктураДанные.Вставить("Коэффициент", 1);
	СтруктураДанные.Вставить("МинимальнаяЦена", 0);
	
	Если ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ЦеныНоменклатуры) Тогда
		СтруктураДанные.Вставить("Цена", ЦенообразованиеСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураДанные));
		СтруктураДанные.Вставить("МинимальнаяЦена", ЦенообразованиеСервер.ПолучитьМинимальнуюЦенуНоменклатуры(СтруктураДанные));		
	Иначе
		СтруктураДанные.Вставить("Цена", 0);
	КонецЕсли;
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеНоменклатураПриИзменении()	

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьТрудоемкость(ДанныеСтроки)
	
	РассчитыватьТрудоемкостьАвтоматически = Не ЗначениеЗаполнено(ДанныеСтроки.Трудоемкость)
		Или ДанныеСтроки.Трудоемкость = ДанныеСтроки.Продолжительность;
		
	Если РассчитыватьТрудоемкостьАвтоматически Тогда
		ДанныеСтроки.Трудоемкость = (ДанныеСтроки.ДатаОкончания - ДанныеСтроки.ДатаНачала) / 3600;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьПродолжительность(ДанныеСтроки)
	
	ДанныеСтроки.Продолжительность = (ДанныеСтроки.ДатаОкончания - ДанныеСтроки.ДатаНачала) / 3600;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьСумму(ДанныеСтроки)
	
	ДанныеСтроки.Сумма = ДанныеСтроки.Цена * ДанныеСтроки.Трудоемкость;
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормой()
	
	ВидРаботВШапке			= Объект.ПоложениеВидаРабот = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке");
	ВнешнееЗадание			= Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаданиеНаРаботу.Внешнее");
	РедактироватьСписком	= Элементы.РедактироватьСписком.Пометка;
	
	Элементы.ВидРаботСписком.Видимость			= ВидРаботВШапке;
	Элементы.ВидРабот.Видимость					= ВидРаботВШапке;
	Элементы.РаботыВидРабот.Видимость			= Не ВидРаботВШапке;
	Элементы.РаботыСтрокаВидРабот.Видимость		= Не ВидРаботВШапке;
	Элементы.Списком.Видимость					= РедактироватьСписком;
	Элементы.ОднаСтрока.Видимость				= Не РедактироватьСписком;
	
	Элементы.ВидЦен.Видимость					= ВнешнееЗадание;
	Элементы.Стоимость.Видимость				= ВнешнееЗадание;
	Элементы.РаботыНоменклатура.Видимость		= ВнешнееЗадание;
	Элементы.РаботыХарактеристика.Видимость		= ВнешнееЗадание;
	
	//Элементы.РаботыСтрокаЗаказчик.Видимость		= ВнешнееЗадание;
	Элементы.РаботыСтрокаНоменклатура.Видимость	= ВнешнееЗадание;
	Элементы.РаботыСтрокаХарактеристика.Видимость= ВнешнееЗадание;
	
	Элементы.РаботыЦена.Видимость				= ВнешнееЗадание;
	Элементы.РаботыСумма.Видимость				= ВнешнееЗадание;
	
	Элементы.РаботыИтогСумма.Видимость			= ВнешнееЗадание;
	// Для неполных прав нет элемента формы
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВидЦенВалютаЦены", "Видимость", ВнешнееЗадание);
	
	Если ВнешнееЗадание Тогда
		Элементы.РаботыЗаказчик.ВыбиратьТип = Истина;
		Элементы.РаботыЗаказчик.ОграничениеТипа = Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя, СправочникСсылка.ДоговорыКонтрагентов, СправочникСсылка.Контрагенты");
		Элементы.РаботыЗаказчик.Заголовок = НСтр("ru = 'Заказчик'");
	Иначе
		Элементы.РаботыЗаказчик.ВыбиратьТип = Ложь;
		Элементы.РаботыЗаказчик.ОграничениеТипа = Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя");
		Элементы.РаботыЗаказчик.Заголовок = НСтр("ru = 'Заказ'");
	КонецЕсли;
	
	Элементы.РаботыСтрокаЗаказчик.ВыбиратьТип = Элементы.РаботыЗаказчик.ВыбиратьТип;
	Элементы.РаботыСтрокаЗаказчик.ОграничениеТипа = Элементы.РаботыЗаказчик.ОграничениеТипа;
	Элементы.РаботыСтрокаЗаказчик.Заголовок = Элементы.РаботыЗаказчик.Заголовок;
	
КонецПроцедуры

&НаСервереБезКонтекста 
Функция ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные)
	
	// Характеристики
	СтруктураДанные.Вставить("ИспользоватьХарактеристики",Ложь);
	СтруктураДанные.Вставить("ПроверятьЗаполнениеХарактеристики",Ложь);
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура) И СтруктураДанные.Номенклатура.ИспользоватьХарактеристики
		Тогда
		ЗначенияПоУмолчанию = НоменклатураВДокументахСервер.ЗначенияНоменклатурыПоУмолчанию(СтруктураДанные.Номенклатура);
		
		Если Не ЗначенияПоУмолчанию = Неопределено
			Тогда		
			ХарактеристикаПоУмолчанию = ЗначенияПоУмолчанию;
		КонецЕсли;
		
		Если НЕ СтруктураДанные.Свойство("Характеристика") Тогда
			СтруктураДанные.Вставить("Характеристика",ХарактеристикаПоУмолчанию);
		Иначе
			СтруктураДанные.Характеристика = ?(ЗначениеЗаполнено(СтруктураДанные.Характеристика), СтруктураДанные.Характеристика, ХарактеристикаПоУмолчанию);
		КонецЕсли;
		
		СтруктураДанные.Вставить("ИспользоватьХарактеристики",Истина);
		СтруктураДанные.Вставить("ПроверятьЗаполнениеХарактеристики",СтруктураДанные.Номенклатура.ПроверятьЗаполнениеХарактеристики);
	КонецЕсли; 
	// Конец Характеристики
	
	Возврат СтруктураДанные;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеОбъектов

&НаКлиенте
Процедура СохранитьДокументКакШаблон(Параметр) Экспорт
	
	ЗаполнениеОбъектовУНФКлиент.СохранитьДокументКакШаблон(Объект, ОтображаемыеРеквизиты(), Параметр);
	
КонецПроцедуры

&НаСервере
Функция ОтображаемыеРеквизиты()
	
	Возврат ЗаполнениеОбъектовУНФ.ОтображаемыеРеквизиты(ЭтотОбъект);
	
КонецФункции

#КонецОбласти

#Область КопированиеСтрокТабличныхЧастей

&НаКлиенте
Процедура РаботыКопироватьСтроки(Команда)
	
	КопироватьСтроки("Работы");
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыВставитьСтроки(Команда)
	
	ВставитьСтроки("Работы");
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьСтроки(ИмяТЧ)
	
	Если КопированиеТабличнойЧастиКлиент.МожноКопироватьСтроки(Объект[ИмяТЧ], Элементы[ИмяТЧ].ТекущиеДанные) Тогда
		КоличествоСкопированных = 0;
		КопироватьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных);
		КопированиеТабличнойЧастиКлиент.ОповеститьПользователяОКопированииСтрок(КоличествоСкопированных);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСтроки(ИмяТЧ)
	
	КоличествоСкопированных = 0;
	КоличествоВставленных = 0;
	ВставитьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных, КоличествоВставленных);
	КопированиеТабличнойЧастиКлиент.ОповеститьПользователяОВставкеСтрок(КоличествоСкопированных, КоличествоВставленных);
	
КонецПроцедуры

&НаСервере
Процедура КопироватьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных)
	
	КопированиеТабличнойЧастиСервер.Копировать(Объект[ИмяТЧ], Элементы[ИмяТЧ].ВыделенныеСтроки, КоличествоСкопированных);
	
КонецПроцедуры

&НаСервере
Процедура ВставитьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных, КоличествоВставленных)
	
	КопированиеТабличнойЧастиСервер.Вставить(Объект, ИмяТЧ, Элементы, КоличествоСкопированных, КоличествоВставленных);
	ОбработатьВставленныеСтрокиНаСервере(ИмяТЧ, КоличествоВставленных); 
	ЦенообразованиеСервер.ДобавитьКолонкуИЗаполнитьМинимальныеЦеныВТЧ(ЭтотОбъект, "Работы");
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВставленныеСтрокиНаСервере(ИмяТЧ, КоличествоВставленных)
	
	Если КоличествоВставленных = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// При включении на форме списка первая строка пустая,
	// если произошла вставка строк и первая строка не заполнена - удаляем ее.
	Если Объект[ИмяТЧ].Количество() = КоличествоВставленных + 1 Тогда
		Если НЕ ЗначениеЗаполнено(Объект[ИмяТЧ][0].Номенклатура) Тогда
			Объект[ИмяТЧ].Удалить(0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры


// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти
