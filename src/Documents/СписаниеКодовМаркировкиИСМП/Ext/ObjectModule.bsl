#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Товары.Очистить();
		ШтрихкодыУпаковок.Очистить();
	КонецЕсли;
	
	ИнтеграцияИСМППереопределяемый.ОбработкаЗаполненияДокумента(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
	ЗаполнитьОбъектПоСтатистике();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ИнтеграцияИСПереопределяемый.ПередЗаписьюОбъекта(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ОтчетПроизводственнойЛинии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары");
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("ДанныеОтчетаПроизводственнойЛинии");
	КонецЕсли;
	
	Если ВидПродукции <> Перечисления.ВидыПродукцииИС.АльтернативныйТабак Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ВидПервичногоДокумента");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаПервичногоДокумента");
		МассивНепроверяемыхРеквизитов.Добавить("НомерПервичногоДокумента");
		МассивНепроверяемыхРеквизитов.Добавить("НаименованиеПервичногоДокумента");
		
	ИначеЕсли ВидПервичногоДокумента <> Перечисления.ВидыПервичныхДокументовИСМП.Прочее Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("НаименованиеПервичногоДокумента");
		
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары.GTIN");
	
	Если ИнтеграцияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукции) Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.ПричинаСписания");
		
		Если ВидПродукции = Перечисления.ВидыПродукцииИС.АльтернативныйТабак Тогда
			ПроверитьЗаполнениеАдреса(Отказ);
		КонецЕсли;
		
	ИначеЕсли ИнтеграцияИСПовтИсп.ЭтоПродукцияИСМП(ВидПродукции, Истина) Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ПричинаСписания");
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъект");
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъектИдентификатор");
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъектАдресСтрокой"); 
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъектИдентификатор");
		МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторПроизводственногоЗаказа");
		МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторПроизводственнойЛинии");
		
		Для Каждого СтрокаТовары Из Товары Цикл
			Если Не ЗначениеЗаполнено(СтрокаТовары.GTIN)
				И Не ЗначениеЗаполнено(СтрокаТовары.Номенклатура) Тогда
				ТекстСообщения = СтрШаблон(
					НСтр(
						"ru = 'В строке %1 табличной части Товары не заполнено поле GTIN'"),
						СтрокаТовары.НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
					ЭтотОбъект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
						"Товары", СтрокаТовары.НомерСтроки, "GTIN"),,
					Отказ);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ИнтеграцияИСМППереопределяемый.ПриОпределенииОбработкиПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ИнтеграцияИСМПСлужебный.ПроверитьЗаполнениеШтрихкодовУпаковок(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияИСМП.ЗаписатьСтатусДокументаИСМППоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДокументОснование   = Неопределено;
	ИдентификаторЗаявки = Неопределено;
	ШтрихкодыУпаковок.Очистить();
	ИдентификаторПроизводственногоЗаказа = "";
	ИдентификаторПроизводственнойЛинии   = "";
	ДанныеОтчетаПроизводственнойЛинии.Очистить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаЗаполнения

Процедура ЗаполнитьОбъектПоСтатистике()
	
	ДанныеСтатистики = ЗаполнениеОбъектовПоСтатистикеИСМП.ДанныеЗаполненияСписанияКодовМаркировкиИСМП(Организация);
	
	Для Каждого КлючИЗначение Из ДанныеСтатистики Цикл
		ЗаполнениеОбъектовПоСтатистикеИСМП.ЗаполнитьПустойРеквизит(ЭтотОбъект, ДанныеСтатистики, КлючИЗначение.Ключ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаПроверкиЗаполнения

Процедура ПроверитьЗаполнениеАдреса(Отказ)
	
	ДанныеСтраны = УправлениеКонтактнойИнформацией.СтранаАдресаКонтактнойИнформации(ПроизводственныйОбъектАдрес);
	
	Если ДанныеСтраны.Ссылка = ПредопределенноеЗначение("Справочник.СтраныМира.Россия") Тогда
		
		ДанныеКИ         = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияВJSON(ПроизводственныйОбъектАдрес);
		СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(ДанныеКИ);
		
		Если Не ЗначениеЗаполнено(СведенияОбАдресе.Индекс) Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнен Индекс в адресе'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ПроизводственныйОбъектАдресСтрокой",, Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СведенияОбАдресе.КодРегиона) Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнен Код региона в адресе'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ПроизводственныйОбъектАдресСтрокой",, Отказ);
		КонецЕсли;
		
	Иначе
		
		Если Не ЗначениеЗаполнено(ДанныеСтраны.Код) Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнен Код страны в адресе'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ПроизводственныйОбъектАдресСтрокой",, Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПроизводственныйОбъектАдресСтрокой) Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнен адрес'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ПроизводственныйОбъектАдресСтрокой",, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли