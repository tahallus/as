#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
	Возврат;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаКадровоеПеремещение, СтруктураДополнительныеСвойства) Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	КадровоеПеремещениеСотрудники.НомерСтроки,
	|	КадровоеПеремещениеСотрудники.Сотрудник,
	|	КадровоеПеремещениеСотрудники.СтруктурнаяЕдиница,
	|	КадровоеПеремещениеСотрудники.Должность,
	|	КадровоеПеремещениеСотрудники.ГрафикРаботы,
	|	КадровоеПеремещениеСотрудники.ЗанимаемыхСтавок,
	|	КадровоеПеремещениеСотрудники.Период,
	|	КадровоеПеремещениеСотрудники.Ссылка
	|ПОМЕСТИТЬ ТаблицаСотрудники
	|ИЗ
	|	Документ.КадровоеПеремещение.Сотрудники КАК КадровоеПеремещениеСотрудники
	|ГДЕ
	|	КадровоеПеремещениеСотрудники.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	КадровоеПеремещениеСотрудники.НомерСтроки,
	|	КадровоеПеремещениеСотрудники.Сотрудник,
	|	КадровоеПеремещениеСотрудники.Период,
	|	КадровоеПеремещениеНачисленияУдержания.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	|	КадровоеПеремещениеНачисленияУдержания.Валюта,
	|	КадровоеПеремещениеНачисленияУдержания.Сумма КАК Сумма,
	|	КадровоеПеремещениеНачисленияУдержания.СчетЗатрат,
	|	КадровоеПеремещениеНачисленияУдержания.Актуальность
	|ПОМЕСТИТЬ ТаблицаНачисленияУдержания
	|ИЗ
	|	Документ.КадровоеПеремещение.Сотрудники КАК КадровоеПеремещениеСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КадровоеПеремещение.НачисленияУдержания КАК КадровоеПеремещениеНачисленияУдержания
	|		ПО КадровоеПеремещениеСотрудники.КлючСвязи = КадровоеПеремещениеНачисленияУдержания.КлючСвязи
	|ГДЕ
	|	КадровоеПеремещениеСотрудники.Ссылка = &Ссылка
	|	И КадровоеПеремещениеНачисленияУдержания.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Организация,
	|	КадровоеПеремещениеСотрудники.НомерСтроки,
	|	КадровоеПеремещениеСотрудники.Сотрудник,
	|	КадровоеПеремещениеСотрудники.Период,
	|	КадровоеПеремещениеНалогиНаДоходы.ВидНачисленияУдержания,
	|	КадровоеПеремещениеНалогиНаДоходы.Валюта,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	КадровоеПеремещениеНалогиНаДоходы.Актуальность
	|ИЗ
	|	Документ.КадровоеПеремещение.Сотрудники КАК КадровоеПеремещениеСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КадровоеПеремещение.НалогиНаДоходы КАК КадровоеПеремещениеНалогиНаДоходы
	|		ПО КадровоеПеремещениеСотрудники.КлючСвязи = КадровоеПеремещениеНалогиНаДоходы.КлючСвязи
	|ГДЕ
	|	КадровоеПеремещениеСотрудники.Ссылка = &Ссылка
	|	И КадровоеПеремещениеНалогиНаДоходы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСотрудники.Организация,
	|	ТаблицаСотрудники.НомерСтроки,
	|	ТаблицаСотрудники.Сотрудник,
	|	ТаблицаСотрудники.СтруктурнаяЕдиница,
	|	ТаблицаСотрудники.Должность,
	|	ТаблицаСотрудники.ГрафикРаботы,
	|	ТаблицаСотрудники.ЗанимаемыхСтавок,
	|	ТаблицаСотрудники.Период
	|ИЗ
	|	ТаблицаСотрудники КАК ТаблицаСотрудники
	|ГДЕ
	|	ТаблицаСотрудники.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКадровоеПеремещение.ПеремещениеИИзменениеСпособаОплаты)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНачисленияУдержания.Организация,
	|	ТаблицаНачисленияУдержания.НомерСтроки,
	|	ТаблицаНачисленияУдержания.Сотрудник,
	|	ТаблицаНачисленияУдержания.Период,
	|	ТаблицаНачисленияУдержания.ВидНачисленияУдержания,
	|	ТаблицаНачисленияУдержания.Валюта,
	|	ТаблицаНачисленияУдержания.Сумма,
	|	ТаблицаНачисленияУдержания.СчетЗатрат,
	|	ТаблицаНачисленияУдержания.Актуальность
	|ИЗ
	|	ТаблицаНачисленияУдержания КАК ТаблицаНачисленияУдержания");
	                
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКадровоеПеремещение);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСотрудники", 				МассивРезультатов[2].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПлановыеНачисленияИУдержания", МассивРезультатов[3].Выгрузить());
	
	СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#Область ИнтерфейсПечати

Процедура СформироватьКадровыйПриказ(ОписаниеПечатнойФормы, МассивОбъектов, ОбъектыПечати) Экспорт
	Перем ПервыйДокумент, НомерСтрокиНачало;
	
	Макет				= УправлениеПечатью.МакетПечатнойФормы(ОписаниеПечатнойФормы.ПолныйПутьКМакету);
	ТабличныйДокумент	= ОписаниеПечатнойФормы.ТабличныйДокумент;
	ДанныеПечати		= Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбщиеСведенияОПеремещении.Ссылка,
	|	ОбщиеСведенияОПеремещении.Номер,
	|	ОбщиеСведенияОПеремещении.Дата КАК ДатаДокумента,
	|	ОбщиеСведенияОПеремещении.Организация КАК Организация,
	|	ОбщиеСведенияОПеремещении.Организация.Префикс КАК Префикс,
	|	ОбщиеСведенияОПеремещении.Организация.КодПоОКПО КАК КодПоОКПО,
	|	ОбщиеСведенияОПеремещении.ПодписьРуководителя.Должность КАК ДолжностьРуководителя,
	|	ОбщиеСведенияОПеремещении.ПодписьРуководителя.РасшифровкаПодписи КАК РуководительРасшифровкаПодписи
	|ИЗ
	|	Документ.КадровоеПеремещение КАК ОбщиеСведенияОПеремещении
	|ГДЕ
	|	ОбщиеСведенияОПеремещении.Ссылка В (&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОСотрудникеПрошлогоПериода.Сотрудник КАК Сотрудник,
	|	СведенияОСотрудникеПрошлогоПериода.Организация КАК Организация,
	|	Максимум(СведенияОСотрудникеПрошлогоПериода.Период) КАК ПериодПредыдущейЗаписи
	|ПОМЕСТИТЬ ПериодыПредыдущихЗаписей
	|ИЗ
	|	Документ.КадровоеПеремещение.Сотрудники КАК СведенияОСотрудниках
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники КАК СведенияОСотрудникеПрошлогоПериода
	|		ПО СведенияОСотрудниках.Ссылка.Организация = СведенияОСотрудникеПрошлогоПериода.Организация
	|		И СведенияОСотрудниках.Сотрудник = СведенияОСотрудникеПрошлогоПериода.Сотрудник
	|		И СведенияОСотрудниках.Период > СведенияОСотрудникеПрошлогоПериода.Период
	|ГДЕ
	|	СведенияОСотрудниках.Ссылка В (&МассивОбъектов)
	|СГРУППИРОВАТЬ ПО
	|	СведенияОСотрудникеПрошлогоПериода.Сотрудник,
	|	СведенияОСотрудникеПрошлогоПериода.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОСотрудниках.КлючСвязи КАК КлючСвязи,
	|	СведенияОСотрудниках.Период КАК ДатаПеревода,
	|	""постоянно"" КАК ВидПеревода,
	|	СведенияОСотрудниках.Сотрудник.Наименование КАК Работник,
	|	СведенияОСотрудниках.Сотрудник.Код КАК ТабельныйНомер,
	|	СведенияОСотрудникеПрошлогоПериода.СтруктурнаяЕдиница КАК ПодразделениеПредыдущее,
	|	СведенияОСотрудникеПрошлогоПериода.Должность.Наименование КАК ДолжностьПредыдущая,
	|	СведенияОСотрудниках.СтруктурнаяЕдиница КАК ПодразделениеНовое,
	|	СведенияОСотрудниках.Должность.Наименование КАК ДолжностьНовая
	|ИЗ
	|	Документ.КадровоеПеремещение.Сотрудники КАК СведенияОСотрудниках
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыПредыдущихЗаписей КАК ПериодыПредыдущихЗаписей
	|		ПО СведенияОСотрудниках.Ссылка.Организация = ПериодыПредыдущихЗаписей.Организация
	|		И СведенияОСотрудниках.Сотрудник = ПериодыПредыдущихЗаписей.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники КАК СведенияОСотрудникеПрошлогоПериода
	|		ПО СведенияОСотрудниках.Ссылка.Организация = СведенияОСотрудникеПрошлогоПериода.Организация
	|		И СведенияОСотрудниках.Сотрудник = СведенияОСотрудникеПрошлогоПериода.Сотрудник
	|		И ПериодыПредыдущихЗаписей.ПериодПредыдущейЗаписи = СведенияОСотрудникеПрошлогоПериода.Период
	|ГДЕ
	|	СведенияОСотрудниках.Ссылка В (&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОНачислениях.КлючСвязи КАК КлючСвязи,
	|	СведенияОНачислениях.Сумма КАК СуммаНачисления,
	|	ВидНачисленияУдержания.Наименование КАК ПредставлениеНачисления
	|ИЗ
	|	Документ.КадровоеПеремещение.НачисленияУдержания КАК СведенияОНачислениях
	|ГДЕ
	|	СведенияОНачислениях.Ссылка В (&МассивОбъектов)
	|	И СведенияОНачислениях.ВидНачисленияУдержания.Тип = Значение(Перечисление.ТипыНачисленийИУдержаний.Начисление)
	|УПОРЯДОЧИТЬ ПО
	|	СведенияОНачислениях.НомерСтроки";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ОбщиеСведенияОПриеме = РезультатЗапроса[0].Выгрузить();
	СведенияОСотрудниках = РезультатЗапроса[2].Выгрузить();
	СведенияОНачислениях = РезультатЗапроса[3].Выгрузить();
	
	Для каждого ДанныеСотрудника Из СведенияОСотрудниках Цикл
		
		ПечатьДокументовУНФ.ПередНачаломФормированияДокумента(ТабличныйДокумент, ПервыйДокумент, НомерСтрокиНачало, ДанныеПечати);
		
		СведенияОбОрганизации = ПечатьДокументовУНФ.СведенияОЮрФизЛице(ОбщиеСведенияОПриеме[0].Организация, ОбщиеСведенияОПриеме[0].ДатаДокумента, ,);
		ДанныеПечати.Вставить("НазваниеОрганизации", ПечатьДокументовУНФ.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование"));
		ДанныеПечати.Вставить("КодПоОКПО", ОбщиеСведенияОПриеме[0].КодПоОКПО);
		ДанныеПечати.Вставить("НомерДокумента", ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(ОбщиеСведенияОПриеме[0].ДатаДокумента, ОбщиеСведенияОПриеме[0].Номер, ОбщиеСведенияОПриеме[0].Префикс));
		ДанныеПечати.Вставить("ДатаДокумента", ОбщиеСведенияОПриеме[0].ДатаДокумента);
		
		ДанныеПечати.Вставить("ДатаНачала", ДанныеСотрудника.ДатаПеревода);
		ДанныеПечати.Вставить("ДатаОкончания", Неопределено);
		ДанныеПечати.Вставить("Работник", ДанныеСотрудника.Работник);
		ДанныеПечати.Вставить("ТабельныйНомер", ДанныеСотрудника.ТабельныйНомер);
		ДанныеПечати.Вставить("ВидПеревода", ДанныеСотрудника.ВидПеревода);
		ДанныеПечати.Вставить("ПодразделениеПредыдущее", ДанныеСотрудника.ПодразделениеПредыдущее);
		ДанныеПечати.Вставить("ДолжностьПредыдущая", ДанныеСотрудника.ДолжностьПредыдущая);
		ДанныеПечати.Вставить("ПодразделениеНовое", ДанныеСотрудника.ПодразделениеНовое);
		ДанныеПечати.Вставить("ДолжностьНовая", ДанныеСотрудника.ДолжностьНовая);
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСвязи", ДанныеСотрудника.КлючСвязи);
		
		ДанныеПечати.Вставить("ОкладТарифнаяСтавкаЦелаяЧасть", Неопределено);
		ДанныеПечати.Вставить("ОкладТарифнаяСтавкаДробнаяЧасть", Неопределено);
		ДанныеПечати.Вставить("Надбавка", Неопределено);
		ДанныеПечати.Вставить("ВалютаТарифнойСтавки", НСтр("ru ='руб.'"));
		ДанныеПечати.Вставить("КопейкиТарифнойСтавки", НСтр("ru ='коп.'"));
		
		ПорядковыйНомерНачисления = 0;
		
		НайденныеСтроки = СведенияОНачислениях.НайтиСтроки(СтруктураОтбора);
		Для каждого СтрокаНачисления Из НайденныеСтроки Цикл
			
			Если ПорядковыйНомерНачисления = 0 Тогда
				
				ДанныеПечати.ОкладТарифнаяСтавкаЦелаяЧасть = Цел(СтрокаНачисления.СуммаНачисления);
				ДанныеПечати.ОкладТарифнаяСтавкаДробнаяЧасть = Формат(СтрокаНачисления.СуммаНачисления - ДанныеПечати.ОкладТарифнаяСтавкаЦелаяЧасть, "ЧН=00");
				
				ПорядковыйНомерНачисления = ПорядковыйНомерНачисления + 1;
				
			ИначеЕсли ПорядковыйНомерНачисления = 1 Тогда
				
				ДанныеПечати.Надбавка  = СокрЛП(СтрокаНачисления.ПредставлениеНачисления) + ": " + Строка(СтрокаНачисления.СуммаНачисления);
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ДанныеПечати.Вставить("ДолжностьРуководителя", ОбщиеСведенияОПриеме[0].ДолжностьРуководителя);
		ДанныеПечати.Вставить("РуководительРасшифровкаПодписи", ОбщиеСведенияОПриеме[0].РуководительРасшифровкаПодписи);
		
		Макет.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(Макет);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ОбщиеСведенияОПриеме[0].Ссылка);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Приказ_Т5");
	Если ПечатнаяФорма <> Неопределено Тогда
		
		ПечатнаяФорма.ТабличныйДокумент = Новый ТабличныйДокумент;
		ПечатнаяФорма.ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КадровоеПеремещение_Т5";
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ.КадровоеПеремещение.ПФ_MXL_Т5";
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Т-5 (Приказ о переводе)'");
		
		СформироватьКадровыйПриказ(ПечатнаяФорма, МассивОбъектов, ОбъектыПечати);
		
	КонецЕсли;
	
	// параметры отправки печатных форм по электронной почте
	ЭлектроннаяПочтаУНФ.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, 
		КоллекцияПечатныхФорм);
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Приказ_Т5";
	КомандаПечати.Представление = НСтр("ru = 'Т-5 (Приказ о переводе)'");
	КомандаПечати.Порядок = 1;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли