
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьИтоги()
	
	Итоги = Новый Соответствие;
	
	ИменаРеквизитовВидовВремени = Новый Соответствие;
	ИменаРеквизитовВидовВремени.Вставить("ПервыйВидВремени", "ПервыйЧасы");
	ИменаРеквизитовВидовВремени.Вставить("ВторойВидВремени", "ВторойЧасы");
	ИменаРеквизитовВидовВремени.Вставить("ТретийВидВремени", "ТретийЧасы");
	
	Для Итератор = 1 По 31 Цикл
		
		Для каждого ЭлементСоответствия Из ИменаРеквизитовВидовВремени Цикл
			
			ИмяРеквизитаВидаВремени = ЭлементСоответствия.Ключ + Строка(Итератор);
			ИмяРеквизитаЧасов = ЭлементСоответствия.Значение + Строка(Итератор);
			
			Если ЗначениеЗаполнено(ЭтотОбъект[ИмяРеквизитаВидаВремени])
				И ЭтотОбъект[ИмяРеквизитаЧасов] <> 0 Тогда
				
				Если Итоги.Получить(ЭтотОбъект[ИмяРеквизитаВидаВремени]) = Неопределено Тогда
					
					Итоги.Вставить(ЭтотОбъект[ИмяРеквизитаВидаВремени], ЭтотОбъект[ИмяРеквизитаЧасов]);
					
				Иначе
					
					Итоги[ЭтотОбъект[ИмяРеквизитаВидаВремени]] = Итоги[ЭтотОбъект[ИмяРеквизитаВидаВремени]] + ЭтотОбъект[ИмяРеквизитаЧасов];
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ВсегоЧасов = 0;
	Элементы.ДекорацияИтоги.Заголовок = "";
	Для каждого ЭлементСоответствия Из Итоги Цикл
		
		Элементы.ДекорацияИтоги.Заголовок = Элементы.ДекорацияИтоги.Заголовок + ?(ПустаяСтрока(Элементы.ДекорацияИтоги.Заголовок), "", ", ") + Строка(ЭлементСоответствия.Ключ) + ":" + ЭлементСоответствия.Значение;
		ВсегоЧасов = ВсегоЧасов + ЭлементСоответствия.Значение;
		
	КонецЦикла;
	
	Элементы.ДекорацияИтоги.Заголовок = НСтр("ru ='Всего:'") + Строка(ВсегоЧасов) + НСтр("ru ='. Из них '") + Элементы.ДекорацияИтоги.Заголовок;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииВидаВремени(ИмяРеквизита)
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект[ИмяРеквизита]) Тогда
		
		ИменаРеквизитовВидовВремени = Новый Соответствие;
		ИменаРеквизитовВидовВремени.Вставить("ПервыйВидВремени", "ПервыйЧасы");
		ИменаРеквизитовВидовВремени.Вставить("ВторойВидВремени", "ВторойЧасы");
		ИменаРеквизитовВидовВремени.Вставить("ТретийВидВремени", "ТретийЧасы");
		
		Для каждого ЭлементСоответствия Из ИменаРеквизитовВидовВремени Цикл
			
			Если Найти(ИмяРеквизита, ЭлементСоответствия.Ключ) > 0 Тогда
				
				Индекс = СтрЗаменить(ИмяРеквизита, ЭлементСоответствия.Ключ, "");
				ИмяРеквизитаЧасы = ЭлементСоответствия.Значение + Индекс;
				ЭтотОбъект[ИмяРеквизитаЧасы] = 0;
				
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКоличестваЧасов(ИмяРеквизита)
	
	Если ЭтотОбъект[ИмяРеквизита] > 0 Тогда
		
		ИменаРеквизитовВидовВремени = Новый Соответствие;
		ИменаРеквизитовВидовВремени.Вставить("ПервыйВидВремени", "ПервыйЧасы");
		ИменаРеквизитовВидовВремени.Вставить("ВторойВидВремени", "ВторойЧасы");
		ИменаРеквизитовВидовВремени.Вставить("ТретийВидВремени", "ТретийЧасы");
		
		Для каждого ЭлементСоответствия Из ИменаРеквизитовВидовВремени Цикл
			
			Если Найти(ИмяРеквизита, ЭлементСоответствия.Значение) > 0 Тогда
				
				Индекс = СтрЗаменить(ИмяРеквизита, ЭлементСоответствия.Значение, "");
				ИмяРеквизитаВидВремени = ЭлементСоответствия.Ключ + Индекс;
				Если НЕ ЗначениеЗаполнено(ЭтотОбъект[ИмяРеквизитаВидВремени]) Тогда
					
					ЭтотОбъект[ИмяРеквизитаВидВремени] = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Справочник.ВидыРабочегоВремени.Работа");
					
				КонецЕсли;
				
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОформитьИПодписатьДниНедели()
	
	СоответствиеДнейНедели = Новый Соответствие;
	СоответствиеДнейНедели.Вставить(1, НСтр("ru ='понедельник'"));
	СоответствиеДнейНедели.Вставить(2, НСтр("ru ='вторник'"));
	СоответствиеДнейНедели.Вставить(3, НСтр("ru ='среда'"));
	СоответствиеДнейНедели.Вставить(4, НСтр("ru ='четверг'"));
	СоответствиеДнейНедели.Вставить(5, НСтр("ru ='пятница'"));
	СоответствиеДнейНедели.Вставить(6, НСтр("ru ='суббота'"));
	СоответствиеДнейНедели.Вставить(7, НСтр("ru ='воскресенье'"));
	
	// АПК:1036-выкл. проверка орфографии не требуется
	СоответствиеМесяц = Новый Соответствие;
	СоответствиеМесяц.Вставить(1, НСтр("ru ='янв., '"));
	СоответствиеМесяц.Вставить(2, НСтр("ru ='фев., '"));
	СоответствиеМесяц.Вставить(3, НСтр("ru ='март, '"));
	СоответствиеМесяц.Вставить(4, НСтр("ru ='апр., '"));
	СоответствиеМесяц.Вставить(5, НСтр("ru ='май, '"));
	СоответствиеМесяц.Вставить(6, НСтр("ru ='июнь, '"));
	СоответствиеМесяц.Вставить(7, НСтр("ru ='июль, '"));
	СоответствиеМесяц.Вставить(8, НСтр("ru ='авг., '"));
	СоответствиеМесяц.Вставить(9, НСтр("ru ='сен., '"));
	СоответствиеМесяц.Вставить(10, НСтр("ru ='окт., '"));
	СоответствиеМесяц.Вставить(11, НСтр("ru ='нояб., '"));
	СоответствиеМесяц.Вставить(12, НСтр("ru ='дек., '"));
	// АПК:1036-вкл
	
	РасшифровкаМесяца = СоответствиеМесяц.Получить(Месяц(ПериодРегистрации));
	Для День = 1 По День(КонецМесяца(ПериодРегистрации)) Цикл
		
		ИмяЭлемента = "Декорация" + Строка(День);
		ДеньНедели = ДеньНедели(Дата(Год(ПериодРегистрации), Месяц(ПериодРегистрации), День));
		
		ЦветЗаголовка = ?(ДеньНедели > 5, Новый Цвет(200, 0, 0), Новый Цвет(0, 0, 205));
		
		Часть1 = Строка(День) + Символы.НПП + РасшифровкаМесяца;
		Часть2 = СоответствиеДнейНедели.Получить(ДеньНедели);
		
		ФорматированнаяСтрока0 = Новый ФорматированнаяСтрока(Часть1, Новый Шрифт(, 12, Истина, Истина), ЦветЗаголовка);
		ФорматированнаяСтрока1 = Новый ФорматированнаяСтрока(Часть2, Новый Шрифт(, 8, Истина, Истина), ЦветЗаголовка);
		
		А = Новый Массив(2);
		А[0] = ФорматированнаяСтрока0;
		А[1] = ФорматированнаяСтрока1;
		
		Элементы[ИмяЭлемента].Заголовок = Новый ФорматированнаяСтрока(А);
		
	КонецЦикла;
	
	Если День < 31 Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаДень31", "Видимость", Ложь);
		
	КонецЕсли;
	
	Если День < 30 Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаДень30", "Видимость", Ложь);
		
	КонецЕсли;
	
	Если День < 29 Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаДень29", "Видимость", Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ЗначенияРеквизитовСтроки);
	
	ОформитьИПодписатьДниНедели();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПоказатьИтоги();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ЗначенияРеквизитовСтроки = Новый Структура;
	
	ИменаРеквизитов = Новый Массив(6);
	ИменаРеквизитов[0] = "ПервыйВидВремени";
	ИменаРеквизитов[1] = "ВторойВидВремени";
	ИменаРеквизитов[2] = "ТретийВидВремени";
	ИменаРеквизитов[3] = "ПервыйЧасы";
	ИменаРеквизитов[4] = "ВторойЧасы";
	ИменаРеквизитов[5] = "ТретийЧасы";
	
	ЗначенияРеквизитовСтроки.Вставить("Сотрудник", Сотрудник);
	ЗначенияРеквизитовСтроки.Вставить("Должность", Должность);
	ЗначенияРеквизитовСтроки.Вставить("СтрокаТаблицы", СтрокаТаблицы);
	
	Для Итератор = 1 По 31 Цикл
		
		Для каждого ИмяРеквизита Из ИменаРеквизитов Цикл
			
			Ключ = ИмяРеквизита + Строка(Итератор);
			ЗначенияРеквизитовСтроки.Вставить(Ключ, ЭтотОбъект[Ключ]);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Закрыть(ЗначенияРеквизитовСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРеквизитов

&НаКлиенте
Процедура ВидВремениПриИзменении(Элемент)
	
	ПриИзмененииВидаВремени(Элемент.Имя);
	ПоказатьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ЧасыПриИзменении(Элемент)
	
	ПриИзмененииКоличестваЧасов(Элемент.Имя);
	ПоказатьИтоги();
	
КонецПроцедуры

#КонецОбласти