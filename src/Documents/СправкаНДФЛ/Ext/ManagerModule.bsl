#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПроцедурыИФункцииПечати

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	// Устанавливаем признак доступности печати покомплектно
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;

	// Проверяем, нужно ли для макета формировать табличный документ
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Справки2НДФЛ") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"Справки2НДФЛ",
			"Справка 2-НДФЛ для сотрудника", СформироватьПечатнуюФорму2НДФЛ(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;

КонецПроцедуры


// Функция формирует табличный документ с печатной формой справки 2-НДФЛ
//
// Возвращаемое значение:
//  Табличный документ - печатная форма справки 2-НДФЛ
//
Функция СформироватьПечатнуюФорму2НДФЛ(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ДанныеНА = СправкиПоНДФЛ.СправкиНДФЛДанныеДляПечати(МассивОбъектов);
	ТаблицыДокумента = ТаблицыДанныхДокументаДляПечати(МассивОбъектов);
	Возврат СправкиПоНДФЛ.СформироватьПечатнуюФорму2НДФЛ(ОбъектыПечати, МассивОбъектов, ДанныеНА, ТаблицыДокумента.Сотрудники, ТаблицыДокумента.СведенияОДоходах, ТаблицыДокумента.СведенияОВычетах, ТаблицыДокумента.Уведомления); 
	
КонецФункции

// Заполняет список команд печати Справка 2-НДФЛ
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтчетность") Тогда
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Справки2НДФЛ";
		КомандаПечати.Представление = НСтр("ru = 'Справка 2-НДФЛ для сотрудника'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		КомандаПечати.Порядок = 1;
	КонецЕсли;
	
КонецПроцедуры

#Область ВспомогательныеПроцедурыИФункции

Функция ТаблицыДанныхДокументаДляПечати(Ссылка)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СправкаНДФЛ.Ссылка КАК Ссылка,
	|	СправкаНДФЛ.ВерсияДанных КАК ВерсияДанных,
	|	СправкаНДФЛ.ПометкаУдаления КАК ПометкаУдаления,
	|	СправкаНДФЛ.Номер КАК НомерСправки,
	|	СправкаНДФЛ.Дата КАК Дата,
	|	СправкаНДФЛ.Проведен КАК Проведен,
	|	СправкаНДФЛ.НалоговыйПериод КАК НалоговыйПериод,
	|	СправкаНДФЛ.Организация КАК Организация,
	|	СправкаНДФЛ.ОКАТО_КПП КАК ОКАТО_КПП,
	|	СправкаНДФЛ.Телефон КАК Телефон,
	|	СправкаНДФЛ.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	СправкаНДФЛ.СправкуПодписал КАК СправкуПодписал,
	|	СправкаНДФЛ.ДолжностьПодписавшегоЛица КАК ДолжностьПодписавшегоЛица,
	|	СправкаНДФЛ.КраткийСоставДокумента КАК КраткийСоставДокумента,
	|	СправкаНДФЛ.Ответственный КАК Ответственный,
	|	СправкаНДФЛ.Комментарий КАК Комментарий,
	|	СправкаНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СправкаНДФЛ.Ставка КАК Ставка,
	|	СправкаНДФЛ.Сотрудник КАК Сотрудник,
	|	СправкаНДФЛ.Фамилия КАК Фамилия,
	|	СправкаНДФЛ.Имя КАК Имя,
	|	СправкаНДФЛ.Отчество КАК Отчество,
	|	СправкаНДФЛ.АдресРФ КАК АдресРФ,
	|	СправкаНДФЛ.ВидДокумента КАК ВидДокумента,
	|	СправкаНДФЛ.СерияДокумента КАК СерияДокумента,
	|	СправкаНДФЛ.НомерДокумента КАК НомерДокумента,
	|	СправкаНДФЛ.ДатаРождения КАК ДатаРождения,
	|	СправкаНДФЛ.ИНН КАК ИНН,
	|	СправкаНДФЛ.Гражданство КАК Гражданство,
	|	СправкаНДФЛ.АдресЗарубежом КАК АдресЗарубежом,
	|	СправкаНДФЛ.СтатусНалогоплательщика КАК СтатусНалогоплательщика,
	|	ЕСТЬNULL(СуммыНалоговСотрудники.ОбщаяСуммаДохода, 0) КАК ОбщаяСуммаДохода,
	|	ЕСТЬNULL(СуммыНалоговСотрудники.ОблагаемаяСуммаДохода, 0) КАК ОблагаемаяСуммаДохода,
	|	ЕСТЬNULL(СуммыНалоговСотрудники.Исчислено, 0) КАК Исчислено,
	|	ЕСТЬNULL(СуммыНалоговСотрудники.Удержано, 0) КАК Удержано,
	|	ЕСТЬNULL(СуммыНалоговСотрудники.Задолженность, 0) КАК Задолженность,
	|	ЕСТЬNULL(СуммыНалоговСотрудники.ИзлишнеУдержано, 0) КАК ИзлишнеУдержано,
	|	ЕСТЬNULL(СуммыНалоговСотрудники.Перечислено, 0) КАК Перечислено
	|ИЗ
	|	Документ.СправкаНДФЛ КАК СправкаНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СправкаНДФЛ.СуммыНалогов КАК СуммыНалоговСотрудники
	|		ПО (СуммыНалоговСотрудники.Ссылка = СправкаНДФЛ.Ссылка)
	|ГДЕ
	|	СправкаНДФЛ.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СправкаНДФЛСведенияОДоходах.Ссылка КАК Ссылка,
	|	СправкаНДФЛСведенияОДоходах.НомерСтроки КАК НомерСтроки,
	|	СправкаНДФЛСведенияОДоходах.Месяц КАК МесяцНалоговогоПериода,
	|	СправкаНДФЛСведенияОДоходах.КодДохода КАК КодДохода,
	|	СправкаНДФЛСведенияОДоходах.СуммаДохода КАК СуммаДохода,
	|	СправкаНДФЛСведенияОДоходах.КодВычета КАК КодВычета,
	|	СправкаНДФЛСведенияОДоходах.СуммаВычета КАК СуммаВычета,
	|	СправкаНДФЛСведенияОДоходах.Ссылка.Номер КАК НомерСправки
	|ИЗ
	|	Документ.СправкаНДФЛ.СведенияОДоходах КАК СправкаНДФЛСведенияОДоходах
	|ГДЕ
	|	СправкаНДФЛСведенияОДоходах.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СправкаНДФЛСведенияОВычетах.Ссылка КАК Ссылка,
	|	СправкаНДФЛСведенияОВычетах.НомерСтроки КАК НомерСтроки,
	|	СправкаНДФЛСведенияОВычетах.КодВычета КАК КодВычета,
	|	СправкаНДФЛСведенияОВычетах.КодВычета.ГруппаВычета КАК ГруппаВычета,
	|	СправкаНДФЛСведенияОВычетах.СуммаВычета КАК СуммаВычета,
	|	СправкаНДФЛСведенияОВычетах.Ссылка.Номер КАК НомерСправки
	|ИЗ
	|	Документ.СправкаНДФЛ.СведенияОВычетах КАК СправкаНДФЛСведенияОВычетах
	|ГДЕ
	|	СправкаНДФЛСведенияОВычетах.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СправкаНДФЛСуммыНалогов.Ссылка КАК Ссылка,
	|	СправкаНДФЛСуммыНалогов.НомерСтроки КАК НомерСтроки,
	|	СправкаНДФЛСуммыНалогов.Ссылка.Номер КАК НомерСправки,
	|	СправкаНДФЛСуммыНалогов.Ставка КАК Ставка,
	|	СправкаНДФЛСуммыНалогов.ОбщаяСуммаДохода КАК ОбщаяСуммаДохода,
	|	СправкаНДФЛСуммыНалогов.ОблагаемаяСуммаДохода КАК ОблагаемаяСуммаДохода,
	|	СправкаНДФЛСуммыНалогов.Исчислено КАК Исчислено,
	|	СправкаНДФЛСуммыНалогов.Удержано КАК Удержано,
	|	СправкаНДФЛСуммыНалогов.Перечислено КАК Перечислено,
	|	СправкаНДФЛСуммыНалогов.ИзлишнеУдержано КАК ИзлишнеУдержано,
	|	СправкаНДФЛСуммыНалогов.Задолженность КАК Задолженность
	|ИЗ
	|	Документ.СправкаНДФЛ.СуммыНалогов КАК СправкаНДФЛСуммыНалогов
	|ГДЕ
	|	СправкаНДФЛСуммыНалогов.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Уведомления.Ссылка КАК Ссылка,
	|	Уведомления.Ссылка.Номер КАК НомерСправки,
	|	Уведомления.НомерСтроки КАК НомерСтроки,
	|	Уведомления.ДатаУведомления КАК ДатаУведомления,
	|	Уведомления.НомерУведомления КАК НомерУведомления,
	|	Уведомления.КодНалоговогоОрганаУведомления КАК КодНалоговогоОрганаУведомления,
	|	Уведомления.ГруппаВычета КАК ГруппаВычета
	|ИЗ
	|	Документ.СправкаНДФЛ.УведомленияНОоПравеНаВычеты КАК Уведомления";
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Возврат Новый Структура("Сотрудники, СведенияОДоходах, СведенияОВычетах, СведенияОбИтогах, Уведомления", Результаты[0].Выгрузить(), Результаты[1].Выгрузить(), Результаты[2].Выгрузить(), Результаты[3].Выгрузить(), Результаты[4].Выгрузить());
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли