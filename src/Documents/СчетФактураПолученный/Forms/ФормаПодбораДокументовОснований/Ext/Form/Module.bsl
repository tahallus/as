
#Область Служебные

&НаКлиенте
Процедура ДобавитьДокументКакОснованиеСчетФактуры()
	
	ТекущиеДанные = Элементы.ОснованияСчетаФактурыПолученного.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ТекущиеДанные.Недоступен Тогда 
		
		ПоказатьПредупреждение( , НСтр("ru='Этот документ уже выбран'"));
		Возврат;
		
	КонецЕсли;
	
	СтруктураВыбора = Новый Структура;
	
	СтруктураВыбора.Вставить("Документ", ТекущиеДанные.ДокументСсылка);
	
	ОповеститьОВыборе(СтруктураВыбора);
	
КонецПроцедуры

&НаСервере
Функция МассивДокументовСВыставленнымиСчетамиФактура()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", Параметры.Ссылка);
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.УстановитьПараметр("Контрагент", Параметры.Контрагент);
	Запрос.УстановитьПараметр("Договор", Параметры.Договор);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОснованияСчетовФактур.ДокументОснование КАК Ссылка
	|ИЗ Документ.СчетФактураПолученный.ДокументыОснования КАК ОснованияСчетовФактур
	|ГДЕ ОснованияСчетовФактур.Ссылка <> &ТекущийДокумент
	|	И ОснованияСчетовФактур.Ссылка.Организация = &Организация
	|	И ОснованияСчетовФактур.Ссылка.Контрагент = &Контрагент
	|	И ОснованияСчетовФактур.Ссылка.Договор = &Договор
	|";
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаЗапроса.ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти

#Область Форма

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ОснованияСчетаФактурыПолученного, "Организация",	Параметры.Организация, ВидСравненияКомпоновкиДанных.Равно, , Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ОснованияСчетаФактурыПолученного, "Контрагент",	Параметры.Контрагент, ВидСравненияКомпоновкиДанных.Равно, , Истина);
	
	Если Параметры.АвансыДоступны = Ложь Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ОснованияСчетаФактурыПолученного, "Договор",	Параметры.Договор, ВидСравненияКомпоновкиДанных.Равно, , Истина);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ОснованияСчетаФактурыПолученного, "ПоступленияДоступны", Параметры.ПоступленияДоступны, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ОснованияСчетаФактурыПолученного, "КомиссияДоступна", Параметры.КомиссияДоступна, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ОснованияСчетаФактурыПолученного, "АвансыДоступны", Параметры.АвансыДоступны, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ОснованияСчетаФактурыПолученного, "КорректировкиДоступны", Параметры.КорректировкиДоступны, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ОснованияСчетаФактурыПолученного, "ЭтоВозврат", Параметры.ЭтоВозврат, ВидСравненияКомпоновкиДанных.Равно, , Истина);
	
	МассивДокументов = МассивДокументовСВыставленнымиСчетамиФактура();
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ОснованияСчетаФактурыПолученного, "ДокументСсылка", МассивДокументов, ВидСравненияКомпоновкиДанных.НеВСписке, , Истина);
	
	ЗаголовокОрганизации = НСтр("ru ='Организация: %1'");
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияОтборОрганизация", "Заголовок", СтрШаблон(ЗаголовокОрганизации, Параметры.Организация));
	
	ЗаголовокКонтрагента = НСтр("ru ='Контрагент: %1 (%2)'");
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияОтборКонтрагентДоговор", "Заголовок", СтрШаблон(ЗаголовокКонтрагента, Параметры.Контрагент, Параметры.Договор));
	
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура ВыбратьДокументОснование(Команда)
	
	ДобавитьДокументКакОснованиеСчетФактуры();
	
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура ОснованияНаРеализациюВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДобавитьДокументКакОснованиеСчетФактуры();
	
КонецПроцедуры

#КонецОбласти