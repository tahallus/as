
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьВХранилищеПокупателейКомиссионногоТовара()
	
	ПоместитьВоВременноеХранилище(Объект.СчетаФактурыВыданныеПокупателям.Выгрузить(), КэшЗначений.АдресРасшифровкиПокупателей);
	
КонецПроцедуры

&НаСервере
Функция ДанныеДокументаТабличнойЧасти(СчетФактураВыданный)
	
	ДанныеДокумента = Новый Структура("Покупатель, Субкомиссионер, СуммаДокумента, СуммаНДСДокумента");
	
	Если ЗначениеЗаполнено(СчетФактураВыданный) Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетФактураВыданный, "Контрагент, СуммаДокумента, СуммаНДСДокумента");
		ЗаполнитьЗначенияСвойств(ДанныеДокумента, ЗначенияРеквизитов);
		
		ДанныеДокумента.Покупатель			= ЗначенияРеквизитов.Контрагент;
		
	КонецЕсли;
	
	Возврат ДанныеДокумента;
	
КонецФункции

&НаСервере
Процедура ОбновитьИтогиДокумента()
	
	Объект.СуммаДокумента = Объект.СчетаФактурыВыданныеПокупателям.Итог("Сумма");
	Объект.СуммаНДСДокумента = Объект.СчетаФактурыВыданныеПокупателям.Итог("НДС");
	
КонецПроцедуры

#КонецОбласти

#Область Форма

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КэшЗначений = Новый Структура("Ссылка, АдресРасшифровкиПокупателей");
	
	ЗаполнитьЗначенияСвойств(КэшЗначений, Параметры);
	ЗаполнитьЗначенияСвойств(Объект, Параметры);
	
	Объект.СчетаФактурыВыданныеПокупателям.Загрузить(ПолучитьИзВременногоХранилища(КэшЗначений.АдресРасшифровкиПокупателей));
	
КонецПроцедуры

#КонецОбласти

#Область Реквизиты

&НаКлиенте
Процедура СчетаФактурыВыданныеПокупателямСчетФактураПриИзменении(Элемент)
	
	ДанныеТекущейСтроки = Элементы.СчетаФактурыВыданныеПокупателям.ТекущиеДанные;
	Если ДанныеТекущейСтроки = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ДанныеДокумента = ДанныеДокументаТабличнойЧасти(ДанныеТекущейСтроки.СчетФактура);
	ЗаполнитьЗначенияСвойств(ДанныеТекущейСтроки, ДанныеДокумента);
	
	ДанныеТекущейСтроки.Сумма	= ДанныеДокумента.СуммаДокумента;
	ДанныеТекущейСтроки.НДС		= ДанныеДокумента.СуммаНДСДокумента;
	
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ЗаписатьВХранилищеПокупателейКомиссионногоТовара();
	
	КэшЗначений.Вставить("СуммаДокумента",		Объект.СуммаДокумента);
	КэшЗначений.Вставить("СуммаНДСДокумента",	Объект.СуммаНДСДокумента);
	КэшЗначений.Вставить("ВыполненаКоманда",	КодВозвратаДиалога.OK);
	
	Закрыть(КэшЗначений);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаФактурыВыданныеПокупателямПриИзменении(Элемент)
	
	ОбновитьИтогиДокумента();
	
КонецПроцедуры

#КонецОбласти