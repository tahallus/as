#Область ОписаниеПеременных

&НаКлиенте
Перем КэшНайденныхКонтактов;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура УстановитьДоступностьКнопкиОткрытияКонтрагента()
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Элементы.Контрагент.КнопкаОткрытия = Истина;
	Иначе
		Элементы.Контрагент.КнопкаОткрытия = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Сбор статистики
	СборСтатистикиМПКлиентСерверПереопределяемый.ОтправитьОткрытиеЭкранаВGA(ЭтаФорма.ИмяФормы);
	// Конец Сбор статистики

	Если ЗначениеЗаполнено(Объект.Основание) Тогда
		ТолькоПросмотр = Истина;
		Элементы.ГруппаОтображенияОснования.Видимость = Истина;
	Иначе
		Элементы.ГруппаОтображенияОснования.Видимость = Ложь;
	КонецЕсли;
	
	#Если НЕ МобильноеПриложениеСервер Тогда
		Элементы.НомерНеПодтвержденЦБ.Видимость = Ложь;
	#КонецЕсли
	
	Элементы.Контрагент.ОтметкаНезаполненного = ОбщегоНазначенияМПВызовСервера.СтатьяЭтоРасчетыСКонтрагентами(Объект.Статья);
	ОбщегоНазначенияМПСервер.УстановитьЗаголовокФормы(ЭтаФорма, НСтр("ru='Расход денег';en='Payment Expense'"));
	
	УстановитьОтображениеНомера();
	
	// Меняем оформление под веб клиент
	#Если НЕ МобильноеПриложениеСервер Тогда
		Элементы.Номер.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
		Элементы.Номер.Ширина = 20;
		Элементы.Статья.Ширина = 38;
		Элементы.ГруппаОтображенияОснования.РастягиватьПоГоризонтали = Ложь;
	#КонецЕсли

	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьКнопкиОткрытияКонтрагента();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбщегоНазначенияМПСервер.УстановитьЗаголовокФормы(ЭтаФорма, НСтр("ru='Расход денег';en='Payment Expense'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзменилосьКоличествоДенег", Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СозданНовыйКонтрагент" Тогда
		Объект.Контрагент = Параметр;
		ЭтаФорма.Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	#Если НЕ МобильноеПриложениеСервер Тогда
		Если ВРег(Метаданные.Имя) = ВРег("УправлениеНебольшойФирмойНаМобильном") Тогда
			Возврат;
		КонецЕсли;
		// АПК:488-выкл методы безопасного запуска обеспечиваются этой функцией
		МодульСинхронизацияПушУведомленияМПУНФ = Вычислить("СинхронизацияПушУведомленияМПУНФ");
		// АПК:488-вкл
		Если ТипЗнч(МодульСинхронизацияПушУведомленияМПУНФ) = Тип("ОбщийМодуль") Тогда
			МодульСинхронизацияПушУведомленияМПУНФ.ОтправитьПушУведомление("001");
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	#Если МобильноеПриложениеКлиент Тогда
		// АПК:488-выкл методы безопасного запуска обеспечиваются этой функцией
		МодульМенеджерКонтактовКлиент = Вычислить("МенеджерКонтактовКлиент");
		// АПК:488-вкл
		Если ТипЗнч(МодульМенеджерКонтактовКлиент) = Тип("ОбщийМодуль") Тогда
			МодульМенеджерКонтактовКлиент.ЗаполнитьСписокВыбораИзКонтактнойКниги(
			Текст,
			ДанныеВыбора,
			СтандартнаяОбработка,
			КэшНайденныхКонтактов,
			Истина
			);
		КонецЕсли;
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	#Если МобильноеПриложениеКлиент Тогда
		// АПК:488-выкл методы безопасного запуска обеспечиваются этой функцией
		МодульМенеджерКонтактовКлиент = Вычислить("МенеджерКонтактовКлиент");
		// АПК:488-вкл
		Если ТипЗнч(МодульМенеджерКонтактовКлиент) = Тип("ОбщийМодуль") Тогда
			МодульМенеджерКонтактовКлиент.ОбработкаВыбора(
			Элемент,
			ВыбранноеЗначение,
			СтандартнаяОбработка,
			ЭтаФорма,
			КэшНайденныхКонтактов
			);
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеНомера()
	
	Если Константы.СинхронизацияВключенаМП.Получить() И Объект.НомерПодтвержден = Ложь Тогда
		ЭтаФорма.Элементы.НомерНеПодтвержденЦБ.Видимость = Истина;
		ЭтаФорма.Элементы.Номер.Видимость = Ложь;
	Иначе
		ЭтаФорма.Элементы.НомерНеПодтвержденЦБ.Видимость = Ложь;
		ЭтаФорма.Элементы.Номер.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДействияКомандныхПанелейФормы

&НаКлиенте
Процедура СтатьяПриИзменении(Элемент)
	
	Элементы.Контрагент.ОтметкаНезаполненного = ОбщегоНазначенияМПВызовСервера.СтатьяЭтоРасчетыСКонтрагентами(Объект.Статья);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	УстановитьДоступностьКнопкиОткрытияКонтрагента();
	
КонецПроцедуры

&НаКлиенте
Процедура Справка(Команда)
	
	// Сбор статистики
	СборСтатистикиМПКлиентСерверПереопределяемый.ОтправитьДействиеВGA(ЭтаФорма.ИмяФормы + ".Команда." + Команда.Имя);
	// Конец Сбор статистики
	
	// АПК:534-выкл методы безопасного запуска обеспечиваются этой функцией
	ПерейтиПоНавигационнойСсылке("https://sbm.1c.ru/about/razdel-dengi/kartochka-dokumenta-oplaty/");
	// АПК:534-вкл
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	// Сбор статистики
	СборСтатистикиМПКлиентПереопределяемый.ОтправитьДействиеВGA(ЭтаФорма.ИмяФормы + ".Закрытие",,,ЗавершениеРаботы);
	// Конец Сбор статистики
	
КонецПроцедуры

#КонецОбласти