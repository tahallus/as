#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Смета";
	КомандаПечати.Представление = НСтр("ru = 'Смета'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 10;
	
КонецПроцедуры


Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	Перем Ошибки;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Смета");
	Если ПечатнаяФорма <> Неопределено Тогда
		
		ПечатнаяФорма.ТабличныйДокумент = Новый ТабличныйДокумент;
		ПечатнаяФорма.ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Смета_Смета";
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ._Смета.ПФ_MXL_Смета";
		ПечатнаяФорма.СинонимМакета = НСтр("ru ='Смета'");
		
		СформироватьСмету(ПечатнаяФорма, МассивОбъектов, ОбъектыПечати, Ошибки);
		
	КонецЕсли;
	
КонецПроцедуры


Процедура СформироватьСмету(ПечатнаяФорма, МассивОбъектов, ОбъектыПечати, Ошибки)

	ТабличныйДокумент = ПечатнаяФорма.ТабличныйДокумент;
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПечатнаяФорма.ПолныйПутьКМакету);
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьВариант = Макет.ПолучитьОбласть("Вариант");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги = Макет.ПолучитьОбласть("Итоги");
	
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	_Смета.Ссылка КАК Документ,
		|	_СметаВарианты.Имя КАК НаименованиеВарианта,
		|	_СметаВарианты.Описание КАК ОписаниеВарианта,
		|	_СметаЗапасы.НомерСтроки КАК НомерСтроки,
		|	_СметаЗапасы.СтатьяЗатрат.Представление КАК СтатьяЗатрат,
		|	_СметаЗапасы.Стоимость КАК Сумма,
		|	_СметаЗапасы.Описание КАК Описание,
		|	_СметаВарианты.НомерВариантаКП КАК НомерВариантаКП,
		|	ВЫБОР
		|		КОГДА _СметаВарианты.НомерВариантаКП = _Смета.ОсновнойВариантКП
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК СортировкаОсновнойВариант,
		|	ЕСТЬNULL(_Смета.ДокументОснование.Контрагент, ""<--->"") КАК Партнер
		|ИЗ
		|	Документ._Смета КАК _Смета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ._Смета.Варианты КАК _СметаВарианты
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ._Смета.Запасы КАК _СметаЗапасы
		|			ПО _СметаВарианты.Ссылка = _СметаЗапасы.Ссылка
		|				И _СметаВарианты.НомерВариантаКП = _СметаЗапасы.НомерВариантаКП
		|		ПО _Смета.Ссылка = _СметаВарианты.Ссылка
		|ГДЕ
		|	_Смета.Ссылка В(&МассивОбъектов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Документ,
		|	СортировкаОсновнойВариант,
		|	НомерВариантаКП,
		|	НомерСтроки
		|ИТОГИ
		|	СУММА(Сумма)
		|ПО
		|	Документ,
		|	НомерВариантаКП";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДокумент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	
	Пока ВыборкаДокумент.Следующий() Цикл
	
    	ВыводитьЗаголовок = Истина;
		
		ВыборкаНомерВариантаКП = ВыборкаДокумент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
		Пока ВыборкаНомерВариантаКП.Следующий() Цикл
			
			ВыводитьВариант = Истина;
			
			ВыборкаДетальныеЗаписи = ВыборкаНомерВариантаКП.Выбрать();
	
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

				Если ВыводитьЗаголовок Тогда 
					ОбластьЗаголовок.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
					ТабличныйДокумент.Вывести(ОбластьЗаголовок);
					ВыводитьЗаголовок = Ложь;
				КонецЕсли;
				
				Если ВыводитьВариант Тогда 
					ОбластьВариант.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
					ТабличныйДокумент.Вывести(ОбластьВариант);
					ВыводитьВариант = Ложь;
				КонецЕсли;
				
				ОбластьСтрока.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
				ТабличныйДокумент.Вывести(ОбластьСтрока);

			КонецЦикла;
			
			ОбластьИтоги.Параметры.Заполнить(ВыборкаНомерВариантаКП);
			ТабличныйДокумент.Вывести(ОбластьИтоги);
			
		КонецЦикла;
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц()
	КонецЦикла;
	
КонецПроцедуры 



#КонецОбласти

#КонецЕсли