
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоСменаОдобренныхНаОтклоненные = Параметры.ЭтоСменаОдобренныхНаОтклоненные;
	ЭтоНедоставленныеВБанк = Параметры.ЭтоНедоставленныеВБанк;
	
	Заявки = Документы.ЗаявкиНаЛьготныйКредит.ЗаявкиСНеправильнымиСтатусамиИзКэша(ЭтоСменаОдобренныхНаОтклоненные);

	ИзменитьОформлениеФормы(Заявки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЛьготныеКредитыКлиент.ПолучитьБанки(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура БанкиСсылкаНажатие(Элемент)
	
	ЛьготныеКредитыКлиент.БанкиСсылкаНажатие(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура БольшеНеНапоминать(Команда)
	БольшеНеНапоминатьНаСервере();
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура БольшеНеНапоминатьНаСервере()
	
	КлючОбъекта = Документы.ЗаявкиНаЛьготныйКредит.КлючОбъектаЗаявкиСНеправильнымСтатусом_БольшеНеПоказывать(
		ЭтоСменаОдобренныхНаОтклоненные);
		
	ХранилищеОбщихНастроек.Сохранить(
		КлючОбъекта,
		,
		Истина);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьОформлениеФормы(Заявки)
	
	Количество = Заявки.Количество();
	
	// Предложение 1
	Подстрока1 = НСтр("ru = 'Ранее вы отправили '");
	Если Количество = 1 Тогда
		Подстрока2 = Новый ФорматированнаяСтрока(НСтр("ru = 'заявку'"),,,,"e1cib/command/ОбщаяКоманда.ЛьготныеКредиты");
	Иначе
		Подстрока2 = Новый ФорматированнаяСтрока(НСтр("ru = 'заявки'"),,,,"e1cib/command/ОбщаяКоманда.ЛьготныеКредиты");
	КонецЕсли;
	
	Подстрока3 = НСтр("ru = ' на льготный кредит.'");
	
	Предложение1 = Новый ФорматированнаяСтрока(
		Подстрока1,
		Подстрока2,
		Подстрока3);

	// Предложение 2
	Подстрока1 = НСтр("ru = 'Обратите внимание, в заявке был ошибочно указан статус рассмотрения.'");
	Подстрока2 = НСтр("ru = 'Правильный статус - '");
	
	Если ЭтоСменаОдобренныхНаОтклоненные Тогда
		Подстрока3 = Новый ФорматированнаяСтрока(НСтр("ru = 'Отклонено банком'"), Новый Шрифт(,,Истина));
	Иначе
		Подстрока3 = Новый ФорматированнаяСтрока(НСтр("ru = 'Отправлено в ФНС'"), Новый Шрифт(,,Истина));
	КонецЕсли;
	
	Предложение2 = Новый ФорматированнаяСтрока(
		Подстрока1,
		Символы.ПС,
		Подстрока2,
		Подстрока3);
		
	Элементы.Текст.Заголовок = Новый ФорматированнаяСтрока(
	Предложение1,
	Символы.ПС,
	Символы.ПС,
	Предложение2);
	
	ИзменитьОформлениеБанков();
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьОформлениеБанков()
	
	Элементы.ГруппаБанки.Видимость = ЭтоСменаОдобренныхНаОтклоненные;
	Элементы.ГруппаВТБ.Видимость = ЭтоНедоставленныеВБанк;
		
КонецПроцедуры

#КонецОбласти






