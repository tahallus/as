#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = НСтр("en='Estimate #';ru='Заказ №'") + ОбщегоНазначенияМПКлиентСервер.ПолучитьПредставлениеНомера(Данные.Номер) + НСтр("ru=' от ';en=' of '") + Формат(Данные.Дата, "ДФ=dd.MM.yyyy");
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ЕстьДокументы() Экспорт
	
	ВыборкаДокументов = Документы.ЗаказМП.Выбрать();
	
	Пока ВыборкаДокументов.Следующий() Цикл
		Если НЕ ВыборкаДокументов.ПометкаУдаления Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция ПолучитьКоличествоЗаказовВСостоянии(СостояниеЗаказа) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ЗаказМП.Ссылка) КАК КоличествоЗаказов
	|ИЗ
	|	Документ.ЗаказМП КАК ЗаказМП
	|ГДЕ
	|	ЗаказМП.СостояниеЗаказа = &СостояниеЗаказа
	|	И НЕ ЗаказМП.ПометкаУдаления
	|	И ЗаказМП.Проведен";
	
	Запрос.УстановитьПараметр("СостояниеЗаказа", СостояниеЗаказа);
	
	ВыборкаЗаказов = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ВыборкаЗаказов.Следующий() Тогда
		Возврат ВыборкаЗаказов.КоличествоЗаказов;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСуммуЗаказовВСостоянии(СостояниеЗаказа) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ЗаказМП.СуммаДокумента), 0) КАК СуммаЗаказов
	|ИЗ
	|	Документ.ЗаказМП КАК ЗаказМП
	|ГДЕ
	|	ЗаказМП.СостояниеЗаказа = &СостояниеЗаказа
	|	И НЕ ЗаказМП.ПометкаУдаления
	|	И ЗаказМП.Проведен";
	
	Запрос.УстановитьПараметр("СостояниеЗаказа", СостояниеЗаказа);
	
	ВыборкаЗаказов = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ВыборкаЗаказов.Следующий() Тогда
		Возврат ВыборкаЗаказов.СуммаЗаказов;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Функция СформироватьПечатнуюФорму(Ссылка) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказМП.Ссылка,
	|	ЗаказМП.ВерсияДанных,
	|	ЗаказМП.ПометкаУдаления,
	|	ЗаказМП.Номер,
	|	ЗаказМП.Дата,
	|	ЗаказМП.Проведен,
	|	ЗаказМП.Покупатель,
	|	ЗаказМП.СостояниеЗаказа,
	|	ЗаказМП.Отгружен,
	|	ЗаказМП.Оплачен,
	|	ЗаказМП.СуммаДокумента,
	|	ЗаказМП.ИзЦентральнойБазы,
	|	ЗаказМП.ОтгруженУстановленВЦентральнойБазе,
	|	ЗаказМП.ОплаченУстановленВЦентральнойБазе,
	|	ЗаказМП.Комментарий,
	|	ЗаказМП.СуммаОплаты,
	|	ЗаказМП.СуммаСкидки,
	|	ЗаказМП.Товары.(
	|		Ссылка,
	|		НомерСтроки,
	|		Товар,
	|		Цена,
	|		Количество,
	|		Сумма
	|	)
	|ИЗ
	|	Документ.ЗаказМП КАК ЗаказМП
	|ГДЕ
	|	ЗаказМП.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Заказ = Запрос.Выполнить().Выбрать();
	Заказ.Следующий();
	
	ТабличныйДокумент = Новый ТабличныйДокумент();
	Макет = Документы.ЗаказМП.ПолучитьМакет("СчетНаОплату");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.ТекстЗаголовка =
	НСтр("ru='Счет на оплату №';en='ESTIMATE #'")
	+ Заказ.Номер
	+ НСтр("ru=' от ';en=' DATE '")
	+ Формат(Заказ.Дата, "ДЛФ=DD");
	
	ОбластьМакета.Параметры.ПредставлениеПоставщика = Константы.НазваниеОрганизацииМП.Получить();
	ОбластьМакета.Параметры.РеквизитыДляОплаты = Константы.ПлатежныеРеквизитыМП.Получить();
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ВыборкаСтрокТовары = Заказ.Товары.Выбрать();
	
	Количество = 0;
	
	Пока ВыборкаСтрокТовары.Следующий() Цикл
		Количество = Количество + 1;
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		ОбластьМакета.Параметры.НомерСтроки = Количество;
		ОбластьМакета.Параметры.Товар = ВыборкаСтрокТовары.Товар.Наименование;
		ОбластьМакета.Параметры.Количество = ВыборкаСтрокТовары.Количество;
		ОбластьМакета.Параметры.Цена = ВыборкаСтрокТовары.Цена;
		ОбластьМакета.Параметры.Сумма = ВыборкаСтрокТовары.Сумма;
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ПодвалТаблицы");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	Если Заказ.СуммаСкидки <> 0 Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ИтогоСкидка");
		ОбластьМакета.Параметры.ВсегоБезСкидки = Заказ.СуммаДокумента + Заказ.СуммаСкидки;
		ОбластьМакета.Параметры.СуммаСкидки = Заказ.СуммаСкидки;
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ИтогоКОплате");
	ОбластьМакета.Параметры.ИтогоКОплате = Заказ.СуммаДокумента;
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	Если НСтр("ru='ru';en='en'") = "ru" Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ПодвалСчета");
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#КонецЕсли
