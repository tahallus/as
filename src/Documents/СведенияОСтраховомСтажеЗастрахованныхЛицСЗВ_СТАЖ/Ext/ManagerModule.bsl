#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьВТПерсональныеДанныеСотрудников(МенеджерВременныхТаблиц, Организация, Год, ФизическиеЛица = Неопределено) Экспорт 
	
	ОтчетныйПериод = Дата(Год, 1, 1);
	
	ПараметрыПолученияСотрудников = УчетСтраховыхВзносов.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	
	ПараметрыПолученияСотрудников.Организация = Организация;
	ПараметрыПолученияСотрудников.НачалоПериода = ОтчетныйПериод; 
	ПараметрыПолученияСотрудников.ОкончаниеПериода = КонецГода(ОтчетныйПериод); 
	ПараметрыПолученияСотрудников.КадровыеДанные = "ФизическоеЛицо, Фамилия, Имя, Отчество, СтраховойНомерПФР, ДатаУвольнения, ЯвляетсяПрокурором, ЯвляетсяВоеннослужащим, РаботаетВСтуденческомОтряде";
	
	Если ПараметрыПолученияСотрудников.Свойство("РаботникиПоДоговорамГПХ") Тогда
		ПараметрыПолученияСотрудников.РаботникиПоДоговорамГПХ = Истина;
	КонецЕсли;	
	
	Если ФизическиеЛица <> Неопределено Тогда
		ПараметрыПолученияСотрудников.СписокФизическихЛиц = ФизическиеЛица;
	КонецЕсли;	
	
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	УчетСтраховыхВзносов.СформироватьТаблицуВТФизическиеЛицаРаботавшиеВОрганизации(
		МенеджерВременныхТаблиц,
		Организация,
		ПараметрыПолученияСотрудников.НачалоПериода,
		ПараметрыПолученияСотрудников.ОкончаниеПериода);
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СотрудникиОрганизации.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТекущиеКадровыеДанныеСотрудников.ДатаПриема КАК ДатаПриема,
	|	ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения КАК ДатаУвольнения,
	|	ЛОЖЬ КАК ЯвляетсяПрокурором,
	|	ЛОЖЬ КАК ЯвляетсяВоеннослужащим,
	|	ЛОЖЬ КАК РаботаетВСтуденческомОтряде,
	|	ТекущиеКадровыеДанныеСотрудников.Сотрудник.Физлицо.СтраховойНомерПФР КАК СтраховойНомерПФР,
	|	ФИОФизЛицСрезПоследних.Фамилия КАК Фамилия,
	|	ФИОФизЛицСрезПоследних.Имя КАК Имя,
	|	ФИОФизЛицСрезПоследних.Отчество КАК Отчество
	|ПОМЕСТИТЬ ВТСотрудникиОрганизации
	|ИЗ
	|	ВТФизическиеЛицаРаботавшиеВОрганизации КАК СотрудникиОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|		ПО (ТекущиеКадровыеДанныеСотрудников.Сотрудник = СотрудникиОрганизации.ФизическоеЛицо)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних КАК ФИОФизЛицСрезПоследних
	|		ПО (ФИОФизЛицСрезПоследних.ФизЛицо = ТекущиеКадровыеДанныеСотрудников.Сотрудник.Физлицо)";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Если ФизическиеЛица <> Неопределено Тогда
		Запрос.УстановитьПараметр("СписокФизическихЛиц", ФизическиеЛица);
		Запрос.Текст = Запрос.Текст + "
		|ГДЕ ТекущиеКадровыеДанныеСотрудников.Сотрудник В (&СписокФизическихЛиц)";
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Запрос.УстановитьПараметр("КонецГода", КонецГода(ОтчетныйПериод));
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", Организация);
			
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СотрудникиОрганизации.ФизическоеЛицо КАК ФизическоеЛицо,
	|	МАКСИМУМ(СотрудникиОрганизации.ДатаУвольнения) КАК ДатаУвольнения
	|ПОМЕСТИТЬ ВТУволенныеСотрудники
	|ИЗ
	|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК РаботающиеСотрудники
	|		ПО СотрудникиОрганизации.ФизическоеЛицо = РаботающиеСотрудники.ФизическоеЛицо
	|			И (РаботающиеСотрудники.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1))
	|ГДЕ
	|	РаботающиеСотрудники.ФизическоеЛицо ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизации.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизации.ФизическоеЛицо КАК Сотрудник,
	|	МАКСИМУМ(СотрудникиОрганизации.Фамилия) КАК Фамилия,
	|	МАКСИМУМ(СотрудникиОрганизации.Имя) КАК Имя,
	|	МАКСИМУМ(СотрудникиОрганизации.Отчество) КАК Отчество,
	|	МАКСИМУМ(СотрудникиОрганизации.СтраховойНомерПФР) КАК СтраховойНомерПФР,
	|	МАКСИМУМ(ЕСТЬNULL(УволенныеСотрудники.ДатаУвольнения, ДАТАВРЕМЯ(1, 1, 1)) <> ДАТАВРЕМЯ(1, 1, 1)
	|			И КОНЕЦПЕРИОДА(УволенныеСотрудники.ДатаУвольнения, ДЕНЬ) = &КонецГода) КАК СотрудникУволен,
	|	МИНИМУМ(СотрудникиОрганизации.ЯвляетсяПрокурором) КАК ЯвляетсяПрокурором,
	|	МИНИМУМ(СотрудникиОрганизации.ЯвляетсяВоеннослужащим) КАК ЯвляетсяВоеннослужащим,
	|	МИНИМУМ(СотрудникиОрганизации.РаботаетВСтуденческомОтряде) КАК РаботаетВСтуденческомОтряде
	|ПОМЕСТИТЬ ВТПерсональныеДанныеСотрудников
	|ИЗ
	|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУволенныеСотрудники КАК УволенныеСотрудники
	|		ПО СотрудникиОрганизации.ФизическоеЛицо = УволенныеСотрудники.ФизическоеЛицо
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизации.ФизическоеЛицо";
				   
	Если ФизическиеЛица = Неопределено Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТПерсональныеДанныеСотрудников", "ВТПерсональныеДанныеСотрудниковПредварительно");			   
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Если ФизическиеЛица <> Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПерсонифицированныйУчет.СоздатьВТПериоды(МенеджерВременныхТаблиц, ОтчетныйПериод, КонецГода(ОтчетныйПериод), "МЕСЯЦ"); 
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	&ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	               |	&ГоловнаяОрганизация КАК Организация,
	               |	ПерсональныеДанныеСотрудников.Сотрудник КАК ФизическоеЛицо,
	               |	Периоды.Период
	               |ПОМЕСТИТЬ ВТПериодыФизическихЛиц
	               |ИЗ
	               |	ВТПерсональныеДанныеСотрудниковПредварительно КАК ПерсональныеДанныеСотрудников
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериоды КАК Периоды
	               |		ПО (ИСТИНА)";
	
	Запрос.Выполнить();
	
	УчетСтраховыхВзносов.СформироватьВТДанныеОПравахНаСтрахование(МенеджерВременныхТаблиц, "ВТПериодыФизическихЛиц", Истина);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ДанныеОПравахНаСтрахование.ФизическоеЛицо
	               |ПОМЕСТИТЬ ВТЗастрахованныеФизическиеЛица
	               |ИЗ
	               |	ВТДанныеОПравахНаСтрахование КАК ДанныеОПравахНаСтрахование
	               |ГДЕ
	               |	ДанныеОПравахНаСтрахование.ЗастрахованПФР
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПерсональныеДанныеСотрудников.Сотрудник КАК Сотрудник,
	               |	ПерсональныеДанныеСотрудников.Фамилия КАК Фамилия,
	               |	ПерсональныеДанныеСотрудников.Имя КАК Имя,
	               |	ПерсональныеДанныеСотрудников.Отчество КАК Отчество,
	               |	ПерсональныеДанныеСотрудников.СтраховойНомерПФР КАК СтраховойНомерПФР,
	               |	ПерсональныеДанныеСотрудников.СотрудникУволен КАК СотрудникУволен
	               |ПОМЕСТИТЬ ВТПерсональныеДанныеСотрудников
	               |ИЗ
	               |	ВТПерсональныеДанныеСотрудниковПредварительно КАК ПерсональныеДанныеСотрудников
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗастрахованныеФизическиеЛица КАК ЗастрахованныеФизическиеЛица
	               |		ПО ПерсональныеДанныеСотрудников.Сотрудник = ЗастрахованныеФизическиеЛица.ФизическоеЛицо
	               |			И (НЕ ПерсональныеДанныеСотрудников.ЯвляетсяПрокурором)
	               |			И (НЕ ПерсональныеДанныеСотрудников.ЯвляетсяВоеннослужащим)
	               |			И (НЕ ПерсональныеДанныеСотрудников.РаботаетВСтуденческомОтряде)";
				   
	Запрос.Выполнить();			   
	
КонецПроцедуры

Процедура СоздатьВТДанныеСтажаПФР(МенеджерВременныхТаблиц, Организация, Год, Сотрудники, УчитываемыеДокументы) Экспорт 
	
	ОтчетныйПериод = ?(ЗначениеЗаполнено(Год), Дата(Год, 01, 01), Дата(1, 1, 1));
	СписокСотрудников = ?(ТипЗнч(Сотрудники) = Тип("Массив"), Сотрудники, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники));
	
	УчетСтраховыхВзносов.СформироватьВТРасширенныеСведенияОДоходахИВзносах(ОтчетныйПериод, КонецГода(ОтчетныйПериод), Организация, МенеджерВременныхТаблиц);
	
	УчетСтраховыхВзносов.СформироватьВТСДаннымиОВзносахИСтажеСотрудников2014(МенеджерВременныхТаблиц, Организация,(ОтчетныйПериод),КонецГода(ОтчетныйПериод),,,, СписокСотрудников);
	
КонецПроцедуры

Процедура ЗаполнитьДанныеОСтажеСотрудников(докСЗВСтаж, Сотрудники) Экспорт
	
	СписокСотрудников = ?(ТипЗнч(Сотрудники) = Тип("Массив"), Сотрудники, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники));
	Год = ?(ЗначениеЗаполнено(докСЗВСтаж.Год), докСЗВСтаж.Год, Год(ТекущаяДатаСеанса()));
	
	ДатыВыходаНаПенсию = Неопределено;
	Если докСЗВСтаж.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии Тогда 
		ДатыВыходаНаПенсию = Новый Соответствие;
		Для Каждого СтрокаСотрудника Из докСЗВСтаж.Сотрудники Цикл 
			Если СписокСотрудников.Найти(СтрокаСотрудника.Сотрудник) <> Неопределено Тогда 
				ДатыВыходаНаПенсию.Вставить(СтрокаСотрудника.Сотрудник, СтрокаСотрудника.ДатаВыходаНаПенсию);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Документы.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.СоздатьВТДанныеСтажаПФР(
		МенеджерВременныхТаблиц, докСЗВСтаж.Организация, Год, СписокСотрудников, докСЗВСтаж.Ссылка);
		
	ДоговорыГПХ = Новый Массив;
	ДоговорыГПХ.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДОГОВОР);
	ДоговорыГПХ.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.НЕОПЛДОГ);
	ДоговорыГПХ.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.НЕОПЛАВТ);
		
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ДоговорыГПХ", ДоговорыГПХ);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
   |	ДанныеОСтажеПФР.ФизическоеЛицо КАК Сотрудник,
   |	ВЫБОР
   |		КОГДА ДанныеОСтажеПФР.ТретийПараметрИсчисляемогоСтажа В (&ДоговорыГПХ)
   |			ТОГДА ИСТИНА
   |		ИНАЧЕ ЛОЖЬ
   |	КОНЕЦ КАК ДоговорГПХ,
   |	ДанныеОСтажеПФР.*
   |ИЗ
   |	ВТДанныеСтажа КАК ДанныеОСтажеПФР
   |
   |УПОРЯДОЧИТЬ ПО
   |	Сотрудник,
   |	ДоговорГПХ УБЫВ,
   |	ДанныеОСтажеПФР.ДатаНачалаПериода";
	
	Выборка = Запрос.Выполнить().Выбрать();
	ПроверяемыеРеквизиты = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
		"Сотрудник,КодПозицииСписка,ОснованиеИсчисляемогоСтажа,ПервыйПараметрИсчисляемогоСтажа,ВторойПараметрИсчисляемогоСтажа,
		|ОснованиеВыслугиЛет,ПервыйПараметрВыслугиЛет,ВторойПараметрВыслугиЛет,ТретийПараметрВыслугиЛет,ОсобыеУсловияТруда,
		|ТерриториальныеУсловия,ПараметрТерриториальныхУсловий,ЗамещениеГосударственныхМуниципальныхДолжностей", , Истина, Истина);
	ТаблицаОтклонений = докСЗВСтаж.ЗаписиОСтаже.Выгрузить(Новый Массив);
	Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл 
		ПредыдущаяСтрока = Неопределено;
		ТаблицаОтклонений.Очистить();
		ИндексПервойСтрокиСотрудника = докСЗВСтаж.ЗаписиОСтаже.Количество();
		Пока Выборка.Следующий() Цикл
			Если ДатыВыходаНаПенсию <> Неопределено И ДатыВыходаНаПенсию[Выборка.Сотрудник] < Выборка.ДатаНачалаПериода Тогда 
				Продолжить;
			КонецЕсли; 
			Если ПредыдущаяСтрока <> Неопределено И ДанныеСтрокСовпадают(ПредыдущаяСтрока, Выборка, ПроверяемыеРеквизиты) Тогда 
				ПредыдущаяСтрока.ДатаОкончанияПериода = Выборка.ДатаОкончанияПериода;
				Если ДатыВыходаНаПенсию <> Неопределено Тогда 
					ПредыдущаяСтрока.ДатаОкончанияПериода = Мин(Выборка.ДатаОкончанияПериода, ДатыВыходаНаПенсию[Выборка.Сотрудник]);
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			ВывестиСтрокиОтклонений(докСЗВСтаж, ТаблицаОтклонений, ПредыдущаяСтрока);
			НоваяСтрока = докСЗВСтаж.ЗаписиОСтаже.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			Если ДатыВыходаНаПенсию <> Неопределено Тогда 
				НоваяСтрока.ДатаОкончанияПериода = Мин(Выборка.ДатаОкончанияПериода, ДатыВыходаНаПенсию[Выборка.Сотрудник]);
			КонецЕсли;

		КонецЦикла;
		ВывестиСтрокиОтклонений(докСЗВСтаж, ТаблицаОтклонений, ПредыдущаяСтрока);
		ИндексПоследнейСтрокиСотрудника = докСЗВСтаж.ЗаписиОСтаже.Количество() - 1;
		СтрокиСтажаПоСотруднику = Новый Массив;
		Для Индекс = ИндексПервойСтрокиСотрудника По ИндексПоследнейСтрокиСотрудника Цикл 
		    СтрокиСтажаПоСотруднику.Добавить(докСЗВСтаж.ЗаписиОСтаже[Индекс]);
		КонецЦикла;
		ПерсонифицированныйУчетКлиентСервер.ВыполнитьНумерациюЗаписейОСтаже(СтрокиСтажаПоСотруднику);
	КонецЦикла;
	
КонецПроцедуры

Функция ДанныеСтрокСовпадают(ПерваяСтрока, ВтораяСтрока, ПроверяемыеРеквизиты)
	
	Для Каждого ИмяРеквизита Из ПроверяемыеРеквизиты Цикл 
		Если Не ЗначениеЗаполнено(ПерваяСтрока[ИмяРеквизита]) И Не ЗначениеЗаполнено(ВтораяСтрока[ИмяРеквизита]) Тогда 
			Продолжить;
		КонецЕсли;
		Если ПерваяСтрока[ИмяРеквизита] <> ВтораяСтрока[ИмяРеквизита] Тогда 
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Процедура ВывестиСтрокиОтклонений(докСЗВСтаж ,ТаблицаОтклонений, ПредыдущаяСтрока)
	
	Для Каждого СтрокаОтклонения Из ТаблицаОтклонений Цикл 
		НоваяСтрока = докСЗВСтаж.ЗаписиОСтаже.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОтклонения);
	КонецЦикла;
	
	ТаблицаОтклонений.Очистить();
	ПредыдущаяСтрока = Неопределено;
	
КонецПроцедуры


Функция ИмяФайла(Организация, Дата, УИД = Неопределено) Экспорт

	Если УИД = Неопределено Тогда
		УИД = Новый УникальныйИдентификатор;
	КонецЕсли;	
	
	Параметры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "РегистрационныйНомерПФР, КодОрганаПФР");
	
	Возврат "ПФР"
			+ "_" + ?(ЗначениеЗаполнено(Параметры.РегистрационныйНомерПФР), Параметры.РегистрационныйНомерПФР, "")
			+ "_" + ?(ЗначениеЗаполнено(Параметры.КодОрганаПФР),СтрЗаменить(Параметры.КодОрганаПФР, "-", ""),"")
			+ "_СЗВ-СТАЖ"
			+ "_" + Формат(Дата, "ДФ=ггггММдд")
			+ "_" + УИД;
			
КонецФункции	

Процедура ОбработкаФормированияФайла(Объект) Экспорт
	
	ТекстФайла = ?(Объект.Формат2019, СформироватьВыходнойФайл2019(Объект.Ссылка), СформироватьВыходнойФайл(Объект.Ссылка));
	СправкиПоНДФЛ.ЗаписатьФайлВАрхив(Объект.Ссылка, Объект.ИмяФайлаДляПФР, ТекстФайла, , "UTF-8", Ложь);
	
КонецПроцедуры

Функция СформироватьВыходнойФайл(Ссылка)
	
	СписокДокументов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка);
	ВыборкаПоШапкеДокумента = ПерсонифицированныйУчет.ЗапросПоШапкеДляПечатиОДВ_1(СписокДокументов, "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ").Выбрать();
	ВыборкаПоШапкеДокумента.Следующий();
	
	ВыборкаПоДаннымДНП = ПерсонифицированныйУчет.ЗапросПоДаннымДосрочногоНазначенияПенсииОДВ_1(СписокДокументов, "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ").Выбрать();
	
	// Загружаем формат файла сведений
	ДеревоФорматаXML = ПолучитьОбщийМакет("ФорматПФР_2018XML");
	ТекстФорматаXML = ДеревоФорматаXML.ПолучитьТекст();
	
	ДеревоФормата = СправкиПоНДФЛ.ЗагрузитьXMLВДокументDOM(ТекстФорматаXML);
	
	ФорматОДВ_1 = СправкиПоНДФЛ.ЗагрузитьФорматНабораЗаписей(ДеревоФормата, "ОДВ_1");
	
	НаборЗаписейДатаЗаполнения = Новый Структура("ДатаЗаполнения", ОбщегоНазначения.СкопироватьРекурсивно(ФорматОДВ_1.ДатаЗаполнения));
	ФорматОДВ_1.Удалить("ДатаЗаполнения");
	НаборЗаписейРуководитель = Новый Структура("Руководитель", ОбщегоНазначения.СкопироватьРекурсивно(ФорматОДВ_1.Руководитель));
	ФорматОДВ_1.Удалить("Руководитель");
	ФорматОснованияДНП = ОбщегоНазначения.СкопироватьРекурсивно(ФорматОДВ_1.ОснованияДНП.Значение);
	ФорматОДВ_1.Удалить("ОснованияДНП");
	
	ФорматОДВ_1.Удалить("Страховая");
	ФорматОДВ_1.Удалить("Накопительная");
	ФорматОДВ_1.Удалить("ТарифСВ");
	ФорматОДВ_1.Удалить("Уплата");
	
	// Создаем начальное дерево
	ДеревоВыгрузки = СправкиПоНДФЛ.СоздатьДеревоXML();
	
	Атрибуты = Новый Соответствие;
	Атрибуты.Вставить("xmlns", "http://пф.рф/ВС/СЗВ-СТАЖ/2018-01-29");
	Атрибуты.Вставить("xmlns:ВС2", "http://пф.рф/ВС/типы/2017-10-23");
	Атрибуты.Вставить("xmlns:АФ4", "http://пф.рф/АФ/2017-08-21");
	Атрибуты.Вставить("xmlns:УТ2", "http://пф.рф/УТ/2017-08-21");
	Атрибуты.Вставить("xmlns:ИС2", "http://пф.рф/ВС/ИС/типы/2017-09-11");
	
	УзелПФР = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(ДеревоВыгрузки, "ЭДПФР", "", Атрибуты);
	
	// Служебная информация о файле
	НаборЗаписейСлужебнаяИнформация = СправкиПоНДФЛ.ЗагрузитьФорматНабораЗаписей(ДеревоФормата, "СлужебнаяИнформация");
	Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ИмяФайлаДляПФР) Тогда
		ЧастиИмениФайла = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СокрЛП(ВыборкаПоШапкеДокумента.ИмяФайлаДляПФР),"_");
		НаборЗаписейСлужебнаяИнформация.АФ4__GUID.Значение = ЧастиИмениФайла[ЧастиИмениФайла.ВГраница()];
	КонецЕсли;
	НаборЗаписейСлужебнаяИнформация.АФ4__ДатаВремя.Значение = ПерсонифицированныйУчет.ПредставлениеДатыВФорматеdateTimeСЧасовымПоясом(ВыборкаПоШапкеДокумента.Дата);
	НаборЗаписейСлужебнаяИнформация.АФ4__ПрограммаПодготовки.Значение = ВРег(РегламентированнаяОтчетность.НазваниеИВерсияПрограммы());
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелПФР, "СлужебнаяИнформация",""), НаборЗаписейСлужебнаяИнформация);
	
	// Данные формы и страхователя
	ФорматОДВ_1.Тип.Значение = 0;	
	НаборЗаписейСтрахователь = ФорматОДВ_1.Страхователь.Значение;
	НаименованиеКраткое = СокрЛП(ВРег(ВыборкаПоШапкеДокумента.НаименованиеСокращенное));
	ПерсонифицированныйУчет.УдалитьПробелыРядомСДефисом(НаименованиеКраткое);
	НаборЗаписейСтрахователь.ИС2__Наименование = НаименованиеКраткое;
	НаборЗаписейСтрахователь.УТ2__РегНомер = ВыборкаПоШапкеДокумента.РегистрационныйНомерПФР;
	НаборЗаписейСтрахователь.УТ2__ИНН = ВыборкаПоШапкеДокумента.ИНН;
	Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.КПП) Тогда
		НаборЗаписейСтрахователь.УТ2__КПП = ВыборкаПоШапкеДокумента.КПП;
	Иначе
		НаборЗаписейСтрахователь.Удалить("УТ2__КПП");
		СтрокаПоляКПП = ФорматОДВ_1.Страхователь.Поля.Найти("УТ2__КПП", "ИмяПоля");
		Если СтрокаПоляКПП <> Неопределено Тогда
			ФорматОДВ_1.Страхователь.Поля.Удалить(СтрокаПоляКПП);
		КонецЕсли;		
	КонецЕсли;
	НаборЗаписейОтчетныйПериод = ФорматОДВ_1.ОтчетныйПериод.Значение;
	НаборЗаписейОтчетныйПериод.Код = 0;
	НаборЗаписейОтчетныйПериод.Год = ВыборкаПоШапкеДокумента.Год;
	
	ФорматОДВ_1.КоличествоЗЛ.Значение = ВыборкаПоШапкеДокумента.КоличествоЗастрахованныхЛиц;	
	
	УзелФормы = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелПФР, "ОДВ-1", "");
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелФормы, ФорматОДВ_1);
	
	Если ВыборкаПоДаннымДНП.Количество() > 0 Тогда 
		УзелСпискаОснованияДНП = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелФормы, "ОснованияДНП", "");
		ФорматОснование = ОбщегоНазначения.СкопироватьРекурсивно(ФорматОснованияДНП.Основание.Значение);
		ФорматОснованияДНП.Удалить("Основание");
		ВсегоШтат = 0;
		ВсегоФакт = 0;
		Пока ВыборкаПоДаннымДНП.Следующий() Цикл 
			НаборЗаписейОснование = ОбщегоНазначения.СкопироватьРекурсивно(ФорматОснование);
			НаборЗаписейОснование.ИС2__Подразделение.Значение =ВРег(ВыборкаПоДаннымДНП.НаименованиеПодразделения);
			НаборЗаписейОснование.ИС2__ПрофессияДолжность.Значение = ВРег(ВыборкаПоДаннымДНП.НаименованиеДолжности);
			НаборЗаписейОснование.ИС2__КоличествоШтат.Значение = ВыборкаПоДаннымДНП.КоличествоРабочихМест;
			НаборЗаписейОснование.ИС2__КоличествоФакт.Значение = ВыборкаПоДаннымДНП.КоличествоРаботающих;
			НаборЗаписейОснование.ИС2__Описание.Значение = ВРег(ВыборкаПоДаннымДНП.ХарактерВыполняемыхРабот);
			НаборЗаписейОснование.ИС2__Документы.Значение = ВРег(ВыборкаПоДаннымДНП.ПервичныеДокументы);
			НаборЗаписейОУТ = НаборЗаписейОснование.ОУТ.Значение;
			НаборЗаписейОУТ.Код = СокрЛП(ВыборкаПоДаннымДНП.ОснованиеВыслугиЛетКод);
			НаборЗаписейОУТ.ПозицияСписка = СокрЛП(ВыборкаПоДаннымДНП.КодПозицииСпискаКод);
			СправкиПоНДФЛ.ДобавитьИнформациюВДерево(СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСпискаОснованияДНП, "Основание", ""), НаборЗаписейОснование);
			ВсегоШтат = ВсегоШтат + ВыборкаПоДаннымДНП.КоличествоРабочихМест;
			ВсегоФакт = ВсегоФакт + ВыборкаПоДаннымДНП.КоличествоРаботающих;
		КонецЦикла;
		ФорматОснованияДНП.ИС2__ВсегоШтат.Значение = ВсегоШтат;
		ФорматОснованияДНП.ИС2__ВсегоФакт.Значение = ВсегоФакт;
		СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСпискаОснованияДНП, ФорматОснованияДНП);
	КонецЕсли;
	
	НаборЗаписейРуководитель.Руководитель.Значение.Должность.Значение = ВРег(ВыборкаПоШапкеДокумента.ДолжностьРуководителя);
	НаборЗаписейФИО = НаборЗаписейРуководитель.Руководитель.Значение.ФИО.Значение;
	
	ФИООтдельно = СтрРазделить(ВыборкаПоШапкеДокумента.Руководитель, " ");
	Если ФИООтдельно.Количество() > 0 Тогда
		НаборЗаписейФИО.УТ2__Фамилия = ВРег(ФИООтдельно[0]);
	КонецЕсли;
	Если ФИООтдельно.Количество() > 1 Тогда
		НаборЗаписейФИО.УТ2__Имя = ВРег(ФИООтдельно[1]);
	КонецЕсли;
	Если ФИООтдельно.Количество() > 2 Тогда
		НаборЗаписейФИО.УТ2__Отчество = ВРег(ФИООтдельно[2]);
	КонецЕсли;
	
	Элемент = НаборЗаписейРуководитель.Руководитель.Значение.ФИО;
	СтрокаПоля = Элемент.Поля.Найти("УТ2__Отчество", "ИмяПоля");
	СтрокаПоля.НеВыводитьВФайл = Не ЗначениеЗаполнено(Элемент.Значение["УТ2__Отчество"]);
	
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелФормы, НаборЗаписейРуководитель);
	
	НаборЗаписейДатаЗаполнения.ДатаЗаполнения.Значение = Формат(ВыборкаПоШапкеДокумента.Дата, "ДФ=yyyy-MM-dd");
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелФормы, НаборЗаписейДатаЗаполнения);
	
	ФорматСЗВ_СТАЖ = СправкиПоНДФЛ.ЗагрузитьФорматНабораЗаписей(ДеревоФормата, "СЗВ_СТАЖ");
	
	ФорматЗЛ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматСЗВ_СТАЖ.ЗЛ.Значение);
	ФорматСЗВ_СТАЖ.Удалить("ЗЛ");
	НаборЗаписейСВ = Новый Структура("СВ", ОбщегоНазначения.СкопироватьРекурсивно(ФорматСЗВ_СТАЖ.СВ));
	ФорматСЗВ_СТАЖ.Удалить("СВ");
	
	ВыборкаПоРаботникам = Документы.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ЗапросПоСотрудникамДляПечати(СписокДокументов).Выбрать();
	
	// Данные формы и страхователя
	НаборЗаписейСтрахователь = ФорматСЗВ_СТАЖ.Страхователь.Значение;
	НаборЗаписейСтрахователь.ИС2__Наименование = ВРег(ВыборкаПоШапкеДокумента.НаименованиеСокращенное);
	НаборЗаписейСтрахователь.УТ2__РегНомер = ВыборкаПоШапкеДокумента.РегистрационныйНомерПФР;
	НаборЗаписейСтрахователь.УТ2__ИНН = ВыборкаПоШапкеДокумента.ИНН;
	Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.КПП) Тогда
		НаборЗаписейСтрахователь.УТ2__КПП = ВыборкаПоШапкеДокумента.КПП;
	Иначе
		НаборЗаписейСтрахователь.Удалить("УТ2__КПП");
		СтрокаПоляКПП = ФорматСЗВ_СТАЖ.Страхователь.Поля.Найти("УТ2__КПП", "ИмяПоля");
		Если СтрокаПоляКПП <> Неопределено Тогда
			ФорматСЗВ_СТАЖ.Страхователь.Поля.Удалить(СтрокаПоляКПП);
		КонецЕсли;		
	КонецЕсли;

	Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.Исходная Тогда
		ФорматСЗВ_СТАЖ.Тип.Значение = 0;	
	ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.Дополняющая Тогда
		ФорматСЗВ_СТАЖ.Тип.Значение = 1;	
	ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии Тогда
		ФорматСЗВ_СТАЖ.Тип.Значение = 2;	
	КонецЕсли;	
	НаборЗаписейОтчетныйПериод = ФорматСЗВ_СТАЖ.ОтчетныйПериод.Значение;
	НаборЗаписейОтчетныйПериод.Код = 0;
	НаборЗаписейОтчетныйПериод.Год = ВыборкаПоШапкеДокумента.Год;
	
	УзелФормы = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелПФР, "СЗВ-СТАЖ", "");
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелФормы, ФорматСЗВ_СТАЖ);
	
	ФорматСтажевыйПериод = ОбщегоНазначения.СкопироватьРекурсивно(ФорматЗЛ.СтажевыйПериод.Значение);
	ФорматЗЛ.Удалить("СтажевыйПериод");
	НаборЗаписейДатаУвольнения = Новый Структура("ДатаУвольнения", ОбщегоНазначения.СкопироватьРекурсивно(ФорматЗЛ.ДатаУвольнения));
	ФорматЗЛ.Удалить("ДатаУвольнения");
	
	НаборЗаписейФизическоеЛицо = ОбщегоНазначения.СкопироватьРекурсивно(ФорматЗЛ);
	
	Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерСтроки") Цикл
		
		// Данные по физлицу
		Фамилия = СокрЛП(ВыборкаПоРаботникам.Фамилия);
		Имя = СокрЛП(ВыборкаПоРаботникам.Имя);
		Отчество = СокрЛП(ВыборкаПоРаботникам.Отчество);
		
		НаборЗаписейЗЛ = НаборЗаписейФизическоеЛицо;
		
		НаборЗаписейЗЛ.СНИЛС.Значение = ВыборкаПоРаботникам.СтраховойНомерПФР;
		НаборЗаписейФИО = НаборЗаписейЗЛ.ФИО.Значение;
		НаборЗаписейФИО.УТ2__Фамилия = ВРег(Фамилия);
		НаборЗаписейФИО.УТ2__Имя = ВРег(Имя);
		НаборЗаписейФИО.УТ2__Отчество = ВРег(Отчество);
		
		Элемент = НаборЗаписейЗЛ.ФИО;
		СтрокаПоля = Элемент.Поля.Найти("УТ2__Отчество", "ИмяПоля");
		СтрокаПоля.НеВыводитьВФайл = Не ЗначениеЗаполнено(Элемент.Значение["УТ2__Отчество"]);
		
		УзелСотрудника = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелФормы, "ЗЛ", "");
		СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСотрудника, НаборЗаписейЗЛ);
		
		ВписатьЗаписиОСтажеВНаборДанных(УзелСотрудника, ФорматСтажевыйПериод, ВыборкаПоРаботникам);
		
		Если ЗначениеЗаполнено(ВыборкаПоРаботникам.СотрудникУволен) Тогда
			НаборЗаписейДатаУвольнения.ДатаУвольнения.Значение = Формат(Дата(ВыборкаПоШапкеДокумента.Год, 12, 31), "ДФ=yyyy-MM-dd");
			СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСотрудника, НаборЗаписейДатаУвольнения);
		КонецЕсли;
		
	КонецЦикла;
			
	Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии Тогда
		НаборЗаписейСВ.СВ.Значение.НачисленыНаОПС = ?(ВыборкаПоШапкеДокумента.НачисленыВзносыНаОПС, "1", "0");
		НаборЗаписейСВ.СВ.Значение.НачисленыПоДТ = ?(ВыборкаПоШапкеДокумента.НачисленыВзносыПоДТ, "1", "0");
		СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелФормы, НаборЗаписейСВ);
	КонецЕсли;
	
	// Преобразуем дерево в строковое описание XML
	ПотокВыгрузкиXML = СправкиПоНДФЛ.СоздатьПотокXML(, "UTF-8");
	СправкиПоНДФЛ.ЗаписатьУзелДереваXMLВXML(ДеревоВыгрузки, ПотокВыгрузкиXML, "xsi", "http://www.w3.org/2001/XMLSchema-instance", Ложь);
	ТекстФайла = ПотокВыгрузкиXML.Закрыть();
	
	Возврат СтрЗаменить(ТекстФайла, "__", ":");
	
КонецФункции

Функция СформироватьВыходнойФайл2019(Ссылка)
	
	СписокДокументов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка);
	ВыборкаПоШапкеДокумента = ПерсонифицированныйУчет.ЗапросПоШапкеДляПечатиОДВ_1(СписокДокументов, "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ").Выбрать();
	ВыборкаПоШапкеДокумента.Следующий();
	
	ВыборкаПоДаннымДНП = ПерсонифицированныйУчет.ЗапросПоДаннымДосрочногоНазначенияПенсииОДВ_1(СписокДокументов, "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ").Выбрать();
	
	// Загружаем формат файла сведений
	ДеревоФорматаXML = ПолучитьОбщийМакет("ФорматПФР_2019XML");
	ТекстФорматаXML = ДеревоФорматаXML.ПолучитьТекст();
	
	ДеревоФормата = СправкиПоНДФЛ.ЗагрузитьXMLВДокументDOM(ТекстФорматаXML);
	
	ФорматОДВ_1 = СправкиПоНДФЛ.ЗагрузитьФорматНабораЗаписей(ДеревоФормата, "ОДВ_1");
	
	НаборЗаписейДатаЗаполнения = Новый Структура("ДатаЗаполнения", ОбщегоНазначения.СкопироватьРекурсивно(ФорматОДВ_1.ДатаЗаполнения));
	ФорматОДВ_1.Удалить("ДатаЗаполнения");
	НаборЗаписейРуководитель = Новый Структура("Руководитель", ОбщегоНазначения.СкопироватьРекурсивно(ФорматОДВ_1.Руководитель));
	ФорматОДВ_1.Удалить("Руководитель");
	ФорматОснованияДНП = ОбщегоНазначения.СкопироватьРекурсивно(ФорматОДВ_1.ОснованияДНП.Значение);
	ФорматОДВ_1.Удалить("ОснованияДНП");
	
	ФорматОДВ_1.Удалить("Страховая");
	ФорматОДВ_1.Удалить("Накопительная");
	ФорматОДВ_1.Удалить("ТарифСВ");
	ФорматОДВ_1.Удалить("Уплата");
	
	// Создаем начальное дерево
	ДеревоВыгрузки = СправкиПоНДФЛ.СоздатьДеревоXML();
	
	Атрибуты = Новый Соответствие;
	Атрибуты.Вставить("xmlns", "http://пф.рф/ВС/СЗВ-СТАЖ/2018-03-22");
	Атрибуты.Вставить("xmlns:ВС2", "http://пф.рф/ВС/типы/2017-10-23");
	Атрибуты.Вставить("xmlns:АФ4", "http://пф.рф/АФ/2017-08-21");
	Атрибуты.Вставить("xmlns:УТ2", "http://пф.рф/УТ/2017-08-21");
	Атрибуты.Вставить("xmlns:ИС4", "http://пф.рф/ВС/ИС/типы/2018-11-20");
	
	УзелПФР = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(ДеревоВыгрузки, "ЭДПФР", "", Атрибуты);
	
	// Служебная информация о файле
	НаборЗаписейСлужебнаяИнформация = СправкиПоНДФЛ.ЗагрузитьФорматНабораЗаписей(ДеревоФормата, "СлужебнаяИнформация");
	Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ИмяФайлаДляПФР) Тогда
		ЧастиИмениФайла = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СокрЛП(ВыборкаПоШапкеДокумента.ИмяФайлаДляПФР),"_");
		НаборЗаписейСлужебнаяИнформация.АФ4__GUID.Значение = ЧастиИмениФайла[ЧастиИмениФайла.ВГраница()];
	КонецЕсли;
	НаборЗаписейСлужебнаяИнформация.АФ4__ДатаВремя.Значение = ПерсонифицированныйУчет.ПредставлениеДатыВФорматеdateTimeСЧасовымПоясом(ВыборкаПоШапкеДокумента.Дата);
	НаборЗаписейСлужебнаяИнформация.АФ4__ПрограммаПодготовки.Значение = ВРег(РегламентированнаяОтчетность.НазваниеИВерсияПрограммы());
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелПФР, "СлужебнаяИнформация",""), НаборЗаписейСлужебнаяИнформация);
	
	// Данные формы и страхователя
	ФорматОДВ_1.Тип.Значение = 0;	
	НаборЗаписейСтрахователь = ФорматОДВ_1.Страхователь.Значение;
	НаименованиеКраткое = СокрЛП(ВРег(ВыборкаПоШапкеДокумента.НаименованиеСокращенное));
	ПерсонифицированныйУчет.УдалитьПробелыРядомСДефисом(НаименованиеКраткое);
	НаборЗаписейСтрахователь.ИС4__Наименование = НаименованиеКраткое;
	НаборЗаписейСтрахователь.УТ2__РегНомер = ВыборкаПоШапкеДокумента.РегистрационныйНомерПФР;
	НаборЗаписейСтрахователь.УТ2__ИНН = ВыборкаПоШапкеДокумента.ИНН;
	Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.КПП) Тогда
		НаборЗаписейСтрахователь.УТ2__КПП = ВыборкаПоШапкеДокумента.КПП;
	Иначе
		НаборЗаписейСтрахователь.Удалить("УТ2__КПП");
		СтрокаПоляКПП = ФорматОДВ_1.Страхователь.Поля.Найти("УТ2__КПП", "ИмяПоля");
		Если СтрокаПоляКПП <> Неопределено Тогда
			ФорматОДВ_1.Страхователь.Поля.Удалить(СтрокаПоляКПП);
		КонецЕсли;		
	КонецЕсли;
	НаборЗаписейОтчетныйПериод = ФорматОДВ_1.ОтчетныйПериод.Значение;
	НаборЗаписейОтчетныйПериод.Код = 0;
	НаборЗаписейОтчетныйПериод.Год = ВыборкаПоШапкеДокумента.Год;
	
	ФорматОДВ_1.КоличествоЗЛ.Значение = ВыборкаПоШапкеДокумента.КоличествоЗастрахованныхЛиц;	
	
	УзелФормы = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелПФР, "ОДВ-1", "");
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелФормы, ФорматОДВ_1);
	
	Если ВыборкаПоДаннымДНП.Количество() > 0 Тогда 
		УзелСпискаОснованияДНП = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелФормы, "ОснованияДНП", "");
		ФорматОснование = ОбщегоНазначения.СкопироватьРекурсивно(ФорматОснованияДНП.Основание.Значение);
		ФорматОснованияДНП.Удалить("Основание");
		ВсегоШтат = 0;
		ВсегоФакт = 0;
		Пока ВыборкаПоДаннымДНП.Следующий() Цикл 
			НаборЗаписейОснование = ОбщегоНазначения.СкопироватьРекурсивно(ФорматОснование);
			НаборЗаписейОснование.ИС4__Подразделение.Значение =ВРег(ВыборкаПоДаннымДНП.НаименованиеПодразделения);
			НаборЗаписейОснование.ИС4__ПрофессияДолжность.Значение = ВРег(ВыборкаПоДаннымДНП.НаименованиеДолжности);
			НаборЗаписейОснование.ИС4__КоличествоШтат.Значение = ВыборкаПоДаннымДНП.КоличествоРабочихМест;
			НаборЗаписейОснование.ИС4__КоличествоФакт.Значение = ВыборкаПоДаннымДНП.КоличествоРаботающих;
			НаборЗаписейОснование.ИС4__Описание.Значение = ВРег(ВыборкаПоДаннымДНП.ХарактерВыполняемыхРабот);
			НаборЗаписейОснование.ИС4__Документы.Значение = ВРег(ВыборкаПоДаннымДНП.ПервичныеДокументы);
			НаборЗаписейОУТ = НаборЗаписейОснование.ОУТ.Значение;
			НаборЗаписейОУТ.Код = СокрЛП(ВыборкаПоДаннымДНП.ОснованиеВыслугиЛетКод);
			НаборЗаписейОУТ.ПозицияСписка = СокрЛП(ВыборкаПоДаннымДНП.КодПозицииСпискаКод);
			СправкиПоНДФЛ.ДобавитьИнформациюВДерево(СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСпискаОснованияДНП, "Основание", ""), НаборЗаписейОснование);
			ВсегоШтат = ВсегоШтат + ВыборкаПоДаннымДНП.КоличествоРабочихМест;
			ВсегоФакт = ВсегоФакт + ВыборкаПоДаннымДНП.КоличествоРаботающих;
		КонецЦикла;
		ФорматОснованияДНП.ИС4__ВсегоШтат.Значение = ВсегоШтат;
		ФорматОснованияДНП.ИС4__ВсегоФакт.Значение = ВсегоФакт;
		СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСпискаОснованияДНП, ФорматОснованияДНП);
	КонецЕсли;
	
	НаборЗаписейРуководитель.Руководитель.Значение.Должность.Значение = ВРег(ВыборкаПоШапкеДокумента.ДолжностьРуководителя);
	НаборЗаписейФИО = НаборЗаписейРуководитель.Руководитель.Значение.ФИО.Значение;
	
	ФИООтдельно = СтрРазделить(ВыборкаПоШапкеДокумента.Руководитель, " ");
	Если ФИООтдельно.Количество() > 0 Тогда
		НаборЗаписейФИО.УТ2__Фамилия = ВРег(ФИООтдельно[0]);
	КонецЕсли;
	Если ФИООтдельно.Количество() > 1 Тогда
		НаборЗаписейФИО.УТ2__Имя = ВРег(ФИООтдельно[1]);
	КонецЕсли;
	Если ФИООтдельно.Количество() > 2 Тогда
		НаборЗаписейФИО.УТ2__Отчество = ВРег(ФИООтдельно[2]);
	КонецЕсли;
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелФормы, НаборЗаписейРуководитель);
	
	НаборЗаписейДатаЗаполнения.ДатаЗаполнения.Значение = Формат(ВыборкаПоШапкеДокумента.Дата, "ДФ=yyyy-MM-dd");
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелФормы, НаборЗаписейДатаЗаполнения);
	
	ФорматСЗВ_СТАЖ = СправкиПоНДФЛ.ЗагрузитьФорматНабораЗаписей(ДеревоФормата, "СЗВ_СТАЖ");
	
	ФорматЗЛ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматСЗВ_СТАЖ.ЗЛ.Значение);
	ФорматСЗВ_СТАЖ.Удалить("ЗЛ");
	НаборЗаписейСВ = Новый Структура("СВ", ОбщегоНазначения.СкопироватьРекурсивно(ФорматСЗВ_СТАЖ.СВ));
	ФорматСЗВ_СТАЖ.Удалить("СВ");
	
	ВыборкаПоРаботникам = Документы.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ЗапросПоСотрудникамДляПечати(СписокДокументов).Выбрать();
	
	// Данные формы и страхователя
	НаборЗаписейСтрахователь = ФорматСЗВ_СТАЖ.Страхователь.Значение;
	НаборЗаписейСтрахователь.ИС4__Наименование = ВРег(ВыборкаПоШапкеДокумента.НаименованиеСокращенное);
	НаборЗаписейСтрахователь.УТ2__РегНомер = ВыборкаПоШапкеДокумента.РегистрационныйНомерПФР;
	НаборЗаписейСтрахователь.УТ2__ИНН = ВыборкаПоШапкеДокумента.ИНН;
	Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.КПП) Тогда
		НаборЗаписейСтрахователь.УТ2__КПП = ВыборкаПоШапкеДокумента.КПП;
	Иначе
		НаборЗаписейСтрахователь.Удалить("УТ2__КПП");
		СтрокаПоляКПП = ФорматСЗВ_СТАЖ.Страхователь.Поля.Найти("УТ2__КПП", "ИмяПоля");
		Если СтрокаПоляКПП <> Неопределено Тогда
			ФорматСЗВ_СТАЖ.Страхователь.Поля.Удалить(СтрокаПоляКПП);
		КонецЕсли;		
	КонецЕсли;

	Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.Исходная Тогда
		ФорматСЗВ_СТАЖ.Тип.Значение = 0;	
	ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.Дополняющая Тогда
		ФорматСЗВ_СТАЖ.Тип.Значение = 1;	
	ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии Тогда
		ФорматСЗВ_СТАЖ.Тип.Значение = 2;	
	КонецЕсли;	
	НаборЗаписейОтчетныйПериод = ФорматСЗВ_СТАЖ.ОтчетныйПериод.Значение;
	НаборЗаписейОтчетныйПериод.Код = 0;
	НаборЗаписейОтчетныйПериод.Год = ВыборкаПоШапкеДокумента.Год;
	
	УзелФормы = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелПФР, "СЗВ-СТАЖ", "");
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелФормы, ФорматСЗВ_СТАЖ);
	
	ФорматСтажевыйПериод = ОбщегоНазначения.СкопироватьРекурсивно(ФорматЗЛ.СтажевыйПериод.Значение);
	ФорматЗЛ.Удалить("СтажевыйПериод");
	НаборЗаписейДатаУвольнения = Новый Структура("ДатаУвольнения", ОбщегоНазначения.СкопироватьРекурсивно(ФорматЗЛ.ДатаУвольнения));
	ФорматЗЛ.Удалить("ДатаУвольнения");
	
	НаборЗаписейФизическоеЛицо = ОбщегоНазначения.СкопироватьРекурсивно(ФорматЗЛ);
	
	Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерСтроки") Цикл
		
		// Данные по физлицу
		Фамилия = СокрЛП(ВыборкаПоРаботникам.Фамилия);
		Имя = СокрЛП(ВыборкаПоРаботникам.Имя);
		Отчество = СокрЛП(ВыборкаПоРаботникам.Отчество);
		
		НаборЗаписейЗЛ = НаборЗаписейФизическоеЛицо;
		
		НаборЗаписейЗЛ.СНИЛС.Значение = ВыборкаПоРаботникам.СтраховойНомерПФР;
		НаборЗаписейФИО = НаборЗаписейЗЛ.ФИО.Значение;
		НаборЗаписейФИО.УТ2__Фамилия = ВРег(Фамилия);
		НаборЗаписейФИО.УТ2__Имя = ВРег(Имя);
		НаборЗаписейФИО.УТ2__Отчество = ВРег(Отчество);
		
		УзелСотрудника = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелФормы, "ЗЛ", "");
		СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСотрудника, НаборЗаписейЗЛ);
		
		ВписатьЗаписиОСтажеВНаборДанных2019(УзелСотрудника, ФорматСтажевыйПериод, ВыборкаПоРаботникам);
		
		Если ЗначениеЗаполнено(ВыборкаПоРаботникам.СотрудникУволен) Тогда
			НаборЗаписейДатаУвольнения.ДатаУвольнения.Значение = Формат(Дата(ВыборкаПоШапкеДокумента.Год, 12, 31), "ДФ=yyyy-MM-dd");
			СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСотрудника, НаборЗаписейДатаУвольнения);
		КонецЕсли;
		
	КонецЦикла;
			
	Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии Тогда
		НаборЗаписейСВ.СВ.Значение.НачисленыНаОПС = ?(ВыборкаПоШапкеДокумента.НачисленыВзносыНаОПС, "1", "0");
		НаборЗаписейСВ.СВ.Значение.НачисленыПоДТ = ?(ВыборкаПоШапкеДокумента.НачисленыВзносыПоДТ, "1", "0");
		СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелФормы, НаборЗаписейСВ);
	КонецЕсли;
	
	// Преобразуем дерево в строковое описание XML
	ПотокВыгрузкиXML = СправкиПоНДФЛ.СоздатьПотокXML(, "UTF-8");
	СправкиПоНДФЛ.ЗаписатьУзелДереваXMLВXML(ДеревоВыгрузки, ПотокВыгрузкиXML, "xsi", "http://www.w3.org/2001/XMLSchema-instance", Ложь);
	ТекстФайла = ПотокВыгрузкиXML.Закрыть();
	
	Возврат СтрЗаменить(ТекстФайла, "__", ":");
	
КонецФункции

Процедура ВписатьЗаписиОСтажеВНаборДанных(УзелСотрудника, ФорматСтажевыйПериод, ВыборкаПоРаботникам) Экспорт	
	
	ФорматЛьготныйСтаж = ФорматСтажевыйПериод.ИС2__ЛьготныйСтаж.Значение;
	ФорматСтажевыйПериод.ИС2__ЛьготныйСтаж.НеВыводитьВФайл = Истина;
	ПараметрыСтажаСПравомНаДосрочнуюПенсию = ПерсонифицированныйУчет.ПараметрыИсчисляемогоСтажаПредусматривающиеПравоНаДосрочнуюПенсию();
			
	Если ЗначениеЗаполнено(ВыборкаПоРаботникам.НомерОсновнойЗаписи) Тогда
		
		Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерОсновнойЗаписи")	Цикл	
			
			ФорматСтажевыйПериод.ИС2__ЛьготныйСтаж.НеВыводитьВФайл = Истина;

			УзелСтажевыйПериод = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСотрудника, "СтажевыйПериод", "");
			СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСтажевыйПериод, ФорматСтажевыйПериод);
			
			УзелСтажевыйПериод.Строки.Найти("ИС2__Период").Строки.Найти("УТ2__С").Значение = Формат(ВыборкаПоРаботникам.ДатаНачалаПериода, "ДФ=yyyy-MM-dd");
			УзелСтажевыйПериод.Строки.Найти("ИС2__Период").Строки.Найти("УТ2__По").Значение = Формат(ВыборкаПоРаботникам.ДатаОкончанияПериода, "ДФ=yyyy-MM-dd");
			
			ВывестиЛьготныйСтаж(ВыборкаПоРаботникам, УзелСтажевыйПериод, ФорматЛьготныйСтаж, ПараметрыСтажаСПравомНаДосрочнуюПенсию);
			
			Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерДополнительнойЗаписи") Цикл
				// В дополнительной записи с номером 0 находится информация об основной записи
				Если ВыборкаПоРаботникам.НомерДополнительнойЗаписи = 0 Тогда
					Продолжить;
				КонецЕсли;
				ВывестиЛьготныйСтаж(ВыборкаПоРаботникам, УзелСтажевыйПериод, ФорматЛьготныйСтаж, ПараметрыСтажаСПравомНаДосрочнуюПенсию);
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры	

Процедура ВписатьЗаписиОСтажеВНаборДанных2019(УзелСотрудника, ФорматСтажевыйПериод, ВыборкаПоРаботникам) Экспорт	
	
	ФорматЛьготныйСтаж = ФорматСтажевыйПериод.ИС4__ЛьготныйСтаж.Значение;
	ФорматСтажевыйПериод.ИС4__ЛьготныйСтаж.НеВыводитьВФайл = Истина;
	ПараметрыСтажаСПравомНаДосрочнуюПенсию = ПерсонифицированныйУчет.ПараметрыИсчисляемогоСтажаПредусматривающиеПравоНаДосрочнуюПенсию();
			
	Если ЗначениеЗаполнено(ВыборкаПоРаботникам.НомерОсновнойЗаписи) Тогда
		
		Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерОсновнойЗаписи")	Цикл	
			
			ФорматСтажевыйПериод.ИС4__ЛьготныйСтаж.НеВыводитьВФайл = Истина;

			УзелСтажевыйПериод = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСотрудника, "СтажевыйПериод", "");
			СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСтажевыйПериод, ФорматСтажевыйПериод);
			
			УзелСтажевыйПериод.Строки.Найти("ИС4__Период").Строки.Найти("УТ2__С").Значение = Формат(ВыборкаПоРаботникам.ДатаНачалаПериода, "ДФ=yyyy-MM-dd");
			УзелСтажевыйПериод.Строки.Найти("ИС4__Период").Строки.Найти("УТ2__По").Значение = Формат(ВыборкаПоРаботникам.ДатаОкончанияПериода, "ДФ=yyyy-MM-dd");
			
			ВывестиЛьготныйСтаж2019(ВыборкаПоРаботникам, УзелСтажевыйПериод, ФорматЛьготныйСтаж, ПараметрыСтажаСПравомНаДосрочнуюПенсию);
			
			Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерДополнительнойЗаписи") Цикл
				// В дополнительной записи с номером 0 находится информация об основной записи
				Если ВыборкаПоРаботникам.НомерДополнительнойЗаписи = 0 Тогда
					Продолжить;
				КонецЕсли;
				ВывестиЛьготныйСтаж2019(ВыборкаПоРаботникам, УзелСтажевыйПериод, ФорматЛьготныйСтаж, ПараметрыСтажаСПравомНаДосрочнуюПенсию);
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры	

Процедура ВывестиЛьготныйСтаж2019(ВыборкаПоРаботникам, УзелСтажевыйПериод, ФорматЛьготныйСтаж, ПараметрыСтажаСПравомНаДосрочнуюПенсию)
	
	ВыводитьЛьготныйСтаж = Ложь;
	
	НаборЗаписейЛьготныйСтаж = ФорматЛьготныйСтаж;
	НаборЗаписейЛьготныйСтаж.ИС4__ТУ.НеВыводитьВФайл = Ложь;
	НаборЗаписейЛьготныйСтаж.ИС4__ОУТ.НеВыводитьВФайл = Ложь;
	НаборЗаписейЛьготныйСтаж.ИС4__ИС.НеВыводитьВФайл = Ложь;
	НаборЗаписейЛьготныйСтаж.ИС4__ДопСведенияИС.НеВыводитьВФайл = Ложь;
	НаборЗаписейЛьготныйСтаж.ИС4__ВЛ.НеВыводитьВФайл = Ложь;
	
	// ДопСведенияИС
	Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) Тогда
		НаборЗаписейЛьготныйСтаж.ИС4__ДопСведенияИС.Значение = ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажаКод;
		ВыводитьЛьготныйСтаж = Истина;
	ИначеЕсли ЗначениеЗаполнено(ВыборкаПоРаботникам.ЗамещениеГосударственныхМуниципальныхДолжностей) Тогда
		НаборЗаписейЛьготныйСтаж.ИС4__ДопСведенияИС.Значение = ВыборкаПоРаботникам.ЗамещениеГосударственныхМуниципальныхДолжностей;
		ВыводитьЛьготныйСтаж = Истина;
	Иначе
		НаборЗаписейЛьготныйСтаж.ИС4__ДопСведенияИС.НеВыводитьВФайл = Истина;	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа)
		И ПараметрыСтажаСПравомНаДосрочнуюПенсию.Найти(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) = Неопределено Тогда 
		
		НаборЗаписейЛьготныйСтаж.ИС4__ВЛ.НеВыводитьВФайл = Истина;
		НаборЗаписейЛьготныйСтаж.ИС4__ИС.НеВыводитьВФайл = Истина;
		НаборЗаписейЛьготныйСтаж.ИС4__ОУТ.НеВыводитьВФайл = Истина;
		НаборЗаписейЛьготныйСтаж.ИС4__ТУ.НеВыводитьВФайл = Истина;
		
	Иначе
		НаборЗаписейТерриториальныеУсловия = НаборЗаписейЛьготныйСтаж.ИС4__ТУ.Значение;
		// Территориальные условия
		Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТерриториальныеУсловияКод) Тогда
			НаборЗаписейТерриториальныеУсловия.ИС4__Основание = СокрЛП(ВыборкаПоРаботникам.ТерриториальныеУсловияКод);
			НаборЗаписейТерриториальныеУсловия.ИС4__Коэффициент = ВыборкаПоРаботникам.ТерриториальныеУсловияСтавка;
			Если ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловияПФР.МКС 
				Или ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловияПФР.РКС 
				Или ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловияПФР.РКСМ 
				Или ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловияПФР.МКСР Тогда
				НаборЗаписейТерриториальныеУсловия.ИС4__Коэффициент = ВыборкаПоРаботникам.ТерриториальныеУсловияСтавка;
			Иначе
				ОписаниеПолей = НаборЗаписейЛьготныйСтаж.ИС4__ТУ.Поля;
				СтрокаКУдалению = ОписаниеПолей.Найти("ИС4__Коэффициент", "ИмяПоля");
				СтрокаКУдалению.НеВыводитьВФайл = Истина;
			КонецЕсли;
		Иначе
			НаборЗаписейЛьготныйСтаж.ИС4__ТУ.НеВыводитьВФайл = Истина;	
		КонецЕсли;	
		ВыводитьЛьготныйСтаж = ВыводитьЛьготныйСтаж Или ЗначениеЗаполнено(НаборЗаписейТерриториальныеУсловия.ИС4__Основание);
		
		// Особые условия труда
		НаборЗаписейОсобыеУсловияТруда = НаборЗаписейЛьготныйСтаж.ИС4__ОУТ.Значение;
		
		НаборЗаписейЛьготныйСтаж.ИС4__ОУТ.НеВыводитьВФайл = Ложь;
		
		Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ОсобыеУсловияТрудаКод) Тогда
			НаборЗаписейОсобыеУсловияТруда.ИС4__Код = СокрЛП(ВыборкаПоРаботникам.ОсобыеУсловияТрудаКод);
			НаборЗаписейОсобыеУсловияТруда.ИС4__ПозицияСписка = ВРег(СокрЛП(ВыборкаПоРаботникам.КодПозицииСпискаКод));
			ВыводитьЛьготныйСтаж = ВыводитьЛьготныйСтаж Или ЗначениеЗаполнено(НаборЗаписейОсобыеУсловияТруда.ИС4__Код);
		Иначе
			НаборЗаписейЛьготныйСтаж.ИС4__ОУТ.НеВыводитьВФайл = Истина;
		КонецЕсли;	
		
		// Исчисляемый стаж
		НаборЗаписейИсчисляемыйСтаж = НаборЗаписейЛьготныйСтаж.ИС4__ИС.Значение;
		НаборЗаписейИсчисляемыйСтаж.ИС4__Основание.НеВыводитьВФайл = Ложь;
		Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажаКод) Тогда
			НаборЗаписейИсчисляемыйСтаж.ИС4__Основание.Значение = СокрЛП(ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажаКод);
		Иначе
			НаборЗаписейИсчисляемыйСтаж.ИС4__Основание.НеВыводитьВФайл = Истина;
		КонецЕсли;
		
		НаборЗаписейЛьготныйСтаж.ИС4__ИС.НеВыводитьВФайл = Ложь;
		НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Ложь;
		НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Ложь;
		
		Если Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажа) И ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) Тогда
			НаборЗаписейЛьготныйСтаж.ИС4__ИС.НеВыводитьВФайл = Истина;
		ИначеЕсли ЗначениеЗаполнено(ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажа) Или ЗначениеЗаполнено(ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа) Или ЗначениеЗаполнено(ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа) Тогда		
			Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) Тогда
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
			ИначеЕсли ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажа = Справочники.ОснованияИсчисляемогоСтраховогоСтажа.ВОДОЛАЗ Тогда
				НаборЗаписейВыработкаВЧасах = НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.Значение;
				НаборЗаписейВыработкаВЧасах.ВС2__Часы = ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа;
				НаборЗаписейВыработкаВЧасах.ВС2__Минуты = ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа;
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
			ИначеЕсли ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа > 0 Или ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа > 0 Тогда
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейВыработкаКалендарная = НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаКалендарная.Значение;
				НаборЗаписейВыработкаКалендарная.ВС2__ВсеМесяцы = ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа;
				НаборЗаписейВыработкаКалендарная.ВС2__ВсеДни = ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа;
			Иначе
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
			КонецЕсли;
			ВыводитьЛьготныйСтаж = Истина;
		Иначе
			НаборЗаписейЛьготныйСтаж.ИС4__ИС.НеВыводитьВФайл = Истина;		
		КонецЕсли;	
		
		ВыводитьЛьготныйСтаж = ВыводитьЛьготныйСтаж Или ЗначениеЗаполнено(НаборЗаписейИсчисляемыйСтаж.ИС4__Основание.Значение);
		
		// Выслуга лет
		НаборЗаписейВыслугаЛет = НаборЗаписейЛьготныйСтаж.ИС4__ВЛ.Значение;
		НаборЗаписейЛьготныйСтаж.ИС4__ВЛ.НеВыводитьВФайл = Ложь;	
		НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Ложь;
		НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Ложь;
		
		Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ОснованиеВыслугиЛетКод) Тогда
			
			НаборЗаписейВыслугаЛет.ИС4__Основание.Значение = ВРег(СокрЛП(ВыборкаПоРаботникам.ОснованиеВыслугиЛетКод));
			НаборЗаписейВыслугаЛет.ИС4__ДоляСтавки.Значение = ВыборкаПоРаботникам.ТретийПараметрВыслугиЛет;
			Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) Тогда	
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
			ИначеЕсли Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ПервыйПараметрВыслугиЛет) И Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ВторойПараметрВыслугиЛет) Тогда
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
			ИначеЕсли ВыборкаПоРаботникам.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.Ст27_15 Тогда
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейВыработкаКалендарная = НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.Значение;
				НаборЗаписейВыработкаКалендарная.ВС2__ВсеМесяцы = ВыборкаПоРаботникам.ПервыйПараметрВыслугиЛет;
				НаборЗаписейВыработкаКалендарная.ВС2__ВсеДни = ВыборкаПоРаботникам.ВторойПараметрВыслугиЛет;
			Иначе
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
				НаборЗаписейВыработкаВЧасах = НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.Значение;
				НаборЗаписейВыработкаВЧасах.ВС2__Часы = ВыборкаПоРаботникам.ПервыйПараметрВыслугиЛет;
				НаборЗаписейВыработкаВЧасах.ВС2__Минуты = ВыборкаПоРаботникам.ВторойПараметрВыслугиЛет;
			КонецЕсли;
			ВыводитьЛьготныйСтаж = Истина;
		Иначе
			НаборЗаписейЛьготныйСтаж.ИС4__ВЛ.НеВыводитьВФайл = Истина;	
		КонецЕсли;	
		
	КонецЕсли;
	
	Если ВыводитьЛьготныйСтаж Тогда
		УзелЛьготныйСтаж = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСтажевыйПериод, "ИС4__ЛьготныйСтаж","");
		СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелЛьготныйСтаж, НаборЗаписейЛьготныйСтаж);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиЛьготныйСтаж(ВыборкаПоРаботникам, УзелСтажевыйПериод, ФорматЛьготныйСтаж, ПараметрыСтажаСПравомНаДосрочнуюПенсию)
	
	ВыводитьЛьготныйСтаж = Ложь;
	
	НаборЗаписейЛьготныйСтаж = ФорматЛьготныйСтаж;
	НаборЗаписейЛьготныйСтаж.ИС2__ТУ.НеВыводитьВФайл = Ложь;
	НаборЗаписейЛьготныйСтаж.ИС2__ОУТ.НеВыводитьВФайл = Ложь;
	НаборЗаписейЛьготныйСтаж.ИС2__ИС.НеВыводитьВФайл = Ложь;
	НаборЗаписейЛьготныйСтаж.ИС2__ДопСведенияИС.НеВыводитьВФайл = Ложь;
	НаборЗаписейЛьготныйСтаж.ИС2__ВЛ.НеВыводитьВФайл = Ложь;
	
	// ДопСведенияИС
	Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) Тогда
		НаборЗаписейЛьготныйСтаж.ИС2__ДопСведенияИС.Значение = ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажаКод;
		ВыводитьЛьготныйСтаж = Истина;
	ИначеЕсли ЗначениеЗаполнено(ВыборкаПоРаботникам.ЗамещениеГосударственныхМуниципальныхДолжностей) Тогда
		НаборЗаписейЛьготныйСтаж.ИС2__ДопСведенияИС.Значение = ВыборкаПоРаботникам.ЗамещениеГосударственныхМуниципальныхДолжностей;
		ВыводитьЛьготныйСтаж = Истина;
	Иначе
		НаборЗаписейЛьготныйСтаж.ИС2__ДопСведенияИС.НеВыводитьВФайл = Истина;	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа)
		И ПараметрыСтажаСПравомНаДосрочнуюПенсию.Найти(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) = Неопределено Тогда 
		
		НаборЗаписейЛьготныйСтаж.ИС2__ВЛ.НеВыводитьВФайл = Истина;
		НаборЗаписейЛьготныйСтаж.ИС2__ИС.НеВыводитьВФайл = Истина;
		НаборЗаписейЛьготныйСтаж.ИС2__ОУТ.НеВыводитьВФайл = Истина;
		НаборЗаписейЛьготныйСтаж.ИС2__ТУ.НеВыводитьВФайл = Истина;
		
	Иначе
		НаборЗаписейТерриториальныеУсловия = НаборЗаписейЛьготныйСтаж.ИС2__ТУ.Значение;
		// Территориальные условия
		Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТерриториальныеУсловияКод) Тогда
			НаборЗаписейТерриториальныеУсловия.ИС2__Основание = СокрЛП(ВыборкаПоРаботникам.ТерриториальныеУсловияКод);
			НаборЗаписейТерриториальныеУсловия.ИС2__Коэффициент = ВыборкаПоРаботникам.ТерриториальныеУсловияСтавка;
			Если ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловияПФР.МКС 
				Или ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловияПФР.РКС 
				Или ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловияПФР.РКСМ 
				Или ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловияПФР.МКСР Тогда
				НаборЗаписейТерриториальныеУсловия.ИС2__Коэффициент = ВыборкаПоРаботникам.ТерриториальныеУсловияСтавка;
			Иначе
				ОписаниеПолей = НаборЗаписейЛьготныйСтаж.ИС2__ТУ.Поля;
				СтрокаКУдалению = ОписаниеПолей.Найти("ИС2__Коэффициент", "ИмяПоля");
				СтрокаКУдалению.НеВыводитьВФайл = Истина;
			КонецЕсли;
		Иначе
			НаборЗаписейЛьготныйСтаж.ИС2__ТУ.НеВыводитьВФайл = Истина;	
		КонецЕсли;	
		ВыводитьЛьготныйСтаж = ВыводитьЛьготныйСтаж Или ЗначениеЗаполнено(НаборЗаписейТерриториальныеУсловия.ИС2__Основание);
		
		// Особые условия труда
		НаборЗаписейОсобыеУсловияТруда = НаборЗаписейЛьготныйСтаж.ИС2__ОУТ.Значение;
		
		НаборЗаписейЛьготныйСтаж.ИС2__ОУТ.НеВыводитьВФайл = Ложь;
		
		Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ОсобыеУсловияТрудаКод) Тогда
			НаборЗаписейОсобыеУсловияТруда.ИС2__Код = СокрЛП(ВыборкаПоРаботникам.ОсобыеУсловияТрудаКод);
			НаборЗаписейОсобыеУсловияТруда.ИС2__ПозицияСписка = ВРег(СокрЛП(ВыборкаПоРаботникам.КодПозицииСпискаКод));
			ВыводитьЛьготныйСтаж = ВыводитьЛьготныйСтаж Или ЗначениеЗаполнено(НаборЗаписейОсобыеУсловияТруда.ИС2__Код);
		Иначе
			НаборЗаписейЛьготныйСтаж.ИС2__ОУТ.НеВыводитьВФайл = Истина;
		КонецЕсли;	
		
		// Исчисляемый стаж
		НаборЗаписейИсчисляемыйСтаж = НаборЗаписейЛьготныйСтаж.ИС2__ИС.Значение;
		НаборЗаписейИсчисляемыйСтаж.ИС2__Основание.НеВыводитьВФайл = Ложь;
		Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажаКод) Тогда
			НаборЗаписейИсчисляемыйСтаж.ИС2__Основание.Значение = СокрЛП(ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажаКод);
		Иначе
			НаборЗаписейИсчисляемыйСтаж.ИС2__Основание.НеВыводитьВФайл = Истина;
		КонецЕсли;
		
		НаборЗаписейЛьготныйСтаж.ИС2__ИС.НеВыводитьВФайл = Ложь;
		НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Ложь;
		НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Ложь;
		
		Если Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажа) И ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) Тогда
			НаборЗаписейЛьготныйСтаж.ИС2__ИС.НеВыводитьВФайл = Истина;
		ИначеЕсли ЗначениеЗаполнено(ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажа) Или ЗначениеЗаполнено(ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа) Или ЗначениеЗаполнено(ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа) Тогда		
			Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) Тогда
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
			ИначеЕсли ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажа = Справочники.ОснованияИсчисляемогоСтраховогоСтажа.ВОДОЛАЗ Тогда
				НаборЗаписейВыработкаВЧасах = НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.Значение;
				НаборЗаписейВыработкаВЧасах.ВС2__Часы = ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа;
				НаборЗаписейВыработкаВЧасах.ВС2__Минуты = ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа;
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
			ИначеЕсли ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа > 0 Или ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа > 0 Тогда
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейВыработкаКалендарная = НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаКалендарная.Значение;
				НаборЗаписейВыработкаКалендарная.ВС2__ВсеМесяцы = ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа;
				НаборЗаписейВыработкаКалендарная.ВС2__ВсеДни = ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа;
			Иначе
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
			КонецЕсли;
			ВыводитьЛьготныйСтаж = Истина;
		Иначе
			НаборЗаписейЛьготныйСтаж.ИС2__ИС.НеВыводитьВФайл = Истина;		
		КонецЕсли;	
		
		ВыводитьЛьготныйСтаж = ВыводитьЛьготныйСтаж Или ЗначениеЗаполнено(НаборЗаписейИсчисляемыйСтаж.ИС2__Основание.Значение);
		
		// Выслуга лет
		НаборЗаписейВыслугаЛет = НаборЗаписейЛьготныйСтаж.ИС2__ВЛ.Значение;
		НаборЗаписейЛьготныйСтаж.ИС2__ВЛ.НеВыводитьВФайл = Ложь;	
		НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Ложь;
		НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Ложь;
		
		Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ОснованиеВыслугиЛетКод) Тогда
			
			НаборЗаписейВыслугаЛет.ИС2__Основание.Значение = ВРег(СокрЛП(ВыборкаПоРаботникам.ОснованиеВыслугиЛетКод));
			НаборЗаписейВыслугаЛет.ИС2__ДоляСтавки.Значение = ВыборкаПоРаботникам.ТретийПараметрВыслугиЛет;
			Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) Тогда	
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
			ИначеЕсли Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ПервыйПараметрВыслугиЛет) И Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ВторойПараметрВыслугиЛет) Тогда
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
			ИначеЕсли ВыборкаПоРаботникам.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.Ст27_15 Тогда
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейВыработкаКалендарная = НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.Значение;
				НаборЗаписейВыработкаКалендарная.ВС2__ВсеМесяцы = ВыборкаПоРаботникам.ПервыйПараметрВыслугиЛет;
				НаборЗаписейВыработкаКалендарная.ВС2__ВсеДни = ВыборкаПоРаботникам.ВторойПараметрВыслугиЛет;
			Иначе
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
				НаборЗаписейВыработкаВЧасах = НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.Значение;
				НаборЗаписейВыработкаВЧасах.ВС2__Часы = ВыборкаПоРаботникам.ПервыйПараметрВыслугиЛет;
				НаборЗаписейВыработкаВЧасах.ВС2__Минуты = ВыборкаПоРаботникам.ВторойПараметрВыслугиЛет;
			КонецЕсли;
			ВыводитьЛьготныйСтаж = Истина;
		Иначе
			НаборЗаписейЛьготныйСтаж.ИС2__ВЛ.НеВыводитьВФайл = Истина;	
		КонецЕсли;	
		
	КонецЕсли;
	
	Если ВыводитьЛьготныйСтаж Тогда
		УзелЛьготныйСтаж = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСтажевыйПериод, "ИС2__ЛьготныйСтаж","");
		СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелЛьготныйСтаж, НаборЗаписейЛьготныйСтаж);
	КонецЕсли;
	
КонецПроцедуры

Функция ВыгрузитьФайлыВоВременноеХранилище(Ссылка, УникальныйИдентификатор = Неопределено) Экспорт
	
	ДанныеФайла = УчетСтраховыхВзносов.ПолучитьДанныеФайла(Ссылка, УникальныйИдентификатор);
	
	ОписаниеВыгруженногоФайла = ПерсонифицированныйУчет.ОписаниеВыгруженногоФайлаОтчетности();
	
	ОписаниеВыгруженногоФайла.Владелец = Ссылка;
	ОписаниеВыгруженногоФайла.АдресВоВременномХранилище = ДанныеФайла.СсылкаНаДвоичныеДанныеФайла;
	ОписаниеВыгруженногоФайла.ИмяФайла = ДанныеФайла.ИмяФайла;
	ОписаниеВыгруженногоФайла.ПроверятьCheckXML = Ложь;
	ОписаниеВыгруженногоФайла.ПроверятьCheckUFA = Истина;
	
	ВыгруженныеФайлы = Новый Массив;
	ВыгруженныеФайлы.Добавить(ОписаниеВыгруженногоФайла);
	
	Возврат ВыгруженныеФайлы;
	
КонецФункции

Функция СтруктураОтбораДанныхДляКвартальнойОтчетности() Экспорт
	СтруктураПараметров = Новый Структура;
	
	СтруктураПараметров.Вставить("КатегорияЗастрахованныхЛиц");
	СтруктураПараметров.Вставить("ТипДоговора");
	СтруктураПараметров.Вставить("СписокФизическихЛиц");
	СтруктураПараметров.Вставить("СписокФизическихЛицДляАвтоКорректировки", Новый Массив);
	
	Возврат СтруктураПараметров; 
КонецФункции

Функция ВариантыИсточниковДанныхСтажа() Экспорт 
	Варианты = Новый Структура;
	Варианты.Вставить("ДанныеУчета", "ДанныеУчета");
	Варианты.Вставить("ДанныеУчетаИФиксированныеДанныеДокументов", "ДанныеУчетаИФиксированныеДанныеДокументов");
	Варианты.Вставить("ДанныеУчетаИФиксированныеДанныеКорректируемыхДокументов", "ДанныеУчетаИФиксированныеДанныеКорректируемыхДокументов");
	Варианты.Вставить("ДанныеКорректируемыхДокументов", "ДанныеКорректируемыхДокументов");

	Возврат Варианты;
	
КонецФункции

#Область ПроцедурыИФункцииПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПФ_MXL_ФормаСЗВ_СТАЖ";
	КомандаПечати.Представление = НСтр("ru = 'СЗВ-СТАЖ'");
	КомандаПечати.Порядок = 10;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПФ_MXL_ФормаОДВ_1";
	КомандаПечати.Представление = НСтр("ru = 'ОДВ-1'");
	КомандаПечати.Порядок = 20;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_ФормаСЗВ_СТАЖ") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ПФ_MXL_ФормаСЗВ_СТАЖ", 
			"СЗВ-СТАЖ", 
			ПечатнаяФормаСЗВ_СТАЖ(МассивОбъектов, ОбъектыПечати));
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_ФормаОДВ_1") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ПФ_MXL_ФормаОДВ_1", 
			"ОДВ-1", 
			ПерсонифицированныйУчет.ПечатнаяФормаОДВ_1(МассивОбъектов, ОбъектыПечати, "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ"));
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатнаяФормаСЗВ_СТАЖ(МассивОбъектов, ОбъектыПечати)
	
	
	ВыборкаПоШапкеДокумента = ЗапросПоШапкеДляПечати(МассивОбъектов).Выбрать();
	ВыборкаПоРаботникам = ЗапросПоСотрудникамДляПечати(МассивОбъектов).Выбрать();
	
	ЭтоПервыйДокументКоллекции = Истина;
	
	Пока ВыборкаПоШапкеДокумента.СледующийПоЗначениюПоля("Ссылка") Цикл 
		
		Макет = ?(ВыборкаПоШапкеДокумента.Формат2019,
		    УправлениеПечатью.МакетПечатнойФормы("Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ПФ_MXL_ФормаСЗВ_СТАЖ_2019"),
			УправлениеПечатью.МакетПечатнойФормы("Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ПФ_MXL_ФормаСЗВ_СТАЖ"));
		
		ДокументРезультат = Новый ТабличныйДокумент;
		ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СЗВ_СТАЖ";
		ДокументРезультат.ПолеСлева = 0;
		ДокументРезультат.ПолеСправа = 0;
		ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьСтрокаСтажа = Макет.ПолучитьОбласть("СтрокаСтажа");
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		ОбластьСтрокаКодПозицииСписка = Макет.ПолучитьОбласть("СтрокаКодПозицииСписка");
		
		Если ЭтоПервыйДокументКоллекции Тогда
			ЭтоПервыйДокументКоллекции = Ложь;	
		Иначе
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;	
		
		ТекущийЛист = Новый ТабличныйДокумент;
		ТекущийЛист.ПолеСлева = 0;
		ТекущийЛист.ПолеСправа = 0;
		ТекущийЛист.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;
		
		РегистрационныйНомерПФР = СтрЗаменить(ВыборкаПоШапкеДокумента.РегистрационныйНомерПФР," ","");
		РегистрационныйНомерПФР = СтрЗаменить(РегистрационныйНомерПФР,"-","");
		ПерсонифицированныйУчет.ВывестиДанныеПоБуквенно(РегистрационныйНомерПФР, ОбластьШапка, "НомРег", 12);
		ПерсонифицированныйУчет.ВывестиДанныеПоБуквенно(РегистрационныйНомерПФР, ОбластьШапка, "НомРег", 12, 13);
		
		ИНН = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ВыборкаПоШапкеДокумента.ИНН, 12, "-", "Справа");
		ПерсонифицированныйУчет.ВывестиДанныеПоБуквенно(ИНН, ОбластьШапка, "ИНН", 12, 13);
		
		ПерсонифицированныйУчет.ВывестиДанныеПоБуквенно(Формат(ВыборкаПоШапкеДокумента.КПП,"ЧГ=0"), ОбластьШапка, "КПП", 9, 10);
		
		ПерсонифицированныйУчет.ВывестиДанныеПоБуквенно(Формат(ВыборкаПоШапкеДокумента.Год,"ЧГ=0"), ОбластьШапка, "Год", 4);
		
		НомерСтраницы = 1;
		НомерДляПечати = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(НомерСтраницы, 5);
		Если Не ВыборкаПоШапкеДокумента.Формат2019 Тогда 
			ПерсонифицированныйУчет.ВывестиДанныеПоБуквенно(РегистрационныйНомерПФР, ОбластьШапка, "НомРег", 12);
			ПерсонифицированныйУчет.ВывестиДанныеПоБуквенно(ИНН, ОбластьШапка, "ИНН", 12);
			ПерсонифицированныйУчет.ВывестиДанныеПоБуквенно(Формат(ВыборкаПоШапкеДокумента.КПП,"ЧГ=0"), ОбластьШапка, "КПП", 9);
			ПерсонифицированныйУчет.ВывестиДанныеПоБуквенно(НомерДляПечати, ОбластьШапка, "НомСтр", 5);
		КонецЕсли;
		
		ОбластьШапка.Параметры.Исходная = ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.Исходная;
		ОбластьШапка.Параметры.Дополняющая = ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.Дополняющая;
		ОбластьШапка.Параметры.НазначениеПенсии = ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии;
		
		ОбластьШапка.Параметры.НаименованиеОрганизации = ВыборкаПоШапкеДокумента.НаименованиеОрганизации;
		ОбластьШапка.Параметры.Организация = ВыборкаПоШапкеДокумента.Организация;
		
		ТекущийЛист.Вывести(ОбластьШапка);
		
		ВыводимыеОбласти = Новый Массив;
		ВыводимыеОбласти.Добавить(ОбластьСтрокаСтажа);
		
		ВыборкаПоРаботникам.Сбросить();
		
		Если ВыборкаПоРаботникам.НайтиСледующий(Новый Структура("Ссылка", ВыборкаПоШапкеДокумента.Ссылка)) Тогда
			
			ВыборкаПоРаботникам.СледующийПоЗначениюПоля("Ссылка");
			НомерСтроки = 0;
			
			Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерСтроки") Цикл
				
				ОчиститьОбласть(ОбластьСтрокаСтажа);
				ЗаполнитьЗначенияСвойств(ОбластьСтрокаСтажа.Параметры, ВыборкаПоРаботникам);
				
				НомерСтроки = НомерСтроки + 1;
				ОбластьСтрокаСтажа.Параметры.НомерСтроки = СтрЗаменить(ВыборкаПоРаботникам.НомерСтроки, Символы.НПП, "") + ".";
				
				Если ВыборкаПоРаботникам.ЗаполненСтаж Тогда
					Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерОсновнойЗаписи") Цикл
						ВывестиСтрокуСтажа(ВыборкаПоРаботникам, ОбластьСтрокаСтажа, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
						Если ЗначениеЗаполнено(ВыборкаПоРаботникам.КодПозицииСпискаКод) Тогда 
							ЗаполнитьЗначенияСвойств(ОбластьСтрокаКодПозицииСписка.Параметры, ВыборкаПоРаботникам);
							ВывестиОбласть(ОбластьСтрокаКодПозицииСписка, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
						КонецЕсли;
						ОбластьСтрокаСтажа.Параметры.Фамилия = "";
						ОбластьСтрокаСтажа.Параметры.Имя = "";
						ОбластьСтрокаСтажа.Параметры.Отчество = "";
						ОбластьСтрокаСтажа.Параметры.СтраховойНомерПФР = "";
						ОбластьСтрокаСтажа.Параметры.НомерСтроки = "";
						Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерДополнительнойЗаписи") Цикл
							Если ВыборкаПоРаботникам.НомерДополнительнойЗаписи = 0 Тогда
								Продолжить;
							КонецЕсли;
							ВывестиСтрокуСтажа(ВыборкаПоРаботникам, ОбластьСтрокаСтажа, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
						КонецЦикла;	
					КонецЦикла;
				Иначе
					ВывестиОбласть(ОбластьСтрокаСтажа, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ОбластьПодвал.Параметры, ВыборкаПоШапкеДокумента);
		ВывестиОбласть(ОбластьПодвал, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
		ДокументРезультат.Вывести(ТекущийЛист);
		
	КонецЦикла;
	
	Возврат ДокументРезультат;
	
КонецФункции

Процедура ВывестиСтрокуСтажа(ВыборкаПоРаботникам, ОбластьСтрокаСтажа, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы)
	
	ЗаполнитьОбластьСтаж(ВыборкаПоРаботникам, ОбластьСтрокаСтажа);
	Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ЗамещениеГосударственныхМуниципальныхДолжностей)
		И Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) Тогда
		ОбластьСтрокаСтажа.Параметры.ДопИТС = ВыборкаПоРаботникам.ЗамещениеГосударственныхМуниципальныхДолжностей;
	КонецЕсли;
	
	ВывестиОбласть(ОбластьСтрокаСтажа, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
	
КонецПроцедуры

Функция ЗапросПоШапкеДляПечати(МассивСсылок)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	ОписаниеИсточникаДанных = ПерсонифицированныйУчет.ОписаниеИсточникаДанныхДляСоздатьВТСведенияОбОрганизациях();
	ОписаниеИсточникаДанных.ИмяТаблицы = "Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ";
	ОписаниеИсточникаДанных.ИмяПоляОрганизация = "Организация";
	ОписаниеИсточникаДанных.ИмяПоляПериод = "ОкончаниеОтчетногоПериода";
	ОписаниеИсточникаДанных.СписокСсылок = МассивСсылок;

	ПерсонифицированныйУчет.СоздатьВТСведенияОбОрганизацияхПоОписаниюДокументаИсточникаДанных(Запрос.МенеджерВременныхТаблиц, ОписаниеИсточникаДанных);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.Ссылка КАК Ссылка,
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.Организация,
	|	СведенияОбОрганизациях.НаименованиеСокращенное КАК НаименованиеОрганизации,
	|	СведенияОбОрганизациях.ИНН КАК ИНН,
	|	СведенияОбОрганизациях.КПП КАК КПП,
	|	СведенияОбОрганизациях.ЮридическоеФизическоеЛицо КАК ЮридическоеФизическоеЛицо,
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.Год,
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ОтчетныйПериод,
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ОкончаниеОтчетногоПериода,
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ТипСведений,
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.Руководитель КАК Руководитель,
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ДолжностьРуководителя КАК ДолжностьРуководителя,
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.Дата,
	|	СведенияОбОрганизациях.КодПоОКПО КАК КодПоОКПО,
	|	СведенияОбОрганизациях.ОГРН КАК ОГРН,
	|	СведенияОбОрганизациях.НаименованиеПолное КАК НаименованиеПолное,
	|	СведенияОбОрганизациях.НаименованиеСокращенное КАК НаименованиеСокращенное,
	|	СведенияОбОрганизациях.РегистрационныйНомерПФР,
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.НачисленыВзносыНаОПС,
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.НачисленыВзносыПоДТ,
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.Формат2019,
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ИмяФайлаДляПФР
	|ПОМЕСТИТЬ ВТДанныеДокументов
	|ИЗ
	|	Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ КАК СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСведенияОбОрганизациях КАК СведенияОбОрганизациях
	|		ПО СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.Организация = СведенияОбОрганизациях.Организация
	|			И СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ОкончаниеОтчетногоПериода = СведенияОбОрганизациях.Период
	|ГДЕ
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.Ссылка В(&МассивСсылок)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Руководитель";
	
	Запрос.Выполнить();
	
	ИменаПолейОтветственныхЛиц = Новый Массив;
	ИменаПолейОтветственныхЛиц.Добавить("Руководитель");
	
	УчетСтраховыхВзносов.СоздатьВТФИООтветственныхЛиц(Запрос.МенеджерВременныхТаблиц, Истина, ИменаПолейОтветственныхЛиц, "ВТДанныеДокументов");
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.РегистрационныйНомерПФР КАК РегистрационныйНомерПФР,
	|	ДанныеДокументов.НаименованиеОрганизации КАК НаименованиеОрганизации,
	|	ДанныеДокументов.Организация,
	|	ДанныеДокументов.ЮридическоеФизическоеЛицо,
	|	ДанныеДокументов.ИНН КАК ИНН,
	|	ДанныеДокументов.КПП КАК КПП,
	|	ДанныеДокументов.ОГРН КАК ОГРН,
	|	ДанныеДокументов.КодПоОКПО КАК ОКПО,
	|	ДанныеДокументов.НаименованиеПолное КАК НаименованиеПолное,
	|	ДанныеДокументов.НаименованиеСокращенное КАК НаименованиеСокращенное,
	|	ДанныеДокументов.Год КАК Год,
	|	ДанныеДокументов.ОтчетныйПериод,
	|	ДанныеДокументов.ТипСведений КАК ТипСведений,
	|	ДанныеДокументов.Руководитель КАК Руководитель,
	|	ДанныеДокументов.ДолжностьРуководителя КАК ДолжностьРуководителя,
	|	ДанныеДокументов.Дата,
	|	ВЫБОР
	|		КОГДА ДанныеДокументов.ТипСведений = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии)
	|			ТОГДА ДанныеДокументов.НачисленыВзносыНаОПС
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НачисленыВзносыНаОПС,
	|	ВЫБОР
	|		КОГДА ДанныеДокументов.ТипСведений = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии)
	|			ТОГДА ДанныеДокументов.НачисленыВзносыПоДТ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НачисленыВзносыПоДТ,
	|	ВЫБОР
	|		КОГДА ДанныеДокументов.ТипСведений = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии)
	|			ТОГДА НЕ ДанныеДокументов.НачисленыВзносыНаОПС
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеНачисленыВзносыНаОПС,
	|	ВЫБОР
	|		КОГДА ДанныеДокументов.ТипСведений = ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии)
	|			ТОГДА НЕ ДанныеДокументов.НачисленыВзносыПоДТ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеНачисленыВзносыПоДТ,
	|	ДанныеДокументов.Формат2019,
	|	ДанныеДокументов.ИмяФайлаДляПФР
	|ИЗ
	|	ВТДанныеДокументов КАК ДанныеДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ВТФИОПоследние
	|		ПО ДанныеДокументов.Ссылка = ВТФИОПоследние.Ссылка
	|			И ДанныеДокументов.Руководитель = ВТФИОПоследние.ФизическоеЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	Год УБЫВ,
	|	ТипСведений,
	|	Ссылка";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапросПоСотрудникамДляПечати(МассивСсылок) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.НомерСтроки КАК НомерСтроки,
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.СтраховойНомерПФР КАК СтраховойНомерПФР,
	|	Сотрудники.Фамилия КАК Фамилия,
	|	Сотрудники.Имя КАК Имя,
	|	Сотрудники.Отчество КАК Отчество,
	|	Сотрудники.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА Сотрудники.СотрудникУволен
	|			ТОГДА ""Х""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК СотрудникУволен
	|ПОМЕСТИТЬ ВТСотрудникиДокумента
	|ИЗ
	|	Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&МассивСсылок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаписиОСтаже.НомерОсновнойЗаписи КАК НомерОсновнойЗаписи,
	|	ЗаписиОСтаже.НомерДополнительнойЗаписи КАК НомерДополнительнойЗаписи,
	|	ЗаписиОСтаже.ДатаНачалаПериода КАК ДатаНачалаПериода,
	|	ЗаписиОСтаже.ДатаОкончанияПериода КАК ДатаОкончанияПериода,
	|	ЗаписиОСтаже.ТерриториальныеУсловия КАК ТерриториальныеУсловия,
	|	ЗаписиОСтаже.ТерриториальныеУсловия.Код КАК ТерриториальныеУсловияКод,
	|	ЗаписиОСтаже.ПараметрТерриториальныхУсловий КАК ПараметрТерриториальныхУсловий,
	|	ЗаписиОСтаже.ПараметрТерриториальныхУсловий КАК ТерриториальныеУсловияСтавка,
	|	ЗаписиОСтаже.ОсобыеУсловияТруда КАК ОсобыеУсловияТруда,
	|	ЗаписиОСтаже.ОсобыеУсловияТруда.Код КАК ОсобыеУсловияТрудаКод,
	|	ЗаписиОСтаже.КодПозицииСписка.Код КАК КодПозицииСпискаКод,
	|	ЗаписиОСтаже.ОснованиеИсчисляемогоСтажа КАК ОснованиеИсчисляемогоСтажа,
	|	ЗаписиОСтаже.ОснованиеИсчисляемогоСтажа.Код КАК ОснованиеИсчисляемогоСтажаКод,
	|	ЗаписиОСтаже.ПервыйПараметрИсчисляемогоСтажа КАК ПервыйПараметрИсчисляемогоСтажа,
	|	ЗаписиОСтаже.ВторойПараметрИсчисляемогоСтажа КАК ВторойПараметрИсчисляемогоСтажа,
	|	ЗаписиОСтаже.ТретийПараметрИсчисляемогоСтажа КАК ТретийПараметрИсчисляемогоСтажа,
	|	ЗаписиОСтаже.ТретийПараметрИсчисляемогоСтажа.Код КАК ТретийПараметрИсчисляемогоСтажаКод,
	|	ЗаписиОСтаже.ОснованиеВыслугиЛет КАК ОснованиеВыслугиЛет,
	|	ЗаписиОСтаже.ОснованиеВыслугиЛет.Код КАК ОснованиеВыслугиЛетКод,
	|	ЗаписиОСтаже.ПервыйПараметрВыслугиЛет КАК ПервыйПараметрВыслугиЛет,
	|	ЗаписиОСтаже.ВторойПараметрВыслугиЛет КАК ВторойПараметрВыслугиЛет,
	|	ЗаписиОСтаже.ТретийПараметрВыслугиЛет КАК ТретийПараметрВыслугиЛет,
	|	ЗаписиОСтаже.ЗамещениеГосударственныхМуниципальныхДолжностей КАК ЗамещениеГосударственныхМуниципальныхДолжностей,
	|	СотрудникиДокумента.НомерСтроки КАК НомерСтроки,
	|	СотрудникиДокумента.Сотрудник КАК Сотрудник,
	|	СотрудникиДокумента.СтраховойНомерПФР КАК СтраховойНомерПФР,
	|	СотрудникиДокумента.Фамилия КАК Фамилия,
	|	СотрудникиДокумента.Имя КАК Имя,
	|	СотрудникиДокумента.Отчество КАК Отчество,
	|	СотрудникиДокумента.СотрудникУволен КАК СотрудникУволен,
	|	СотрудникиДокумента.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ЗаписиОСтаже.НомерОсновнойЗаписи ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЗаполненСтаж
	|ИЗ
	|	ВТСотрудникиДокумента КАК СотрудникиДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ЗаписиОСтаже КАК ЗаписиОСтаже
	|		ПО СотрудникиДокумента.Сотрудник = ЗаписиОСтаже.Сотрудник
	|			И СотрудникиДокумента.Ссылка = ЗаписиОСтаже.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	СотрудникиДокумента.Ссылка.Год УБЫВ,
	|	СотрудникиДокумента.Ссылка.ТипСведений,
	|	Ссылка,
	|	НомерСтроки,
	|	НомерОсновнойЗаписи,
	|	НомерДополнительнойЗаписи";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Процедура ВывестиОбласть(ВыводимаяОбласть, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы)
	
	ВыводимыеОбласти = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыводимаяОбласть);
	Если ТекущийЛист.ПроверитьВывод(ВыводимыеОбласти) Тогда 
		ТекущийЛист.Вывести(ВыводимаяОбласть);
	Иначе 
		ДокументРезультат.Вывести(ТекущийЛист);
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		ТекущийЛист = НовыйПечатныйЛист(ВыборкаПоШапкеДокумента, ДокументРезультат, Макет, НомерСтраницы);
		ТекущийЛист.Вывести(ВыводимаяОбласть);
	КонецЕсли;	
	
КонецПроцедуры 

Функция НовыйПечатныйЛист(ВыборкаПоШапкеДокумента, ДокументРезультат, Макет, НомерСтраницы)
	
	ТекущийЛист = Новый ТабличныйДокумент;
	ТекущийЛист.ПолеСлева = 0;
	ТекущийЛист.ПолеСправа = 0;
	ТекущийЛист.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;
	
	Если ВыборкаПоШапкеДокумента.Формат2019 Тогда 
		Возврат ТекущийЛист;
	КонецЕсли;
	
	ОбластьКолонтитул1 = Макет.ПолучитьОбласть("R2C1:R2C24");
	ОбластьКолонтитул2 = Макет.ПолучитьОбласть("R3C1:R4C27");
	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	РегистрационныйНомерПФР = СтрЗаменить(ВыборкаПоШапкеДокумента.РегистрационныйНомерПФР," ","");
	РегистрационныйНомерПФР = СтрЗаменить(РегистрационныйНомерПФР,"-","");
	ПерсонифицированныйУчет.ВывестиДанныеПоБуквенно(РегистрационныйНомерПФР, ОбластьКолонтитул1, "НомРег", 12);
	
	НомерСтраницы = НомерСтраницы + 1;
	НомерДляПечати = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(НомерСтраницы, 5);
	ПерсонифицированныйУчет.ВывестиДанныеПоБуквенно(НомерДляПечати, ОбластьКолонтитул1, "НомСтр", 5);
	
	ИНН = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ВыборкаПоШапкеДокумента.ИНН, 12, "-", "Справа");
	ПерсонифицированныйУчет.ВывестиДанныеПоБуквенно(ИНН, ОбластьКолонтитул2, "ИНН", 12);
	
	ПерсонифицированныйУчет.ВывестиДанныеПоБуквенно(Формат(ВыборкаПоШапкеДокумента.КПП,"ЧГ=0"), ОбластьКолонтитул2, "КПП", 9);
	
	ТекущийЛист.Вывести(ОбластьКолонтитул1);
	ТекущийЛист.Вывести(ОбластьКолонтитул2);
	ТекущийЛист.Вывести(ОбластьПустаяСтрока);
	
	Возврат ТекущийЛист;
	
КонецФункции

Процедура ЗаполнитьОбластьСтаж(ВыборкаЗаписейСтажа, ОбластьСтаж)
	
	ОбластьСтаж.Параметры.ДатаНачалаПериода = Формат(ВыборкаЗаписейСтажа.ДатаНачалаПериода, "ДФ=""дд.ММ.гггг""");
	ОбластьСтаж.Параметры.ДатаОкончанияПериода = Формат(ВыборкаЗаписейСтажа.ДатаОкончанияПериода, "ДФ=""дд.ММ.гггг""");
	
	ОбластьСтаж.Параметры.ТерриториальныеУсловияКод = ВыборкаЗаписейСтажа.ТерриториальныеУсловияКод;
	Если ЗначениеЗаполнено(ВыборкаЗаписейСтажа.ТерриториальныеУсловияСтавка) Тогда 
		ОбластьСтаж.Параметры.ТерриториальныеУсловияКод = Строка(ВыборкаЗаписейСтажа.ТерриториальныеУсловияКод) + " " + ВыборкаЗаписейСтажа.ТерриториальныеУсловияСтавка;
	КонецЕсли;
	ОбластьСтаж.Параметры.ОсобыеУсловияТрудаКод = ВыборкаЗаписейСтажа.ОсобыеУсловияТрудаКод;
	ОбластьСтаж.Параметры.ОснованиеИсчисляемогоСтажаКод = ВыборкаЗаписейСтажа.ОснованиеИсчисляемогоСтажаКод;
	ОбластьСтаж.Параметры.ОснованиеВыслугиЛетКод = ВыборкаЗаписейСтажа.ОснованиеВыслугиЛетКод;
	ОбластьСтаж.Параметры.ДопИТС = ПерсонифицированныйУчет.ПредставлениеПараметровИсчисляемогоТрудовогоСтажа(ВыборкаЗаписейСтажа);
	
	ПерсонифицированныйУчет.ПредставлениеПараметровПенсииЗаВыслугуЛет(ВыборкаЗаписейСтажа, ОбластьСтаж.Параметры.ДопВЛ, ОбластьСтаж.Параметры.ДопВЛСтавка);
	
КонецПроцедуры

Процедура ОчиститьОбласть(ОбластьМакета)
	
	Для Счетчик = 0 По ОбластьМакета.Параметры.Количество() - 1 Цикл
		ОбластьМакета.Параметры[Счетчик] = ""
	КонецЦикла;
	
КонецПроцедуры


Процедура ПеренестиДолжностьИРуководителя(Параметры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.РуководительУдалить.Наименование, """") КАК Руководитель,
	|	ЕСТЬNULL(СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ДолжностьРуководителяУдалить.Наименование, """") КАК ДолжностьРуководителя
	|ИЗ
	|	Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ КАК СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокОбъект.Руководитель = Выборка.Руководитель;
		ДокОбъект.ДолжностьРуководителя = Выборка.ДолжностьРуководителя;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокОбъект, Ложь, Ложь);
	КонецЦикла;
	
	Если Параметры<>Неопределено Тогда
		Параметры.ОбработкаЗавершена = Истина;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли