#Область ОписаниеПеременных

&НаКлиенте
Перем СтарыеЗначенияКонтролируемыхПолейВзносы;

&НаКлиенте
Перем СтарыеЗначенияКонтролируемыхПолейСтаж;

&НаКлиенте
Перем СтарыеЗначенияКонтролируемыхПолейЗаработок;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПерсонифицированныйУчет.ХраненияОшибокВДанныхФормыДополнитьФорму(ЭтотОбъект);
	ПерсонифицированныйУчет.ИндикацияОшибокДополнитьФорму(ЭтотОбъект, ОписаниеЭлементовСИндикациейОшибок());
	
	УстановитьКраткоеОтображениеКатегорииВТаблицах(ЭтотОбъект, "КатегорияЗастрахованныхЛиц", "КатегорияЗастрахованныхЛиц");
	
	РедактируемыйДокументСсылка = Параметры.РедактируемыйДокументСсылка;
	ТипСведений = Параметры.ТипСведений;
	Организация = Параметры.Организация;
	Период = Параметры.Период;
	ОкончаниеПериода = ПерсонифицированныйУчетКлиентСервер.ОкончаниеОтчетногоПериодаСтажаПерсУчета(Период);
	Сотрудник = Параметры.Сотрудник;
	ОтменаИсправленийДоступна = Параметры.ОтменаИсправленийДоступна;
	ИзменениеБезОграничений = Параметры.ИзменениеБезОграничений;
	ЧтениеБезОграничений = Параметры.ЧтениеБезОграничений;
	
	ДобавитьКонтрольИсправлений();
	УстановитьСписокВыбораМесяцев();
	
	ИменаДополняемыхТаблиц = Новый Массив;
	ИменаДополняемыхТаблиц.Добавить("СведенияОЗаработке");
	ИменаДополняемыхТаблиц.Добавить("ЗаписиОСтаже");
	ДобавитьЗаголовкиКПолямТаблицФормы(ЭтаФорма, ИменаДополняемыхТаблиц, ОписаниеКолонокЗаголовковТаблицФормы());
	
	ДанныеИзВременногоХранилищаВДанныеФормы(Параметры.АдресВоВременномХранилище);
	
	УстановитьЗаголовокКарточки();
	
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)	
	
	СтруктураДанныхПоСотруднику = СтруктураДанныхПоСотруднику(ЭтотОбъект);	
	
	ПерсонифицированныйУчетКлиент.ДокументыСЗВУстановитьЗначенияКонтролируемыхПолей(
		СтруктураДанныхПоСотруднику, 
		КонтролируемыеПоля, 
		СтарыеЗначенияКонтролируемыхПолейВзносы);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОтмененаИсправления" Тогда
		ДанныеИзВременногоХранилищаВДанныеФормы(Параметр.АдресХранилища);
		Активизировать();
	ИначеЕсли ИмяСобытия = "ИзменениеДанныхФизическогоЛица" И Не ТолькоПросмотр Тогда 
		ЗаполнитьИзменившиесяДанныеФизическогоЛица(Параметр);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФамилияПриИзменении(Элемент)
	
	ПерсонифицированныйУчетКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Фамилия");
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяПриИзменении(Элемент)
	
	ПерсонифицированныйУчетКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Имя");
	
КонецПроцедуры

&НаКлиенте
Процедура СтраховойНомерПФРПриИзменении(Элемент)
	
	ПерсонифицированныйУчетКлиентСервер.УстановитьФорматСтраховогоНомераПФР(СтраховойНомерПФР);
	ПерсонифицированныйУчетКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "СтраховойНомерПФР");
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОтчествоПриИзменении(Элемент)
	
	ПерсонифицированныйУчетКлиент.ПриИзмененииДанныхВЭлементеСФлагомИндикацииОшибок(ЭтотОбъект, Элемент, "Отчество");
	
КонецПроцедуры

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗаписиОСтаже

&НаКлиенте
Процедура ЗаписиОСтажеПриАктивизацииСтроки(Элемент)
	
	СтруктураДанныхПоСотруднику = СтруктураДанныхПоСотруднику(ЭтотОбъект);
	
	ПерсонифицированныйУчетКлиент.ДокументыСЗВЗаписиОСтажеПриАктивацииСтроки(
		Элементы.ЗаписиОСтаже.ТекущиеДанные, 
		КонтролируемыеПоля, 
		СтарыеЗначенияКонтролируемыхПолейСтаж, 
		СтруктураДанныхПоСотруднику);	
			
КонецПроцедуры

&НаКлиенте
Процедура ЗаписиОСтажеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.ЗаписиОСтаже.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		УстановитьЗаголовкиВСтрокеТаблицы(
			ЭтотОбъект, 
			ТекущиеДанные,
			ОписаниеКолонокЗаголовковТаблицыЗаписиОСтаже(),
			Истина);
	КонецЕсли;
	
	Если НоваяСтрока И Не Копирование Тогда 
		ТекущиеДанные.ОснованиеВыслугиЛет = ?(Период >= '20020101', 
			ПредопределенноеЗначение("Справочник.ОснованияДосрочногоНазначенияПенсии.ПустаяСсылка"),
			ПредопределенноеЗначение("Справочник.ОснованияДосрочногоНазначенияПенсииДляСЗВК.ПустаяСсылка"));
		ТекущиеДанные.ОсобыеУсловияТруда = ?(Период >= '20020101', 
			ПредопределенноеЗначение("Справочник.ОсобыеУсловияТрудаПФР.ПустаяСсылка"),
			ПредопределенноеЗначение("Справочник.ОсобыеУсловияТрудаДляСЗВКПФР.ПустаяСсылка"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписиОСтажеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанныхПоСотруднику = СтруктураДанныхПоСотруднику(ЭтотОбъект);
	
	ПерсонифицированныйУчетКлиент.ДокументыСЗВЗаписиОСтажеПриОкончанииРедактирования(
		Элементы.ЗаписиОСтаже.ТекущиеДанные, 
		НоваяСтрока, 
		КонтролируемыеПоля, 
		СтарыеЗначенияКонтролируемыхПолейСтаж, 
		СтруктураДанныхПоСотруднику);
			
	ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураДанныхПоСотруднику);	
	
	Если НоваяСтрока Тогда
		ПерсонифицированныйУчетКлиентСервер.ВыполнитьНумерациюЗаписейОСтаже(ЗаписиОСтаже);
	КонецЕсли;
	
	ПерсонифицированныйУчетКлиент.ПриОкончанииРедактированияСтрокиТаблицыСИндикациейОшибок(
		ЭтотОбъект,
		Элемент,
		НоваяСтрока,
		"ЗаписиОСтаже");
		
КонецПроцедуры
	
&НаКлиенте
Процедура ЗаписиОСтажеПередУдалением(Элемент, Отказ)
	
	ПерсонифицированныйУчетКлиент.ПередУдалениемСтрокиТаблицыСИндикациейОшибок(ЭтотОбъект, Элемент, "ЗаписиОСтаже");
	
КонецПроцедуры	

&НаКлиенте
Процедура ЗаписиОСтажеПослеУдаления(Элемент)
	
	ФиксСтаж = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСведенияОЗаработке

&НаКлиенте
Процедура СведенияОЗаработкеПриАктивизацииСтроки(Элемент)
	
	СтруктураДанныхПоСотруднику = СтруктураДанныхПоСотруднику(ЭтотОбъект);
			
	ДанныеТекущейСтроки = Элементы.СведенияОЗаработке.ТекущиеДанные;	
		
	Если ДанныеТекущейСтроки <> Неопределено И КонтролируемыеПоля <> Неопределено Тогда
		ПерсонифицированныйУчетКлиент.ДокументыСЗВКонтрольИсправленийПриАктивацииСтроки(
			ДанныеТекущейСтроки, 
			КонтролируемыеПоля, 
			СтарыеЗначенияКонтролируемыхПолейЗаработок, 
			СтруктураДанныхПоСотруднику);													 
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СведенияОЗаработкеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		УстановитьЗаголовкиВСтрокеТаблицы(ЭтаФорма, Элемент.ТекущиеДанные, ОписаниеКолонокЗаголовковТаблицы("СведенияОЗаработке"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СведенияОЗаработкеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанныхПоСотруднику = СтруктураДанныхПоСотруднику(ЭтотОбъект);
		
	ДанныеТекущейСтроки = Элементы.СведенияОЗаработке.ТекущиеДанные;	
	
	ПерсонифицированныйУчетКлиент.ДокументыСЗВКонтрольИсправленийПриОкончанииРедактированияСтроки(
		ДанныеТекущейСтроки, 
		КонтролируемыеПоля, 
		СтарыеЗначенияКонтролируемыхПолейЗаработок,
		СтруктураДанныхПоСотруднику);	
		
	ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураДанныхПоСотруднику);
	
	СпозиционироватьСтроку(СведенияОЗаработке, ДанныеТекущейСтроки);
	
	ПерсонифицированныйУчетКлиент.ПриОкончанииРедактированияСтрокиТаблицыСИндикациейОшибок(
		ЭтотОбъект,
		Элемент,
		НоваяСтрока,
		"СведенияОЗаработке");
		
КонецПроцедуры
	
&НаКлиенте
Процедура СведенияОЗаработкеПередУдалением(Элемент, Отказ)
	
	ПерсонифицированныйУчетКлиент.ПередУдалениемСтрокиТаблицыСИндикациейОшибок(ЭтотОбъект, Элемент, "СведенияОЗаработке");
	
КонецПроцедуры	

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСведенияОЗаработкеНаВредныхИТяжелыхРаботах

&НаКлиенте
Процедура СведенияОЗаработкеНаВредныхИТяжелыхРаботахПриАктивизацииСтроки(Элемент)
	
	СтруктураДанныхПоСотруднику = СтруктураДанныхПоСотруднику(ЭтотОбъект);
			
	ДанныеТекущейСтроки = Элементы.СведенияОЗаработкеНаВредныхИТяжелыхРаботах.ТекущиеДанные;	
		
	Если ДанныеТекущейСтроки <> Неопределено И КонтролируемыеПоля <> Неопределено Тогда
		ПерсонифицированныйУчетКлиент.ДокументыСЗВКонтрольИсправленийПриАктивацииСтроки(
			ДанныеТекущейСтроки, 
			КонтролируемыеПоля, 
			СтарыеЗначенияКонтролируемыхПолейЗаработок, 
			СтруктураДанныхПоСотруднику);													 
	КонецЕсли;		
				
КонецПроцедуры

&НаКлиенте
Процедура СведенияОЗаработкеНаВредныхИТяжелыхРаботахПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	СтруктураДанныхПоСотруднику = СтруктураДанныхПоСотруднику(ЭтотОбъект);
	
	ДанныеТекущейСтроки = Элементы.СведенияОЗаработкеНаВредныхИТяжелыхРаботах.ТекущиеДанные;	
		
	ПерсонифицированныйУчетКлиент.ДокументыСЗВКонтрольИсправленийПриОкончанииРедактированияСтроки(
		ДанныеТекущейСтроки, 
		КонтролируемыеПоля, 
		СтарыеЗначенияКонтролируемыхПолейЗаработок,
		СтруктураДанныхПоСотруднику);	
		
	ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураДанныхПоСотруднику);

	СпозиционироватьСтроку(СведенияОЗаработкеНаВредныхИТяжелыхРаботах, ДанныеТекущейСтроки, "КлассУсловийТруда");
	
КонецПроцедуры

&НаКлиенте
Процедура СведенияОЗаработкеНаВредныхИТяжелыхРаботахПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеТекущейСтроки = Элементы.СведенияОЗаработкеНаВредныхИТяжелыхРаботах.ТекущиеДанные;	
	СпозиционироватьСтроку(СведенияОЗаработкеНаВредныхИТяжелыхРаботах, ДанныеТекущейСтроки);
	
	ПерсонифицированныйУчетКлиент.ПриОкончанииРедактированияСтрокиТаблицыСИндикациейОшибок(
		ЭтотОбъект,
		Элемент,
		НоваяСтрока,
		"СведенияОЗаработкеНаВредныхИТяжелыхРаботах");
		
КонецПроцедуры
	
&НаКлиенте
Процедура СведенияОЗаработкеНаВредныхИТяжелыхРаботахПередУдалением(Элемент, Отказ)
	
	ПерсонифицированныйУчетКлиент.ПередУдалениемСтрокиТаблицыСИндикациейОшибок(ЭтотОбъект, Элемент, "СведенияОЗаработкеНаВредныхИТяжелыхРаботах");
	
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеРаздела66

&НаКлиенте
Процедура КорректирующиеСведенияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ПерсонифицированныйУчетКлиент.ПриОкончанииРедактированияСтрокиТаблицыСИндикациейОшибок(
		ЭтотОбъект,
		Элемент,
		НоваяСтрока,
		"КорректирующиеСведения");
		
КонецПроцедуры

&НаКлиенте
Процедура КорректирующиеСведенияПередУдалением(Элемент, Отказ)
	
	ПерсонифицированныйУчетКлиент.ПередУдалениемСтрокиТаблицыСИндикациейОшибок(ЭтотОбъект, Элемент, "КорректирующиеСведения");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьИсправленияПоВзносам(Команда)
	
	ФиксНачисленныеВзносы = Ложь;
	ФиксЗаработок = Ложь;
	Модифицированность = Истина;
	
	АдресВоВременномХранилище = ДанныеСотрудникаВоВременноеХранилище();
	ВладелецФормы.ОтменитьИсправленияПоВзносам(АдресВоВременномХранилище, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьИсправленияПоСтажу(Команда)
	
	ФиксСтаж = Ложь;
	ЗаписиОСтаже.Очистить();
	
	Модифицированность = Истина;
	АдресВоВременномХранилище = ДанныеСотрудникаВоВременноеХранилище();
	ВладелецФормы.ОтменитьИсправленияПоСтажу(АдресВоВременномХранилище, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьДанныеФизическогоЛица(Команда)
	
	ПараметрыФормы = Новый Структура("Ключ, АктивнаяСтраница, РедактируемоеПоле", Сотрудник, "ЛичныеДанные");
	ОткрытьФорму("Справочник.Сотрудники.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция  СоответствиеДанныхОбъектаДаннымФормы()
	
	ОписаниеСвязиДанных = ПерсонифицированныйУчет.ОписаниеСвязиДанных();
	
	ПерсонифицированныйУчет.ДобавитьОписаниеСвязиДанныхСтрокТаблиц(
		ОписаниеСвязиДанных,
		"ЗаписиОСтаже",
		"*",
		"ЗаписиОСтаже",
		"");
		
	ПерсонифицированныйУчет.ДобавитьОписаниеСвязиДанныхСтрокТаблиц(
		ОписаниеСвязиДанных,
		"СведенияОЗаработке",
		"*",
		"СведенияОЗаработке",
		"");
		
	ПерсонифицированныйУчет.ДобавитьОписаниеСвязиДанныхСтрокТаблиц(
		ОписаниеСвязиДанных,
		"СведенияОЗаработкеНаВредныхИТяжелыхРаботах",
		"*",
		"СведенияОЗаработкеНаВредныхИТяжелыхРаботах",
		"");
		
	ПерсонифицированныйУчет.ДобавитьОписаниеСвязиДанныхСтрокТаблиц(
		ОписаниеСвязиДанных,
		"КорректирующиеСведения",
		"*",
		"КорректирующиеСведения",
		"");
		
	ПерсонифицированныйУчет.ДобавитьОписаниеСвязиДанныхСтрокТаблиц(
		ОписаниеСвязиДанных,
		"СведенияОбУплаченныхВзносах",
		"*",
		"СведенияОбУплаченныхВзносах",
		"");
			
	ПерсонифицированныйУчет.ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, "", "Сотрудник");	
	
	Возврат ОписаниеСвязиДанных;
	
КонецФункции	

&НаКлиенте
Процедура ВыбратьИЗакрыть(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	АдресВоВременномХранилище = ДанныеСотрудникаВоВременноеХранилище();
	
	ПараметрыОповещения = Новый Структура("РедактируемыйДокументСсылка, Сотрудник, АдресВоВременномХранилище");
	ПараметрыОповещения.РедактируемыйДокументСсылка = РедактируемыйДокументСсылка;
	ПараметрыОповещения.Сотрудник = Сотрудник;
	ПараметрыОповещения.АдресВоВременномХранилище = АдресВоВременномХранилище;	
	
	Оповестить("ИзмененыДанныеСотрудникаСЗВ_КОРР", ПараметрыОповещения, ЭтаФорма);
	
	Модифицированность = Ложь;

	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	
	ДоступностьИсправленийВзносов = ФиксНачисленныеВзносы Или ФиксЗаработок;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗаписиОСтажеОтменитьИсправленияПоСтажу", "Доступность", ФиксСтаж);
	
	УстановитьВидимостьКолонокЗаголовков(ЭтаФорма, "СведенияОЗаработке", ОписаниеКолонокЗаголовковТаблицы("СведенияОЗаработке"));
	
	Если Не ОтменаИсправленийДоступна Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗаписиОСтажеОтменитьИсправленияПоСтажу", "Видимость", Ложь);
	КонецЕсли;	
	
	ОтчетныйГод = Год(Период);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НомерДоговора", "Видимость", ОтчетныйГод <= 2001);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДатаДоговора", "Видимость", ОтчетныйГод <= 2001);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДополнительныйТариф", "Видимость", ОтчетныйГод >= 2000 И ОтчетныйГод < 2017);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КатегорияЗастрахованныхЛиц", "Видимость", ОтчетныйГод < 2017);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТипДоговора", "Видимость", ОтчетныйГод = 2010 Или ОтчетныйГод = 2013);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗаработокСтраница", "Видимость", ЧтениеБезОграничений И (ОтчетныйГод <= 2001 Или (ОтчетныйГод >= 2010 И ОтчетныйГод < 2017)));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СведенияОЗаработкеЗаработок", "Доступность", ОтчетныйГод <= 2001 Или ОтчетныйГод >= 2010);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СведенияОЗаработкеОблагаетсяВзносамиДоПредельнойВеличины", "Доступность", ОтчетныйГод <= 2001 Или ОтчетныйГод >= 2010);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СведенияОЗаработкеПоДоговорамГПХДоПредельнойВеличины", "Доступность", ОтчетныйГод >= 2010);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СведенияОЗаработкеОблагаетсяВзносамиСвышеПредельнойВеличины", "Доступность", ОтчетныйГод >= 2010);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СведенияОЗаработкеПоДоговорамГПХСвышеПредельнойВеличины", "Доступность", ОтчетныйГод >= 2010);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КорректирующиеСведенияУплачиваемыеСтрахователем", "Видимость", ОтчетныйГод <= 2001);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КорректирующиеСведенияУплачиваемыеИзЗаработкаЗЛ", "Видимость", ОтчетныйГод <= 2000);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КорректирующиеСведенияДоначисленоНаОПС", "Видимость", ОтчетныйГод >= 2014);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КорректирующиеСведенияДоначисленоСтраховая", "Видимость", ОтчетныйГод >= 2002);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КорректирующиеСведенияДоначисленоНакопительная", "Видимость", ОтчетныйГод >= 2002 И ОтчетныйГод <= 2013);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДоначисленныеВзносыСтраница", "Видимость", ЧтениеБезОграничений И ОтчетныйГод < 2017);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СведенияОбУплаченныхВзносахСтраница", "Видимость", ЧтениеБезОграничений И ОтчетныйГод >= 2010 И ОтчетныйГод <= 2013);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗаработокВредныеУсловияСтраница", "Видимость", ЧтениеБезОграничений И ОтчетныйГод < 2017);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗаработокСтраница", "ТолькоПросмотр", Не ИзменениеБезОграничений);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДоначисленныеВзносыСтраница", "ТолькоПросмотр", Не ИзменениеБезОграничений);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СведенияОбУплаченныхВзносахСтраница", "ТолькоПросмотр", Не ИзменениеБезОграничений);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗаработокВредныеУсловияСтраница", "ТолькоПросмотр", Не ИзменениеБезОграничений);
	
	Если ОтчетныйГод >= 2014 Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КорректирующиеСведенияДоначисленоСтраховая", "Заголовок", НСтр("ru = 'По дополнительному тарифу'"));
	КонецЕсли;
	
	Если ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Отменяющая Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Страницы", "Видимость", Ложь);
	КонецЕсли;
	
	ИспользоватьЗамещение = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы.ПерсонифицированныйУчетРасширенная") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("ПерсонифицированныйУчетРасширенный");
	    ИспользоватьЗамещение = Модуль.ИспользоватьЗамещениеГосударственныхМуниципальныхДолжностей();
	КонецЕсли;                                                              
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗаписиОСтажеЗамещениеГосударственныхМуниципальныхДолжностей", "Видимость", ИспользоватьЗамещение И ОтчетныйГод >= 2017);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗаписиОСтажеЗамещениеГосударственныхМуниципальныхДолжностейЗаголовок", "Видимость", ИспользоватьЗамещение И ОтчетныйГод >= 2017);
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьЗаголовокКарточки()
	
	ИнфонадписьЗаголовокКарточки = НСтр("ru = 'Данные застрахованного лица'");
	
КонецПроцедуры	

&НаСервере
Процедура ДанныеИзВременногоХранилищаВДанныеФормы(АдресВоВременномХранилище)
	
	Модифицированность = Ложь;
	
	ДанныеДокументаПоСотруднику = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	
	Если ДанныеДокументаПоСотруднику = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Не удалось получить редактируемые данные.'");	
	КонецЕсли;
	
	Сотрудник = ДанныеДокументаПоСотруднику.Сотрудник;
	Фамилия = ДанныеДокументаПоСотруднику.Фамилия;
	Имя = ДанныеДокументаПоСотруднику.Имя;
	Отчество = ДанныеДокументаПоСотруднику.Отчество;
	СотрудникУволен = ДанныеДокументаПоСотруднику.СотрудникУволен;
	СтраховойНомерПФР = ДанныеДокументаПоСотруднику.СтраховойНомерПФР;
	Заработок = ДанныеДокументаПоСотруднику.Заработок;
	Доначислено = ДанныеДокументаПоСотруднику.Доначислено;
	Уплачено = ДанныеДокументаПоСотруднику.Уплачено;
	ТипДоговора = ДанныеДокументаПоСотруднику.ТипДоговора;
	КатегорияЗастрахованныхЛиц = ДанныеДокументаПоСотруднику.КатегорияЗастрахованныхЛиц;
	НомерДоговора = ДанныеДокументаПоСотруднику.НомерДоговора;
	ДатаДоговора = ДанныеДокументаПоСотруднику.ДатаДоговора;
	ДополнительныйТариф = ДанныеДокументаПоСотруднику.ДополнительныйТариф;
	ФиксНачисленныеВзносы = ДанныеДокументаПоСотруднику.ФиксНачисленныеВзносы;
	ФиксСтаж = ДанныеДокументаПоСотруднику.ФиксСтаж;
	ФиксЗаработок = ДанныеДокументаПоСотруднику.ФиксЗаработок;
	
	ЗаписиОСтаже.Очистить();
	Для Каждого СтрокаТаблицы Из ДанныеДокументаПоСотруднику.ЗаписиОСтаже Цикл
		СтрокаТаблицыФормы = ЗаписиОСтаже.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыФормы, СтрокаТаблицы);
	КонецЦикла;	
	
	КорректирующиеСведения.Очистить();
	Для Каждого СтрокаТаблицы Из ДанныеДокументаПоСотруднику.КорректирующиеСведения Цикл
		СтрокаТаблицыФормы = КорректирующиеСведения.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыФормы, СтрокаТаблицы);
	КонецЦикла;	

	СведенияОЗаработке.Очистить();
	Для Каждого СтрокаЗаработок Из ДанныеДокументаПоСотруднику.СведенияОЗаработке Цикл
		СтрокаТаблицы = СведенияОЗаработке.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаЗаработок);
		СпозиционироватьСтроку(СведенияОЗаработке, СтрокаТаблицы);
	КонецЦикла;
	
	СведенияОЗаработкеНаВредныхИТяжелыхРаботах.Очистить();
	Для Каждого СтрокаЗаработок Из ДанныеДокументаПоСотруднику.СведенияОЗаработкеНаВредныхИТяжелыхРаботах Цикл
		СтрокаТаблицы = СведенияОЗаработкеНаВредныхИТяжелыхРаботах.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаЗаработок);
		СпозиционироватьСтроку(СведенияОЗаработкеНаВредныхИТяжелыхРаботах, СтрокаТаблицы, "КлассУсловийТруда");
	КонецЦикла;	
	
	СведенияОбУплаченныхВзносах.Очистить();
	Для Каждого СтрокаТаблицы Из ДанныеДокументаПоСотруднику.СведенияОбУплаченныхВзносах Цикл
		СтрокаТаблицыФормы = СведенияОбУплаченныхВзносах.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыФормы, СтрокаТаблицы);
	КонецЦикла;	
	
	ПерсонифицированныйУчет.УстановитьЗаголовкиВТаблице(ЭтаФорма, СведенияОЗаработке, ОписаниеКолонокЗаголовковТаблицы("СведенияОЗаработке"));
	ПерсонифицированныйУчет.УстановитьЗаголовкиВТаблице(ЭтаФорма, ЗаписиОСтаже, ОписаниеКолонокЗаголовковТаблицы("ЗаписиОСтаже"));
	
	Ошибки = Неопределено;
	ДанныеДокументаПоСотруднику.Свойство("Ошибки", Ошибки);
	
	Если Ошибки <> Неопределено Тогда 
		ПерсонифицированныйУчет.ПреобразоватьПутиКДаннымВСообщенияхПользователю(
			ЭтотОбъект,
			ЭтотОбъект,
			Ошибки,
			СоответствиеДанныхОбъектаДаннымФормы());
		ПерсонифицированныйУчет.ПоместитьСообщенияОбОшибкахВДанныеФормы(ЭтотОбъект, Ошибки);	
		ПерсонифицированныйУчет.УстановитьФлагиНаличияОшибокПоСообщениямОбОшибках(ЭтотОбъект, Ошибки, , Истина);	
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере 
Функция ДанныеСотрудникаВоВременноеХранилище()	
	
	ДанныеСотрудника = Новый Структура;
	ДанныеСотрудника.Вставить("Сотрудник", Сотрудник);
	ДанныеСотрудника.Вставить("СтраховойНомерПФР", СтраховойНомерПФР);
	ДанныеСотрудника.Вставить("Фамилия", Фамилия);
	ДанныеСотрудника.Вставить("Имя", Имя);
	ДанныеСотрудника.Вставить("Отчество", Отчество);
	ДанныеСотрудника.Вставить("ТипДоговора", ТипДоговора);
	ДанныеСотрудника.Вставить("КатегорияЗастрахованныхЛиц", КатегорияЗастрахованныхЛиц);
	ДанныеСотрудника.Вставить("НомерДоговора", НомерДоговора);
	ДанныеСотрудника.Вставить("ДатаДоговора", ДатаДоговора);
	ДанныеСотрудника.Вставить("ДополнительныйТариф", ДополнительныйТариф);
	ДанныеСотрудника.Вставить("ФиксНачисленныеВзносы", ФиксНачисленныеВзносы);
	ДанныеСотрудника.Вставить("ФиксСтаж", ФиксСтаж);
	ДанныеСотрудника.Вставить("ФиксЗаработок", ФиксЗаработок);
	ДанныеСотрудника.Вставить("Модифицированность", Модифицированность);
	ДанныеСотрудника.Вставить("ИсходныйНомерСтроки", ИсходныйНомерСтроки);
	ДанныеСотрудника.Вставить("РедактируемыйДокументСсылка", РедактируемыйДокументСсылка);
	ДанныеСотрудника.Вставить("ИзменилисьДанныеФизическогоЛица", ИзменилисьДанныеФизическогоЛица);
	ДанныеСотрудника.Вставить("СотрудникУволен", СотрудникУволен);
	
	СведенияОЗаработкеСотрудника = Новый Массив;
	ДанныеСотрудника.Вставить("СведенияОЗаработке", СведенияОЗаработкеСотрудника);
	
	Для Каждого СтрокаСведенийОЗаработке Из СведенияОЗаработке Цикл
		СтруктураПолейСведенияОЗаработке = СтруктураПолейСведенияОЗаработке();
		ЗаполнитьЗначенияСвойств(СтруктураПолейСведенияОЗаработке, СтрокаСведенийОЗаработке);
		СведенияОЗаработкеСотрудника.Добавить(СтруктураПолейСведенияОЗаработке);
	КонецЦикла;	
	
	СведенияОЗаработкеНаВредныхИТяжелыхРаботахСотрудника = Новый Массив;
	ДанныеСотрудника.Вставить("СведенияОЗаработкеНаВредныхИТяжелыхРаботах", СведенияОЗаработкеНаВредныхИТяжелыхРаботахСотрудника);
	
	Для Каждого СтрокаСведенийОЗаработке Из СведенияОЗаработкеНаВредныхИТяжелыхРаботах Цикл
		СтруктураПолейСведенияОЗаработке = СтруктураПолейСведенияОЗаработкеНаВредныхИТяжелыхРаботах();
		ЗаполнитьЗначенияСвойств(СтруктураПолейСведенияОЗаработке, СтрокаСведенийОЗаработке);
		СведенияОЗаработкеНаВредныхИТяжелыхРаботахСотрудника.Добавить(СтруктураПолейСведенияОЗаработке);
	КонецЦикла;	
		
	ЗаписиОСтажеСотрудника = Новый Массив;	
	ДанныеСотрудника.Вставить("ЗаписиОСтаже", ЗаписиОСтажеСотрудника);
	
	ПорядокЗаписей = ПорядокЗаписейСтажа();
	
	Для Каждого ЭлементПорядка Из ПорядокЗаписей Цикл
		СтруктураПолейЗаписиОСтаже = СтруктураПолейЗаписиОСтаже();
		СтрокаЗаписиОСтаже = ЗаписиОСтаже[ЭлементПорядка.ИндексСтроки];
		ЗаполнитьЗначенияСвойств(СтруктураПолейЗаписиОСтаже, СтрокаЗаписиОСтаже);
		ЗаписиОСтажеСотрудника.Добавить(СтруктураПолейЗаписиОСтаже);
	КонецЦикла;	
	
	КорректирующиеСведенияСотрудника = Новый Массив;
	ДанныеСотрудника.Вставить("КорректирующиеСведения", КорректирующиеСведенияСотрудника);
	
	Для Каждого СтрокаКорректирующихСведений Из КорректирующиеСведения Цикл
		СтруктураПолейКорректирующихСведений = СтруктураПолейДоначисленныхВзносов();
		ЗаполнитьЗначенияСвойств(СтруктураПолейКорректирующихСведений, СтрокаКорректирующихСведений);
		КорректирующиеСведенияСотрудника.Добавить(СтруктураПолейКорректирующихСведений);
	КонецЦикла;	
	
	СведенияОбУплаченныхВзносахСотрудника = Новый Массив;
	ДанныеСотрудника.Вставить("СведенияОбУплаченныхВзносах", СведенияОбУплаченныхВзносахСотрудника);
	
	Для Каждого СтрокаУплаченныеВзносы Из СведенияОбУплаченныхВзносах Цикл 
		СтруктураПолейСведенияОбУплаченныхВзносах = СтруктураПолейСведенияОбУплаченныхВзносах();
		ЗаполнитьЗначенияСвойств(СтруктураПолейСведенияОбУплаченныхВзносах, СтрокаУплаченныеВзносы);
		ДанныеСотрудника.СведенияОбУплаченныхВзносах.Добавить(СтруктураПолейСведенияОбУплаченныхВзносах);
	КонецЦикла;
	
	Ошибки = ПерсонифицированныйУчет.СообщенияОбОшибкахИзДанныхФормы(ЭтотОбъект);
	
	ДанныеСотрудника.Вставить("Ошибки", Ошибки);
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеСотрудника);
	
КонецФункции	

&НаСервере
Функция СтруктураПолейСведенияОЗаработке()
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("КатегорияЗастрахованныхЛиц");
	СтруктураПолей.Вставить("КодМесяца");
	СтруктураПолей.Вставить("Заработок");
	СтруктураПолей.Вставить("ОблагаетсяВзносами");
	СтруктураПолей.Вставить("ОблагаетсяВзносамиДоПредельнойВеличины");
	СтруктураПолей.Вставить("ОблагаетсяВзносамиСвышеПредельнойВеличины");
	СтруктураПолей.Вставить("ПоДоговорамГПХДоПредельнойВеличины");
	СтруктураПолей.Вставить("ПоДоговорамГПХСвышеПредельнойВеличины");
	
	Возврат СтруктураПолей;
	
КонецФункции

&НаСервере
Функция СтруктураПолейСведенияОЗаработкеНаВредныхИТяжелыхРаботах()
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("КодМесяца");
	СтруктураПолей.Вставить("КлассУсловийТруда");
	СтруктураПолей.Вставить("ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах");
	СтруктураПолей.Вставить("ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах");
	
	Возврат СтруктураПолей;
	
КонецФункции

&НаСервере
Функция СтруктураПолейЗаписиОСтаже()
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("НомерОсновнойЗаписи");
	СтруктураПолей.Вставить("НомерДополнительнойЗаписи");
	СтруктураПолей.Вставить("ДатаНачалаПериода");
	СтруктураПолей.Вставить("ДатаОкончанияПериода");
	СтруктураПолей.Вставить("ОсобыеУсловияТруда");
	СтруктураПолей.Вставить("КодПозицииСписка");
	СтруктураПолей.Вставить("ОснованиеИсчисляемогоСтажа");
	СтруктураПолей.Вставить("ПервыйПараметрИсчисляемогоСтажа");
	СтруктураПолей.Вставить("ВторойПараметрИсчисляемогоСтажа");
	СтруктураПолей.Вставить("ТретийПараметрИсчисляемогоСтажа");
	СтруктураПолей.Вставить("ОснованиеВыслугиЛет");
	СтруктураПолей.Вставить("ПервыйПараметрВыслугиЛет");
	СтруктураПолей.Вставить("ВторойПараметрВыслугиЛет");
	СтруктураПолей.Вставить("ТретийПараметрВыслугиЛет");
	СтруктураПолей.Вставить("ТерриториальныеУсловия");
	СтруктураПолей.Вставить("ПараметрТерриториальныхУсловий");
	СтруктураПолей.Вставить("ЗамещениеГосударственныхМуниципальныхДолжностей");

	Возврат СтруктураПолей;
	
КонецФункции

&НаСервере
Функция СтруктураПолейДоначисленныхВзносов()
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("КодМесяца");
	СтруктураПолей.Вставить("ДоначисленоНаОПС");
	СтруктураПолей.Вставить("ДоначисленоСтраховая");
	СтруктураПолей.Вставить("ДоначисленоНакопительная");
	СтруктураПолей.Вставить("УплачиваемыеСтрахователем");
	СтруктураПолей.Вставить("УплачиваемыеИзЗаработкаЗЛ");
	
	Возврат СтруктураПолей;
	
КонецФункции

&НаСервере
Функция СтруктураПолейСведенияОбУплаченныхВзносах()
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("КодМесяца");
	СтруктураПолей.Вставить("УплаченоСтраховая");
	СтруктураПолей.Вставить("УплаченоНакопительная");
	
	Возврат СтруктураПолей;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтруктураДанныхПоСотруднику(Форма)
	
	СтруктураДанныхПоСотруднику = Новый Структура;
	
	СтруктураДанныхПоСотруднику.Вставить("ФиксНачисленныеВзносы", Форма.ФиксНачисленныеВзносы);
	СтруктураДанныхПоСотруднику.Вставить("ФиксСтаж", Форма.ФиксСтаж);
	СтруктураДанныхПоСотруднику.Вставить("ФиксЗаработок", Форма.ФиксЗаработок);

	ЗаполнитьЗначенияСвойств(СтруктураДанныхПоСотруднику, Форма);
	
	Возврат СтруктураДанныхПоСотруднику;
	
КонецФункции	

&НаКлиенте
Процедура ЗаполнитьИзменившиесяДанныеФизическогоЛица(ДанныеФизическогоЛица)	
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеФизическогоЛица);
	ИзменилисьДанныеФизическогоЛица = Истина;
	
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура СпозиционироватьСтроку(СведенияОЗаработке, СтрокаТаблицы, ИмяПоляГруппировкиСтрок = Неопределено)
	
	СтрокиГруппы = ?(ИмяПоляГруппировкиСтрок = Неопределено, СведенияОЗаработке,
		СведенияОЗаработке.НайтиСтроки(Новый Структура(ИмяПоляГруппировкиСтрок, СтрокаТаблицы[ИмяПоляГруппировкиСтрок])));
		
	Если Не ЗначениеЗаполнено(СтрокаТаблицы.КодМесяца) Тогда 
		Возврат;
	КонецЕсли;
	
	ПорядокМесяцев = ПерсонифицированныйУчетКлиентСервер.ПорядокМесяцевСЗВ_КОРР();
	НомерМесяца = ПорядокМесяцев[СтрокаТаблицы.КодМесяца];
	
	Если НомерМесяца = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	НовыйИндексСтроки = СведенияОЗаработке.Индекс(СтрокаТаблицы);
	Для Каждого ОбрабатываемаяСтрока Из СтрокиГруппы Цикл
		НомерМесяцаСтроки = ПорядокМесяцев[ОбрабатываемаяСтрока.КодМесяца];
		Если НомерМесяцаСтроки = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		Если НомерМесяца < НомерМесяцаСтроки И НовыйИндексСтроки > СведенияОЗаработке.Индекс(ОбрабатываемаяСтрока) Тогда
			НовыйИндексСтроки = СведенияОЗаработке.Индекс(ОбрабатываемаяСтрока);
		КонецЕсли;			
	КонецЦикла;	
	
	Если НовыйИндексСтроки <> СведенияОЗаработке.Индекс(СтрокаТаблицы) Тогда
		Сдвиг = НовыйИндексСтроки - СведенияОЗаработке.Индекс(СтрокаТаблицы);	
		СведенияОЗаработке.Сдвинуть(СведенияОЗаработке.Индекс(СтрокаТаблицы), Сдвиг);		
	КонецЕсли;
	
КонецПроцедуры	

#Область МеханизмОтображенияОшибок

&НаКлиенте
Процедура ПоказатьОшибки(ИмяПоляИндикацииОшибки, ПоказыватьТекстОшибки) Экспорт
	ПоказыватьТекстОшибки = Истина;	
КонецПроцедуры	

&НаСервере
Функция ОписаниеЭлементовСИндикациейОшибок() Экспорт
	
	КоллекцияОписанийЭлементов = Новый Массив;
	
	ОписаниеЭлемента = ПерсонифицированныйУчет.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();
	
	ОписаниеЭлемента = ПерсонифицированныйУчет.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();	
	ОписаниеЭлемента.ИмяЭлемента = "ИнфонадписьЗаголовокКарточки";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ОтображатьНепривязанныеОшибки = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Сотрудник";
	ПерсонифицированныйУчет.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
			
	ОписаниеЭлемента = ПерсонифицированныйУчет.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();	
	ОписаниеЭлемента.ИмяЭлемента = "Фамилия";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Фамилия";
	ПерсонифицированныйУчет.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ПерсонифицированныйУчет.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();	
	ОписаниеЭлемента.ИмяЭлемента = "Имя";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Имя";
	ПерсонифицированныйУчет.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
		
	ОписаниеЭлемента = ПерсонифицированныйУчет.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();	
	ОписаниеЭлемента.ИмяЭлемента = "Отчество";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "Отчество";
	ПерсонифицированныйУчет.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ПерсонифицированныйУчет.ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки();	
	ОписаниеЭлемента.ИмяЭлемента = "СтраховойНомерПФР";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "СтраховойНомерПФР";
	ПерсонифицированныйУчет.ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента);

	ОписаниеЭлемента = ПерсонифицированныйУчет.ОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы();	
	ОписаниеЭлемента.ИмяЭлемента = "ЗаписиОСтаже";
	ОписаниеЭлемента.ИмяЭлементаТаблица = "ЗаписиОСтаже";	
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "ЗаписиОСтаже";	
	ПерсонифицированныйУчет.ДобавитьОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы(КоллекцияОписанийЭлементов, ОписаниеЭлемента);

	ОписаниеЭлемента = ПерсонифицированныйУчет.ОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы();	
	ОписаниеЭлемента.ИмяЭлемента = "СведенияОЗаработке";
	ОписаниеЭлемента.ИмяЭлементаТаблица = "СведенияОЗаработке";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "СведенияОЗаработке";	
	ПерсонифицированныйУчет.ДобавитьОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ПерсонифицированныйУчет.ОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы();	
	ОписаниеЭлемента.ИмяЭлемента = "КорректирующиеСведения";
	ОписаниеЭлемента.ИмяЭлементаТаблица = "КорректирующиеСведения";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "КорректирующиеСведения";	
	ПерсонифицированныйУчет.ДобавитьОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	ОписаниеЭлемента = ПерсонифицированныйУчет.ОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы();	
	ОписаниеЭлемента.ИмяЭлемента = "СведенияОЗаработкеНаВредныхИТяжелыхРаботах";
	ОписаниеЭлемента.ИмяЭлементаТаблица = "СведенияОЗаработкеНаВредныхИТяжелыхРаботах";
	ОписаниеЭлемента.ОтображатьГиперссылку = Истина;
	ОписаниеЭлемента.ПутьКДаннымФормыСодержащимОшибку = "СведенияОЗаработкеНаВредныхИТяжелыхРаботах";	
	ПерсонифицированныйУчет.ДобавитьОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы(КоллекцияОписанийЭлементов, ОписаниеЭлемента);
	
	Возврат КоллекцияОписанийЭлементов;
	
КонецФункции	

&НаКлиентеНаСервереБезКонтекста
Функция УстановитьЗаголовкиВСтрокеТаблицы(Форма, СтрокаТаблицы, ОписаниеКолонокЗаголовков, ЗаполнятьЗаголовкиПоУмолчанию = Истина)
	Если ЗаполнятьЗаголовкиПоУмолчанию Тогда
		ЗаполнитьТекстыЗаголовковКолонокПоУмолчанию(Форма, ОписаниеКолонокЗаголовков);
	КонецЕсли;	

	Для Каждого ОписаниеЗаголовка Из ОписаниеКолонокЗаголовков Цикл
		СтрокаТаблицы[ОписаниеЗаголовка.ПолеТаблицы + "Заголовок"] = ОписаниеЗаголовка.Заголовок + ":";			
	КонецЦикла;
КонецФункции


// Добавление полей заголовков.

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеКолонокЗаголовковТаблицФормы()
	
	ОписаниеКолонокЗаголовковТаблиц = Новый Соответствие;
	ОписаниеКолонокЗаголовковТаблиц.Вставить("СведенияОЗаработке", ОписаниеКолонокЗаголовковТаблицы("СведенияОЗаработке")); 
	ОписаниеКолонокЗаголовковТаблиц.Вставить("ЗаписиОСтаже", ОписаниеКолонокЗаголовковТаблицы("ЗаписиОСтаже")); 
	
	Возврат ОписаниеКолонокЗаголовковТаблиц;
	
КонецФункции	

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеКолонокЗаголовковТаблицыСведенияОЗаработке()
	
	ОписаниеЗаголовковКолонок = Новый Массив;
	
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "СведенияОЗаработкеОблагаетсяВзносамиДоПредельнойВеличины";
	ОписаниеЗаголовка.Заголовок = "";
	ОписаниеЗаголовка.Ширина = 7;
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
		
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "СведенияОЗаработкеПоДоговорамГПХДоПредельнойВеличины";
	ОписаниеЗаголовка.Заголовок = "";
	ОписаниеЗаголовка.Ширина = 18;
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
	
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "СведенияОЗаработкеОблагаетсяВзносамиСвышеПредельнойВеличины";
	ОписаниеЗаголовка.Заголовок = "";
	ОписаниеЗаголовка.Ширина = 7;
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
		
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "СведенияОЗаработкеПоДоговорамГПХСвышеПредельнойВеличины";
	ОписаниеЗаголовка.Заголовок = "";
	ОписаниеЗаголовка.Ширина = 18;
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
	
	Возврат ОписаниеЗаголовковКолонок;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеКолонокЗаголовковТаблицыЗаписиОСтаже()
	
	ОписаниеЗаголовковКолонок = Новый Массив;
	
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "ЗаписиОСтажеТерриториальныеУсловия";
	ОписаниеЗаголовка.Заголовок = "";
	
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
	
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "ЗаписиОСтажеПараметрТерриториальныхУсловий";
	ОписаниеЗаголовка.Заголовок = "";
	
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
	
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "ПачкаСЗВ_6_4ОбъектЗаписиОСтажеОснованиеИсчисляемогоСтажа";
	ОписаниеЗаголовка.Заголовок = "";
	
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
	
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "ПачкаСЗВ_6_4ОбъектЗаписиОСтажеТретийПараметрИсчисляемогоСтажа";
	ОписаниеЗаголовка.Заголовок = "";
	
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
	
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "ПачкаСЗВ_6_4ОбъектЗаписиОСтажеОснованиеВыслугиЛет";
	ОписаниеЗаголовка.Заголовок = "";
	
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
	
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "ПачкаСЗВ_6_4ОбъектЗаписиОСтажеПервыйПараметрВыслугиЛет";
	ОписаниеЗаголовка.Заголовок = "";
	
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
	
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "ПачкаСЗВ_6_4ОбъектЗаписиОСтажеВторойПараметрВыслугиЛет";
	ОписаниеЗаголовка.Заголовок = "";
	
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
	
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "ПачкаСЗВ_6_4ОбъектЗаписиОСтажеТретийПараметрВыслугиЛет";
	ОписаниеЗаголовка.Заголовок = "";
	
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
	
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "ПачкаСЗВ_6_4ОбъектЗаписиОСтажеПервыйПараметрИсчисляемогоСтажа";
	ОписаниеЗаголовка.Заголовок = "";
	
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
	
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "ПачкаСЗВ_6_4ОбъектЗаписиОСтажеВторойПараметрИсчисляемогоСтажа";
	ОписаниеЗаголовка.Заголовок = "";
	
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
	
	ОписаниеЗаголовка = СтруктураОписанияКолонокЗаголовков();
	ОписаниеЗаголовка.ПолеТаблицы = "ЗаписиОСтажеЗамещениеГосударственныхМуниципальныхДолжностей";
	ОписаниеЗаголовка.Заголовок = НСтр("ru = 'Код'");
	
	ОписаниеЗаголовковКолонок.Добавить(ОписаниеЗаголовка);
	
	Возврат ОписаниеЗаголовковКолонок;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеКолонокЗаголовковТаблицы(ИмяТаблицы)
	
	Если ИмяТаблицы = "СведенияОЗаработке" Тогда
		ОписаниеКолонок = ОписаниеКолонокЗаголовковТаблицыСведенияОЗаработке();
	ИначеЕсли ИмяТаблицы = "ЗаписиОСтаже" Тогда
		ОписаниеКолонок = ОписаниеКолонокЗаголовковТаблицыЗаписиОСтаже();
	КонецЕсли;	
		
	Возврат ОписаниеКолонок;
	
КонецФункции	

&НаКлиенте
Процедура Подключаемый_ЭлементИндикацииОшибкиНажатие(Элемент, СтандартнаяОбработка)
	
	ПерсонифицированныйУчетКлиент.ЭлементИндикацииОшибкиНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры	

&НаКлиенте
Процедура Подключаемый_ЭлементИндикацииПриАктивизацииЯчейки(Элемент)
	
	ПерсонифицированныйУчетКлиент.ЭлементИндикацииПриАктивизацииЯчейки(ЭтотОбъект, Элемент);
	
КонецПроцедуры	

&НаСервере
Функция ПорядокЗаписейСтажа()
	
	ВидыСтажаГПХ = Новый Массив;
	
	ВидыСтажаГПХ.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДОГОВОР);
	ВидыСтажаГПХ.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.НЕОПЛАВТ);
	ВидыСтажаГПХ.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.НЕОПЛДОГ);
	
	ПорядокЗаписейСтажа = Новый ТаблицаЗначений;
	ПорядокЗаписейСтажа.Колонки.Добавить("ИндексСтроки", Новый ОписаниеТипов("Число"));
	ПорядокЗаписейСтажа.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	
	ОбычныеЗаписиСтажа = Новый Массив;
	
	Для Каждого СтрокаСтажа Из ЗаписиОСтаже Цикл
		Если ВидыСтажаГПХ.Найти(СтрокаСтажа.ТретийПараметрИсчисляемогоСтажа) <> Неопределено Тогда
			ЭлементПорядка = ПорядокЗаписейСтажа.Добавить();
			ЭлементПорядка.ДатаНачала = СтрокаСтажа.ДатаНачалаПериода;
			ЭлементПорядка.ИндексСтроки = ЗаписиОСтаже.Индекс(СтрокаСтажа);
		Иначе
			ОбычныеЗаписиСтажа.Добавить(СтрокаСтажа);	
		КонецЕсли;	
	КонецЦикла;	
	
	ПорядокЗаписейСтажа.Сортировать("ДатаНачала");
	
	Для Каждого СтрокаСтажа Из ОбычныеЗаписиСтажа Цикл
		ЭлементПорядка = ПорядокЗаписейСтажа.Добавить();
		ЭлементПорядка.ИндексСтроки = ЗаписиОСтаже.Индекс(СтрокаСтажа);	
	КонецЦикла;
	
	Возврат ПорядокЗаписейСтажа;
	
КонецФункции	

&НаСервере
Процедура УстановитьСписокВыбораМесяцев()
	
	ПервыйМесяц = Месяц(Период);
	ПоследнийМесяц  = Месяц(ОкончаниеПериода);
	
	Элементы.СведенияОЗаработкеКодМесяца.СписокВыбора.Очистить();
	Элементы.КорректирующиеСведенияКодМесяца.СписокВыбора.Очистить();
	Элементы.СведенияОбУплаченныхВзносахКодМесяца.СписокВыбора.Очистить();
	Элементы.СведенияОЗаработкеНаВредныхИТяжелыхРаботахКодМесяца.СписокВыбора.Очистить();
	
	КодыМесяцев = ПерсонифицированныйУчетКлиентСервер.КодыМесяцевСЗВ_КОРР();
	
	Для Сч = ПервыйМесяц По ПоследнийМесяц Цикл 
		Элементы.СведенияОЗаработкеКодМесяца.СписокВыбора.Добавить(КодыМесяцев[Сч]);
		Элементы.КорректирующиеСведенияКодМесяца.СписокВыбора.Добавить(КодыМесяцев[Сч]);
		Элементы.СведенияОбУплаченныхВзносахКодМесяца.СписокВыбора.Добавить(КодыМесяцев[Сч]);
		Элементы.СведенияОЗаработкеНаВредныхИТяжелыхРаботахКодМесяца.СписокВыбора.Добавить(КодыМесяцев[Сч]);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКонтрольИсправлений() Экспорт
	
	КонтролируемыеПоляВзносыНачисленные = Новый Массив;
	КонтролируемыеПоляВзносыНачисленные.Добавить("НачисленоСтраховая");
	КонтролируемыеПоляВзносыНачисленные.Добавить("НачисленоНакопительная");
	КонтролируемыеПоляВзносыНачисленные.Добавить("ДоНачисленоСтраховая");
	КонтролируемыеПоляВзносыНачисленные.Добавить("ДоНачисленоНакопительная");
	
	КонтролируемыеПоляСтаж = Новый Массив;
	КонтролируемыеПоляСтаж.Добавить("НомерОсновнойЗаписи");
	КонтролируемыеПоляСтаж.Добавить("НомерДополнительнойЗаписи");
	КонтролируемыеПоляСтаж.Добавить("ДатаНачалаПериода");
	КонтролируемыеПоляСтаж.Добавить("ДатаОкончанияПериода");
	КонтролируемыеПоляСтаж.Добавить("ОсобыеУсловияТруда");
	КонтролируемыеПоляСтаж.Добавить("КодПозицииСписка");
	КонтролируемыеПоляСтаж.Добавить("ОснованиеИсчисляемогоСтажа");
	КонтролируемыеПоляСтаж.Добавить("ПервыйПараметрИсчисляемогоСтажа");
	КонтролируемыеПоляСтаж.Добавить("ВторойПараметрИсчисляемогоСтажа");
	КонтролируемыеПоляСтаж.Добавить("ТретийПараметрИсчисляемогоСтажа");
	КонтролируемыеПоляСтаж.Добавить("ОснованиеВыслугиЛет");
	КонтролируемыеПоляСтаж.Добавить("ПервыйПараметрВыслугиЛет");
	КонтролируемыеПоляСтаж.Добавить("ВторойПараметрВыслугиЛет");
	КонтролируемыеПоляСтаж.Добавить("ТретийПараметрВыслугиЛет");
	КонтролируемыеПоляСтаж.Добавить("ТерриториальныеУсловия");
	КонтролируемыеПоляСтаж.Добавить("ПараметрТерриториальныхУсловий");
	
	КонтролируемыеПоляЗаработок = Новый Массив;
	КонтролируемыеПоляЗаработок.Добавить("КодМесяца");
	КонтролируемыеПоляЗаработок.Добавить("Заработок");
	КонтролируемыеПоляЗаработок.Добавить("ОблагаетсяВзносамиДоПредельнойВеличины");
	КонтролируемыеПоляЗаработок.Добавить("ПоДоговорамГПХДоПредельнойВеличины");
	КонтролируемыеПоляЗаработок.Добавить("ОблагаетсяВзносамиСвышеПредельнойВеличины");
	КонтролируемыеПоляЗаработок.Добавить("ПоДоговорамГПХСвышеПредельнойВеличины");
	
	КонтролируемыеПоляЗаработокВредный = Новый Массив;
	КонтролируемыеПоляЗаработокВредный.Добавить("КодМесяца");
	КонтролируемыеПоляЗаработокВредный.Добавить("КлассУсловийТруда");
	КонтролируемыеПоляЗаработокВредный.Добавить("ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах");
	КонтролируемыеПоляЗаработокВредный.Добавить("ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах");

	СведенияОКонтролируемыхПолях = Новый Структура;
	
	ОписаниеКонтролируемыхПолей = Новый Структура("ИмяПоляФиксДанных, КонтролируемыеПоля", "ФиксНачисленныеВзносы", Новый ФиксированныйМассив(КонтролируемыеПоляВзносыНачисленные));
	СведенияОКонтролируемыхПолях.Вставить("НачисленныеВзносы", Новый ФиксированнаяСтруктура(ОписаниеКонтролируемыхПолей));
	
	ОписаниеКонтролируемыхПолей = Новый Структура("ИмяПоляФиксДанных, КонтролируемыеПоля", "ФиксСтаж", Новый ФиксированныйМассив(КонтролируемыеПоляСтаж));
	СведенияОКонтролируемыхПолях.Вставить("Стаж",  Новый ФиксированнаяСтруктура(ОписаниеКонтролируемыхПолей));	
	
	ОписаниеКонтролируемыхПолей = Новый Структура("ИмяПоляФиксДанных, КонтролируемыеПоля", "ФиксЗаработок", Новый ФиксированныйМассив(КонтролируемыеПоляЗаработок));
	СведенияОКонтролируемыхПолях.Вставить("Заработок", Новый ФиксированнаяСтруктура(ОписаниеКонтролируемыхПолей));
	
	ОписаниеКонтролируемыхПолей = Новый Структура("ИмяПоляФиксДанных, КонтролируемыеПоля", "ФиксЗаработок", Новый ФиксированныйМассив(КонтролируемыеПоляЗаработокВредный));
	СведенияОКонтролируемыхПолях.Вставить("ФиксЗаработокВредный", Новый ФиксированнаяСтруктура(ОписаниеКонтролируемыхПолей));
			
	КонтролируемыеПоля = Новый ФиксированнаяСтруктура(СведенияОКонтролируемыхПолях);

КонецПроцедуры	

&НаСервере
Процедура УстановитьКраткоеОтображениеКатегорииВТаблицах(Форма, ПутьКДанным, ИмяЭлемента) Экспорт 	
	Для Каждого ОписаниеЗначения Из Метаданные.Перечисления.КатегорииЗастрахованныхЛицДляПФР.ЗначенияПеречисления Цикл
		ЭлементОформления = Форма.УсловноеОформление.Элементы.Добавить();
	
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", ОписаниеЗначения.Имя + Символы.ПС + ОписаниеЗначения.Синоним);
		
		Отбор = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Отбор.ПравоеЗначение = Перечисления.КатегорииЗастрахованныхЛицДляПФР[ОписаниеЗначения.Имя];
		
		ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ИмяЭлемента);
	КонецЦикла;		
КонецПроцедуры	


&НаСервере
Процедура ДобавитьЗаголовкиКПолямТаблицФормы(Форма, ИменаДополняемыхТаблиц, ОписаниеКолонокЗаголовковТаблицФормы) Экспорт
	
	ДобавляемыеРеквизиты = Новый Массив;
		
	Для Каждого ИмяТаблицы Из ИменаДополняемыхТаблиц Цикл
		ОписаниеКолонокЗаголовков = ОписаниеКолонокЗаголовковТаблицФормы[ИмяТаблицы];
		
		Таблица = Форма.Элементы.Найти(ИмяТаблицы);
		
		Если Таблица = Неопределено
			Или ОписаниеКолонокЗаголовков = Неопределено Тогда
			
			Продолжить;
		КонецЕсли;	
		
		ЗаполнитьТекстыЗаголовковКолонокПоУмолчанию(Форма, ОписаниеКолонокЗаголовков);
		
		ПутьКДаннымТаблицы = Таблица.ПутьКДанным;
		Для Каждого ОписаниеЗаголовка Из ОписаниеКолонокЗаголовков Цикл
			КлассификаторСтроки = Новый КвалификаторыСтроки(СтрДлина(ОписаниеЗаголовка.Заголовок) + 1);
			
			ТипЗаголовка = Новый ОписаниеТипов("Строка", , КлассификаторСтроки);
			
			РеквизитЗаголовок = Новый РеквизитФормы(ОписаниеЗаголовка.ПолеТаблицы + "Заголовок", ТипЗаголовка, ПутьКДаннымТаблицы);
			
			ДобавляемыеРеквизиты.Добавить(РеквизитЗаголовок);	
		КонецЦикла;
		
	КонецЦикла;	
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	УсловноеОформлениеВидимостиПолей = Новый Соответствие;
	
	Для Каждого ЭлементОформления Из Форма.УсловноеОформление.Элементы Цикл
		Если ЭлементОформления.Оформление.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Видимость")).Использование Тогда
			Для Каждого ОформляемоеПоле Из ЭлементОформления.Поля.Элементы Цикл
				Если ОформляемоеПоле.Использование Тогда
					ЭлементыОформленияПоля = УсловноеОформлениеВидимостиПолей.Получить(ОформляемоеПоле.Поле);
					
					Если ЭлементыОформленияПоля = Неопределено Тогда
						ЭлементыОформленияПоля = Новый Массив;
						УсловноеОформлениеВидимостиПолей.Вставить(ОформляемоеПоле.Поле, ЭлементыОформленияПоля);
					КонецЕсли;
					
					ЭлементыОформленияПоля.Добавить(ЭлементОформления);
				КонецЕсли;
			КонецЦикла;		
		КонецЕсли;	
	КонецЦикла;	
	
	Для Каждого ИмяТаблицы Из ИменаДополняемыхТаблиц Цикл
		ОписаниеКолонокЗаголовков = ОписаниеКолонокЗаголовковТаблицФормы[ИмяТаблицы];
		
		Таблица = Форма.Элементы.Найти(ИмяТаблицы);
		
		Если Таблица = Неопределено
			Или ОписаниеКолонокЗаголовков = Неопределено Тогда
			
			Продолжить;
		КонецЕсли;	
		
		ПутьКДаннымТаблицы = Таблица.ПутьКДанным;
		Для Каждого ОписаниеЗаголовка Из ОписаниеКолонокЗаголовков Цикл
			ПолеТаблицы = Форма.Элементы.Найти(ОписаниеЗаголовка.ПолеТаблицы);
			
			ПолеТаблицы.ОтображатьВШапке = Ложь;
			
			Если ПолеТаблицы = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ГруппаЗаголовка = Форма.Элементы.Вставить(ПолеТаблицы.Имя + "ЗаголовокГруппа", Тип("ГруппаФормы"), ПолеТаблицы.Родитель, ПолеТаблицы);
			ГруппаЗаголовка.Вид = ВидГруппыФормы.ГруппаКолонок;
			ГруппаЗаголовка.ОтображатьВШапке = Ложь;
			ГруппаЗаголовка.Группировка = ГруппировкаКолонок.Горизонтальная;
			
			ПолеЗаголовка = Форма.Элементы.Добавить(ПолеТаблицы.Имя + "Заголовок", Тип("ПолеФормы"), ГруппаЗаголовка);
			ПолеЗаголовка.ПутьКДанным = ПутьКДаннымТаблицы + "." + ПолеТаблицы.Имя + "Заголовок";
			ПолеЗаголовка.Вид = ВидПоляФормы.ПолеНадписи;
			ПолеЗаголовка.ТолькоПросмотр = Истина;
			ПолеЗаголовка.ОтображатьВШапке = Ложь;
			ПолеЗаголовка.Шрифт = Новый Шрифт(ПолеТаблицы.Шрифт,,,,Истина);
			ПолеЗаголовка.РастягиватьПоГоризонтали = Ложь;
			
			Если ОписаниеЗаголовка.Ширина = 0 Тогда
				ПолеЗаголовка.Ширина = СтрДлина(ОписаниеЗаголовка.Заголовок) + 1;
			Иначе
				ПолеЗаголовка.Ширина = ОписаниеЗаголовка.Ширина;
			КонецЕсли;	
			
			Форма.Элементы.Переместить(ПолеТаблицы, ГруппаЗаголовка);
			
			ПолеКомпоновки = Новый ПолеКомпоновкиДанных(ПолеТаблицы.Имя);
			
			ЭлементыОформленияПоля = УсловноеОформлениеВидимостиПолей.Получить(ПолеКомпоновки);
			
			Если ЭлементыОформленияПоля <> Неопределено Тогда
				Для Каждого ЭлементОформления Из ЭлементыОформленияПоля Цикл
					ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
					ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ПолеЗаголовка.Имя);
				КонецЦикла;	
			КонецЕсли;	
		КонецЦикла;	
	КонецЦикла;	
	
КонецПроцедуры	

Процедура УстановитьВидимостьКолонокЗаголовков(Форма, ИмяТаблицы, ОписаниеКолонокЗаголовковТаблицы) Экспорт	
	Для Каждого ОписаниеКолонки Из ОписаниеКолонокЗаголовковТаблицы Цикл
		КолонкаТаблицы = Форма.Элементы.Найти(ОписаниеКолонки.ПолеТаблицы);
		Видимость = ?(КолонкаТаблицы = Неопределено, Ложь, КолонкаТаблицы.Видимость);
		
		КолонкаЗаголовок = Форма.Элементы.Найти(ОписаниеКолонки.ПолеТаблицы + "Заголовок");
		
		Если КолонкаЗаголовок <> Неопределено Тогда
			КолонкаЗаголовок.Видимость = Видимость;
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Функция СтруктураОписанияКолонокЗаголовков()
	СтруктураОписания = Новый Структура;
	
	СтруктураОписания.Вставить("ПолеТаблицы");
	СтруктураОписания.Вставить("Заголовок");
	СтруктураОписания.Вставить("Ширина", 0);
	
	Возврат СтруктураОписания;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьТекстыЗаголовковКолонокПоУмолчанию(Форма, ОписаниеКолонокЗаголовков)
	Если ОписаниеКолонокЗаголовков <> Неопределено Тогда
		Для Каждого ОписаниеКолонки Из ОписаниеКолонокЗаголовков Цикл
			Если Не ЗначениеЗаполнено(ОписаниеКолонки.Заголовок) Тогда
				ПолеТаблицы = Форма.Элементы.Найти(ОписаниеКолонки.ПолеТаблицы);
				
				Если ПолеТаблицы <> Неопределено Тогда
					ОписаниеКолонки.Заголовок = ПолеТаблицы.Заголовок;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

СтарыеЗначенияКонтролируемыхПолейВзносы = Новый Структура;

СтарыеЗначенияКонтролируемыхПолейСтаж = Новый Структура;

СтарыеЗначенияКонтролируемыхПолейЗаработок = Новый Структура;

#КонецОбласти

#КонецОбласти
