#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ОбновитьПредставления() Экспорт
	
	Если ПоложениеИсполнителя=Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		ИсполнительПредставление = "";
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			ТекИсполнитель = СокрЛП(Строка(СтрокаТабличнойЧасти.Исполнитель));
			Если ПустаяСтрока(ТекИсполнитель) Тогда
				Продолжить;
			КонецЕсли;
			Если Найти(ИсполнительПредставление, ТекИсполнитель+Символы.ПС)>0 Тогда
				Продолжить;
			КонецЕсли; 
			ИсполнительПредставление = ИсполнительПредставление+ТекИсполнитель+Символы.ПС;
		КонецЦикла;
		ИсполнительПредставление = СокрЛП(ИсполнительПредставление);
	Иначе
		ИсполнительПредставление = Строка(Исполнитель);
	КонецЕсли; 
	
	Если ПоложениеСтруктурнойЕдиницы=Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		СтруктурнаяЕдиницаПредставление = "";
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			Если ПоложениеИсполнителя=Перечисления.ПоложениеРеквизитаНаФорме.ВШапке И ТипЗнч(Исполнитель)=Тип("СправочникСсылка.Бригады") Тогда
				Прервать;
			КонецЕсли;
			Если ТипЗнч(СтрокаТабличнойЧасти.Исполнитель)=Тип("СправочникСсылка.Бригады") Тогда
				Продолжить;
			КонецЕсли; 
			ТекСтруктурнаяЕдиница = СокрЛП(Строка(СтрокаТабличнойЧасти.СтруктурнаяЕдиница));
			Если ПустаяСтрока(ТекСтруктурнаяЕдиница) Тогда
				Продолжить;
			КонецЕсли;
			Если Найти(СтруктурнаяЕдиницаПредставление, ТекСтруктурнаяЕдиница+Символы.ПС)>0 Тогда
				Продолжить;
			КонецЕсли; 
			СтруктурнаяЕдиницаПредставление = СтруктурнаяЕдиницаПредставление+ТекСтруктурнаяЕдиница+Символы.ПС;
		КонецЦикла;
		Для каждого СтрокаТабличнойЧасти Из СоставБригады Цикл
			ТекСтруктурнаяЕдиница = СокрЛП(Строка(СтрокаТабличнойЧасти.СтруктурнаяЕдиница));
			Если ПустаяСтрока(ТекСтруктурнаяЕдиница) Тогда
				Продолжить;
			КонецЕсли;
			Если Найти(СтруктурнаяЕдиницаПредставление, ТекСтруктурнаяЕдиница+Символы.ПС)>0 Тогда
				Продолжить;
			КонецЕсли; 
			СтруктурнаяЕдиницаПредставление = СтруктурнаяЕдиницаПредставление+ТекСтруктурнаяЕдиница+Символы.ПС;
		КонецЦикла;
		СтруктурнаяЕдиницаПредставление = СокрЛП(СтруктурнаяЕдиницаПредставление);
	Иначе
		СтруктурнаяЕдиницаПредставление = Строка(СтруктурнаяЕдиница);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытий

// В обработчике события ОбработкаЗаполнения документа выполняется
// - заполнение документа по инвентаризации запасов на складе.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ПоложениеИсполнителя=Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		СоставБригады.Очистить();
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(ВидЦен) Тогда
		ВидЦен = Справочники.ВидыЦен.Учетная;
	КонецЕсли; 
	ВалютаДокумента = ВидЦен.ВалютаЦены;
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("Структура")] = "ЗаполнитьПоСтруктуре";
	СтратегияЗаполнения[Тип("ДокументСсылка.ЗаказПокупателя")] = "ЗаполнитьПоЗаказуПокупателя";
	СтратегияЗаполнения[Тип("ДокументСсылка.ЗаказНаПроизводство")] = "ЗаполнитьПоЗаказуНаПроизводство";
	СтратегияЗаполнения[Тип("ДокументСсылка.СборкаЗапасов")] = "ЗаполнитьПоСборкеЗапасов";
	
	Если Константы.ФункциональнаяОпцияРезервированиеЗапасов.Получить() Тогда
		ИсключаяСвойства = "";
	Иначе
		ИсключаяСвойства = "ЗаказПокупателя";
	КонецЕсли;
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения, ИсключаяСвойства);
	
	Если Исполнитель = Неопределено Тогда
		Исполнитель = Справочники.Сотрудники.ПустаяСсылка();
	КонецЕсли;
	Для каждого СтрокаТабличнойЧасти Из Операции Цикл
		Если СтрокаТабличнойЧасти.Исполнитель = Неопределено Тогда
			СтрокаТабличнойЧасти.Исполнитель = Справочники.Сотрудники.ПустаяСсылка();
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры // ОбработкаЗаполнения()

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Закрыт Тогда
		ПроверяемыеРеквизиты.Добавить("ДатаЗакрытия");
	КонецЕсли;
		
	Если ПоложениеИсполнителя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		ПроверяемыеРеквизиты.Добавить("Операции.Исполнитель");
	Иначе
		ПроверяемыеРеквизиты.Добавить("Исполнитель");
	КонецЕсли;
		
	Если ПоложениеСтруктурнойЕдиницы = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		ПроверяемыеРеквизиты.Добавить("СоставБригады.СтруктурнаяЕдиница");
	Иначе
		ПроверяемыеРеквизиты.Добавить("СтруктурнаяЕдиница");
	КонецЕсли;
	
	НоменклатураВДокументахСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, Отказ, Истина);
	
	// Проверка заполнения состава
	Если ПоложениеИсполнителя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СоставБригады", СоставБригады.Выгрузить());
		Запрос.УстановитьПараметр("Операции", Операции.Выгрузить());
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Операции.НомерСтроки КАК НомерСтроки,
		|	Операции.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ Операции
		|ИЗ
		|	&Операции КАК Операции
		|ГДЕ
		|	Операции.Исполнитель ССЫЛКА Справочник.Бригады
		|	И Операции.КлючСвязи <> 0
		|	И Операции.Исполнитель <> ЗНАЧЕНИЕ(Справочник.Бригады.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставБригады.Сотрудник КАК Сотрудник,
		|	СоставБригады.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ СоставБригады
		|ИЗ
		|	&СоставБригады КАК СоставБригады
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Операции.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	Операции КАК Операции
		|		ЛЕВОЕ СОЕДИНЕНИЕ СоставБригады КАК СоставБригады
		|		ПО Операции.КлючСвязи = СоставБригады.КлючСвязи
		|ГДЕ
		|	СоставБригады.Сотрудник ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	Операции.НомерСтроки";
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнен состав бригады в строке %1.'"), Выборка.НомерСтроки);
			КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Операции", Выборка.НомерСтроки,
				"Исполнитель");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Исполнитель)=Тип("СправочникСсылка.Бригады") И СоставБригады.Количество()=0 Тогда
		
		ТекстСообщения = НСтр("ru = 'Не заполнен состав бригады.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "СоставБригады", , Отказ);
		
	КонецЕсли;
	
	Если Закрыт Тогда
		ПроверитьЗачислениеСебестоимости(Отказ);
		ПроверитьСчетаУчетаЗатратНоменклатуры();
	КонецЕсли; 
		
КонецПроцедуры

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПоложениеЗаказаНаПроизводство<>Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			СтрокаТабличнойЧасти.ЗаказНаПроизводство = ЗаказНаПроизводство;
		КонецЦикла; 
	Иначе
		ЗаказНаПроизводство = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(Операции, "ЗаказНаПроизводство");
	КонецЕсли;
	Если ПоложениеИсполнителя<>Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			СтрокаТабличнойЧасти.Исполнитель = Исполнитель;
		КонецЦикла; 
	Иначе
		Исполнитель = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(Операции, "Исполнитель");
	КонецЕсли;
	Если ПоложениеСтруктурнойЕдиницы<>Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = СтруктурнаяЕдиница;
		КонецЦикла; 
		Для каждого СтрокаТабличнойЧасти Из СоставБригады Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = СтруктурнаяЕдиница;
		КонецЦикла; 
	Иначе
		СтруктурнаяЕдиница = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(Операции, "СтруктурнаяЕдиница");
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(ВидЦен) Тогда
		ВидЦен = Справочники.ВидыЦен.Учетная;
	КонецЕсли; 
	
	СуммаДокумента = Операции.Итог("Стоимость");
	
	ОбновитьПредставления();
	
	// Этапы производства
	Если НЕ Отказ 
		И РежимЗаписи = РежимЗаписиДокумента.Проведение
		И ПолучитьФункциональнуюОпцию("ИспользоватьЭтапыПроизводства") Тогда
		ПроверяемыеСпецификации = Новый Массив;
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
				Продолжить;
			КонецЕсли; 
			ПроверяемыеСпецификации.Добавить(СтрокаТабличнойЧасти.Спецификация);
		КонецЦикла;
		СпецификацииСЭтапами = ПроизводствоСервер.СпецификацииСПоэтапнымПроизводством(ПроверяемыеСпецификации);
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
				СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
			ИначеЕсли НЕ ПолучитьФункциональнуюОпцию("ВыполнениеЭтаповРазнымиПодразделениями")
				ИЛИ СпецификацииСЭтапами.Найти(СтрокаТабличнойЧасти.Спецификация) = Неопределено Тогда 
				// При производстве на одной структурной единице завершающие подразделения заполняются изготовителем
				СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = СтруктурнаяЕдиница;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// Этапы производства
	ПроверитьЗаполнениеЭтаповПроизводства(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 

	// Дополнительная проверка на заполненность структурной единицы в ТЧ Операции, где это необходимо
	Если ПоложениеСтруктурнойЕдиницы = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Исполнитель) И ТипЗнч(СтрокаТабличнойЧасти.Исполнитель)=Тип("СправочникСсылка.Сотрудники") И НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтруктурнаяЕдиница) Тогда
				ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Не заполнена колонка ""Подразделение""'"),,
				СтрШаблон("Объект.Операции[%1].СтруктурнаяЕдиница", Операции.Индекс(СтрокаТабличнойЧасти)),,
				Отказ);
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.СдельныйНаряд.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ПроведениеДокументовУНФ.ОтразитьДвижения("НачисленияИУдержания", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("РасчетыСПерсоналом", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("Запасы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("СдельныеНаряды", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьУправленческий(ДополнительныеСвойства, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗаказыНаПроизводство", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЭтапыПроизводства", ТаблицыДляДвижений, Движения, Отказ);

	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль
	Документы.СдельныйНаряд.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)

	// Этапы производства
	ПроверитьЗаполнениеЭтаповПроизводства(Отказ, Истина);
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 

	// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

#КонецОбласти

#Область ПроцедурыЗаполненияДокумента

// АПК:299-выкл процедуры вызываются см. ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент

// Обработчик заполнения документа на основании структуры.
//
// Параметры:
//  ДанныеЗаполнения - Структура.
//
Процедура ЗаполнитьПоСтруктуре(ДанныеЗаполнения) Экспорт
	
	Если ДанныеЗаполнения.Свойство("Операции") Тогда
		Для каждого СтрокаОперации Из ДанныеЗаполнения.Операции Цикл
			НоваяСтрока = Операции.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОперации);
		КонецЦикла;
		ДействияПослеЗаполненияТЧ();
	ИначеЕсли ДанныеЗаполнения.Свойство("МассивЗаказовНаПроизводство") Тогда
		ЗаполнитьПоЗаказуНаПроизводство(ДанныеЗаполнения);
	КонецЕсли; 
	
КонецПроцедуры

// Обработчик заполнения на основании документа ЗаказПокупателя.
//
// Параметры:
//  ДокументСсылкаЗаказПокупателя - ДокументСсылка.ЗаказПокупателя.
//
Процедура ЗаполнитьПоЗаказуПокупателя(ДокументСсылкаЗаказПокупателя) Экспорт
	
	Если ДокументСсылкаЗаказПокупателя.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаПродажу 
		ИЛИ ДокументСсылкаЗаказПокупателя.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаПереработку Тогда
		ЗаполнитьПоЗаказуНаПродажуПереработку(ДокументСсылкаЗаказПокупателя);
	ИначеЕсли ДокументСсылкаЗаказПокупателя.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд Тогда
		ЗаполнитьПоЗаказНаряду(ДокументСсылкаЗаказПокупателя);
	КонецЕсли;
	
	ДокументОснование = ДокументСсылкаЗаказПокупателя;
	
	ЗаполнитьЗначенияПереключаемыхРеквизитовВТЧ();
	
КонецПроцедуры // ЗаполнитьПоЗаказуПокупателя()

// Обработчик заполнения на основании документа ЗаказНаПроизводство.
//
// Параметры:
//  ДокументСсылкаЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство.
//
Процедура ЗаполнитьПоЗаказуНаПроизводство(ДокументСсылкаЗаказНаПроизводство) Экспорт
	
	ИмяНастройки = "ПоложениеЗаказаНаПроизводствоВДокументахОтгрузки";
	// Основание и настройка документа.
	Если ТипЗнч(ДокументСсылкаЗаказНаПроизводство) = Тип("Структура") 
		И ДокументСсылкаЗаказНаПроизводство.Свойство("МассивЗаказовНаПроизводство") Тогда
		МассивЗаказов = ДокументСсылкаЗаказНаПроизводство.МассивЗаказовНаПроизводство;
		ПоложениеЗаказаНаПроизводство = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
		ПоложениеИсполнителя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
		ПоложениеСтруктурнойЕдиницы = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
	Иначе
		МассивЗаказов = Новый Массив;
		МассивЗаказов.Добавить(ДокументСсылкаЗаказНаПроизводство);
		ПоложениеЗаказаНаПроизводство = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки(ИмяНастройки);
		Если НЕ ЗначениеЗаполнено(ПоложениеЗаказаНаПроизводство) Тогда
			ПоложениеЗаказаНаПроизводство = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
		КонецЕсли;
		Если ПоложениеЗаказаНаПроизводство = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
			ЗаказНаПроизводство = ДокументСсылкаЗаказНаПроизводство;
		КонецЕсли;
	КонецЕсли;
	Если МассивЗаказов.Количество()=0 Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказНаПроизводство.ВидОперации КАК ВидОперации,
	|	ЗаказНаПроизводство.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|ГДЕ
	|	ЗаказНаПроизводство.Ссылка В(&МассивЗаказов)";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если Выборка.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Склад Тогда
		ВызватьИсключение НСтр("ru = 'Сдельный наряд не может быть введен на основании заказа на производство по складу!'");
	КонецЕсли;
	
	Если Выборка.ВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Сборка Тогда
		ЗаполнитьПоЗаказуНаПроизводствоСборка(МассивЗаказов);
	ИначеЕсли Выборка.ВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка Тогда
		ЗаполнитьПоЗаказуНаПроизводствоРазборка(МассивЗаказов);
	КонецЕсли;
	
	Если МассивЗаказов.Количество()=1 Тогда
		ДокументОснование = МассивЗаказов[0];
	КонецЕсли; 
	
	ЗаполнитьЗначенияПереключаемыхРеквизитовВТЧ();
	
КонецПроцедуры

// Обработчик заполнения на основании документа СборкаЗапасов.
//
// Параметры:
//  ДокументСсылкаСборкаЗапасов - ДокументСсылка.СборкаЗапасов.
//
Процедура ЗаполнитьПоСборкеЗапасов(ДокументСсылкаСборкаЗапасов) Экспорт
	
	Если ДокументСсылкаСборкаЗапасов.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Склад Тогда
		ВызватьИсключение НСтр("ru = 'Сдельный наряд не может быть введен на основании производства по складу!'");
	КонецЕсли;
	
	Если ДокументСсылкаСборкаЗапасов.ВидОперации = Перечисления.ВидыОперацийСборкаЗапасов.Сборка Тогда
		ЗаполнитьПоСборкеЗапасовСборка(ДокументСсылкаСборкаЗапасов);
	ИначеЕсли ДокументСсылкаСборкаЗапасов.ВидОперации = Перечисления.ВидыОперацийСборкаЗапасов.Разборка Тогда
		ЗаполнитьПоСборкеЗапасовРазборка(ДокументСсылкаСборкаЗапасов);
	КонецЕсли;
	
	ДокументОснование = ДокументСсылкаСборкаЗапасов;
	Закрыт = Истина;
	
	Если ПоложениеИсполнителя=Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		// Заполнение состава бригады по умолчанию
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.КлючСвязи) Тогда
				ТабличныеЧастиУНФКлиентСервер.ЗаполнитьКлючСвязи(Операции, СтрокаТабличнойЧасти, "КлючСвязи");
			КонецЕсли; 
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Исполнитель) И ТипЗнч(СтрокаТабличнойЧасти.Исполнитель)=Тип("СправочникСсылка.Бригады") Тогда
				ТабличныеЧастиУНФКлиентСервер.УдалитьСтрокиПоКлючуСвязи(СоставБригады, СтрокаТабличнойЧасти);
				ТаблицаСостава = Справочники.Бригады.СоставБригады(СтрокаТабличнойЧасти.Исполнитель, Организация, Дата);
				Для каждого СтрокаСостава Из ТаблицаСостава Цикл
					НоваяСтрока = СоставБригады.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСостава);
					НоваяСтрока.КТУ = 1;
					Если ПоложениеСтруктурнойЕдиницы=Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
						НоваяСтрока.СтруктурнаяЕдиница = СтруктурнаяЕдиница;
					КонецЕсли;
					НоваяСтрока.КлючСвязи = СтрокаТабличнойЧасти.КлючСвязи;
				КонецЦикла; 
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
	ЗаполнитьЗначенияПереключаемыхРеквизитовВТЧ();
	
КонецПроцедуры

// АПК:299-вкл

Процедура ЗаполнитьПоЗаказуНаПродажуПереработку(ДокументСсылкаЗаказПокупателя)
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылкаЗаказПокупателя,
			Новый Структура("Организация, ВидОперации, СтруктурнаяЕдиницаПродажи, ДатаОтгрузки, ОжидаетсяВыборВариантаКП"));
	
	Документы.ЗаказПокупателя.ПроверитьВозможностьВводаНаОснованииЗаказаПокупателя(
		ДокументСсылкаЗаказПокупателя, Новый Структура("ОжидаетсяВыборВариантаКП", ЗначенияРеквизитов.ОжидаетсяВыборВариантаКП));
		
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначенияРеквизитов);
	СтруктурнаяЕдиница = ЗначенияРеквизитов.СтруктурнаяЕдиницаПродажи;
	ДатаЗакрытия = ЗначенияРеквизитов.ДатаОтгрузки;
	
	Период = ?(ЗначениеЗаполнено(ЗначенияРеквизитов.ДатаОтгрузки), ЗначенияРеквизитов.ДатаОтгрузки, ТекущаяДатаСеанса());
	
	Запрос = Новый Запрос;
	
	Документы.ЗаказПокупателя.ДобавитьТаблицуЗапасыВМенеджерВременныхТаблиц(
		ДокументСсылкаЗаказПокупателя, 
		Запрос.МенеджерВременныхТаблиц, 
		Ложь);
	
	ПроизводствоСервер.РазузловатьОперации(Запрос, ДокументСсылкаЗаказПокупателя, "Запасы");
	
	Запрос.УстановитьПараметр("ТаблицаПорядковОкругления", ЦенообразованиеСервер.ТаблицаПорядковОкругления());
	Запрос.УстановитьПараметр("ДокументОснование", ДокументСсылкаЗаказПокупателя);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ВидЦен", ВидЦен);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПорядковОкругления.Порядок КАК Порядок,
	|	ТаблицаПорядковОкругления.Значение КАК Значение
	|ПОМЕСТИТЬ ТаблицаПорядковОкругления
	|ИЗ
	|	&ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗаказПокупателяЗапасы.НомерСтроки КАК НомерСтроки,
	|	ЗаказПокупателяЗапасы.Ссылка КАК ЗаказПокупателя,
	|	ЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяЗапасы.Характеристика КАК Характеристика,
	|	ЗаказПокупателяЗапасы.Партия КАК Партия,
	|	(ЗаказПокупателяЗапасы.Количество - ЗаказПокупателяЗапасы.Резерв) * ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.ЕдиницыИзмерения)
	|				И ЗаказПокупателяЗапасы.ЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ЗаказПокупателяЗапасы.ЕдиницаИзмерения.Коэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ * ЕСТЬNULL(ВЫБОР
	|			КОГДА ТаблицаОпераций.Количество = 0
	|				ТОГДА 1
	|			ИНАЧЕ ТаблицаОпераций.Количество
	|		КОНЕЦ, 1) КАК Количество,
	|	ЗаказПокупателяЗапасы.Спецификация КАК Спецификация,
	|	ЗаказПокупателяЗапасы.Спецификация.ВидПроизводства КАК ВидПроизводства,
	|	ТаблицаОпераций.Операция КАК Операция,
	|	ТаблицаОпераций.Этап КАК Этап,
	|	ВЫБОР
	|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ТаблицаОпераций.Операция.ЕдиницаИзмерения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
	|				И ТаблицаОпераций.Операция <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ТаблицаОпераций.Операция.ФиксированнаяСтоимость
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ФиксированнаяСтоимость,
	|	ЕСТЬNULL(ТаблицаОпераций.НормаВремени, 0) КАК НормаВремени,
	|	ТаблицаОпераций.Уровень КАК Уровень,
	|	ТаблицаОпераций.Порядок КАК Порядок,
	|	ТаблицаОпераций.НомерСтроки КАК НомерСтрокиОперации
	|ПОМЕСТИТЬ Операции
	|ИЗ
	|	ВТЗаказПокупателяЗапасы КАК ЗаказПокупателяЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОпераций КАК ТаблицаОпераций
	|		ПО ЗаказПокупателяЗапасы.Спецификация = ТаблицаОпераций.Спецификация
	|ГДЕ
	|	(ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ИЛИ ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|				И (ЗаказПокупателяЗапасы.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|					ИЛИ ЗаказПокупателяЗапасы.Номенклатура.СпособПополнения = ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Операции.Период КАК Период,
	|	Операции.НомерСтроки КАК НомерСтроки,
	|	Операции.ЗаказПокупателя КАК ЗаказПокупателя,
	|	Операции.Номенклатура КАК Номенклатура,
	|	Операции.Характеристика КАК Характеристика,
	|	Операции.Партия КАК Партия,
	|	Операции.Количество КАК КоличествоПлан,
	|	Операции.Спецификация КАК Спецификация,
	|	Операции.ВидПроизводства КАК ВидПроизводства,
	|	Операции.Операция КАК Операция,
	|	Операции.Этап КАК Этап,
	|	Операции.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
	|	Операции.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Операции.НормаВремени КАК НормаВремени,
	|	(ВЫРАЗИТЬ(ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1), 0) / ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01) КАК ЧИСЛО(15, 0))) * ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01) КАК Расценка
	|ИЗ
	|	Операции КАК Операции
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления
	|			ПО ЦеныНоменклатурыСрезПоследних.ВидЦен.ПорядокОкругления = ТаблицаПорядковОкругления.Порядок
	|		ПО Операции.Операция = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	Операции.НомерСтроки,
	|	Операции.Уровень,
	|	Операции.Порядок,
	|	Операции.НомерСтрокиОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыОстатки.Характеристика КАК Характеристика,
	|	ЗаказыОстатки.Операция КАК Операция,
	|	ЗаказыОстатки.Этап КАК Этап,
	|	СУММА(ЗаказыОстатки.Остаток) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		Операции.Номенклатура КАК Номенклатура,
	|		Операции.Характеристика КАК Характеристика,
	|		Операции.Операция КАК Операция,
	|		Операции.Этап КАК Этап,
	|		Операции.Количество КАК Остаток
	|	ИЗ
	|		Операции КАК Операции
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СдельныеНаряды.Номенклатура,
	|		СдельныеНаряды.Характеристика,
	|		СдельныеНаряды.Операция,
	|		СдельныеНаряды.Этап,
	|		-СдельныеНаряды.КоличествоПланОборот
	|	ИЗ
	|		РегистрНакопления.СдельныеНаряды.Обороты(, , Авто, ЗаказПокупателя = &ДокументОснование) КАК СдельныеНаряды
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаСдельныеНаряды.Номенклатура,
	|		ДвиженияДокументаСдельныеНаряды.Характеристика,
	|		ДвиженияДокументаСдельныеНаряды.Операция,
	|		ДвиженияДокументаСдельныеНаряды.Этап,
	|		ДвиженияДокументаСдельныеНаряды.КоличествоПлан
	|	ИЗ
	|		РегистрНакопления.СдельныеНаряды КАК ДвиженияДокументаСдельныеНаряды
	|	ГДЕ
	|		ДвиженияДокументаСдельныеНаряды.Регистратор = &Ссылка
	|		И ДвиженияДокументаСдельныеНаряды.ЗаказПокупателя = &ДокументОснование) КАК ЗаказыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыОстатки.Номенклатура,
	|	ЗаказыОстатки.Характеристика,
	|	ЗаказыОстатки.Операция,
	|	ЗаказыОстатки.Этап";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ТаблицаОстатков = МассивРезультатов[3].Выгрузить();
	Выборка = МассивРезультатов[2].Выбрать();
	
	ЗаполнитьОперации(Выборка, ТаблицаОстатков);
	
КонецПроцедуры
 
Процедура ЗаполнитьПоЗаказНаряду(ДокументСсылкаЗаказПокупателя)
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылкаЗаказПокупателя,
		Новый Структура("Организация, ВидОперации, СтруктурнаяЕдиницаПродажи, Старт, Финиш, ДатаОтгрузки, ОжидаетсяВыборВариантаКП"));
	
	Документы.ЗаказПокупателя.ПроверитьВозможностьВводаНаОснованииЗаказаПокупателя(
		ДокументСсылкаЗаказПокупателя, Новый Структура("ОжидаетсяВыборВариантаКП", ЗначенияРеквизитов.ОжидаетсяВыборВариантаКП));
	
	Организация = ЗначенияРеквизитов.Организация;
	СтруктурнаяЕдиница = ЗначенияРеквизитов.СтруктурнаяЕдиницаПродажи;
	
	ДатаЗакрытия = ЗначенияРеквизитов.Финиш;
	Период = ?(ЗначениеЗаполнено(ЗначенияРеквизитов.Старт), ЗначенияРеквизитов.Старт, ТекущаяДатаСеанса());
	
	Запрос = Новый Запрос;
	ПроизводствоСервер.РазузловатьОперации(Запрос, ДокументСсылкаЗаказПокупателя, "Работы");
	
	Запрос.УстановитьПараметр("ТаблицаПорядковОкругления", ЦенообразованиеСервер.ТаблицаПорядковОкругления());
	Запрос.УстановитьПараметр("ДокументОснование", ДокументСсылкаЗаказПокупателя);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ВидЦен", ВидЦен);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПорядковОкругления.Порядок КАК Порядок,
	|	ТаблицаПорядковОкругления.Значение КАК Значение
	|ПОМЕСТИТЬ ТаблицаПорядковОкругления
	|ИЗ
	|	&ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗаказПокупателяРаботы.НомерСтроки КАК НомерСтроки,
	|	ЗаказПокупателяРаботы.Ссылка КАК ЗаказПокупателя,
	|	ЗаказПокупателяРаботы.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяРаботы.Характеристика КАК Характеристика,
	|	ЗаказПокупателяРаботы.Количество * ЗаказПокупателяРаботы.Кратность * ЗаказПокупателяРаботы.Коэффициент * ЕСТЬNULL(ВЫБОР
	|			КОГДА ТаблицаОпераций.Количество = 0
	|				ТОГДА 1
	|			ИНАЧЕ ТаблицаОпераций.Количество
	|		КОНЕЦ, 1) КАК Количество,
	|	ЗаказПокупателяРаботы.Спецификация КАК Спецификация,
	|	ТаблицаОпераций.Операция КАК Операция,
	|	ТаблицаОпераций.Этап КАК Этап,
	|	ВЫБОР
	|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ТаблицаОпераций.Операция.ЕдиницаИзмерения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
	|				И ТаблицаОпераций.Операция <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ТаблицаОпераций.Операция.ФиксированнаяСтоимость
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ФиксированнаяСтоимость,
	|	ЕСТЬNULL(ТаблицаОпераций.НормаВремени, 0) КАК НормаВремени,
	|	ТаблицаОпераций.Уровень КАК Уровень,
	|	ТаблицаОпераций.Порядок КАК Порядок,
	|	ТаблицаОпераций.НомерСтроки КАК НомерСтрокиОперации
	|ПОМЕСТИТЬ Операции
	|ИЗ
	|	Документ.ЗаказПокупателя.Работы КАК ЗаказПокупателяРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОпераций КАК ТаблицаОпераций
	|		ПО ЗаказПокупателяРаботы.Спецификация = ТаблицаОпераций.Спецификация
	|ГДЕ
	|	ЗаказПокупателяРаботы.Ссылка = &ДокументОснование
	|	И ЗаказПокупателяРаботы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Операции.Период КАК Период,
	|	Операции.НомерСтроки КАК НомерСтроки,
	|	Операции.ЗаказПокупателя КАК ЗаказПокупателя,
	|	Операции.Номенклатура КАК Номенклатура,
	|	Операции.Характеристика КАК Характеристика,
	|	Операции.Количество КАК КоличествоПлан,
	|	Операции.Спецификация КАК Спецификация,
	|	ЗНАЧЕНИЕ(Справочник.ВидыПроизводства.ПустаяСсылка) КАК ВидПроизводства,
	|	Операции.Операция КАК Операция,
	|	Операции.Этап КАК Этап,
	|	Операции.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
	|	Операции.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Операции.НормаВремени КАК НормаВремени,
	|	(ВЫРАЗИТЬ(ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1), 0) / ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01) КАК ЧИСЛО(15, 0))) * ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01) КАК Расценка
	|ИЗ
	|	Операции КАК Операции
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления
	|			ПО ЦеныНоменклатурыСрезПоследних.ВидЦен.ПорядокОкругления = ТаблицаПорядковОкругления.Порядок
	|		ПО Операции.Операция = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	Операции.НомерСтроки,
	|	Операции.Уровень,
	|	Операции.Порядок,
	|	Операции.НомерСтрокиОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыОстатки.Характеристика КАК Характеристика,
	|	ЗаказыОстатки.Операция КАК Операция,
	|	ЗаказыОстатки.Этап КАК Этап,
	|	СУММА(ЗаказыОстатки.Остаток) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		Операции.Номенклатура КАК Номенклатура,
	|		Операции.Характеристика КАК Характеристика,
	|		Операции.Операция КАК Операция,
	|		Операции.Этап КАК Этап,
	|		Операции.Количество КАК Остаток
	|	ИЗ
	|		Операции КАК Операции
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СдельныеНаряды.Номенклатура,
	|		СдельныеНаряды.Характеристика,
	|		СдельныеНаряды.Операция,
	|		СдельныеНаряды.Этап,
	|		-ВЫБОР
	|			КОГДА СдельныеНаряды.КоличествоФактОборот = 0
	|				ТОГДА СдельныеНаряды.КоличествоПланОборот
	|			ИНАЧЕ СдельныеНаряды.КоличествоФактОборот
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.СдельныеНаряды.Обороты(, , Авто, ЗаказПокупателя = &ДокументОснование) КАК СдельныеНаряды
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаСдельныеНаряды.Номенклатура,
	|		ДвиженияДокументаСдельныеНаряды.Характеристика,
	|		ДвиженияДокументаСдельныеНаряды.Операция,
	|		ДвиженияДокументаСдельныеНаряды.Этап,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаСдельныеНаряды.КоличествоФакт = 0
	|				ТОГДА ДвиженияДокументаСдельныеНаряды.КоличествоПлан
	|			ИНАЧЕ ДвиженияДокументаСдельныеНаряды.КоличествоФакт
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.СдельныеНаряды КАК ДвиженияДокументаСдельныеНаряды
	|	ГДЕ
	|		ДвиженияДокументаСдельныеНаряды.Регистратор = &Ссылка
	|		И ДвиженияДокументаСдельныеНаряды.ЗаказПокупателя = &ДокументОснование) КАК ЗаказыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыОстатки.Номенклатура,
	|	ЗаказыОстатки.Характеристика,
	|	ЗаказыОстатки.Операция,
	|	ЗаказыОстатки.Этап";
		
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ТаблицаОстатков = МассивРезультатов[3].Выгрузить();
	Выборка = МассивРезультатов[2].Выбрать();
	
	ЗаполнитьОперации(Выборка, ТаблицаОстатков);
	
КонецПроцедуры
 
Процедура ЗаполнитьПоЗаказуНаПроизводствоСборка(МассивЗаказов)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказНаПроизводство.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ЗаказНаПроизводство.СтруктурнаяЕдиницаОпераций = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|				И ЗаказНаПроизводство.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗаказНаПроизводство.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗаказНаПроизводство.СтруктурнаяЕдиницаОпераций
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ЗаказНаПроизводство.Исполнитель КАК Исполнитель,
	|	ЗаказНаПроизводство.ЗапланированыОперации КАК ЗапланированыОперации,
	|	МАКСИМУМ(ЗаказНаПроизводство.Финиш) КАК Финиш,
	|	ЗаказНаПроизводство.ПоложениеИсполнителя КАК ПоложениеИсполнителя,
	|	ЗаказНаПроизводство.ПоложениеСтруктурнойЕдиницыОпераций КАК ПоложениеСтруктурнойЕдиницы
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|ГДЕ
	|	ЗаказНаПроизводство.Ссылка В(&МассивЗаказов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказНаПроизводство.Организация,
	|	ВЫБОР
	|		КОГДА ЗаказНаПроизводство.СтруктурнаяЕдиницаОпераций = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|				И ЗаказНаПроизводство.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗаказНаПроизводство.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗаказНаПроизводство.СтруктурнаяЕдиницаОпераций
	|	КОНЕЦ,
	|	ЗаказНаПроизводство.Исполнитель,
	|	ЗаказНаПроизводство.ЗапланированыОперации,
	|	ЗаказНаПроизводство.ПоложениеИсполнителя,
	|	ЗаказНаПроизводство.ПоложениеСтруктурнойЕдиницыОпераций";
	ВыборкаШапка = Запрос.Выполнить().Выбрать();
	ВыборкаШапка.Следующий();
	Период = ?(ЗначениеЗаполнено(ВыборкаШапка.Финиш), ВыборкаШапка.Финиш, ТекущаяДатаСеанса());
	
	ИменаРеквизитов = "Организация";
	Если МассивЗаказов.Количество() = 1 Тогда
		ИменаРеквизитов = ИменаРеквизитов + ", СтруктурнаяЕдиница, Исполнитель, ПоложениеИсполнителя, ПоложениеСтруктурнойЕдиницы";
	КонецЕсли; 
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка, ИменаРеквизитов);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаПорядковОкругления", ЦенообразованиеСервер.ТаблицаПорядковОкругления());
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ВидЦен", ВидЦен);
	
	ПолучитьСоставСпецификацийЗаказовПокупателей(Запрос);
	
	Если ВыборкаШапка.ЗапланированыОперации Тогда
		
		Закрыт = Истина;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказНаПроизводствоОперации.Ссылка КАК Ссылка,
		|	ЗаказНаПроизводствоОперации.КлючСвязиПродукция КАК КлючСвязиПродукция,
		|	ЗаказНаПроизводствоОперации.Ссылка.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ЗаказНаПроизводствоОперации.СтруктурнаяЕдиница = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|				И Шапка.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
		|			ТОГДА Шапка.СтруктурнаяЕдиница
		|		ИНАЧЕ ЗаказНаПроизводствоОперации.СтруктурнаяЕдиница
		|	КОНЕЦ КАК СтруктурнаяЕдиница,
		|	ЗаказНаПроизводствоОперации.Ссылка.Финиш КАК ДатаЗакрытия,
		|	ЗаказНаПроизводствоОперации.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ЗаказНаПроизводствоОперации.НомерСтроки КАК НомерСтрокиОперации,
		|	ЕСТЬNULL(ЗаказНаПроизводствоОперации.КоличествоПлан / ВЫБОР
		|			КОГДА ЗаказНаПроизводствоПродукция.Количество = 0
		|				ТОГДА 1
		|			ИНАЧЕ ЗаказНаПроизводствоПродукция.Количество * ВЫБОР
		|					КОГДА ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
		|						ТОГДА ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения.Коэффициент
		|					ИНАЧЕ 1
		|				КОНЕЦ
		|		КОНЕЦ, 0) КАК Количество,
		|	ЕСТЬNULL(ЗаказНаПроизводствоПродукция.Количество * ВЫБОР
		|			КОГДА ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
		|				ТОГДА ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения.Коэффициент
		|			ИНАЧЕ 1
		|		КОНЕЦ, 0) КАК КоличествоПродукции,
		|	ЗаказНаПроизводствоОперации.Операция КАК Операция,
		|	ЗаказНаПроизводствоОперации.Операция.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
		|	ЗаказНаПроизводствоОперации.Этап КАК Этап,
		|	ЗаказНаПроизводствоОперации.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЗаказНаПроизводствоОперации.НормаВремени КАК НормаВремени,
		|	ЗаказНаПроизводствоОперации.КлючСвязи КАК КлючСвязи,
		|	ЗаказНаПроизводствоОперации.Исполнитель КАК Исполнитель,
		|	ЗаказНаПроизводствоПродукция.НомерСтроки КАК НомерСтроки,
		|	ЗаказНаПроизводствоПродукция.Номенклатура КАК Номенклатура,
		|	ЗаказНаПроизводствоПродукция.Характеристика КАК Характеристика,
		|	ЗаказНаПроизводствоПродукция.Спецификация КАК Спецификация,
		|	ЗаказНаПроизводствоПродукция.Спецификация.ВидПроизводства КАК ВидПроизводства,
		|	ЗаказНаПроизводствоПродукция.Партия КАК Партия,
		|	ЗаказНаПроизводствоПродукция.ПодразделениеЗавершающегоЭтапа КАК ПодразделениеЗавершающегоЭтапа
		|ПОМЕСТИТЬ ТаблицаОпераций
		|ИЗ
		|	Документ.ЗаказНаПроизводство.Операции КАК ЗаказНаПроизводствоОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
		|		ПО ЗаказНаПроизводствоОперации.КлючСвязиПродукция = ЗаказНаПроизводствоПродукция.КлючСвязи
		|			И ЗаказНаПроизводствоОперации.Ссылка = ЗаказНаПроизводствоПродукция.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство КАК Шапка
		|		ПО ЗаказНаПроизводствоОперации.Ссылка = Шапка.Ссылка
		|ГДЕ
		|	ЗаказНаПроизводствоОперации.Ссылка В(&МассивЗаказов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОпераций.Ссылка КАК ЗаказНаПроизводство,
		|	ТаблицаОпераций.Организация КАК Организация,
		|	ТаблицаОпераций.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ТаблицаОпераций.ДатаЗакрытия КАК ДатаЗакрытия,
		|	&Период КАК Период,
		|	ТаблицаОпераций.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ТаблицаОпераций.НомерСтроки КАК НомерСтроки,
		|	ТаблицаОпераций.Номенклатура КАК Номенклатура,
		|	ТаблицаОпераций.Характеристика КАК Характеристика,
		|	ТаблицаОпераций.Количество * ТаблицаОпераций.КоличествоПродукции КАК Количество,
		|	ТаблицаОпераций.Спецификация КАК Спецификация,
		|	ТаблицаОпераций.ВидПроизводства КАК ВидПроизводства,
		|	ТаблицаОпераций.Партия КАК Партия,
		|	ТаблицаОпераций.Операция КАК Операция,
		|	ТаблицаОпераций.Этап КАК Этап,
		|	ТаблицаОпераций.ПодразделениеЗавершающегоЭтапа КАК ПодразделениеЗавершающегоЭтапа,
		|	ТаблицаОпераций.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
		|				И ТаблицаОпераций.Операция <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			ТОГДА ТаблицаОпераций.ФиксированнаяСтоимость
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ФиксированнаяСтоимость,
		|	ТаблицаОпераций.НормаВремени КАК НормаВремени,
		|	ТаблицаОпераций.КлючСвязи КАК КлючСвязи,
		|	ТаблицаОпераций.Исполнитель КАК Исполнитель,
		|	0 КАК Уровень,
		|	0 КАК Порядок,
		|	ТаблицаОпераций.НомерСтрокиОперации КАК НомерСтрокиОперации
		|ПОМЕСТИТЬ Операции
		|ИЗ
		|	ТаблицаОпераций КАК ТаблицаОпераций";
		
	Иначе
		
		ПроизводствоСервер.РазузловатьОперации(Запрос, МассивЗаказов, "Продукция");
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказНаПроизводствоПродукция.Ссылка КАК ЗаказНаПроизводство,
		|	ЗаказНаПроизводствоПродукция.Ссылка.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА Шапка.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
		|			ТОГДА Шапка.СтруктурнаяЕдиница
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|	КОНЕЦ КАК СтруктурнаяЕдиница,
		|	ЗаказНаПроизводствоПродукция.Ссылка.Финиш КАК ДатаЗакрытия,
		|	&Период КАК Период,
		|	ЗаказНаПроизводствоПродукция.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ЗаказНаПроизводствоПродукция.НомерСтроки КАК НомерСтроки,
		|	ЗаказНаПроизводствоПродукция.Номенклатура КАК Номенклатура,
		|	ЗаказНаПроизводствоПродукция.Характеристика КАК Характеристика,
		|	ЗаказНаПроизводствоПродукция.Количество * ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
		|			ТОГДА 1
		|		ИНАЧЕ ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения.Коэффициент
		|	КОНЕЦ * ЕСТЬNULL(ВЫБОР
		|			КОГДА ТаблицаОпераций.Количество = 0
		|				ТОГДА 1
		|			ИНАЧЕ ТаблицаОпераций.Количество
		|		КОНЕЦ, 1) КАК Количество,
		|	ЗаказНаПроизводствоПродукция.Спецификация КАК Спецификация,
		|	ЗаказНаПроизводствоПродукция.Спецификация.ВидПроизводства КАК ВидПроизводства,
		|	ЗаказНаПроизводствоПродукция.Партия КАК Партия,
		|	ТаблицаОпераций.Операция КАК Операция,
		|	ТаблицаОпераций.Этап КАК Этап,
		|	ЗаказНаПроизводствоПродукция.ПодразделениеЗавершающегоЭтапа КАК ПодразделениеЗавершающегоЭтапа,
		|	ВЫБОР
		|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
		|			ТОГДА ТаблицаОпераций.Операция.ЕдиницаИзмерения
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
		|	КОНЕЦ КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
		|				И ТаблицаОпераций.Операция <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			ТОГДА ТаблицаОпераций.Операция.ФиксированнаяСтоимость
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ФиксированнаяСтоимость,
		|	ЕСТЬNULL(ТаблицаОпераций.НормаВремени, 0) КАК НормаВремени,
		|	0 КАК КлючСвязи,
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Исполнитель,
		|	ТаблицаОпераций.Уровень КАК Уровень,
		|	ТаблицаОпераций.Порядок КАК Порядок,
		|	ТаблицаОпераций.НомерСтроки КАК НомерСтрокиОперации
		|ПОМЕСТИТЬ Операции
		|ИЗ
		|	Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОпераций КАК ТаблицаОпераций
		|		ПО ЗаказНаПроизводствоПродукция.Спецификация = ТаблицаОпераций.Спецификация
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство КАК Шапка
		|		ПО ЗаказНаПроизводствоПродукция.Ссылка = Шапка.Ссылка
		|ГДЕ
		|	ЗаказНаПроизводствоПродукция.Ссылка В(&МассивЗаказов)
		|	И ЗаказНаПроизводствоПродукция.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))";
		
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПорядковОкругления.Порядок КАК Порядок,
	|	ТаблицаПорядковОкругления.Значение КАК Значение
	|ПОМЕСТИТЬ ТаблицаПорядковОкругления
	|ИЗ
	|	&ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Операции.ЗаказНаПроизводство КАК Основание,
	|	Операции.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Операции.Организация КАК Организация,
	|	Операции.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	Операции.Исполнитель КАК Исполнитель,
	|	Операции.ДатаЗакрытия КАК ДатаЗакрытия,
	|	Операции.Период КАК Период,
	|	Операции.ЗаказПокупателя КАК ЗаказПокупателя,
	|	Операции.НомерСтроки КАК НомерСтроки,
	|	Операции.КлючСвязи КАК КлючСвязи,
	|	Операции.Номенклатура КАК Номенклатура,
	|	Операции.Характеристика КАК Характеристика,
	|	Операции.Количество КАК КоличествоПлан,
	|	Операции.Спецификация КАК Спецификация,
	|	Операции.ВидПроизводства КАК ВидПроизводства,
	|	Операции.Партия КАК Партия,
	|	Операции.Операция КАК Операция,
	|	Операции.Этап КАК Этап,
	|	Операции.ПодразделениеЗавершающегоЭтапа КАК ПодразделениеЗавершающегоЭтапа,
	|	Операции.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
	|	Операции.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Операции.НормаВремени КАК НормаВремени,
	|	(ВЫРАЗИТЬ(ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1), 0) / ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01) КАК ЧИСЛО(15, 0))) * ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01) КАК Расценка
	|ИЗ
	|	Операции КАК Операции
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления
	|			ПО ЦеныНоменклатурыСрезПоследних.ВидЦен.ПорядокОкругления = ТаблицаПорядковОкругления.Порядок
	|		ПО Операции.Операция = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	Операции.ЗаказНаПроизводство,
	|	Операции.НомерСтроки,
	|	Операции.Уровень,
	|	Операции.Порядок,
	|	Операции.НомерСтрокиОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыОстатки.ЗаказНаПроизводство КАК Основание,
	|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыОстатки.Характеристика КАК Характеристика,
	|	ЗаказыОстатки.Операция КАК Операция,
	|	ЗаказыОстатки.Этап КАК Этап,
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		Операции.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|		Операции.Номенклатура КАК Номенклатура,
	|		Операции.Характеристика КАК Характеристика,
	|		Операции.Операция КАК Операция,
	|		Операции.Этап КАК Этап,
	|		Операции.Количество КАК КоличествоОстаток
	|	ИЗ
	|		Операции КАК Операции
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СдельныеНаряды.ДокументПроизводства,
	|		СдельныеНаряды.Номенклатура,
	|		СдельныеНаряды.Характеристика,
	|		СдельныеНаряды.Операция,
	|		СдельныеНаряды.Этап,
	|		-СдельныеНаряды.КоличествоПланОборот
	|	ИЗ
	|		РегистрНакопления.СдельныеНаряды.Обороты(, , Авто, ДокументПроизводства В (&МассивЗаказов)) КАК СдельныеНаряды
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаСдельныеНаряды.ДокументПроизводства,
	|		ДвиженияДокументаСдельныеНаряды.Номенклатура,
	|		ДвиженияДокументаСдельныеНаряды.Характеристика,
	|		ДвиженияДокументаСдельныеНаряды.Операция,
	|		ДвиженияДокументаСдельныеНаряды.Этап,
	|		ДвиженияДокументаСдельныеНаряды.КоличествоПлан
	|	ИЗ
	|		РегистрНакопления.СдельныеНаряды КАК ДвиженияДокументаСдельныеНаряды
	|	ГДЕ
	|		ДвиженияДокументаСдельныеНаряды.Регистратор = &Ссылка
	|		И ДвиженияДокументаСдельныеНаряды.ДокументПроизводства В(&МассивЗаказов)) КАК ЗаказыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыОстатки.ЗаказНаПроизводство,
	|	ЗаказыОстатки.Номенклатура,
	|	ЗаказыОстатки.Характеристика,
	|	ЗаказыОстатки.Операция,
	|	ЗаказыОстатки.Этап";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ТаблицаОстатков = МассивРезультатов[2].Выгрузить();
	Выборка = МассивРезультатов[1].Выбрать();
	
	ОбъединитьСоставыСпецификацийЗаказов(Запрос);
	
	СоответствиеКлючейСвязи = Новый Соответствие;
	ТаблицаОстатковЗаказа = ОстаткиЗаказа(МассивЗаказов, Запрос);
	ЗаполнитьОперации(Выборка, ТаблицаОстатков, ТаблицаОстатковЗаказа, Закрыт, СоответствиеКлючейСвязи);
	
	ЗаполнитьСоставБригадыПоЗаказуНаПроизводство(МассивЗаказов, СоответствиеКлючейСвязи);
	
	ДатаЗакрытия = '0001-01-01';
	Если Закрыт Тогда
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			ДатаЗакрытия = Макс(ДатаЗакрытия, СтрокаТабличнойЧасти.Период);
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуНаПроизводствоРазборка(МассивЗаказов)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказНаПроизводство.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ЗаказНаПроизводство.СтруктурнаяЕдиницаОпераций = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|				И ЗаказНаПроизводство.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗаказНаПроизводство.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗаказНаПроизводство.СтруктурнаяЕдиницаОпераций
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ЗаказНаПроизводство.Исполнитель КАК Исполнитель,
	|	ЗаказНаПроизводство.ЗапланированыОперации КАК ЗапланированыОперации,
	|	МАКСИМУМ(ЗаказНаПроизводство.Финиш) КАК Финиш,
	|	ЗаказНаПроизводство.ПоложениеИсполнителя КАК ПоложениеИсполнителя,
	|	ЗаказНаПроизводство.ПоложениеСтруктурнойЕдиницыОпераций КАК ПоложениеСтруктурнойЕдиницы
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|ГДЕ
	|	ЗаказНаПроизводство.Ссылка В(&МассивЗаказов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказНаПроизводство.Организация,
	|	ВЫБОР
	|		КОГДА ЗаказНаПроизводство.СтруктурнаяЕдиницаОпераций = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|				И ЗаказНаПроизводство.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗаказНаПроизводство.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗаказНаПроизводство.СтруктурнаяЕдиницаОпераций
	|	КОНЕЦ,
	|	ЗаказНаПроизводство.Исполнитель,
	|	ЗаказНаПроизводство.ЗапланированыОперации,
	|	ЗаказНаПроизводство.ПоложениеИсполнителя,
	|	ЗаказНаПроизводство.ПоложениеСтруктурнойЕдиницыОпераций";
	ВыборкаШапка = Запрос.Выполнить().Выбрать();
	ВыборкаШапка.Следующий();
	Период = ?(ЗначениеЗаполнено(ВыборкаШапка.Финиш), ВыборкаШапка.Финиш, ТекущаяДатаСеанса());
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка, "Организация" + ?(МассивЗаказов.Количество()=1, ", СтруктурнаяЕдиница, Исполнитель, ПоложениеИсполнителя, ПоложениеСтруктурнойЕдиницы", ""));
	
	ТаблицаСостав = ПустаяТаблицаСостава();
	
	ЗаполнитьТаблицуСостава("ЗаказНаПроизводство", МассивЗаказов, ТаблицаСостав);
	
	ТекСпецификация = Неопределено;
	НомерСтроки = 0;
	ТаблицаСостав.Сортировать("БазоваяСпецификация");
	Для каждого СтрокаСостава Из ТаблицаСостав Цикл
		Если СтрокаСостава.БазоваяСпецификация<>ТекСпецификация Тогда
			НомерСтроки = 0;
			ТекСпецификация = СтрокаСостава.БазоваяСпецификация;
		КонецЕсли;
		НомерСтроки = НомерСтроки+1;
		СтрокаСостава.НомерСтроки = НомерСтроки;
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаСостав", ТаблицаСостав);
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Период);
	
	Если ВыборкаШапка.ЗапланированыОперации Тогда
		
		Закрыт = Истина;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказНаПроизводствоОперации.Ссылка КАК Ссылка,
		|	ЗаказНаПроизводствоОперации.КлючСвязиПродукция КАК КлючСвязиПродукция,
		|	ЗаказНаПроизводствоОперации.Ссылка.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ЗаказНаПроизводствоОперации.СтруктурнаяЕдиница = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|				И Шапка.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
		|			ТОГДА Шапка.СтруктурнаяЕдиница
		|		ИНАЧЕ ЗаказНаПроизводствоОперации.СтруктурнаяЕдиница
		|	КОНЕЦ КАК СтруктурнаяЕдиница,
		|	ЗаказНаПроизводствоОперации.Ссылка.Финиш КАК ДатаЗакрытия,
		|	ЗаказНаПроизводствоОперации.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ЗаказНаПроизводствоОперации.НомерСтроки КАК НомерСтрокиОперации,
		|	ЗаказНаПроизводствоОперации.КоличествоПлан / ВЫБОР
		|		КОГДА ЗаказНаПроизводствоПродукция.Количество = 0
		|			ТОГДА 1
		|		ИНАЧЕ ЗаказНаПроизводствоПродукция.Количество * ВЫБОР
		|				КОГДА ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
		|					ТОГДА ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения.Коэффициент
		|				ИНАЧЕ 1
		|			КОНЕЦ
		|	КОНЕЦ КАК Количество,
		|	ЗаказНаПроизводствоПродукция.Количество * ВЫБОР
		|		КОГДА ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
		|			ТОГДА ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения.Коэффициент
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК КоличествоПродукции,
		|	ЗаказНаПроизводствоОперации.Операция КАК Операция,
		|	ЗаказНаПроизводствоОперации.Операция.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
		|	ЗаказНаПроизводствоОперации.Этап КАК Этап,
		|	ЗаказНаПроизводствоОперации.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЗаказНаПроизводствоОперации.НормаВремени КАК НормаВремени,
		|	ЗаказНаПроизводствоОперации.КлючСвязи КАК КлючСвязи,
		|	ЗаказНаПроизводствоОперации.Исполнитель КАК Исполнитель,
		|	ЗаказНаПроизводствоПродукция.НомерСтроки КАК НомерСтроки,
		|	ЗаказНаПроизводствоПродукция.Номенклатура КАК Номенклатура,
		|	ЗаказНаПроизводствоПродукция.Характеристика КАК Характеристика,
		|	ЗаказНаПроизводствоПродукция.Спецификация КАК Спецификация,
		|	ЗаказНаПроизводствоПродукция.Спецификация.ВидПроизводства КАК ВидПроизводства,
		|	ЗаказНаПроизводствоПродукция.Партия КАК Партия,
		|	ЗаказНаПроизводствоПродукция.ПодразделениеЗавершающегоЭтапа КАК ПодразделениеЗавершающегоЭтапа
		|ПОМЕСТИТЬ ТаблицаОпераций
		|ИЗ
		|	Документ.ЗаказНаПроизводство.Операции КАК ЗаказНаПроизводствоОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
		|		ПО ЗаказНаПроизводствоОперации.КлючСвязиПродукция = ЗаказНаПроизводствоПродукция.КлючСвязи
		|			И ЗаказНаПроизводствоОперации.Ссылка = ЗаказНаПроизводствоПродукция.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство КАК Шапка
		|		ПО ЗаказНаПроизводствоОперации.Ссылка = Шапка.Ссылка
		|ГДЕ
		|	ЗаказНаПроизводствоОперации.Ссылка В(&МассивЗаказов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОпераций.Ссылка КАК ЗаказНаПроизводство,
		|	ТаблицаОпераций.Организация КАК Организация,
		|	ТаблицаОпераций.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ТаблицаОпераций.ДатаЗакрытия КАК ДатаЗакрытия,
		|	&Период КАК Период,
		|	ТаблицаОпераций.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ТаблицаОпераций.НомерСтроки КАК НомерСтроки,
		|	ТаблицаОпераций.Номенклатура КАК Номенклатура,
		|	ТаблицаОпераций.Характеристика КАК Характеристика,
		|	ТаблицаОпераций.Количество КАК Количество,
		|	ТаблицаОпераций.Спецификация КАК Спецификация,
		|	ТаблицаОпераций.ВидПроизводства КАК ВидПроизводства,
		|	ТаблицаОпераций.Партия КАК Партия,
		|	ТаблицаОпераций.Операция КАК Операция,
		|	ТаблицаОпераций.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
		|				И ТаблицаОпераций.Операция <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			ТОГДА ТаблицаОпераций.ФиксированнаяСтоимость
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ФиксированнаяСтоимость,
		|	ТаблицаОпераций.НормаВремени КАК НормаВремени,
		|	ТаблицаОпераций.КлючСвязи КАК КлючСвязи,
		|	ТаблицаОпераций.Исполнитель КАК Исполнитель,
		|	ТаблицаОпераций.НомерСтрокиОперации КАК НомерСтрокиОперации
		|ПОМЕСТИТЬ Операции
		|ИЗ
		|	ТаблицаОпераций КАК ТаблицаОпераций";
		
	Иначе
		
		ПроизводствоСервер.РазузловатьОперации(Запрос, МассивЗаказов, "Продукция");
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказНаПроизводствоПродукция.Ссылка КАК ЗаказНаПроизводство,
		|	ЗаказНаПроизводствоПродукция.Ссылка.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА Шапка.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
		|			ТОГДА Шапка.СтруктурнаяЕдиница
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|	КОНЕЦ КАК СтруктурнаяЕдиница,
		|	ЗаказНаПроизводствоПродукция.Ссылка.Финиш КАК ДатаЗакрытия,
		|	&Период КАК Период,
		|	ЗаказНаПроизводствоПродукция.Ссылка.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ЗаказНаПроизводствоПродукция.НомерСтроки КАК НомерСтроки,
		|	ЗаказНаПроизводствоПродукция.Номенклатура КАК Номенклатура,
		|	ЗаказНаПроизводствоПродукция.Характеристика КАК Характеристика,
		|	ЗаказНаПроизводствоПродукция.Количество * ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения) = ТИП(Справочник.ЕдиницыИзмерения)
		|				И ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|			ТОГДА ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения.Коэффициент
		|		ИНАЧЕ 1
		|	КОНЕЦ * ЕСТЬNULL(ВЫБОР
		|			КОГДА ТаблицаОпераций.Количество = 0
		|				ТОГДА 1
		|			ИНАЧЕ ТаблицаОпераций.Количество
		|		КОНЕЦ, 1) КАК Количество,
		|	ЗаказНаПроизводствоПродукция.Спецификация КАК Спецификация,
		|	ЗНАЧЕНИЕ(Справочник.ВидыПроизводства.ПустаяСсылка) КАК ВидПроизводства,
		|	ЗаказНаПроизводствоПродукция.Партия КАК Партия,
		|	ТаблицаОпераций.Операция КАК Операция,
		|	ВЫБОР
		|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
		|				И ТаблицаОпераций.Операция <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			ТОГДА ТаблицаОпераций.Операция.ФиксированнаяСтоимость
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ФиксированнаяСтоимость,
		|	ВЫБОР
		|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
		|			ТОГДА ТаблицаОпераций.Операция.ЕдиницаИзмерения
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
		|	КОНЕЦ КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ТаблицаОпераций.НормаВремени, 0) КАК НормаВремени,
		|	0 КАК КлючСвязи,
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Исполнитель,
		|	ТаблицаОпераций.НомерСтроки КАК НомерСтрокиОперации
		|ПОМЕСТИТЬ Операции
		|ИЗ
		|	Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОпераций КАК ТаблицаОпераций
		|		ПО ЗаказНаПроизводствоПродукция.Спецификация = ТаблицаОпераций.Спецификация
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство КАК Шапка
		|		ПО ЗаказНаПроизводствоПродукция.Ссылка = Шапка.Ссылка
		|ГДЕ
		|	ЗаказНаПроизводствоПродукция.Ссылка В(&МассивЗаказов)
		|	И ЗаказНаПроизводствоПродукция.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))";
		
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Выборка = ПолучитьВыборкуОперацииРазборка(Запрос);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказыОстатки.ЗаказНаПроизводство КАК Основание,
	|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыОстатки.Характеристика КАК Характеристика,
	|	ЗаказыОстатки.Операция КАК Операция,
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОперацииИСостав.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|		ОперацииИСостав.Номенклатура КАК Номенклатура,
	|		ОперацииИСостав.Характеристика КАК Характеристика,
	|		ОперацииИСостав.Операция КАК Операция,
	|		ОперацииИСостав.Количество КАК КоличествоОстаток
	|	ИЗ
	|		ОперацииИСостав КАК ОперацииИСостав
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СдельныеНаряды.ДокументПроизводства,
	|		СдельныеНаряды.Номенклатура,
	|		СдельныеНаряды.Характеристика,
	|		СдельныеНаряды.Операция,
	|		-СдельныеНаряды.КоличествоПланОборот
	|	ИЗ
	|		РегистрНакопления.СдельныеНаряды.Обороты(, , Авто, ДокументПроизводства В (&МассивЗаказов)) КАК СдельныеНаряды
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаСдельныеНаряды.ДокументПроизводства,
	|		ДвиженияДокументаСдельныеНаряды.Номенклатура,
	|		ДвиженияДокументаСдельныеНаряды.Характеристика,
	|		ДвиженияДокументаСдельныеНаряды.Операция,
	|		ДвиженияДокументаСдельныеНаряды.КоличествоПлан
	|	ИЗ
	|		РегистрНакопления.СдельныеНаряды КАК ДвиженияДокументаСдельныеНаряды
	|	ГДЕ
	|		ДвиженияДокументаСдельныеНаряды.Регистратор = &Ссылка
	|		И ДвиженияДокументаСдельныеНаряды.ДокументПроизводства В(&МассивЗаказов)) КАК ЗаказыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыОстатки.ЗаказНаПроизводство,
	|	ЗаказыОстатки.Номенклатура,
	|	ЗаказыОстатки.Характеристика,
	|	ЗаказыОстатки.Операция";
	ТаблицаОстатков = Запрос.Выполнить().Выгрузить();
	
	СоответствиеКлючейСвязи = Новый Соответствие;
	ЗаполнитьОперации(Выборка, ТаблицаОстатков, , Закрыт, СоответствиеКлючейСвязи);
	
	ЗаполнитьСоставБригадыПоЗаказуНаПроизводство(МассивЗаказов, СоответствиеКлючейСвязи);
	
	ДатаЗакрытия = '0001-01-01';
	Если Закрыт Тогда
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			ДатаЗакрытия = Макс(ДатаЗакрытия, СтрокаТабличнойЧасти.Период);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьПоСборкеЗапасовСборка(ДокументСсылкаСборкаЗапасов)
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылкаСборкаЗапасов,
			"Организация, СтруктурнаяЕдиница, Дата, ЗаказНаПроизводство, ЗаказНаПроизводство.ЗапланированыОперации, ЗаказНаПроизводство.ПоложениеИсполнителя, ЗаказНаПроизводство.Исполнитель");
			
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначенияРеквизитов);
	ДатаЗакрытия = Дата;
	ПоложениеЗаказаНаПроизводство = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
	Период = ?(ЗначениеЗаполнено(ЗначенияРеквизитов.Дата), ЗначенияРеквизитов.Дата, ТекущаяДатаСеанса());
	Если ЗначениеЗаполнено(ЗначенияРеквизитов.ЗаказНаПроизводство) Тогда
		ПоложениеИсполнителя = ЗначенияРеквизитов.ЗаказНаПроизводствоПоложениеИсполнителя;
		Исполнитель = ЗначенияРеквизитов.ЗаказНаПроизводствоИсполнитель;
		Если ПоложениеИсполнителя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке 
			И ЗначениеЗаполнено(Исполнитель)
			И ТипЗнч(Исполнитель) = Тип("СправочникСсылка.Бригады") Тогда
			СоставБригады.Загрузить(ЗначенияРеквизитов.ЗаказНаПроизводство.СоставБригады.Выгрузить());
		КонецЕсли; 
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Если НЕ ЗначениеЗаполнено(ЗначенияРеквизитов.ЗаказНаПроизводство) 
		ИЛИ НЕ ЗначенияРеквизитов.ЗаказНаПроизводствоЗапланированыОперации Тогда
		ПодготовитьТаблицыНоменклатурыОснований(Запрос, ДокументСсылкаСборкаЗапасов); 
		ПроизводствоСервер.РазузловатьОперации(Запрос);
		Запрос.УстановитьПараметр("ОперацииПоЗаказу", Ложь);
	Иначе
		ДобавитьТаблицуОперацийПоЗаказуНаПроизводство(Запрос, ЗначенияРеквизитов.ЗаказНаПроизводство);
		Запрос.УстановитьПараметр("ОперацииПоЗаказу", Истина);
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("ЗаказНаПроизводство", ЗначенияРеквизитов.ЗаказНаПроизводство);
	Запрос.УстановитьПараметр("ТаблицаПорядковОкругления", ЦенообразованиеСервер.ТаблицаПорядковОкругления());
	Запрос.УстановитьПараметр("ДокументОснование", ДокументСсылкаСборкаЗапасов);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ВидЦен", ВидЦен);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПорядковОкругления.Порядок КАК Порядок,
	|	ТаблицаПорядковОкругления.Значение КАК Значение
	|ПОМЕСТИТЬ ТаблицаПорядковОкругления
	|ИЗ
	|	&ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СборкаЗапасовПродукция.Ссылка.Организация КАК Организация,
	|	СборкаЗапасовПродукция.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА СборкаЗапасовПродукция.Ссылка.ЗаказНаПроизводство = ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|			ТОГДА СборкаЗапасовПродукция.Ссылка.Дата
	|		ИНАЧЕ СборкаЗапасовПродукция.Ссылка.ЗаказНаПроизводство.Финиш
	|	КОНЕЦ КАК ДатаЗакрытия,
	|	&Период КАК Период,
	|	СборкаЗапасовПродукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СборкаЗапасовПродукция.НомерСтроки КАК НомерСтроки,
	|	СборкаЗапасовПродукция.Номенклатура КАК Номенклатура,
	|	СборкаЗапасовПродукция.Характеристика КАК Характеристика,
	|	СборкаЗапасовПродукция.Количество * ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(СборкаЗапасовПродукция.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА 1
	|		ИНАЧЕ СборкаЗапасовПродукция.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ * ЕСТЬNULL(ВЫБОР
	|			КОГДА ТаблицаОпераций.Количество = 0
	|				ТОГДА 1
	|			ИНАЧЕ ТаблицаОпераций.Количество
	|		КОНЕЦ, 1) КАК Количество,
	|	СборкаЗапасовПродукция.Спецификация КАК Спецификация,
	|	СборкаЗапасовПродукция.Спецификация.ВидПроизводства КАК ВидПроизводства,
	|	ТаблицаОпераций.Операция КАК Операция,
	|	ВЫБОР
	|		КОГДА СборкаЗапасовПродукция.Ссылка.ЗаказНаПроизводство = ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|				И СборкаЗапасовПродукция.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаОпераций.Этап
	|	КОНЕЦ КАК Этап,
	|	ВЫБОР
	|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
	|				И ТаблицаОпераций.Операция <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ТаблицаОпераций.Операция.ЕдиницаИзмерения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
	|				И ТаблицаОпераций.Операция <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ТаблицаОпераций.Операция.ФиксированнаяСтоимость
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ФиксированнаяСтоимость,
	|	ЕСТЬNULL(ТаблицаОпераций.НормаВремени, 0) КАК НормаВремени,
	|	ТаблицаОпераций.Уровень КАК Уровень,
	|	ТаблицаОпераций.Порядок КАК Порядок,
	|	ТаблицаОпераций.НомерСтроки КАК НомерСтрокиОперации,
	|	ТаблицаОпераций.Исполнитель КАК Исполнитель
	|ПОМЕСТИТЬ Операции
	|ИЗ
	|	Документ.СборкаЗапасов.Продукция КАК СборкаЗапасовПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОпераций КАК ТаблицаОпераций
	|		ПО СборкаЗапасовПродукция.Спецификация = ТаблицаОпераций.Спецификация
	|ГДЕ
	|	СборкаЗапасовПродукция.Ссылка = &ДокументОснование
	|	И СборкаЗапасовПродукция.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Операции.Организация КАК Организация,
	|	Операции.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	Операции.ДатаЗакрытия КАК ДатаЗакрытия,
	|	Операции.Период КАК Период,
	|	Операции.ЗаказПокупателя КАК ЗаказПокупателя,
	|	Операции.НомерСтроки КАК НомерСтроки,
	|	Операции.Номенклатура КАК Номенклатура,
	|	Операции.Характеристика КАК Характеристика,
	|	Операции.Количество КАК КоличествоПлан,
	|	Операции.Спецификация КАК Спецификация,
	|	Операции.ВидПроизводства КАК ВидПроизводства,
	|	Операции.Операция КАК Операция,
	|	Операции.Этап КАК Этап,
	|	Операции.Исполнитель КАК Исполнитель,
	|	Операции.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
	|	Операции.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Операции.НормаВремени КАК НормаВремени,
	|	(ВЫРАЗИТЬ(ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1), 0) / ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01) КАК ЧИСЛО(15, 0))) * ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01) КАК Расценка
	|ИЗ
	|	Операции КАК Операции
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления
	|			ПО ЦеныНоменклатурыСрезПоследних.ВидЦен.ПорядокОкругления = ТаблицаПорядковОкругления.Порядок
	|		ПО Операции.Операция = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	Операции.НомерСтроки,
	|	Операции.Уровень,
	|	Операции.Порядок,
	|	Операции.НомерСтрокиОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыОстатки.Характеристика КАК Характеристика,
	|	ЗаказыОстатки.Операция КАК Операция,
	|	ЗаказыОстатки.Этап КАК Этап,
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		Операции.Номенклатура КАК Номенклатура,
	|		Операции.Характеристика КАК Характеристика,
	|		Операции.Операция КАК Операция,
	|		Операции.Этап КАК Этап,
	|		Операции.Количество КАК КоличествоОстаток
	|	ИЗ
	|		Операции КАК Операции
	|	ГДЕ
	|		НЕ &ОперацииПоЗаказу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗаказНаПроизводствоПродукция.Номенклатура,
	|		ЗаказНаПроизводствоПродукция.Характеристика,
	|		ЗаказНаПроизводствоОперации.Операция,
	|		ЗаказНаПроизводствоОперации.Этап,
	|		ЗаказНаПроизводствоОперации.КоличествоПлан
	|	ИЗ
	|		Документ.ЗаказНаПроизводство.Операции КАК ЗаказНаПроизводствоОперации
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
	|			ПО ЗаказНаПроизводствоОперации.Ссылка = ЗаказНаПроизводствоПродукция.Ссылка
	|				И ЗаказНаПроизводствоОперации.КлючСвязиПродукция = ЗаказНаПроизводствоПродукция.КлючСвязи
	|	ГДЕ
	|		ЗаказНаПроизводствоОперации.Ссылка = &ЗаказНаПроизводство И &ОперацииПоЗаказу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СдельныеНаряды.Номенклатура,
	|		СдельныеНаряды.Характеристика,
	|		СдельныеНаряды.Операция,
	|		СдельныеНаряды.Этап,
	|		-ВЫБОР
	|			КОГДА СдельныеНаряды.КоличествоФактОборот = 0
	|				ТОГДА СдельныеНаряды.КоличествоПланОборот
	|			ИНАЧЕ СдельныеНаряды.КоличествоФактОборот
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.СдельныеНаряды.Обороты(
	|				,
	|				,
	|				Авто,
	|				ДокументПроизводства = ВЫБОР
	|					КОГДА &ЗаказНаПроизводство = ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|						ТОГДА &ДокументОснование
	|					ИНАЧЕ &ЗаказНаПроизводство
	|				КОНЕЦ) КАК СдельныеНаряды
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаСдельныеНаряды.Номенклатура,
	|		ДвиженияДокументаСдельныеНаряды.Характеристика,
	|		ДвиженияДокументаСдельныеНаряды.Операция,
	|		ДвиженияДокументаСдельныеНаряды.Этап,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаСдельныеНаряды.КоличествоФакт = 0
	|				ТОГДА ДвиженияДокументаСдельныеНаряды.КоличествоПлан
	|			ИНАЧЕ ДвиженияДокументаСдельныеНаряды.КоличествоФакт
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.СдельныеНаряды КАК ДвиженияДокументаСдельныеНаряды
	|	ГДЕ
	|		ДвиженияДокументаСдельныеНаряды.Регистратор = &Ссылка
	|		И ДвиженияДокументаСдельныеНаряды.ДокументПроизводства = ВЫБОР
	|				КОГДА &ЗаказНаПроизводство = ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|					ТОГДА &ДокументОснование
	|				ИНАЧЕ &ЗаказНаПроизводство
	|			КОНЕЦ) КАК ЗаказыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыОстатки.Номенклатура,
	|	ЗаказыОстатки.Характеристика,
	|	ЗаказыОстатки.Операция,
	|	ЗаказыОстатки.Этап";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ТаблицаОстатков = МассивРезультатов[3].Выгрузить();
	Выборка = МассивРезультатов[2].Выбрать();
	
	ТаблицаОстатковЗаказа = ОстаткиЗаказа(ДокументСсылкаСборкаЗапасов, Запрос);
	ЗаполнитьОперации(Выборка, ТаблицаОстатков, ТаблицаОстатковЗаказа, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПоСборкеЗапасовРазборка(ДокументСсылкаСборкаЗапасов)
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылкаСборкаЗапасов,
			"Организация, СтруктурнаяЕдиница, ЗаказНаПроизводство, Дата, ЗаказНаПроизводство.ЗапланированыОперации, ЗаказНаПроизводство.ПоложениеИсполнителя, ЗаказНаПроизводство.Исполнитель");
			
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначенияРеквизитов);
	ДатаЗакрытия = Дата;
	ПоложениеЗаказаНаПроизводство = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
	Период = ?(ЗначениеЗаполнено(ЗначенияРеквизитов.Дата), ЗначенияРеквизитов.Дата, ТекущаяДатаСеанса());
	Если ЗначениеЗаполнено(ЗначенияРеквизитов.ЗаказНаПроизводство) Тогда
		ПоложениеИсполнителя = ЗначенияРеквизитов.ЗаказНаПроизводствоПоложениеИсполнителя;
		Исполнитель = ЗначенияРеквизитов.ЗаказНаПроизводствоИсполнитель;
		Если ПоложениеИсполнителя=Перечисления.ПоложениеРеквизитаНаФорме.ВШапке
			И ЗначениеЗаполнено(Исполнитель)
			И ТипЗнч(Исполнитель)=Тип("СправочникСсылка.Бригады") Тогда
			СоставБригады.Загрузить(ЗначенияРеквизитов.ЗаказНаПроизводство.СоставБригады.Выгрузить());
		КонецЕсли; 
	КонецЕсли; 
	
	ТаблицаСостав = ПустаяТаблицаСостава();
	МассивДокументов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументСсылкаСборкаЗапасов);
	
	ЗаполнитьТаблицуСостава("СборкаЗапасов", МассивДокументов, ТаблицаСостав);
	
	ТекСпецификация = Неопределено;
	НомерСтроки = 0;
	ТаблицаСостав.Сортировать("БазоваяСпецификация");
	Для каждого СтрокаСостава Из ТаблицаСостав Цикл
		Если СтрокаСостава.БазоваяСпецификация<>ТекСпецификация Тогда
			НомерСтроки = 0;
			ТекСпецификация = СтрокаСостава.БазоваяСпецификация;
		КонецЕсли;
		НомерСтроки = НомерСтроки+1;
		СтрокаСостава.НомерСтроки = НомерСтроки;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если НЕ ЗначениеЗаполнено(ЗначенияРеквизитов.ЗаказНаПроизводство) ИЛИ ЗначенияРеквизитов.ЗаказНаПроизводствоЗапланированыОперации<>Истина Тогда
		ПроизводствоСервер.РазузловатьОперации(Запрос, ДокументСсылкаСборкаЗапасов, "Продукция");
	Иначе
		ДобавитьТаблицуОперацийПоЗаказуНаПроизводство(Запрос, ЗначенияРеквизитов.ЗаказНаПроизводство);
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("ТаблицаСостав", ТаблицаСостав);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументСсылкаСборкаЗапасов);
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СборкаЗапасовПродукция.Ссылка КАК ЗаказНаПроизводство,
	|	СборкаЗапасовПродукция.Ссылка.Организация КАК Организация,
	|	СборкаЗапасовПродукция.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	СборкаЗапасовПродукция.Ссылка.Дата КАК ДатаЗакрытия,
	|	&Период КАК Период,
	|	СборкаЗапасовПродукция.Ссылка.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СборкаЗапасовПродукция.НомерСтроки КАК НомерСтроки,
	|	СборкаЗапасовПродукция.Номенклатура КАК Номенклатура,
	|	СборкаЗапасовПродукция.Характеристика КАК Характеристика,
	|	СборкаЗапасовПродукция.Количество * ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(СборкаЗапасовПродукция.ЕдиницаИзмерения) = ТИП(Справочник.ЕдиницыИзмерения)
	|				И СборкаЗапасовПродукция.ЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА СборкаЗапасовПродукция.ЕдиницаИзмерения.Коэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ * ЕСТЬNULL(ВЫБОР
	|			КОГДА ТаблицаОпераций.Количество = 0
	|				ТОГДА 1
	|			ИНАЧЕ ТаблицаОпераций.Количество
	|		КОНЕЦ, 1) КАК Количество,
	|	СборкаЗапасовПродукция.Количество * ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(СборкаЗапасовПродукция.ЕдиницаИзмерения) = ТИП(Справочник.ЕдиницыИзмерения)
	|				И СборкаЗапасовПродукция.ЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА СборкаЗапасовПродукция.ЕдиницаИзмерения.Коэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КоличествоПродукции,
	|	СборкаЗапасовПродукция.Спецификация КАК Спецификация,
	|	ЗНАЧЕНИЕ(Справочник.ВидыПроизводства.ПустаяСсылка) КАК ВидПроизводства,
	|	СборкаЗапасовПродукция.Партия КАК Партия,
	|	ТаблицаОпераций.Операция КАК Операция,
	|	ВЫБОР
	|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
	|				И ТаблицаОпераций.Операция <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ТаблицаОпераций.Операция.ФиксированнаяСтоимость
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ФиксированнаяСтоимость,
	|	ТаблицаОпераций.Операция.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(ТаблицаОпераций.НормаВремени, 0) КАК НормаВремени,
	|	0 КАК КлючСвязи,
	|	ТаблицаОпераций.НомерСтроки КАК НомерСтрокиОперации,
	|	ТаблицаОпераций.Исполнитель КАК Исполнитель
	|ПОМЕСТИТЬ Операции
	|ИЗ
	|	Документ.СборкаЗапасов.Продукция КАК СборкаЗапасовПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОпераций КАК ТаблицаОпераций
	|		ПО СборкаЗапасовПродукция.Спецификация = ТаблицаОпераций.Спецификация
	|ГДЕ
	|	СборкаЗапасовПродукция.Ссылка В(&МассивДокументов)";
	Запрос.Выполнить();
	
	Выборка = ПолучитьВыборкуОперацииРазборка(Запрос);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыОстатки.Характеристика КАК Характеристика,
	|	ЗаказыОстатки.Операция КАК Операция,
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОперацииИСостав.Номенклатура КАК Номенклатура,
	|		ОперацииИСостав.Характеристика КАК Характеристика,
	|		ОперацииИСостав.Операция КАК Операция,
	|		ОперацииИСостав.Количество КАК КоличествоОстаток
	|	ИЗ
	|		ОперацииИСостав КАК ОперацииИСостав
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СдельныеНаряды.Номенклатура,
	|		СдельныеНаряды.Характеристика,
	|		СдельныеНаряды.Операция,
	|		-СдельныеНаряды.КоличествоФактОборот
	|	ИЗ
	|		РегистрНакопления.СдельныеНаряды.Обороты(
	|				,
	|				,
	|				Авто,
	|				ДокументПроизводства = ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(&ДокументОснование КАК Документ.СборкаЗапасов).ЗаказНаПроизводство = ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|						ТОГДА &ДокументОснование
	|					ИНАЧЕ ВЫРАЗИТЬ(&ДокументОснование КАК Документ.СборкаЗапасов).ЗаказНаПроизводство
	|				КОНЕЦ) КАК СдельныеНаряды
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаСдельныеНаряды.Номенклатура,
	|		ДвиженияДокументаСдельныеНаряды.Характеристика,
	|		ДвиженияДокументаСдельныеНаряды.Операция,
	|		ДвиженияДокументаСдельныеНаряды.КоличествоФакт
	|	ИЗ
	|		РегистрНакопления.СдельныеНаряды КАК ДвиженияДокументаСдельныеНаряды
	|	ГДЕ
	|		ДвиженияДокументаСдельныеНаряды.Регистратор = &Ссылка
	|		И ДвиженияДокументаСдельныеНаряды.ДокументПроизводства = ВЫБОР
	|				КОГДА ВЫРАЗИТЬ(&ДокументОснование КАК Документ.СборкаЗапасов).ЗаказНаПроизводство = ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|					ТОГДА &ДокументОснование
	|				ИНАЧЕ ВЫРАЗИТЬ(&ДокументОснование КАК Документ.СборкаЗапасов).ЗаказНаПроизводство
	|			КОНЕЦ) КАК ЗаказыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыОстатки.Номенклатура,
	|	ЗаказыОстатки.Характеристика,
	|	ЗаказыОстатки.Операция";
	ТаблицаОстатков = Запрос.Выполнить().Выгрузить();
	
	ЗаполнитьОперации(Выборка, ТаблицаОстатков, , Истина);
	
КонецПроцедуры

// Процедура заполняет табличную часть по спецификации.
//
Процедура ЗаполнитьТаблицуПоСпецификации(БазоваяСпецификация, ПоСпецификации, ПоЕдиницаИзмерения, ПоКоличество, ПоДоляСтоимости = Неопределено, ТаблицаСостав)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МАКСИМУМ(СпецификацииСостав.НомерСтроки) КАК СпецификацииСоставНомерСтроки,
	|	СпецификацииСостав.Номенклатура КАК Номенклатура,
	|	СпецификацииСостав.ТипСтрокиСостава КАК ТипСтрокиСостава,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА СпецификацииСостав.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	СпецификацииСостав.Спецификация КАК Спецификация,
	|	СпецификацииСостав.ДоляСтоимости КАК ДоляСтоимости,
	|	СпецификацииСостав.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(СпецификацииСостав.Количество / СпецификацииСостав.КоличествоПродукции * &Коэффициент * &Количество) КАК Количество
	|ИЗ
	|	Справочник.Спецификации.Состав КАК СпецификацииСостав
	|ГДЕ
	|	СпецификацииСостав.Ссылка = &Спецификация
	|	И СпецификацииСостав.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацииСостав.Номенклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА СпецификацииСостав.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	СпецификацииСостав.Спецификация,
	|	СпецификацииСостав.ЕдиницаИзмерения,
	|	СпецификацииСостав.ТипСтрокиСостава,
	|	СпецификацииСостав.ДоляСтоимости
	|
	|УПОРЯДОЧИТЬ ПО
	|	СпецификацииСоставНомерСтроки");
	
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики", Константы.ФункциональнаяОпцияИспользоватьХарактеристики.Получить());
	
	Запрос.УстановитьПараметр("Спецификация", ПоСпецификации);
	Запрос.УстановитьПараметр("Количество", ПоКоличество);
	
	Если ТипЗнч(ПоЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения")
		И ЗначениеЗаполнено(ПоЕдиницаИзмерения) Тогда
		ПоКоэффициент = ПоЕдиницаИзмерения.Коэффициент;
	Иначе
		ПоКоэффициент = 1;
	КонецЕсли;
	Запрос.УстановитьПараметр("Коэффициент", ПоКоэффициент);
	
	ТаблицаСостава = Запрос.Выполнить().Выгрузить();
	ИтогДоляСтоимости = ТаблицаСостава.Итог("ДоляСтоимости");
	Для каждого СтрокаСостава Из ТаблицаСостава Цикл
		
		Если СтрокаСостава.ТипСтрокиСостава = Перечисления.ТипыСтрокСоставаСпецификации.Узел Тогда
			
			ДоляУзла = ?(ПоДоляСтоимости<>Неопределено И ИтогДоляСтоимости<>0, ПоДоляСтоимости*СтрокаСостава.ДоляСтоимости/ИтогДоляСтоимости, СтрокаСостава.ДоляСтоимости);
			ЗаполнитьТаблицуПоСпецификации(БазоваяСпецификация, СтрокаСостава.Спецификация, СтрокаСостава.ЕдиницаИзмерения, СтрокаСостава.Количество, ДоляУзла, ТаблицаСостав);
			
		Иначе
			
			НоваяСтрока = ТаблицаСостав.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСостава);
			НоваяСтрока.БазоваяСпецификация = БазоваяСпецификация;
			Если ПоДоляСтоимости<>Неопределено И ИтогДоляСтоимости<>0 Тогда
				НоваяСтрока.ДоляСтоимости = ПоДоляСтоимости*НоваяСтрока.ДоляСтоимости/ИтогДоляСтоимости;
			КонецЕсли; 
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьТаблицуПоСпецификации()

Процедура ЗаполнитьТаблицуСостава(ИмяДокумента, МассивДокументов, ТаблицаСостав)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Документ.ВидОперации КАК ВидОперации,
	|	Документ.РучноеРаспределение КАК РучноеРаспределение,
	|	Документ.Продукция.(
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Партия КАК Партия,
	|		Количество КАК Количество,
	|		Резерв КАК Резерв,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Номенклатура.ЕдиницаИзмерения КАК БазоваяЕдиница,
	|		ВЫБОР
	|			КОГДА Документ.Продукция.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				ТОГДА Документ.Продукция.ЕдиницаИзмерения.Коэффициент
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК Коэффициент,
	|		Спецификация КАК Спецификация,
	|		КлючСвязи КАК КлючСвязи,
	|		СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗаказПокупателя КАК ЗаказПокупателя
	|	) КАК Продукция,
	|	Документ.Запасы.(
	|		НомерСтроки КАК НомерСтроки,
	|		Этап КАК Этап,
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Партия КАК Партия,
	|		Количество КАК Количество,
	|		Резерв КАК Резерв,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Спецификация КАК Спецификация,
	|		ВЫБОР
	|			КОГДА Документ.Запасы.ДоляСтоимости = 0
	|				ТОГДА 1
	|			ИНАЧЕ Документ.Запасы.ДоляСтоимости
	|		КОНЕЦ КАК ДоляСтоимости,
	|		КлючСвязи КАК КлючСвязи,
	|		СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗаказПокупателя КАК ЗаказПокупателя
	|	) КАК Запасы,
	|	Документ.РаспределениеЗапасов.(
	|		НомерСтроки КАК НомерСтроки,
	|		Количество КАК Количество,
	|		КлючСвязиПродукция КАК КлючСвязиПродукция,
	|		Этап КАК Этап,
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Партия КАК Партия,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Спецификация КАК Спецификация,
	|		СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗаказПокупателя КАК ЗаказПокупателя
	|	) КАК РаспределениеЗапасов
	|ИЗ
	|	Документ.СборкаЗапасов КАК Документ
	|ГДЕ
	|	Документ.Ссылка В(&МассивДокументов)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.СборкаЗапасов", "Документ." + ИмяДокумента);
	Если ИмяДокумента="ЗаказНаПроизводство" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство", "Документ.Ссылка КАК ЗаказНаПроизводство");
	КонецЕсли; 
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ТаблицаПродукция = Выборка.Продукция.Выгрузить();
		ТаблицаЗапасы = Выборка.Запасы.Выгрузить();
		Если ТаблицаЗапасы.Количество()=0 Тогда
			Для каждого СтрПродукция Из ТаблицаПродукция Цикл
				ЗаполнитьТаблицуПоСпецификации(СтрПродукция.Спецификация, СтрПродукция.Спецификация, СтрПродукция.БазоваяЕдиница, 1, , ТаблицаСостав);
			КонецЦикла;
			Продолжить;
		ИначеЕсли ТипЗнч(Выборка.Ссылка)=Тип("ДокументСсылка.СборкаЗапасов") ИЛИ Выборка.РучноеРаспределение Тогда
			ТаблицаРаспределениеЗапасов = Выборка.РаспределениеЗапасов.Выгрузить();
		Иначе
			ТаблицаРаспределениеЗапасов = Выборка.РаспределениеЗапасов.Выгрузить().СкопироватьКолонки();
			ПроизводствоСервер.РаспределитьМатериалы(ТаблицаПродукция, ТаблицаЗапасы, ТаблицаРаспределениеЗапасов, , Выборка.ЗаказНаПроизводство, Выборка.ВидОперации);
		КонецЕсли;
		ОбъединитьЗапасыИРаспределение(ТаблицаЗапасы, ТаблицаРаспределениеЗапасов);
		Для каждого СтрПродукция Из ТаблицаПродукция Цикл
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("КлючСвязиПродукция", СтрПродукция.КлючСвязи);
			СтрокиРаспределение = ТаблицаРаспределениеЗапасов.НайтиСтроки(СтруктураОтбора);
			Для каждого СтрокаРаспределение Из СтрокиРаспределение Цикл
				НоваяСтрока = ТаблицаСостав.Добавить();
				НоваяСтрока.БазоваяСпецификация = СтрПродукция.Спецификация;
				НоваяСтрока.Номенклатура = СтрокаРаспределение.Номенклатура;
				НоваяСтрока.Характеристика = СтрокаРаспределение.Характеристика;
				Если СтрПродукция.Количество = 0 ИЛИ СтрПродукция.Коэффициент = 0 Тогда
					НоваяСтрока.Количество = 0;
				Иначе
					НоваяСтрока.Количество = СтрокаРаспределение.Количество / СтрПродукция.Количество / СтрПродукция.Коэффициент;
				КонецЕсли; 
				НоваяСтрока.ДоляСтоимости = СтрокаРаспределение.ДоляСтоимости;
			КонецЦикла; 
		КонецЦикла;
	КонецЦикла; 
	
КонецПроцедуры

Процедура ОбъединитьЗапасыИРаспределение(ТаблицаЗапасы, ТаблицаРаспределениеЗапасов)
	
	ТаблицаЗапасы.Свернуть("Номенклатура, Характеристика, Партия, ЕдиницаИзмерения, Спецификация, СтруктурнаяЕдиница, ЗаказПокупателя", "ДоляСтоимости, Количество");
	ТаблицаРаспределениеЗапасов.Колонки.Добавить("ДоляСтоимости", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	ТаблицаРаспределениеЗапасов.Колонки.Добавить("Распределено", Новый ОписаниеТипов("Булево"));
	
	Для каждого СтрокаЗапасы Из ТаблицаЗапасы Цикл
		СтруктураОтбора = Новый Структура("Номенклатура, Характеристика, Партия, Спецификация, ЕдиницаИзмерения, СтруктурнаяЕдиница, ЗаказПокупателя");
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаЗапасы);
		СтруктураОтбора.Вставить("Распределено", Ложь);
		СтрокиРаспределение = ТаблицаРаспределениеЗапасов.НайтиСтроки(СтруктураОтбора);
		КоличествоРаспределить = СтрокаЗапасы.Количество;
		ДоляСтоимостиРаспределить = СтрокаЗапасы.ДоляСтоимости;
		Для каждого СтрокаРаспределение Из СтрокиРаспределение Цикл
			Если КоличествоРаспределить = СтрокаРаспределение.Количество Тогда
				СтрокаРаспределение.ДоляСтоимости = ДоляСтоимостиРаспределить;
				СтрокаРаспределение.Распределено = Истина;
				Прервать;
			ИначеЕсли КоличествоРаспределить > СтрокаРаспределение.Количество Тогда
				Если КоличествоРаспределить <> 0 Тогда
					СтрокаРаспределение.ДоляСтоимости = ДоляСтоимостиРаспределить * СтрокаРаспределение.Количество / КоличествоРаспределить;
				КонецЕсли; 
				СтрокаРаспределение.Распределено = Истина;
				КоличествоРаспределить = КоличествоРаспределить - СтрокаРаспределение.Количество;
				ДоляСтоимостиРаспределить = ДоляСтоимостиРаспределить - СтрокаРаспределение.ДоляСтоимости;
			ИначеЕсли КоличествоРаспределить<СтрокаРаспределение.Количество Тогда
				НоваяСтрока = ТаблицаРаспределениеЗапасов.Вставить(ТаблицаРаспределениеЗапасов.Индекс(СтрокаРаспределение));
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРаспределение);
				НоваяСтрока.Количество = КоличествоРаспределить;
				НоваяСтрока.ДоляСтоимости = ДоляСтоимостиРаспределить;
				НоваяСтрока.Распределено = Истина;
				СтрокаРаспределение.Количество = СтрокаРаспределение.Количество - НоваяСтрока.Количество;
				Прервать;
			КонецЕсли; 
		КонецЦикла; 
	КонецЦикла; 
	
	Если ТаблицаРаспределениеЗапасов.Колонки.Найти("НомерСтроки")<>Неопределено Тогда
		ТаблицаРаспределениеЗапасов.Сортировать("НомерСтроки");
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьСоставБригадыПоЗаказуНаПроизводство(МассивЗаказов, СоответствиеКлючейСвязи)
	
	СоставБригады.Очистить();
	
	Бригады = Новый Массив;
	Для каждого СтрокаТабличнойЧасти Из Операции Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Исполнитель) ИЛИ ТипЗнч(СтрокаТабличнойЧасти.Исполнитель)<>Тип("СправочникСсылка.Бригады") Тогда
			Продолжить;
		КонецЕсли;
		Если Бригады.Найти(СтрокаТабличнойЧасти.Исполнитель)<>Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Бригады.Добавить(СтрокаТабличнойЧасти.Исполнитель);
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.УстановитьПараметр("ПоложениеИсполнителя", ПоложениеИсполнителя);
	Запрос.УстановитьПараметр("Бригады", Бригады);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказНаПроизводствоСоставБригады.Ссылка КАК Ссылка,
	|	ЗаказНаПроизводствоСоставБригады.Сотрудник КАК Сотрудник,
	|	ЗаказНаПроизводствоСоставБригады.КТУ КАК КТУ,
	|	ЗаказНаПроизводствоСоставБригады.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗаказНаПроизводствоСоставБригады.КлючСвязи КАК КлючСвязи
	|ИЗ
	|	Документ.ЗаказНаПроизводство.СоставБригады КАК ЗаказНаПроизводствоСоставБригады
	|ГДЕ
	|	ЗаказНаПроизводствоСоставБригады.Ссылка В(&МассивЗаказов)
	|	И ЗаказНаПроизводствоСоставБригады.Ссылка.ПоложениеИсполнителя = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|	И &ПоложениеИсполнителя = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|	И ЗаказНаПроизводствоСоставБригады.КлючСвязи <> 0
	|	И ЗаказНаПроизводствоСоставБригады.Ссылка.ЗапланированыОперации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказНаПроизводствоСоставБригады.Ссылка,
	|	ЗаказНаПроизводствоСоставБригады.Сотрудник,
	|	ЗаказНаПроизводствоСоставБригады.КТУ,
	|	ЗаказНаПроизводствоСоставБригады.СтруктурнаяЕдиница,
	|	ЗаказНаПроизводствоОперации.КлючСвязи
	|ИЗ
	|	Документ.ЗаказНаПроизводство.Операции КАК ЗаказНаПроизводствоОперации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.СоставБригады КАК ЗаказНаПроизводствоСоставБригады
	|		ПО ЗаказНаПроизводствоОперации.Ссылка = ЗаказНаПроизводствоСоставБригады.Ссылка
	|ГДЕ
	|	ЗаказНаПроизводствоОперации.Ссылка В(&МассивЗаказов)
	|	И ЗаказНаПроизводствоОперации.Ссылка.ПоложениеИсполнителя <> ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|	И &ПоложениеИсполнителя = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|	И ЗаказНаПроизводствоОперации.Ссылка.Исполнитель ССЫЛКА Справочник.Бригады
	|	И ЗаказНаПроизводствоОперации.КлючСвязи <> 0
	|	И ЗаказНаПроизводствоОперации.Ссылка.ЗапланированыОперации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказНаПроизводствоСоставБригады.Ссылка,
	|	ЗаказНаПроизводствоСоставБригады.Сотрудник,
	|	ЗаказНаПроизводствоСоставБригады.КТУ,
	|	ЗаказНаПроизводствоСоставБригады.СтруктурнаяЕдиница,
	|	0
	|ИЗ
	|	Документ.ЗаказНаПроизводство.СоставБригады КАК ЗаказНаПроизводствоСоставБригады
	|ГДЕ
	|	ЗаказНаПроизводствоСоставБригады.Ссылка В(&МассивЗаказов)
	|	И ЗаказНаПроизводствоСоставБригады.Ссылка.ПоложениеИсполнителя <> ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|	И &ПоложениеИсполнителя <> ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|	И ЗаказНаПроизводствоСоставБригады.Ссылка.Исполнитель ССЫЛКА Справочник.Бригады
	|	И ЗаказНаПроизводствоСоставБригады.КлючСвязи = 0
	|	И ЗаказНаПроизводствоСоставБригады.Ссылка.ЗапланированыОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БригадыСостав.Ссылка КАК Ссылка,
	|	1 КАК КТУ,
	|	БригадыСостав.Сотрудник КАК Сотрудник,
	|	ЕСТЬNULL(СотрудникиСрезПоследних.СтруктурнаяЕдиница, &СтруктурнаяЕдиница) КАК СтруктурнаяЕдиница
	|ИЗ
	|	Справочник.Бригады.Состав КАК БригадыСостав
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники.СрезПоследних КАК СотрудникиСрезПоследних
	|		ПО БригадыСостав.Сотрудник = СотрудникиСрезПоследних.Сотрудник
	|			И (СотрудникиСрезПоследних.Организация = &Организация)
	|ГДЕ
	|	БригадыСостав.Ссылка В(&Бригады)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	БригадыСостав.НомерСтроки";
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаСостава = Результат[0].Выгрузить();
	ТаблицаСтандартногоСостава = Результат[1].Выгрузить();
	
	ИсполнительВШапке = (ПоложениеИсполнителя <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти);
	БригадаВШапке = ИсполнительВШапке
		И ЗначениеЗаполнено(Исполнитель)
		И ТипЗнч(Исполнитель)=Тип("СправочникСсылка.Бригады");
	Если БригадаВШапке Тогда
		// Состав шапки
		Для каждого СтрокаТабличнойЧасти Из ТаблицаСостава Цикл
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.КлючСвязи) Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = СоставБригады.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		КонецЦикла;
		Если СоставБригады.Количество()=0 Тогда
			// Состав по умолчанию
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("Ссылка", Исполнитель);
			СтрокиСостава = ТаблицаСтандартногоСостава.НайтиСтроки(СтруктураОтбора);
			Для каждого СтрокаСостава Из СтрокиСостава Цикл
				НоваяСтрока = СоставБригады.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСостава);
				НоваяСтрока.КлючСвязи = 0;
				НоваяСтрока.СтруктурнаяЕдиница = СтруктурнаяЕдиница;
			КонецЦикла; 
		КонецЕсли; 
	ИначеЕсли НЕ ИсполнительВШапке Тогда 
		// Состав ТЧ
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			Если НЕ ИсполнительБригада(СтрокаТабличнойЧасти) Тогда
				Продолжить;
			КонецЕсли;
			СтрокиСостава = СтрокиСостава(ТаблицаСостава, ТаблицаСтандартногоСостава, СоответствиеКлючейСвязи, СтрокаТабличнойЧасти);
			Для каждого СтрокаСостава Из СтрокиСостава Цикл
				НоваяСтрока = СоставБригады.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСостава);
				НоваяСтрока.КлючСвязи = СтрокаТабличнойЧасти.КлючСвязи;
				Если НЕ ЗначениеЗаполнено(НоваяСтрока.СтруктурнаяЕдиница) Тогда
					НоваяСтрока.СтруктурнаяЕдиница = СтрокаТабличнойЧасти.СтруктурнаяЕдиница;
				КонецЕсли; 
			КонецЦикла; 
		КонецЦикла;  
	КонецЕсли;  
	
КонецПроцедуры

Функция ИсполнительБригада(СтрокаТабличнойЧасти)
	
	Возврат ЗначениеЗаполнено(СтрокаТабличнойЧасти.КлючСвязи)
		И ЗначениеЗаполнено(СтрокаТабличнойЧасти.Исполнитель)
		И ТипЗнч(СтрокаТабличнойЧасти.Исполнитель) = Тип("СправочникСсылка.Бригады");
	
КонецФункции

Функция СтрокиСостава(ТаблицаСостава, ТаблицаСтандартногоСостава, СоответствиеКлючейСвязи, СтрокаТабличнойЧасти)
		
	Если СоответствиеКлючейСвязи.Получить(СтрокаТабличнойЧасти.ЗаказНаПроизводство) = Неопределено Тогда
		СоответствиеКлючейСвязи.Вставить(СтрокаТабличнойЧасти.ЗаказНаПроизводство, Новый Соответствие)
	КонецЕсли; 
	СтарыйКлючСвязи = СоответствиеКлючейСвязи.Получить(СтрокаТабличнойЧасти.ЗаказНаПроизводство).Получить(СтрокаТабличнойЧасти.КлючСвязи);
	Если НЕ ЗначениеЗаполнено(СтарыйКлючСвязи) Тогда
		СтрокиСостава = Новый Массив;
	Иначе
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСвязи", СтарыйКлючСвязи);
		СтруктураОтбора.Вставить("Ссылка", СтрокаТабличнойЧасти.ЗаказНаПроизводство);
		СтрокиСостава = ТаблицаСостава.НайтиСтроки(СтруктураОтбора);
	КонецЕсли;
	Если СтрокиСостава.Количество()=0 Тогда
		// Состав по умолчанию
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Ссылка", СтрокаТабличнойЧасти.Исполнитель);
		СтрокиСостава = ТаблицаСтандартногоСостава.НайтиСтроки(СтруктураОтбора);
	КонецЕсли; 
	Возврат СтрокиСостава;
	
КонецФункции

Процедура ДействияПослеЗаполненияТЧ()
		
	ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "Операции", "ЗаказНаПроизводство", "ПоложениеЗаказаНаПроизводство");
	ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "Операции", "Исполнитель", "ПоложениеИсполнителя");
	ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "Операции", "СтруктурнаяЕдиница", "ПоложениеСтруктурнойЕдиницы");
	ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "СоставБригады", "СтруктурнаяЕдиница", "ПоложениеСтруктурнойЕдиницы");
	
КонецПроцедуры

Процедура ПодготовитьТаблицыНоменклатурыОснований(Запрос, ДокументСсылкаСборкаЗапасов)
	
	Запрос.УстановитьПараметр("СборкаЗапасов", ДокументСсылкаСборкаЗапасов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СборкаЗапасов.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|ПОМЕСТИТЬ СвязанныеЗаказыНаПроизводство
	|ИЗ
	|	Документ.СборкаЗапасов КАК СборкаЗапасов
	|ГДЕ
	|	СборкаЗапасов.Ссылка = &СборкаЗапасов
	|	И СборкаЗапасов.ЗаказНаПроизводство <> ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СборкаЗапасовПродукция.ЗаказПокупателя КАК ЗаказПокупателя
	|ПОМЕСТИТЬ СвязанныеЗаказыПокупателей
	|ИЗ
	|	Документ.СборкаЗапасов.Продукция КАК СборкаЗапасовПродукция
	|ГДЕ
	|	СборкаЗапасовПродукция.Ссылка = &СборкаЗапасов
	|	И СборкаЗапасовПродукция.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СборкаЗапасовПродукция.Номенклатура КАК Номенклатура,
	|	СборкаЗапасовПродукция.Спецификация КАК Спецификация
	|ПОМЕСТИТЬ ТаблицаСпецификаций
	|ИЗ
	|	Документ.СборкаЗапасов.Продукция КАК СборкаЗапасовПродукция
	|ГДЕ
	|	СборкаЗапасовПродукция.Ссылка = &СборкаЗапасов
	|	И СборкаЗапасовПродукция.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказНаПроизводствоПродукция.Номенклатура,
	|	ЗаказНаПроизводствоПродукция.Спецификация
	|ИЗ
	|	Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
	|ГДЕ
	|	ЗаказНаПроизводствоПродукция.Ссылка В
	|			(ВЫБРАТЬ
	|				СвязанныеЗаказыНаПроизводство.ЗаказНаПроизводство
	|			ИЗ
	|				СвязанныеЗаказыНаПроизводство)
	|	И ЗаказНаПроизводствоПродукция.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказПокупателяЗапасы.Номенклатура,
	|	ЗаказПокупателяЗапасы.Спецификация
	|ИЗ
	|	Документ.ЗаказПокупателя.Запасы КАК ЗаказПокупателяЗапасы
	|ГДЕ
	|	ЗаказПокупателяЗапасы.Ссылка В
	|			(ВЫБРАТЬ
	|				СвязанныеЗаказыПокупателей.ЗаказПокупателя
	|			ИЗ
	|				СвязанныеЗаказыПокупателей)
	|	И НЕ ЗаказПокупателяЗапасы.ЭтоРазделитель
	|	И ЗаказПокупателяЗапасы.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|	И ЗаказПокупателяЗапасы.НомерВариантаКП = ЗаказПокупателяЗапасы.Ссылка.ОсновнойВариантКП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	0 КАК Уровень,
	|	0 КАК Порядок,
	|	0 КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка) КАК Этап,
	|	ТаблицаСпецификаций.Спецификация КАК Спецификация,
	|	ТаблицаСпецификаций.Спецификация КАК ВложеннаяСпецификация,
	|	ИСТИНА КАК ЭтоУзел,
	|	ТаблицаСпецификаций.Номенклатура КАК Операция,
	|	1 КАК НормаВремени,
	|	1 КАК Количество
	|ПОМЕСТИТЬ ТаблицаОпераций
	|ИЗ
	|	ТаблицаСпецификаций КАК ТаблицаСпецификаций
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСпецификаций.Спецификация,
	|	ТаблицаСпецификаций.Номенклатура";
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ПолучитьВыборкуОперацииРазборка(Запрос)
	
	Запрос.УстановитьПараметр("ТаблицаПорядковОкругления", ЦенообразованиеСервер.ТаблицаПорядковОкругления());
	Запрос.УстановитьПараметр("ВидЦен", ВидЦен);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПорядковОкругления.Порядок КАК Порядок,
	|	ТаблицаПорядковОкругления.Значение КАК Значение
	|ПОМЕСТИТЬ ТаблицаПорядковОкругления
	|ИЗ
	|	&ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСостав.БазоваяСпецификация КАК БазоваяСпецификация,
	|	ТаблицаСостав.НомерСтроки КАК НомерСтроки,
	|	ТаблицаСостав.Номенклатура КАК Номенклатура,
	|	ТаблицаСостав.Характеристика КАК Характеристика,
	|	ТаблицаСостав.Спецификация КАК Спецификация,
	|	ТаблицаСостав.Количество КАК Количество,
	|	ТаблицаСостав.ДоляСтоимости КАК ДоляСтоимости
	|ПОМЕСТИТЬ ТаблицаСостав
	|ИЗ
	|	&ТаблицаСостав КАК ТаблицаСостав
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСостав.БазоваяСпецификация КАК Спецификация,
	|	СУММА(ТаблицаСостав.ДоляСтоимости) КАК ДоляСтоимости
	|ПОМЕСТИТЬ ИтогиСпецификации
	|ИЗ
	|	ТаблицаСостав КАК ТаблицаСостав
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСостав.БазоваяСпецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Операции.ЗаказНаПроизводство КАК Основание,
	|	Операции.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Операции.Период КАК Период,
	|	Операции.Организация КАК Организация,
	|	Операции.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	Операции.Исполнитель КАК Исполнитель,
	|	Операции.ДатаЗакрытия КАК ДатаЗакрытия,
	|	Операции.КлючСвязи КАК КлючСвязи,
	|	Операции.ЗаказПокупателя КАК ЗаказПокупателя,
	|	Операции.НомерСтроки КАК НомерСтроки,
	|	Операции.НомерСтрокиОперации КАК НомерСтрокиОперации,
	|	ЕСТЬNULL(ТаблицаСостав.НомерСтроки, 1) КАК НомерСтрокиСостава,
	|	ЕСТЬNULL(ТаблицаСостав.Номенклатура, Операции.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ТаблицаСостав.Характеристика, Операции.Характеристика) КАК Характеристика,
	|	Операции.Количество * ЕСТЬNULL(ТаблицаСостав.Количество, 1) КАК Количество,
	|	Операции.Спецификация КАК Спецификация,
	|	Операции.ВидПроизводства КАК ВидПроизводства,
	|	Операции.Партия КАК Партия,
	|	Операции.Операция КАК Операция,
	|	Операции.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
	|	Операции.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ТаблицаСостав.Количество, 1) = 0
	|				ТОГДА 0
	|			ИНАЧЕ Операции.НормаВремени / ЕСТЬNULL(ТаблицаСостав.Количество, 1) * ЕСТЬNULL(ВЫБОР
	|						КОГДА ИтогиСпецификации.ДоляСтоимости = 0
	|							ТОГДА 1
	|						ИНАЧЕ ТаблицаСостав.ДоляСтоимости / ИтогиСпецификации.ДоляСтоимости
	|					КОНЕЦ, 1)
	|		КОНЕЦ КАК ЧИСЛО(15, 3)) КАК НормаВремени
	|ПОМЕСТИТЬ ОперацииИСостав
	|ИЗ
	|	Операции КАК Операции
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСостав КАК ТаблицаСостав
	|			ЛЕВОЕ СОЕДИНЕНИЕ ИтогиСпецификации КАК ИтогиСпецификации
	|			ПО ТаблицаСостав.БазоваяСпецификация = ИтогиСпецификации.Спецификация
	|		ПО Операции.Спецификация = ТаблицаСостав.БазоваяСпецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИтогиНормы.Спецификация КАК Спецификация,
	|	(ИтогиНормы.НормаВремени - ЕСТЬNULL(ВложенныйЗапрос.НормаВремени, 0)) / ЕСТЬNULL(СтрокаСМинимальнымКоличеством.Количество, 1) КАК НормаВремени,
	|	ЕСТЬNULL(СтрокаСМинимальнымКоличеством.НомерСтроки, 1) КАК НомерСтроки
	|ПОМЕСТИТЬ РасхождениеНормы
	|ИЗ
	|	(ВЫБРАТЬ
	|		Операции.Спецификация КАК Спецификация,
	|		СУММА(Операции.НормаВремени * Операции.КоличествоПродукции) КАК НормаВремени
	|	ИЗ
	|		Операции КАК Операции
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Операции.Спецификация) КАК ИтогиНормы
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ОперацииИСостав.Спецификация КАК Спецификация,
	|			СУММА(ВЫРАЗИТЬ(ОперацииИСостав.НормаВремени * ОперацииИСостав.Количество КАК ЧИСЛО(15, 3))) КАК НормаВремени,
	|			МАКСИМУМ(ОперацииИСостав.НомерСтрокиСостава) КАК НомерСтроки
	|		ИЗ
	|			ОперацииИСостав КАК ОперацииИСостав
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ОперацииИСостав.Спецификация) КАК ВложенныйЗапрос
	|		ПО ИтогиНормы.Спецификация = ВложенныйЗапрос.Спецификация,
	|	(ВЫБРАТЬ
	|		МинимальноеКоличество.Количество КАК Количество,
	|		МАКСИМУМ(ТаблицаСостав.НомерСтроки) КАК НомерСтроки
	|	ИЗ
	|		(ВЫБРАТЬ
	|			МИНИМУМ(ТаблицаСостав.Количество) КАК Количество
	|		ИЗ
	|			ТаблицаСостав КАК ТаблицаСостав
	|		ГДЕ
	|			ТаблицаСостав.Количество <> 0) КАК МинимальноеКоличество
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСостав КАК ТаблицаСостав
	|			ПО МинимальноеКоличество.Количество = ТаблицаСостав.Количество
	|	
	|	СГРУППИРОВАТЬ ПО
	|		МинимальноеКоличество.Количество) КАК СтрокаСМинимальнымКоличеством
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацииИСостав.Основание КАК Основание,
	|	ОперацииИСостав.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ОперацииИСостав.Период КАК Период,
	|	ОперацииИСостав.Организация КАК Организация,
	|	ОперацииИСостав.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ОперацииИСостав.Исполнитель КАК Исполнитель,
	|	ОперацииИСостав.ДатаЗакрытия КАК ДатаЗакрытия,
	|	ОперацииИСостав.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ОперацииИСостав.Номенклатура КАК Номенклатура,
	|	ОперацииИСостав.Характеристика КАК Характеристика,
	|	ОперацииИСостав.Количество КАК КоличествоПлан,
	|	ОперацииИСостав.Спецификация КАК Спецификация,
	|	ОперацииИСостав.ВидПроизводства КАК ВидПроизводства,
	|	ОперацииИСостав.Партия КАК Партия,
	|	ОперацииИСостав.Операция КАК Операция,
	|	ОперацииИСостав.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
	|	ОперацииИСостав.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ОперацииИСостав.НормаВремени + ЕСТЬNULL(РасхождениеНормы.НормаВремени, 0) КАК НормаВремени,
	|	(ВЫРАЗИТЬ(ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1), 0) / ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01) КАК ЧИСЛО(15, 0))) * ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01) КАК Расценка
	|ИЗ
	|	ОперацииИСостав КАК ОперацииИСостав
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления
	|			ПО ЦеныНоменклатурыСрезПоследних.ВидЦен.ПорядокОкругления = ТаблицаПорядковОкругления.Порядок
	|		ПО ОперацииИСостав.Операция = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РасхождениеНормы КАК РасхождениеНормы
	|		ПО ОперацииИСостав.Спецификация = РасхождениеНормы.Спецификация
	|			И ОперацииИСостав.НомерСтрокиСостава = РасхождениеНормы.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОперацииИСостав.ЗаказНаПроизводство,
	|	ОперацииИСостав.НомерСтроки,
	|	ОперацииИСостав.НомерСтрокиОперации";
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Процедура ПолучитьСоставСпецификацийЗаказовПокупателей(Запрос)
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказНаПроизводствоПродукция.ЗаказПокупателя КАК ЗаказПокупателя
	|ИЗ
	|	Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
	|ГДЕ
	|	ЗаказНаПроизводствоПродукция.Ссылка В(&МассивЗаказов)
	|	И ЗаказНаПроизводствоПродукция.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказНаПроизводствоПродукция.ЗаказПокупателя";
	МассивЗаказов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ЗаказПокупателя");
	
	Если МассивЗаказов.Количество() = 0 Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	0 КАК НомерСтроки,
		|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка) КАК Этап,
		|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка) КАК Спецификация,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Операция,
		|	0 КАК НормаВремени,
		|	0 КАК Количество
		|ПОМЕСТИТЬ ТаблицаОперацийЗаказовПокупателей";
		Запрос.Выполнить();
		
	Иначе
		
		ПроизводствоСервер.РазузловатьОперации(Запрос, МассивЗаказов, "Запасы");
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаОпераций.НомерСтроки КАК НомерСтроки,
		|	ТаблицаОпераций.Этап КАК Этап,
		|	ТаблицаОпераций.Спецификация КАК Спецификация,
		|	ТаблицаОпераций.Операция КАК Операция,
		|	ТаблицаОпераций.НормаВремени КАК НормаВремени,
		|	ТаблицаОпераций.Количество КАК Количество
		|ПОМЕСТИТЬ ТаблицаОперацийЗаказовПокупателей
		|ИЗ
		|	ТаблицаОпераций КАК ТаблицаОпераций
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаблицаОпераций";
		Запрос.Выполнить();
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбъединитьСоставыСпецификацийЗаказов(Запрос)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаОперацийЗаказовПокупателей.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОперацийЗаказовПокупателей.Этап КАК Этап,
	|	ТаблицаОперацийЗаказовПокупателей.Спецификация КАК Спецификация,
	|	ТаблицаОперацийЗаказовПокупателей.Операция КАК Операция,
	|	ТаблицаОперацийЗаказовПокупателей.Количество КАК Количество
	|ПОМЕСТИТЬ СоставыСпецификаций
	|ИЗ
	|	ТаблицаОперацийЗаказовПокупателей КАК ТаблицаОперацийЗаказовПокупателей
	|ГДЕ
	|	НЕ ТаблицаОперацийЗаказовПокупателей.Спецификация В
	|				(ВЫБРАТЬ
	|					ТаблицаОпераций.Спецификация
	|				ИЗ
	|					ТаблицаОпераций КАК ТаблицаОпераций)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаОпераций.НомерСтроки,
	|	ТаблицаОпераций.Этап,
	|	ТаблицаОпераций.Спецификация,
	|	ТаблицаОпераций.Операция,
	|	ТаблицаОпераций.Количество
	|ИЗ
	|	ТаблицаОпераций КАК ТаблицаОпераций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаОпераций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставыСпецификаций.НомерСтроки КАК НомерСтроки,
	|	СоставыСпецификаций.Этап КАК Этап,
	|	СоставыСпецификаций.Спецификация КАК Спецификация,
	|	СоставыСпецификаций.Операция КАК Операция,
	|	СоставыСпецификаций.Количество КАК Количество
	|ПОМЕСТИТЬ ТаблицаОпераций
	|ИЗ
	|	СоставыСпецификаций КАК СоставыСпецификаций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СоставыСпецификаций";
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ОстаткиЗаказа(Основание, Запрос)
	
	Если ТипЗнч(Основание) = Тип("Массив") Тогда
		Если Основание.Количество() = 0 Тогда
			Возврат Неопределено;
		КонецЕсли; 
		ПерваяСсылка = Основание[0];
		Основания = Основание;
	Иначе
		ПерваяСсылка = Основание;
		Основания = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Основание);
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Основания", Основания);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеТЧ.Номенклатура КАК Номенклатура,
	|	ДанныеТЧ.Характеристика КАК Характеристика,
	|	ДанныеТЧ.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ДанныеТЧ.Спецификация КАК Спецификация
	|ПОМЕСТИТЬ ДанныеОтбора
	|ИЗ
	|	Документ.ЗаказНаПроизводство.Продукция КАК ДанныеТЧ
	|ГДЕ
	|	ДанныеТЧ.Ссылка В(&Основания)
	|	И ДанныеТЧ.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеТЧ.ЗаказПокупателя,
	|	ДанныеТЧ.Номенклатура,
	|	ДанныеТЧ.Характеристика,
	|	ДанныеТЧ.Спецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОтбора.ЗаказПокупателя КАК ЗаказПокупателя
	|ИЗ
	|	ДанныеОтбора КАК ДанныеОтбора
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеОтбора.ЗаказПокупателя";
	ВидОперацииОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПерваяСсылка, "ВидОперации");
	Если ВидОперацииОснование = Перечисления.ВидыОперацийЗаказНаПроизводство.Сборка
		ИЛИ ВидОперацииОснование=Перечисления.ВидыОперацийСборкаЗапасов.Сборка Тогда
		ИмяТЧ = "Продукция";
	Иначе
		ИмяТЧ = "Запасы";
	КонецЕсли;
	ИмяДокумента = ПерваяСсылка.Метаданные().Имя;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЗаказНаПроизводство.Продукция", ИмяДокумента + "." + ИмяТЧ);
	МассивЗаказов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ЗаказПокупателя");
	Если МассивЗаказов.Количество()=0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	Документы.ЗаказПокупателя.ДобавитьТаблицуЗапасыВМенеджерВременныхТаблиц(МассивЗаказов, Запрос.МенеджерВременныхТаблиц, Ложь);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыОстатки.Характеристика КАК Характеристика,
	|	ЗаказыОстатки.Операция КАК Операция,
	|	ЗаказыОстатки.Этап КАК Этап,
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТЗаказПокупателяЗапасы.Ссылка КАК ЗаказПокупателя,
	|		ВТЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
	|		ВТЗаказПокупателяЗапасы.Характеристика КАК Характеристика,
	|		ТаблицаОпераций.Операция КАК Операция,
	|		ТаблицаОпераций.Этап КАК Этап,
	|		(ВТЗаказПокупателяЗапасы.Количество - ВТЗаказПокупателяЗапасы.Резерв) * ВТЗаказПокупателяЗапасы.Коэффициент * ТаблицаОпераций.Количество КАК КоличествоОстаток,
	|		(ВТЗаказПокупателяЗапасы.Количество - ВТЗаказПокупателяЗапасы.Резерв) * ВТЗаказПокупателяЗапасы.Коэффициент * ТаблицаОпераций.Количество КАК Запланировано
	|	ИЗ
	|		ВТЗаказПокупателяЗапасы КАК ВТЗаказПокупателяЗапасы
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОпераций КАК ТаблицаОпераций
	|			ПО ВТЗаказПокупателяЗапасы.Спецификация = ТаблицаОпераций.Спецификация
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПокупателя КАК ДокументЗаказПокупателя
	|			ПО ВТЗаказПокупателяЗапасы.Ссылка = ДокументЗаказПокупателя.Ссылка
	|	ГДЕ
	|		(ВТЗаказПокупателяЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|				ИЛИ ВТЗаказПокупателяЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|		И ДокументЗаказПокупателя.Проведен
	|		И (ВТЗаказПокупателяЗапасы.Ссылка, ВТЗаказПокупателяЗапасы.Номенклатура, ВТЗаказПокупателяЗапасы.Характеристика) В
	|				(ВЫБРАТЬ
	|					ДанныеОтбора.ЗаказПокупателя,
	|					ДанныеОтбора.Номенклатура,
	|					ДанныеОтбора.Характеристика
	|				ИЗ
	|					ДанныеОтбора)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СдельныеНаряды.ЗаказПокупателя,
	|		СдельныеНаряды.Номенклатура,
	|		СдельныеНаряды.Характеристика,
	|		СдельныеНаряды.Операция,
	|		СдельныеНаряды.Этап,
	|		-СдельныеНаряды.КоличествоПланОборот,
	|		0
	|	ИЗ
	|		РегистрНакопления.СдельныеНаряды.Обороты(
	|				,
	|				,
	|				Авто,
	|				(ЗаказПокупателя, Номенклатура, Характеристика) В
	|					(ВЫБРАТЬ
	|						ДанныеОтбора.ЗаказПокупателя,
	|						ДанныеОтбора.Номенклатура,
	|						ДанныеОтбора.Характеристика
	|					ИЗ
	|						ДанныеОтбора)) КАК СдельныеНаряды
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаСдельныеНаряды.ЗаказПокупателя,
	|		ДвиженияДокументаСдельныеНаряды.Номенклатура,
	|		ДвиженияДокументаСдельныеНаряды.Характеристика,
	|		ДвиженияДокументаСдельныеНаряды.Операция,
	|		ДвиженияДокументаСдельныеНаряды.Этап,
	|		ДвиженияДокументаСдельныеНаряды.КоличествоПлан,
	|		0
	|	ИЗ
	|		РегистрНакопления.СдельныеНаряды КАК ДвиженияДокументаСдельныеНаряды
	|	ГДЕ
	|		ДвиженияДокументаСдельныеНаряды.Регистратор = &Ссылка
	|		И (ДвиженияДокументаСдельныеНаряды.ЗаказПокупателя, ДвиженияДокументаСдельныеНаряды.Номенклатура, ДвиженияДокументаСдельныеНаряды.Характеристика) В
	|				(ВЫБРАТЬ
	|					ДанныеОтбора.ЗаказПокупателя,
	|					ДанныеОтбора.Номенклатура,
	|					ДанныеОтбора.Характеристика
	|				ИЗ
	|					ДанныеОтбора)) КАК ЗаказыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыОстатки.ЗаказПокупателя,
	|	ЗаказыОстатки.Номенклатура,
	|	ЗаказыОстатки.Характеристика,
	|	ЗаказыОстатки.Операция,
	|	ЗаказыОстатки.Этап
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЗаказыОстатки.Запланировано) > 0";
	ТаблицаОстатковЗаказа = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаОстатковЗаказа;
	
КонецФункции

Процедура ЗаполнитьОперации(ВыборкаДанных, 
	ТаблицаОстатков, 
	ТаблицаОстатковЗаказа = Неопределено, 
	ЗаполнятьФактическоеКоличество = Ложь, 
	СоответствиеКлючейСвязи = Неопределено)
	
	СохранятьКлючСвязи = (ВыборкаДанных.Владелец().Колонки.Найти("КлючСвязи") <> Неопределено);
	Если СохранятьКлючСвязи ИЛИ ТипЗнч(СоответствиеКлючейСвязи) <> Тип("Соответствие") Тогда
		СоответствиеКлючейСвязи = Новый Соответствие;
	КонецЕсли;
	
	ЕстьОснование = (ТаблицаОстатков.Колонки.Найти("Основание") <> Неопределено);
	ЕстьЭтап = (ТаблицаОстатков.Колонки.Найти("Этап") <> Неопределено);
	
	Операции.Очистить();
	КлючСвязи = 0;
	Если ТаблицаОстатков.Количество() > 0 И ВыборкаДанных.Количество() > 0 Тогда
		
		ТаблицаОбщихОстатков = ТаблицаОстатков.Скопировать();
		ТаблицаОбщихОстатков.Свернуть("Номенклатура, Характеристика", "Остаток");
		ТаблицаОбщихОстатков.Индексы.Добавить("Номенклатура,Характеристика");
		ТаблицаОстатков.Индексы.Добавить("Номенклатура,Характеристика,Операция");
		
		Пока ВыборкаДанных.Следующий() Цикл
			
			СтруктураДляПоиска = Новый Структура;
			СтруктураДляПоиска.Вставить("Номенклатура", ВыборкаДанных.Номенклатура);
			СтруктураДляПоиска.Вставить("Характеристика", ВыборкаДанных.Характеристика);
			
			МассивОбщихОстатков = ТаблицаОбщихОстатков.НайтиСтроки(СтруктураДляПоиска);
			Если МассивОбщихОстатков.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			ОбщийОстаток = МассивОбщихОстатков[0].Остаток;
			
			СтруктураДляПоиска.Вставить("Операция", ВыборкаДанных.Операция);
			Если ЕстьЭтап Тогда
				СтруктураДляПоиска.Вставить("Этап", ВыборкаДанных.Этап);
			КонецЕсли; 
			
			ОстатокПоЗаказу = Неопределено;
			Если ТаблицаОстатковЗаказа <> Неопределено 
				И ЗначениеЗаполнено(ВыборкаДанных.ЗаказПокупателя) Тогда
				СтруктураДляПоиска.Вставить("ЗаказПокупателя", ВыборкаДанных.ЗаказПокупателя);
				МассивОстатковПоЗаказу = ТаблицаОстатковЗаказа.НайтиСтроки(СтруктураДляПоиска);
				Если МассивОстатковПоЗаказу.Количество() > 0 Тогда
					ОстатокПоЗаказу = МассивОстатковПоЗаказу[0].Остаток;
				КонецЕсли;
				СтруктураДляПоиска.Удалить("ЗаказПокупателя");
			КонецЕсли; 
			
			Если ЕстьОснование Тогда
				СтруктураДляПоиска.Вставить("Основание", ВыборкаДанных.Основание);
			КонецЕсли; 
			
			МассивОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураДляПоиска);
			Остаток = ?(МассивОстатков.Количество() = 0, 0, МассивОстатков[0].Остаток);
			
			КоличествоКСписанию = ВыборкаДанных.КоличествоПлан;
			МожноСписать = Мин(ОбщийОстаток, Остаток);
			Если ОстатокПоЗаказу <> Неопределено Тогда
				МожноСписать = Мин(МожноСписать, ОстатокПоЗаказу);
			КонецЕсли; 
			Если НЕ ЗначениеЗаполнено(ВыборкаДанных.Спецификация) Тогда
				// Строки с незаполненной спецификацией не контролируются
				МожноСписать = КоличествоКСписанию;
			КонецЕсли; 
			Списать = Мин(МожноСписать, КоличествоКСписанию);
			Если Списать <= 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			КлючСвязи = КлючСвязи + 1;
			НоваяСтрока = Операции.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДанных);
			Если НЕ Константы.ФункциональнаяОпцияРезервированиеЗапасов.Получить() Тогда
				НоваяСтрока.ЗаказПокупателя = Документы.ЗаказПокупателя.ПустаяСсылка();
			КонецЕсли; 
			НоваяСтрока.КлючСвязи = КлючСвязи;
			Если СохранятьКлючСвязи И ЗначениеЗаполнено(ВыборкаДанных.КлючСвязи) Тогда
				Если ЕстьОснование Тогда
					Если СоответствиеКлючейСвязи.Получить(ВыборкаДанных.Основание) = Неопределено Тогда
						СоответствиеКлючейСвязи.Вставить(ВыборкаДанных.Основание, Новый Соответствие)
					КонецЕсли; 
					СоответствиеКлючейСвязи.Получить(ВыборкаДанных.Основание).Вставить(КлючСвязи, ВыборкаДанных.КлючСвязи);
				Иначе
					СоответствиеКлючейСвязи.Вставить(КлючСвязи, ВыборкаДанных.КлючСвязи);
				КонецЕсли; 
			КонецЕсли; 
			
			НоваяСтрока.КоличествоПлан = Списать;
			Если ЗаполнятьФактическоеКоличество Тогда
				НоваяСтрока.КоличествоФакт = НоваяСтрока.КоличествоПлан;
				НоваяСтрока.Нормочасы = НоваяСтрока.НормаВремени * НоваяСтрока.КоличествоФакт;
				Если НЕ ВыборкаДанных.ФиксированнаяСтоимость Тогда
					НоваяСтрока.Стоимость = НоваяСтрока.Расценка * НоваяСтрока.НормаВремени * НоваяСтрока.КоличествоФакт;
				Иначе
					НоваяСтрока.Стоимость = НоваяСтрока.Расценка * НоваяСтрока.КоличествоФакт;
				КонецЕсли; 
			КонецЕсли; 
			
			МассивОбщихОстатков[0].Остаток = МассивОбщихОстатков[0].Остаток - Списать;
			Если МассивОбщихОстатков[0].Остаток <= 0 Тогда
				ТаблицаОбщихОстатков.Удалить(МассивОбщихОстатков[0]);
			КонецЕсли;
			Если МассивОстатков.Количество() > 0 Тогда
				МассивОстатков[0].Остаток = МассивОстатков[0].Остаток - Списать;
				Если МассивОстатков[0].Остаток <= 0 Тогда
					ТаблицаОстатков.Удалить(МассивОстатков[0]);
				КонецЕсли;
			КонецЕсли;
			Если ТаблицаОстатковЗаказа <> Неопределено 
				И ЗначениеЗаполнено(ВыборкаДанных.ЗаказПокупателя)
				И МассивОстатковПоЗаказу.Количество() > 0 Тогда
				МассивОстатковПоЗаказу[0].Остаток = МассивОстатковПоЗаказу[0].Остаток - Списать;
			КонецЕсли; 
				
		КонецЦикла;
		
	КонецЕсли;
	
	ДействияПослеЗаполненияТЧ();
	
КонецПроцедуры
 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПустаяТаблицаСостава()
	
	ТаблицаСостав = Новый ТаблицаЗначений;
	
	ТаблицаСостав.Колонки.Добавить("БазоваяСпецификация", Новый ОписаниеТипов("СправочникСсылка.Спецификации"));
	ТаблицаСостав.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаСостав.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаСостав.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаСостав.Колонки.Добавить("Спецификация", Новый ОписаниеТипов("СправочникСсылка.Спецификации"));
	ТаблицаСостав.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	ТаблицаСостав.Колонки.Добавить("ДоляСтоимости", Новый ОписаниеТипов("Число"));
	
	Возврат ТаблицаСостав;
	
КонецФункции

Процедура ПроверитьЗаполнениеЭтаповПроизводства(Отказ, ОтменаПроведения = Ложь)
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьЭтапыПроизводства") Тогда
		Возврат;
	КонецЕсли;
	
	// Формирование таблицы продукции, использующей этапы производства
	ИменаКолонок = "НомерСтроки, Номенклатура, Характеристика, Партия, Спецификация, ЗаказПокупателя, ЗаказНаПроизводство, Этап, ПодразделениеЗавершающегоЭтапа, КоличествоФакт";
	Если ОтменаПроведения Тогда
		ТаблицаПродукции = Операции.ВыгрузитьКолонки(ИменаКолонок);
	Иначе
		ТаблицаПродукции = Операции.Выгрузить(, ИменаКолонок);
	КонецЕсли; 
	ТаблицаПродукции.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения,СправочникСсылка.КлассификаторЕдиницИзмерения"));
	ТаблицаПродукции.ЗаполнитьЗначения(Справочники.КлассификаторЕдиницИзмерения.шт, "ЕдиницаИзмерения");
	ТаблицаПродукции.Колонки.Добавить("ИмяТЧ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(100)));
	ТаблицаПродукции.ЗаполнитьЗначения("Операции", "ИмяТЧ");
	ТаблицаПродукции.Колонки.КоличествоФакт.Имя = "Количество";
	Если ПоложениеЗаказаНаПроизводство<>Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		ТаблицаПродукции.ЗаполнитьЗначения(ЗаказНаПроизводство, "ЗаказНаПроизводство");
	КонецЕсли;
	МассивСпецификаций = Новый Массив;
	Для каждого СтрокаТабличнойЧасти Из ТаблицаПродукции Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
			Продолжить;
		КонецЕсли;
		Если МассивСпецификаций.Найти(СтрокаТабличнойЧасти.Спецификация)=Неопределено Тогда
			МассивСпецификаций.Добавить(СтрокаТабличнойЧасти.Спецификация);
		КонецЕсли; 
	КонецЦикла; 
	МассивСпецификаций = ПроизводствоСервер.СпецификацииСПоэтапнымПроизводством(МассивСпецификаций);
	ПроверяемаяПродукция = ТаблицаПродукции.СкопироватьКолонки();
	Для каждого СтрокаПродукция Из ТаблицаПродукции Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаПродукция.Спецификация) ИЛИ МассивСпецификаций.Найти(СтрокаПродукция.Спецификация)=Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказНаПроизводство) 
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТабличнойЧасти.ЗаказНаПроизводство, "ВидОперации")=Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка Тогда
			// Разборка не может выполняться поэтапно
			Продолжить;
		КонецЕсли; 
		ЗаполнитьЗначенияСвойств(ПроверяемаяПродукция.Добавить(), СтрокаПродукция);
		Если НЕ ЗначениеЗаполнено(СтрокаПродукция.Этап) Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнен этап производства в строке %1 списка ""Операции""'"),
				СтрокаПродукция.НомерСтроки);
			КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Операции", СтрокаПродукция.НомерСтроки,
				"Этап");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
		КонецЕсли; 
		Если Не ЗначениеЗаполнено(СтрокаПродукция.ЗаказНаПроизводство)
			И Не ЗначениеЗаполнено(СтрокаПродукция.ЗаказПокупателя) Тогда
			ТекстСообщения = СтрШаблон(НСтр(
				"ru = 'Для использования поэтапной сборки требуется указать заказ покупателя или заказ на производство (строка %1)'"),
				СтрокаПродукция.НомерСтроки);
			КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Операции", СтрокаПродукция.НомерСтроки,
				"Спецификация");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
		КонецЕсли; 
	КонецЦикла;
	
	Если ПолучитьФункциональнуюОпцию("ВыполнениеЭтаповРазнымиПодразделениями") Тогда
		Для Каждого СтрокаПродукция Из ПроверяемаяПродукция Цикл
			Если Не ЗначениеЗаполнено(СтрокаПродукция.ПодразделениеЗавершающегоЭтапа) Тогда
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'Не заполнена колонка ""Завершающее подразделение""  в строке %1 списка ""Продукция""'"),
					СтрокаПродукция.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция",
					СтрокаПродукция.НомерСтроки, "ПодразделениеЗавершающегоЭтапа");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
	
	// Проверка уникальности завершающего подразделения
	ПроизводствоСервер.ПроверкаПоэтапногоПроизводства(ЭтотОбъект, ПроверяемаяПродукция, Отказ);
	
КонецПроцедуры

Процедура ДобавитьТаблицуОперацийПоЗаказуНаПроизводство(Запрос, ЗаказНаПроизводство)
	
	Запрос.УстановитьПараметр("ЗаказНаПроизводство", ЗаказНаПроизводство);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Уровень,
	|	1 КАК Порядок,
	|	Операции.НомерСтроки КАК НомерСтроки,
	|	Операции.Этап КАК Этап,
	|	Продукция.Спецификация КАК Спецификация,
	|	НЕОПРЕДЕЛЕНО КАК ВложеннаяСпецификация,
	|	ЛОЖЬ КАК ЭтоУзел,
	|	Операции.Операция КАК Операция,
	|	Операции.КоличествоПлан / ВЫБОР
	|		КОГДА Продукция.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ Продукция.Количество
	|	КОНЕЦ КАК Количество,
	|	Операции.НормаВремени КАК НормаВремени,
	|	Операции.Исполнитель КАК Исполнитель
	|ПОМЕСТИТЬ ТаблицаОпераций
	|ИЗ
	|	Документ.ЗаказНаПроизводство.Операции КАК Операции
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Продукция.Спецификация КАК Спецификация,
	|			Продукция.Количество * ВЫБОР
	|				КОГДА Продукция.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|						И Продукция.ЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|					ТОГДА Продукция.ЕдиницаИзмерения.Коэффициент
	|				ИНАЧЕ 1
	|			КОНЕЦ КАК Количество,
	|			Продукция.КлючСвязи КАК КлючСвязи
	|		ИЗ
	|			Документ.ЗаказНаПроизводство.Продукция КАК Продукция
	|		ГДЕ
	|			Продукция.Ссылка = &ЗаказНаПроизводство) КАК Продукция
	|		ПО Операции.КлючСвязиПродукция = Продукция.КлючСвязи
	|ГДЕ
	|	Операции.Ссылка = &ЗаказНаПроизводство
	|	И НЕ Продукция.КлючСвязи ЕСТЬ NULL";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПроверитьЗачислениеСебестоимости(Отказ)
	
	ДокументыПроверки = Новый ТаблицаЗначений;
	ДокументыПроверки.Колонки.Добавить("Документ", Новый ОписаниеТипов("ДокументСсылка.СборкаЗапасов, ДокументСсылка.ЗаказНаПроизводство"));
	ДокументыПроверки.Колонки.Добавить("СтруктурнаяЕдиница", Новый ОписаниеТипов("СправочникСсылка.СтруктурныеЕдиницы"));
	ДокументыПроверки.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5, 0)));
	
	Если ЗначениеЗаполнено(ДокументОснование)
		И ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.СборкаЗапасов") Тогда
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Этап) Тогда
				Продолжить;
			КонецЕсли; 
			МассивСтруктурныхЕдиниц = СтруктурныеЕдиницыПроверки(СтрокаТабличнойЧасти);
			Для каждого ПроверяемаяСтруктурнаяЕдиница Из МассивСтруктурныхЕдиниц Цикл
				НоваяСтрока = ДокументыПроверки.Добавить();
				НоваяСтрока.Документ = ДокументОснование;
				НоваяСтрока.СтруктурнаяЕдиница = ПроверяемаяСтруктурнаяЕдиница;
				НоваяСтрока.НомерСтроки = СтрокаТабличнойЧасти.НомерСтроки;
			КонецЦикла; 
		КонецЦикла;
	КонецЕсли; 
	Для каждого СтрокаТабличнойЧасти Из Операции Цикл
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Этап) Тогда
			Продолжить;
		КонецЕсли; 
		ЗаказПроверки = ?(ПоложениеЗаказаНаПроизводство=Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти, СтрокаТабличнойЧасти.ЗаказНаПроизводство, ЗаказНаПроизводство);
		Если НЕ ЗначениеЗаполнено(ЗаказПроверки) Тогда
			Продолжить;
		КонецЕсли; 
		МассивСтруктурныхЕдиниц = СтруктурныеЕдиницыПроверки(СтрокаТабличнойЧасти);
		Для каждого ПроверяемаяСтруктурнаяЕдиница Из МассивСтруктурныхЕдиниц Цикл
			НоваяСтрока = ДокументыПроверки.Добавить();
			НоваяСтрока.Документ = ЗаказПроверки;
			НоваяСтрока.СтруктурнаяЕдиница = ПроверяемаяСтруктурнаяЕдиница;
			НоваяСтрока.НомерСтроки = СтрокаТабличнойЧасти.НомерСтроки;
		КонецЦикла; 
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументыПроверки", ДокументыПроверки);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументыПроверки.Документ КАК Документ,
	|	ДокументыПроверки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ДокументыПроверки.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ДокументыПроверки
	|ИЗ
	|	&ДокументыПроверки КАК ДокументыПроверки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Документ КАК Документ,
	|	ВложенныйЗапрос.НомерСтроки КАК НомерСтроки,
	|	ВложенныйЗапрос.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	СборкаЗапасов.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаФакт
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументыПроверки.Документ КАК Документ,
	|		ДокументыПроверки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		МИНИМУМ(ДокументыПроверки.НомерСтроки) КАК НомерСтроки
	|	ИЗ
	|		ДокументыПроверки КАК ДокументыПроверки
	|	ГДЕ
	|		ДокументыПроверки.Документ ССЫЛКА Документ.СборкаЗапасов
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДокументыПроверки.Документ,
	|		ДокументыПроверки.СтруктурнаяЕдиница) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СборкаЗапасов КАК СборкаЗапасов
	|		ПО ВложенныйЗапрос.Документ = СборкаЗапасов.Ссылка
	|ГДЕ
	|	ВложенныйЗапрос.СтруктурнаяЕдиница <> СборкаЗапасов.СтруктурнаяЕдиница
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Документ,
	|	ВложенныйЗапрос.НомерСтроки,
	|	ВложенныйЗапрос.СтруктурнаяЕдиница,
	|	ЗаказНаПроизводство.СтруктурнаяЕдиница
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументыПроверки.Документ КАК Документ,
	|		ДокументыПроверки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		МИНИМУМ(ДокументыПроверки.НомерСтроки) КАК НомерСтроки
	|	ИЗ
	|		ДокументыПроверки КАК ДокументыПроверки
	|	ГДЕ
	|		ДокументыПроверки.Документ ССЫЛКА Документ.ЗаказНаПроизводство
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДокументыПроверки.Документ,
	|		ДокументыПроверки.СтруктурнаяЕдиница) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|		ПО ВложенныйЗапрос.Документ = ЗаказНаПроизводство.Ссылка
	|ГДЕ
	|	ВложенныйЗапрос.СтруктурнаяЕдиница <> ЗаказНаПроизводство.СтруктурнаяЕдиница";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ТекстСообщения = СтрШаблон(НСтр(
			"ru = 'Изготовитель %3 документа %1 не соответствует структурной единице указанной в сдельном наряде (%2)'"),
			Выборка.Документ, Выборка.СтруктурнаяЕдиница, Выборка.СтруктурнаяЕдиницаФакт);
		Если ПоложениеСтруктурнойЕдиницы=Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Операции", Выборка.НомерСтроки,
				"СтруктурнаяЕдиница");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
		Иначе
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "СтруктурнаяЕдиница", , Отказ);
		КонецЕсли; 
	КонецЦикла; 
		
КонецПроцедуры

Функция СтруктурныеЕдиницыПроверки(СтрокаОперации)
	
	Результат = Новый Массив;
	Если ПоложениеСтруктурнойЕдиницы<>Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Результат.Добавить(СтруктурнаяЕдиница);
	ИначеЕсли ТипЗнч(СтрокаОперации.Исполнитель)=Тип("СправочникСсылка.Бригады") Тогда
		СтрокиБригады = ТабличныеЧастиУНФКлиентСервер.СтрокиПоКлючуСвязи(СоставБригады, СтрокаОперации.КлючСвязи);
		Для каждого СтрокаБригады Из СтрокиБригады Цикл
			Результат.Добавить(СтрокаБригады.СтруктурнаяЕдиница);
		КонецЦикла;
	Иначе
		Результат.Добавить(СтрокаОперации.СтруктурнаяЕдиница);	
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

Процедура ПроверитьСчетаУчетаЗатратНоменклатуры()
	
	МассивНоменклатуры = Новый Массив;
	Для каждого СтрокаТабличнойЧасти Из Операции Цикл
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) И МассивНоменклатуры.Найти(СтрокаТабличнойЧасти.Номенклатура)=Неопределено Тогда
			МассивНоменклатуры.Добавить(СтрокаТабличнойЧасти.Номенклатура);
		КонецЕсли; 
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", МассивНоменклатуры);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	Номенклатура.СчетУчетаЗатрат.ТипСчета КАК ТипСчета
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&Номенклатура)
	|	И НЕ Номенклатура.СчетУчетаЗатрат.ТипСчета В (ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.НезавершенноеПроизводство), ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.КосвенныеЗатраты))";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаТабличнойЧасти = Операции.Найти(Выборка.Номенклатура, "Номенклатура");
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Номенклатура %1 использует счет учета затрат с типом ""%2"". Для таких позиций расчет себестоимости не будет выполнен.
										|Следует использовать счета с типом ""Незавершенное производство"" и ""Косвенные затраты""'"),
			Выборка.Номенклатура, Выборка.ТипСчета);
		КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Операции",
			СтрокаТабличнойЧасти.НомерСтроки, "Номенклатура");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле);
	КонецЦикла; 
		
КонецПроцедуры

Процедура ЗаполнитьЗначенияПереключаемыхРеквизитовВТЧ()
		
	ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "Операции", "СтруктурнаяЕдиница", "ПоложениеСтруктурнойЕдиницы");
	ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "СоставБригады", "СтруктурнаяЕдиница", "ПоложениеСтруктурнойЕдиницы");
	ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "Операции", "Исполнитель", "ПоложениеИсполнителя");
	ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "Операции", "ЗаказНаПроизводство", "ПоложениеЗаказаНаПроизводство");
	
КонецПроцедуры
 
#КонецОбласти 

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли