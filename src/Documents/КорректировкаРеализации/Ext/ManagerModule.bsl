#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Контрагент)
	|	И ЗначениеРазрешено(СтруктурнаяЕдиница)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Проведение

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасы(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаЗапасы.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ЛОЖЬ КАК Возврат,
	|	ТаблицаЗапасы.Документ КАК Документ,
	|	ТаблицаЗапасы.Документ КАК ДокументПродажи,
	|	ТаблицаЗапасы.Заказ КАК ЗаказПродажи,
	|	ТаблицаЗапасы.ПодразделениеПродажи КАК Подразделение,
	|	ТаблицаЗапасы.Ответственный КАК Ответственный,
	|	ТаблицаЗапасы.ПодразделениеПродажи КАК ПодразделениеПродажи,
	|	ТаблицаЗапасы.НаправлениеДеятельностиПродажи КАК НаправлениеДеятельности,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость КАК СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.КоррОрганизация КАК КоррОрганизация,
	|	ЕСТЬNULL(ТаблицаЗапасы.СтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК СтруктурнаяЕдиница,
	|	ЕСТЬNULL(ТаблицаЗапасы.КоррСтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)) КАК КоррСтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ТаблицаЗапасы.КоррСчетУчета КАК КоррСчетУчета,
	|	ТаблицаЗапасы.ТоварыНаКомиссии КАК ТоварыНаКомиссии,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.КоррНоменклатура КАК КоррНоменклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.КоррХарактеристика КАК КоррХарактеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.КоррПартия КАК КоррПартия,
	|	ТаблицаЗапасы.Заказ КАК ЗаказПокупателя,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка) КАК КоррЗаказПокупателя,
	|	СУММА(ТаблицаЗапасы.Количество) КАК Количество,
	|	СУММА(ТаблицаЗапасы.Резерв) КАК Резерв,
	|	ТаблицаЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ТаблицаЗапасы.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ТаблицаЗапасы.Сумма) КАК Сумма,
	|	0 КАК Себестоимость,
	|	ЛОЖЬ КАК ФиксированнаяСтоимость,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость КАК СчетДт,
	|	ТаблицаЗапасы.СчетУчета КАК СчетКт,
	|	ВЫРАЗИТЬ(&СписаниеЗапасов КАК СТРОКА(100)) КАК Содержание,
	|	ВЫРАЗИТЬ(&СписаниеЗапасов КАК СТРОКА(100)) КАК СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы,
	|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
	|ГДЕ
	|	ТаблицаЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Период,
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.Документ,
	|	ТаблицаЗапасы.ПодразделениеПродажи,
	|	ТаблицаЗапасы.НаправлениеДеятельностиПродажи,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.КоррОрганизация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.КоррСтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.КоррСчетУчета,
	|	ТаблицаЗапасы.ТоварыНаКомиссии,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.КоррНоменклатура,
	|	ТаблицаЗапасы.КоррХарактеристика,
	|	ТаблицаЗапасы.КоррПартия,
	|	ТаблицаЗапасы.Ответственный,
	|	ТаблицаЗапасы.СтавкаНДС,
	|	ТаблицаЗапасы.Заказ,
	|	ТаблицаЗапасы.Документ,
	|	ТаблицаЗапасы.ПодразделениеПродажи,
	|	ТаблицаЗапасы.Заказ,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.СчетУчета";
	
	Запрос.УстановитьПараметр("СписаниеЗапасов", НСтр("ru = 'Списание запасов'"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасы", РезультатЗапроса.Выгрузить());
	
	СформироватьТаблицаЗапасыПродажа(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // СформироватьТаблицаЗапасы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыРаботы(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаЗапасы.Период КАК Период,
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ТаблицаЗапасы.Документ КАК Документ,
	|	ТаблицаЗапасы.Документ КАК ДокументПродажи,
	|	ТаблицаЗапасы.ДокументОснование КАК ЗаказПродажи,
	|	ТаблицаЗапасы.ПодразделениеПродажи КАК Подразделение,
	|	ТаблицаЗапасы.Ответственный КАК Ответственный,
	|	ТаблицаЗапасы.НаправлениеДеятельностиПродажи КАК НаправлениеДеятельности,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость КАК СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ВЫБОР
	|		КОГДА Константы.ФункциональнаяОпцияРезервированиеЗапасов
	|			ТОГДА ТаблицаЗапасы.ДокументОснование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ТаблицаЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ТаблицаЗапасы.Количество) КАК Количество,
	|	0 КАК Сумма,
	|	СУММА(ТаблицаЗапасы.СуммаНДС) КАК СуммаНДС,
	|	ЛОЖЬ КАК ФиксированнаяСтоимость,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость КАК СчетДт,
	|	ТаблицаЗапасы.СчетУчета КАК СчетКт,
	|	ВЫРАЗИТЬ(&СборкаЗапасов КАК СТРОКА(100)) КАК СодержаниеПроводки,
	|	ВЫРАЗИТЬ(&СборкаЗапасов КАК СТРОКА(100)) КАК Содержание,
	|	ЛОЖЬ КАК ЗатратыНаВыпуск,
	|	ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка) КАК КоррСтруктурнаяЕдиница,
	|	ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка) КАК КоррСчетУчета,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК КоррНоменклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК КоррХарактеристика,
	|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка) КАК КоррПартия,
	|	НЕОПРЕДЕЛЕНО КАК КоррЗаказПокупателя,
	|	ТаблицаЗапасы.Спецификация КАК Спецификация,
	|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка) КАК КоррСпецификация
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Период,
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.Документ,
	|	ТаблицаЗапасы.НаправлениеДеятельностиПродажи,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.ДокументОснование,
	|	ТаблицаЗапасы.Спецификация,
	|	ТаблицаЗапасы.СтавкаНДС,
	|	Константы.ФункциональнаяОпцияРезервированиеЗапасов,
	|	ТаблицаЗапасы.Ответственный,
	|	ТаблицаЗапасы.ПодразделениеПродажи,
	|	ТаблицаЗапасы.Период,
	|	ТаблицаЗапасы.Документ,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.СчетУчета";
	
	Запрос.УстановитьПараметр("СборкаЗапасов", НСтр("ru = 'Сборка запасов'"));
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасы", РезультатЗапроса.Выгрузить());
	
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	ВременнаяТаблицаЗапасы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.СкопироватьКолонки();
	
	Для н = 0 По СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Количество() - 1 Цикл
		
		СтрокаТаблицаЗапасы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы[н];
		
		КоличествоТребуется = СтрокаТаблицаЗапасы.Количество;
		
		Если КоличествоТребуется <> 0 Тогда
			
			// Добавим выпуск работы.
			СтрокаТаблицыПриход = ВременнаяТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаТаблицаЗапасы);
			
			СтрокаТаблицыПриход.ДокументПродажи = Неопределено;
			СтрокаТаблицыПриход.ЗаказПродажи = Неопределено;
			СтрокаТаблицыПриход.Подразделение = Неопределено;
			СтрокаТаблицыПриход.Ответственный = Неопределено;
			
			СуммаКСписанию = 0;
			
			// Добавим реализацию работы.
			СтрокаТаблицыРасход = ВременнаяТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
			
			СтрокаТаблицыРасход.ВидДвижения = ВидДвиженияНакопления.Расход;
			СтрокаТаблицыРасход.СодержаниеПроводки = НСтр("ru = 'Списание запасов'");
			
			СтрокаТаблицыРасход.Сумма = СуммаКСписанию;
				
		КонецЕсли;
			
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы = ВременнаяТаблицаЗапасы;
	
КонецПроцедуры // СформироватьТаблицаЗапасы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыТовары(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗапасы.НомерСтроки КАК НомерСтроки,
	|	ТаблицаЗапасы.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ИСТИНА КАК Выполнен,
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ТаблицаЗапасы.Документ КАК Документ,
	|	ТаблицаЗапасы.Документ КАК ДокументПродажи,
	|	ТаблицаЗапасы.Заказ КАК ЗаказПродажи,
	|	ТаблицаЗапасы.ПодразделениеПродажи КАК Подразделение,
	|	ТаблицаЗапасы.Ответственный КАК Ответственный,
	|	ТаблицаЗапасы.НаправлениеДеятельностиПродажи КАК НаправлениеДеятельности,
	|	ТаблицаЗапасы.ПодразделениеПродажи КАК ПодразделениеПродажи,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость КАК СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.КоррОрганизация КАК КоррОрганизация,
	|	ЕСТЬNULL(ТаблицаЗапасы.СтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)) КАК СтруктурнаяЕдиница,
	|	ЕСТЬNULL(ТаблицаЗапасы.КоррСтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)) КАК КоррСтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ТаблицаЗапасы.КоррСчетУчета КАК КоррСчетУчета,
	|	ТаблицаЗапасы.ТоварыНаКомиссии КАК ТоварыНаКомиссии,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.КоррНоменклатура КАК КоррНоменклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.КоррХарактеристика КАК КоррХарактеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.КоррПартия КАК КоррПартия,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка) КАК ЗаказПокупателя,
	|	ТаблицаЗапасы.КоррЗаказ КАК КоррЗаказПокупателя,
	|	ТаблицаЗапасы.Количество КАК Количество,
	|	ТаблицаЗапасы.Резерв КАК Резерв,
	|	ТаблицаЗапасы.Резерв КАК РезервОтгрузка,
	|	ТаблицаЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаЗапасы.СуммаНДС КАК СуммаНДС,
	|	ТаблицаЗапасы.Сумма КАК Сумма,
	|	0 КАК Себестоимость,
	|	ЛОЖЬ КАК ФиксированнаяСтоимость,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость КАК СчетДт,
	|	ТаблицаЗапасы.СчетУчета КАК СчетКт,
	|	ВЫРАЗИТЬ(&СписаниеЗапасов КАК СТРОКА(100)) КАК Содержание,
	|	ВЫРАЗИТЬ(&СписаниеЗапасов КАК СТРОКА(100)) КАК СодержаниеПроводки,
	|	ТаблицаЗапасы.ЕстьВДокументеРеализации КАК ЕстьВДокументеРеализации
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|ГДЕ
	|	ТаблицаЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.НомерСтроки,
	|	ТаблицаЗапасы.Период,
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.Заказ,
	|	ТаблицаЗапасы.НаправлениеДеятельностиПродажи,
	|	ТаблицаЗапасы.ПодразделениеПродажи,
	|	ТаблицаЗапасы.Ответственный,
	|	ТаблицаЗапасы.КоррОрганизация,
	|	ТаблицаЗапасы.КоррСчетУчета,
	|	ТаблицаЗапасы.ТоварыНаКомиссии,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.КоррНоменклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.КоррХарактеристика,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.КоррПартия,
	|	ТаблицаЗапасы.КоррЗаказ,
	|	ТаблицаЗапасы.Количество,
	|	ТаблицаЗапасы.Резерв,
	|	ТаблицаЗапасы.СтавкаНДС,
	|	ТаблицаЗапасы.СуммаНДС,
	|	ТаблицаЗапасы.Сумма,
	|	ТаблицаЗапасы.Документ,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.ЕстьВДокументеРеализации,
	|	ЕСТЬNULL(ТаблицаЗапасы.СтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)),
	|	ЕСТЬNULL(ТаблицаЗапасы.КоррСтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)),
	|	ТаблицаЗапасы.Документ,
	|	ТаблицаЗапасы.ПодразделениеПродажи,
	|	ТаблицаЗапасы.Резерв,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.СчетУчета";
	
	Запрос.УстановитьПараметр("СписаниеЗапасов", НСтр("ru = 'Списание запасов'"));
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыЗапасы", Запрос.Выполнить().Выгрузить());
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	// Установка исключительной блокировки контролируемых остатков запасов.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаЗапасы.Организация КАК Организация,
	|		ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|		ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|		ТаблицаЗапасы.Характеристика КАК Характеристика,
	|		ТаблицаЗапасы.Партия КАК Партия,
	|		ВЫБОР
	|			КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				ТОГДА ТаблицаЗапасы.Заказ
	|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		КОНЕЦ КАК ЗаказПокупателя
	|	ИЗ
	|		ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|	ГДЕ
	|		ТаблицаЗапасы.Заказ <> НЕОПРЕДЕЛЕНО
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаЗапасы.Организация,
	|		ТаблицаЗапасы.СтруктурнаяЕдиница,
	|		ТаблицаЗапасы.СчетУчета,
	|		ТаблицаЗапасы.Номенклатура,
	|		ТаблицаЗапасы.Характеристика,
	|		ТаблицаЗапасы.Партия,
	|		ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	ИЗ
	|		ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы) КАК ТаблицаЗапасы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.ЗаказПокупателя";
    
    РезультатЗапроса = Запрос.Выполнить();
    
    Блокировка = Новый БлокировкаДанных;
    ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Запасы");
    ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
    ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
    
    Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
    	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
    КонецЦикла;
    Блокировка.Заблокировать();
	
	ДокументСсылкаЗаказНаряд = СтруктураДополнительныеСвойства.ДляПроведения.ДокументРеализации;
	
    // Получение остатков запасов по стоимости.
    Запрос.Текст =
    "ВЫБРАТЬ
    |	ЗапасыОстатки.Организация КАК Организация,
    |	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
    |	ЗапасыОстатки.СчетУчета КАК СчетУчета,
    |	ЗапасыОстатки.Номенклатура КАК Номенклатура,
    |	ЗапасыОстатки.Характеристика КАК Характеристика,
    |	ЗапасыОстатки.Партия КАК Партия,
    |	ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
    |	СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоОстаток,
    |	СУММА(ЗапасыОстатки.СуммаОстаток) КАК СуммаОстаток
    |ИЗ
    |	(ВЫБРАТЬ
    |		ЗапасыОстатки.Организация КАК Организация,
    |		ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
    |		ЗапасыОстатки.СчетУчета КАК СчетУчета,
    |		ЗапасыОстатки.Номенклатура КАК Номенклатура,
    |		ЗапасыОстатки.Характеристика КАК Характеристика,
    |		ЗапасыОстатки.Партия КАК Партия,
    |		ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
    |		СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоОстаток,
    |		СУММА(ЗапасыОстатки.СуммаОстаток) КАК СуммаОстаток
    |	ИЗ
    |		РегистрНакопления.Запасы.Остатки(
    |				&МоментКонтроля,
    |				(Организация, СтруктурнаяЕдиница, СчетУчета, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
    |					(ВЫБРАТЬ
    |						ТаблицаЗапасы.Организация КАК Организация,
    |						ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
    |						ТаблицаЗапасы.СчетУчета КАК СчетУчета,
    |						ТаблицаЗапасы.Номенклатура КАК Номенклатура,
    |						ТаблицаЗапасы.Характеристика КАК Характеристика,
    |						ТаблицаЗапасы.Партия КАК Партия,
    |						ВЫБОР
    |							КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
    |								ТОГДА ТаблицаЗапасы.Заказ
    |							ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
    |						КОНЕЦ
    |					ИЗ
    |						ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
    |					ГДЕ
    |						ТаблицаЗапасы.Заказ <> НЕОПРЕДЕЛЕНО)) КАК ЗапасыОстатки
    |	
    |	СГРУППИРОВАТЬ ПО
    |		ЗапасыОстатки.Организация,
    |		ЗапасыОстатки.СтруктурнаяЕдиница,
    |		ЗапасыОстатки.СчетУчета,
    |		ЗапасыОстатки.Номенклатура,
    |		ЗапасыОстатки.Характеристика,
    |		ЗапасыОстатки.Партия,
    |		ЗапасыОстатки.ЗаказПокупателя
    |	
    |	ОБЪЕДИНИТЬ ВСЕ
    |	
    |	ВЫБРАТЬ
    |		ЗапасыОстатки.Организация,
    |		ЗапасыОстатки.СтруктурнаяЕдиница,
    |		ЗапасыОстатки.СчетУчета,
    |		ЗапасыОстатки.Номенклатура,
    |		ЗапасыОстатки.Характеристика,
    |		ЗапасыОстатки.Партия,
    |		ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка),
    |		СУММА(ЗапасыОстатки.КоличествоОстаток),
    |		СУММА(ЗапасыОстатки.СуммаОстаток)
    |	ИЗ
    |		РегистрНакопления.Запасы.Остатки(
    |				&МоментКонтроля,
    |				(Организация, СтруктурнаяЕдиница, СчетУчета, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
    |					(ВЫБРАТЬ
    |						ТаблицаЗапасы.Организация,
    |						ТаблицаЗапасы.СтруктурнаяЕдиница,
    |						ТаблицаЗапасы.СчетУчета,
    |						ТаблицаЗапасы.Номенклатура,
    |						ТаблицаЗапасы.Характеристика,
    |						ТаблицаЗапасы.Партия,
    |						ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
    |					ИЗ
    |						ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы)) КАК ЗапасыОстатки
    |	
    |	СГРУППИРОВАТЬ ПО
    |		ЗапасыОстатки.Организация,
    |		ЗапасыОстатки.СтруктурнаяЕдиница,
    |		ЗапасыОстатки.СчетУчета,
    |		ЗапасыОстатки.Номенклатура,
    |		ЗапасыОстатки.Характеристика,
    |		ЗапасыОстатки.Партия
    |	
    |	ОБЪЕДИНИТЬ ВСЕ
    |	
    |	ВЫБРАТЬ
    |		ДвиженияДокументаЗапасы.Организация,
    |		ДвиженияДокументаЗапасы.СтруктурнаяЕдиница,
    |		ДвиженияДокументаЗапасы.СчетУчета,
    |		ДвиженияДокументаЗапасы.Номенклатура,
    |		ДвиженияДокументаЗапасы.Характеристика,
    |		ДвиженияДокументаЗапасы.Партия,
    |		ДвиженияДокументаЗапасы.ЗаказПокупателя,
    |		ВЫБОР
    |			КОГДА ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
    |				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
    |			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
    |		КОНЕЦ,
    |		ВЫБОР
    |			КОГДА ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
    |				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗапасы.Сумма, 0)
    |			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗапасы.Сумма, 0)
    |		КОНЕЦ
    |	ИЗ
    |		РегистрНакопления.Запасы КАК ДвиженияДокументаЗапасы
    |	ГДЕ
    |		ДвиженияДокументаЗапасы.Регистратор = &Ссылка
    |		И ДвиженияДокументаЗапасы.Период <= &ПериодКонтроля) КАК ЗапасыОстатки
    |
    |СГРУППИРОВАТЬ ПО
    |	ЗапасыОстатки.Организация,
    |	ЗапасыОстатки.СтруктурнаяЕдиница,
    |	ЗапасыОстатки.СчетУчета,
    |	ЗапасыОстатки.Номенклатура,
    |	ЗапасыОстатки.Характеристика,
    |	ЗапасыОстатки.Партия,
    |	ЗапасыОстатки.ЗаказПокупателя";
    
    Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКорректировкаРеализации);
    Запрос.УстановитьПараметр("ДокументРеализации", ДокументСсылкаЗаказНаряд);
	Запрос.УстановитьПараметр("МоментКонтроля", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.Ссылка.Дата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.Ссылка.Дата);
    
    РезультатЗапроса = Запрос.Выполнить();
    
    ТаблицаЗапасыОстатки = РезультатЗапроса.Выгрузить();
    ТаблицаЗапасыОстатки.Индексы.Добавить("Организация,СтруктурнаяЕдиница,СчетУчета,Номенклатура,Характеристика,Партия,ЗаказПокупателя");
	
    ПустойЗаказПокупателя = Документы.ЗаказПокупателя.ПустаяСсылка();
	РезервированиеЗапасов = Константы.ФункциональнаяОпцияРезервированиеЗапасов.Получить();
	
	Для н = 0 По СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыЗапасы.Количество() - 1 Цикл
		
		СтрокаТаблицаЗапасы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыЗапасы[н];
		Если СтрокаТаблицаЗапасы.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;

		// Т.к. в заказе-наряде сами резервируем и сами списываем, то всегда списываем из свободного остатка все количество.
		КоличествоТребуетсяВсего = СтрокаТаблицаЗапасы.Количество;
		
		Если КоличествоТребуетсяВсего < 0 Тогда
			
			КоличествоТребуетсяРезерв = ?(-КоличествоТребуетсяВсего > СтрокаТаблицаЗапасы.Резерв, - СтрокаТаблицаЗапасы.Резерв, КоличествоТребуетсяВсего);
			КоличествоТребуетсяСвободныйОстаток = КоличествоТребуетсяВсего - КоличествоТребуетсяРезерв;
			
			Если КоличествоТребуетсяРезерв <> 0 Тогда
				
				// Спишем запасы из свободного остатка.
				СтрокаТаблицыРасход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
				
				СтрокаТаблицыРасход.КоррСчетУчета = СтрокаТаблицаЗапасы.СчетУчета;
				СтрокаТаблицыРасход.КоррСтруктурнаяЕдиница = СтрокаТаблицаЗапасы.СтруктурнаяЕдиница;
				СтрокаТаблицыРасход.КоррНоменклатура = СтрокаТаблицаЗапасы.Номенклатура;
				СтрокаТаблицыРасход.КоррХарактеристика = СтрокаТаблицаЗапасы.Характеристика;
				СтрокаТаблицыРасход.КоррПартия = СтрокаТаблицаЗапасы.Партия;
				СтрокаТаблицыРасход.КоррЗаказПокупателя = ДокументСсылкаЗаказНаряд;
				
				СтрокаТаблицыРасход.ЗаказПродажи = Неопределено;
				СтрокаТаблицыРасход.ДокументПродажи = Неопределено;
				СтрокаТаблицыРасход.Подразделение = Неопределено;
				СтрокаТаблицыРасход.Ответственный = Неопределено;
				СтрокаТаблицыРасход.СтавкаНДС = Неопределено;
				
				СтрокаТаблицыРасход.ЗатратыНаВыпуск = Ложь;
				СтрокаТаблицыРасход.Сумма = 0;
				СтрокаТаблицыРасход.Количество = КоличествоТребуетсяРезерв;
				СтрокаТаблицыРасход.СодержаниеПроводки = НСтр("ru='Списание запасов из свободного остатка в резерв'");
				
				// Поставим их в резерв.
				СтрокаТаблицыПриход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаТаблицаЗапасы);
				
				СтрокаТаблицыПриход.ВидДвижения = ВидДвиженияНакопления.Приход;
				
				СтрокаТаблицыПриход.СчетУчета = СтрокаТаблицаЗапасы.СчетУчета;
				СтрокаТаблицыПриход.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.СтруктурнаяЕдиница;
				СтрокаТаблицыПриход.Номенклатура = СтрокаТаблицаЗапасы.Номенклатура;
				СтрокаТаблицыПриход.Характеристика = СтрокаТаблицаЗапасы.Характеристика;
				СтрокаТаблицыПриход.Партия = СтрокаТаблицаЗапасы.Партия;
				СтрокаТаблицыПриход.ЗаказПокупателя = ДокументСсылкаЗаказНаряд;
				
				СтрокаТаблицыПриход.КоррСчетУчета = СтрокаТаблицаЗапасы.СчетУчета;
				СтрокаТаблицыПриход.КоррСтруктурнаяЕдиница = СтрокаТаблицаЗапасы.СтруктурнаяЕдиница;
				СтрокаТаблицыПриход.КоррНоменклатура = СтрокаТаблицаЗапасы.Номенклатура;
				СтрокаТаблицыПриход.КоррХарактеристика = СтрокаТаблицаЗапасы.Характеристика;
				СтрокаТаблицыПриход.КоррПартия = СтрокаТаблицаЗапасы.Партия;
				СтрокаТаблицыПриход.КоррЗаказПокупателя = ПустойЗаказПокупателя;
				
				СтрокаТаблицыПриход.ЗаказПродажи = Неопределено;
				СтрокаТаблицыПриход.ДокументПродажи = Неопределено;
				СтрокаТаблицыПриход.Подразделение = Неопределено;
				СтрокаТаблицыПриход.Ответственный = Неопределено;
				СтрокаТаблицыПриход.СтавкаНДС = Неопределено;
				
				СтрокаТаблицыПриход.ЗатратыНаВыпуск = Ложь;
				СтрокаТаблицыПриход.Сумма = 0;
				СтрокаТаблицыПриход.Количество = КоличествоТребуетсяРезерв;
				СтрокаТаблицыПриход.СодержаниеПроводки = НСтр("ru='Поступление запасов в резерв из свободного остатка'");
				
				// Спишем запасы из резерва.
				СтрокаТаблицыРасход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
				
				СтрокаТаблицыРасход.ЗаказПокупателя = ДокументСсылкаЗаказНаряд;
				СтрокаТаблицыРасход.ЗатратыНаВыпуск = Ложь;
				СтрокаТаблицыРасход.Сумма = 0;
				СтрокаТаблицыРасход.Количество = КоличествоТребуетсяРезерв;
				СтрокаТаблицыРасход.СодержаниеПроводки = НСтр("ru='Продажа запасов из резерва'");
				
			КонецЕсли;
			
			Если КоличествоТребуетсяСвободныйОстаток <> 0 Тогда
				
				// Спишем запасы из свободного остатка.
				СтрокаТаблицыРасход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
				
				СтрокаТаблицыРасход.ЗаказПокупателя = ПустойЗаказПокупателя;
				СтрокаТаблицыРасход.ЗатратыНаВыпуск = Ложь;
				СтрокаТаблицыРасход.Сумма = 0;
				СтрокаТаблицыРасход.Количество = КоличествоТребуетсяСвободныйОстаток;
				СтрокаТаблицыРасход.СодержаниеПроводки = НСтр("ru='Продажа запасов из свободного остатка'");
				
				Если НЕ РезервированиеЗапасов Тогда
					СтрокаТаблицыРасход.КоррЗаказПокупателя = ПустойЗаказПокупателя;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			
			Если СтрокаТаблицаЗапасы.ЕстьВДокументеРеализации Тогда
				КоличествоТребуетсяРезерв = 0;
				КоличествоТребуетсяРезервОтгрузка = 0;
			Иначе
				КоличествоТребуетсяРезерв = СтрокаТаблицаЗапасы.Резерв;
				КоличествоТребуетсяРезервОтгрузка = СтрокаТаблицаЗапасы.РезервОтгрузка;
			КонецЕсли;
			КоличествоТребуетсяСвободныйОстаток = КоличествоТребуетсяВсего - КоличествоТребуетсяРезерв;
			
			СтруктураДляПоиска = Новый Структура;
			СтруктураДляПоиска.Вставить("Организация", СтрокаТаблицаЗапасы.Организация);
			СтруктураДляПоиска.Вставить("СтруктурнаяЕдиница", СтрокаТаблицаЗапасы.СтруктурнаяЕдиница);
			СтруктураДляПоиска.Вставить("СчетУчета", СтрокаТаблицаЗапасы.СчетУчета);
			СтруктураДляПоиска.Вставить("Номенклатура", СтрокаТаблицаЗапасы.Номенклатура);
			СтруктураДляПоиска.Вставить("Характеристика", СтрокаТаблицаЗапасы.Характеристика);
			СтруктураДляПоиска.Вставить("Партия", СтрокаТаблицаЗапасы.Партия);
			
			Если КоличествоТребуетсяВсего > 0 Тогда
				
				СтруктураДляПоиска.Вставить("ЗаказПокупателя", ПустойЗаказПокупателя);
				
				МассивСтрокОстатков = ТаблицаЗапасыОстатки.НайтиСтроки(СтруктураДляПоиска);
				
				КоличествоОстаток = 0;
				СуммаОстаток = 0;
				
				Если МассивСтрокОстатков.Количество() > 0 Тогда
					КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток;
					СуммаОстаток = МассивСтрокОстатков[0].СуммаОстаток;
				КонецЕсли;
				
				Если КоличествоТребуетсяРезерв > 0 Тогда // Нужно сделать резерв.
					
					Если КоличествоОстаток > 0 И КоличествоОстаток > КоличествоТребуетсяРезерв Тогда

						СуммаКСписанию = Окр(СуммаОстаток * КоличествоТребуетсяРезерв / КоличествоОстаток , 2, 1);

						МассивСтрокОстатков[0].КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток - КоличествоТребуетсяРезерв;
						МассивСтрокОстатков[0].СуммаОстаток = МассивСтрокОстатков[0].СуммаОстаток - СуммаКСписанию;

					ИначеЕсли КоличествоОстаток = КоличествоТребуетсяРезерв Тогда

						СуммаКСписанию = СуммаОстаток;

						МассивСтрокОстатков[0].КоличествоОстаток = 0;
						МассивСтрокОстатков[0].СуммаОстаток = 0;

					Иначе
						СуммаКСписанию = 0;
					КонецЕсли;
					
					// Спишем запасы из свободного остатка.
					СтрокаТаблицыРасход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
					
					СтрокаТаблицыРасход.КоррСчетУчета = СтрокаТаблицаЗапасы.СчетУчета;
					СтрокаТаблицыРасход.КоррСтруктурнаяЕдиница = СтрокаТаблицаЗапасы.СтруктурнаяЕдиница;
					СтрокаТаблицыРасход.КоррНоменклатура = СтрокаТаблицаЗапасы.Номенклатура;
					СтрокаТаблицыРасход.КоррХарактеристика = СтрокаТаблицаЗапасы.Характеристика;
					СтрокаТаблицыРасход.КоррПартия = СтрокаТаблицаЗапасы.Партия;
					СтрокаТаблицыРасход.КоррЗаказПокупателя = ДокументСсылкаЗаказНаряд;
					
					СтрокаТаблицыРасход.ЗаказПродажи = Неопределено;
					СтрокаТаблицыРасход.ДокументПродажи = Неопределено;
					СтрокаТаблицыРасход.Подразделение = Неопределено;
					СтрокаТаблицыРасход.Ответственный = Неопределено;
					СтрокаТаблицыРасход.СтавкаНДС = Неопределено;
					
					СтрокаТаблицыРасход.ЗатратыНаВыпуск = Ложь;
					СтрокаТаблицыРасход.Сумма = СуммаКСписанию;
					СтрокаТаблицыРасход.Количество = КоличествоТребуетсяРезерв;
					СтрокаТаблицыРасход.СодержаниеПроводки = НСтр("ru='Списание запасов из свободного остатка в резерв'");
					
					// Поставим их в резерв.
					СтрокаТаблицыПриход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаТаблицаЗапасы);
					
					СтрокаТаблицыПриход.ВидДвижения = ВидДвиженияНакопления.Приход;
					
					СтрокаТаблицыПриход.СчетУчета = СтрокаТаблицаЗапасы.СчетУчета;
					СтрокаТаблицыПриход.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.СтруктурнаяЕдиница;
					СтрокаТаблицыПриход.Номенклатура = СтрокаТаблицаЗапасы.Номенклатура;
					СтрокаТаблицыПриход.Характеристика = СтрокаТаблицаЗапасы.Характеристика;
					СтрокаТаблицыПриход.Партия = СтрокаТаблицаЗапасы.Партия;
					СтрокаТаблицыПриход.ЗаказПокупателя = ДокументСсылкаЗаказНаряд;
					
					СтрокаТаблицыПриход.КоррСчетУчета = СтрокаТаблицаЗапасы.СчетУчета;
					СтрокаТаблицыПриход.КоррСтруктурнаяЕдиница = СтрокаТаблицаЗапасы.СтруктурнаяЕдиница;
					СтрокаТаблицыПриход.КоррНоменклатура = СтрокаТаблицаЗапасы.Номенклатура;
					СтрокаТаблицыПриход.КоррХарактеристика = СтрокаТаблицаЗапасы.Характеристика;
					СтрокаТаблицыПриход.КоррПартия = СтрокаТаблицаЗапасы.Партия;
					СтрокаТаблицыПриход.КоррЗаказПокупателя = ПустойЗаказПокупателя;
					
					СтрокаТаблицыПриход.ЗаказПродажи = Неопределено;
					СтрокаТаблицыПриход.ДокументПродажи = Неопределено;
					СтрокаТаблицыПриход.Подразделение = Неопределено;
					СтрокаТаблицыПриход.Ответственный = Неопределено;
					СтрокаТаблицыПриход.СтавкаНДС = Неопределено;
					
					СтрокаТаблицыПриход.ЗатратыНаВыпуск = Ложь;
					СтрокаТаблицыПриход.Сумма = СуммаКСписанию;
					СтрокаТаблицыПриход.Количество = КоличествоТребуетсяРезерв;
					СтрокаТаблицыПриход.СодержаниеПроводки = НСтр("ru='Поступление запасов в резерв из свободного остатка'");
					
					Если СтрокаТаблицаЗапасы.Выполнен Тогда // Если заказ выполнен - надо и продать.
						
						// Спишем запасы из резерва.
						СтрокаТаблицыРасход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
						
						СтрокаТаблицыРасход.ЗаказПокупателя = ДокументСсылкаЗаказНаряд;
						СтрокаТаблицыРасход.ЗатратыНаВыпуск = Ложь;
						СтрокаТаблицыРасход.Сумма = СуммаКСписанию;
						СтрокаТаблицыРасход.Количество = КоличествоТребуетсяРезерв;
						СтрокаТаблицыРасход.СодержаниеПроводки = НСтр("ru='Продажа запасов из резерва'");
						
						Если Окр(СуммаКСписанию, 2, 1) <> 0 Тогда
							
							// Сформируем проводки.
							СтрокаТаблицаУправленческий = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаТаблицаУправленческий, СтрокаТаблицаЗапасы);
							СтрокаТаблицаУправленческий.Сумма = СуммаКСписанию;
							
							// Продвигаем доходы и расходы.
							СтрокаДоходыИРасходы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаДоходыИРасходы, СтрокаТаблицаЗапасы);
							
							СтрокаДоходыИРасходы.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.ПодразделениеПродажи;
							СтрокаДоходыИРасходы.ЗаказПокупателя = ДокументСсылкаЗаказНаряд;
							СтрокаДоходыИРасходы.СчетУчета = СтрокаТаблицаЗапасы.СчетУчетаСебестоимость;
							СтрокаДоходыИРасходы.СуммаДоходов = 0;
							СтрокаДоходыИРасходы.СуммаРасходов = СуммаКСписанию;
							СтрокаДоходыИРасходы.Сумма = СуммаКСписанию;
							
							СтрокаДоходыИРасходы.Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылкаКорректировкаРеализации, "Проект");
							
							СтрокаДоходыИРасходы.СодержаниеПроводки = НСтр("ru='Отражение расходов'");
							
							// Продвигаем себестоимость продаж.
							СтрокаПродажи = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПродажи.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаПродажи, СтрокаТаблицаЗапасы);
							СтрокаПродажи.ЗаказПокупателя = ДокументСсылкаЗаказНаряд;
							СтрокаПродажи.Количество = 0;
							СтрокаПродажи.Сумма = 0;
							СтрокаПродажи.СуммаНДС = 0;
							СтрокаПродажи.Себестоимость = СуммаКСписанию;				
							СтрокаПродажи.Склад = СтрокаТаблицаЗапасы.СтруктурнаяЕдиница;	
							
						КонецЕсли;
							
					КонецЕсли;
					
				КонецЕсли;
				
				// Если заказ на выполнении - надо списать резерв др. документов.
				КоличествоРезервОтгрузка = КоличествоТребуетсяРезервОтгрузка - КоличествоТребуетсяРезерв;
				Если СтрокаТаблицаЗапасы.Выполнен 
					И КоличествоРезервОтгрузка > 0 Тогда
					
					КоличествоТребуетсяСвободныйОстаток = КоличествоТребуетсяСвободныйОстаток - КоличествоРезервОтгрузка;
					
					СтруктураДляПоиска.Вставить("ЗаказПокупателя", ДокументСсылкаЗаказНаряд);
				
					МассивСтрокОстатковОтгрузка = ТаблицаЗапасыОстатки.НайтиСтроки(СтруктураДляПоиска);
					
					КоличествоОстатокОтгрузка = 0;
					СуммаОстатокОтгрузка = 0;
					
					Если МассивСтрокОстатковОтгрузка.Количество() > 0 Тогда
						КоличествоОстатокОтгрузка = МассивСтрокОстатковОтгрузка[0].КоличествоОстаток;
						СуммаОстатокОтгрузка = МассивСтрокОстатковОтгрузка[0].СуммаОстаток;
					КонецЕсли;
					
					Если КоличествоОстатокОтгрузка > 0 И КоличествоОстатокОтгрузка > КоличествоРезервОтгрузка Тогда

						СуммаКСписаниюОтгрузка = Окр(СуммаОстатокОтгрузка * КоличествоРезервОтгрузка / КоличествоОстатокОтгрузка , 2, 1);

						МассивСтрокОстатковОтгрузка[0].КоличествоОстаток = МассивСтрокОстатковОтгрузка[0].КоличествоОстаток - КоличествоРезервОтгрузка;
						МассивСтрокОстатковОтгрузка[0].СуммаОстаток = МассивСтрокОстатковОтгрузка[0].СуммаОстаток - СуммаКСписаниюОтгрузка;

					ИначеЕсли КоличествоОстатокОтгрузка = КоличествоРезервОтгрузка Тогда

						СуммаКСписаниюОтгрузка = СуммаОстатокОтгрузка;

						МассивСтрокОстатковОтгрузка[0].КоличествоОстаток = 0;
						МассивСтрокОстатковОтгрузка[0].СуммаОстаток = 0;

					Иначе
						СуммаКСписаниюОтгрузка = 0;	
					КонецЕсли;
					
					// Спишем запасы из резерва.
					СтрокаТаблицыРасход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
						
					СтрокаТаблицыРасход.ЗаказПокупателя = ДокументСсылкаЗаказНаряд;
					СтрокаТаблицыРасход.ЗатратыНаВыпуск = Ложь;
					СтрокаТаблицыРасход.Сумма = СуммаКСписаниюОтгрузка;
					
					СтрокаТаблицыРасход.Количество = КоличествоРезервОтгрузка;
					
					СтрокаТаблицыРасход.СодержаниеПроводки = НСтр("ru='Продажа запасов из резерва'");
					
					Если Окр(СуммаКСписаниюОтгрузка, 2, 1) <> 0 Тогда
						
						// Сформируем проводки.
						СтрокаТаблицаУправленческий = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаТаблицаУправленческий, СтрокаТаблицаЗапасы);
						СтрокаТаблицаУправленческий.Сумма = СуммаКСписаниюОтгрузка;
						
						// Продвигаем доходы и расходы.
						СтрокаДоходыИРасходы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаДоходыИРасходы, СтрокаТаблицаЗапасы);
						
						СтрокаДоходыИРасходы.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.ПодразделениеПродажи;
						СтрокаДоходыИРасходы.ЗаказПокупателя = ДокументСсылкаЗаказНаряд;
						СтрокаДоходыИРасходы.СчетУчета = СтрокаТаблицаЗапасы.СчетУчетаСебестоимость;
						СтрокаДоходыИРасходы.СуммаДоходов = 0;
						СтрокаДоходыИРасходы.СуммаРасходов = СуммаКСписаниюОтгрузка;
						СтрокаДоходыИРасходы.Сумма = СуммаКСписаниюОтгрузка;
						
						СтрокаДоходыИРасходы.Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылкаКорректировкаРеализации, "Проект");
						
						СтрокаДоходыИРасходы.СодержаниеПроводки = НСтр("ru='Отражение расходов'");
						
						// Продвигаем себестоимость продаж.
						СтрокаПродажи = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПродажи.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаПродажи, СтрокаТаблицаЗапасы);
						СтрокаПродажи.ЗаказПокупателя = ДокументСсылкаЗаказНаряд;
						СтрокаПродажи.Количество = 0;
						СтрокаПродажи.Сумма = 0;
						СтрокаПродажи.СуммаНДС = 0;
						СтрокаПродажи.Себестоимость = СуммаКСписаниюОтгрузка;				
						СтрокаПродажи.Склад = СтрокаТаблицаЗапасы.СтруктурнаяЕдиница;	
						
					КонецЕсли;
					
				КонецЕсли;
				
				Если КоличествоТребуетсяСвободныйОстаток > 0 Тогда
					
					Если СтрокаТаблицаЗапасы.Выполнен Тогда // Если заказ выполнении - надо и продать.
						
						Если КоличествоОстаток > 0 И КоличествоОстаток > КоличествоТребуетсяСвободныйОстаток Тогда

							СуммаКСписанию = Окр(СуммаОстаток * КоличествоТребуетсяСвободныйОстаток / КоличествоОстаток , 2, 1);

							МассивСтрокОстатков[0].КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток - КоличествоТребуетсяСвободныйОстаток;
							МассивСтрокОстатков[0].СуммаОстаток = МассивСтрокОстатков[0].СуммаОстаток - СуммаКСписанию;

						ИначеЕсли КоличествоОстаток = КоличествоТребуетсяСвободныйОстаток Тогда

							СуммаКСписанию = СуммаОстаток;

							МассивСтрокОстатков[0].КоличествоОстаток = 0;
							МассивСтрокОстатков[0].СуммаОстаток = 0;

						Иначе
							СуммаКСписанию = 0;	
						КонецЕсли;	
					
						// Спишем запасы из свободного остатка.
						СтрокаТаблицыРасход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
							
						СтрокаТаблицыРасход.ЗаказПокупателя = ПустойЗаказПокупателя;
						СтрокаТаблицыРасход.ЗатратыНаВыпуск = Ложь;
						СтрокаТаблицыРасход.Сумма = СуммаКСписанию;
						СтрокаТаблицыРасход.Количество = КоличествоТребуетсяСвободныйОстаток;
						СтрокаТаблицыРасход.СодержаниеПроводки = НСтр("ru='Продажа запасов из свободного остатка'");
						
						Если НЕ РезервированиеЗапасов Тогда
							СтрокаТаблицыРасход.КоррЗаказПокупателя = ПустойЗаказПокупателя;
						КонецЕсли;
						
						Если Окр(СуммаКСписанию, 2, 1) <> 0 Тогда
							
							// Сформируем проводки.
							СтрокаТаблицаУправленческий = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаТаблицаУправленческий, СтрокаТаблицаЗапасы);
							СтрокаТаблицаУправленческий.Сумма = СуммаКСписанию;
							
							// Продвигаем доходы и расходы.
							СтрокаДоходыИРасходы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаДоходыИРасходы, СтрокаТаблицаЗапасы);
							
							СтрокаДоходыИРасходы.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.ПодразделениеПродажи;
							СтрокаДоходыИРасходы.ЗаказПокупателя = ДокументСсылкаЗаказНаряд;
							СтрокаДоходыИРасходы.СчетУчета = СтрокаТаблицаЗапасы.СчетУчетаСебестоимость;
							СтрокаДоходыИРасходы.СуммаДоходов = 0;
							СтрокаДоходыИРасходы.СуммаРасходов = СуммаКСписанию;
							СтрокаДоходыИРасходы.Сумма = СуммаКСписанию;
							
							СтрокаДоходыИРасходы.Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылкаКорректировкаРеализации, "Проект");
							
							СтрокаДоходыИРасходы.СодержаниеПроводки = НСтр("ru='Отражение расходов'");
							
							// Продвигаем себестоимость продаж.
							СтрокаПродажи = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПродажи.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаПродажи, СтрокаТаблицаЗапасы);
							СтрокаПродажи.ЗаказПокупателя = ДокументСсылкаЗаказНаряд;
							СтрокаПродажи.Количество = 0;
							СтрокаПродажи.Сумма = 0;
							СтрокаПродажи.СуммаНДС = 0;
							СтрокаПродажи.Себестоимость = СуммаКСписанию;				
							СтрокаПродажи.Склад = СтрокаТаблицаЗапасы.СтруктурнаяЕдиница;	
							
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Удалить("ТаблицаЗапасыЗапасы");
	
КонецПроцедуры // СформироватьТаблицаЗапасы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыПродажа(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	// Установка исключительной блокировки контролируемых остатков запасов.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаЗапасы.Организация КАК Организация,
	|		ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|		ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|		ТаблицаЗапасы.Характеристика КАК Характеристика,
	|		ТаблицаЗапасы.Партия КАК Партия,
	|		ТаблицаЗапасы.Заказ КАК ЗаказПокупателя
	|	ИЗ
	|		ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|	ГДЕ
	|		ТаблицаЗапасы.Заказ <> НЕОПРЕДЕЛЕНО
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаЗапасы.Организация,
	|		ТаблицаЗапасы.СтруктурнаяЕдиница,
	|		ТаблицаЗапасы.СчетУчета,
	|		ТаблицаЗапасы.Номенклатура,
	|		ТаблицаЗапасы.Характеристика,
	|		ТаблицаЗапасы.Партия,
	|		ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	ИЗ
	|		ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы) КАК ТаблицаЗапасы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.ЗаказПокупателя";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Запасы");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	// Получение остатков запасов по стоимости.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗапасыОстатки.Организация КАК Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗапасыОстатки.СчетУчета КАК СчетУчета,
	|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыОстатки.Характеристика КАК Характеристика,
	|	ЗапасыОстатки.Партия КАК Партия,
	|	ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	|	СУММА(ЗапасыОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	ИСТИНА КАК Продажа,
	|	&ДатаКорректировки КАК ДатаОстатка
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыОстатки.Организация КАК Организация,
	|		ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗапасыОстатки.СчетУчета КАК СчетУчета,
	|		ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыОстатки.Характеристика КАК Характеристика,
	|		ЗапасыОстатки.Партия КАК Партия,
	|		ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|		ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|		ЗапасыОстатки.СуммаОстаток КАК СуммаОстаток
	|	ИЗ
	|		РегистрНакопления.Запасы.Остатки(
	|				&МоментКонтроля,
	|				(Организация, СтруктурнаяЕдиница, СчетУчета, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
	|					(ВЫБРАТЬ
	|						ТаблицаЗапасы.Организация КАК Организация,
	|						ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|						ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|						ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|						ТаблицаЗапасы.Характеристика КАК Характеристика,
	|						ТаблицаЗапасы.Партия КАК Партия,
	|						ТаблицаЗапасы.Заказ КАК ЗаказПокупателя
	|					ИЗ
	|						ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|					ГДЕ
	|						ТаблицаЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|				
	|					ОБЪЕДИНИТЬ ВСЕ
	|				
	|					ВЫБРАТЬ
	|						ТаблицаЗапасы.Организация,
	|						ТаблицаЗапасы.СтруктурнаяЕдиница,
	|						ТаблицаЗапасы.СчетУчета,
	|						ТаблицаЗапасы.Номенклатура,
	|						ТаблицаЗапасы.Характеристика,
	|						ТаблицаЗапасы.Партия,
	|						ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|					ИЗ
	|						ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы)) КАК ЗапасыОстатки) КАК ЗапасыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыОстатки.Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница,
	|	ЗапасыОстатки.СчетУчета,
	|	ЗапасыОстатки.Номенклатура,
	|	ЗапасыОстатки.Характеристика,
	|	ЗапасыОстатки.Партия,
	|	ЗапасыОстатки.ЗаказПокупателя
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Запасы.Организация,
	|	Запасы.СтруктурнаяЕдиница,
	|	Запасы.СчетУчета,
	|	Запасы.Номенклатура,
	|	Запасы.Характеристика,
	|	Запасы.Партия,
	|	Запасы.ЗаказПокупателя,
	|	СУММА(ВЫБОР
	|			КОГДА Запасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА Запасы.Количество
	|			ИНАЧЕ -Запасы.Количество
	|		КОНЕЦ),
	|	СУММА(ВЫБОР
	|			КОГДА Запасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА Запасы.Сумма
	|			ИНАЧЕ -Запасы.Сумма
	|		КОНЕЦ),
	|	ЛОЖЬ,
	|	МИНИМУМ(Запасы.Период)
	|ИЗ
	|	РегистрНакопления.Запасы КАК Запасы
	|ГДЕ
	|	Запасы.ДокументПродажи В
	|			(ВЫБРАТЬ
	|				ТаблицаШапка.ДокументОснование
	|			ИЗ
	|				ВременнаяТаблицаШапка КАК ТаблицаШапка
	|			ГДЕ
	|				(ТаблицаШапка.ДокументОснование ССЫЛКА Документ.РасходнаяНакладная
	|					ИЛИ ТаблицаШапка.ДокументОснование ССЫЛКА Документ.АктВыполненныхРабот
	|					ИЛИ ТаблицаШапка.ДокументОснование ССЫЛКА Документ.КорректировкаРеализации))
	|	И Запасы.Регистратор <> &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Запасы.Организация,
	|	Запасы.СтруктурнаяЕдиница,
	|	Запасы.СчетУчета,
	|	Запасы.Номенклатура,
	|	Запасы.Характеристика,
	|	Запасы.Партия,
	|	Запасы.ЗаказПокупателя
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Запасы.Организация,
	|	Запасы.СтруктурнаяЕдиница,
	|	Запасы.СчетУчета,
	|	Запасы.Номенклатура,
	|	Запасы.Характеристика,
	|	Запасы.Партия,
	|	Запасы.ЗаказПокупателя,
	|	СУММА(ВЫБОР
	|			КОГДА Запасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА Запасы.Количество
	|			ИНАЧЕ -Запасы.Количество
	|		КОНЕЦ),
	|	СУММА(ВЫБОР
	|			КОГДА Запасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА Запасы.Сумма
	|			ИНАЧЕ -Запасы.Сумма
	|		КОНЕЦ),
	|	ЛОЖЬ,
	|	МИНИМУМ(Запасы.Период)
	|ИЗ
	|	РегистрНакопления.Запасы КАК Запасы
	|ГДЕ
	|	Запасы.ЗаказПокупателя В
	|			(ВЫБРАТЬ
	|				ТаблицаШапка.ДокументОснование
	|			ИЗ
	|				ВременнаяТаблицаШапка КАК ТаблицаШапка
	|			ГДЕ
	|				ТаблицаШапка.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя)
	|	И Запасы.Регистратор <> &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Запасы.Организация,
	|	Запасы.СтруктурнаяЕдиница,
	|	Запасы.СчетУчета,
	|	Запасы.Номенклатура,
	|	Запасы.Характеристика,
	|	Запасы.Партия,
	|	Запасы.ЗаказПокупателя";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКорректировкаРеализации);
	ДатаКорректировки = СтруктураДополнительныеСвойства.ДляПроведения.Дата;
	Запрос.УстановитьПараметр("ДатаКорректировки", ДатаКорректировки);
	Запрос.УстановитьПараметр("МоментКонтроля", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Исключая));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаЗапасыОстатки = РезультатЗапроса.Выгрузить();
	ТаблицаЗапасыОстатки.Индексы.Добавить("Организация,СтруктурнаяЕдиница,СчетУчета,Номенклатура,Характеристика,Партия,ЗаказПокупателя,Продажа");
	
	ВременнаяТаблицаЗапасы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.СкопироватьКолонки();
	
	Для н = 0 По СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Количество() - 1 Цикл
		
		СтрокаТаблицаЗапасы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы[н];
		
		СтруктураДляПоиска = Новый Структура;
		СтруктураДляПоиска.Вставить("Организация", СтрокаТаблицаЗапасы.Организация);
		СтруктураДляПоиска.Вставить("СтруктурнаяЕдиница", СтрокаТаблицаЗапасы.СтруктурнаяЕдиница);
		СтруктураДляПоиска.Вставить("СчетУчета", СтрокаТаблицаЗапасы.СчетУчета);
		СтруктураДляПоиска.Вставить("Номенклатура", СтрокаТаблицаЗапасы.Номенклатура);
		СтруктураДляПоиска.Вставить("Характеристика", СтрокаТаблицаЗапасы.Характеристика);
		СтруктураДляПоиска.Вставить("Партия", СтрокаТаблицаЗапасы.Партия);
		Если СтрокаТаблицаЗапасы.Количество >= 0 Тогда
			СтруктураДляПоиска.Вставить("Продажа", Истина);
			СебестоимостьПоОстатку = Истина;
		Иначе
			СтруктураДляПоиска.Вставить("Продажа", Ложь);
			СебестоимостьПоОстатку = Ложь;
		КонецЕсли;
		
		КоличествоТребуетсяРезерв = ?(ЗначениеЗаполнено(СтрокаТаблицаЗапасы.Резерв), СтрокаТаблицаЗапасы.Резерв, 0);
		КоличествоТребуетсяСвободныйОстаток = ?(ЗначениеЗаполнено(СтрокаТаблицаЗапасы.Количество), СтрокаТаблицаЗапасы.Количество, 0);
		
		КорректировкаВМинус = Ложь;
		Если КоличествоТребуетсяСвободныйОстаток < 0 Тогда
			КоличествоТребуетсяСвободныйОстаток = -КоличествоТребуетсяСвободныйОстаток;
			КорректировкаВМинус = Истина;
		КонецЕсли;
		
		Если КоличествоТребуетсяРезерв > 0 Тогда
			
			КоличествоТребуетсяСвободныйОстаток = КоличествоТребуетсяСвободныйОстаток - КоличествоТребуетсяРезерв;
			
			СтруктураДляПоиска.Вставить("ЗаказПокупателя", СтрокаТаблицаЗапасы.ЗаказПокупателя);
			
			МассивСтрокОстатков = ТаблицаЗапасыОстатки.НайтиСтроки(СтруктураДляПоиска);
			Если НЕ СебестоимостьПоОстатку И МассивСтрокОстатков.Количество() = 0 Тогда
				СтруктураДляПоиска.Вставить("Продажа", Истина);
				МассивСтрокОстатков = ТаблицаЗапасыОстатки.НайтиСтроки(СтруктураДляПоиска);
				СебестоимостьПоОстатку = Истина;
			КонецЕсли;
			
			КоличествоОстаток = 0;
			СуммаОстаток = 0;
			
			Если МассивСтрокОстатков.Количество() > 0 Тогда
				КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток;
				СуммаОстаток = МассивСтрокОстатков[0].СуммаОстаток;
				МесяцПродажиИВозвратаСовпадает = (НачалоМесяца(ДатаКорректировки) = НачалоМесяца(МассивСтрокОстатков[0].ДатаОстатка));
			Иначе
				МесяцПродажиИВозвратаСовпадает = Истина;
			КонецЕсли;
			
			Если КоличествоОстаток > 0 И КоличествоОстаток > КоличествоТребуетсяРезерв Тогда
				
				СуммаКСписанию = Окр(СуммаОстаток * КоличествоТребуетсяРезерв / КоличествоОстаток , 2, 1);
				
				МассивСтрокОстатков[0].КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток - КоличествоТребуетсяРезерв;
				МассивСтрокОстатков[0].СуммаОстаток = МассивСтрокОстатков[0].СуммаОстаток - СуммаКСписанию;
				
			ИначеЕсли КоличествоОстаток = КоличествоТребуетсяРезерв Тогда
				
				СуммаКСписанию = СуммаОстаток;
				
				МассивСтрокОстатков[0].КоличествоОстаток = 0;
				МассивСтрокОстатков[0].СуммаОстаток = 0;
				
			Иначе
				СуммаКСписанию = 0;
			КонецЕсли;
			
			Если КорректировкаВМинус Тогда
				СуммаКСписанию = -СуммаКСписанию;
			КонецЕсли;
			
			ОстатокКСписанию = КоличествоТребуетсяРезерв;
			Если КорректировкаВМинус Тогда
				ОстатокКСписанию = -ОстатокКСписанию;
			КонецЕсли;
			
			// Расход. Запасы.
			СтрокаТаблицыРасход = ВременнаяТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
			
			СтрокаТаблицыРасход.Сумма = СуммаКСписанию;
			СтрокаТаблицыРасход.Количество = ОстатокКСписанию;
			Если КорректировкаВМинус Тогда
				СтрокаТаблицыРасход.Возврат = Истина;
			КонецЕсли;
			Если НЕ СебестоимостьПоОстатку И НЕ МесяцПродажиИВозвратаСовпадает Тогда
				СтрокаТаблицыРасход.ФиксированнаяСтоимость = Истина;
			КонецЕсли; 
			
			// Сформируем проводки.
			Если Окр(СуммаКСписанию, 2, 1) <> 0 Тогда
				СтрокаТаблицаУправленческий = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицаУправленческий, СтрокаТаблицаЗапасы);
				СтрокаТаблицаУправленческий.Сумма = СуммаКСписанию;
			КонецЕсли;
			
			Если СтрокаТаблицаЗапасы.ТоварыНаКомиссии Тогда
				
			ИначеЕсли Окр(СуммаКСписанию, 2, 1) <> 0 Тогда
				
				// Продвигаем доходы и расходы.
				СтрокаДоходыИРасходы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДоходыИРасходы, СтрокаТаблицаЗапасы);
				
				СтрокаДоходыИРасходы.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.ПодразделениеПродажи;
				СтрокаДоходыИРасходы.СчетУчета = СтрокаТаблицаЗапасы.СчетУчетаСебестоимость;
				СтрокаДоходыИРасходы.СуммаДоходов = 0;
				СтрокаДоходыИРасходы.СуммаРасходов = СуммаКСписанию;
				СтрокаДоходыИРасходы.Сумма = СуммаКСписанию;
				
				СтрокаДоходыИРасходы.Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылкаКорректировкаРеализации, "Проект");
				
				СтрокаДоходыИРасходы.СодержаниеПроводки = НСтр("ru='Отражение расходов'");
				
			КонецЕсли;
			
			Если Окр(СуммаКСписанию, 2, 1) <> 0 Тогда
				
				// Продвигаем себестоимость продаж.
				СтрокаПродажи = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПродажи.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПродажи, СтрокаТаблицаЗапасы);
				СтрокаПродажи.Количество = 0;
				СтрокаПродажи.Сумма = 0;
				СтрокаПродажи.СуммаНДС = 0;
				СтрокаПродажи.Себестоимость = СуммаКСписанию;
				СтрокаПродажи.Склад = СтрокаТаблицаЗапасы.СтруктурнаяЕдиница;	
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если СтрокаТаблицаЗапасы.Количество >= 0 Тогда
			СтруктураДляПоиска.Вставить("Продажа", Истина);
			СебестоимостьПоОстатку = Истина;
		Иначе
			СтруктураДляПоиска.Вставить("Продажа", Ложь);
			СебестоимостьПоОстатку = Ложь;
		КонецЕсли;
		
		Если КоличествоТребуетсяСвободныйОстаток > 0 Тогда
			
			СтруктураДляПоиска.Вставить("ЗаказПокупателя", Документы.ЗаказПокупателя.ПустаяСсылка());
			
			МассивСтрокОстатков = ТаблицаЗапасыОстатки.НайтиСтроки(СтруктураДляПоиска);
			Если НЕ СебестоимостьПоОстатку И МассивСтрокОстатков.Количество() = 0 Тогда
				СтруктураДляПоиска.Вставить("Продажа", Истина);
				МассивСтрокОстатков = ТаблицаЗапасыОстатки.НайтиСтроки(СтруктураДляПоиска);
				СебестоимостьПоОстатку = Истина;
			КонецЕсли;
			
			КоличествоОстаток = 0;
			СуммаОстаток = 0;
			
			Если МассивСтрокОстатков.Количество() > 0 Тогда
				КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток;
				СуммаОстаток = МассивСтрокОстатков[0].СуммаОстаток;
				МесяцПродажиИВозвратаСовпадает = (НачалоМесяца(ДатаКорректировки) = НачалоМесяца(МассивСтрокОстатков[0].ДатаОстатка));
			Иначе
				МесяцПродажиИВозвратаСовпадает = Истина;
			КонецЕсли;
			
			Если КоличествоОстаток > 0 И КоличествоОстаток > КоличествоТребуетсяСвободныйОстаток Тогда
				
				СуммаКСписанию = Окр(СуммаОстаток * КоличествоТребуетсяСвободныйОстаток / КоличествоОстаток , 2, 1);
				
				МассивСтрокОстатков[0].КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток - КоличествоТребуетсяСвободныйОстаток;
				МассивСтрокОстатков[0].СуммаОстаток = МассивСтрокОстатков[0].СуммаОстаток - СуммаКСписанию;
				
			ИначеЕсли КоличествоОстаток = КоличествоТребуетсяСвободныйОстаток Тогда
				
				СуммаКСписанию = СуммаОстаток;
				
				МассивСтрокОстатков[0].КоличествоОстаток = 0;
				МассивСтрокОстатков[0].СуммаОстаток = 0;
				
			Иначе
				СуммаКСписанию = 0;
			КонецЕсли;
			
			Если КорректировкаВМинус Тогда
				СуммаКСписанию = -СуммаКСписанию;
			КонецЕсли;
			
			ОстатокКСписанию = КоличествоТребуетсяСвободныйОстаток;
			Если КорректировкаВМинус Тогда
				ОстатокКСписанию = -ОстатокКСписанию;
			КонецЕсли;
			
			// Расход. Запасы.
			СтрокаТаблицыРасход = ВременнаяТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
			
			СтрокаТаблицыРасход.Сумма = СуммаКСписанию;
			СтрокаТаблицыРасход.Количество = ОстатокКСписанию;
			СтрокаТаблицыРасход.ЗаказПокупателя = Документы.ЗаказПокупателя.ПустаяСсылка();
			Если КорректировкаВМинус Тогда
				СтрокаТаблицыРасход.Возврат = Истина;
			КонецЕсли;
			Если НЕ СебестоимостьПоОстатку И НЕ МесяцПродажиИВозвратаСовпадает Тогда
				СтрокаТаблицыРасход.ФиксированнаяСтоимость = Истина;
			КонецЕсли; 
			
			// Сформируем проводки.
			Если Окр(СуммаКСписанию, 2, 1) <> 0 Тогда
				СтрокаТаблицаУправленческий = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицаУправленческий, СтрокаТаблицаЗапасы);
				СтрокаТаблицаУправленческий.Сумма = СуммаКСписанию;
			КонецЕсли;
			
			Если СтрокаТаблицаЗапасы.ТоварыНаКомиссии Тогда
				
			ИначеЕсли Окр(СуммаКСписанию, 2, 1) <> 0 Тогда
				
				// Продвигаем доходы и расходы.
				СтрокаДоходыИРасходы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДоходыИРасходы, СтрокаТаблицаЗапасы);
				
				СтрокаДоходыИРасходы.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.ПодразделениеПродажи;
				СтрокаДоходыИРасходы.СчетУчета = СтрокаТаблицаЗапасы.СчетУчетаСебестоимость;
				СтрокаДоходыИРасходы.СуммаДоходов = 0;
				СтрокаДоходыИРасходы.СуммаРасходов = СуммаКСписанию;
				СтрокаДоходыИРасходы.Сумма = СуммаКСписанию;
				
				СтрокаДоходыИРасходы.Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылкаКорректировкаРеализации, "Проект");
				
				СтрокаДоходыИРасходы.СодержаниеПроводки = НСтр("ru='Отражение расходов'");
				
			КонецЕсли;
			
			Если Окр(СуммаКСписанию, 2, 1) <> 0 Тогда
				
				// Продвигаем себестоимость продаж.
				СтрокаПродажи = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПродажи.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПродажи, СтрокаТаблицаЗапасы);
				СтрокаПродажи.Количество = 0;
				СтрокаПродажи.Сумма = 0;
				СтрокаПродажи.СуммаНДС = 0;
				СтрокаПродажи.Себестоимость = СуммаКСписанию;
				СтрокаПродажи.Склад = СтрокаТаблицаЗапасы.СтруктурнаяЕдиница;	
				
			КонецЕсли;	
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы = ВременнаяТаблицаЗапасы;
	
КонецПроцедуры // СформироватьТаблицаЗапасыПродажа()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПродажи(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПродажи.Период КАК Период,
	|	ТаблицаПродажи.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаПродажи.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ТаблицаПродажи.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК Склад,
	|	ТаблицаПродажи.Номенклатура КАК Номенклатура,
	|	ТаблицаПродажи.Характеристика КАК Характеристика,
	|	ТаблицаПродажи.Партия КАК Партия,
	|	ВЫБОР
	|		КОГДА ТаблицаПродажи.ЭтоЗаказНаряд
	|			ТОГДА ТаблицаПродажи.Документ
	|		ИНАЧЕ ТаблицаПродажи.Заказ
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ТаблицаПродажи.Документ КАК Документ,
	|	ТаблицаПродажи.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаПродажи.ПодразделениеПродажи КАК Подразделение,
	|	ТаблицаПродажи.Проект КАК Проект,
	|	ТаблицаПродажи.Ответственный КАК Ответственный,
	|	СУММА(ТаблицаПродажи.Количество) КАК Количество,
	|	СУММА(ТаблицаПродажи.СуммаНДСЗакупкиПродажи) КАК СуммаНДС,
	|	СУММА(ТаблицаПродажи.Сумма) КАК Сумма,
	|	0 КАК Себестоимость,
	|	ТаблицаПродажи.НоменклатураНабора КАК НоменклатураНабора,
	|	ТаблицаПродажи.ХарактеристикаНабора КАК ХарактеристикаНабора
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаПродажи
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПродажи.Период,
	|	ТаблицаПродажи.Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаПродажи.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ТаблицаПродажи.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаПродажи.Номенклатура,
	|	ТаблицаПродажи.Характеристика,
	|	ТаблицаПродажи.Партия,
	|	ТаблицаПродажи.Документ,
	|	ТаблицаПродажи.СтавкаНДС,
	|	ТаблицаПродажи.ПодразделениеПродажи,
	|	ТаблицаПродажи.Проект,
	|	ТаблицаПродажи.Ответственный,
	|	ВЫБОР
	|		КОГДА ТаблицаПродажи.ЭтоЗаказНаряд
	|			ТОГДА ТаблицаПродажи.Документ
	|		ИНАЧЕ ТаблицаПродажи.Заказ
	|	КОНЕЦ,
	|	ТаблицаПродажи.НоменклатураНабора,
	|	ТаблицаПродажи.ХарактеристикаНабора";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПродажи", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаПродажи()

// Формирует таблицу значений, содержащую данные для проведения по регистру ПродажиПоДисконтнымКартам.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПродажиПоДисконтнойКарте(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Если ДокументСсылкаКорректировкаРеализации.ДисконтнаяКарта.Пустая() Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПродажиПоДисконтнымКартам", Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = ДисконтныеКартыУНФСервер.ТекстЗапросаПродажиПоДисконтнойКарте("КорректировкаРеализации");
	
	Запрос.УстановитьПараметр("ПроцентСкидки", ДисконтныеКартыУНФСервер.ВычислитьПроцентСкидкиПоДисконтнойКарте(СтруктураДополнительныеСвойства.ДляПроведения.Дата,
																													   ДокументСсылкаКорректировкаРеализации.ДисконтнаяКарта));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПродажиПоДисконтнымКартам", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаПродажиПоДисконтнойКарте()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаВыпускПродукции(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаВыпускПродукции.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаВыпускПродукции.Период КАК Период,
	|	ТаблицаВыпускПродукции.Организация КАК Организация,
	|	ТаблицаВыпускПродукции.ПодразделениеПродажи КАК СтруктурнаяЕдиница,
	|	ТаблицаВыпускПродукции.Номенклатура КАК Номенклатура,
	|	ТаблицаВыпускПродукции.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаВыпускПродукции.ЭтоЗаказНаряд
	|			ТОГДА ТаблицаВыпускПродукции.Документ
	|		ИНАЧЕ ТаблицаВыпускПродукции.Заказ
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	СУММА(ТаблицаВыпускПродукции.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаВыпускПродукции
	|ГДЕ
	|	ТаблицаВыпускПродукции.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаВыпускПродукции.Период,
	|	ТаблицаВыпускПродукции.Организация,
	|	ТаблицаВыпускПродукции.ПодразделениеПродажи,
	|	ТаблицаВыпускПродукции.Номенклатура,
	|	ТаблицаВыпускПродукции.Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаВыпускПродукции.ЭтоЗаказНаряд
	|			ТОГДА ТаблицаВыпускПродукции.Документ
	|		ИНАЧЕ ТаблицаВыпускПродукции.Заказ
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаВыпускПродукции.Количество) <> 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВыпускПродукции", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаВыпускПродукции()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаГрафикДвиженияЗапасов(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗапасы.НомерСтроки КАК НомерСтроки,
	|	НАЧАЛОПЕРИОДА(ТаблицаЗапасы.Период, ДЕНЬ) КАК Период,
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДвиженийЗапасов.Отгрузка) КАК ТипДвижения,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.УчетПотребностиПоСкладам
	|				И ТаблицаЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ТаблицаЗапасы.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК Склад,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.ЭтоЗаказНаряд
	|			ТОГДА ТаблицаЗапасы.Документ
	|		ИНАЧЕ ТаблицаЗапасы.Заказ
	|	КОНЕЦ КАК Заказ,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	СУММА(ТаблицаЗапасы.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.УчетПотребностиПоСкладам
	|				И ТаблицаЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ТаблицаЗапасы.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаЗапасы.НомерСтроки,
	|	НАЧАЛОПЕРИОДА(ТаблицаЗапасы.Период, ДЕНЬ),
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.ЭтоЗаказНаряд
	|			ТОГДА ТаблицаЗапасы.Документ
	|		ИНАЧЕ ТаблицаЗапасы.Заказ
	|	КОНЕЦ,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаЗапасы.Количество) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаГрафикДвиженияЗапасов", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаВыпускПродукции()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыНаСкладах(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыНаСкладах.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗапасыНаСкладах.Период КАК Период,
	|	ТаблицаЗапасыНаСкладах.Организация КАК Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия КАК Партия,
	|	ТаблицаЗапасыНаСкладах.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасыНаСкладах.Ячейка КАК Ячейка,
	|	СУММА(ТаблицаЗапасыНаСкладах.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыНаСкладах
	|ГДЕ
	|	ТаблицаЗапасыНаСкладах.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|	И (НЕ ТаблицаЗапасыНаСкладах.ОрдерныйСклад)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыНаСкладах.Период,
	|	ТаблицаЗапасыНаСкладах.Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия,
	|	ТаблицаЗапасыНаСкладах.СтруктурнаяЕдиница,
	|	ТаблицаЗапасыНаСкладах.Ячейка";
		
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыНаСкладах", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗапасыНаСкладах()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыКРасходуСоСкладов(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыКРасходуСоСкладов.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаЗапасыКРасходуСоСкладов.Период КАК Период,
	|	ТаблицаЗапасыКРасходуСоСкладов.Организация КАК Организация,
	|	ТаблицаЗапасыКРасходуСоСкладов.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыКРасходуСоСкладов.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыКРасходуСоСкладов.Партия КАК Партия,
	|	ТаблицаЗапасыКРасходуСоСкладов.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	СУММА(ТаблицаЗапасыКРасходуСоСкладов.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыКРасходуСоСкладов
	|ГДЕ
	|	ТаблицаЗапасыКРасходуСоСкладов.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|	И ТаблицаЗапасыКРасходуСоСкладов.ОрдерныйСклад
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыКРасходуСоСкладов.Период,
	|	ТаблицаЗапасыКРасходуСоСкладов.Организация,
	|	ТаблицаЗапасыКРасходуСоСкладов.Номенклатура,
	|	ТаблицаЗапасыКРасходуСоСкладов.Характеристика,
	|	ТаблицаЗапасыКРасходуСоСкладов.Партия,
	|	ТаблицаЗапасыКРасходуСоСкладов.СтруктурнаяЕдиница";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыКРасходуСоСкладов", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗапасыКРасходуСоСкладов()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыПринятые(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыПринятые.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаЗапасыПринятые.Период КАК Период,
	|	ТаблицаЗапасыПринятые.Организация КАК Организация,
	|	ТаблицаЗапасыПринятые.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыПринятые.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыПринятые.Партия КАК Партия,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	ТаблицаЗапасыПринятые.Заказ КАК Заказ,
	|	ТаблицаЗапасыПринятые.СчетУчетаРасчетовСПоставщиком КАК СчетУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ОтчетКомитенту) КАК ТипПриемаПередачи,
	|	СУММА(ТаблицаЗапасыПринятые.Количество) КАК Количество,
	|	0 КАК СуммаРасчетов,
	|	СУММА(ТаблицаЗапасыПринятые.Сумма) КАК Сумма,
	|	СУММА(ТаблицаЗапасыПринятые.Сумма) КАК СуммаПродажи,
	|	&ВалютаУпрУчета КАК Валюта,
	|	СУММА(ТаблицаЗапасыПринятые.Сумма) КАК СуммаВал,
	|	ВЫРАЗИТЬ(&ПриемЗапасовТоварыНаКомиссии КАК СТРОКА(100)) КАК СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыПринятые
	|ГДЕ
	|	ТаблицаЗапасыПринятые.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|	И ТаблицаЗапасыПринятые.ТоварыНаКомиссии
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыПринятые.Период,
	|	ТаблицаЗапасыПринятые.Организация,
	|	ТаблицаЗапасыПринятые.Номенклатура,
	|	ТаблицаЗапасыПринятые.Характеристика,
	|	ТаблицаЗапасыПринятые.Партия,
	|	ТаблицаЗапасыПринятые.Контрагент,
	|	ТаблицаЗапасыПринятые.Договор,
	|	ТаблицаЗапасыПринятые.Заказ,
	|	ТаблицаЗапасыПринятые.СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаЗапасыПринятые.СчетУчета";
	
	Запрос.УстановитьПараметр("ПриемЗапасовТоварыНаКомиссии", НСтр("ru = 'Прием запасов'"));
	Запрос.УстановитьПараметр("ВалютаУпрУчета", Константы.ВалютаУчета.Получить());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыИАгентскиеУслугиПринятые", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗапасыПринятые()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗаказыПокупателей(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	ДокументРеализации = СтруктураДополнительныеСвойства.ДляПроведения.ДокументРеализации;
	Если ТипЗнч(ДокументРеализации) <> Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МИНИМУМ(ТаблицаЗаказыПокупателей.НомерСтроки) КАК НомерСтроки,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ТаблицаЗаказыПокупателей.Период КАК Период,
		|	ТаблицаЗаказыПокупателей.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаказыПокупателей.УчетПотребностиПоСкладам
		|				И ТаблицаЗаказыПокупателей.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
		|			ТОГДА ТаблицаЗаказыПокупателей.СтруктурнаяЕдиница
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|	КОНЕЦ КАК Склад,
		|	ТаблицаЗаказыПокупателей.Номенклатура КАК Номенклатура,
		|	ТаблицаЗаказыПокупателей.Характеристика КАК Характеристика,
		|	ТаблицаЗаказыПокупателей.Заказ КАК ЗаказПокупателя,
		|	СУММА(ТаблицаЗаказыПокупателей.Количество) КАК Количество
		|ИЗ
		|	ВременнаяТаблицаЗапасы КАК ТаблицаЗаказыПокупателей
		|ГДЕ
		|	ТаблицаЗаказыПокупателей.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаЗаказыПокупателей.Период,
		|	ТаблицаЗаказыПокупателей.Организация,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаказыПокупателей.УчетПотребностиПоСкладам
		|				И ТаблицаЗаказыПокупателей.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
		|			ТОГДА ТаблицаЗаказыПокупателей.СтруктурнаяЕдиница
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|	КОНЕЦ,
		|	ТаблицаЗаказыПокупателей.Номенклатура,
		|	ТаблицаЗаказыПокупателей.Характеристика,
		|	ТаблицаЗаказыПокупателей.Заказ
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТаблицаЗаказыПокупателей.Количество) <> 0";
		
		РезультатЗапроса = Запрос.Выполнить();
		ТаблицаЗаказыПокупателей = РезультатЗапроса.Выгрузить();
		РегистрыНакопления.ЗаказыПокупателей.ДобавитьДвиженияСУчетомСкладов(ДокументСсылкаКорректировкаРеализации, ТаблицаЗаказыПокупателей);
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗаказыПокупателей", ТаблицаЗаказыПокупателей);
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МИНИМУМ(ТаблицаЗаказыПокупателей.НомерСтроки) КАК НомерСтроки,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ТаблицаЗаказыПокупателей.Период КАК Период,
		|	ТаблицаЗаказыПокупателей.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаказыПокупателей.УчетПотребностиПоСкладам
		|				И ТаблицаЗаказыПокупателей.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
		|			ТОГДА ТаблицаЗаказыПокупателей.СтруктурнаяЕдиница
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|	КОНЕЦ КАК Склад,
		|	ТаблицаЗаказыПокупателей.Номенклатура КАК Номенклатура,
		|	ТаблицаЗаказыПокупателей.Характеристика КАК Характеристика,
		|	ТаблицаЗаказыПокупателей.Документ КАК ЗаказПокупателя,
		|	СУММА(ТаблицаЗаказыПокупателей.Количество) КАК Количество
		|ИЗ
		|	ВременнаяТаблицаЗапасы КАК ТаблицаЗаказыПокупателей
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаЗаказыПокупателей.Период,
		|	ТаблицаЗаказыПокупателей.Организация,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаказыПокупателей.УчетПотребностиПоСкладам
		|				И ТаблицаЗаказыПокупателей.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
		|			ТОГДА ТаблицаЗаказыПокупателей.СтруктурнаяЕдиница
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|	КОНЕЦ,
		|	ТаблицаЗаказыПокупателей.Номенклатура,
		|	ТаблицаЗаказыПокупателей.Характеристика,
		|	ТаблицаЗаказыПокупателей.Документ
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТаблицаЗаказыПокупателей.Количество) <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МИНИМУМ(ТаблицаЗаказыПокупателей.НомерСтроки),
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
		|	ТаблицаЗаказыПокупателей.Период,
		|	ТаблицаЗаказыПокупателей.Организация,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаказыПокупателей.УчетПотребностиПоСкладам
		|				И ТаблицаЗаказыПокупателей.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
		|			ТОГДА ТаблицаЗаказыПокупателей.СтруктурнаяЕдиница
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|	КОНЕЦ,
		|	ТаблицаЗаказыПокупателей.Номенклатура,
		|	ТаблицаЗаказыПокупателей.Характеристика,
		|	ТаблицаЗаказыПокупателей.Документ,
		|	СУММА(ТаблицаЗаказыПокупателей.Количество)
		|ИЗ
		|	ВременнаяТаблицаЗапасы КАК ТаблицаЗаказыПокупателей
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаЗаказыПокупателей.Период,
		|	ТаблицаЗаказыПокупателей.Организация,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаказыПокупателей.УчетПотребностиПоСкладам
		|				И ТаблицаЗаказыПокупателей.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
		|			ТОГДА ТаблицаЗаказыПокупателей.СтруктурнаяЕдиница
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|	КОНЕЦ,
		|	ТаблицаЗаказыПокупателей.Номенклатура,
		|	ТаблицаЗаказыПокупателей.Характеристика,
		|	ТаблицаЗаказыПокупателей.Документ
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТаблицаЗаказыПокупателей.Количество) <> 0";
	
		РезультатЗапроса = Запрос.Выполнить();
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗаказыПокупателей", РезультатЗапроса.Выгрузить());
		
	КонецЕсли;

	
КонецПроцедуры // СформироватьТаблицаЗаказыПокупателей()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходы(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ТаблицаДоходыИРасходы.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДоходыИРасходы.Период КАК Период,
	|	ТаблицаДоходыИРасходы.Организация КАК Организация,
	|	ТаблицаДоходыИРасходы.ПодразделениеПродажи КАК СтруктурнаяЕдиница,
	|	ТаблицаДоходыИРасходы.Проект КАК Проект,
	|	ТаблицаДоходыИРасходы.НаправлениеДеятельностиПродажи КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ТаблицаДоходыИРасходы.ЭтоЗаказНаряд
	|			ТОГДА ТаблицаДоходыИРасходы.Документ
	|		ИНАЧЕ ТаблицаДоходыИРасходы.Заказ
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ТаблицаДоходыИРасходы.СчетУчетаПродажи КАК СчетУчета,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	ВЫРАЗИТЬ(&Выручка КАК СТРОКА(100)) КАК СодержаниеПроводки,
	|	СУММА(ТаблицаДоходыИРасходы.Сумма) КАК СуммаДоходов,
	|	0 КАК СуммаРасходов,
	|	СУММА(ТаблицаДоходыИРасходы.Сумма) КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДоходыИРасходы
	|ГДЕ
	|	НЕ ТаблицаДоходыИРасходы.ТоварыНаКомиссии
	|	И ТаблицаДоходыИРасходы.Сумма <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДоходыИРасходы.Период,
	|	ТаблицаДоходыИРасходы.НомерСтроки,
	|	ТаблицаДоходыИРасходы.Организация,
	|	ТаблицаДоходыИРасходы.ПодразделениеПродажи,
	|	ТаблицаДоходыИРасходы.Проект,
	|	ТаблицаДоходыИРасходы.НаправлениеДеятельностиПродажи,
	|	ВЫБОР
	|		КОГДА ТаблицаДоходыИРасходы.ЭтоЗаказНаряд
	|			ТОГДА ТаблицаДоходыИРасходы.Документ
	|		ИНАЧЕ ТаблицаДоходыИРасходы.Заказ
	|	КОНЕЦ,
	|	ТаблицаДоходыИРасходы.СчетУчетаПродажи
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА &ПоложительнаяКурсоваяРазницаСчетУчета
	|		ИНАЧЕ &ОтрицательнаяКурсоваяРазницаСчетУчета
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Дата КАК Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Организация КАК Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Валюта КАК Валюта,
	|		СУММА(ТаблицаКурсовыхРазницРасчетыСПокупателями.СуммаКурсовойРазницы) КАК СуммаКурсовойРазницы
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Дата КАК Дата,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.Валюта КАК Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы КАК СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаКурсовыхРазницРасчетыСПокупателями
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Валюта
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаКурсовыхРазницРасчетыСПокупателями.СуммаКурсовойРазницы) >= 0.005
	|			ИЛИ СУММА(ТаблицаКурсовыхРазницРасчетыСПокупателями.СуммаКурсовойРазницы) <= -0.005)) КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ПоложительнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеДоходы);
	Запрос.УстановитьПараметр("ОтрицательнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеРасходы);
	Запрос.УстановитьПараметр("Выручка", НСтр("ru='Выручка от продажи'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходы", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПокупателями(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКорректировкаРеализации);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ВозникновениеОбязательствПокупателя", НСтр("ru='Возникновение обязательств покупателя'"));
	Запрос.УстановитьПараметр("ЗачетАванса", НСтр("ru='Зачет предоплаты'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	РасчетыПроведениеДокументов.СформироватьДвиженияПоВзаиморасчетам(СтруктураДополнительныеСвойства, ДокументСсылкаКорректировкаРеализации, Запрос.Параметры);
	РасчетыПроведениеДокументов.ПересчитатьТаблицыПоКурсамВалют(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	РасчетыПроведениеДокументов.ПоместитьПересчитанныеТаблицыВМенеджерВременныхТаблиц(СтруктураДополнительныеСвойства);
	
	// Установка исключительной блокировки контролируемых остатков расчетов с контрагентами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПокупателями.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыСПокупателями.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасчетыСПокупателями.Договор КАК Договор,
	|	ВременнаяТаблицаРасчетыСПокупателями.Документ КАК Документ,
	|	ВременнаяТаблицаРасчетыСПокупателями.Заказ КАК Заказ,
	|	ВременнаяТаблицаРасчетыСПокупателями.ТипРасчетов КАК ТипРасчетов
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПокупателями";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПокупателями");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = КурсовыеРазницыУНФ.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыСПокупателями(Запрос.МенеджерВременныхТаблиц, Истина, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПокупателями", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры

// Процедура формирования таблицы платежного календаря.
//
// Параметры:
//	ДокументСсылка - ДокументСсылка.ПриходДенежныхСредствПлан - Текущий документ
//	ДополнительныеСвойства - ДополнительныеСвойства - Дополнительные свойства документа
//
Процедура СформироватьТаблицаПлатежныйКалендарь(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКорректировкаРеализации);
	Запрос.УстановитьПараметр("ЗаказНаряд", СтруктураДополнительныеСвойства.ДляПроведения.ДокументРеализации);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.ДатаОплатыДоИзменения КАК Период,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Ссылка.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА -(ВЫРАЗИТЬ(ТаблицаДокумента.СуммаОплатыДоИзменения * ВЫБОР
	|						КОГДА КурсыВалютРасчетов.Курс <> 0
	|								И КурсыВалютДокумента.Кратность <> 0
	|							ТОГДА КурсыВалютДокумента.Курс * КурсыВалютРасчетов.Кратность / (ЕСТЬNULL(КурсыВалютРасчетов.Курс, 1) * ЕСТЬNULL(КурсыВалютДокумента.Кратность, 1))
	|						ИНАЧЕ 1
	|					КОНЕЦ КАК ЧИСЛО(15, 2)))
	|		ИНАЧЕ -ТаблицаДокумента.СуммаОплатыДоИзменения
	|	КОНЕЦ КАК Сумма,
	|	ТаблицаДокумента.Ссылка
	|ПОМЕСТИТЬ Вт_ТаблицаДокумента
	|ИЗ
	|	Документ.КорректировкаРеализации.ПлатежныйКалендарь КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО ТаблицаДокумента.Ссылка.Договор.ВалютаРасчетов = КурсыВалютРасчетов.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютДокумента
	|		ПО ТаблицаДокумента.Ссылка.ВалютаДокумента = КурсыВалютДокумента.Валюта
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И ТаблицаДокумента.Ссылка.ОтражатьВУчете
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.ДатаОплаты,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Ссылка.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаДокумента.СуммаОплаты * ВЫБОР
	|						КОГДА КурсыВалютРасчетов.Курс <> 0
	|								И КурсыВалютДокумента.Кратность <> 0
	|							ТОГДА КурсыВалютДокумента.Курс * КурсыВалютРасчетов.Кратность / (ЕСТЬNULL(КурсыВалютРасчетов.Курс, 1) * ЕСТЬNULL(КурсыВалютДокумента.Кратность, 1))
	|						ИНАЧЕ 1
	|					КОНЕЦ КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ ТаблицаДокумента.СуммаОплаты
	|	КОНЕЦ,
	|	ТаблицаДокумента.Ссылка
	|ИЗ
	|	Документ.КорректировкаРеализации.ПлатежныйКалендарь КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО ТаблицаДокумента.Ссылка.Договор.ВалютаРасчетов = КурсыВалютРасчетов.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютДокумента
	|		ПО ТаблицаДокумента.Ссылка.ВалютаДокумента = КурсыВалютДокумента.Валюта
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И ТаблицаДокумента.Ссылка.ОтражатьВУчете
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Период,
	|	&Организация,
	|	ТаблицаДокумента.Ссылка.ТипДенежныхСредств КАК ТипДенежныхСредств,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыУтвержденияПлатежей.Утвержден) КАК СтатусУтвержденияПлатежа,
	|	&ЗаказНаряд КАК СчетНаОплату,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей) КАК Статья,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Ссылка.ТипДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Наличные)
	|			ТОГДА ТаблицаДокумента.Ссылка.Касса
	|		КОГДА ТаблицаДокумента.Ссылка.ТипДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Безналичные)
	|			ТОГДА ТаблицаДокумента.Ссылка.БанковскийСчет
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК БанковскийСчетКасса,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Ссылка.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА ТаблицаДокумента.Ссылка.Договор.ВалютаРасчетов
	|		ИНАЧЕ ТаблицаДокумента.Ссылка.ВалютаДокумента
	|	КОНЕЦ КАК Валюта,
	|	ТаблицаДокумента.Сумма
	|ИЗ
	|	Вт_ТаблицаДокумента КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО ТаблицаДокумента.Ссылка.Договор.ВалютаРасчетов = КурсыВалютРасчетов.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютДокумента
	|		ПО ТаблицаДокумента.Ссылка.ВалютаДокумента = КурсыВалютДокумента.Валюта";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПлатежныйКалендарь", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаПлатежныйКалендарь()

// Процедура формирования таблицы счетов на оплату.
//
// Параметры:
//	ДокументСсылка - ДокументСсылка.ПриходДенежныхСредствПлан - Текущий документ
//	ДополнительныеСвойства - ДополнительныеСвойства - Дополнительные свойства документа
//
Процедура СформироватьТаблицаОплатаСчетовИЗаказов(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ЗаказНаряд", СтруктураДополнительныеСвойства.ДляПроведения.ДокументРеализации);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	&ЗаказНаряд КАК СчетНаОплату,
	|	СУММА(ТаблицаДокумента.Всего) КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаИзмененияЗапасов КАК ТаблицаДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка.Дата";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОплатаСчетовИЗаказов", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаОплатаСчетовИЗаказов()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыОтложенные(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКорректировкаРеализации);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Период КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.НаправлениеДеятельностиПродажи КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Сумма КАК СуммаДоходов,
	|	ТаблицаДокумента.Сумма КАК НеРаспределено
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|	И ТаблицаДокумента.Сумма <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Организация КАК Организация,
	|	СУММА(ТаблицаДокумента.Сумма) КАК СуммаКСписанию
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|	И НЕ ТаблицаДокумента.ЭтоЭквайринговаяОперация
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Статья КАК Статья
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|	И НЕ ТаблицаДокумента.ЭтоЭквайринговаяОперация
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаЗапасыДоходыИРасходыОтложенные = МассивРезультатов[0].Выгрузить();
	ВыборкаРезультатаЗапроса = МассивРезультатов[1].Выбрать();
	
	ТаблицаПредоплатаДоходыИРасходыОтложенные = ТаблицаЗапасыДоходыИРасходыОтложенные.Скопировать();
	ТаблицаПредоплатаДоходыИРасходыОтложенные.Очистить();
	
	Если ВыборкаРезультатаЗапроса.Следующий() Тогда
		СуммаКСписанию = ВыборкаРезультатаЗапроса.СуммаКСписанию;
		Для каждого СтрокаЗапасыДоходыИРасходыОтложенные Из ТаблицаЗапасыДоходыИРасходыОтложенные Цикл
			Если СуммаКСписанию = 0 Тогда
				Продолжить
			ИначеЕсли СтрокаЗапасыДоходыИРасходыОтложенные.СуммаДоходов <= СуммаКСписанию Тогда
				СтрокаПредоплатаДоходыИРасходыОтложенные = ТаблицаПредоплатаДоходыИРасходыОтложенные.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПредоплатаДоходыИРасходыОтложенные, СтрокаЗапасыДоходыИРасходыОтложенные);
				СуммаКСписанию = СуммаКСписанию - СтрокаЗапасыДоходыИРасходыОтложенные.СуммаДоходов;
			ИначеЕсли СтрокаЗапасыДоходыИРасходыОтложенные.СуммаДоходов > СуммаКСписанию Тогда
				СтрокаПредоплатаДоходыИРасходыОтложенные = ТаблицаПредоплатаДоходыИРасходыОтложенные.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПредоплатаДоходыИРасходыОтложенные, СтрокаЗапасыДоходыИРасходыОтложенные);
				СтрокаПредоплатаДоходыИРасходыОтложенные.СуммаДоходов = СуммаКСписанию;
				СуммаКСписанию = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для каждого СтрокаПредоплатаДоходыИРасходыОтложенные Из ТаблицаПредоплатаДоходыИРасходыОтложенные Цикл
		СтрокаЗапасыДоходыИРасходыОтложенные = ТаблицаЗапасыДоходыИРасходыОтложенные.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗапасыДоходыИРасходыОтложенные, СтрокаПредоплатаДоходыИРасходыОтложенные);
		СтрокаЗапасыДоходыИРасходыОтложенные.ВидДвижения = ВидДвиженияНакопления.Расход;
		СтрокаЗапасыДоходыИРасходыОтложенные.НеРаспределено = -СтрокаПредоплатаДоходыИРасходыОтложенные.СуммаДоходов;
	КонецЦикла;
	
	ВыборкаРезультатаЗапроса = МассивРезультатов[2].Выбрать();
	
	Если ВыборкаРезультатаЗапроса.Следующий() Тогда
		Статья = ВыборкаРезультатаЗапроса.Статья;
	Иначе
		Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	Таблица.Период КАК Период,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Документ КАК Документ,
	|	&Статья КАК Статья,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Таблица.СуммаДоходов КАК СуммаДоходов
	|ПОМЕСТИТЬ ВременнаяТаблицаПредоплатаДоходыИРасходыОтложенные
	|ИЗ
	|	&Таблица КАК Таблица";
	Запрос.УстановитьПараметр("Таблица", ТаблицаПредоплатаДоходыИРасходыОтложенные);
	Запрос.УстановитьПараметр("Статья", Статья);
	
	Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыОтложенные", ТаблицаЗапасыДоходыИРасходыОтложенные);
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыОтложенные()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыНераспределенные(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаДокумента.Период КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	ТаблицаДокумента.Сумма КАК СуммаДоходов
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|	И НЕ ТаблицаДокумента.ЭтоЭквайринговаяОперация";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыНераспределенные", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыНераспределенные()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыКассовыйМетод(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКорректировкаРеализации);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.ДокументДата КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	-ТаблицаДокумента.Сумма КАК СуммаДоходов
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ТаблицаДокумента.ЭтоЭквайринговаяОперация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Период,
	|	Таблица.Организация,
	|	Таблица.НаправлениеДеятельности,
	|	Таблица.Статья,
	|	Таблица.СуммаДоходов
	|ИЗ
	|	ВременнаяТаблицаПредоплатаДоходыИРасходыОтложенные КАК Таблица";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыКассовыйМетод", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыКассовыйМетод()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаУправленческий(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ТаблицаУправленческий.НомерСтроки КАК НомерСтроки,
	|	ТаблицаУправленческий.Период КАК Период,
	|	ТаблицаУправленческий.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ТаблицаУправленческий.СчетУчетаРасчетовСПокупателем КАК СчетДт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПокупателем.Валютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПокупателем.Валютный
	|			ТОГДА ТаблицаУправленческий.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалДт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.ТоварыНаКомиссии
	|			ТОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПоставщиком
	|		ИНАЧЕ ТаблицаУправленческий.СчетУчетаПродажи
	|	КОНЕЦ КАК СчетКт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.ТоварыНаКомиссии
	|			ТОГДА &ВалютаУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаКт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.ТоварыНаКомиссии
	|			ТОГДА ТаблицаУправленческий.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалКт,
	|	ТаблицаУправленческий.Сумма КАК Сумма,
	|	&ОтражениеВыручки КАК Содержание
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаУправленческий
	|ГДЕ
	|	ТаблицаУправленческий.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	1,
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.СчетУчетаАвансовПокупателя,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаАвансовПокупателяВалютный
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаАвансовПокупателяВалютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаРасчетовСПокупателемВалютный
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаРасчетовСПокупателемВалютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	&ЗачетПредоплаты
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаДокумента.Период КАК Период,
	|		ТаблицаДокумента.Организация КАК Организация,
	|		ТаблицаДокумента.СчетУчетаАвансовПокупателя КАК СчетУчетаАвансовПокупателя,
	|		ТаблицаДокумента.СчетУчетаАвансовПокупателяВалютный КАК СчетУчетаАвансовПокупателяВалютный,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПокупателемВалютный КАК СчетУчетаРасчетовСПокупателемВалютный,
	|		ТаблицаДокумента.ВалютаРасчетов КАК ВалютаРасчетов,
	|		СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВал,
	|		СУММА(ТаблицаДокумента.Сумма) КАК Сумма
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Период КАК Период,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.СчетУчетаАвансовПокупателя КАК СчетУчетаАвансовПокупателя,
	|			ТаблицаДокумента.СчетУчетаАвансовПокупателя.Валютный КАК СчетУчетаАвансовПокупателяВалютный,
	|			ТаблицаДокумента.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|			ТаблицаДокумента.СчетУчетаРасчетовСПокупателем.Валютный КАК СчетУчетаРасчетовСПокупателемВалютный,
	|			ТаблицаДокумента.ВалютаРасчетов КАК ВалютаРасчетов,
	|			ТаблицаДокумента.СуммаВал КАК СуммаВал,
	|			ТаблицаДокумента.Сумма КАК Сумма
	|		ИЗ
	|			ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.Контрагент.СчетУчетаАвансовПокупателя,
	|			ТаблицаДокумента.Контрагент.СчетУчетаАвансовПокупателя.Валютный,
	|			ТаблицаДокумента.Контрагент.СчетУчетаРасчетовСПокупателем,
	|			ТаблицаДокумента.Контрагент.СчетУчетаРасчетовСПокупателем.Валютный,
	|			ТаблицаДокумента.Валюта,
	|			0,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаДокумента
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаДокумента.Период,
	|		ТаблицаДокумента.Организация,
	|		ТаблицаДокумента.СчетУчетаАвансовПокупателя,
	|		ТаблицаДокумента.СчетУчетаАвансовПокупателяВалютный,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПокупателем,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПокупателемВалютный,
	|		ТаблицаДокумента.ВалютаРасчетов
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаДокумента.Сумма) >= 0.005
	|			ИЛИ СУММА(ТаблицаДокумента.Сумма) <= -0.005
	|			ИЛИ СУММА(ТаблицаДокумента.СуммаВал) >= 0.005
	|			ИЛИ СУММА(ТаблицаДокумента.СуммаВал) <= -0.005)) КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	1,
	|	ТаблицаУправленческий.Дата,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаУправленческий.СчетУчета
	|		ИНАЧЕ &ОтрицательнаяКурсоваяРазницаСчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|				И ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА &ПоложительнаяКурсоваяРазницаСчетУчета
	|		ИНАЧЕ ТаблицаУправленческий.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы < 0
	|				И ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаУправленческий.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаУправленческий.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Дата КАК Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Организация КАК Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.СчетУчета КАК СчетУчета,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.СчетУчетаВалютный КАК СчетУчетаВалютный,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Валюта КАК Валюта,
	|		СУММА(ТаблицаКурсовыхРазницРасчетыСПокупателями.СуммаКурсовойРазницы) КАК СуммаКурсовойРазницы
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Дата КАК Дата,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.СчетУчета КАК СчетУчета,
	|			ТаблицаДокумента.СчетУчета.Валютный КАК СчетУчетаВалютный,
	|			ТаблицаДокумента.Валюта КАК Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы КАК СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.СчетУчета,
	|			ТаблицаДокумента.СчетУчета.Валютный,
	|			ТаблицаДокумента.Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаКурсовыхРазницРасчетыСПокупателями
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.СчетУчета,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.СчетУчетаВалютный,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Валюта
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаКурсовыхРазницРасчетыСПокупателями.СуммаКурсовойРазницы) >= 0.005
	|			ИЛИ СУММА(ТаблицаКурсовыхРазницРасчетыСПокупателями.СуммаКурсовойРазницы) <= -0.005)) КАК ТаблицаУправленческий
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ЗачетПредоплаты", НСтр("ru = 'Зачет предоплаты'"));
	Запрос.УстановитьПараметр("ОтражениеВыручки", НСтр("ru = 'Выручка от продажи'"));
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ПоложительнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеДоходы);
	Запрос.УстановитьПараметр("ОтрицательнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеРасходы);
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл  
		НоваяПроводка = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПроводка, Выборка);
	КонецЦикла;
	
КонецПроцедуры // СформироватьТаблицаУправленческий()

// Формирует таблицу значений, содержащую данные для проведения по регистру "СуммыДокументовРегламентированныйУчет".
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСуммыДокументовРегламентированныйУчет(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства)
	
	Если СтруктураДополнительныеСвойства.ВалютаДокумента <> СтруктураДополнительныеСвойства.НациональнаяВалюта Тогда
		РасчетыПроведениеДокументов.СформироватьТаблицаСуммыДокументовРегламентированныйУчет(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	Иначе
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСуммыДокументовРегламентированныйУчет", Новый ТаблицаЗначений);
	КонецЕсли;
	
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапросаИнициализироватьДанныеДокумента();
	
	ДокументРеализации = ПолучитьИсправляемыйДокументРеализации(ДокументСсылкаКорректировкаРеализации, Истина);
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКорректировкаРеализации);
	Запрос.УстановитьПараметр("ДокументРеализации", ДокументРеализации);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьХарактеристики);
	Запрос.УстановитьПараметр("ИспользоватьПартии", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьПартии);
	Запрос.УстановитьПараметр("УчетПоЯчейкам", СтруктураДополнительныеСвойства.УчетнаяПолитика.УчетПоЯчейкам);
	Запрос.УстановитьПараметр("РазрешитьСкладыВТабличныхЧастях", СтруктураДополнительныеСвойства.УчетнаяПолитика.РазрешитьСкладыВТабличныхЧастях);
	
	// Прослеживаемость
	Запрос.УстановитьПараметр("ВестиУчетПрослеживаемыхТоваров",
		СтруктураДополнительныеСвойства.УчетнаяПолитика.ВестиУчетПрослеживаемыхТоваров);
	Запрос.УстановитьПараметр("ИсправляемыйДокумент", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылкаКорректировкаРеализации, "ДокументОснование", Истина));
	// Конец Прослеживаемость
	
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаНациональная", Константы.НациональнаяВалюта.Получить());
	Запрос.УстановитьПараметр("ВалютаНакладной", ДокументСсылкаКорректировкаРеализации.ВалютаДокумента);
	
	// Перед выполнением запроса проверим, необходим ли пересчет из валюты в валюту
	РасчетыПроведениеДокументов.МодифицироватьЗапросДляПересчетаВВалюту(ДокументСсылкаКорректировкаРеализации, Запрос, СтруктураДополнительныеСвойства);
	
	Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ДляПроведения.Вставить("ДокументРеализации", ДокументРеализации);
	ОснованиеЗаказНаряд = ТипЗнч(ДокументРеализации) = Тип("ДокументСсылка.ЗаказПокупателя");
	
	// Формирование проводок документа.
	ПроведениеДокументовУНФ.СформироватьТаблицуПроводок(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаРасчетыСПокупателями(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПродажи(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПродажиПоДисконтнойКарте(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	СформироватьТаблицаВыпускПродукции(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасыНаСкладах(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасыКРасходуСоСкладов(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасыПринятые(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗаказыПокупателей(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходы(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	
	Если НЕ ОснованиеЗаказНаряд Тогда
		СформироватьТаблицаЗапасы(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	Иначе
		СформироватьТаблицаГрафикДвиженияЗапасов(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
		СформироватьТаблицаЗапасыРаботы(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
		СформироватьТаблицаЗапасыТовары(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	КонецЕсли;
	
	СформироватьТаблицаЗапасыВРазрезеГТД(СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаДоходыИРасходыОтложенные(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыНераспределенные(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыКассовыйМетод(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаПлатежныйКалендарь(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	СформироватьТаблицаОплатаСчетовИЗаказов(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	
	// Эквайринг
	ЭквайринговыеОперацииСервер.СформироватьТаблицаДоходыИРасходыКассовыйМетодЭквайринг(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	
	// Прослеживаемость
	ПрослеживаемостьУНФ.СформироватьДвиженияПрослеживаемыхТоваров(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОперацииСПрослеживаемымиТоварами", Новый ТаблицаЗначений);
	// Конец Прослеживаемость
	
	СформироватьТаблицаУправленческий(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаСуммыДокументовРегламентированныйУчет(ДокументСсылкаКорректировкаРеализации, СтруктураДополнительныеСвойства);
	
	Если ДокументСсылкаКорректировкаРеализации.Договор.РасчетыВУсловныхЕдиницах И ДокументСсылкаКорректировкаРеализации.Предоплата.Количество() > 0 Тогда  //ToDo: Под вопросом
			
			// Поведение будет реализовано в будущих версиях.
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Обратите внимание, что для корректировочных документов в валюте по договору в у.е. распределение авансов по курсу авансов не поддерживается.'");
			Сообщение.Сообщить();
			
	КонецЕсли;
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

Функция ТекстЗапросаИнициализироватьДанныеДокумента()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Ссылка.Договор.ЭтоДоговорОбслуживания
	|				И КорректировкаРеализации.Договор.ДоговорОбслуживанияНаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВестиУчетРасходовПоДоговорамОбслуживания,
	|	КорректировкаРеализации.Ссылка КАК Ссылка,
	|	&ДокументРеализации КАК КорректируемыйДокумент,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаИРаспределенияПлатежей.Вручную) КАК СпособЗачетаПредоплаты,
	|	КорректировкаРеализации.Ссылка.Договор.ДоговорОбслуживанияНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	КорректировкаРеализации.ДокументОснование КАК ДокументОснование,
	|	КорректировкаРеализации.Договор.ВалютаРасчетов КАК ДоговорВалютаРасчетов,
	|	КорректировкаРеализации.Договор.РасчетыВУсловныхЕдиницах КАК ДоговорРасчетыВУсловныхЕдиницах,
	|	КорректировкаРеализации.Контрагент КАК Контрагент,
	|	КорректировкаРеализации.ВалютаДокумента КАК ВалютаДокумента,
	|	КорректировкаРеализации.Кратность КАК Кратность,
	|	КорректировкаРеализации.Курс КАК Курс,
	|	КорректировкаРеализации.Договор КАК Договор,
	|	КорректировкаРеализации.Дата КАК Дата,
	|	КорректировкаРеализации.Дата КАК Период,
	|	КорректировкаРеализации.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	КорректировкаРеализации.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	КорректировкаРеализации.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам
	|ПОМЕСТИТЬ ВременнаяТаблицаШапка
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	КорректировкаРеализации.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ ВременнаяТаблицаКурсыВалютСрезПоследних
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта В (&ВалютаУчета, &ВалютаНациональная, &ВалютаНакладной)) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкаРеализацииЗапасы.Ссылка КАК Ссылка,
	|	КорректировкаРеализацииЗапасы.Ссылка.Договор КАК Договор,
	|	КорректировкаРеализацииЗапасы.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаРеализацииЗапасы.Номенклатура КАК Номенклатура,
	|	КорректировкаРеализацииЗапасы.Характеристика КАК Характеристика,
	|	КорректировкаРеализацииЗапасы.Партия КАК Партия,
	|	КорректировкаРеализацииЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	КорректировкаРеализацииЗапасы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	КорректировкаРеализацииЗапасы.НомерГТД КАК НомерГТД,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаРеализацииЗапасы.ЕстьВДокументеРеализации
	|			ТОГДА КорректировкаРеализацииЗапасы.Количество
	|		ИНАЧЕ КорректировкаРеализацииЗапасы.Количество - КорректировкаРеализацииЗапасы.КоличествоДоИзменения
	|	КОНЕЦ КАК Количество,
	|	КорректировкаРеализацииЗапасы.Цена КАК Цена,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаРеализацииЗапасы.ЕстьВДокументеРеализации
	|			ТОГДА КорректировкаРеализацииЗапасы.Сумма
	|		ИНАЧЕ КорректировкаРеализацииЗапасы.Сумма - КорректировкаРеализацииЗапасы.СуммаДоИзменения
	|	КОНЕЦ КАК Сумма,
	|	КорректировкаРеализацииЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаРеализацииЗапасы.ЕстьВДокументеРеализации
	|			ТОГДА КорректировкаРеализацииЗапасы.СуммаНДС
	|		ИНАЧЕ КорректировкаРеализацииЗапасы.СуммаНДС - КорректировкаРеализацииЗапасы.СуммаНДСДоИзменения
	|	КОНЕЦ КАК СуммаНДС,
	|	КорректировкаРеализацииЗапасы.Заказ КАК Заказ,
	|	КорректировкаРеализацииЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	КорректировкаРеализацииЗапасы.Ячейка КАК Ячейка,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаРеализацииЗапасы.ЕстьВДокументеРеализации
	|			ТОГДА КорректировкаРеализацииЗапасы.Всего
	|		ИНАЧЕ КорректировкаРеализацииЗапасы.Всего - КорректировкаРеализацииЗапасы.ВсегоДоИзменения
	|	КОНЕЦ КАК Всего,
	|	КорректировкаРеализацииЗапасы.Содержание КАК Содержание,
	|	КорректировкаРеализацииЗапасы.НомерСтроки КАК НомерСтроки,
	|	КорректировкаРеализацииЗапасы.Резерв КАК Резерв,
	|	КорректировкаРеализацииЗапасы.КлючСвязи КАК КлючСвязи,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(&ДокументРеализации) = ТИП(Документ.ЗаказПокупателя)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОснованиеЗаказНаряд,
	|	КорректировкаРеализацииЗапасы.Спецификация КАК Спецификация,
	|	КорректировкаРеализацииЗапасы.ЕстьВДокументеРеализации КАК ЕстьВДокументеРеализации,
	|	КорректировкаРеализацииЗапасы.НоменклатураНабора КАК НоменклатураНабора,
	|	КорректировкаРеализацииЗапасы.ХарактеристикаНабора КАК ХарактеристикаНабора
	|ПОМЕСТИТЬ ВременнаяТаблицаИзмененияЗапасов
	|ИЗ
	|	Документ.КорректировкаРеализации.Запасы КАК КорректировкаРеализацииЗапасы
	|ГДЕ
	|	КорректировкаРеализацииЗапасы.Ссылка = &Ссылка
	|	И (ВЫБОР
	|				КОГДА НЕ КорректировкаРеализацииЗапасы.ЕстьВДокументеРеализации
	|					ТОГДА КорректировкаРеализацииЗапасы.Количество
	|				ИНАЧЕ КорректировкаРеализацииЗапасы.Количество - КорректировкаРеализацииЗапасы.КоличествоДоИзменения
	|			КОНЕЦ <> 0
	|			ИЛИ ВЫБОР
	|				КОГДА НЕ КорректировкаРеализацииЗапасы.ЕстьВДокументеРеализации
	|					ТОГДА КорректировкаРеализацииЗапасы.Сумма
	|				ИНАЧЕ КорректировкаРеализацииЗапасы.Сумма - КорректировкаРеализацииЗапасы.СуммаДоИзменения
	|			КОНЕЦ <> 0)
	|	И КорректировкаРеализацииЗапасы.Ссылка.ОтражатьВУчете
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка,
	|	КорректировкаРеализации.Договор,
	|	"""",
	|	КорректировкаРеализации.НоменклатураДоставки,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка),
	|	КорректировкаРеализации.НоменклатураДоставки.ЕдиницаИзмерения,
	|	КорректировкаРеализации.НоменклатураДоставки.СтранаПроисхождения,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка),
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаРеализации.ЕстьВДокументеРеализации
	|				И КорректировкаРеализации.СтоимостьДоставки > 0
	|			ТОГДА 1
	|		КОГДА КорректировкаРеализации.ОтменаДоставки
	|			ТОГДА -1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	КорректировкаРеализации.СтоимостьДоставки,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаРеализации.ЕстьВДокументеРеализации
	|			ТОГДА КорректировкаРеализации.СтоимостьДоставки
	|		ИНАЧЕ КорректировкаРеализации.СтоимостьДоставки - КорректировкаРеализации.СтоимостьДоставкиДоИзменения
	|	КОНЕЦ,
	|	КорректировкаРеализации.СтавкаНДСДоставки,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаРеализации.ЕстьВДокументеРеализации
	|			ТОГДА КорректировкаРеализации.СуммаНДСДоставки
	|		ИНАЧЕ КорректировкаРеализации.СуммаНДСДоставки - КорректировкаРеализации.СуммаНДСДоставкиДоИзменения
	|	КОНЕЦ,
	|	КорректировкаРеализации.Заказ,
	|	КорректировкаРеализации.СтруктурнаяЕдиница,
	|	КорректировкаРеализации.Ячейка,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаРеализации.ЕстьВДокументеРеализации
	|			ТОГДА КорректировкаРеализации.СтоимостьДоставки
	|		ИНАЧЕ КорректировкаРеализации.СтоимостьДоставки - КорректировкаРеализации.СтоимостьДоставкиДоИзменения
	|	КОНЕЦ + ВЫБОР
	|		КОГДА КорректировкаРеализации.СуммаВключаетНДС
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НЕ КорректировкаРеализации.ЕстьВДокументеРеализации
	|					ТОГДА КорректировкаРеализации.СуммаНДСДоставки
	|				ИНАЧЕ КорректировкаРеализации.СуммаНДСДоставки - КорректировкаРеализации.СуммаНДСДоставкиДоИзменения
	|			КОНЕЦ
	|	КОНЕЦ,
	|	"""",
	|	0,
	|	0,
	|	0,
	|	ЛОЖЬ,
	|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка),
	|	КорректировкаРеализации.ЕстьВДокументеРеализации,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	КорректировкаРеализации.Ссылка = &Ссылка
	|	И ВЫБОР
	|			КОГДА НЕ КорректировкаРеализации.ЕстьВДокументеРеализации
	|				ТОГДА КорректировкаРеализации.СтоимостьДоставки
	|			ИНАЧЕ КорректировкаРеализации.СтоимостьДоставки - КорректировкаРеализации.СтоимостьДоставкиДоИзменения
	|		КОНЕЦ <> 0
	|	И КорректировкаРеализации.ОтражатьВУчете
	|	И КорректировкаРеализации.НоменклатураДоставки <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИзмененияЗапасов.НомерСтроки КАК НомерСтроки,
	|	ИзмененияЗапасов.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ИзмененияЗапасов.Ссылка.ВидОперации КАК ВидОперации,
	|	&ДокументРеализации КАК Документ,
	|	ИзмененияЗапасов.Ссылка.Ответственный КАК Ответственный,
	|	ИзмененияЗапасов.Ссылка.ДокументОснование КАК ДокументОснование,
	|	ИзмененияЗапасов.Ссылка.Контрагент КАК Контрагент,
	|	ИзмененияЗапасов.Ссылка.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	ИзмененияЗапасов.Ссылка.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ИзмененияЗапасов.Ссылка.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	ИзмененияЗапасов.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	ИзмененияЗапасов.Ссылка.Договор КАК Договор,
	|	ИзмененияЗапасов.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК КоррОрганизация,
	|	ИзмененияЗапасов.Ссылка.Подразделение КАК ПодразделениеПродажи,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности
	|		ИНАЧЕ ИзмененияЗапасов.Номенклатура.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельностиПродажи,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности.СчетУчетаВыручкиОтПродаж
	|		ИНАЧЕ ИзмененияЗапасов.Номенклатура.НаправлениеДеятельности.СчетУчетаВыручкиОтПродаж
	|	КОНЕЦ КАК СчетУчетаПродажи,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности.СчетУчетаСебестоимостиПродаж
	|		ИНАЧЕ ИзмененияЗапасов.Номенклатура.НаправлениеДеятельности.СчетУчетаСебестоимостиПродаж
	|	КОНЕЦ КАК СчетУчетаСебестоимость,
	|	ИзмененияЗапасов.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ИзмененияЗапасов.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка) КАК КоррСтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ИзмененияЗапасов.СтруктурнаяЕдиница.ОрдерныйСклад
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдерныйСклад,
	|	ВЫБОР
	|		КОГДА &УчетПоЯчейкам
	|			ТОГДА ИзмененияЗапасов.Ячейка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|	КОНЕЦ КАК Ячейка,
	|	ВЫБОР
	|		КОГДА ИзмененияЗапасов.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ИзмененияЗапасов.Номенклатура.СчетУчетаЗапасов
	|		ИНАЧЕ ИзмененияЗапасов.Номенклатура.СчетУчетаЗатрат
	|	КОНЕЦ КАК СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка) КАК КоррСчетУчета,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПартии
	|				И ИзмененияЗапасов.Партия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварыНаКомиссии)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТоварыНаКомиссии,
	|	ИзмененияЗапасов.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК КоррНоменклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА ИзмененияЗапасов.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК КоррХарактеристика,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПартии
	|			ТОГДА ИзмененияЗапасов.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Партия,
	|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка) КАК КоррПартия,
	|	ВЫБОР
	|		КОГДА ИзмененияЗапасов.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ИзмененияЗапасов.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ВЫБОР
	|		КОГДА ИзмененияЗапасов.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ИзмененияЗапасов.Заказ.УчетПотребностиПоСкладам
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК УчетПотребностиПоСкладам,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка) КАК КоррЗаказ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ИзмененияЗапасов.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА ИзмененияЗапасов.Количество
	|		ИНАЧЕ ИзмененияЗапасов.Количество * ИзмененияЗапасов.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ИзмененияЗапасов.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА ИзмененияЗапасов.Резерв
	|		ИНАЧЕ ИзмененияЗапасов.Резерв * ИзмененияЗапасов.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Резерв,
	|	ИзмененияЗапасов.СтавкаНДС КАК СтавкаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|						ТОГДА ИзмененияЗапасов.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|					ИНАЧЕ ИзмененияЗапасов.СуммаНДС * ИзмененияЗапасов.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ИзмененияЗапасов.Ссылка.Кратность)
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|						ТОГДА ИзмененияЗапасов.СуммаНДС
	|					ИНАЧЕ ИзмененияЗапасов.СуммаНДС * ИзмененияЗапасов.Ссылка.Курс / ИзмененияЗапасов.Ссылка.Кратность
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСРег,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ИзмененияЗапасов.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ИзмененияЗапасов.СуммаНДС * ИзмененияЗапасов.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ИзмененияЗапасов.Ссылка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСЗакупкиПродажи,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ИзмененияЗапасов.Всего * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ИзмененияЗапасов.Всего * ИзмененияЗапасов.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ИзмененияЗапасов.Ссылка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|						ТОГДА ИзмененияЗапасов.СуммаНДС * КурсыРегВалюты.Курс * ИзмененияЗапасов.Ссылка.Кратность / (ИзмененияЗапасов.Ссылка.Курс * КурсыРегВалюты.Кратность)
	|					ИНАЧЕ ИзмененияЗапасов.СуммаНДС
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ИзмененияЗапасов.Всего * КурсыРегВалюты.Курс * ИзмененияЗапасов.Ссылка.Кратность / (ИзмененияЗапасов.Ссылка.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ИзмененияЗапасов.Всего
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ИзмененияЗапасов.Всего
	|			ИНАЧЕ ИзмененияЗапасов.Всего * ИзмененияЗапасов.Ссылка.Курс / ИзмененияЗапасов.Ссылка.Кратность
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаРег,
	|	ИзмененияЗапасов.Всего КАК СуммаРасчетовПринятыеПереданные,
	|	ИзмененияЗапасов.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|	ИзмененияЗапасов.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	ИзмененияЗапасов.Ссылка.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ИзмененияЗапасов.КлючСвязи КАК КлючСвязи,
	|	ИзмененияЗапасов.Спецификация КАК Спецификация,
	|	ИзмененияЗапасов.ЕстьВДокументеРеализации КАК ЕстьВДокументеРеализации,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(&ДокументРеализации) = ТИП(Документ.ЗаказПокупателя)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоЗаказНаряд,
	|	ИзмененияЗапасов.СуммаНДС КАК СуммаНДСВалютаДокумента,
	|	ИзмененияЗапасов.Всего КАК ВсегоВалютаДокумента,
	|	ИзмененияЗапасов.НоменклатураНабора КАК НоменклатураНабора,
	|	ИзмененияЗапасов.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ИзмененияЗапасов.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ИзмененияЗапасов.НомерГТД КАК НомерГТД,
	|	NULL КАК КорректируемыйДокумент,
	|	ИзмененияЗапасов.Ссылка.Проект КАК Проект
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасы
	|ИЗ
	|	ВременнаяТаблицаИзмененияЗапасов КАК ИзмененияЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыУпрВалюты
	|		ПО (КурсыУпрВалюты.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыРегВалюты
	|		ПО (КурсыРегВалюты.Валюта = &ВалютаНациональная),
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	ИзмененияЗапасов.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(КорректировкаРеализацииПредоплата.НомерСтроки) КАК НомерСтроки,
	|	КорректировкаРеализацииПредоплата.Ссылка.ВидОперации КАК ВидОперации,
	|	КорректировкаРеализацииПредоплата.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент КАК Контрагент,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.СчетУчетаАвансовПокупателя КАК СчетУчетаАвансовПокупателя,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.СчетУчетаАвансовПоставщику КАК СчетУчетаАвансовПоставщику,
	|	КорректировкаРеализацииПредоплата.Ссылка.Договор КАК Договор,
	|	КорректировкаРеализацииПредоплата.Ссылка.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(&ДокументРеализации) = ТИП(Документ.ЗаказПокупателя)
	|			ТОГДА &ДокументРеализации
	|		ИНАЧЕ КорректировкаРеализацииПредоплата.Заказ
	|	КОНЕЦ КАК Заказ,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее) КАК НаправлениеДеятельностиПродажи,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс) КАК ТипРасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг) КАК ТипРасчетовКуда,
	|	&ДокументРеализации КАК ДокументКуда,
	|	КорректировкаРеализацииПредоплата.Ссылка.ДокументОснование КАК ДокументОснование,
	|	КорректировкаРеализацииПредоплата.Документ КАК Документ,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаРеализацииПредоплата.Ссылка.Контрагент.ВестиРасчетыПоДокументам
	|				ИЛИ ТИПЗНАЧЕНИЯ(КорректировкаРеализацииПредоплата.Документ) = ТИП(Документ.Взаимозачет)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА КорректировкаРеализацииПредоплата.Документ ССЫЛКА Документ.РасходСоСчета
	|					ТОГДА ВЫРАЗИТЬ(КорректировкаРеализацииПредоплата.Документ КАК Документ.РасходСоСчета).Статья
	|				КОГДА КорректировкаРеализацииПредоплата.Документ ССЫЛКА Документ.ПоступлениеВКассу
	|					ТОГДА ВЫРАЗИТЬ(КорректировкаРеализацииПредоплата.Документ КАК Документ.ПоступлениеВКассу).Статья
	|				КОГДА КорректировкаРеализацииПредоплата.Документ ССЫЛКА Документ.РасходИзКассы
	|					ТОГДА ВЫРАЗИТЬ(КорректировкаРеализацииПредоплата.Документ КАК Документ.РасходИзКассы).Статья
	|				КОГДА КорректировкаРеализацииПредоплата.Документ ССЫЛКА Документ.ПоступлениеНаСчет
	|					ТОГДА ВЫРАЗИТЬ(КорректировкаРеализацииПредоплата.Документ КАК Документ.ПоступлениеНаСчет).Статья
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей)
	|			КОНЕЦ
	|	КОНЕЦ КАК Статья,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализацииПредоплата.Ссылка.Контрагент.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА КорректировкаРеализацииПредоплата.Документ ССЫЛКА Документ.РасходСоСчета
	|						ТОГДА ВЫРАЗИТЬ(КорректировкаРеализацииПредоплата.Документ КАК Документ.РасходСоСчета).Дата
	|					КОГДА КорректировкаРеализацииПредоплата.Документ ССЫЛКА Документ.ПоступлениеВКассу
	|						ТОГДА ВЫРАЗИТЬ(КорректировкаРеализацииПредоплата.Документ КАК Документ.ПоступлениеВКассу).Дата
	|					КОГДА КорректировкаРеализацииПредоплата.Документ ССЫЛКА Документ.РасходИзКассы
	|						ТОГДА ВЫРАЗИТЬ(КорректировкаРеализацииПредоплата.Документ КАК Документ.РасходИзКассы).Дата
	|					КОГДА КорректировкаРеализацииПредоплата.Документ ССЫЛКА Документ.ПоступлениеНаСчет
	|						ТОГДА ВЫРАЗИТЬ(КорректировкаРеализацииПредоплата.Документ КАК Документ.ПоступлениеНаСчет).Дата
	|					КОГДА КорректировкаРеализацииПредоплата.Документ ССЫЛКА Документ.Взаимозачет
	|						ТОГДА ВЫРАЗИТЬ(КорректировкаРеализацииПредоплата.Документ КАК Документ.Взаимозачет).Дата
	|				КОНЕЦ
	|		ИНАЧЕ КорректировкаРеализацииПредоплата.Ссылка.Дата
	|	КОНЕЦ КАК ДокументДата,
	|	СУММА(ВЫРАЗИТЬ(ВЫБОР
	|				КОГДА НЕ КорректировкаРеализацииПредоплата.ЕстьВДокументеРеализации
	|					ТОГДА КорректировкаРеализацииПредоплата.СуммаРасчетов
	|				ИНАЧЕ КорректировкаРеализацииПредоплата.СуммаРасчетов - КорректировкаРеализацииПредоплата.СуммаРасчетовДоИзменения
	|			КОНЕЦ * КурсыВалютДокументаСрезПоследних.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютДокументаСрезПоследних.Кратность) КАК ЧИСЛО(15, 2))) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ КорректировкаРеализацииПредоплата.ЕстьВДокументеРеализации
	|				ТОГДА КорректировкаРеализацииПредоплата.СуммаРасчетов
	|			ИНАЧЕ КорректировкаРеализацииПредоплата.СуммаРасчетов - КорректировкаРеализацииПредоплата.СуммаРасчетовДоИзменения
	|		КОНЕЦ) КАК СуммаВал,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(КорректировкаРеализацииПредоплата.Документ) = ТИП(Документ.ОперацияПоПлатежнымКартам)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоЭквайринговаяОперация,
	|	КорректировкаРеализацииПредоплата.Документ.ЭквайринговыйТерминал КАК ЭквайринговыйТерминал,
	|	НАЧАЛОПЕРИОДА(КорректировкаРеализацииПредоплата.Документ.Дата, ДЕНЬ) КАК ДатаПлатежа,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(&ДокументРеализации) = ТИП(Документ.ЗаказПокупателя)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоЗаказНаряд,
	|	КорректировкаРеализацииПредоплата.Курс КАК Курс,
	|	КорректировкаРеализацииПредоплата.Кратность КАК Кратность,
	|	КорректировкаРеализацииПредоплата.Ссылка.Договор.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	СУММА(ВЫРАЗИТЬ(КорректировкаРеализацииПредоплата.СуммаРасчетов * КорректировкаРеализацииПредоплата.Курс / КорректировкаРеализацииПредоплата.Кратность КАК ЧИСЛО(15, 2))) КАК СуммаРубПоКурсуАванса,
	|	ЛОЖЬ КАК ОплатаСертификатом,
	|	"""" КАК НомерСертификата
	|ПОМЕСТИТЬ ВременнаяТаблицаПредоплата
	|ИЗ
	|	Документ.КорректировкаРеализации.Предоплата КАК КорректировкаРеализацииПредоплата
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютДокументаСрезПоследних
	|		ПО (КурсыВалютДокументаСрезПоследних.Валюта = &ВалютаНакладной)
	|ГДЕ
	|	КорректировкаРеализацииПредоплата.Ссылка = &Ссылка
	|	И КорректировкаРеализацииПредоплата.Ссылка.ОтражатьВУчете
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииПредоплата.Ссылка,
	|	КорректировкаРеализацииПредоплата.Документ,
	|	КорректировкаРеализацииПредоплата.Ссылка.Дата,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент,
	|	КорректировкаРеализацииПредоплата.Ссылка.Договор,
	|	КорректировкаРеализацииПредоплата.Ссылка.Договор.ВалютаРасчетов,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.СчетУчетаАвансовПокупателя,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.СчетУчетаАвансовПоставщику,
	|	КорректировкаРеализацииПредоплата.Ссылка.ВидОперации,
	|	КорректировкаРеализацииПредоплата.Ссылка.ДокументОснование,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.ВестиРасчетыПоДоговорам,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.ВестиРасчетыПоДокументам,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.ВестиРасчетыПоЗаказам,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам,
	|	КорректировкаРеализацииПредоплата.Документ.ЭквайринговыйТерминал,
	|	НАЧАЛОПЕРИОДА(КорректировкаРеализацииПредоплата.Документ.Дата, ДЕНЬ),
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(&ДокументРеализации) = ТИП(Документ.ЗаказПокупателя)
	|			ТОГДА &ДокументРеализации
	|		ИНАЧЕ КорректировкаРеализацииПредоплата.Заказ
	|	КОНЕЦ,
	|	КорректировкаРеализацииПредоплата.Курс,
	|	КорректировкаРеализацииПредоплата.Кратность,
	|	КорректировкаРеализацииПредоплата.Ссылка.Договор.РасчетыВУсловныхЕдиницах
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(КорректировкаРеализацииПредоплата.НомерСтроки) КАК НомерСтроки,
	|	КорректировкаРеализацииПредоплата.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент КАК Контрагент,
	|	КорректировкаРеализацииПредоплата.Ссылка.Договор КАК Договор,
	|	КорректировкаРеализацииПредоплата.Ссылка.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(&ДокументРеализации) = ТИП(Документ.ЗаказПокупателя)
	|			ТОГДА &ДокументРеализации
	|		ИНАЧЕ КорректировкаРеализацииПредоплата.Заказ
	|	КОНЕЦ КАК Заказ,
	|	КорректировкаРеализацииПредоплата.Ссылка.ДокументОснование КАК ДокументОснование,
	|	КорректировкаРеализацииПредоплата.Документ КАК Документ,
	|	СУММА(ВЫРАЗИТЬ(КорректировкаРеализацииПредоплата.СуммаПлатежа * КурсыВалютДокументаСрезПоследних.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютДокументаСрезПоследних.Кратность) КАК ЧИСЛО(15, 2))) КАК Сумма,
	|	СУММА(КорректировкаРеализацииПредоплата.СуммаРасчетов) КАК СуммаВал,
	|	КорректировкаРеализацииПредоплата.Ссылка.Договор.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	СУММА(ВЫРАЗИТЬ(КорректировкаРеализацииПредоплата.СуммаРасчетов * КорректировкаРеализацииПредоплата.Курс / КорректировкаРеализацииПредоплата.Кратность КАК ЧИСЛО(15, 2))) КАК СуммаРубПоКурсуАванса
	|ПОМЕСТИТЬ ВременнаяТаблицаПредоплатаРег
	|ИЗ
	|	Документ.КорректировкаРеализации.Предоплата КАК КорректировкаРеализацииПредоплата
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютДокументаСрезПоследних
	|		ПО (КурсыВалютДокументаСрезПоследних.Валюта = &ВалютаНакладной)
	|ГДЕ
	|	КорректировкаРеализацииПредоплата.Ссылка = &Ссылка
	|	И КорректировкаРеализацииПредоплата.Ссылка.ОтражатьВУчете
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализацииПредоплата.Ссылка,
	|	КорректировкаРеализацииПредоплата.Документ,
	|	КорректировкаРеализацииПредоплата.Ссылка.Дата,
	|	КорректировкаРеализацииПредоплата.Ссылка.Контрагент,
	|	КорректировкаРеализацииПредоплата.Ссылка.Договор,
	|	КорректировкаРеализацииПредоплата.Ссылка.Договор.ВалютаРасчетов,
	|	КорректировкаРеализацииПредоплата.Ссылка.ДокументОснование,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(&ДокументРеализации) = ТИП(Документ.ЗаказПокупателя)
	|			ТОГДА &ДокументРеализации
	|		ИНАЧЕ КорректировкаРеализацииПредоплата.Заказ
	|	КОНЕЦ,
	|	КорректировкаРеализацииПредоплата.Ссылка.Договор.РасчетыВУсловныхЕдиницах
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияПрослеживаемостиДокументов.ВидДвижения КАК ВидДвижения,
	|	МАКСИМУМ(СведенияПрослеживаемостиДокументов.НомерСтроки) КАК НомерСтроки,
	|	СведенияПрослеживаемостиДокументов.РНПТ КАК РНПТ,
	|	СУММА(СведенияПрослеживаемостиДокументов.Количество) КАК Количество,
	|	СУММА(СведенияПрослеживаемостиДокументов.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	СведенияПрослеживаемостиДокументов.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВременнаяТаблицаСведенияПрослеживаемости
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|		СведенияПрослеживаемости.НомерСтроки КАК НомерСтроки,
	|		СведенияПрослеживаемости.РНПТ КАК РНПТ,
	|		СведенияПрослеживаемости.Количество КАК Количество,
	|		СведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|		СведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки
	|	ИЗ
	|		Документ.КорректировкаРеализации.СведенияПрослеживаемости КАК СведенияПрослеживаемости
	|	ГДЕ
	|		СведенияПрослеживаемости.Ссылка = &Ссылка
	|		И &ВестиУчетПрослеживаемыхТоваров
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|		0,
	|		СведенияПрослеживаемости.РНПТ,
	|		-СведенияПрослеживаемости.Количество,
	|		-СведенияПрослеживаемости.КоличествоПрослеживаемости,
	|		СведенияПрослеживаемости.ИдентификаторСтроки
	|	ИЗ
	|		Документ.РасходнаяНакладная.СведенияПрослеживаемости КАК СведенияПрослеживаемости
	|	ГДЕ
	|		СведенияПрослеживаемости.Ссылка = &ИсправляемыйДокумент
	|		И &ВестиУчетПрослеживаемыхТоваров
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|		0,
	|		СведенияПрослеживаемости.РНПТ,
	|		-СведенияПрослеживаемости.Количество,
	|		-СведенияПрослеживаемости.КоличествоПрослеживаемости,
	|		СведенияПрослеживаемости.ИдентификаторСтроки
	|	ИЗ
	|		Документ.КорректировкаРеализации.СведенияПрослеживаемости КАК СведенияПрослеживаемости
	|	ГДЕ
	|		СведенияПрослеживаемости.Ссылка = &ИсправляемыйДокумент
	|		И &ВестиУчетПрослеживаемыхТоваров) КАК СведенияПрослеживаемостиДокументов
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияПрослеживаемостиДокументов.ИдентификаторСтроки,
	|	СведенияПрослеживаемостиДокументов.РНПТ,
	|	СведенияПрослеживаемостиДокументов.ВидДвижения
	|
	|ИМЕЮЩИЕ
	|	(СУММА(СведенияПрослеживаемостиДокументов.Количество) <> 0
	|		ИЛИ СУММА(СведенияПрослеживаемостиДокументов.КоличествоПрослеживаемости) <> 0)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Выполняет контроль возникновения отрицательных остатков.
//
Процедура ВыполнитьКонтроль(ДокументСсылкаКорректировкаРеализации, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт
	
	Если ПроведениеДокументовУНФ.КонтрольОстатковВыключен() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Если временные таблицы "ДвиженияЗапасыИзменение", "ДвиженияЗапасыНаСкладахИзменение",
	// "ДвиженияЗапасыКРасходуСоСкладовИзменение", "ДвиженияЗапасыПринятыеИзменение", "ДвиженияРазмещениеЗаказовИзменение"
	// содержат записи, необходимо выполнить контроль реализации товаров.
	
	Если СтруктураВременныеТаблицы.ДвиженияЗапасыИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗапасыНаСкладахИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗапасыКРасходуСоСкладовИзменение 
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗапасыПринятыеИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗаказыПокупателейИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияРасчетыСПокупателямиИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗапасыВРазрезеГТДИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияПрослеживаемыеТоварыИзменение Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияЗапасыНаСкладахИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗапасыНаСкладахИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.Партия КАК ПартияПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.Ячейка КАК ЯчейкаПредставление,
		|	ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
		|	ЗапасыНаСкладахОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыНаСкладахИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыНаСкладах,
		|	ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыНаСкладах
		|ИЗ
		|	ДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыНаСкладах.Остатки(&МоментКонтроля, ) КАК ЗапасыНаСкладахОстатки
		|		ПО ДвиженияЗапасыНаСкладахИзменение.Организация = ЗапасыНаСкладахОстатки.Организация
		|			И ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница = ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница
		|			И ДвиженияЗапасыНаСкладахИзменение.Номенклатура = ЗапасыНаСкладахОстатки.Номенклатура
		|			И ДвиженияЗапасыНаСкладахИзменение.Характеристика = ЗапасыНаСкладахОстатки.Характеристика
		|			И ДвиженияЗапасыНаСкладахИзменение.Партия = ЗапасыНаСкладахОстатки.Партия
		|			И ДвиженияЗапасыНаСкладахИзменение.Ячейка = ЗапасыНаСкладахОстатки.Ячейка
		|			И (ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗапасыИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияЗапасыИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаПредставление,
		|	ДвиженияЗапасыИзменение.СчетУчета КАК СчетУчетаПредставление,
		|	ДвиженияЗапасыИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияЗапасыИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ДвиженияЗапасыИзменение.Партия КАК ПартияПредставление,
		|	ДвиженияЗапасыИзменение.ЗаказПокупателя КАК ЗаказПокупателяПредставление,
		|	ЗапасыОстатки.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
		|	ЗапасыОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасы,
		|	ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасы,
		|	ЕСТЬNULL(ЗапасыОстатки.СуммаОстаток, 0) КАК СуммаОстатокЗапасы
		|ИЗ
		|	ДвиженияЗапасыИзменение КАК ДвиженияЗапасыИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.Запасы.Остатки(&МоментКонтроля, ) КАК ЗапасыОстатки
		|		ПО ДвиженияЗапасыИзменение.Организация = ЗапасыОстатки.Организация
		|			И ДвиженияЗапасыИзменение.СтруктурнаяЕдиница = ЗапасыОстатки.СтруктурнаяЕдиница
		|			И ДвиженияЗапасыИзменение.СчетУчета = ЗапасыОстатки.СчетУчета
		|			И ДвиженияЗапасыИзменение.Номенклатура = ЗапасыОстатки.Номенклатура
		|			И ДвиженияЗапасыИзменение.Характеристика = ЗапасыОстатки.Характеристика
		|			И ДвиженияЗапасыИзменение.Партия = ЗапасыОстатки.Партия
		|			И ДвиженияЗапасыИзменение.ЗаказПокупателя = ЗапасыОстатки.ЗаказПокупателя
		|			И (ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыПринятыеИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗапасыПринятыеИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияЗапасыПринятыеИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияЗапасыПринятыеИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ДвиженияЗапасыПринятыеИзменение.Партия КАК ПартияПредставление,
		|	ДвиженияЗапасыПринятыеИзменение.Контрагент КАК КонтрагентПредставление,
		|	ДвиженияЗапасыПринятыеИзменение.Договор КАК ДоговорПредставление,
		|	ДвиженияЗапасыПринятыеИзменение.Заказ КАК ЗаказПредставление,
		|	ДвиженияЗапасыПринятыеИзменение.ТипПриемаПередачи КАК ТипПриемаПередачиПредставление,
		|	ЗапасыПринятыеОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыПринятыеИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыПринятыеОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыПринятые,
		|	ЕСТЬNULL(ЗапасыПринятыеОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыПринятые,
		|	ЕСТЬNULL(ДвиженияЗапасыПринятыеИзменение.СуммаРасчетовИзменение, 0) + ЕСТЬNULL(ЗапасыПринятыеОстатки.СуммаРасчетовОстаток, 0) КАК СуммаРасчетовЗапасыПринятые,
		|	ЕСТЬNULL(ЗапасыПринятыеОстатки.СуммаРасчетовОстаток, 0) КАК СуммаРасчетовОстатокЗапасыПринятые
		|ИЗ
		|	ДвиженияЗапасыПринятыеИзменение КАК ДвиженияЗапасыПринятыеИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыИАгентскиеУслугиПринятые.Остатки(&МоментКонтроля, ) КАК ЗапасыПринятыеОстатки
		|		ПО ДвиженияЗапасыПринятыеИзменение.Организация = ЗапасыПринятыеОстатки.Организация
		|			И ДвиженияЗапасыПринятыеИзменение.Номенклатура = ЗапасыПринятыеОстатки.Номенклатура
		|			И ДвиженияЗапасыПринятыеИзменение.Характеристика = ЗапасыПринятыеОстатки.Характеристика
		|			И ДвиженияЗапасыПринятыеИзменение.Партия = ЗапасыПринятыеОстатки.Партия
		|			И ДвиженияЗапасыПринятыеИзменение.Контрагент = ЗапасыПринятыеОстатки.Контрагент
		|			И ДвиженияЗапасыПринятыеИзменение.Договор = ЗапасыПринятыеОстатки.Договор
		|			И ДвиженияЗапасыПринятыеИзменение.Заказ = ЗапасыПринятыеОстатки.Заказ
		|			И ДвиженияЗапасыПринятыеИзменение.ТипПриемаПередачи = ЗапасыПринятыеОстатки.ТипПриемаПередачи
		|			И (ЕСТЬNULL(ЗапасыПринятыеОстатки.КоличествоОстаток, 0) < 0
		|				ИЛИ ЕСТЬNULL(ЗапасыПринятыеОстатки.СуммаРасчетовОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗаказыПокупателейИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗаказыПокупателейИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияЗаказыПокупателейИзменение.Склад КАК СкладПредставление,
		|	ДвиженияЗаказыПокупателейИзменение.ЗаказПокупателя КАК ЗаказПредставление,
		|	ДвиженияЗаказыПокупателейИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияЗаказыПокупателейИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ЗаказыПокупателейОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗаказыПокупателейИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗаказыПокупателейОстатки.КоличествоОстаток, 0) КАК ОстатокЗаказыПокупателей,
		|	ЕСТЬNULL(ЗаказыПокупателейОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗаказыПокупателей
		|ИЗ
		|	ДвиженияЗаказыПокупателейИзменение КАК ДвиженияЗаказыПокупателейИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПокупателей.Остатки(&МоментКонтроля, ) КАК ЗаказыПокупателейОстатки
		|		ПО ДвиженияЗаказыПокупателейИзменение.Организация = ЗаказыПокупателейОстатки.Организация
		|			И ДвиженияЗаказыПокупателейИзменение.Склад = ЗаказыПокупателейОстатки.Склад
		|			И ДвиженияЗаказыПокупателейИзменение.ЗаказПокупателя = ЗаказыПокупателейОстатки.ЗаказПокупателя
		|			И ДвиженияЗаказыПокупателейИзменение.Номенклатура = ЗаказыПокупателейОстатки.Номенклатура
		|			И ДвиженияЗаказыПокупателейИзменение.Характеристика = ЗаказыПокупателейОстатки.Характеристика
		|			И (ЕСТЬNULL(ЗаказыПокупателейОстатки.КоличествоОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияРасчетыСПокупателямиИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияРасчетыСПокупателямиИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияРасчетыСПокупателямиИзменение.Контрагент КАК КонтрагентПредставление,
		|	ДвиженияРасчетыСПокупателямиИзменение.Договор КАК ДоговорПредставление,
		|	ДвиженияРасчетыСПокупателямиИзменение.Договор.ВалютаРасчетов КАК ВалютаПредставление,
		|	ДвиженияРасчетыСПокупателямиИзменение.Документ КАК ДокументПредставление,
		|	ДвиженияРасчетыСПокупателямиИзменение.Заказ КАК ЗаказПредставление,
		|	ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов КАК ТипРасчетовПредставление,
		|	ЛОЖЬ КАК ДвиженияДенежныхДокументов,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаВалИзменение КАК СуммаВалИзменение,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаВалПриЗаписи - ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) КАК СуммаПолученныхАвансов,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаВалИзменение + ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) КАК СуммаНепогашеннойЗадолженности,
		|	ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
		|	ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов КАК ТипРасчетов
		|ИЗ
		|	ДвиженияРасчетыСПокупателямиИзменение КАК ДвиженияРасчетыСПокупателямиИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПокупателями.Остатки(&МоментКонтроля, ) КАК РасчетыСПокупателямиОстатки
		|		ПО ДвиженияРасчетыСПокупателямиИзменение.Организация = РасчетыСПокупателямиОстатки.Организация
		|			И ДвиженияРасчетыСПокупателямиИзменение.Контрагент = РасчетыСПокупателямиОстатки.Контрагент
		|			И ДвиженияРасчетыСПокупателямиИзменение.Договор = РасчетыСПокупателямиОстатки.Договор
		|			И ДвиженияРасчетыСПокупателямиИзменение.Документ = РасчетыСПокупателямиОстатки.Документ
		|			И ДвиженияРасчетыСПокупателямиИзменение.Заказ = РасчетыСПокупателямиОстатки.Заказ
		|			И ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов = РасчетыСПокупателямиОстатки.ТипРасчетов
		|			И (ВЫБОР
		|				КОГДА ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
		|					ТОГДА ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) > 0
		|				ИНАЧЕ ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) < 0
		|			КОНЕЦ)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыВРазрезеГТДИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД) КАК НомерГТДПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Партия) КАК ПартияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения) КАК СтранаПроисхожденияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗапасыВРазрезеГТДОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыВРазрезеГТДИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыВРазрезеГТД,
		|	ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыВРазрезеГТД
		|ИЗ
		|	ДвиженияЗапасыВРазрезеГТДИзменение КАК ДвиженияЗапасыВРазрезеГТДИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыВРазрезеГТД.Остатки(
		|				&МоментКонтроля,
		|				(Организация, НомерГТД, Номенклатура, Характеристика, Партия, СтранаПроисхождения) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД КАК НомерГТД,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения КАК СтранаПроисхождения
		|					ИЗ
		|						ДвиженияЗапасыВРазрезеГТДИзменение КАК ДвиженияЗапасыВРазрезеГТДИзменение)) КАК ЗапасыВРазрезеГТДОстатки
		|		ПО ДвиженияЗапасыВРазрезеГТДИзменение.Организация = ЗапасыВРазрезеГТДОстатки.Организация
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД = ЗапасыВРазрезеГТДОстатки.НомерГТД
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура = ЗапасыВРазрезеГТДОстатки.Номенклатура
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика = ЗапасыВРазрезеГТДОстатки.Характеристика
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Партия = ЗапасыВРазрезеГТДОстатки.Партия
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения = ЗапасыВРазрезеГТДОстатки.СтранаПроисхождения
		|ГДЕ
		|	ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияПрослеживаемыеТоварыИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияПрослеживаемыеТоварыИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияПрослеживаемыеТоварыИзменение.РНПТ КАК РНПТПредставление,
		|	ДвиженияПрослеживаемыеТоварыИзменение.СтранаПроисхождения КАК СтранаПроисхожденияПредставление,
		|	ДвиженияПрослеживаемыеТоварыИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияПрослеживаемыеТоварыИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ДвиженияПрослеживаемыеТоварыИзменение.Партия КАК ПартияПредставление,
		|	ДвиженияПрослеживаемыеТоварыИзменение.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ДвиженияПрослеживаемыеТоварыИзменение.Номенклатура.ТоварнаяНоменклатураВЭД.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПрослеживаемостиПредставление,
		|	ЕСТЬNULL(ДвиженияПрослеживаемыеТоварыИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.КоличествоОстаток, 0) КАК ОстатокКоличество,
		|	ЕСТЬNULL(ДвиженияПрослеживаемыеТоварыИзменение.КоличествоПрослеживаемостиИзменение, 0) + ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.КоличествоПрослеживаемостиОстаток, 0) КАК ОстатокКоличествоПрослеживаемости,
		|	ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.КоличествоОстаток, 0) КАК ОстатокКоличествоТекущий,
		|	ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.КоличествоПрослеживаемостиОстаток, 0) КАК ОстатокКоличествоПрослеживаемостиТекущий
		|ИЗ
		|	ДвиженияПрослеживаемыеТоварыИзменение КАК ДвиженияПрослеживаемыеТоварыИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрослеживаемыеТовары.Остатки(&МоментКонтроля, ) КАК ПрослеживаемыеТоварыОстатки
		|		ПО ДвиженияПрослеживаемыеТоварыИзменение.Организация = ПрослеживаемыеТоварыОстатки.Организация
		|			И ДвиженияПрослеживаемыеТоварыИзменение.РНПТ = ПрослеживаемыеТоварыОстатки.РНПТ
		|			И ДвиженияПрослеживаемыеТоварыИзменение.СтранаПроисхождения = ПрослеживаемыеТоварыОстатки.СтранаПроисхождения
		|			И ДвиженияПрослеживаемыеТоварыИзменение.Номенклатура = ПрослеживаемыеТоварыОстатки.Номенклатура
		|			И ДвиженияПрослеживаемыеТоварыИзменение.Характеристика = ПрослеживаемыеТоварыОстатки.Характеристика
		|			И ДвиженияПрослеживаемыеТоварыИзменение.Партия = ПрослеживаемыеТоварыОстатки.Партия
		|			И (ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.КоличествоОстаток, 0) < 0
		|				ИЛИ ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.КоличествоПрослеживаемостиОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки");
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("МоментКонтроля", ДополнительныеСвойства.ДляПроведения.МоментКонтроля);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		Если НЕ МассивРезультатов[0].Пустой()
			ИЛИ НЕ МассивРезультатов[1].Пустой()
			ИЛИ НЕ МассивРезультатов[2].Пустой()
			ИЛИ НЕ МассивРезультатов[3].Пустой()
			ИЛИ НЕ МассивРезультатов[4].Пустой()
			ИЛИ НЕ МассивРезультатов[5].Пустой()
			ИЛИ НЕ МассивРезультатов[6].Пустой() Тогда
			
			ДокументОбъектРасходнаяНакладная = ДокументСсылкаКорректировкаРеализации.ПолучитьОбъект()
			
		КонецЕсли;
		
		// Отрицательный остаток запасов на складе.
		Если НЕ МассивРезультатов[0].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[0].Выбрать();
			КонтрольОстатковУНФ.ЗапасыНаСкладахСписком(ДокументОбъектРасходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток учета запасов.
		Если НЕ МассивРезультатов[1].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[1].Выбрать();
			КонтрольОстатковУНФ.ЗапасыСписком(ДокументОбъектРасходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток запасов принятых.
		Если НЕ МассивРезультатов[2].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[2].Выбрать();
			КонтрольОстатковУНФ.ЗапасыПринятые(ДокументОбъектРасходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по заказу покупателя.
		Если НЕ МассивРезультатов[3].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[3].Выбрать();
			КонтрольОстатковУНФ.ЗаказыПокупателей(ДокументОбъектРасходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по расчетам с покупателями.
		Если НЕ МассивРезультатов[4].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[4].Выбрать();
			КонтрольОстатковУНФ.РасчетыСПокупателями(ДокументОбъектРасходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по остаткам запасов в разрезе номеров ГТД.
		Если Константы.КонтролироватьОстаткиПоНомерамГТД.Получить()
			И НЕ МассивРезультатов[5].Пустой() Тогда
			
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[5].Выбрать();
			КонтрольОстатковУНФ.ЗапасыВРазрезеГТД(ДокументОбъектРасходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
			
		КонецЕсли;
		
		// Отрицательный остаток учета прослеживаемых товаров.
		Если Константы.ВестиУчетПрослеживаемыхТоваров.Получить() Тогда
			
			Если Не МассивРезультатов[6].Пустой() Тогда
				ВыборкаИзРезультатаЗапроса = МассивРезультатов[6].Выбрать();
				УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструПрослеживаемыеТовары(
					ДокументОбъектРасходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
			КонецЕсли;
			
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтроль()

Процедура СформироватьТаблицаЗапасыВРазрезеГТД(СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыНаСкладах.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗапасыНаСкладах.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия КАК Партия,
	|	ТаблицаЗапасыНаСкладах.НомерГТД КАК НомерГТД,
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(ТаблицаЗапасыНаСкладах.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаИзмененияЗапасов КАК ТаблицаЗапасыНаСкладах,
	|	Константа.ФункциональнаяОпцияУчетГТД КАК ФункциональнаяОпцияУчетГТД
	|ГДЕ
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|	И ТаблицаЗапасыНаСкладах.СтранаПроисхождения <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|	И ТаблицаЗапасыНаСкладах.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|	И ФункциональнаяОпцияУчетГТД.Значение
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыНаСкладах.Ссылка.Дата,
	|	ТаблицаЗапасыНаСкладах.Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия,
	|	ТаблицаЗапасыНаСкладах.НомерГТД,
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения";
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыВРазрезеГТД", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

Функция ПолучитьПоследнийКорректирующийДокумент(Ссылка, ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ЭтотДокумент", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КорректировкаРеализации.Ссылка
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	КорректировкаРеализации.ИсправляемыйДокументРеализации = &Ссылка
	|	И КорректировкаРеализации.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)
	|	И КорректировкаРеализации.Ссылка <> &ЭтотДокумент
	|	И КорректировкаРеализации.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	КорректировкаРеализации.Дата УБЫВ";
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.Ссылка;
	Иначе
		Возврат ДокументСсылка;
	КонецЕсли;
		
КонецФункции

Функция СформироватьПараметрыИсправленияКорректировочногоДокумента(ВидОперации, ДокументРеализации, ИсправляемыйДокументРеализации) Экспорт
	
	СтруктураПараметров = Новый Структура;
	
	Если НЕ ЗначениеЗаполнено(ДокументРеализации) ИЛИ НЕ ЗначениеЗаполнено(ИсправляемыйДокументРеализации) Тогда
		
		Если ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки Тогда
			СтруктураПараметров.Вставить("НомерИсправления", 1);
		Иначе
			СтруктураПараметров.Вставить("НомерИсправления", Неопределено);
		КонецЕсли;

		Возврат СтруктураПараметров;
		
	КонецЕсли;                                                                                            
	
	Если НЕ ЗначениеЗаполнено(ВидОперации)  
		ИЛИ ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
		
		Если ТипЗнч(ДокументРеализации) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда 
			
			РеквизитыИсправляемогоДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ИсправляемыйДокументРеализации, "Номер, Дата");
			РеквизитыДокументаРеализации 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументРеализации, "Номер, Дата, НомерИсправления");
			
			СтруктураПараметров.Вставить("НомерИсходногоДокумента", 			
				ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыИсправляемогоДокумента.Номер, Истина, Ложь));
			СтруктураПараметров.Вставить("ДатаИсходногоДокумента", 				РеквизитыИсправляемогоДокумента.Дата);
			СтруктураПараметров.Вставить("НомерИсправленияИсходногоДокумента", 	РеквизитыДокументаРеализации.НомерИсправления);
			СтруктураПараметров.Вставить("ДатаИсправленияИсходногоДокумента", 	РеквизитыДокументаРеализации.Дата);
			
			СтруктураПараметров.Вставить("НомерИсправления", 								Неопределено);
			СтруктураПараметров.Вставить("НомерИсправляемогоКорректировочногоДокумента", 	Неопределено);
			СтруктураПараметров.Вставить("ДатаИсправляемогоКорректировочногоДокумента", 	Неопределено);
			
		Иначе
			
			РеквизитыДокументаРеализации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументРеализации, "Номер, Дата");
			
			СтруктураПараметров.Вставить("НомерИсходногоДокумента", 	
				ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыДокументаРеализации.Номер, Истина, Ложь));
			СтруктураПараметров.Вставить("ДатаИсходногоДокумента", РеквизитыДокументаРеализации.Дата);
			
			СтруктураПараметров.Вставить("НомерИсправления", 								Неопределено);
			СтруктураПараметров.Вставить("НомерИсправляемогоКорректировочногоДокумента", 	Неопределено);
			СтруктураПараметров.Вставить("ДатаИсправляемогоКорректировочногоДокумента", 	Неопределено);
			СтруктураПараметров.Вставить("НомерИсправленияИсходногоДокумента", 				Неопределено);
			СтруктураПараметров.Вставить("ДатаИсправленияИсходногоДокумента", 				Неопределено);
			
		КонецЕсли;
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки Тогда
		
		Если ТипЗнч(ДокументРеализации) = Тип("ДокументСсылка.КорректировкаРеализации")
			И ТипЗнч(ИсправляемыйДокументРеализации) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда 
			
			РеквизитыДокументаРеализации 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументРеализации, "Номер, Дата, НомерИсправления");
			РеквизитыИсправляемогоДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ИсправляемыйДокументРеализации, "Номер, Дата, 
				|НомерИсходногоДокумента, ДатаИсходногоДокумента, НомерИсправленияИсходногоДокумента, ДатаИсправленияИсходногоДокумента");
			
			СтруктураПараметров.Вставить("НомерИсправляемогоКорректировочногоДокумента", 
				ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыИсправляемогоДокумента.Номер, Истина, Ложь));
			СтруктураПараметров.Вставить("ДатаИсправляемогоКорректировочногоДокумента",  РеквизитыИсправляемогоДокумента.Дата);
			
			СтруктураПараметров.Вставить("НомерИсходногоДокумента", 			РеквизитыИсправляемогоДокумента.НомерИсходногоДокумента);
			СтруктураПараметров.Вставить("ДатаИсходногоДокумента", 				РеквизитыИсправляемогоДокумента.ДатаИсходногоДокумента);
			СтруктураПараметров.Вставить("НомерИсправленияИсходногоДокумента", 	РеквизитыИсправляемогоДокумента.НомерИсправленияИсходногоДокумента);
			СтруктураПараметров.Вставить("ДатаИсправленияИсходногоДокумента", 	РеквизитыИсправляемогоДокумента.ДатаИсправленияИсходногоДокумента);
			
			СтруктураПараметров.Вставить("НомерИсправления", 					РеквизитыДокументаРеализации.НомерИсправления + 1);
			
		ИначеЕсли ТипЗнч(ДокументРеализации) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда 
			
			РеквизитыДокументаРеализации 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументРеализации, "Номер, Дата, НомерИсправления, 
				|НомерИсходногоДокумента, ДатаИсходногоДокумента, НомерИсправленияИсходногоДокумента, ДатаИсправленияИсходногоДокумента");
			
			СтруктураПараметров.Вставить("НомерИсправляемогоКорректировочногоДокумента", 	
				ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыДокументаРеализации.Номер, Истина, Ложь));
			СтруктураПараметров.Вставить("ДатаИсправляемогоКорректировочногоДокумента", РеквизитыДокументаРеализации.Дата);
			
			СтруктураПараметров.Вставить("НомерИсходногоДокумента", 			РеквизитыДокументаРеализации.НомерИсходногоДокумента);
			СтруктураПараметров.Вставить("ДатаИсходногоДокумента", 				РеквизитыДокументаРеализации.ДатаИсходногоДокумента);
			СтруктураПараметров.Вставить("НомерИсправленияИсходногоДокумента", 	РеквизитыДокументаРеализации.НомерИсправленияИсходногоДокумента);
			СтруктураПараметров.Вставить("ДатаИсправленияИсходногоДокумента", 	РеквизитыДокументаРеализации.ДатаИсправленияИсходногоДокумента);
			
			СтруктураПараметров.Вставить("НомерИсправления", 					РеквизитыДокументаРеализации.НомерИсправления + 1);
			
		Иначе
			
			РеквизитыДокументаРеализации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументРеализации, "Номер, Дата");
			
			СтруктураПараметров.Вставить("НомерИсходногоДокумента", 	
				ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыДокументаРеализации.Номер, Истина, Ложь));
			СтруктураПараметров.Вставить("ДатаИсходногоДокумента", 							РеквизитыДокументаРеализации.Дата);
			
			СтруктураПараметров.Вставить("НомерИсправления", 								1);
			СтруктураПараметров.Вставить("НомерИсправленияИсходногоДокумента", 				Неопределено);
			СтруктураПараметров.Вставить("ДатаИсправленияИсходногоДокумента", 				Неопределено);
			СтруктураПараметров.Вставить("НомерИсправляемогоКорректировочногоДокумента", 	Неопределено);
			СтруктураПараметров.Вставить("ДатаИсправляемогоКорректировочногоДокумента", 	Неопределено);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураПараметров;
	
КонецФункции

// Функция находит и возвращает документ, являющийся отправной точкой исправлений (либо РТУ либо корректировку РТУ)
// либо первоначальный документ РТУ при передаче истины в параметр Исходный
Функция ПолучитьИсправляемыйДокументРеализации(ДокРеализации, Исходный = Ложь) Экспорт
	
	Если ЗначениеЗаполнено(ДокРеализации) 
		И ТипЗнч(ДокРеализации) = Тип("ДокументСсылка.КорректировкаРеализации")
		И (ДокРеализации.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки ИЛИ Исходный)
		И ДокРеализации.ДокументОснование <> ДокРеализации Тогда
		
		Возврат ПолучитьИсправляемыйДокументРеализации(ДокРеализации.ДокументОснование, Исходный);
		
	Иначе
		Возврат ДокРеализации;
	КонецЕсли;
	
КонецФункции

#КонецОбласти 

#Область ИнтерфейсПечати

Функция ДанныеДокументовРегУчет(МассивОбъектов, ИспользоватьФаксимиле, ПечатнаяФормаТолькоВРублях = Истина, Ошибки = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ТЧЗапасы", Перечисления.ТабличныеЧастиДокументов.Запасы);
	Запрос.УстановитьПараметр("НациональнаяВалюта", Константы.НациональнаяВалюта.Получить());
	Запрос.УстановитьПараметр("ИспользоватьФаксимиле", ИспользоватьФаксимиле);
	Запрос.УстановитьПараметр("ПечатнаяФормаТолькоВРублях", ПечатнаяФормаТолькоВРублях);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка КАК Ссылка,
	|	КорректировкаРеализации.Дата КАК ДатаДокумента,
	|	ВЫРАЗИТЬ(КорректировкаРеализации.Номер КАК СТРОКА(12)) КАК Номер,
	|	КорректировкаРеализации.НомерИсправления КАК НомерИсправления,
	|	КорректировкаРеализации.Дата КАК ДатаИсправления,
	|	КорректировкаРеализации.ДокументОснование КАК ДокументОснование,
	|	ЗНАЧЕНИЕ(Документ.СчетФактура.ПустаяСсылка) КАК ОригинальныйСчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСчетФактура.Продажа) КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Организация.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ КорректировкаРеализации.Организация
	|	КОНЕЦ КАК Организация,
	|	КорректировкаРеализации.Организация.Префикс КАК Префикс,
	|	КорректировкаРеализации.Организация.ФайлЛоготип КАК Логотип,
	|	КорректировкаРеализации.Организация.ФайлФаксимильнаяПечать КАК ФаксимилеПечати,
	|	НЕОПРЕДЕЛЕНО КАК ФаксимилеПустая,
	|	ВЫБОР
	|		КОГДА &ИспользоватьФаксимиле = ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДаНет.Да)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ДаНет.Нет)
	|	КОНЕЦ КАК ИспользоватьФаксимиле,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Организация.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Организация.ЦифровойИндексОбособленногоПодразделения
	|		КОГДА КорректировкаРеализации.Подразделение.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				И КорректировкаРеализации.Подразделение.ГоловнаяОрганизация = КорректировкаРеализации.Организация
	|			ТОГДА КорректировкаРеализации.Подразделение.ЦифровойИндексОбособленногоПодразделения
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ЦифровойИндексОбособленногоПодразделения,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Организация.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Организация
	|		КОГДА КорректировкаРеализации.Подразделение.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				И КорректировкаРеализации.Подразделение.ГоловнаяОрганизация = КорректировкаРеализации.Организация
	|			ТОГДА КорректировкаРеализации.Подразделение
	|		ИНАЧЕ КорректировкаРеализации.Организация
	|	КОНЕЦ КАК ОбособленноеПодразделениеПоставщика,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Организация.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.НеОблагаетсяНДС)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СтатусУПД,
	|	ЛОЖЬ КАК ЭтоСводныйСчетФактура,
	|	ИСТИНА КАК ЭтоКорректировка,
	|	КорректировкаРеализации.БанковскийСчет КАК БанковскийСчет,
	|	ПРЕДСТАВЛЕНИЕ(КорректировкаРеализации.Подразделение) КАК ПредставлениеПодразделения,
	|	ПРЕДСТАВЛЕНИЕ(КорректировкаРеализации.СтруктурнаяЕдиница) КАК ПредставлениеСкладаСписания,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Грузоотправитель
	|		КОГДА КорректировкаРеализации.Организация.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Организация
	|		КОГДА КорректировкаРеализации.Подразделение.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				И КорректировкаРеализации.Подразделение.ГоловнаяОрганизация = КорректировкаРеализации.Организация
	|			ТОГДА КорректировкаРеализации.Подразделение
	|		ИНАЧЕ КорректировкаРеализации.Организация
	|	КОНЕЦ КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Контрагент.ГоловнойКонтрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И КорректировкаРеализации.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА КорректировкаРеализации.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ КорректировкаРеализации.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Контрагент.ГоловнойКонтрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И КорректировкаРеализации.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА КорректировкаРеализации.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ КорректировкаРеализации.Контрагент
	|	КОНЕЦ КАК Покупатель,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузополучатель.ГоловнойКонтрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И КорректировкаРеализации.Грузополучатель.ГоловнойКонтрагент = КорректировкаРеализации.Контрагент
	|				И КорректировкаРеализации.Грузополучатель.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА КорректировкаРеализации.Грузополучатель
	|		ИНАЧЕ КорректировкаРеализации.Контрагент
	|	КОНЕЦ КАК ОбособленноеПодразделениеПокупателя,
	|	КорректировкаРеализации.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузополучатель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Грузополучатель
	|		ИНАЧЕ КорректировкаРеализации.Контрагент
	|	КОНЕЦ КАК Грузополучатель,
	|	КорректировкаРеализации.АдресДоставки КАК АдресДоставки,
	|	КорректировкаРеализации.КонтактноеЛицоПодписант.Наименование КАК РасшифровкаПодписиКонтрагента,
	|	КорректировкаРеализации.ДоверенностьНомер КАК ДоверенностьНомер,
	|	КорректировкаРеализации.ДоверенностьДата КАК ДоверенностьДата,
	|	КорректировкаРеализации.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	КорректировкаРеализации.ДоверенностьЛицо КАК ДоверенностьЛицо,
	|	КорректировкаРеализации.Договор.Представление КАК Основание,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|			ТОГДА НациональнаяВалюта.Значение
	|		ИНАЧЕ КорректировкаРеализации.ВалютаДокумента
	|	КОНЕЦ КАК ВалютаДокумента,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|			ТОГДА НациональнаяВалюта.Значение.НаименованиеПолное
	|		ИНАЧЕ КорректировкаРеализации.ВалютаДокумента.НаименованиеПолное
	|	КОНЕЦ КАК ВалютаНаименование,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|			ТОГДА НациональнаяВалюта.Значение.Код
	|		ИНАЧЕ КорректировкаРеализации.ВалютаДокумента.Код
	|	КОНЕЦ КАК ВалютаКод,
	|	КорректировкаРеализации.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	КорректировкаРеализации.НДСВключатьВСтоимость КАК НДСВключатьВСтоимость,
	|	КорректировкаРеализации.Курс КАК Курс,
	|	КорректировкаРеализации.Кратность КАК Кратность,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеОснования,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеПечатиСсылка,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеНомер,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеДата,
	|	НЕОПРЕДЕЛЕНО КАК ТранспортнаяНакладнаяНомер,
	|	НЕОПРЕДЕЛЕНО КАК ТранспортнаяНакладнаяДата,
	|	КорректировкаРеализации.ПодписьРуководителя.Должность КАК ДолжностьРуководителя,
	|	КорректировкаРеализации.ПодписьРуководителя.РасшифровкаПодписи КАК РасшифровкаПодписиРуководителя,
	|	КорректировкаРеализации.ПодписьРуководителя.Факсимиле КАК ФаксимилеРуководителя,
	|	КорректировкаРеализации.ПодписьГлавногоБухгалтера.РасшифровкаПодписи КАК РасшифровкаПодписиГлавногоБухгалтера,
	|	КорректировкаРеализации.ПодписьГлавногоБухгалтера.Факсимиле КАК ФаксимилеГлавногоБухгалтера,
	|	КорректировкаРеализации.ПодписьКладовщика.Должность КАК ДолжностьКладовщика,
	|	КорректировкаРеализации.ПодписьКладовщика.РасшифровкаПодписи КАК РасшифровкаПодписиКладовщика,
	|	КорректировкаРеализации.ПодписьКладовщика.Факсимиле КАК ФаксимилеКладовщика,
	|	КорректировкаРеализации.НоменклатураДоставки КАК НоменклатураДоставки,
	|	ПРЕДСТАВЛЕНИЕ(КорректировкаРеализации.НоменклатураДоставки) КАК ПредставлениеНоменклатурыДоставки,
	|	КорректировкаРеализации.НоменклатураДоставки.Код КАК КодНоменклатурыДоставки,
	|	КорректировкаРеализации.НоменклатураДоставки.Артикул КАК АртикулНоменклатурыДоставки,
	|	КорректировкаРеализации.СтоимостьДоставки КАК СтоимостьДоставки,
	|	КорректировкаРеализации.СтавкаНДСДоставки КАК СтавкаНДСДоставки,
	|	КорректировкаРеализации.СуммаНДСДоставки КАК СуммаНДСДоставки,
	|	0 КАК Вес,
	|	0 КАК Объем,
	|	КорректировкаРеализации.Запасы.(
	|		Номенклатура КАК Номенклатура,
	|		Содержание КАК Содержание,
	|		ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(КорректировкаРеализации.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|				ТОГДА КорректировкаРеализации.Запасы.Номенклатура.Наименование
	|			ИНАЧЕ ВЫРАЗИТЬ(КорректировкаРеализации.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|		КОНЕЦ КАК ПредставлениеНоменклатуры,
	|		Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|		Номенклатура.Код КАК ЗапасКод,
	|		Номенклатура.Код КАК Код,
	|		Номенклатура.Артикул КАК Артикул,
	|		Номенклатура.Штрихкод КАК Штрихкод,
	|		Номенклатура.ТоварнаяНоменклатураВЭД КАК КодТНВЭД,
	|		Характеристика КАК Характеристика,
	|		ВЫБОР
	|			КОГДА КорректировкаРеализации.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И КорректировкаРеализации.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА КорректировкаРеализации.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Наименование
	|			ИНАЧЕ КорректировкаРеализации.Запасы.Номенклатура.ЕдиницаИзмерения.Наименование
	|		КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Наименование,
	|		ВЫБОР
	|			КОГДА КорректировкаРеализации.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И КорректировкаРеализации.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА КорректировкаРеализации.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Код
	|			ИНАЧЕ КорректировкаРеализации.Запасы.Номенклатура.ЕдиницаИзмерения.Код
	|		КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Код,
	|		ВЫБОР
	|			КОГДА КорректировкаРеализации.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И КорректировкаРеализации.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА КорректировкаРеализации.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору
	|			ИНАЧЕ КорректировкаРеализации.Запасы.Номенклатура.ЕдиницаИзмерения
	|		КОНЕЦ КАК ЕдиницаИзмерения,
	|		ЕдиницаИзмерения КАК ВидУпаковки,
	|		ВЫБОР
	|			КОГДА КорректировкаРеализации.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И КорректировкаРеализации.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА 1
	|			КОГДА КорректировкаРеализации.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.КлассификаторЕдиницИзмерения
	|				ТОГДА 1
	|			ИНАЧЕ КорректировкаРеализации.Запасы.ЕдиницаИзмерения.Коэффициент
	|		КОНЕЦ КАК КоличествоВОдномМесте,
	|		Количество КАК КоличествоМест,
	|		ВЫБОР
	|			КОГДА КорректировкаРеализации.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение <> ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|				ТОГДА КорректировкаРеализации.Запасы.ЕдиницаИзмерения.Коэффициент
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК КоэффициентЕдиницыИзмерения,
	|		Количество КАК Количество,
	|		СтавкаНДС КАК СтавкаНДС,
	|		0 КАК МассаБрутто,
	|		0 КАК СуммаСкидкиПоСтроке,
	|		ВЫРАЗИТЬ(КорректировкаРеализации.Запасы.Цена * КорректировкаРеализации.Курс / КорректировкаРеализации.Кратность КАК ЧИСЛО(15, 2)) КАК Цена,
	|		ВЫРАЗИТЬ(КорректировкаРеализации.Запасы.Сумма * КорректировкаРеализации.Курс / КорректировкаРеализации.Кратность КАК ЧИСЛО(15, 2)) КАК Сумма,
	|		ВЫРАЗИТЬ(КорректировкаРеализации.Запасы.СуммаНДС * КорректировкаРеализации.Курс / КорректировкаРеализации.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|		ВЫРАЗИТЬ(КорректировкаРеализации.Запасы.Всего * КорректировкаРеализации.Курс / КорректировкаРеализации.Кратность КАК ЧИСЛО(15, 2)) КАК Всего,
	|		СтранаПроисхождения КАК СтранаСсылка,
	|		ПРЕДСТАВЛЕНИЕ(КорректировкаРеализации.Запасы.СтранаПроисхождения) КАК СтранаПредставление,
	|		СтранаПроисхождения.Код КАК СтранаКод,
	|		НомерГТД.РегистрационныйНомер КАК ПредставлениеГТД,
	|		0 КАК Вес,
	|		0 КАК Объем
	|	) КАК ТаблицаЗапасы,
	|	КорректировкаРеализации.СведенияПрослеживаемости.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		РНПТ КАК РНПТ,
	|		Количество КАК Количество,
	|		КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|		ИдентификаторСтроки КАК ИдентификаторСтроки
	|	) КАК СведенияПрослеживаемости,
	|	ЛОЖЬ КАК ЕстьПрослеживаемыеТовары
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации,
	|	Константа.НациональнаяВалюта КАК НациональнаяВалюта,
	|	Константа.ПечатьПользовательскихЕдиницИзмерения КАК ПечатьПользовательскихЕдиницИзмерения
	|ГДЕ
	|	КорректировкаРеализации.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Содержание КАК Содержание,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(ВложенныйЗапрос.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|			ТОГДА ВложенныйЗапрос.Номенклатура.Наименование
	|		ИНАЧЕ ВЫРАЗИТЬ(ВложенныйЗапрос.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|	КОНЕЦ КАК ПредставлениеНоменклатуры,
	|	ВложенныйЗапрос.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ВложенныйЗапрос.Номенклатура.Код КАК ЗапасКод,
	|	ВложенныйЗапрос.Номенклатура.Код КАК Код,
	|	ВложенныйЗапрос.Номенклатура.Артикул КАК Артикул,
	|	ВложенныйЗапрос.Номенклатура.Штрихкод КАК Штрихкод,
	|	ВложенныйЗапрос.Номенклатура.ТоварнаяНоменклатураВЭД КАК КодТНВЭД,
	|	ВЫБОР
	|		КОГДА &ИспользоватьФаксимиле = ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДаНет.Да)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ДаНет.Нет)
	|	КОНЕЦ КАК ИспользоватьФаксимиле,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	ВложенныйЗапрос.ЕдиницаИзмеренияПоОКЕИ_Наименование КАК ЕдиницаИзмеренияПоОКЕИ_Наименование,
	|	ВложенныйЗапрос.ЕдиницаИзмеренияПоОКЕИ_Код КАК ЕдиницаИзмеренияПоОКЕИ_Код,
	|	ВложенныйЗапрос.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.ВидУпаковки КАК ВидУпаковки,
	|	ВложенныйЗапрос.КоличествоВОдномМесте КАК КоличествоВОдномМесте,
	|	ВложенныйЗапрос.КоличествоМест КАК КоличествоМест,
	|	ВложенныйЗапрос.КоэффициентЕдиницыИзмерения КАК КоэффициентЕдиницыИзмерения,
	|	ВложенныйЗапрос.МассаБрутто КАК МассаБрутто,
	|	ВложенныйЗапрос.Количество КАК Количество,
	|	ВложенныйЗапрос.Ссылка КАК Ссылка,
	|	ВложенныйЗапрос.СтавкаНДС КАК СтавкаНДС,
	|	0 КАК СуммаСкидкиПоСтроке,
	|	ВложенныйЗапрос.Цена КАК Цена,
	|	ВложенныйЗапрос.Сумма КАК Сумма,
	|	ВложенныйЗапрос.СуммаНДС КАК СуммаНДС,
	|	ВложенныйЗапрос.Всего КАК Всего,
	|	ВложенныйЗапрос.НомерСтроки КАК НомерСтроки,
	|	ВложенныйЗапрос.СтранаСсылка КАК СтранаСсылка,
	|	ВложенныйЗапрос.СтранаПредставление КАК СтранаПредставление,
	|	ВложенныйЗапрос.СтранаКод КАК СтранаКод,
	|	ВложенныйЗапрос.ПредставлениеГТД КАК ПредставлениеГТД,
	|	ВложенныйЗапрос.Вес КАК Вес,
	|	ВложенныйЗапрос.Объем КАК Объем,
	|	ВложенныйЗапрос.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	ВложенныйЗапрос.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.ПрослеживаемыйТовар
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПрослеживаемыйТоварЧисло,
	|	ВложенныйЗапрос.КодТНВЭД.ЕдиницаИзмерения.Код КАК КодТНВЭДЕдиницаИзмеренияКод,
	|	ВложенныйЗапрос.КодТНВЭД.ЕдиницаИзмерения.Наименование КАК КодТНВЭДЕдиницаИзмеренияНаименование,
	|	ВложенныйЗапрос.Ссылка.ДатаИсходногоДокумента КАК ДатаОтгрузочногоДокумента,
	|	ВложенныйЗапрос.Ссылка.НомерИсходногоДокумента КАК НомерОтгрузочногоДокумента
	|ИЗ
	|	(ВЫБРАТЬ
	|		КорректировкаРеализацииЗапасы.Номенклатура КАК Номенклатура,
	|		ВЫБОР
	|			КОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Наименование
	|			ИНАЧЕ КорректировкаРеализацииЗапасы.Номенклатура.ЕдиницаИзмерения.Наименование
	|		КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Наименование,
	|		ВЫБОР
	|			КОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Код
	|			ИНАЧЕ КорректировкаРеализацииЗапасы.Номенклатура.ЕдиницаИзмерения.Код
	|		КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Код,
	|		ВЫБОР
	|			КОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору
	|			ИНАЧЕ КорректировкаРеализацииЗапасы.Номенклатура.ЕдиницаИзмерения
	|		КОНЕЦ КАК ЕдиницаИзмерения,
	|		КорректировкаРеализацииЗапасы.ЕдиницаИзмерения КАК ВидУпаковки,
	|		ВЫБОР
	|			КОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА 1
	|			КОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.КлассификаторЕдиницИзмерения
	|				ТОГДА 1
	|			ИНАЧЕ КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.Коэффициент
	|		КОНЕЦ КАК КоличествоВОдномМесте,
	|		КорректировкаРеализацииЗапасы.Количество КАК КоличествоМест,
	|		ВЫБОР
	|			КОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение <> ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|				ТОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.Коэффициент
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК КоэффициентЕдиницыИзмерения,
	|		КорректировкаРеализацииЗапасы.СтавкаНДС КАК СтавкаНДС,
	|		0 КАК СуммаСкидкиПоСтроке,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях
	|				ТОГДА ВЫБОР
	|						КОГДА КорректировкаРеализацииЗапасы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|							ТОГДА КорректировкаРеализацииЗапасы.Цена
	|						КОГДА СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|							ТОГДА ВЫРАЗИТЬ(КорректировкаРеализацииЗапасы.Цена * КорректировкаРеализацииЗапасы.Ссылка.Курс / КорректировкаРеализацииЗапасы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|						ИНАЧЕ ВЫБОР
	|								КОГДА КорректировкаРеализацииЗапасы.Количество = 0
	|									ТОГДА СуммыДокументовРегламентированныйУчет.Всего - СуммыДокументовРегламентированныйУчет.Всего
	|								ИНАЧЕ ВЫРАЗИТЬ((СуммыДокументовРегламентированныйУчет.Всего - СуммыДокументовРегламентированныйУчет.Всего) / КорректировкаРеализацииЗапасы.Количество КАК ЧИСЛО(15, 2))
	|							КОНЕЦ
	|					КОНЕЦ
	|			ИНАЧЕ КорректировкаРеализацииЗапасы.Цена
	|		КОНЕЦ КАК Цена,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях
	|				ТОГДА ВЫБОР
	|						КОГДА КорректировкаРеализацииЗапасы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|							ТОГДА КорректировкаРеализацииЗапасы.Сумма
	|						КОГДА СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|							ТОГДА ВЫРАЗИТЬ(КорректировкаРеализацииЗапасы.Сумма * КорректировкаРеализацииЗапасы.Ссылка.Курс / КорректировкаРеализацииЗапасы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|						ИНАЧЕ ВЫБОР
	|								КОГДА КорректировкаРеализацииЗапасы.Ссылка.СуммаВключаетНДС
	|									ТОГДА СуммыДокументовРегламентированныйУчет.Всего
	|								ИНАЧЕ ВЫРАЗИТЬ(СуммыДокументовРегламентированныйУчет.Всего / КорректировкаРеализацииЗапасы.Количество КАК ЧИСЛО(15, 2))
	|							КОНЕЦ
	|					КОНЕЦ
	|			ИНАЧЕ КорректировкаРеализацииЗапасы.Сумма
	|		КОНЕЦ КАК Сумма,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях
	|				ТОГДА ВЫБОР
	|						КОГДА КорректировкаРеализацииЗапасы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|							ТОГДА КорректировкаРеализацииЗапасы.СуммаНДС
	|						КОГДА СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|							ТОГДА ВЫРАЗИТЬ(КорректировкаРеализацииЗапасы.СуммаНДС * КорректировкаРеализацииЗапасы.Ссылка.Курс / КорректировкаРеализацииЗапасы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|						ИНАЧЕ СуммыДокументовРегламентированныйУчет.НДС
	|					КОНЕЦ
	|			ИНАЧЕ КорректировкаРеализацииЗапасы.СуммаНДС
	|		КОНЕЦ КАК СуммаНДС,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях
	|				ТОГДА ВЫБОР
	|						КОГДА КорректировкаРеализацииЗапасы.Ссылка.ВалютаДокумента = &НациональнаяВалюта
	|							ТОГДА КорректировкаРеализацииЗапасы.Всего
	|						КОГДА СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|							ТОГДА ВЫРАЗИТЬ(КорректировкаРеализацииЗапасы.Всего * КорректировкаРеализацииЗапасы.Ссылка.Курс / КорректировкаРеализацииЗапасы.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|						ИНАЧЕ СуммыДокументовРегламентированныйУчет.Всего
	|					КОНЕЦ
	|			ИНАЧЕ КорректировкаРеализацииЗапасы.Всего
	|		КОНЕЦ КАК Всего,
	|		0 КАК МассаБрутто,
	|		КорректировкаРеализацииЗапасы.Количество КАК Количество,
	|		КорректировкаРеализацииЗапасы.НомерСтроки КАК НомерСтроки,
	|		КорректировкаРеализацииЗапасы.Характеристика КАК Характеристика,
	|		ВЫРАЗИТЬ(КорректировкаРеализацииЗапасы.Содержание КАК СТРОКА(1000)) КАК Содержание,
	|		КорректировкаРеализацииЗапасы.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|		КорректировкаРеализацииЗапасы.Ссылка.Курс КАК Курс,
	|		КорректировкаРеализацииЗапасы.Ссылка.Кратность КАК Кратность,
	|		КорректировкаРеализацииЗапасы.Ссылка КАК Ссылка,
	|		КорректировкаРеализацииЗапасы.СтранаПроисхождения КАК СтранаСсылка,
	|		ПРЕДСТАВЛЕНИЕ(КорректировкаРеализацииЗапасы.СтранаПроисхождения) КАК СтранаПредставление,
	|		КорректировкаРеализацииЗапасы.СтранаПроисхождения.Код КАК СтранаКод,
	|		ПРЕДСТАВЛЕНИЕ(КорректировкаРеализацииЗапасы.НомерГТД) КАК ПредставлениеГТД,
	|		0 КАК Вес,
	|		0 КАК Объем,
	|		ВЫБОР
	|			КОГДА КорректировкаРеализацииЗапасы.КодТНВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|				ТОГДА КорректировкаРеализацииЗапасы.Номенклатура.ТоварнаяНоменклатураВЭД
	|			ИНАЧЕ КорректировкаРеализацииЗапасы.КодТНВЭД
	|		КОНЕЦ КАК КодТНВЭД,
	|		КорректировкаРеализацииЗапасы.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|		КорректировкаРеализацииЗапасы.ИдентификаторСтроки КАК ИдентификаторСтроки
	|	ИЗ
	|		Документ.КорректировкаРеализации.Запасы КАК КорректировкаРеализацииЗапасы
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовРегламентированныйУчет КАК СуммыДокументовРегламентированныйУчет
	|			ПО КорректировкаРеализацииЗапасы.Ссылка = СуммыДокументовРегламентированныйУчет.Регистратор
	|				И КорректировкаРеализацииЗапасы.НомерСтроки = СуммыДокументовРегламентированныйУчет.НомерСтрокиДокумента,
	|		Константа.ПечатьПользовательскихЕдиницИзмерения КАК ПечатьПользовательскихЕдиницИзмерения
	|	ГДЕ
	|		КорректировкаРеализацииЗапасы.Ссылка В(&МассивОбъектов)) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ТаблицаДанныеДокументов = МассивРезультатов[0].Выгрузить();
	
	ТЗСтрок = МассивРезультатов[1].Выгрузить();
	Для Каждого ТекущаяСтрока Из ТаблицаДанныеДокументов Цикл
		
		НайденныеСтроки = ТЗСтрок.НайтиСтроки(Новый Структура("Ссылка", ТекущаяСтрока.Ссылка));
		Если НайденныеСтроки.Количество() > 0 Тогда
			ТекущаяСтрока.ТаблицаЗапасы = ТЗСтрок.Скопировать(НайденныеСтроки);
			
			ТекущаяСтрока.ЕстьПрослеживаемыеТовары = (ТекущаяСтрока.ТаблицаЗапасы.Итог("ПрослеживаемыйТоварЧисло") > 0);
			Если ТекущаяСтрока.ЕстьПрослеживаемыеТовары Тогда
				ДополнитьСведениямиОПрослеживаемости(ТекущаяСтрока.СведенияПрослеживаемости, ТекущаяСтрока.ТаблицаЗапасы);
			КонецЕсли;

		КонецЕсли;
		
	КонецЦикла;
	
	ОснованиеПечатиИзРасходнойНакладной(ТаблицаДанныеДокументов, "ПредставлениеОснования");
	ДоставкаСервер.ДобавитьСтрокуДоставкиУниверсальныеДанные(ТаблицаДанныеДокументов, "ТаблицаЗапасы");
	
	Возврат ТаблицаДанныеДокументов;
	
КонецФункции

Процедура ДополнитьСведениямиОПрослеживаемости(СведенияОПрослеживаемости, ТаблицаДокумента)
	
	МассивИдентифицирующихРеквизитов = Новый Массив();
	МассивИдентифицирующихРеквизитов.Добавить("ИдентификаторСтроки");
	
	ТаблицаДокумента.Колонки.Добавить("СведенияОПрослеживаемости");
	
	Для Каждого СтрокаТаблицыДокумента Из ТаблицаДокумента Цикл
		
		ТаблицаСведенийОПрослеживаемости = НовыйТаблицаСведенийОПрослеживаемости();
		
		ПараметрыОтбора = ПараметрыОтбораСтрокДляРНПТ(МассивИдентифицирующихРеквизитов, СтрокаТаблицыДокумента);
		СтрокиСРНПТ = СведенияОПрослеживаемости.НайтиСтроки(ПараметрыОтбора);
		Для Каждого НайденнаяСтрока Из СтрокиСРНПТ Цикл
			СтрокаСведенийОПрослеживаемости = ТаблицаСведенийОПрослеживаемости.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаСведенийОПрослеживаемости, НайденнаяСтрока);
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ТаблицаСведенийОПрослеживаемости) Тогда
			СтрокаТаблицыДокумента.СведенияОПрослеживаемости = ТаблицаСведенийОПрослеживаемости;
		Иначе
			СтрокаТаблицыДокумента.СведенияОПрослеживаемости = Неопределено;;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПараметрыОтбораСтрокДляРНПТ(ИдентифицирующиеРеквизитыСтроки, СтрокаСчетаФактуры)
	
	СтруктураОтбора = Новый Структура;
	Для Каждого ИдентифицирующийРеквизит Из ИдентифицирующиеРеквизитыСтроки Цикл
		СтруктураОтбора.Вставить(ИдентифицирующийРеквизит,СтрокаСчетаФактуры[ИдентифицирующийРеквизит]); 
	КонецЦикла;
	
	Возврат СтруктураОтбора;
	
КонецФункции

Функция НовыйТаблицаСведенийОПрослеживаемости()
	
	ТаблицаСведенийОПрослеживаемости = Новый ТаблицаЗначений();
	ТаблицаСведенийОПрослеживаемости.Колонки.Добавить("ИдентификаторСтроки");
	ТаблицаСведенийОПрослеживаемости.Колонки.Добавить("РНПТ");
	ТаблицаСведенийОПрослеживаемости.Колонки.Добавить("Количество");
	ТаблицаСведенийОПрослеживаемости.Колонки.Добавить("КоличествоПрослеживаемости");
	
	Возврат ТаблицаСведенийОПрослеживаемости;
	
КонецФункции

Функция УниверсальныйЗапросПоДаннымДокумента(МассивОбъектов, ИспользоватьФаксимиле)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ИспользоватьФаксимиле", ИспользоватьФаксимиле);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка КАК Ссылка
	|	,КорректировкаРеализации.Дата КАК ДатаДокумента
	|	,КорректировкаРеализации.Номер КАК Номер
	|	,""Расходная накладная"" КАК ПредставлениеРегистратора
	|	,Неопределено КАК ВидОперации
	|	,КорректировкаРеализации.Организация КАК Организация
	|	,КорректировкаРеализации.Организация.Префикс КАК Префикс
	|	,КорректировкаРеализации.Организация.ФайлЛоготип КАК ФайлЛоготип
	|	,КорректировкаРеализации.Организация.ФайлФаксимильнаяПечать КАК ФаксимилеПечати
	|	,КорректировкаРеализации.СуммаВключаетНДС КАК СуммаВключаетНДС
	|	,КорректировкаРеализации.ВалютаДокумента КАК ВалютаДокумента
	|	,Выбор КОГДА &ИспользоватьФаксимиле = ИСТИНА
	|		ТОГДА Значение(Перечисление.ДаНет.Да)
	|		ИНАЧЕ Значение(Перечисление.ДаНет.Нет) КОНЕЦ КАК ИспользоватьФаксимиле
	|	,КорректировкаРеализации.ПодписьРуководителя.Должность КАК ДолжностьРуководителя
	|	,КорректировкаРеализации.ПодписьРуководителя.РасшифровкаПодписи КАК РасшифровкаПодписиРуководителя
	|	,КорректировкаРеализации.ПодписьРуководителя.РасшифровкаПодписи КАК РасшифровкаПодписиВыполнилРаботыУслуги
	|	,КорректировкаРеализации.ПодписьРуководителя.Факсимиле КАК ФаксимилеРуководителя
	|	,КорректировкаРеализации.ПодписьГлавногоБухгалтера.Должность КАК ДолжностьГлавногоБухгалтера
	|	,КорректировкаРеализации.ПодписьГлавногоБухгалтера.РасшифровкаПодписи КАК РасшифровкаПодписиГлавногоБухгалтера
	|	,КорректировкаРеализации.ПодписьГлавногоБухгалтера.Факсимиле КАК ФаксимилеГлавногоБухгалтера
	|	,КорректировкаРеализации.ПодписьКладовщика.Должность КАК ДолжностьКладовщика
	|	,КорректировкаРеализации.ПодписьКладовщика.РасшифровкаПодписи КАК РасшифровкаПодписиКладовщика
	|	,КорректировкаРеализации.ПодписьКладовщика.Факсимиле КАК ФаксимилеКладовщика
	|	,КорректировкаРеализации.БанковскийСчет КАК БанковскийСчет
	|	,КорректировкаРеализации.Контрагент КАК Контрагент
	|	,КорректировкаРеализации.КонтактноеЛицоПодписант.Наименование КАК РасшифровкаПодписиКонтрагента
	|	,КорректировкаРеализации.КонтактноеЛицоПодписант.Наименование КАК РасшифровкаПодписиПринялРаботыУслуги
	|	,КорректировкаРеализации.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента
	|	,КорректировкаРеализации.Договор
	|	,КорректировкаРеализации.АдресДоставки КАК АдресДоставки
	|	,Неопределено КАК ДополнительныеУсловия
	|	,Неопределено КАК ОснованиеНомер
	|	,Неопределено КАК ОснованиеДата
	|	,Неопределено КАК ОснованиеПечати
	|	,Неопределено КАК ОснованиеПечатиСсылка
	|	,КорректировкаРеализации.ДокументОснование
	|	,КорректировкаРеализации.Ответственный
	|	,КорректировкаРеализации.Автор
	|	,КорректировкаРеализации.ДисконтнаяКарта
	|	,КорректировкаРеализации.Комментарий КАК Комментарий
	|	,КорректировкаРеализации.Комментарий
	|	,КорректировкаРеализации.НоменклатураДоставки
	|	,ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(КорректировкаРеализации.НоменклатураДоставки.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|			ТОГДА КорректировкаРеализации.НоменклатураДоставки.Наименование
	|		ИНАЧЕ ВЫРАЗИТЬ(КорректировкаРеализации.НоменклатураДоставки.НаименованиеПолное КАК СТРОКА(1000))
	|	КОНЕЦ КАК ПредставлениеНоменклатурыДоставки
	|	,КорректировкаРеализации.НоменклатураДоставки.Код КАК КодДоставки
	|	,КорректировкаРеализации.НоменклатураДоставки.Артикул КАК АртикулДоставки
	|	,КорректировкаРеализации.НоменклатураДоставки.ЕдиницаИзмерения КАК ЕдиницаИзмеренияДоставки
	|	,КорректировкаРеализации.СтоимостьДоставки
	|	,КорректировкаРеализации.СтавкаНДСДоставки
	|	,КорректировкаРеализации.СуммаНДСДоставки
	|
	//::: Запасы
	|	,КорректировкаРеализации.Запасы.(
	|		НомерСтроки КАК НомерСтроки
	|		,Содержание
	|		,Выбор КОГДА (ВЫРАЗИТЬ(КорректировкаРеализации.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|			ТОГДА КорректировкаРеализации.Запасы.Номенклатура.Наименование
	|			ИНАЧЕ ВЫРАЗИТЬ(КорректировкаРеализации.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)) КОНЕЦ КАК ПредставлениеНоменклатуры
	|		,Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры
	|		,Номенклатура.Код КАК Код
	|		,Номенклатура.Артикул КАК Артикул
	|		,Номенклатура.Штрихкод КАК Штрихкод
	|		,Характеристика
	|		,Партия
	|		,ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|		,Количество КАК Количество
	|		,Цена КАК Цена
	|		,Сумма КАК Сумма
	|		,СтавкаНДС КАК СтавкаНДС
	|		,СуммаНДС КАК СуммаНДС
	|		,Всего КАК Всего
	|		,0 КАК ПроцентСкидкиНаценки
	|		,0 КАК ЕстьСкидка
	|		,0 КАК СуммаАвтоматическойСкидки
	|		,-1 КАК КлючСвязи
	|	) КАК ТаблицаРаботыУслуги
	|
	// ::: Серии номенклатуры (не используются)
	|	,Неопределено КАК ТаблицаСерииНоменклатуры
	|
	|ИЗ Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ КорректировкаРеализации.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО Ссылка, НомерСтроки";
	
	ДанныеДокументов = Запрос.Выполнить().Выгрузить();
	
	ОснованиеПечатиИзРасходнойНакладной(ДанныеДокументов, "ОснованиеПечати");
	ДоставкаСервер.ДобавитьСтрокуДоставкиУниверсальныеДанные(ДанныеДокументов, "ТаблицаРаботыУслуги");
	
	Возврат ДанныеДокументов;
	
КонецФункции

Функция ТекстЗапросаДанныеДляПечатиКорректировочныхСчетовФактур(НомераТаблиц) Экспорт

	НомераТаблиц.Вставить("ВалютаРегламентированногоУчета",    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты",         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты",                         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаНоменклатуры",      НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаДокумента",         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаСведенияПрослеживаемости", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаСведенияПрослеживаемостиДоИзменения", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КонстантаВалютаРегламентированногоУчета.Значение КАК ВалютаРеглУчета
	|ПОМЕСТИТЬ КонстантаВалютаРегламентированногоУчета
	|ИЗ
	|	Константа.НациональнаяВалюта КАК КонстантаВалютаРегламентированногоУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА Реквизиты.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК Организация,
	|	Реквизиты.Организация КАК ОбособленноеПодразделениеПоставщика,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА Реквизиты.Организация.ЦифровойИндексОбособленногоПодразделения
	|		КОГДА Реквизиты.Подразделение.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				И Реквизиты.Подразделение.ГоловнаяОрганизация = Реквизиты.Организация
	|			ТОГДА Реквизиты.Подразделение.ЦифровойИндексОбособленногоПодразделения
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ЦифровойИндексОбособленногоПодразделения,
	|	Реквизиты.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА Реквизиты.Контрагент.ГоловнойКонтрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И Реквизиты.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА Реквизиты.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ Реквизиты.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	Реквизиты.Контрагент КАК ОбособленноеПодразделениеПокупателя,
	|	Реквизиты.ПодписьРуководителя.Должность КАК ДолжностьРуководителя,
	|	Реквизиты.ПодписьРуководителя.РасшифровкаПодписи КАК РасшифровкаПодписиРуководителя,
	|	Реквизиты.ПодписьГлавногоБухгалтера.Должность КАК ДолжностьГлавногоБухгалтера,
	|	Реквизиты.ПодписьГлавногоБухгалтера.РасшифровкаПодписи КАК РасшифровкаПодписиГлавногоБухгалтера,
	|	ВЫБОР
	|		КОГДА КонстантаВалютаРегламентированногоУчета.ВалютаРеглУчета <> Реквизиты.ВалютаДокумента
	|				И Реквизиты.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА КонстантаВалютаРегламентированногоУчета.ВалютаРеглУчета
	|		ИНАЧЕ Реквизиты.ВалютаДокумента
	|	КОНЕЦ КАК ВалютаДокумента,
	|	Реквизиты.Договор КАК Договор,
	|	Реквизиты.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ВЫБОР
	|		КОГДА КонстантаВалютаРегламентированногоУчета.ВалютаРеглУчета <> Реквизиты.ВалютаДокумента
	|				И Реквизиты.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПересчет,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Исправление,
	|	ВЫБОР
	|		КОГДА Реквизиты.Курс = 0
	|			ТОГДА 1
	|		ИНАЧЕ Реквизиты.Курс
	|	КОНЕЦ КАК Курс,
	|	ВЫБОР
	|		КОГДА Реквизиты.Кратность = 0
	|			ТОГДА 1
	|		ИНАЧЕ Реквизиты.Кратность
	|	КОНЕЦ КАК Кратность,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторГосКонтракта,
	|	ВложенныйЗапрос.МаксНомерСтроки КАК МаксНомерСтроки
	|ПОМЕСТИТЬ ВременнаяТаблицаРеквизиты
	|ИЗ
	|	Документ.КорректировкаРеализации КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонстантаВалютаРегламентированногоУчета КАК КонстантаВалютаРегламентированногоУчета
	|		ПО (ИСТИНА),
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(КорректировкаРеализацииЗапасы.НомерСтроки) КАК МаксНомерСтроки
	|	ИЗ
	|		Документ.КорректировкаРеализации.Запасы КАК КорректировкаРеализацииЗапасы
	|	ГДЕ
	|		КорректировкаРеализацииЗапасы.Ссылка = &ДокументОснование) КАК ВложенныйЗапрос
	|ГДЕ
	|	Реквизиты.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК ДатаОснования,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Подразделение КАК Подразделение,
	|	Реквизиты.ЦифровойИндексОбособленногоПодразделения КАК ЦифровойИндексОбособленногоПодразделения,
	|	Реквизиты.Организация КАК Поставщик,
	|	ВЫБОР
	|		КОГДА &АдресПоставщика <> """"
	|			ТОГДА &АдресПоставщика
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК АдресПоставщика,
	|	ВЫБОР
	|		КОГДА &ИННКПППоставщика <> """"
	|			ТОГДА &ИННКПППоставщика
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ИННКПППоставщика,
	|	Реквизиты.Организация.ИНН КАК ИННПоставщика,
	|	Реквизиты.Организация.КПП КАК КПППоставщика,
	|	Реквизиты.ОбособленноеПодразделениеПоставщика КАК ОбособленноеПодразделениеПоставщика,
	|	Реквизиты.Контрагент КАК Покупатель,
	|	Реквизиты.Контрагент.ИНН КАК ИННПокупателя,
	|	Реквизиты.Контрагент.КПП КАК КПППокупателя,
	|	Реквизиты.ОбособленноеПодразделениеПокупателя КАК ОбособленноеПодразделениеПокупателя,
	|	Реквизиты.ВалютаДокумента КАК Валюта,
	|	Реквизиты.Договор.ВалютаРасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.Договор.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.Договор.Представление КАК Основание,
	|	Реквизиты.ИдентификаторГосКонтракта КАК ИдентификаторГосКонтракта,
	|	Реквизиты.Ссылка.НомерИсходногоДокумента КАК НомерОтгрузочногоДокумента,
	|	Реквизиты.Ссылка.ДатаИсходногоДокумента КАК ДатаОтгрузочногоДокумента
	|ИЗ
	|	ВременнаяТаблицаРеквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Ссылка КАК Ссылка,
	|	1 КАК НомерТабЧасти,
	|	ТаблицаНоменклатуры.НомерСтроки КАК НомерСтроки,
	|	ТаблицаНоменклатуры.Номенклатура КАК Товар,
	|	ТаблицаНоменклатуры.Номенклатура.ТоварнаяНоменклатураВЭД.Код КАК ТоварКодТНВЭД,
	|	ТаблицаНоменклатуры.Номенклатура.Код КАК ТоварКод,
	|	ТаблицаНоменклатуры.Номенклатура.Артикул КАК ТоварАртикул,
	|	ТаблицаНоменклатуры.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ТаблицаНоменклатуры.Характеристика КАК Характеристика,
	|	ТаблицаНоменклатуры.СтранаПроисхождения КАК СтранаПроисхождения,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеСтраны,
	|	ТаблицаНоменклатуры.НомерГТД КАК НомерГТД,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеГТД,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|				И ТаблицаНоменклатуры.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|			ТОГДА ТаблицаНоменклатуры.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору
	|		ИНАЧЕ ТаблицаНоменклатуры.Номенклатура.ЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ТаблицаНоменклатуры.СтавкаНДС КАК СтавкаНДС,
	|	ЛОЖЬ КАК ЭтоУслуга,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				И ПечатьПользовательскихЕдиницИзмерения.Значение <> ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.Исправление
	|						ТОГДА ТаблицаНоменклатуры.КоличествоДоКорректировки * ТаблицаНоменклатуры.ЕдиницаИзмерения.Коэффициент
	|					ИНАЧЕ ТаблицаНоменклатуры.КоличествоДоИзменения * ТаблицаНоменклатуры.ЕдиницаИзмерения.Коэффициент
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.Исправление
	|					ТОГДА ТаблицаНоменклатуры.КоличествоДоКорректировки
	|				ИНАЧЕ ТаблицаНоменклатуры.КоличествоДоИзменения
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоДоИзменения,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				И ПечатьПользовательскихЕдиницИзмерения.Значение <> ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|			ТОГДА ТаблицаНоменклатуры.Количество * ТаблицаНоменклатуры.ЕдиницаИзмерения.Коэффициент
	|		ИНАЧЕ ТаблицаНоменклатуры.Количество
	|	КОНЕЦ КАК КоличествоПослеИзменения,
	|	ВЫБОР
	|		КОГДА НЕ СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаНоменклатуры.КоличествоДоКорректировки = 0
	|							И ТаблицаНоменклатуры.КоличествоДоИзменения = 0
	|						ТОГДА СуммыДокументовРегламентированныйУчет.Всего - СуммыДокументовРегламентированныйУчет.НДС
	|					ИНАЧЕ ВЫРАЗИТЬ((СуммыДокументовРегламентированныйУчет.Всего - СуммыДокументовРегламентированныйУчет.НДС) / ВЫБОР
	|								КОГДА Реквизиты.Исправление
	|									ТОГДА ТаблицаНоменклатуры.КоличествоДоКорректировки
	|								ИНАЧЕ ТаблицаНоменклатуры.КоличествоДоИзменения
	|							КОНЕЦ КАК ЧИСЛО(15, 2))
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.Исправление
	|					ТОГДА ТаблицаНоменклатуры.ЦенаДоКорректировки
	|				ИНАЧЕ ТаблицаНоменклатуры.ЦенаДоИзменения
	|			КОНЕЦ * ВЫБОР
	|				КОГДА Реквизиты.ИспользоватьПересчет
	|					ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|				ИНАЧЕ 1
	|			КОНЕЦ
	|	КОНЕЦ КАК ЦенаДоИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ТаблицаНоменклатуры.ЦенаДоКорректировки
	|		ИНАЧЕ ТаблицаНоменклатуры.ЦенаДоИзменения
	|	КОНЕЦ КАК ЦенаДоИзмененияВалДокумента,
	|	ТаблицаНоменклатуры.Цена * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЦенаПослеИзменения,
	|	ТаблицаНоменклатуры.Цена КАК ЦенаПослеИзмененияВалДокумента,
	|	ВЫБОР
	|		КОГДА НЕ СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|			ТОГДА СуммыДокументовРегламентированныйУчет.НДС
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.Исправление
	|					ТОГДА ТаблицаНоменклатуры.СуммаНДСДоКорректировки
	|				ИНАЧЕ ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|			КОНЕЦ * ВЫБОР
	|				КОГДА Реквизиты.ИспользоватьПересчет
	|					ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|				ИНАЧЕ 1
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаНДСДоИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ТаблицаНоменклатуры.СуммаНДСДоКорректировки
	|		ИНАЧЕ ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|	КОНЕЦ КАК СуммаНДСДоИзмененияВалДокумента,
	|	ТаблицаНоменклатуры.СуммаНДС * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаНДСПослеИзменения,
	|	ТаблицаНоменклатуры.СуммаНДС КАК СуммаНДСПослеИзмененияВалДокумента,
	|	ВЫБОР
	|		КОГДА НЕ СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|			ТОГДА ВЫРАЗИТЬ(СуммыДокументовРегламентированныйУчет.Всего - СуммыДокументовРегламентированныйУчет.НДС КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.Исправление
	|					ТОГДА ВЫБОР
	|							КОГДА Реквизиты.СуммаВключаетНДС
	|								ТОГДА ТаблицаНоменклатуры.СуммаДоКорректировки - ТаблицаНоменклатуры.СуммаНДСДоКорректировки
	|							ИНАЧЕ ТаблицаНоменклатуры.СуммаДоКорректировки
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Реквизиты.СуммаВключаетНДС
	|							ТОГДА ТаблицаНоменклатуры.СуммаДоИзменения - ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|						ИНАЧЕ ТаблицаНоменклатуры.СуммаДоИзменения
	|					КОНЕЦ
	|			КОНЕЦ * ВЫБОР
	|				КОГДА Реквизиты.ИспользоватьПересчет
	|					ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|				ИНАЧЕ 1
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьБезНДСДоИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.СуммаВключаетНДС
	|						ТОГДА ТаблицаНоменклатуры.СуммаДоКорректировки - ТаблицаНоменклатуры.СуммаНДСДоКорректировки
	|					ИНАЧЕ ТаблицаНоменклатуры.СуммаДоКорректировки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.СуммаВключаетНДС
	|					ТОГДА ТаблицаНоменклатуры.СуммаДоИзменения - ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|				ИНАЧЕ ТаблицаНоменклатуры.СуммаДоИзменения
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьБезНДСДоИзмененияВалДокумента,
	|	ВЫБОР
	|		КОГДА Реквизиты.СуммаВключаетНДС
	|			ТОГДА ТаблицаНоменклатуры.Сумма - ТаблицаНоменклатуры.СуммаНДС
	|		ИНАЧЕ ТаблицаНоменклатуры.Сумма
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СтоимостьБезНДСПослеИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.СуммаВключаетНДС
	|			ТОГДА ТаблицаНоменклатуры.Сумма - ТаблицаНоменклатуры.СуммаНДС
	|		ИНАЧЕ ТаблицаНоменклатуры.Сумма
	|	КОНЕЦ КАК СтоимостьБезНДСПослеИзмененияВалДокумента,
	|	ВЫБОР
	|		КОГДА НЕ СуммыДокументовРегламентированныйУчет.Всего ЕСТЬ NULL
	|			ТОГДА СуммыДокументовРегламентированныйУчет.Всего
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.Исправление
	|					ТОГДА ВЫБОР
	|							КОГДА Реквизиты.СуммаВключаетНДС
	|								ТОГДА ТаблицаНоменклатуры.СуммаДоКорректировки
	|							ИНАЧЕ ТаблицаНоменклатуры.СуммаДоКорректировки + ТаблицаНоменклатуры.СуммаНДСДоКорректировки
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Реквизиты.СуммаВключаетНДС
	|							ТОГДА ТаблицаНоменклатуры.СуммаДоИзменения
	|						ИНАЧЕ ТаблицаНоменклатуры.СуммаДоИзменения + ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|					КОНЕЦ
	|			КОНЕЦ * ВЫБОР
	|				КОГДА Реквизиты.ИспользоватьПересчет
	|					ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|				ИНАЧЕ 1
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьСНДСДоИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.СуммаВключаетНДС
	|			ТОГДА ТаблицаНоменклатуры.Сумма
	|		ИНАЧЕ ТаблицаНоменклатуры.Сумма + ТаблицаНоменклатуры.СуммаНДС
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СтоимостьСНДСПослеИзменения,
	|	ТаблицаНоменклатуры.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.КодТНВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|			ТОГДА ТаблицаНоменклатуры.Номенклатура.ТоварнаяНоменклатураВЭД.ЕдиницаИзмерения.Код
	|		ИНАЧЕ ТаблицаНоменклатуры.КодТНВЭД.ЕдиницаИзмерения.Код
	|	КОНЕЦ КАК КодТНВЭДЕдиницаИзмеренияКод,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.КодТНВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|			ТОГДА ТаблицаНоменклатуры.Номенклатура.ТоварнаяНоменклатураВЭД.ЕдиницаИзмерения.Наименование
	|		ИНАЧЕ ТаблицаНоменклатуры.КодТНВЭД.ЕдиницаИзмерения.Наименование
	|	КОНЕЦ КАК КодТНВЭДЕдиницаИзмеренияНаименование,
	|	ТаблицаНоменклатуры.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.КорректировкаРеализации.Запасы КАК ТаблицаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаРеквизиты КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = ТаблицаНоменклатуры.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовРегламентированныйУчет КАК СуммыДокументовРегламентированныйУчет
	|		ПО ТаблицаНоменклатуры.Ссылка.ДокументОснование = СуммыДокументовРегламентированныйУчет.Регистратор
	|			И ТаблицаНоменклатуры.КлючСвязи = СуммыДокументовРегламентированныйУчет.НомерСтрокиДокумента,
	|	Константа.ПечатьПользовательскихЕдиницИзмерения КАК ПечатьПользовательскихЕдиницИзмерения
	|ГДЕ
	|	ТаблицаНоменклатуры.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка,
	|	1,
	|	Реквизиты.МаксНомерСтроки + 1,
	|	КорректировкаРеализации.НоменклатураДоставки,
	|	КорректировкаРеализации.НоменклатураДоставки.ТоварнаяНоменклатураВЭД.Код,
	|	КорректировкаРеализации.НоменклатураДоставки.Код,
	|	КорректировкаРеализации.НоменклатураДоставки.Артикул,
	|	КорректировкаРеализации.НоменклатураДоставки.НаименованиеПолное,
	|	NULL,
	|	NULL,
	|	НЕОПРЕДЕЛЕНО,
	|	NULL,
	|	НЕОПРЕДЕЛЕНО,
	|	КорректировкаРеализации.НоменклатураДоставки.ЕдиницаИзмерения,
	|	КорректировкаРеализации.СтавкаНДСДоставки,
	|	ИСТИНА,
	|	ВЫБОР
	|		КОГДА ВариантПечатиДоставки.Значение = ЗНАЧЕНИЕ(Перечисление.ВариантыПечатиДоставки.НеВыводитьБесплатнуюДоставку)
	|				И ВЫБОР
	|					КОГДА Реквизиты.Исправление
	|						ТОГДА КорректировкаРеализации.СтоимостьДоставкиДоКорректировки
	|					ИНАЧЕ КорректировкаРеализации.СтоимостьДоставкиДоИзменения
	|				КОНЕЦ = 0
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА (ВариантПечатиДоставки.Значение = ЗНАЧЕНИЕ(Перечисление.ВариантыПечатиДоставки.НеВыводитьБесплатнуюДоставку)
	|				ИЛИ КорректировкаРеализации.ОтменаДоставки)
	|				И КорректировкаРеализации.СтоимостьДоставки = 0
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА КорректировкаРеализации.СтоимостьДоставкиДоКорректировки
	|		ИНАЧЕ КорректировкаРеализации.СтоимостьДоставкиДоИзменения
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	КорректировкаРеализации.СтоимостьДоставкиДоКорректировки,
	|	КорректировкаРеализации.СтоимостьДоставки * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	КорректировкаРеализации.СтоимостьДоставки,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА КорректировкаРеализации.СуммаНДСДоставкиДоКорректировки
	|		ИНАЧЕ КорректировкаРеализации.СуммаНДСДоставкиДоИзменения
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА КорректировкаРеализации.СуммаНДСДоставкиДоКорректировки
	|		ИНАЧЕ КорректировкаРеализации.СуммаНДСДоставкиДоИзменения
	|	КОНЕЦ,
	|	КорректировкаРеализации.СуммаНДСДоставки * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	КорректировкаРеализации.СуммаНДСДоставки,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.СуммаВключаетНДС
	|						ТОГДА КорректировкаРеализации.СтоимостьДоставкиДоКорректировки - КорректировкаРеализации.СуммаНДСДоставкиДоКорректировки
	|					ИНАЧЕ КорректировкаРеализации.СтоимостьДоставкиДоКорректировки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.СуммаВключаетНДС
	|					ТОГДА КорректировкаРеализации.СтоимостьДоставкиДоИзменения - КорректировкаРеализации.СуммаНДСДоставкиДоИзменения
	|				ИНАЧЕ КорректировкаРеализации.СтоимостьДоставкиДоИзменения
	|			КОНЕЦ
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.СуммаВключаетНДС
	|						ТОГДА КорректировкаРеализации.СтоимостьДоставкиДоКорректировки - КорректировкаРеализации.СуммаНДСДоставкиДоКорректировки
	|					ИНАЧЕ КорректировкаРеализации.СтоимостьДоставкиДоКорректировки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.СуммаВключаетНДС
	|					ТОГДА КорректировкаРеализации.СтоимостьДоставкиДоИзменения - КорректировкаРеализации.СуммаНДСДоставкиДоИзменения
	|				ИНАЧЕ КорректировкаРеализации.СтоимостьДоставкиДоИзменения
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.СуммаВключаетНДС
	|			ТОГДА КорректировкаРеализации.СтоимостьДоставки - КорректировкаРеализации.СуммаНДСДоставки
	|		ИНАЧЕ КорректировкаРеализации.СтоимостьДоставки
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.СуммаВключаетНДС
	|			ТОГДА КорректировкаРеализации.СтоимостьДоставки - КорректировкаРеализации.СуммаНДСДоставки
	|		ИНАЧЕ КорректировкаРеализации.СтоимостьДоставки
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.СуммаВключаетНДС
	|						ТОГДА КорректировкаРеализации.СтоимостьДоставкиДоКорректировки
	|					ИНАЧЕ КорректировкаРеализации.СтоимостьДоставкиДоКорректировки + КорректировкаРеализации.СуммаНДСДоставкиДоКорректировки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.СуммаВключаетНДС
	|					ТОГДА КорректировкаРеализации.СтоимостьДоставкиДоИзменения
	|				ИНАЧЕ КорректировкаРеализации.СтоимостьДоставкиДоИзменения + КорректировкаРеализации.СуммаНДСДоставкиДоИзменения
	|			КОНЕЦ
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.СуммаВключаетНДС
	|			ТОГДА КорректировкаРеализации.СтоимостьДоставки
	|		ИНАЧЕ КорректировкаРеализации.СтоимостьДоставки + КорректировкаРеализации.СуммаНДСДоставки
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ЛОЖЬ,
	|	NULL,
	|	NULL,
	|	""""
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаРеквизиты КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = КорректировкаРеализации.Ссылка),
	|	Константа.ВариантПечатиДоставки КАК ВариантПечатиДоставки
	|ГДЕ
	|	КорректировкаРеализации.Ссылка = &ДокументОснование
	|	И КорректировкаРеализации.НоменклатураДоставки <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.НомерТабЧасти КАК НомерТабЧасти,
	|	ТаблицаНоменклатуры.НомерСтроки КАК НомерСтроки,
	|	ТаблицаНоменклатуры.Товар КАК Товар,
	|	ТаблицаНоменклатуры.ТоварКодТНВЭД КАК ТоварКодТНВЭД,
	|	ТаблицаНоменклатуры.ТоварКод КАК ТоварКод,
	|	ТаблицаНоменклатуры.ТоварАртикул КАК ТоварАртикул,
	|	ТаблицаНоменклатуры.ТоварКод КАК Код,
	|	ТаблицаНоменклатуры.ТоварАртикул КАК Артикул,
	|	ТаблицаНоменклатуры.Характеристика КАК Характеристика,
	|	ТаблицаНоменклатуры.ТоварНаименование КАК ТоварНаименование,
	|	ТаблицаНоменклатуры.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаНоменклатуры.ПредставлениеСтраны КАК ПредставлениеСтраны,
	|	ТаблицаНоменклатуры.НомерГТД КАК НомерГТД,
	|	ТаблицаНоменклатуры.ПредставлениеГТД КАК ПредставлениеГТД,
	|	ТаблицаНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаНоменклатуры.КоличествоДоИзменения КАК КоличествоДоИзменения,
	|	ТаблицаНоменклатуры.КоличествоПослеИзменения КАК КоличествоПослеИзменения,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.КоличествоДоИзменения = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения / ТаблицаНоменклатуры.КоличествоДоИзменения
	|	КОНЕЦ КАК ЦенаДоИзменения,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.КоличествоПослеИзменения = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения / ТаблицаНоменклатуры.КоличествоПослеИзменения
	|	КОНЕЦ КАК ЦенаПослеИзменения,
	|	ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения КАК СтоимостьБезНДСДоИзменения,
	|	ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения КАК СтоимостьБезНДСПослеИзменения,
	|	ТаблицаНоменклатуры.СуммаНДСПослеИзменения КАК СуммаНДСПослеИзменения,
	|	ТаблицаНоменклатуры.СуммаНДСДоИзменения КАК СуммаНДСДоИзменения,
	|	ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения КАК СтоимостьСНДСДоИзменения,
	|	ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения КАК СтоимостьСНДСПослеИзменения,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения > ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения
	|			ТОГДА ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения - ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаБезНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения > ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения
	|			ТОГДА ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения - ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаБезНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СуммаНДСДоИзменения > ТаблицаНоменклатуры.СуммаНДСПослеИзменения
	|			ТОГДА ТаблицаНоменклатуры.СуммаНДСДоИзменения - ТаблицаНоменклатуры.СуммаНДСПослеИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СуммаНДСПослеИзменения > ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|			ТОГДА ТаблицаНоменклатуры.СуммаНДСПослеИзменения - ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения > ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения
	|			ТОГДА ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения - ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаСНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения > ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения
	|			ТОГДА ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения - ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаСНДСУвеличение,
	|	ТаблицаНоменклатуры.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаНоменклатуры.ЭтоУслуга КАК ЭтоУслуга,
	|	ТаблицаНоменклатуры.Ссылка КАК Ссылка,
	|	ТаблицаНоменклатуры.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	ТаблицаНоменклатуры.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаНоменклатуры.КодТНВЭДЕдиницаИзмеренияКод КАК КодТНВЭДЕдиницаИзмеренияКод,
	|	ТаблицаНоменклатуры.КодТНВЭДЕдиницаИзмеренияНаименование КАК КодТНВЭДЕдиницаИзмеренияНаименование
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|ГДЕ
	|	(ТаблицаНоменклатуры.КоличествоПослеИзменения <> ТаблицаНоменклатуры.КоличествоДоИзменения
	|			ИЛИ ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзмененияВалДокумента <> ТаблицаНоменклатуры.СтоимостьБезНДСДоИзмененияВалДокумента
	|			ИЛИ ТаблицаНоменклатуры.СуммаНДСПослеИзмененияВалДокумента <> ТаблицаНоменклатуры.СуммаНДСДоИзмененияВалДокумента
	|			ИЛИ ТаблицаНоменклатуры.ЦенаПослеИзмененияВалДокумента <> ТаблицаНоменклатуры.ЦенаДоИзмененияВалДокумента)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкаРеализацииСведенияПрослеживаемости.Ссылка КАК Ссылка,
	|	КорректировкаРеализацииСведенияПрослеживаемости.НомерСтроки КАК НомерСтроки,
	|	КорректировкаРеализацииСведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаРеализацииСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	КорректировкаРеализацииСведенияПрослеживаемости.Количество КАК Количество,
	|	КорректировкаРеализацииСведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости
	|ПОМЕСТИТЬ ТаблицаСведенияПрослеживаемости
	|ИЗ
	|	Документ.КорректировкаРеализации.СведенияПрослеживаемости КАК КорректировкаРеализацииСведенияПрослеживаемости
	|ГДЕ
	|	КорректировкаРеализацииСведенияПрослеживаемости.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкаРеализацииСведенияПрослеживаемости.Ссылка КАК Ссылка,
	|	КорректировкаРеализацииСведенияПрослеживаемости.НомерСтроки КАК НомерСтроки,
	|	КорректировкаРеализацииСведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаРеализацииСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	КорректировкаРеализацииСведенияПрослеживаемости.Количество КАК Количество,
	|	КорректировкаРеализацииСведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости
	|ПОМЕСТИТЬ ТаблицаСведенияПрослеживаемостиДоИзменения
	|ИЗ
	|	Документ.КорректировкаРеализации.СведенияПрослеживаемости КАК КорректировкаРеализацииСведенияПрослеживаемости
	|ГДЕ
	|	КорректировкаРеализацииСведенияПрослеживаемости.Ссылка = &ИсправляемыйДокументРеализации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходнаяНакладнаяСведенияПрослеживаемости.Ссылка,
	|	РасходнаяНакладнаяСведенияПрослеживаемости.НомерСтроки,
	|	РасходнаяНакладнаяСведенияПрослеживаемости.ИдентификаторСтроки,
	|	РасходнаяНакладнаяСведенияПрослеживаемости.РНПТ,
	|	РасходнаяНакладнаяСведенияПрослеживаемости.Количество,
	|	РасходнаяНакладнаяСведенияПрослеживаемости.КоличествоПрослеживаемости
	|ИЗ
	|	Документ.РасходнаяНакладная.СведенияПрослеживаемости КАК РасходнаяНакладнаяСведенияПрослеживаемости
	|ГДЕ
	|	РасходнаяНакладнаяСведенияПрослеживаемости.Ссылка = &ИсправляемыйДокументРеализации";
	
	Возврат ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();

КонецФункции

Функция ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов() Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Документ.СчетФактура.ПустаяСсылка) КАК СчетФактура,
	|	КорректировкаРеализации.Дата КАК Дата,
	|	КорректировкаРеализации.Номер КАК Номер,
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаФактуры,
	|	КорректировкаРеализации.ПодписьРуководителя.Должность КАК ДолжностьРуководителя,
	|	КорректировкаРеализации.ПодписьРуководителя.РасшифровкаПодписи КАК РасшифровкаПодписиРуководителя,
	|	КорректировкаРеализации.ПодписьГлавногоБухгалтера.Должность КАК ДолжностьГлавногоБухгалтера,
	|	КорректировкаРеализации.ПодписьГлавногоБухгалтера.РасшифровкаПодписи КАК РасшифровкаПодписиГлавногоБухгалтера,
	|	КорректировкаРеализации.ПодписьКладовщика.Должность КАК ДолжностьКладовщика,
	|	КорректировкаРеализации.ПодписьКладовщика.РасшифровкаПодписи КАК РасшифровкаПодписиКладовщика,
	|	ИСТИНА КАК СчетФактураБезНДС,
	|	ИСТИНА КАК Исправление,
	|	""--"" КАК НомерИсправления,
	|	КорректировкаРеализации.Дата КАК ДатаИсправления,
	|	""--"" КАК НомерИсправленияКорректировочного,
	|	""--"" КАК ДатаИсправленияКорректировочного,
	|	ИСТИНА КАК УдалитьПрефиксыИзНомера,
	|	ИСТИНА КАК ЭтоСчетФактураВыданный,
	|	ИСТИНА КАК ВыводитьСуммуБезНДС,
	|	КорректировкаРеализации.Ссылка КАК ДокументОснование,
	|	1 КАК НомерСтроки,
	|	КорректировкаРеализации.Контрагент,
	|	КорректировкаРеализации.Договор КАК ДоговорКонтрагента,
	|	"""" КАК КППКонтрагента,
	|	Неопределено КАК ИдентификаторГосКонтракта,
	|	КорректировкаРеализации.Дата КАК ДатаСведений
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	КорректировкаРеализации.Ссылка В(&МассивОбъектов)
	|	И КорректировкаРеализации.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)";
	
	Возврат ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	
КонецФункции

Процедура ОснованиеПечатиИзРасходнойНакладной(ДанныеДокументовТОРГ12, ИмяРеквизитаПечати)
	
	Для каждого СтрокаДанныхДокумента Из ДанныеДокументовТОРГ12 Цикл
		
		ИсходныйДокумент = ПечатьДокументовУНФ.ПолучитьИсходныйДокументКорректировки(СтрокаДанныхДокумента.Ссылка);
		Если ЗначениеЗаполнено(ИсходныйДокумент) Тогда
			
			Если СтрокаДанныхДокумента.Владелец().Колонки.Найти("ОригинальныйСчетФактура") <> Неопределено Тогда
				
				ОписаниеОригинальногоСФ = СчетаФактурыУНФ.ПолучитьПодчиненныйСчетФактуру(ИсходныйДокумент, Ложь);
				Если ОписаниеОригинальногоСФ <> Неопределено Тогда
					
					СтрокаДанныхДокумента.ОригинальныйСчетФактура = ОписаниеОригинальногоСФ.Ссылка;
					
				КонецЕсли;
				
			КонецЕсли;
			
			МетаданныеДокументаОснования = ИсходныйДокумент.Метаданные();
			
			Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ОснованиеПечати", МетаданныеДокументаОснования) Тогда
				
				СтрокаДанныхДокумента[ИмяРеквизитаПечати] = ИсходныйДокумент.ОснованиеПечати;
				
			КонецЕсли;
			
			Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ОснованиеПечатиСсылка", МетаданныеДокументаОснования) Тогда
				
				ДанныеОснованияПечати = ПечатьДокументовУНФ.ПредставлениеНомераДатыОснованияПечати(ИсходныйДокумент.ОснованиеПечатиСсылка);
				СтрокаДанныхДокумента.ОснованиеНомер = ДанныеОснованияПечати.Номер;
				СтрокаДанныхДокумента.ОснованиеДата = ДанныеОснованияПечати.Дата;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Сформировать печатные формы объектов
//
Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета, ИспользоватьФаксимиле = Ложь)
	Перем Ошибки;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ПервыйДокумент = Истина;
	
	ПараметрыНоменклатуры = Новый Структура;
	
	Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
	
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если ИмяМакета = "М15" Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	КорректировкаРеализации.Дата КАК ДатаДокумента,
			|	КорректировкаРеализации.Номер КАК Номер,
			|	КорректировкаРеализации.Организация КАК Руководители,
			|	КорректировкаРеализации.Организация КАК Организация,
			|	КорректировкаРеализации.БанковскийСчет КАК БанковскийСчет,
			|	КорректировкаРеализации.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
			|	КорректировкаРеализации.Организация.Префикс КАК Префикс,
			|	КорректировкаРеализации.СтруктурнаяЕдиница КАК СкладНаименование,
			|	КорректировкаРеализации.Контрагент КАК Контрагент,
			|	КорректировкаРеализации.Договор.Представление КАК Основание,
			|	КорректировкаРеализации.ВалютаДокумента,
			|	КорректировкаРеализации.СуммаВключаетНДС,
			|	КорректировкаРеализации.СтруктурнаяЕдиница.МОЛ КАК МОЛ,
			|	КорректировкаРеализации.Ответственный.Физлицо КАК Ответственный,
			|	КорректировкаРеализации.НДСВключатьВСтоимость,
			|	КорректировкаРеализации.Курс,
			|	КорректировкаРеализации.Кратность,
			|	КорректировкаРеализации.ПодписьРуководителя.Должность КАК ДолжностьРуководителя,
			|	КорректировкаРеализации.ПодписьРуководителя.РасшифровкаПодписи КАК РасшифровкаПодписиРуководителя,
			|	КорректировкаРеализации.ПодписьГлавногоБухгалтера.РасшифровкаПодписи КАК РасшифровкаПодписиГлавногоБухгалтера,
			|	КорректировкаРеализации.ПодписьКладовщика.Должность КАК ДолжностьКладовщика,
			|	КорректировкаРеализации.ПодписьКладовщика.РасшифровкаПодписи КАК РасшифровкаПодписиКладовщика,
			|	КорректировкаРеализации.КонтактноеЛицоПодписант.Наименование КАК РасшифровкаПодписиКонтрагента
			|ИЗ
			|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
			|ГДЕ
			|	КорректировкаРеализации.Ссылка = &ТекущийДокумент";
			
			Шапка = Запрос.Выполнить().Выбрать();
			Шапка.Следующий();
			
			ИспользоватьПересчет = (НЕ Шапка.ВалютаДокумента = Константы.НациональнаяВалюта.Получить());
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	КорректировкаРеализацииЗапасы.Номенклатура КАК Номенклатура,
			|	ВЫБОР
			|		КОГДА (ВЫРАЗИТЬ(КорректировкаРеализацииЗапасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
			|			ТОГДА КорректировкаРеализацииЗапасы.Номенклатура.Наименование
			|		ИНАЧЕ ВЫРАЗИТЬ(КорректировкаРеализацииЗапасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
			|	КОНЕЦ КАК ЗапасНаименование,
			|	КорректировкаРеализацииЗапасы.Характеристика КАК Характеристика,
			|	ВЫРАЗИТЬ(КорректировкаРеализацииЗапасы.Содержание КАК СТРОКА(1000)) КАК Содержание,
			|	КорректировкаРеализацииЗапасы.Номенклатура.Код КАК ЗапасКод,
			|	КорректировкаРеализацииЗапасы.Номенклатура.Код КАК Код,
			|	КорректировкаРеализацииЗапасы.Номенклатура.Артикул КАК Артикул,
			|	КорректировкаРеализацииЗапасы.Номенклатура.Штрихкод КАК Штрихкод,
			|	КорректировкаРеализацииЗапасы.ЕдиницаИзмерения КАК ВидУпаковки,
			|	ВЫБОР
			|		КОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
			|				И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
			|				И КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
			|			ТОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Наименование
			|		ИНАЧЕ КорректировкаРеализацииЗапасы.Номенклатура.ЕдиницаИзмерения.Наименование
			|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
			|	ВЫБОР
			|		КОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
			|				И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
			|				И КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
			|			ТОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Код
			|		ИНАЧЕ КорректировкаРеализацииЗапасы.Номенклатура.ЕдиницаИзмерения.Код
			|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
			|	КорректировкаРеализацииЗапасы.СтавкаНДС КАК СтавкаНДС,
			|	МАКСИМУМ(&Цена_Параметр) КАК Цена,
			|	СУММА(ВЫБОР
			|			КОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
			|					И ПечатьПользовательскихЕдиницИзмерения.Значение <> ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
			|				ТОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.Коэффициент * КорректировкаРеализацииЗапасы.Количество
			|			ИНАЧЕ КорректировкаРеализацииЗапасы.Количество
			|		КОНЕЦ) КАК Количество,
			|	СУММА(&Сумма_Параметр) КАК Сумма,
			|	СУММА(&СуммаНДС_Параметр) КАК СуммаНДС,
			|	СУММА(&Всего_Параметр) КАК Всего,
			|	МИНИМУМ(КорректировкаРеализацииЗапасы.НомерСтроки) КАК НомерСтроки
			|ИЗ
			|	Документ.КорректировкаРеализации.Запасы КАК КорректировкаРеализацииЗапасы,
			|	Константа.ПечатьПользовательскихЕдиницИзмерения КАК ПечатьПользовательскихЕдиницИзмерения
			|ГДЕ
			|	КорректировкаРеализацииЗапасы.Ссылка = &ТекущийДокумент
			|	И НЕ КорректировкаРеализацииЗапасы.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
			|
			|СГРУППИРОВАТЬ ПО
			|	КорректировкаРеализацииЗапасы.Номенклатура,
			|	КорректировкаРеализацииЗапасы.ЕдиницаИзмерения,
			|	КорректировкаРеализацииЗапасы.СтавкаНДС,
			|	КорректировкаРеализацииЗапасы.Характеристика,
			|	ВЫРАЗИТЬ(КорректировкаРеализацииЗапасы.Содержание КАК СТРОКА(1000)),
			|	КорректировкаРеализацииЗапасы.Номенклатура.Код,
			|	КорректировкаРеализацииЗапасы.Номенклатура.Артикул,
			|	КорректировкаРеализацииЗапасы.Номенклатура.Штрихкод,
			|	ВЫБОР
			|		КОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
			|				И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
			|				И КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
			|			ТОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Наименование
			|		ИНАЧЕ КорректировкаРеализацииЗапасы.Номенклатура.ЕдиницаИзмерения.Наименование
			|	КОНЕЦ,
			|	ВЫБОР
			|		КОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
			|				И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
			|				И КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
			|			ТОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Код
			|		ИНАЧЕ КорректировкаРеализацииЗапасы.Номенклатура.ЕдиницаИзмерения.Код
			|	КОНЕЦ,
			|	ВЫБОР
			|		КОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
			|				И ПечатьПользовательскихЕдиницИзмерения.Значение <> ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
			|			ТОГДА КорректировкаРеализацииЗапасы.ЕдиницаИзмерения.Коэффициент
			|		ИНАЧЕ 1
			|	КОНЕЦ,
			|	ВЫБОР
			|		КОГДА (ВЫРАЗИТЬ(КорректировкаРеализацииЗапасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
			|			ТОГДА КорректировкаРеализацииЗапасы.Номенклатура.Наименование
			|		ИНАЧЕ ВЫРАЗИТЬ(КорректировкаРеализацииЗапасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
			|	КОНЕЦ,
			|	КорректировкаРеализацииЗапасы.Номенклатура.Код
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки";
			
			Если ИспользоватьПересчет Тогда
				
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Цена_Параметр", 		"Выразить(КорректировкаРеализацииЗапасы.Цена * &Курс / &Кратность КАК Число(15,2))");
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Сумма_Параметр", 	"Выразить(КорректировкаРеализацииЗапасы.Сумма * &Курс / &Кратность КАК Число(15,2))");
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СуммаНДС_Параметр", 	"Выразить(КорректировкаРеализацииЗапасы.СуммаНДС * &Курс / &Кратность КАК Число(15,2))");
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Всего_Параметр", 	"Выразить(КорректировкаРеализацииЗапасы.Всего * &Курс / &Кратность КАК Число(15,2))");
				
				Запрос.УстановитьПараметр("Курс",		Шапка.Курс);
				Запрос.УстановитьПараметр("Кратность",	Шапка.Кратность);
				
			Иначе
				
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Цена_Параметр", 		"КорректировкаРеализацииЗапасы.Цена");
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Сумма_Параметр", 	"КорректировкаРеализацииЗапасы.Сумма");
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СуммаНДС_Параметр", 	"КорректировкаРеализацииЗапасы.СуммаНДС");
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Всего_Параметр", 	"КорректировкаРеализацииЗапасы.Всего");
				
			КонецЕсли;
			
			ВыборкаСтрокЗапасы = Запрос.Выполнить().Выбрать();
			
			ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КорректировкаРеализации_М15";

			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.КорректировкаРеализации.ПФ_MXL_М15");
			
			ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка");
			ОбластьМакетаЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
			ОбластьМакетаСтрока = Макет.ПолучитьОбласть("Строка");
			ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
			
			СведенияОбОрганизации = ПечатьДокументовУНФ.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента, , Шапка.БанковскийСчет);
			СведенияОГрузополучателе = ПечатьДокументовУНФ.СведенияОЮрФизЛице(Шапка.Контрагент, Шапка.ДатаДокумента, , Шапка.БанковскийСчетКонтрагента);
			
			ОбластьМакетаШапка.Параметры.Заполнить(Шапка);
			
			НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(Шапка.ДатаДокумента, Шапка.Номер, Шапка.Префикс);
			
			ОбластьМакетаШапка.Параметры.НомерДокумента = НомерДокумента;
			ОбластьМакетаШапка.Параметры.ДатаДокумента = Шапка.ДатаДокумента;
			ОбластьМакетаШапка.Параметры.ПредставлениеОрганизации = ПечатьДокументовУНФ.ОписаниеОрганизации(СведенияОбОрганизации);
			ОбластьМакетаШапка.Параметры.КонтрагентНаименование = ПечатьДокументовУНФ.ОписаниеОрганизации(СведенияОГрузополучателе,"ПолноеНаименование,");
			ОбластьМакетаШапка.Параметры.ОрганизацияПоОКПО = СведенияОбОрганизации.КодПоОКПО;
			
			ТабличныйДокумент.Вывести(ОбластьМакетаШапка);
			
			// Выводим заголовок таблицы
			ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);
			
			НомерСтраницы = 1;
			НомерСтроки = 0;
			КоличествоСтрок = ВыборкаСтрокЗапасы.Количество();
			
			// Инициализация итогов в документе
			ИтогоСуммаБезНДС = 0;
			ИтогоСуммаНДС = 0;
			ИтогоВсегоСНДС = 0;
			
			// Создаем массив для проверки вывода
			МассивВыводимыхОбластей = Новый Массив;
			
			// Выводим многострочную часть документа
			Пока ВыборкаСтрокЗапасы.Следующий() Цикл
				
				Если ВыборкаСтрокЗапасы.Количество = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				НомерСтроки = НомерСтроки + 1;
				
				// Проверим вывод
				СтрокаСПодвалом = Новый Массив;
				Если НомерСтроки = 1 Тогда
					СтрокаСПодвалом.Добавить(ОбластьМакетаЗаголовокТаблицы); // если первая строка, то должен
				КонецЕсли;                                                   // помещаться заголовок
				СтрокаСПодвалом.Добавить(ОбластьМакетаСтрока);
				Если НомерСтроки = КоличествоСтрок Тогда           // если последняя строка, должен
					СтрокаСПодвалом.Добавить(ОбластьМакетаПодвал); // помещаться и подвал документа
				КонецЕсли;

				Если НомерСтроки <> 1 И НЕ ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
					
					// Выведем заголовок таблицы
					НомерСтраницы	= НомерСтраницы + 1;
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ОбластьМакетаЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
					ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);
					
				КонецЕсли;
				
				ОбластьМакетаСтрока.Параметры.Заполнить(ВыборкаСтрокЗапасы);
				
				Если ЗначениеЗаполнено(ВыборкаСтрокЗапасы.Содержание) Тогда
					ОбластьМакетаСтрока.Параметры.ЗапасНаименование = ВыборкаСтрокЗапасы.Содержание;
				Иначе
					ОбластьМакетаСтрока.Параметры.ЗапасНаименование = ПечатьДокументовУНФ.ПредставлениеНоменклатурыДляПечати(ВыборкаСтрокЗапасы.ЗапасНаименование, 
																		ВыборкаСтрокЗапасы.Характеристика, ВыборкаСтрокЗапасы.Артикул);
				КонецЕсли;
				
				ПараметрыНоменклатуры.Вставить("Код", ВыборкаСтрокЗапасы.Код);
				ПараметрыНоменклатуры.Вставить("Артикул", ВыборкаСтрокЗапасы.Артикул);
				ОбластьМакетаСтрока.Параметры.ПредставлениеКодаНоменклатуры = ПечатьДокументовУНФ.ПредставлениеКодаНоменклатуры(ПараметрыНоменклатуры);
				
				СуммаСНДС	= ВыборкаСтрокЗапасы.Всего;
				СуммаНДС	= ВыборкаСтрокЗапасы.СуммаНДС;
				СуммаБезНДС = ВыборкаСтрокЗапасы.Сумма - ?(Шапка.СуммаВключаетНДС, ВыборкаСтрокЗапасы.СуммаНДС, 0);
				
				Количество = ВыборкаСтрокЗапасы.Количество;
				Цена = СуммаБезНДС / ?(Количество = 0, 1, Количество);
				
				ОбластьМакетаСтрока.Параметры.Количество = Количество;
				ОбластьМакетаСтрока.Параметры.СуммаСНДС = СуммаСНДС;
				ОбластьМакетаСтрока.Параметры.СуммаБезНДС = СуммаБезНДС;
				ОбластьМакетаСтрока.Параметры.СуммаНДС = СуммаНДС;
				ОбластьМакетаСтрока.Параметры.Цена = Цена;
				
				ТабличныйДокумент.Вывести(ОбластьМакетаСтрока);
				
				ИтогоСуммаБезНДС = ИтогоСуммаБезНДС + СуммаБезНДС;
				ИтогоВсегоСНДС = ИтогоВсегоСНДС + СуммаСНДС;
				
			КонецЦикла;
			
			// Выводим подвал документа
			ОбластьМакетаПодвал.Параметры.Заполнить(Шапка);
			ТабличныйДокумент.Вывести(ОбластьМакетаПодвал);
			
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ТекущийДокумент);
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	Перем Ошибки;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, Обработки.ПечатьАктаОбОказанииУслуг.ИдентификаторПечатнойФормы(Ложь, Ложь));
	Если ПечатнаяФорма <> Неопределено Тогда
		
		ПечатнаяФорма.ТабличныйДокумент = Новый ТабличныйДокумент;
		ПечатнаяФорма.ТабличныйДокумент.КлючПараметровПечати = Обработки.ПечатьАктаОбОказанииУслуг.КлючПараметровПечати();
		ПечатнаяФорма.ПолныйПутьКМакету = Обработки.ПечатьАктаОбОказанииУслуг.ПолныйПутьКМакету(Ложь);
		ПечатнаяФорма.СинонимМакета = Обработки.ПечатьАктаОбОказанииУслуг.ПредставлениеПФ(Ложь, Ложь);
		
		ДанныеОбъектовПечати = УниверсальныйЗапросПоДаннымДокумента(МассивОбъектов, Ложь);
		Обработки.ПечатьАктаОбОказанииУслуг.СформироватьПФ(ПечатнаяФорма, ДанныеОбъектовПечати, ОбъектыПечати, Ложь);
		
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, Обработки.ПечатьАктаОбОказанииУслуг.ИдентификаторПечатнойФормы(Ложь, Истина));
	Если ПечатнаяФорма <> Неопределено Тогда
		
		ПечатнаяФорма.ТабличныйДокумент = Новый ТабличныйДокумент;
		ПечатнаяФорма.ТабличныйДокумент.КлючПараметровПечати = Обработки.ПечатьАктаОбОказанииУслуг.КлючПараметровПечати();
		ПечатнаяФорма.ПолныйПутьКМакету = Обработки.ПечатьАктаОбОказанииУслуг.ПолныйПутьКМакету(Ложь);
		ПечатнаяФорма.СинонимМакета = Обработки.ПечатьАктаОбОказанииУслуг.ПредставлениеПФ(Ложь, Истина);
		
		ДанныеОбъектовПечати = УниверсальныйЗапросПоДаннымДокумента(МассивОбъектов, Истина);
		Обработки.ПечатьАктаОбОказанииУслуг.СформироватьПФ(ПечатнаяФорма, ДанныеОбъектовПечати, ОбъектыПечати, Ложь);
		
	КонецЕсли;
	
	ВозможныеВарианты = Обработки.ПечатьТОРГ12.МатрицаВозможныхВариантов();
	Для каждого СтрокаТаблицы Из ВозможныеВарианты Цикл
		
		ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, Обработки.ПечатьТОРГ12.ИдентификаторПечатнойФормы(СтрокаТаблицы.ВключаяУслуги, СтрокаТаблицы.ИспользоватьФаксимиле));
		Если ПечатнаяФорма <> Неопределено Тогда
			
			ПечатнаяФорма.ТабличныйДокумент = Новый ТабличныйДокумент;
			ПечатнаяФорма.ТабличныйДокумент.КлючПараметровПечати = Обработки.ПечатьТОРГ12.КлючПараметровПечати();
			ПечатнаяФорма.ПолныйПутьКМакету = Обработки.ПечатьТОРГ12.ПолныйПутьКМакету();
			ПечатнаяФорма.СинонимМакета = Обработки.ПечатьТОРГ12.ПредставлениеПФ(СтрокаТаблицы.ВключаяУслуги, СтрокаТаблицы.ИспользоватьФаксимиле);
			
			ДанныеОбъектовПечати = ДанныеДокументовРегУчет(МассивОбъектов, СтрокаТаблицы.ИспользоватьФаксимиле, Истина, Ошибки);
			Обработки.ПечатьТОРГ12.СформироватьПФ(ПечатнаяФорма, ДанныеОбъектовПечати, ОбъектыПечати, СтрокаТаблицы.ВключаяУслуги);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ВозможныеВарианты = Обработки.ПечатьНакладная.МатрицаВозможныхВариантов();
	Для каждого СтрокаТаблицы Из ВозможныеВарианты Цикл
		
		ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, Обработки.ПечатьНакладная.ИдентификаторПечатнойФормы(СтрокаТаблицы.ВключаяУслуги, СтрокаТаблицы.ИспользоватьФаксимиле));
		Если ПечатнаяФорма <> Неопределено Тогда
			
			ПечатнаяФорма.ТабличныйДокумент = Новый ТабличныйДокумент;
			ПечатнаяФорма.ТабличныйДокумент.КлючПараметровПечати = Обработки.ПечатьНакладная.КлючПараметровПечати();
			ПечатнаяФорма.ПолныйПутьКМакету = Обработки.ПечатьНакладная.ПолныйПутьКМакету();
			ПечатнаяФорма.СинонимМакета = Обработки.ПечатьНакладная.ПредставлениеПФ(СтрокаТаблицы.ВключаяУслуги, СтрокаТаблицы.ИспользоватьФаксимиле);
			
			ДанныеОбъектовПечати = УниверсальныйЗапросПоДаннымДокумента(МассивОбъектов, СтрокаТаблицы.ИспользоватьФаксимиле);
			ДанныеОбъектовПечати.Колонки.ТаблицаРаботыУслуги.Имя = "ТаблицаЗапасы";
			
			Обработки.ПечатьНакладная.СформироватьПФ(ПечатнаяФорма, ДанныеОбъектовПечати, ОбъектыПечати, СтрокаТаблицы.ВключаяУслуги);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М15") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "М15", "М15 (Накладная на отпуск материалов)", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "М15"));
		
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТТН", "1-Т (Товарно-транспортная накладная)", Обработки.ПечатьТТН.ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		
	КонецЕсли;
	
	Если Ошибки <> Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
	КонецЕсли;
	
	// параметры отправки печатных форм по электронной почте
	ЭлектроннаяПочтаУНФ.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, 
		КоллекцияПечатныхФорм);
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	ЗначениеИдентификатора = "АктОбОказанииУслуг,ТОРГ12,ТОРГ12СУслугами,Обработка.ПечатьСчетФактура.СчетФактура,М15,ТТН,Накладная,НакладнаяСУслугами";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = ЗначениеИдентификатора;
	КомандаПечати.Представление = ПечатьДокументовУНФ.ПредставлениеКомплектаДокументов();
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.ДополнитьКомплектВнешнимиПечатнымиФормами = Истина;
	КомандаПечати.Порядок = 1;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = Обработки.ПечатьАктаОбОказанииУслуг.ИдентификаторПечатнойФормы(Ложь, Ложь);
	КомандаПечати.Представление = Обработки.ПечатьАктаОбОказанииУслуг.ПредставлениеПФ(Ложь, Ложь);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 4;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = Обработки.ПечатьАктаОбОказанииУслуг.ИдентификаторПечатнойФормы(Ложь, Истина);
	КомандаПечати.Представление = Обработки.ПечатьАктаОбОказанииУслуг.ПредставлениеПФ(Ложь, Истина);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.МестоРазмещения = "ПодменюПечатьФаксимиле";
	КомандаПечати.Порядок = 4;
	
	
	ВозможныеВарианты = Обработки.ПечатьТОРГ12.МатрицаВозможныхВариантов();
	Для каждого СтрокаТаблицы Из ВозможныеВарианты Цикл
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = Обработки.ПечатьТОРГ12.ИдентификаторПечатнойФормы(СтрокаТаблицы.ВключаяУслуги, СтрокаТаблицы.ИспользоватьФаксимиле);
		КомандаПечати.Представление = Обработки.ПечатьТОРГ12.ПредставлениеПФ(СтрокаТаблицы.ВключаяУслуги, СтрокаТаблицы.ИспользоватьФаксимиле);
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		КомандаПечати.Порядок = 7;
		
		Если СтрокаТаблицы.ИспользоватьФаксимиле Тогда
			
			КомандаПечати.МестоРазмещения = "ПодменюПечатьФаксимиле";
			КомандаПечати.Порядок = 10;
			
		КонецЕсли;
		
	КонецЦикла;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "М15";
	КомандаПечати.Представление = НСтр("ru = 'М15 (Накладная на отпуск материалов)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 14;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТТН";
	КомандаПечати.Представление = НСтр("ru = '1-Т (Товарно-транспортная накладная)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 17;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Обработка.ПечатьСчетФактура.СчетФактура";
	КомандаПечати.Представление = Обработки.ПечатьСчетФактура.ПредставлениеПФ(Ложь, Ложь);
	КомандаПечати.ФункциональныеОпции = "ПередачаТоваровНаКомиссию,ПриемТоваровНаКомиссию,ИспользуетсяОСНО,ИспользоватьСчетаФактурыИсходящие";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 20;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Обработка.ПечатьСчетФактура.УниверсальныйПередаточныйДокумент";
	КомандаПечати.Представление = Обработки.ПечатьСчетФактура.ПредставлениеПФ(Истина, Ложь);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 23;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Обработка.ПечатьСчетФактура.УниверсальныйПередаточныйДокументФаксимиле";
	КомандаПечати.Представление = Обработки.ПечатьСчетФактура.ПредставлениеПФ(Истина, Истина);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.МестоРазмещения = "ПодменюПечатьФаксимиле";
	КомандаПечати.Порядок = 23;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "ПечатьДокументовУНФКлиент.ПечатьУКД";
	КомандаПечати.Идентификатор = "УниверсальныйКорректировочныйДокумент";
	КомандаПечати.Представление = НСтр("ru = 'Универсальный корректировочный документ'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 26;
	
	ВозможныеВарианты = Обработки.ПечатьНакладная.МатрицаВозможныхВариантов();
	Для каждого СтрокаТаблицы Из ВозможныеВарианты Цикл
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = Обработки.ПечатьНакладная.ИдентификаторПечатнойФормы(СтрокаТаблицы.ВключаяУслуги, СтрокаТаблицы.ИспользоватьФаксимиле);
		КомандаПечати.Представление = Обработки.ПечатьНакладная.ПредставлениеПФ(СтрокаТаблицы.ВключаяУслуги, СтрокаТаблицы.ИспользоватьФаксимиле);
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		КомандаПечати.Порядок = 29;
		
		Если СтрокаТаблицы.ИспользоватьФаксимиле Тогда
			
			КомандаПечати.МестоРазмещения = "ПодменюПечатьФаксимиле";
			КомандаПечати.Порядок = 32;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли