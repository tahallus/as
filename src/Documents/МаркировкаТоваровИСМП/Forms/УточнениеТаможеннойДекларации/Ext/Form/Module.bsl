#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ДатаДекларации  = Параметры.ДатаДекларации;
	РегистрационныйНомерДекларации = Параметры.РегистрационныйНомерДекларации;
	
	СброситьРазмерыИПоложениеОкна();
	
	СобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Отказ = Ложь;
	ПроверитьЗаполнениеРеквизитов(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ДатаДекларации",  ДатаДекларации);
	Результат.Вставить("РегистрационныйНомерДекларации", РегистрационныйНомерДекларации);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ХранилищеСистемныхНастроек.Удалить(
			"Документ.МаркировкаТоваровИСМП.Форма.УточнениеТаможеннойДекларации", "", ИмяПользователя);
	КонецЕсли;
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеРеквизитов(Отказ)
	
	ОчиститьСообщения();
	
	ШаблонСообщения = Нстр("ru='Поле ""%1"" не заполнено'");
	
	Если Не ЗначениеЗаполнено(РегистрационныйНомерДекларации) Тогда
		ТекстСообщения = СтрШаблон(ШаблонСообщения, "Номер регистрации ДТ");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "РегистрационныйНомерДекларации",, Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаДекларации) Тогда
		ТекстСообщения = СтрШаблон(ШаблонСообщения, "Дата регистрации ДТ");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "ДатаДекларации",, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти