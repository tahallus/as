
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураДанных = ДанныеЗаказаНаСайте(Параметры.Ссылка);
	
	ИнформацияПоЗаказу = СтруктураДанных.ИнформацияПоЗаказу;
	ИнформацияПоКонтрагенту = СтруктураДанных.ИнформацияПоКонтрагенту;
	
	Макет = Документы.ЗаказПокупателя.ПолучитьМакет("ИнформацияПоКонтрагенту");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = СтрШаблон(НСтр("ru = 'Данные контрагента по заказу с сайта №%1 от %2.'"),
		ИнформацияПоЗаказу.НомерНаСайте, Формат(ИнформацияПоЗаказу.ДатаНаСайте, "ДЛФ=DD"));
	Результат.Вывести(ОбластьМакета);
	
	Для каждого Строка Из ИнформацияПоКонтрагенту Цикл
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, Строка);
		Результат.Вывести(ОбластьМакета);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеЗаказаНаСайте(ЗаказПокупателя)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ЗаказыПокупателейССайта.НомерЗаказаНаСайте, """") КАК НомерНаСайте,
	|	ЕСТЬNULL(ЗаказыПокупателейССайта.ДатаЗаказаНаСайте, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаНаСайте
	|ИЗ
	|	Документ.ЗаказПокупателя КАК Заказ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаказыПокупателейССайта КАК ЗаказыПокупателейССайта
	|		ПО Заказ.Ссылка = ЗаказыПокупателейССайта.ЗаказПокупателя
	|ГДЕ
	|	Заказ.Ссылка = &ЗаказПокупателя
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнформацияПоКонтрагенту.Вид,
	|	ИнформацияПоКонтрагенту.Представление
	|ИЗ
	|	Документ.ЗаказПокупателя.ИнформацияПоКонтрагенту КАК ИнформацияПоКонтрагенту
	|ГДЕ
	|	ИнформацияПоКонтрагенту.Ссылка = &ЗаказПокупателя";
	
	Запрос.УстановитьПараметр("ЗаказПокупателя", ЗаказПокупателя);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ИнформацияПоЗаказу = МассивРезультатов[0].Выбрать();
	ИнформацияПоЗаказу.Следующий();
	
	ИнформацияПоКонтрагенту = МассивРезультатов[1].Выгрузить();
	
	СтруктураДанных = Новый Структура;
	
	СтруктураДанных.Вставить("ИнформацияПоЗаказу", ИнформацияПоЗаказу);
	СтруктураДанных.Вставить("ИнформацияПоКонтрагенту", ИнформацияПоКонтрагенту);
	
	Возврат СтруктураДанных;
	
КонецФункции

#КонецОбласти
