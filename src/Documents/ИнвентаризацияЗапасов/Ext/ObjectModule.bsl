#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка) 	
	
	
	Если ДанныеЗаполнения <> Неопределено И ДанныеЗаполнения.Свойство("СписокНоменклатуры") Тогда
		
		СписокНоменклатуры = ДанныеЗаполнения.СписокНоменклатуры;
		
		ДанныеЗаполнения.Удалить("СписокНоменклатуры");
		
		ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения); 
		
		Документы.ИнвентаризацияЗапасов.ЗаполнитьПоОстаткамНаСкладе(ЭтотОбъект, СписокНоменклатуры, Неопределено, Неопределено);		
		
		Возврат;
		
	КонецЕсли;	 
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения); 
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Серии номенклатуры
	Если СерииНоменклатурыУНФ.ИспользоватьСерииНоменклатурыОстатки() = Истина Тогда
		СерииНоменклатурыУНФ.ПроверкаЗаполненияСерийНоменклатуры(Отказ, Запасы, СерииНоменклатуры, СтруктурнаяЕдиница, ЭтотОбъект);
	КонецЕсли;
	
	НоменклатураВДокументахСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, Отказ, Истина);
		
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если (ЭтотОбъект.ПометкаУдаления) И (НЕ Ссылка.ПометкаУдаления) Тогда
		ПрослеживаемостьПереопределяемый.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
		// Запись наборов записей
		ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
		
		ПрослеживаемостьУНФ.УдалениеПроведенияПервичногоДокумента(Ссылка);
	
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПрослеживаемостьПереопределяемый.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	ПараметрыПроведения = Документы.ИнвентаризацияЗапасов.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПрослеживаемостьУНФ.ЗарегистрироватьПрослеживаемыйТовар(ПараметрыПроведения.Реквизиты);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли