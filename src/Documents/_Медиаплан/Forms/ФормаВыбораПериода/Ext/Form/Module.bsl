
&НаКлиенте
Процедура ВыборДатыПриАктивизацииДаты(Элемент)
	
	Если Не ЗначениеЗаполнено(ВыбраннаяДата) Тогда 
		ВыбраннаяДата = ВыборДаты;
	Иначе
		НачалоПериода = Мин(ВыбраннаяДата, ВыборДаты);
		КонецПериода = Макс(ВыбраннаяДата, ВыборДаты);
		ВыбраннаяДата = Неопределено;
	КонецЕсли;
	
	Элементы.ВыборДаты.Обновить();	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВыбраннаяДата = Неопределено;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "НачалоПериода, КонецПериода, Количество");
	ПересчитатьПараметры();
КонецПроцедуры

&НаКлиенте
Процедура ВыборДатыПриВыводеПериода(Элемент, ОформлениеПериода)
		
	Для Каждого ТекущаяДата Из ОформлениеПериода.Даты Цикл
		
		Если ТекущаяДата.Дата >= НачалоПериода И ТекущаяДата.Дата <= КонецПериода И Не ЗначениеЗаполнено(ВыбраннаяДата) Тогда 
			ТекущаяДата.ЦветФона = WebЦвета.ГолубойСоСтальнымОттенком;
			ПересчитатьПараметры();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьПараметры()

	КоличествоДней = (КонецПериода-НачалоПериода)/86400 + 1;
	КоличествоВДень = Окр(Количество/КоличествоДней,0);
	ПериодСтрокой = ПредставлениеПериода(НачалоПериода, КонецПериода, "L=ru_RU");

КонецПроцедуры

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	ПересчитатьПараметры();
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Если ЗначениеЗаполнено(НачалоПериода) и ЗначениеЗаполнено(КонецПериода) Тогда 
		Закрыть(ПолучитьТаблицуПериодов());
	Иначе 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выбран период";
		Сообщение.Сообщить();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)      
	
	РезультатВыбора = Неопределено;
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТаблицуПериодов()

	РезультатВыбора = Новый ТаблицаЗначений;
	РезультатВыбора.Колонки.Добавить("НачалоПериода");
	РезультатВыбора.Колонки.Добавить("КонецПериода");
	РезультатВыбора.Колонки.Добавить("Количество");
	
	НП = НачалоПериода;
	
	Пока Месяц(НП) <> Месяц(КонецПериода) Цикл
		
		НовыйПериод = РезультатВыбора.Добавить();
		НовыйПериод.НачалоПериода = НП;
		НовыйПериод.КонецПериода = НачалоДня(КонецМесяца(НП));
		НовыйПериод.Количество = ((НовыйПериод.КонецПериода-НовыйПериод.НачалоПериода)/86400 + 1)*КоличествоВДень;
		
		НП = КонецМесяца(НП) + 1;
		
	КонецЦикла;
	
	КоличествоРаспределено = РезультатВыбора.Итог("Количество");
	НовыйПериод = РезультатВыбора.Добавить();
	НовыйПериод.НачалоПериода = НП;
	НовыйПериод.КонецПериода = КонецПериода;
	НовыйПериод.Количество = Количество - КоличествоРаспределено;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(РезультатВыбора,ЭтаФорма.УникальныйИдентификатор);
	
	Возврат АдресХранилища;
	
КонецФункции
