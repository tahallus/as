#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "МедиапланОсновной";
	КомандаПечати.Представление = НСтр("ru = 'Медиаплан (осн.)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 10;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "МедиапланПрогноз";
	КомандаПечати.Представление = НСтр("ru = 'Медиаплан (доп.)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 20;

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "МедиапланПолный";
	КомандаПечати.Представление = НСтр("ru = 'Медиаплан (полн.)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 30;

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	Перем Ошибки;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "МедиапланОсновной");
	Если ПечатнаяФорма <> Неопределено Тогда
		
		ПечатнаяФорма.ТабличныйДокумент = Новый ТабличныйДокумент;
		ПечатнаяФорма.ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Медиаплан_МедиапланОсновной";
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ._Медиаплан.ПФ_MXL_Медиаплан";
		ПечатнаяФорма.СинонимМакета = НСтр("ru ='Медиаплан'");
		
		СформироватьМедиаплан(ПечатнаяФорма, МассивОбъектов, ОбъектыПечати, Ошибки, "Основной");
		
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "МедиапланПрогноз");
	Если ПечатнаяФорма <> Неопределено Тогда
		
		ПечатнаяФорма.ТабличныйДокумент = Новый ТабличныйДокумент;
		ПечатнаяФорма.ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Медиаплан_МедиапланПрогноз";
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ._Медиаплан.ПФ_MXL_Медиаплан";
		ПечатнаяФорма.СинонимМакета = НСтр("ru ='Медиаплан'");
		
		СформироватьМедиаплан(ПечатнаяФорма, МассивОбъектов, ОбъектыПечати, Ошибки, "Прогноз");
		
	КонецЕсли;

	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "МедиапланПолный");
	Если ПечатнаяФорма <> Неопределено Тогда
		
		ПечатнаяФорма.ТабличныйДокумент = Новый ТабличныйДокумент;
		ПечатнаяФорма.ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Медиаплан_МедиапланПолный";
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ._Медиаплан.ПФ_MXL_Медиаплан";
		ПечатнаяФорма.СинонимМакета = НСтр("ru ='Медиаплан'");
		
		СформироватьМедиаплан(ПечатнаяФорма, МассивОбъектов, ОбъектыПечати, Ошибки, "Полный");
		
	КонецЕсли;

КонецПроцедуры

Процедура СформироватьМедиаплан(ПечатнаяФорма, МассивОбъектов, ОбъектыПечати, Ошибки, Вариант)
	Перем ПервыйДокумент, НомерСтрокиНачало;
	
	ТабличныйДокумент = ПечатнаяФорма.ТабличныйДокумент;
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПечатнаяФорма.ПолныйПутьКМакету);
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок" + "|" + Вариант);
	ОбластьЗаголовок.Параметры.Партнер = МассивОбъектов[0].Партнер;
	ОбластьЗаголовок.Параметры.ОбщийПериод = Формат(МассивОбъектов[0].ДатаНачала, "ДФ=dd/MM/yy") + " - " + Формат(МассивОбъектов[0].ДатаОкончания, "ДФ=dd/MM/yy");
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	
	ОбластьОпцииЗаголовок = Макет.ПолучитьОбласть("ОпцииЗаголовок" + "|" + Вариант);
	ОбластьОпцииШапка = Макет.ПолучитьОбласть("ОпцииШапка" + "|" + Вариант);
	ОбластьОпцииСтрока = Макет.ПолучитьОбласть("ОпцииСтрока" + "|" + Вариант);
	ОбластьОпцииПодвал = Макет.ПолучитьОбласть("ОпцииПодвал" + "|" + Вариант);
	
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка" + "|" + Вариант);
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал" + "|" + Вариант);
	ОбластьИтоги = Макет.ПолучитьОбласть("Итоги" + "|" + Вариант);
	ОбластьИтоги1 = Макет.ПолучитьОбласть("Итоги1" + "|" + Вариант);
	
	ОбластьРазделитель = Макет.ПолучитьОбласть("Разделитель" + "|" + Вариант);
	ОбластьСсылка = Макет.ПолучитьОбласть("Ссылка" + "|" + Вариант);
	
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	_МедиапланСтрокиМедиаплана.Номенклатура._Продукт КАК Продукт,
		|	_МедиапланСтрокиМедиаплана.Номенклатура._Формат КАК Формат,
		|	_МедиапланСтрокиМедиаплана.Номенклатура._ПозицияНаСайте КАК ПозицияНаСайте,
		|	ПРЕДСТАВЛЕНИЕ(_МедиапланСтрокиМедиаплана.Номенклатура._ТипРазмещения) КАК ТипРазмещения,
		|	_МедиапланСтрокиМедиапланаРасшифровка.ЦенаРасчетная КАК Цена,
		|	ЕСТЬNULL(_МедиапланНаборыОпций.НомерСтроки, 0) КАК ОпцииНомер,
		|	ЕСТЬNULL(_МедиапланНаборыОпций.Коэффициент, 1) КАК ОпцииКоэффициент,
		|	_МедиапланСтрокиМедиапланаРасшифровка.СтавкаНДС КАК СтавкаНДС,
		|	_МедиапланСтрокиМедиапланаРасшифровка.ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
		|	_МедиапланСтрокиМедиапланаРасшифровка.Кликабельность КАК Кликабельность,
		|	_МедиапланСтрокиМедиапланаРасшифровка.НедельныйТрафик КАК НедельныйТрафик,
		|	_МедиапланСтрокиМедиапланаРасшифровка.Бонус КАК Бонус,
		|	_МедиапланСтрокиМедиапланаРасшифровка.КлючСвязиОпции КАК КлючСвязиОпции,
		|	_МедиапланСтрокиМедиапланаРасшифровка.ОпциональныйКоэффициент КАК ОпциональныйКоэффициент,
		|	_МедиапланСтрокиМедиапланаРасшифровка.СезонныйКоэффициент КАК СезонныйКоэффициент,
		|	_МедиапланСтрокиМедиапланаРасшифровка.ДатаНачала КАК ДатаНачала,
		|	_МедиапланСтрокиМедиапланаРасшифровка.ДатаОкончания КАК ДатаОкончания,
		|	_МедиапланСтрокиМедиапланаРасшифровка.Количество КАК Количество,
		|	_МедиапланСтрокиМедиапланаРасшифровка.БонусКоличество КАК БонусКоличество,
		|	_МедиапланСтрокиМедиапланаРасшифровка.Сумма КАК Сумма,
		|	_МедиапланСтрокиМедиапланаРасшифровка.Сумма + _МедиапланСтрокиМедиапланаРасшифровка.СуммаСкидкиНаценки КАК СуммаБезСкидок,
		|	_МедиапланСтрокиМедиапланаРасшифровка.БонусСумма КАК БонусСумма,
		|	_МедиапланСтрокиМедиапланаРасшифровка.СуммаСкидкиНаценки КАК СуммаСкидкиНаценки,
		|	_МедиапланСтрокиМедиапланаРасшифровка.СуммаНДС КАК СуммаНДС,
		|	_МедиапланСтрокиМедиапланаРасшифровка.Всего КАК Всего,
		|	_МедиапланСтрокиМедиапланаРасшифровка.Клики КАК Клики,
		|	_МедиапланСтрокиМедиапланаРасшифровка.ЦенаЗаКлик КАК ЦенаЗаКлик,
		|	_МедиапланСтрокиМедиапланаРасшифровка.ОбщийТрафик КАК ОбщийТрафик,
		|	_МедиапланСтрокиМедиапланаРасшифровка.ДоляОтОбщегоТрафика КАК ДоляОтОбщегоТрафика,
		|	_МедиапланСтрокиМедиапланаРасшифровка.БонусПоказов КАК БонусПоказов,
		|	_МедиапланСтрокиМедиапланаРасшифровка.Показов КАК Показов,
		|	_МедиапланСтрокиМедиапланаРасшифровка.ЦенаЗа1000Показов КАК ЦенаЗа1000Показов,
		|	_МедиапланСтрокиМедиаплана.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	_МедиапланСтрокиМедиапланаРасшифровка.БонусКоличество * _МедиапланСтрокиМедиапланаРасшифровка.ЦенаРасчетная КАК БонусСуммаБезСкидок
		|ИЗ
		|	Документ._Медиаплан.СтрокиМедиапланаРасшифровка КАК _МедиапланСтрокиМедиапланаРасшифровка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ._Медиаплан.СтрокиМедиаплана КАК _МедиапланСтрокиМедиаплана
		|		ПО _МедиапланСтрокиМедиапланаРасшифровка.Ссылка = _МедиапланСтрокиМедиаплана.Ссылка
		|			И _МедиапланСтрокиМедиапланаРасшифровка.КлючСвязи = _МедиапланСтрокиМедиаплана.КлючСвязи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ._Медиаплан.НаборыОпций КАК _МедиапланНаборыОпций
		|		ПО _МедиапланСтрокиМедиапланаРасшифровка.Ссылка = _МедиапланНаборыОпций.Ссылка
		|			И _МедиапланСтрокиМедиапланаРасшифровка.КлючСвязиОпции = _МедиапланНаборыОпций.КлючСвязиОпции
		|ГДЕ
		|	_МедиапланСтрокиМедиапланаРасшифровка.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", МассивОбъектов[0]);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	НомерСтроки = 1;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ОбластьСтрока.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
		ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
		ОбластьСтрока.Параметры.ОписаниеОпции = ?(ВыборкаДетальныеЗаписи.ОпцииНомер = 0,"","[" + ВыборкаДетальныеЗаписи.ОпцииНомер + "] - " + ВыборкаДетальныеЗаписи.ОпцииКоэффициент);
		ОбластьСтрока.Параметры.ПериодРазмещения = СформироватьСтрокуПериода(ВыборкаДетальныеЗаписи.ДатаНачала, ВыборкаДетальныеЗаписи.ДатаОкончания, ВыборкаДетальныеЗаписи.СезонныйКоэффициент);
 		ТабличныйДокумент.Вывести(ОбластьСтрока);
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	ДанныеПодвала = ПолучитьДанныеПодвала(МассивОбъектов[0], ТабличныйДокумент, ОбластьПодвал);

	ОбластьПодвал.Параметры.Заполнить(ДанныеПодвала);
	ТабличныйДокумент.Вывести(ОбластьПодвал);

	
	ДанныеСметы = ПолучитьСуммуСметы(МассивОбъектов[0]);
	Если ДанныеСметы = Неопределено Тогда 
		ОбластьИтоги.Параметры.ИтогоСумма = ДанныеПодвала.ИтогоСумма;
		ОбластьИтоги.Параметры.ИтогоСуммаНДС = ДанныеПодвала.ИтогоСуммаНДС;
		ОбластьИтоги.Параметры.ИтоговаяСтоимостьСНДС = ДанныеПодвала.ИтоговаяСтоимостьСНДС;
		ТабличныйДокумент.Вывести(ОбластьИтоги);
	Иначе 
		ОбластьИтоги1.Параметры.ИтогоСумма = ДанныеПодвала.ИтогоСумма;
		ОбластьИтоги1.Параметры.ИтогоСметаБезНДС = ДанныеСметы.СуммаБезНДС;
		ОбластьИтоги1.Параметры.ИтогоСуммаНДС = ДанныеПодвала.ИтогоСуммаНДС + ДанныеСметы.СуммаНДС;
		ОбластьИтоги1.Параметры.ИтоговаяСтоимостьСНДС = ДанныеПодвала.ИтоговаяСтоимостьСНДС + ДанныеСметы.Всего;
		ТабличныйДокумент.Вывести(ОбластьИтоги1);
	КонецЕсли;
	
	ВывестиСсылкиНаПлощадки(ТабличныйДокумент, ОбластьСсылка, МассивОбъектов[0]);

	ТабличныйДокумент.Вывести(ОбластьРазделитель);  
	
	ВывестиДанныеНаборовОпций(МассивОбъектов[0], ТабличныйДокумент, ОбластьОпцииШапка, ОбластьОпцииСтрока, ОбластьОпцииПодвал, ОбластьОпцииЗаголовок);
	
	
	
КонецПроцедуры // СформироватьМедиаплан()


Функция СформироватьСтрокуПериода(ДатаНачала, ДатаОкончания, СезонныйКоэффициент)
	
	КоличествоДней = (ДатаОкончания - ДатаНачала)/86400 + 1;
	ЦелоеКоличествоНедель = КоличествоДней % 7 = 0;
	Если ЦелоеКоличествоНедель Тогда 
		Количество = КоличествоДней/7; 
		КоличествоНаПечать = ПечатьДокументовУНФ.ФормаМножественногоЧисла("неделя", "недели", "недель", Количество);
	Иначе
		Количество = КоличествоДней;
		КоличествоНаПечать = ПечатьДокументовУНФ.ФормаМножественногоЧисла("день", "дня", "дней", КоличествоДней);
	КонецЕсли;
	
	СтрокаРезультат = Формат(ДатаНачала, "ДФ=dd/MM/yy") + " - " + Формат(ДатаОкончания, "ДФ=dd/MM/yy") + Символы.ПС + Количество + " " + КоличествоНаПечать + "/" + СезонныйКоэффициент; 	
	
	Возврат СокрЛП(СтрокаРезультат);

КонецФункции

Функция ПолучитьДанныеПоТаргетингу(Ссылка)
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	_МедиапланРазмещениеСтрокиМедиаплана.Ссылка КАК Ссылка,
		|	_МедиапланРазмещениеСтрокиМедиаплана.НомерСтроки КАК НомерСтроки,
		|	_МедиапланРазмещениеСтрокиМедиаплана.КлючСвязи КАК КлючСвязи,
		|	_МедиапланРазмещениеСтрокиМедиаплана.Номенклатура КАК Номенклатура,
		|	_МедиапланРазмещениеСтрокиМедиаплана.Площадка КАК Площадка,
		|	_МедиапланРазмещениеСтрокиМедиаплана.НеУчитыватьФакт КАК НеУчитыватьФакт,
		|	_МедиапланРазмещениеСтрокиМедиаплана.Баннер КАК Баннер,
		|	_МедиапланРазмещениеСтрокиМедиаплана.Количество КАК Количество
		|ИЗ
		|	Документ._МедиапланРазмещение.СтрокиМедиаплана КАК _МедиапланРазмещениеСтрокиМедиаплана
		|ИТОГИ ПО
		|	Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСсылка.Следующий() Цикл
		// Вставить обработку выборки ВыборкаСсылка
	
		ВыборкаДетальныеЗаписи = ВыборкаСсылка.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			// Вставить обработку выборки ВыборкаДетальныеЗаписи
		КонецЦикла;
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	

	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ИтоговыйКоэффициент = 1;
	Пока Выборка.Следующий() Цикл
		СтрокаРезультат = СтрокаРезультат + Выборка.Таргетинг + " - " + Выборка.Коэффициент + Символы.ПС;
		ИтоговыйКоэффициент = ИтоговыйКоэффициент * Выборка.Коэффициент;
	КонецЦикла;                                                                                                                                                                                  
	
	СтрокаРезультат = СтрокаРезультат + "Общий коэф. " + ИтоговыйКоэффициент;
	
	Возврат СокрЛП(СтрокаРезультат);
	
КонецФункции

Процедура ВывестиДанныеНаборовОпций(Ссылка, ТабличныйДокумент, ОбластьОпцииШапка, ОбластьОпцииСтрока, ОбластьОпцииПодвал, ОбластьОпцииЗаголовок)

	
	Запрос = Новый Запрос;
	
	//Установка значений параметров
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	_МедиапланСтрокиМедиапланаРасшифровка.КлючСвязиОпции КАК КлючСвязиОпции
	|ПОМЕСТИТЬ ВТ_ОпцииНаборы
	|ИЗ
	|	Документ._Медиаплан.СтрокиМедиапланаРасшифровка КАК _МедиапланСтрокиМедиапланаРасшифровка
	|ГДЕ
	|	_МедиапланСтрокиМедиапланаРасшифровка.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	_МедиапланСтрокиМедиапланаРасшифровка.КлючСвязиОпции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_МедиапланНаборыОпций.НомерСтроки КАК НомерНабора,
	|	_МедиапланНаборыОпций.Коэффициент КАК КоэффициентНабора,
	|	_МедиапланНаборыОпцийРасшифровка.НомерСтроки КАК НомерСтроки,
	|	_МедиапланНаборыОпцийРасшифровка.Опция.Наименование КАК Опция,
	|	_МедиапланНаборыОпцийРасшифровка.Уточнение КАК Уточнение,
	|	_МедиапланНаборыОпцийРасшифровка.Коэффициент КАК КоэффициентОпции
	|ИЗ
	|	Документ._Медиаплан.НаборыОпцийРасшифровка КАК _МедиапланНаборыОпцийРасшифровка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ._Медиаплан.НаборыОпций КАК _МедиапланНаборыОпций
	|		ПО _МедиапланНаборыОпцийРасшифровка.Ссылка = _МедиапланНаборыОпций.Ссылка
	|			И _МедиапланНаборыОпцийРасшифровка.КлючСвязиОпции = _МедиапланНаборыОпций.КлючСвязиОпции
	|ГДЕ
	|	_МедиапланНаборыОпцийРасшифровка.КлючСвязиОпции В
	|			(ВЫБРАТЬ
	|				ВТ_ОпцииНаборы.КлючСвязиОпции КАК КлючСвязиОпции
	|			ИЗ
	|				ВТ_ОпцииНаборы КАК ВТ_ОпцииНаборы)
	|	И _МедиапланНаборыОпцийРасшифровка.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерНабора,
	|	НомерСтроки
	|ИТОГИ
	|	МАКСИМУМ(КоэффициентНабора)
	|ПО
	|	НомерНабора";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	Иначе
		ТабличныйДокумент.Вывести(ОбластьОпцииЗаголовок);
	КонецЕсли;
	
	ВыборкаНабор = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНабор.Следующий() Цикл
		ОбластьОпцииШапка.Параметры.НомерНабора = "[" + ВыборкаНабор.НомерНабора + "]";
		ТабличныйДокумент.Вывести(ОбластьОпцииШапка);
		ВыборкаСостав = ВыборкаНабор.Выбрать();
		Пока ВыборкаСостав.Следующий() Цикл
			ОбластьОпцииСтрока.Параметры.Заполнить(ВыборкаСостав);
			Уточнение = СокрЛП(ВыборкаСостав.Уточнение);
			ОбластьОпцииСтрока.Параметры.ОпцияОписание = ВыборкаСостав.Опция + ?(ПустаяСтрока(Уточнение), "", " [" + Уточнение + "]");
			ТабличныйДокумент.Вывести(ОбластьОпцииСтрока);
		КонецЦикла;
		ОбластьОпцииПодвал.Параметры.Заполнить(ВыборкаНабор);
		ТабличныйДокумент.Вывести(ОбластьОпцииПодвал);
	КонецЦикла;

КонецПроцедуры

Функция ПолучитьДанныеПодвала(Ссылка, ТабличныйДокумент, ОбластьПодвал)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(_МедиапланСтрокиМедиапланаРасшифровка.Сумма) КАК ИтогоСумма,
		|	СУММА(_МедиапланСтрокиМедиапланаРасшифровка.Сумма + _МедиапланСтрокиМедиапланаРасшифровка.СуммаСкидкиНаценки + _МедиапланСтрокиМедиапланаРасшифровка.ЦенаРасчетная * _МедиапланСтрокиМедиапланаРасшифровка.БонусКоличество) КАК ИтогоСуммаБезСкидок,
		|	СУММА(_МедиапланСтрокиМедиапланаРасшифровка.СуммаНДС) КАК ИтогоСуммаНДС,
		|	СУММА(_МедиапланСтрокиМедиапланаРасшифровка.Всего) КАК ИтоговаяСтоимостьСНДС,
		|	СУММА(_МедиапланСтрокиМедиапланаРасшифровка.Показов) КАК ИтогоПоказов,
		|	СУММА(_МедиапланСтрокиМедиапланаРасшифровка.Клики) КАК ИтогоКлики
		|ПОМЕСТИТЬ ВТ_Итоги
		|ИЗ
		|	Документ._Медиаплан.СтрокиМедиапланаРасшифровка КАК _МедиапланСтрокиМедиапланаРасшифровка
		|ГДЕ
		|	_МедиапланСтрокиМедиапланаРасшифровка.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Итоги.ИтогоСумма КАК ИтогоСумма,
		|	ВТ_Итоги.ИтогоСуммаБезСкидок КАК ИтогоСуммаБезСкидок,
		|	ВТ_Итоги.ИтогоСуммаНДС КАК ИтогоСуммаНДС,
		|	ВТ_Итоги.ИтоговаяСтоимостьСНДС КАК ИтоговаяСтоимостьСНДС,
		|	ВТ_Итоги.ИтогоПоказов КАК ИтогоПоказов,
		|	ВТ_Итоги.ИтогоКлики КАК ИтогоКлики,
		|	ВЫРАЗИТЬ((ВТ_Итоги.ИтогоСуммаБезСкидок - ВТ_Итоги.ИтогоСумма) / ВТ_Итоги.ИтогоСуммаБезСкидок * 100 КАК ЧИСЛО(15, 0)) КАК ИтогоСкидка,
		|	ВЫРАЗИТЬ(ВТ_Итоги.ИтогоСумма / ВТ_Итоги.ИтогоПоказов * 1000 КАК ЧИСЛО(15, 2)) КАК ИтогоЦенаЗа1000Показов,
		|	ВЫРАЗИТЬ(ВТ_Итоги.ИтогоКлики / ВТ_Итоги.ИтогоПоказов * 100 КАК ЧИСЛО(15, 2)) КАК ИтогоКликабельность,
		|	ВЫРАЗИТЬ(ВТ_Итоги.ИтогоСумма / ВТ_Итоги.ИтогоКлики КАК ЧИСЛО(15, 2)) КАК ИтогоЦенаЗаКлик
		|ИЗ
		|	ВТ_Итоги КАК ВТ_Итоги";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить()[0];
	
	Возврат РезультатЗапроса;
	//
	//ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	//
	//Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
	//	ОбластьПодвал.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
	//	ТабличныйДокумент.Вывести(ОбластьПодвал);
	//КонецЦикла;

КонецФункции

Процедура ВывестиСсылкиНаПлощадки(ТабличныйДокумент, ОбластьСсылка, СсылкаНаОбъект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура_Состав.Площадка КАК Площадка
		|ПОМЕСТИТЬ ВТ_Площадки
		|ИЗ
		|	Справочник.Номенклатура._Состав КАК Номенклатура_Состав
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ._Медиаплан.СтрокиМедиаплана КАК _МедиапланСтрокиМедиаплана
		|		ПО Номенклатура_Состав.Ссылка = _МедиапланСтрокиМедиаплана.Номенклатура
		|ГДЕ
		|	_МедиапланСтрокиМедиаплана.Ссылка = &СсылкаНаОбъект
		|
		|СГРУППИРОВАТЬ ПО
		|	Номенклатура_Состав.Площадка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Площадки.Площадка.Представление КАК ПлощадкаПредставление,
		|	ВТ_Площадки.Площадка.СсылкаНаДемо КАК ПлощадкаСсылкаНаДемо
		|ИЗ
		|	ВТ_Площадки КАК ВТ_Площадки";
	
	Запрос.УстановитьПараметр("СсылкаНаОбъект", СсылкаНаОбъект);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ОбластьСсылка.Параметры.Площадка = ВыборкаДетальныеЗаписи.ПлощадкаПредставление;
		ОбластьСсылка.Параметры.ПлощадкаСсылкаНаДемо = ВыборкаДетальныеЗаписи.ПлощадкаСсылкаНаДемо;
		ТабличныйДокумент.Вывести(ОбластьСсылка);
	КонецЦикла;

КонецПроцедуры

Функция ПолучитьСуммуСметы(СсылкаНаОбъект)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	_Смета.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_Смета
		|ИЗ
		|	Документ._Медиаплан КАК _Медиаплан
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ._ЗаявкаКлиента КАК _ЗаявкаКлиента
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ._Смета КАК _Смета
		|			ПО _ЗаявкаКлиента.Ссылка = _Смета.ДокументОснование
		|		ПО _Медиаплан.ДокументОснование = _ЗаявкаКлиента.Ссылка
		|ГДЕ
		|	_Медиаплан.Ссылка = &СсылкаНаОбъект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(_СметаЗапасы.Стоимость) КАК Стоимость,
		|	ВТ_Смета.Ссылка.СтавкаНДС КАК СтавкаНДС,
		|	ВТ_Смета.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС
		|ИЗ
		|	ВТ_Смета КАК ВТ_Смета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ._Смета.Запасы КАК _СметаЗапасы
		|		ПО ВТ_Смета.Ссылка = _СметаЗапасы.Ссылка
		|			И ВТ_Смета.Ссылка.ОсновнойВариантКП = _СметаЗапасы.НомерВариантаКП
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Смета.Ссылка.СтавкаНДС,
		|	ВТ_Смета.Ссылка.СуммаВключаетНДС";
	
	Запрос.УстановитьПараметр("СсылкаНаОбъект", СсылкаНаОбъект);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 

		Сумма = ВыборкаДетальныеЗаписи.Стоимость;
		
		СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(ВыборкаДетальныеЗаписи.СтавкаНДС);
		
		СуммаВключаетНДС = ВыборкаДетальныеЗаписи.СуммаВключаетНДС;
		
		СуммаНДС = ?(СуммаВключаетНДС, Сумма - (Сумма) / ((СтавкаНДС + 100) / 100), Сумма * СтавкаНДС / 100);
		
		Всего = Сумма + ?(СуммаВключаетНДС, 0, СуммаНДС);
		
		СуммаБезНДС = Всего - СуммаНДС;
		
		Возврат Новый Структура("СуммаБезНДС, СуммаНДС, Всего", СуммаБезНДС, СуммаНДС, Всего);
	Иначе
		Возврат Неопределено;	
	КонецЕсли;

КонецФункции

#КонецОбласти

#КонецЕсли