#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет необходимость перепроведения заказа при смене состояния.
// 
// Возвращаемое значение:
//  РежимЗаписиДокумента - Рекомендуемый режим записи документа.
//
Функция РежимЗаписиПриСменеСостояния() Экспорт
	
	Возврат СостоянияЗаказов.РежимЗаписиДокументаПриСменеСостояния(ЭтотОбъект);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	УправлениеНебольшойФирмойЭлектронныеДокументыСервер.ОчиститьДатуНомерВходящегоДокумента(ЭтотОбъект);
	
	СостояниеЗаказа = ЗаполнениеОбъектовУНФ.ПолучитьСостояниеЗаказаПоставщику();
	ВариантЗавершения = Перечисления.ВариантыЗавершенияЗаказа.ПустаяСсылка();
	УдалитьЗакрыт = Ложь;
	Событие = Документы.Событие.ПустаяСсылка();
	РезервироватьДенежныеСредства = Ложь;
	УчетПотребностиПоСкладам = ПолучитьФункциональнуюОпцию("УчетПотребностиПоСкладам");
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("Структура")] = "ЗаполнитьПоСтруктуре";
	СтратегияЗаполнения[Тип("ДокументСсылка.ЗаказПокупателя")] = "ЗаполнитьПоЗаказуПокупателя";
	СтратегияЗаполнения[Тип("ДокументСсылка.ЗаказНаПроизводство")] = "ЗаполнитьПоЗаказуНаПроизводство";
	СтратегияЗаполнения[Тип("СправочникСсылка.ДоговорыКонтрагентов")] = "ЗаполнитьПоДоговоруКонтрагента";
	СтратегияЗаполнения[Тип("ДокументСсылка.СчетНаОплатуПоставщика")] = "ЗаполнитьПоСчетуНаОплатуПоставщика";
	
	ИсключаяСвойства = "СостояниеЗаказа,ВариантЗавершения";
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения, ИсключаяСвойства);
	
	ДозаполнитьПоУмолчанию(ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Положение даты поступления
	Если ПоложениеДатыПоступления <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
			СтрокаТабличнойЧасти.ДатаПоступления = ДатаПоступления;
		КонецЦикла;
	Иначе
		ДатаПоступления = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(Запасы, "ДатаПоступления");
	КонецЕсли;
	
	// Положение заказа покупателя
	Если ПоложениеЗаказаПокупателя <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = ЗаказПокупателя;
		КонецЦикла;
		Для каждого СтрокаТабличнойЧасти Из Материалы Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = ЗаказПокупателя;
		КонецЦикла;
	Иначе
		Если Запасы.Количество() > 0 Тогда
			ЗаказПокупателя = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(Запасы, "ЗаказПокупателя");
		ИначеЕсли Материалы.Количество() > 0 Тогда
			ЗаказПокупателя = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(Материалы, "ЗаказПокупателя");
		Иначе
			ЗаказПокупателя = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	// Положение склада
	Если ПоложениеСклада <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти
		ИЛИ ВидОперации = Перечисления.ВидыОперацийЗаказПоставщику.ЗаказНаПереработку Тогда
		ЗаполнитьСтруктурнуюЕдиницуРезерваВТЧ();
	Иначе
		Если Запасы.Количество() > 0 Тогда
			СтруктураПолей = СтруктурнаяЕдиницаДляШапки(Запасы);
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураПолей);
		ИначеЕсли Материалы.Количество() > 0 Тогда
			СтруктураПолей = СтруктурнаяЕдиницаДляШапки(Материалы);
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураПолей);
		Иначе
			СтруктурнаяЕдиницаРезерв = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
		КонецЕсли; 
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Контрагент)
	И НЕ Контрагент.ВестиРасчетыПоДоговорам
	И НЕ ЗначениеЗаполнено(Договор) Тогда
		Договор = Справочники.ДоговорыКонтрагентов.ДоговорПоУмолчанию(Контрагент);
	КонецЕсли;
	
	СуммаДокумента = Запасы.Итог("Всего");
	
	// ИнтеграцияГИСМ
	ЕстьКиЗГИСМ = ИнтеграцияГИСМУНФ.ЕстьКиЗГИСМ(Запасы);
	// Конец ИнтеграцияГИСМ
	
	СостоянияЗаказов.ПередЗаписьюЗаказа(ЭтотОбъект);
	
	ПривестиДанныеКСогласованномуСостоянию();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СостоянияЗаказов.ПриЗаписиЗаказа(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.ЗаказПоставщику.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ПроведениеДокументовУНФ.ОтразитьДвижения("ГрафикДвиженияЗапасов", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗаказыПоставщикам", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ПотребностьВЗапасах", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("РазмещениеЗаказов", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("Запасы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ПлатежныйКалендарь", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ОплатаСчетовИЗаказов", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДенежныеСредстваВРезерве", ТаблицыДляДвижений, Движения, Отказ);
	
	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	// Контроль
	Документы.ЗаказПоставщику.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для удаления проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль
	Документы.ЗаказПоставщику.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ, Истина);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Контрагент.ВестиРасчетыПоДоговорам Тогда
		ЗаполнениеОбъектовУНФ.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "Договор");
	КонецЕсли;
	
	УчетПотребностиПоСкладам = ПолучитьФункциональнуюОпцию("УчетПотребностиПоСкладам");
	Если ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Если ВидОперации = Перечисления.ВидыОперацийЗаказПоставщику.ЗаказНаПереработку Тогда
			Если НЕ ЗначениеЗаполнено(СтруктурнаяЕдиницаРезерв) И УчетПотребностиПоСкладам Тогда
				ТекстСообщения = НСтр("ru = 'Не заполнен склад резерва.'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СтруктурнаяЕдиницаРезерв", , Отказ);
			КонецЕсли;
			Для каждого СтрокаМатериалы Из Материалы Цикл
				Если НЕ ЗначениеЗаполнено(СтрокаМатериалы.СтруктурнаяЕдиницаРезерв) И УчетПотребностиПоСкладам Тогда
					ТекстСообщения = СтрШаблон(НСтр(
						"ru = 'Не заполнен склад резерва в строке %1 списка ""Материалы в переработку"".'"),
						СтрокаМатериалы.НомерСтроки);
					КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Материалы", СтрокаМатериалы.НомерСтроки,
						"СтруктурнаяЕдиницаРезерв");
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
				КонецЕсли;
			КонецЦикла;
		Иначе
			ПодходящаяНоменклатура = НоменклатураВДокументахСервер.НоменклатураТабличнойЧастиПоТипу(
				ЭтотОбъект, Перечисления.ТипыНоменклатуры.Запас);
			Для каждого СтрокаЗапасы Из Запасы Цикл
				Если ПодходящаяНоменклатура.Найти(СтрокаЗапасы.Номенклатура) = Неопределено Тогда
					Продолжить;	
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(СтрокаЗапасы.СтруктурнаяЕдиницаРезерв) И УчетПотребностиПоСкладам Тогда
					ТекстСообщения = СтрШаблон(НСтр(
						"ru = 'Не заполнен склад резерва в строке %1 списка ""Товары и услуги"".'"),
						СтрокаЗапасы.НомерСтроки);
					КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки,
						"СтруктурнаяЕдиницаРезерв");
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		Если (Материалы.Итог("Резерв") > 0 ИЛИ УчетПотребностиПоСкладам)
			И НЕ ЗначениеЗаполнено(СтруктурнаяЕдиницаРезерв) Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнен склад резерва.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СтруктурнаяЕдиницаРезерв", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	
	Если ЗапланироватьОплату
	   И ТипДенежныхСредств = Перечисления.ТипыДенежныхСредств.Безналичные Тогда
	   
		ЗаполнениеОбъектовУНФ.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "Касса");
		
	ИначеЕсли ЗапланироватьОплату
	   И ТипДенежныхСредств = Перечисления.ТипыДенежныхСредств.Наличные Тогда
	   
		ЗаполнениеОбъектовУНФ.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "БанковскийСчет");
		
	Иначе
		
		ЗаполнениеОбъектовУНФ.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "Касса");
		ЗаполнениеОбъектовУНФ.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "БанковскийСчет");
		
	КонецЕсли;
	
	Если ЗапланироватьОплату
	   И ПлатежныйКалендарь.Количество() = 1
	   И НЕ ЗначениеЗаполнено(ПлатежныйКалендарь[0].ДатаОплаты) Тогда
		
		ТекстСообщения = НСтр("ru = 'Поле ""Дата оплаты"" не заполнено.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ДатаОплаты", , Отказ);
		ЗаполнениеОбъектовУНФ.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты,
			"ПлатежныйКалендарь.ДатаОплаты");
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("РезервированиеЗапасов")
		И ВидОперации = Перечисления.ВидыОперацийЗаказПоставщику.ЗаказНаПереработку Тогда
		
		Для каждого СтрокаМатериалы Из Материалы Цикл
			
			Если СтрокаМатериалы.Резерв > СтрокаМатериалы.Количество Тогда
				
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'В строке №%1 табл. части ""Материалы в переработку"" количество позиций
					|к списанию из резерва превышает общее количество материалов.'"),
					СтрокаМатериалы.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Материалы",
					СтрокаМатериалы.НомерСтроки, "Резерв");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Скидка 100%.
	ЕстьРучныеСкидки = ПолучитьФункциональнуюОпцию("ИспользоватьРучныеСкидкиНаценкиПродажи");
	Если ЕстьРучныеСкидки Тогда
		Для каждого СтрокаЗапасы Из Запасы Цикл
			ТекСумма = Окр(СтрокаЗапасы.Цена * СтрокаЗапасы.Количество, 2);
			ТекСуммаРучнойСкидки = ?(ЕстьРучныеСкидки, СтрокаЗапасы.СуммаСкидкиНаценки, 0);
			Если СтрокаЗапасы.ПроцентСкидкиНаценки <> 100 И ТекСуммаРучнойСкидки < ТекСумма
				И НЕ ЗначениеЗаполнено(СтрокаЗапасы.Сумма) Тогда
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'Не заполнена колонка ""Сумма"" в строке %1 списка ""Товары и услуги"".'"),
					СтрокаЗапасы.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки,
					"Сумма");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ПоложениеДатыПоступления = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ДатаПоступления"));
	Иначе
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Запасы.ДатаПоступления"));
	КонецЕсли;
	
	Если Не СостояниеЗаказа = Справочники.СостоянияЗаказовНаПроизводство.Завершен Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ВариантЗавершения"));
	КонецЕсли;
	
	НоменклатураВДокументахСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЗаполненияДокумента

// АПК:299-выкл процедуры вызываются в ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент()

Процедура ЗаполнитьПоЗаказуПокупателя(ДокументСсылкаЗаказПокупателя) Экспорт
	
	Если Не ЗначениеЗаполнено(ДокументСсылкаЗаказПокупателя) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДокументСсылкаЗаказПокупателя) = Тип("Массив") Тогда
		МассивЗаказов = ДокументСсылкаЗаказПокупателя;
	Иначе
		МассивЗаказов = Новый Массив;
		МассивЗаказов.Добавить(ДокументСсылкаЗаказПокупателя);
	КонецЕсли;
	Если МассивЗаказов.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка документов
	ИменаРеквизитов = Новый Массив;
	ИменаРеквизитов.Добавить("Организация, Ссылка, ВидОперации, Старт, ДатаОтгрузки, СостояниеЗаказа, Проведен");
	ИменаРеквизитов.Добавить("ОжидаетсяВыборВариантаКП, ПоложениеДатыОтгрузки, ОжидаетсяВыборВариантаКП");
	ИменаРеквизитов.Добавить("СтруктурнаяЕдиницаРезерв, ПоложениеСклада");
	ИменаРеквизитовСтрокой = СтрСоединить(ИменаРеквизитов, ", ");
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивЗаказов, ИменаРеквизитовСтрокой);
	Для каждого КлючИЗначение Из ЗначенияРеквизитов Цикл
		Документы.ЗаказПокупателя.ПроверитьВозможностьВводаНаОснованииЗаказаПокупателя(КлючИЗначение.Значение.Ссылка, КлючИЗначение.Значение);
	КонецЦикла;
	
	// Шапка
	РеквизитыЗаказа = КлючИЗначение.Значение;
	Организация = РеквизитыЗаказа.Организация;
	Если МассивЗаказов.Количество() = 1 Тогда
		ЗаказПокупателя = РеквизитыЗаказа.Ссылка;
		Если РеквизитыЗаказа.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд Тогда
			ДатаПоступления = РеквизитыЗаказа.Старт;
			ПоложениеДатыПоступления = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
		Иначе
			ДатаПоступления = РеквизитыЗаказа.ДатаОтгрузки;
			ПоложениеДатыПоступления = РеквизитыЗаказа.ПоложениеДатыОтгрузки;
		КонецЕсли;
		СтруктурнаяЕдиницаРезерв = РеквизитыЗаказа.СтруктурнаяЕдиницаРезерв;
		ПоложениеСклада = РеквизитыЗаказа.ПоложениеСклада;
	Иначе
		ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
		ПоложениеДатыПоступления = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
		ПоложениеСклада = ?(ПолучитьФункциональнуюОпцию("РазрешитьСкладыВТабличныхЧастях"), 
			Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти, Перечисления.ПоложениеРеквизитаНаФорме.ВШапке);
	КонецЕсли; 
	
	// Заполнение табличной части.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Документы.ЗаказПокупателя.ДобавитьТаблицуЗапасыВМенеджерВременныхТаблиц(МассивЗаказов, Запрос.МенеджерВременныхТаблиц, Ложь);
	
	Если ПолучитьФункциональнуюОпцию("РезервированиеЗапасов") Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
		|	ЗапасыОстатки.Характеристика КАК Характеристика,
		|	ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ЗапасыОстатки.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры
		|ПОМЕСТИТЬ ЗаказыОстатки
		|ИЗ
		|	РегистрНакопления.Запасы.Остатки(
		|			,
		|			ЗаказПокупателя В (&МассивЗаказов)
		|				И Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))) КАК ЗапасыОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказыПокупателейОбороты.ЗаказПокупателя,
		|	ЗаказыПокупателейОбороты.Номенклатура,
		|	ЗаказыПокупателейОбороты.Характеристика,
		|	ЗаказыПокупателейОбороты.КоличествоРасход,
		|	ЗаказыПокупателейОбороты.Номенклатура.ТипНоменклатуры
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей.Обороты(, , Период, ЗаказПокупателя В (&МассивЗаказов)) КАК ЗаказыПокупателейОбороты
		|ГДЕ
		|	ЗаказыПокупателейОбороты.КоличествоРасход > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РазмещениеОстатки.ЗаказПокупателя,
		|	РазмещениеОстатки.Номенклатура,
		|	РазмещениеОстатки.Характеристика,
		|	РазмещениеОстатки.КоличествоОстаток,
		|	РазмещениеОстатки.Номенклатура.ТипНоменклатуры
		|ИЗ
		|	РегистрНакопления.РазмещениеЗаказов.Остатки(
		|			,
		|			ЗаказПокупателя В (&МассивЗаказов)
		|				И Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))) КАК РазмещениеОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвиженияДокументаРазмещениеЗаказов.ЗаказПокупателя,
		|	ДвиженияДокументаРазмещениеЗаказов.Номенклатура,
		|	ДвиженияДокументаРазмещениеЗаказов.Характеристика,
		|	ВЫБОР
		|		КОГДА ДвиженияДокументаРазмещениеЗаказов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			ТОГДА ЕСТЬNULL(ДвиженияДокументаРазмещениеЗаказов.Количество, 0)
		|		ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаРазмещениеЗаказов.Количество, 0)
		|	КОНЕЦ,
		|	ДвиженияДокументаРазмещениеЗаказов.Номенклатура.ТипНоменклатуры
		|ИЗ
		|	РегистрНакопления.РазмещениеЗаказов КАК ДвиженияДокументаРазмещениеЗаказов
		|ГДЕ
		|	ДвиженияДокументаРазмещениеЗаказов.Регистратор = &Ссылка
		|	И ДвиженияДокументаРазмещениеЗаказов.ЗаказПокупателя В(&МассивЗаказов)";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказПоставщикуЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ЗаказПоставщикуЗапасы.Номенклатура КАК Номенклатура,
		|	ЗаказПоставщикуЗапасы.Характеристика КАК Характеристика,
		|	ЗаказПоставщикуЗапасы.Количество * ВЫБОР
		|		КОГДА ЗаказПоставщикуЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
		|			ТОГДА ВЫРАЗИТЬ(ЗаказПоставщикуЗапасы.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК КоличествоОстаток,
		|	ЗаказПоставщикуЗапасы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры
		|ПОМЕСТИТЬ ЗаказыОстатки
		|ИЗ
		|	Документ.ЗаказПоставщику.Запасы КАК ЗаказПоставщикуЗапасы
		|ГДЕ
		|	ЗаказПоставщикуЗапасы.ЗаказПокупателя В(&МассивЗаказов)
		|	И ЗаказПоставщикуЗапасы.Ссылка <> &Ссылка
		|	И ЗаказПоставщикуЗапасы.Ссылка.Проведен";
		
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Запрос.Текст = 
	Справочники.СтавкиНДС.ПолучитьТекстЗапросаСозданияВТСтавкиНДС() +
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ЗаказыОстатки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ЗаказыОстатки.Характеристика
	|	КОНЕЦ КАК Характеристика,
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	|	ЗаказыОстатки.ТипНоменклатуры КАК ТипНоменклатуры
	|ИЗ
	|	ЗаказыОстатки КАК ЗаказыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыОстатки.ЗаказПокупателя,
	|	ЗаказыОстатки.Номенклатура,
	|	ЗаказыОстатки.ТипНоменклатуры,
	|	ВЫБОР
	|		КОГДА ЗаказыОстатки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ЗаказыОстатки.Характеристика
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПокупателяЗапасы.Ссылка КАК ЗаказПокупателя,
	|	Шапка.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА Шапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд)
	|			ТОГДА Шапка.Старт
	|		ИНАЧЕ ЗаказПокупателяЗапасы.ДатаОтгрузки
	|	КОНЕЦ КАК ДатаПоступления,
	|	МИНИМУМ(ЗаказПокупателяЗапасы.НомерСтроки) КАК НомерСтроки,
	|	ЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ЗаказПокупателяЗапасы.Характеристика
	|	КОНЕЦ КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА 1
	|		ИНАЧЕ ЗаказПокупателяЗапасы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Коэффициент,
	|	ЗаказПокупателяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПокупателяЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ЗаказПокупателяЗапасы.Количество) КАК Количество,
	|	СУММА(ЗаказПокупателяЗапасы.КоличествоСобрано) КАК КоличествоСобрано,
	|	ЗаказПокупателяЗапасы.ЕстьСборка КАК ЕстьСборка,
	|	ЗаказПокупателяЗапасы.Спецификация КАК Спецификация,
	|	ВЫБОР
	|		КОГДА НЕ ЗаказПокупателяЗапасы.ТипНоменклатурыЗапас
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|		КОГДА Шапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаПереработку)
	|			ТОГДА Шапка.СтруктурнаяЕдиницаРезерв
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|				ИЛИ Шапка.УчетПотребностиПоСкладам
	|			ТОГДА ЗаказПокупателяЗапасы.СтруктурнаяЕдиницаРезерв
	|		ИНАЧЕ ЗаказПокупателяЗапасы.Номенклатура.Склад
	|	КОНЕЦ КАК СтруктурнаяЕдиницаРезерв,
	|	ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры
	|ИЗ
	|	ВТЗаказПокупателяЗапасы КАК ЗаказПокупателяЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПокупателя КАК Шапка
	|		ПО ЗаказПокупателяЗапасы.Ссылка = Шапка.Ссылка,
	|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
	|ГДЕ
	|	ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|	И ЗаказПокупателяЗапасы.Номенклатура.СпособПополнения <> ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство)
	|	И (ЗаказПокупателяЗапасы.Спецификация = ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|			ИЛИ ЗаказПокупателяЗапасы.Номенклатура.СпособПополнения = ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Переработка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказПокупателяЗапасы.Ссылка,
	|	Шапка.Организация,
	|	ЗаказПокупателяЗапасы.Номенклатура,
	|	ЗаказПокупателяЗапасы.ЕдиницаИзмерения,
	|	ЗаказПокупателяЗапасы.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА 1
	|		ИНАЧЕ ЗаказПокупателяЗапасы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Шапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд)
	|			ТОГДА Шапка.Старт
	|		ИНАЧЕ ЗаказПокупателяЗапасы.ДатаОтгрузки
	|	КОНЕЦ,
	|	ЗаказПокупателяЗапасы.Спецификация,
	|	ВЫБОР
	|		КОГДА НЕ ЗаказПокупателяЗапасы.ТипНоменклатурыЗапас
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|		КОГДА Шапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаПереработку)
	|			ТОГДА Шапка.СтруктурнаяЕдиницаРезерв
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|				ИЛИ Шапка.УчетПотребностиПоСкладам
	|			ТОГДА ЗаказПокупателяЗапасы.СтруктурнаяЕдиницаРезерв
	|		ИНАЧЕ ЗаказПокупателяЗапасы.Номенклатура.Склад
	|	КОНЕЦ,
	|	ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры,
	|	ЗаказПокупателяЗапасы.ЕстьСборка,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ЗаказПокупателяЗапасы.Характеристика
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказПокупателя,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказПокупателя.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ЗаказНаряды
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.Ссылка В(&МассивЗаказов)
	|	И ЗаказПокупателя.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПокупателяМатериалы.Ссылка КАК ЗаказПокупателя,
	|	Шапка.Организация КАК Организация,
	|	ЗаказПокупателяМатериалы.СтруктурнаяЕдиницаРезерв КАК СтруктурнаяЕдиницаРезерв,
	|	Шапка.Старт КАК ДатаПоступления,
	|	МИНИМУМ(ЗаказПокупателяМатериалы.НомерСтроки) КАК НомерСтроки,
	|	ЗаказПокупателяМатериалы.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяМатериалы.Характеристика КАК Характеристика,
	|	ЗаказПокупателяМатериалы.Партия КАК Партия,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяМатериалы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА 1
	|		ИНАЧЕ ЗаказПокупателяМатериалы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Коэффициент,
	|	ЗаказПокупателяМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ВТСтавкиНоменклатура.СтавкаНДС ЕСТЬ NULL
	|			ТОГДА ВТСтавкиОрганизация.СтавкаНДС
	|		ИНАЧЕ ВТСтавкиНоменклатура.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДС,
	|	СУММА(ЗаказПокупателяМатериалы.Количество) КАК Количество,
	|	ЗаказПокупателяМатериалы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры
	|ИЗ
	|	Документ.ЗаказПокупателя.Материалы КАК ЗаказПокупателяМатериалы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПокупателя КАК Шапка
	|		ПО ЗаказПокупателяМатериалы.Ссылка = Шапка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНДС КАК ВТСтавкиНоменклатура
	|		ПО (ВТСтавкиНоменклатура.ВидСтавкиНДС = ЗаказПокупателяМатериалы.Номенклатура.ВидСтавкиНДС)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНДС КАК ВТСтавкиОрганизация
	|		ПО (ВТСтавкиОрганизация.ВидСтавкиНДС = ЗаказПокупателяМатериалы.Ссылка.Организация.ВидСтавкиНДСПоУмолчанию)
	|ГДЕ
	|	ЗаказПокупателяМатериалы.Ссылка В
	|			(ВЫБРАТЬ
	|				ЗаказНаряды.Ссылка
	|			ИЗ
	|				ЗаказНаряды)
	|	И ЗаказПокупателяМатериалы.Номенклатура.СпособПополнения <> ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказПокупателяМатериалы.Ссылка,
	|	ЗаказПокупателяМатериалы.Номенклатура,
	|	ЗаказПокупателяМатериалы.Партия,
	|	ЗаказПокупателяМатериалы.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ВТСтавкиНоменклатура.СтавкаНДС ЕСТЬ NULL
	|			ТОГДА ВТСтавкиОрганизация.СтавкаНДС
	|		ИНАЧЕ ВТСтавкиНоменклатура.СтавкаНДС
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяМатериалы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА 1
	|		ИНАЧЕ ЗаказПокупателяМатериалы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ,
	|	Шапка.Старт,
	|	Шапка.Организация,
	|	ЗаказПокупателяМатериалы.СтруктурнаяЕдиницаРезерв,
	|	ЗаказПокупателяМатериалы.Характеристика,
	|	ЗаказПокупателяМатериалы.Номенклатура.ТипНоменклатуры
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказПокупателя,
	|	НомерСтроки";
	
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ТаблицаОстатков = МассивРезультатов[МассивРезультатов.Количество() - 4].Выгрузить();
	ТаблицаОстатков.Индексы.Добавить("ЗаказПокупателя, Номенклатура, Характеристика");
	
	Запасы.Очистить();
	СтруктураДляПоиска = Новый Структура;
	
	Выборка = МассивРезультатов[МассивРезультатов.Количество() - 3].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьСборкуЗаказов") И Выборка.ЕстьСборка Тогда
			ВыборкаСобрано = Выборка.КоличествоСобрано;
		Иначе
			ВыборкаСобрано = 0;
		КонецЕсли;
		
		СтруктураДляПоиска.Очистить();
		СтруктураДляПоиска.Вставить("ЗаказПокупателя", Выборка.ЗаказПокупателя);
		СтруктураДляПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
		СтруктураДляПоиска.Вставить("Характеристика", Выборка.Характеристика);
		
		МассивСтрокОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураДляПоиска);
		КоличествоВБазовой = Выборка.Количество * Выборка.Коэффициент;
		Если МассивСтрокОстатков.Количество() = 0 Тогда
			ДоступноеКоличество = Выборка.Количество - ВыборкаСобрано;
		ИначеЕсли МассивСтрокОстатков[0].КоличествоОстаток > КоличествоВБазовой Тогда
			ДоступноеКоличество = 0;
			МассивСтрокОстатков[0].КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток - КоличествоВБазовой;
		Иначе
			ДоступноеКоличество = Выборка.Количество - Макс(МассивСтрокОстатков[0].КоличествоОстаток / Выборка.Коэффициент, ВыборкаСобрано);
			ТаблицаОстатков.Удалить(МассивСтрокОстатков[0]);
		КонецЕсли;
		Если ДоступноеКоличество <= 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Количество = ДоступноеКоличество;
		
	КонецЦикла;
	
	// Заказ-наряды
	Выборка = МассивРезультатов[МассивРезультатов.Количество() - 1].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураДляПоиска.Очистить();
		СтруктураДляПоиска.Вставить("ЗаказПокупателя", Выборка.ЗаказПокупателя);
		СтруктураДляПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
		СтруктураДляПоиска.Вставить("Характеристика", Выборка.Характеристика);
		
		МассивСтрокОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураДляПоиска);
		КоличествоВБазовой = Выборка.Количество * Выборка.Коэффициент;
		Если МассивСтрокОстатков.Количество() = 0 Тогда
			ДоступноеКоличество = Выборка.Количество;
		ИначеЕсли МассивСтрокОстатков[0].КоличествоОстаток > КоличествоВБазовой Тогда
			ДоступноеКоличество = 0;
			МассивСтрокОстатков[0].КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток - КоличествоВБазовой;
		Иначе
			ДоступноеКоличество = Выборка.Количество - МассивСтрокОстатков[0].КоличествоОстаток / Выборка.Коэффициент;
			ТаблицаОстатков.Удалить(МассивСтрокОстатков[0]);
		КонецЕсли;
		Если ДоступноеКоличество <= 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Количество = ДоступноеКоличество;
		
	КонецЦикла;
	
	Если ПоложениеДатыПоступления = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		
		ПоложениеДатыПоступления = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
		Если Запасы.Количество() > 0 Тогда
			ДатаПоступления = Запасы[0].ДатаПоступления;
			Для каждого СтрокаЗапасы Из Запасы Цикл
				Если ДатаПоступления <> СтрокаЗапасы.ДатаПоступления Тогда
					ПоложениеДатыПоступления = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
					Прервать;
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли;
		
	КонецЕсли; 
	
	ЗаполнитьСкладыПоУмолчанию(); 
		
КонецПроцедуры

Процедура ЗаполнитьПоКалькуляции(ДокументСсылкаЗаказПокупателя) Экспорт
	
	Если Не ЗначениеЗаполнено(ДокументСсылкаЗаказПокупателя) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДокументСсылкаЗаказПокупателя)=Тип("Массив") Тогда
		МассивЗаказов = ДокументСсылкаЗаказПокупателя;
	Иначе
		МассивЗаказов = Новый Массив;
		МассивЗаказов.Добавить(ДокументСсылкаЗаказПокупателя);
	КонецЕсли;
	Если МассивЗаказов.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка документов
	ИменаРеквизитов = Новый Массив;
	ИменаРеквизитов.Добавить("Организация, Ссылка, ВидОперации, Старт, ДатаОтгрузки, СостояниеЗаказа, Проведен");
	ИменаРеквизитов.Добавить("ОжидаетсяВыборВариантаКП, ПоложениеДатыОтгрузки, КалькуляцияРассчитана");
	ИменаРеквизитов.Добавить("ОжидаетсяВыборВариантаКП, СтруктурнаяЕдиницаРезерв, ПоложениеСклада");
	ИменаРеквизитовСтрокой = СтрСоединить(ИменаРеквизитов, ", ");
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивЗаказов, ИменаРеквизитовСтрокой);
	Для каждого КлючИЗначение Из ЗначенияРеквизитов Цикл
		РеквизитыЗаказа = КлючИЗначение.Значение;
		Документы.ЗаказПокупателя.ПроверитьВозможностьВводаНаОснованииЗаказаПокупателя(РеквизитыЗаказа.Ссылка, РеквизитыЗаказа);
		Если НЕ РеквизитыЗаказа.КалькуляцияРассчитана Тогда
			ТекстОшибки = НСтр("ru = 'По заказу %Документ% калькуляция не рассчитана.'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", РеквизитыЗаказа.Ссылка);
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
	КонецЦикла;
	
	// Шапка
	Организация = РеквизитыЗаказа.Организация;
	Если МассивЗаказов.Количество() = 1 Тогда
		ЗаказПокупателя = РеквизитыЗаказа.Ссылка;
		Если РеквизитыЗаказа.ВидОперации=Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд Тогда
			ДатаПоступления = РеквизитыЗаказа.Старт;
		Иначе
			ДатаПоступления = РеквизитыЗаказа.ДатаОтгрузки;
			ПоложениеДатыПоступления = РеквизитыЗаказа.ПоложениеДатыОтгрузки;
		КонецЕсли;
		ПоложениеСклада = РеквизитыЗаказа.ПоложениеСклада;
		Если ПоложениеСклада <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			СтруктурнаяЕдиницаРезерв = РеквизитыЗаказа.СтруктурнаяЕдиницаРезерв;
		КонецЕсли; 
	Иначе
		ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
		ПоложениеДатыПоступления = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
	КонецЕсли; 
	
	// Получение данных
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = ТекстЗапросаЗаполнитьПоКалькуляции();
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ТаблицаОстатков = МассивРезультатов[МассивРезультатов.Количество()-2].Выгрузить();
	ТаблицаОстатков.Индексы.Добавить("ЗаказПокупателя,Номенклатура,Характеристика");
	
	// Заполнение табличной части.
	Запасы.Очистить();
	
	Выборка = МассивРезультатов[МассивРезультатов.Количество()-1].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураДляПоиска = Новый Структура;
		СтруктураДляПоиска.Вставить("ЗаказПокупателя", Выборка.ЗаказПокупателя);
		СтруктураДляПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
		СтруктураДляПоиска.Вставить("Характеристика", Выборка.Характеристика);
		
		МассивСтрокОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураДляПоиска);
		Если МассивСтрокОстатков.Количество() = 0 Тогда
			СтрокаОстатков = Новый Структура("КоличествоРазмещено", 0);
		Иначе
			СтрокаОстатков = МассивСтрокОстатков[0];
		КонецЕсли; 
		Если СтрокаОстатков.КоличествоРазмещено > 0 И СтрокаОстатков.КоличествоРазмещено >= Выборка.Количество Тогда
			СтрокаОстатков.КоличествоРазмещено = СтрокаОстатков.КоличествоРазмещено - Выборка.Количество;
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Количество = (Выборка.Количество - СтрокаОстатков.КоличествоРазмещено) / Выборка.Коэффициент;
		НоваяСтрока.ДатаПоступления = Выборка.ЗапасыДатаПоступления;
		
		Если ТипЗнч(СтрокаОстатков)<>Тип("Структура") Тогда
			ТаблицаОстатков.Удалить(СтрокаОстатков);
		КонецЕсли; 
		
	КонецЦикла;
	
	// Дата поступления
	Если ПоложениеДатыПоступления = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		ТаблицаДат = Запасы.Выгрузить();
		ТаблицаДат.Свернуть("ДатаПоступления");
		Если ТаблицаДат.Количество()=1 Тогда
			ПоложениеДатыПоступления = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
			ДатаПоступления = ТаблицаДат[0].ДатаПоступления;
		КонецЕсли; 
	КонецЕсли; 
	
	ЗаполнитьСкладыПоУмолчанию();
	
КонецПроцедуры

Функция ТекстЗапросаЗаполнитьПоКалькуляции()
	
	ТекстЗапроса = 
	Справочники.СтавкиНДС.ПолучитьТекстЗапросаСозданияВТСтавкиНДС() +
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыОстатки.Характеристика КАК Характеристика,
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) КАК КоличествоРазмещено
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|		ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыОстатки.Характеристика КАК Характеристика,
	|		ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.Запасы.Остатки(
	|				,
	|				ЗаказПокупателя В (&МассивЗаказов)
	|					И Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))) КАК ЗапасыОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РазмещениеОстатки.ЗаказПокупателя,
	|		РазмещениеОстатки.Номенклатура,
	|		РазмещениеОстатки.Характеристика,
	|		РазмещениеОстатки.КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.РазмещениеЗаказов.Остатки(, ЗаказПокупателя В (&МассивЗаказов)) КАК РазмещениеОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаРазмещениеЗаказов.ЗаказПокупателя,
	|		ДвиженияДокументаРазмещениеЗаказов.Номенклатура,
	|		ДвиженияДокументаРазмещениеЗаказов.Характеристика,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаРазмещениеЗаказов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЕСТЬNULL(ДвиженияДокументаРазмещениеЗаказов.Количество, 0)
	|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаРазмещениеЗаказов.Количество, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РазмещениеЗаказов КАК ДвиженияДокументаРазмещениеЗаказов
	|	ГДЕ
	|		ДвиженияДокументаРазмещениеЗаказов.Регистратор = &Ссылка
	|		И ДвиженияДокументаРазмещениеЗаказов.ЗаказПокупателя В(&МассивЗаказов)) КАК ЗаказыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыОстатки.ЗаказПокупателя,
	|	ЗаказыОстатки.Номенклатура,
	|	ЗаказыОстатки.Характеристика
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПокупателяКалькуляция.Ссылка КАК ЗаказПокупателя,
	|	ЗаказПокупателяКалькуляция.Ссылка.Организация КАК Организация,
	|	ЗаказПокупателяКалькуляция.Ссылка.ДатаОтгрузки КАК ДатаПоступления,
	|	МИНИМУМ(ЗаказПокупателяКалькуляция.НомерСтроки) КАК НомерСтроки,
	|	ЗаказПокупателяКалькуляция.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателяКалькуляция.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ЗаказПокупателяКалькуляция.Номенклатура.Склад
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК СтруктурнаяЕдиницаРезерв,
	|	ЗаказПокупателяКалькуляция.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяКалькуляция.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА 1
	|		ИНАЧЕ ЗаказПокупателяКалькуляция.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Коэффициент,
	|	ЗаказПокупателяКалькуляция.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПокупателяКалькуляция.Ссылка.ДатаОтгрузки КАК ЗапасыДатаПоступления,
	|	ВЫБОР
	|		КОГДА ВТСтавкиНоменклатура.СтавкаНДС ЕСТЬ NULL
	|			ТОГДА ВТСтавкиОрганизация.СтавкаНДС
	|		ИНАЧЕ ВТСтавкиНоменклатура.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДС,
	|	СУММА(ЗаказПокупателяКалькуляция.Количество) КАК Количество,
	|	ЗаказПокупателяКалькуляция.Спецификация КАК Спецификация
	|ИЗ
	|	Документ.ЗаказПокупателя.Калькуляция КАК ЗаказПокупателяКалькуляция
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНДС КАК ВТСтавкиНоменклатура
	|		ПО (ВТСтавкиНоменклатура.ВидСтавкиНДС = ЗаказПокупателяКалькуляция.Номенклатура.ВидСтавкиНДС)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНДС КАК ВТСтавкиОрганизация
	|		ПО (ВТСтавкиОрганизация.ВидСтавкиНДС = ЗаказПокупателяКалькуляция.Ссылка.Организация.ВидСтавкиНДСПоУмолчанию)
	|ГДЕ
	|	ЗаказПокупателяКалькуляция.Ссылка В(&МассивЗаказов)
	|	И ЗаказПокупателяКалькуляция.Ссылка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаПродажу), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаПереработку))
	|	И ЗаказПокупателяКалькуляция.НомерВариантаКП = ЗаказПокупателяКалькуляция.Ссылка.ОсновнойВариантКП
	|	И ЗаказПокупателяКалькуляция.Спецификация = ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|	И ЗаказПокупателяКалькуляция.Номенклатура ССЫЛКА Справочник.Номенклатура
	|	И ЗаказПокупателяКалькуляция.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|	И (ЗаказПокупателяКалькуляция.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ИЛИ ЗаказПокупателяКалькуляция.Номенклатура.СпособПополнения <> ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство))
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказПокупателяКалькуляция.Ссылка,
	|	ЗаказПокупателяКалькуляция.Ссылка.Организация,
	|	ЗаказПокупателяКалькуляция.Ссылка.ДатаОтгрузки,
	|	ЗаказПокупателяКалькуляция.Номенклатура,
	|	ЗаказПокупателяКалькуляция.Характеристика,
	|	ЗаказПокупателяКалькуляция.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ВТСтавкиНоменклатура.СтавкаНДС ЕСТЬ NULL
	|			ТОГДА ВТСтавкиОрганизация.СтавкаНДС
	|		ИНАЧЕ ВТСтавкиНоменклатура.СтавкаНДС
	|	КОНЕЦ,
	|	ЗаказПокупателяКалькуляция.Спецификация,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяКалькуляция.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА 1
	|		ИНАЧЕ ЗаказПокупателяКалькуляция.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателяКалькуляция.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ЗаказПокупателяКалькуляция.Номенклатура.Склад
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ,
	|	ЗаказПокупателяКалькуляция.Ссылка.ДатаОтгрузки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказПокупателя,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗаполнитьПоЗаказуНаПроизводство(ДокументСсылкаЗаказНаПроизводство) Экспорт
	
	ОснованиеВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылкаЗаказНаПроизводство, "ВидОперации");
	
	Если ОснованиеВидОперации <> Перечисления.ВидыОперацийЗаказНаПроизводство.Сборка
		И ОснованиеВидОперации <> Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка
		Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказНаПроизводство.Ссылка КАК Заказ,
	|	ЗаказНаПроизводство.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|				ИЛИ ЗаказНаПроизводство.УчетПотребностиПоЗаказам
	|			ТОГДА ЗаказНаПроизводство.ЗаказПокупателя
	|		ИНАЧЕ ЗаказНаПроизводство.ДокументОснование
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ЗаказНаПроизводство.ДокументОснование КАК ДокументОснование,
	|	ЗаказНаПроизводство.Старт КАК ДатаПоступления,
	|	ЗаказНаПроизводство.ПоложениеЗаказаПокупателя КАК ПоложениеЗаказаПокупателя,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|				ИЛИ ЗаказНаПроизводство.УчетПотребностиПоСкладам
	|			ТОГДА ЗаказНаПроизводство.СтруктурнаяЕдиницаРезерв
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК СтруктурнаяЕдиницаРезерв,
	|	ЗаказНаПроизводство.ПоложениеСклада КАК ПоложениеСклада,
	|	ЗаказНаПроизводство.Запасы.(
	|		Ссылка.Старт КАК ДатаПоступления,
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ЗаказНаПроизводство.Запасы.Количество - ЗаказНаПроизводство.Запасы.Резерв КАК Количество,
	|		Номенклатура.ВидСтавкиНДС КАК ВидСтавкиНДС,
	|		ЗаказПокупателя КАК ЗаказПокупателя,
	|		ВЫБОР
	|			КОГДА ЗаказНаПроизводство.Запасы.СтруктурнаяЕдиница <> ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|					И (ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|						ИЛИ ЗаказНаПроизводство.УчетПотребностиПоСкладам)
	|				ТОГДА ЗаказНаПроизводство.Запасы.СтруктурнаяЕдиница
	|			ИНАЧЕ ЗаказНаПроизводство.Запасы.Номенклатура.Склад
	|		КОНЕЦ КАК СтруктурнаяЕдиницаРезерв
	|	) КАК Запасы
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
	|ГДЕ
	|	ЗаказНаПроизводство.Ссылка = &ДокументОснование
	|	И ЗаказНаПроизводство.Запасы.Количество - ЗаказНаПроизводство.Запасы.Резерв > 0
	|	И ЗаказНаПроизводство.Запасы.Номенклатура.СпособПополнения <> ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство)
	|	И (ЗаказНаПроизводство.Запасы.Спецификация = ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|			ИЛИ ЗаказНаПроизводство.Запасы.Номенклатура.СпособПополнения = ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Переработка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказНаПроизводство.Запасы.НомерСтроки");
	
	Если ОснованиеВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка Тогда
		Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"ЗаказНаПроизводство.Запасы.",
		"ЗаказНаПроизводство.Продукция.");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументСсылкаЗаказНаПроизводство);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаИзРезультатаЗапроса);
	Если НЕ ПолучитьФункциональнуюОпцию("РазрешитьСкладыВТабличныхЧастях") Тогда
		ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
	КонецЕсли; 
	
	СтавкаНДС = Неопределено;
	СтавкаНДСИзНоменклатуры = Ложь;
	СоответствиеСтавок = Справочники.СтавкиНДС.СоответствиеСтавокНДС();
	
	Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда
		СтавкаНДСИзНоменклатуры = Истина;
		СтавкаНДС = СоответствиеСтавок.Получить(Организация.ВидСтавкиНДСПоУмолчанию);
	ИначеЕсли НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
		СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
	Иначе
		СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
	КонецЕсли;
	
	Запасы.Загрузить(ВыборкаИзРезультатаЗапроса.Запасы.Выгрузить());
	
	Для каждого СтрокаЗапасы Из Запасы Цикл
		Если СтавкаНДСИзНоменклатуры Тогда
			Если Не ЗначениеЗаполнено(СтрокаЗапасы.СтавкаНДС) И ЗначениеЗаполнено(СтрокаЗапасы.Номенклатура) Тогда
				СтрокаЗапасы.СтавкаНДС = СоответствиеСтавок.Получить(СтрокаЗапасы.Номенклатура.ВидСтавкиНДС);
			КонецЕсли;
		Иначе
			СтрокаЗапасы.СтавкаНДС = СтавкаНДС;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьСкладыПоУмолчанию(); 
	
КонецПроцедуры

Процедура ЗаполнитьПоСчетуНаОплатуПоставщика(ДокументСсылкаСчетНаОплатуПоставщика) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СчетНаОплатуПоставщика.Ссылка КАК ДокументОснование,
	|	СчетНаОплатуПоставщика.Организация КАК Организация,
	|	СчетНаОплатуПоставщика.Контрагент КАК Контрагент,
	|	СчетНаОплатуПоставщика.Договор КАК Договор,
	|	СчетНаОплатуПоставщика.ВалютаДокумента КАК ВалютаДокумента,
	|	СчетНаОплатуПоставщика.Кратность КАК Кратность,
	|	СчетНаОплатуПоставщика.Курс КАК Курс,
	|	СчетНаОплатуПоставщика.НалогообложениеНДС КАК НалогообложениеНДС,
	|	СчетНаОплатуПоставщика.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	СчетНаОплатуПоставщика.ВидЦенКонтрагента КАК ВидЦенКонтрагента,
	|	СчетНаОплатуПоставщика.Касса КАК Касса,
	|	СчетНаОплатуПоставщика.СуммаДокумента КАК СуммаДокумента,
	|	СчетНаОплатуПоставщика.Запасы.(
	|		Номенклатура КАК Номенклатура,
	|		Номенклатура.Склад КАК СтруктурнаяЕдиницаРезерв,
	|		Характеристика КАК Характеристика,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество КАК Количество,
	|		Цена КАК Цена,
	|		ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
	|		СуммаСкидкиНаценки КАК СуммаСкидкиНаценки,
	|		Сумма КАК Сумма,
	|		СтавкаНДС КАК СтавкаНДС,
	|		СуммаНДС КАК СуммаНДС,
	|		Всего КАК Всего,
	|		Содержание КАК Содержание
	|	) КАК Запасы
	|ИЗ
	|	Документ.СчетНаОплатуПоставщика КАК СчетНаОплатуПоставщика,
	|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
	|ГДЕ
	|	СчетНаОплатуПоставщика.Ссылка = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетНаОплатуПоставщика.Запасы.НомерСтроки");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументСсылкаСчетНаОплатуПоставщика);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаИзРезультатаЗапроса);
	
	СтавкаНДС = Неопределено;
	СтавкаНДСИзНоменклатуры = Ложь;
	СоответствиеСтавок = Справочники.СтавкиНДС.СоответствиеСтавокНДС();
	
	Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда
		СтавкаНДСИзНоменклатуры = Истина;
		СтавкаНДС = СоответствиеСтавок.Получить(Организация.ВидСтавкиНДСПоУмолчанию);
	ИначеЕсли НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
		СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
	Иначе
		СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
	КонецЕсли;
	
	Запасы.Загрузить(ВыборкаИзРезультатаЗапроса.Запасы.Выгрузить());
	
	Для Каждого СтрокаЗапасы Из Запасы Цикл
		Если СтавкаНДСИзНоменклатуры Тогда
			Если Не ЗначениеЗаполнено(СтрокаЗапасы.СтавкаНДС) И ЗначениеЗаполнено(СтрокаЗапасы.Номенклатура) Тогда
				СтрокаЗапасы.СтавкаНДС = СоответствиеСтавок.Получить(СтрокаЗапасы.Номенклатура.ВидСтавкиНДС);
			КонецЕсли;
		Иначе
			СтрокаЗапасы.СтавкаНДС = СтавкаНДС;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьСкладыПоУмолчанию();
	
КонецПроцедуры

Процедура ЗаполнитьКолонкуРезервПоОстаткам() Экспорт
	
	Материалы.ЗагрузитьКолонку(Новый Массив(Материалы.Количество()), "Резерв");
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка) КАК Партия
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасы
	|ИЗ
	|	&ТаблицаЗапасы КАК ТаблицаЗапасы";
	
	Запрос.УстановитьПараметр("ТаблицаЗапасы", Материалы.Выгрузить());
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗапасыОстатки.Организация КАК Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗапасыОстатки.СчетУчета КАК СчетУчета,
	|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыОстатки.Характеристика КАК Характеристика,
	|	ЗапасыОстатки.Партия КАК Партия,
	|	СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыОстатки.Организация КАК Организация,
	|		ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗапасыОстатки.СчетУчета КАК СчетУчета,
	|		ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыОстатки.Характеристика КАК Характеристика,
	|		ЗапасыОстатки.Партия КАК Партия,
	|		ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.Запасы.Остатки(
	|				,
	|				(Организация, СтруктурнаяЕдиница, СчетУчета, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
	|					(ВЫБРАТЬ
	|						&Организация,
	|						&СтруктурнаяЕдиница,
	|						ТаблицаЗапасы.Номенклатура.СчетУчетаЗапасов,
	|						ТаблицаЗапасы.Номенклатура,
	|						ТаблицаЗапасы.Характеристика,
	|						ТаблицаЗапасы.Партия,
	|						ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка) КАК ЗаказПокупателя
	|					ИЗ
	|						ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы)) КАК ЗапасыОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаЗапасы.Организация,
	|		ДвиженияДокументаЗапасы.СтруктурнаяЕдиница,
	|		ДвиженияДокументаЗапасы.СчетУчета,
	|		ДвиженияДокументаЗапасы.Номенклатура,
	|		ДвиженияДокументаЗапасы.Характеристика,
	|		ДвиженияДокументаЗапасы.Партия,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
	|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.Запасы КАК ДвиженияДокументаЗапасы
	|	ГДЕ
	|		ДвиженияДокументаЗапасы.Регистратор = &Ссылка
	|		И ДвиженияДокументаЗапасы.Период <= &Период
	|		И ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)) КАК ЗапасыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыОстатки.Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница,
	|	ЗапасыОстатки.СчетУчета,
	|	ЗапасыОстатки.Номенклатура,
	|	ЗапасыОстатки.Характеристика,
	|	ЗапасыОстатки.Партия";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Организация", Константы.УчетПоКомпании.Компания(Организация));
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиницаРезерв);
	
	ТаблицаПериодов = Новый ТаблицаЗначений();
	ТаблицаПериодов.Колонки.Добавить("ДатаОтгрузки");
	ТаблицаПериодов.Колонки.Добавить("СтрокаЗапасы");
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураДляПоиска = Новый Структура;
		СтруктураДляПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
		СтруктураДляПоиска.Вставить("Характеристика", Выборка.Характеристика);
		
		МассивСтрокЗапасы = Материалы.НайтиСтроки(СтруктураДляПоиска);
		Для каждого СтрокаЗапасы Из МассивСтрокЗапасы Цикл
			НоваяСтрока = ТаблицаПериодов.Добавить();
			НоваяСтрока.ДатаОтгрузки = СтрокаЗапасы.ДатаОтгрузки;
			НоваяСтрока.СтрокаЗапасы = СтрокаЗапасы;
		КонецЦикла;
		
		ВсегоОстаток = Выборка.КоличествоОстаток;
		ТаблицаПериодов.Сортировать("ДатаОтгрузки");
		Для каждого СтрокаТаблицыПериодов Из ТаблицаПериодов Цикл
			СтрокаЗапасы = СтрокаТаблицыПериодов.СтрокаЗапасы;
			ВсегоОстаток = ?(ТипЗнч(СтрокаЗапасы.ЕдиницаИзмерения) = ТИП("СправочникСсылка.КлассификаторЕдиницИзмерения"), ВсегоОстаток, ВсегоОстаток / СтрокаЗапасы.ЕдиницаИзмерения.Коэффициент);
			Если СтрокаЗапасы.Количество >= ВсегоОстаток Тогда
				СтрокаЗапасы.Резерв = ВсегоОстаток;
				ВсегоОстаток = 0;
			Иначе
				СтрокаЗапасы.Резерв = СтрокаЗапасы.Количество;
				ВсегоОстаток = ВсегоОстаток - СтрокаЗапасы.Количество;
				ВсегоОстаток = ?(ТипЗнч(СтрокаЗапасы.ЕдиницаИзмерения) = ТИП("СправочникСсылка.КлассификаторЕдиницИзмерения"), ВсегоОстаток, ВсегоОстаток * СтрокаЗапасы.ЕдиницаИзмерения.Коэффициент);
			КонецЕсли;
		КонецЦикла;
		
		ТаблицаПериодов.Очистить();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДозаполнитьПоУмолчанию(ДанныеЗаполнения)
	
	Если Не ЗначениеЗаполнено(СостояниеЗаказа) Тогда
		СостояниеЗаказа = ЗаполнениеОбъектовУНФ.ПолучитьСостояниеЗаказаПоставщику();
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказНаПроизводство")
		И ДанныеЗаполнения.ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
		ЗаказПокупателя = ДанныеЗаполнения.ЗаказПокупателя;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		УчетПотребностиПоСкладам = ПолучитьФункциональнуюОпцию("УчетПотребностиПоСкладам");
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьПоСпецификации(СтекСпецификацийУзлов, ТаблицаУзлы = Неопределено) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПродукция.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПродукция.Количество КАК Количество,
	|	ТаблицаПродукция.Коэффициент КАК Коэффициент,
	|	ТаблицаПродукция.Спецификация КАК Спецификация,
	|	ТаблицаПродукция.ЗаказПокупателя КАК ЗаказПокупателя
	|ПОМЕСТИТЬ ВременнаяТаблицаПродукция
	|ИЗ
	|	&ТаблицаПродукция КАК ТаблицаПродукция
	|ГДЕ
	|	ТаблицаПродукция.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)";
	
	Если ТаблицаУзлы = Неопределено Тогда
		Материалы.Очистить();
		ТаблицаПродукция = Запасы.Выгрузить();
		Массив = Новый Массив();
		Массив.Добавить(Тип("Число"));
		ОписаниеТиповЧ = Новый ОписаниеТипов(Массив, , ,Новый КвалификаторыЧисла(10,3));
		ТаблицаПродукция.Колонки.Добавить("Коэффициент", ОписаниеТиповЧ);
		Для каждого СтрокаПродукция Из ТаблицаПродукция Цикл
			Если ЗначениеЗаполнено(СтрокаПродукция.ЕдиницаИзмерения)
				И ТипЗнч(СтрокаПродукция.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
				СтрокаПродукция.Коэффициент = СтрокаПродукция.ЕдиницаИзмерения.Коэффициент;
			Иначе
				СтрокаПродукция.Коэффициент = 1;
			КонецЕсли;
		КонецЦикла;
		ТаблицаУзлы = ТаблицаПродукция.СкопироватьКолонки("НомерСтроки,Количество,Коэффициент,Спецификация,ЗаказПокупателя");
		Запрос.УстановитьПараметр("ТаблицаПродукция", ТаблицаПродукция);
	Иначе
		Запрос.УстановитьПараметр("ТаблицаПродукция", ТаблицаУзлы);
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МИНИМУМ(ТаблицаПродукция.НомерСтроки) КАК НомерСтрокиПродукции,
	|	ТаблицаПродукция.Спецификация КАК СпецификацияПродукции,
	|	ТаблицаПродукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	МИНИМУМ(ТаблицаМатериалы.НомерСтроки) КАК НомерСтрокиСостава,
	|	ТаблицаМатериалы.ТипСтрокиСостава КАК ТипСтрокиСостава,
	|	ТаблицаМатериалы.Номенклатура КАК Номенклатура,
	|	ТаблицаМатериалы.Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ИспользоватьХарактеристики.Значение
	|			ТОГДА ТаблицаМатериалы.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	СУММА(ТаблицаМатериалы.Количество / ТаблицаМатериалы.КоличествоПродукции * ТаблицаПродукция.Коэффициент * ТаблицаПродукция.Количество) КАК Количество,
	|	ТаблицаМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаМатериалы.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|				И ТИПЗНАЧЕНИЯ(ТаблицаМатериалы.ЕдиницаИзмерения) = ТИП(Справочник.ЕдиницыИзмерения)
	|				И ТаблицаМатериалы.ЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ТаблицаМатериалы.ЕдиницаИзмерения.Коэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Коэффициент,
	|	ТаблицаМатериалы.ДоляСтоимости КАК ДоляСтоимости,
	|	ТаблицаМатериалы.Спецификация КАК Спецификация
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ТаблицаПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Спецификации.Состав КАК ТаблицаМатериалы
	|		ПО ТаблицаПродукция.Спецификация = ТаблицаМатериалы.Ссылка,
	|	Константа.ФункциональнаяОпцияИспользоватьХарактеристики КАК ИспользоватьХарактеристики
	|ГДЕ
	|	ТаблицаМатериалы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПродукция.Спецификация,
	|	ТаблицаПродукция.ЗаказПокупателя,
	|	ТаблицаМатериалы.ТипСтрокиСостава,
	|	ТаблицаМатериалы.Номенклатура,
	|	ТаблицаМатериалы.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаМатериалы.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|				И ТИПЗНАЧЕНИЯ(ТаблицаМатериалы.ЕдиницаИзмерения) = ТИП(Справочник.ЕдиницыИзмерения)
	|				И ТаблицаМатериалы.ЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ТаблицаМатериалы.ЕдиницаИзмерения.Коэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ТаблицаМатериалы.ДоляСтоимости,
	|	ТаблицаМатериалы.Спецификация,
	|	ВЫБОР
	|		КОГДА ИспользоватьХарактеристики.Значение
	|			ТОГДА ТаблицаМатериалы.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаМатериалы.Номенклатура.СтранаПроисхождения
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиПродукции,
	|	НомерСтрокиСостава";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ТипСтрокиСостава = Перечисления.ТипыСтрокСоставаСпецификации.Узел Тогда
			ТаблицаУзлы.Очистить();
			Если НЕ СтекСпецификацийУзлов.Найти(Выборка.Спецификация) = Неопределено Тогда
				ТекстСообщения = НСтр("ru = 'При попытке заполнить табличную часть Материалы по спецификации,
									|обнаружено рекурсивное вхождение элемента'")+" "+Выборка.Номенклатура+" "+НСтр("ru = 'в спецификации'")+" "+Выборка.СпецификацияПродукции+"
									|Операция не выполнена.";
				ВызватьИсключение ТекстСообщения;
			КонецЕсли;
			СтекСпецификацийУзлов.Добавить(Выборка.Спецификация);
			НоваяСтрока = ТаблицаУзлы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			ЗаполнитьТабличнуюЧастьПоСпецификации(СтекСпецификацийУзлов, ТаблицаУзлы);
		Иначе
			НоваяСтрока = Материалы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.ДатаОтгрузки = Дата;
		КонецЕсли;
	КонецЦикла;
	
	СтекСпецификацийУзлов.Очистить();
	Материалы.Свернуть("Номенклатура, Характеристика, ЕдиницаИзмерения, Спецификация, ДатаОтгрузки, ЗаказПокупателя", "Количество, Резерв");
	
КонецПроцедуры

Процедура ЗаполнитьПоДоговоруКонтрагента(ДанныеЗаполнения) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор,
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
	|	ДоговорыКонтрагентов.Организация КАК Организация,
	|	ДоговорыКонтрагентов.ВалютаРасчетов КАК ВалютаДокумента,
	|	ДоговорыКонтрагентов.ВидЦенКонтрагента КАК ВидЦенКонтрагента,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.ВидЦенКонтрагента.ЦенаВключаетНДС, ЛОЖЬ) КАК НДСВключатьВСтоимость
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	ВыборкаШапка = Запрос.Выполнить().Выбрать();
	
	ВыборкаШапка.Следующий();
	
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	
	Если ВалютаДокумента <> Константы.НациональнаяВалюта.Получить() Тогда
		СтруктураПоВалюте = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Договор.ВалютаРасчетов));
		Курс = СтруктураПоВалюте.Курс;
		Кратность = СтруктураПоВалюте.Кратность;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоСтруктуре(ДанныеЗаполнения) Экспорт
	
	Если ДанныеЗаполнения.Свойство("МассивЗаказовПокупателей") Тогда
		ЗаполнитьПоЗаказуПокупателя(ДанныеЗаполнения.МассивЗаказовПокупателей);
	ИначеЕсли ДанныеЗаполнения.Свойство("ПоКалькуляции") Тогда
		ЗаполнитьПоКалькуляции(ДанныеЗаполнения.ЗаказПокупателя);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСкладыПоУмолчанию()
		
	Если НЕ ПолучитьФункциональнуюОпцию("УчетПоНесколькимСкладам") Тогда
		ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
		СтруктурнаяЕдиницаРезерв = Справочники.СтруктурныеЕдиницы.ОсновнойСклад;
	ИначеЕсли НЕ ПолучитьФункциональнуюОпцию("РазрешитьСкладыВТабличныхЧастях") Тогда
		ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
	ИначеЕсли НЕ ЗначениеЗаполнено(ПоложениеСклада) Тогда
		ПоложениеСклада = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
			Пользователи.ТекущийПользователь(), "ПоложениеСкладаВДокументахПоступления");
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(СтруктурнаяЕдиницаРезерв) Тогда
		СтруктурнаяЕдиницаРезерв = Справочники.СтруктурныеЕдиницы.ОсновнойСклад();
	КонецЕсли; 
	ПодходящаяНоменклатура = НоменклатураВДокументахСервер.НоменклатураТабличнойЧастиПоТипу(
		ЭтотОбъект, Перечисления.ТипыНоменклатуры.Запас);
	Для каждого СтрокаЗапасы Из Запасы Цикл
		Если ПодходящаяНоменклатура.Найти(СтрокаЗапасы.Номенклатура) = Неопределено Тогда
			СтрокаЗапасы.СтруктурнаяЕдиницаРезерв = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
		ИначеЕсли НЕ ЗначениеЗаполнено(СтрокаЗапасы.СтруктурнаяЕдиницаРезерв) 
			ИЛИ ПоложениеСклада <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			СтрокаЗапасы.СтруктурнаяЕдиницаРезерв = СтруктурнаяЕдиницаРезерв;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// АПК:299-вкл

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПривестиДанныеКСогласованномуСостоянию()
	
	Если СостояниеЗаказа <> Справочники.СостоянияЗаказовПоставщикам.Завершен Тогда
		ВариантЗавершения = Перечисления.ВариантыЗавершенияЗаказа.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСтруктурнуюЕдиницуРезерваВТЧ()
	
	Если ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти
		И ВидОперации <> Перечисления.ВидыОперацийЗаказПоставщику.ЗаказНаПереработку Тогда
		Возврат;
	КонецЕсли;
	
	ПодходящаяНоменклатура = НоменклатураВДокументахСервер.НоменклатураТабличнойЧастиПоТипу(
		ЭтотОбъект, Перечисления.ТипыНоменклатуры.Запас);
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		СтрокаТабличнойЧасти.СтруктурнаяЕдиницаРезерв = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		Если ПодходящаяНоменклатура.Найти(СтрокаТабличнойЧасти.Номенклатура) = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		СтрокаТабличнойЧасти.СтруктурнаяЕдиницаРезерв = СтруктурнаяЕдиницаРезерв;
	КонецЦикла;
	
	Если ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТабличнойЧасти Из Материалы Цикл
		СтрокаТабличнойЧасти.СтруктурнаяЕдиницаРезерв = СтруктурнаяЕдиницаРезерв;
	КонецЦикла;
	
КонецПроцедуры

Функция СтруктурнаяЕдиницаДляШапки(ТабличнаяЧасть)
	
	Результат = Новый Структура;
	Результат.Вставить("СтруктурнаяЕдиницаРезерв", Неопределено);
	
	Если ТабличнаяЧасть.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат.СтруктурнаяЕдиницаРезерв = ТабличнаяЧасть[0].СтруктурнаяЕдиницаРезерв;
	
	Если ТабличнаяЧасть.Количество() = 1 Тогда
		Возврат Результат;
	КонецЕсли;
	
	ТаблицаСтруктурныеЕдиницы = ТабличнаяЧасть.Выгрузить(, "СтруктурнаяЕдиницаРезерв");
	ТаблицаСтруктурныеЕдиницы.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	Для Каждого ТекСтрока Из ТаблицаСтруктурныеЕдиницы Цикл
		ТекСтрока.Количество = 1;
	КонецЦикла;
	ТаблицаСтруктурныеЕдиницы.Свернуть("СтруктурнаяЕдиницаРезерв", "Количество");
	
	Если ТаблицаСтруктурныеЕдиницы.Количество() < 2 Тогда
		Возврат Результат;
	КонецЕсли;
	
	ТаблицаСтруктурныеЕдиницы.Сортировать("Количество Убыв");
	
	Если ТаблицаСтруктурныеЕдиницы[0].Количество = ТаблицаСтруктурныеЕдиницы[1].Количество Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат.СтруктурнаяЕдиницаРезерв = ТаблицаСтруктурныеЕдиницы[0].СтруктурнаяЕдиницаРезерв;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли