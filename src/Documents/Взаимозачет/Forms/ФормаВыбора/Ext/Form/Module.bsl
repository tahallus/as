#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДинамическиеСпискиУНФ.ОтображатьТолькоВремяДляТекущейДаты(Список);
	
	УстановитьОтборСписка();
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборСписка()
	
	Если Не (Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Контрагент") И Параметры.Отбор.Количество() = 1) Тогда
		Возврат;
	КонецЕсли;
	
	// Добавим отбор в настройках динамического списка.
	ОтборГруппа = Список.КомпоновщикНастроек.Настройки.Отбор;
	
	// Добавим группу отбора.
	ГруппаОтбора = ОтборГруппа.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.Использование = Истина;
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	//ГруппаОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный;
	
	// Добавим отбор по контрагенту
	ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Контрагент");
	ЭлементОтбора.ПравоеЗначение = Параметры.Отбор.Контрагент;
	ЭлементОтбора.Использование = Истина;
	
	ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КонтрагентИсточник");
	ЭлементОтбора.ПравоеЗначение = Параметры.Отбор.Контрагент;
	ЭлементОтбора.Использование = Истина;
	
	ЭтотОбъект.Параметры.Отбор.Очистить();
	
КонецПроцедуры

#КонецОбласти
