#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет необходимость перепроведения заказа при смене состояния.
// 
// Возвращаемое значение:
//  РежимЗаписиДокумента - Рекомендуемый режим записи документа.
//
Функция РежимЗаписиПриСменеСостояния() Экспорт
	
	Возврат СостоянияЗаказов.РежимЗаписиДокументаПриСменеСостояния(ЭтотОбъект);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	УправлениеНебольшойФирмойЭлектронныеДокументыСервер.ОчиститьДатуНомерВходящегоДокумента(ЭтотОбъект);
	
	СостояниеЗаказа = ЗаполнениеОбъектовУНФ.ПолучитьСостояниеЗаказаНаПроизводство();
	ВариантЗавершения = Перечисления.ВариантыЗавершенияЗаказа.ПустаяСсылка();
	УдалитьЗакрыт = Ложь;
	УчетПотребностиПоЗаказамНаПроизводство = Истина;
	УчетПотребностиПоСкладам = ПолучитьФункциональнуюОпцию("УчетПотребностиПоСкладам");
	УчетПотребностиПоЗаказам = Истина;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("Структура")] = "ЗаполнитьПоСтруктуре";
	СтратегияЗаполнения[Тип("ДокументСсылка.ЗаказПокупателя")] = "ЗаполнитьПоЗаказуПокупателя";
	СтратегияЗаполнения[Тип("ДокументСсылка.ЗаказНаПроизводство")] = "ЗаполнитьПоЗаказуНаПроизводство";
	
	ИсключаяСвойства = "СостояниеЗаказа,ВариантЗавершения";
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения, ИсключаяСвойства);
	
	ДозаполнитьПоУмолчанию();
	
	Если Исполнитель = Неопределено Тогда
		Исполнитель = Справочники.Сотрудники.ПустаяСсылка();
	КонецЕсли;
	Для каждого СтрокаТабличнойЧасти Из Операции Цикл
		Если СтрокаТабличнойЧасти.Исполнитель = Неопределено Тогда
			СтрокаТабличнойЧасти.Исполнитель = Справочники.Сотрудники.ПустаяСсылка();
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроизводствоСервер.ЗаполнитьКлючиСвязи(Запасы);
	ПроизводствоСервер.ЗаполнитьКлючиСвязи(Продукция);
	
	СписокРесурсов = "";
	Для каждого СтрокаРесурс Из РесурсыПредприятия Цикл
		СписокРесурсов = СписокРесурсов + ?(СписокРесурсов = "","","; " + Символы.ПС) + СокрЛП(СтрокаРесурс.РесурсПредприятия);
	КонецЦикла;
	
	СписокНоменклатуры = "";
	ФОИспользоватьХарактеристики = Константы.ФункциональнаяОпцияИспользоватьХарактеристики.Получить();
	Для каждого СтрокаПродукция Из Продукция Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаПродукция.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		
		ХарактеристикаПредставление = "";
		Если ФОИспользоватьХарактеристики И ЗначениеЗаполнено(СтрокаПродукция.Характеристика) Тогда
			ХарактеристикаПредставление = " (" + СокрЛП(СтрокаПродукция.Характеристика) + ")";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СписокНоменклатуры) Тогда
			СписокНоменклатуры = СписокНоменклатуры + Символы.ПС;
		КонецЕсли;
		СписокНоменклатуры = СписокНоменклатуры + СокрЛП(СтрокаПродукция.Номенклатура) + ХарактеристикаПредставление + ", " + СтрокаПродукция.Количество + " " + СокрЛП(СтрокаПродукция.ЕдиницаИзмерения);
		
	КонецЦикла;
	
	СостоянияЗаказов.ПередЗаписьюЗаказа(ЭтотОбъект);
	
	ПривестиДанныеКСогласованномуСостоянию();
	
	Если ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		ЗаказПокупателя = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(Продукция, "ЗаказПокупателя");
	Иначе
		Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = ЗаказПокупателя;
		КонецЦикла; 
		Для каждого СтрокаТабличнойЧасти Из Продукция Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = ЗаказПокупателя;
		КонецЦикла; 
		Если РучноеРаспределение Тогда
			Для каждого СтрокаТабличнойЧасти Из РаспределениеЗапасов Цикл
				СтрокаТабличнойЧасти.ЗаказПокупателя = ЗаказПокупателя;
			КонецЦикла;
		КонецЕсли; 
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = ЗаказПокупателя;
		КонецЦикла; 
	КонецЕсли;
	
	Если ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		ТЧ = ?(ВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка, Продукция, Запасы);
		СтруктурнаяЕдиницаРезерв = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(ТЧ, "СтруктурнаяЕдиница");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка Тогда
		Для каждого СтрокаТабличнойЧасти Из Продукция Цикл
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя) ИЛИ УчетПотребностиПоСкладам Тогда
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = СтруктурнаяЕдиницаРезерв;
			Иначе
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
			КонецЕсли; 
		КонецЦикла; 
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Сборка Тогда
		Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя) ИЛИ УчетПотребностиПоСкладам Тогда
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = СтруктурнаяЕдиницаРезерв;
			Иначе
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	
	Если ПоложениеСтруктурнойЕдиницыОпераций = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		СтруктурнаяЕдиницаОпераций = СтруктурнаяЕдиницаОперацийШапки();
	Иначе
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = СтруктурнаяЕдиницаОпераций;
		КонецЦикла; 
		Для каждого СтрокаТабличнойЧасти Из СоставБригады Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = СтруктурнаяЕдиницаОпераций;
		КонецЦикла; 
	КонецЕсли; 
	
	Если ПоложениеИсполнителя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Исполнитель = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(Операции, "Исполнитель");
	Иначе
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			СтрокаТабличнойЧасти.Исполнитель = Исполнитель;
		КонецЦикла; 
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка Тогда
		Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
		КонецЦикла;
		Если РучноеРаспределение Тогда
			Для каждого СтрокаТабличнойЧасти Из РаспределениеЗапасов Цикл
				СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
			КонецЦикла;
		КонецЕсли; 
	Иначе
		Для каждого СтрокаТабличнойЧасти Из Продукция Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
		КонецЦикла;
		Если РучноеРаспределение И ПоложениеСклада <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			Для каждого СтрокаТабличнойЧасти Из РаспределениеЗапасов Цикл
				Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя) ИЛИ УчетПотребностиПоСкладам Тогда
					СтрокаТабличнойЧасти.СтруктурнаяЕдиница = СтруктурнаяЕдиницаРезерв;
				Иначе
					СтрокаТабличнойЧасти.СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли; 
	
	Если НЕ РучноеРаспределение
		И РежимЗаписи = РежимЗаписиДокумента.Проведение
		И ПолучитьФункциональнуюОпцию("ИспользоватьЭтапыПроизводства") Тогда
		ПроизводствоСервер.РаспределитьМатериалы(Продукция, Запасы, РаспределениеЗапасов, , Истина, ВидОперации);
	ИначеЕсли НЕ РучноеРаспределение Тогда 
		РаспределениеЗапасов.Очистить();
	КонецЕсли;
	
	ЗапланированыОперации = (Операции.Количество()>0);
	
	// ПараметрическиеСпецификации
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ПроизводствоФормулыСервер.РассчитатьПараметрическиеСпецификации(ЭтотОбъект, "Продукция", Отказ);
	КонецЕсли; 
	// Конец ПараметрическиеСпецификации
	
	// Этапы производства
	ТипСтруктурнойЕдиницы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "ТипСтруктурнойЕдиницы", Истина);
	Если НЕ Отказ 
		И РежимЗаписи = РежимЗаписиДокумента.Проведение
		И ПолучитьФункциональнуюОпцию("ИспользоватьЭтапыПроизводства")
		И ВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Сборка
		И ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Подразделение Тогда
		ПроверяемыеСпецификации = Новый Массив;
		Для каждого СтрокаТабличнойЧасти Из Продукция Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
				Продолжить;
			КонецЕсли; 
			ПроверяемыеСпецификации.Добавить(СтрокаТабличнойЧасти.Спецификация);
		КонецЦикла;
		СпецификацииСЭтапами = ПроизводствоСервер.СпецификацииСПоэтапнымПроизводством(ПроверяемыеСпецификации);
		Для каждого СтрокаТабличнойЧасти Из Продукция Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
				СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
			ИначеЕсли НЕ ПолучитьФункциональнуюОпцию("ВыполнениеЭтаповРазнымиПодразделениями")
				ИЛИ СпецификацииСЭтапами.Найти(СтрокаТабличнойЧасти.Спецификация) = Неопределено Тогда 
				// При производстве на одной структурной единице завершающие подразделения заполняются изготовителем
				СтрокаТабличнойЧасти.ПодразделениеЗавершающегоЭтапа = СтруктурнаяЕдиница;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СостоянияЗаказов.ПриЗаписиЗаказа(ЭтотОбъект);
	
	ПроизводствоСервер.ОбновитьСвязанныеСпецификации(Ссылка, ПометкаУдаления);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Этапы производства
	ПроверитьЗаполнениеЭтаповПроизводства(Отказ, ВариантЗавершения=Перечисления.ВариантыЗавершенияЗаказа.Отменен);
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 

	// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.ЗаказНаПроизводство.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ПроведениеДокументовУНФ.ОтразитьДвижения("ГрафикДвиженияЗапасов", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗаказыНаПроизводство", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ПотребностьВЗапасах", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("РазмещениеЗаказов", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("Запасы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ВыпускПродукции", ТаблицыДляДвижений, Движения, Отказ);
	
	// Этапы производства
	Если ВидОперации=Перечисления.ВидыОперацийЗаказНаПроизводство.Сборка Тогда
		ПроведениеДокументовУНФ.ОтразитьДвижения("ЭтапыПроизводства", ТаблицыДляДвижений, Движения, Отказ);
	КонецЕсли; 
	
	// Ресурсы предприятия
	Если ПолучитьФункциональнуюОпцию("ПланироватьЗагрузкуРесурсовПредприятия") Тогда
		ПроведениеДокументовУНФ.ОтразитьДвижения("РасписаниеЗагрузкиРесурсов", ТаблицыДляДвижений, Движения, Отказ);
		ТаблицаРасписаниеЗагрузкиРесурсов = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРасписаниеЗагрузкиРесурсов;
		ПланированиеРесурсовУНФ.СформироватьЗаписиКалендаряСотрудникаПоРесурсам(Ссылка, ТаблицаРасписаниеЗагрузкиРесурсов, Отказ);
	КонецЕсли;
	
	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль
	Документы.ЗаказНаПроизводство.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Этапы производства
	ПроверитьЗаполнениеЭтаповПроизводства(Отказ, Истина);
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 

	// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Ресурсы предприятия
	Если ПолучитьФункциональнуюОпцию("ПланироватьЗагрузкуРесурсовПредприятия") Тогда
		ПланированиеРесурсовУНФ.СформироватьЗаписиКалендаряСотрудникаПоРесурсам(Ссылка,Неопределено,Отказ);
	КонецЕсли;
	
	// Контроль
	Документы.ЗаказНаПроизводство.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ, Истина);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Константы.ФункциональнаяОпцияРезервированиеЗапасов.Получить() Тогда
		
		Если ВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Сборка Тогда
			
			Для каждого СтрокаЗапасы Из Запасы Цикл
				
				Если СтрокаЗапасы.Резерв > СтрокаЗапасы.Количество Тогда
					
					ТекстСообщения = СтрШаблон(НСтр(
						"ru = 'В строке №%1 табл. части ""Материалы"" количество резервируемых позиций превышает общее количество материалов.'"),
						СтрокаЗапасы.НомерСтроки);
					КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы",
						СтрокаЗапасы.НомерСтроки, "Резерв");
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
					
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			Для каждого СтрокаПродукция Из Продукция Цикл
				
				Если СтрокаПродукция.Резерв > СтрокаПродукция.Количество Тогда
					
					ТекстСообщения = СтрШаблон(НСтр(
						"ru = 'В строке №%1 табл. части ""Продукция"" количество резервируемых позиций превышает общее количество продукции.'"),
						СтрокаПродукция.НомерСтроки);
					КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция",
						СтрокаПродукция.НомерСтроки, "Резерв");
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Запасы.Количество() > 0 Тогда
		
		СтруктураОтбора = Новый Структура("ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Услуга);
		МассивСтрокУслуги = Продукция.НайтиСтроки(СтруктураОтбора);
		Если Продукция.Количество() = МассивСтрокУслуги.Количество() Тогда
			
			ТекстСообщения = НСтр("ru = 'Планирование потребностей в материалах не выполняется для услуг.
							|В табличной части ""Продукция"" указаны только услуги. Необходимо очистить табличную часть ""Материалы"".'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не СостояниеЗаказа = Справочники.СостоянияЗаказовНаПроизводство.Завершен Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ВариантЗавершения"));
	КонецЕсли;
	
	НоменклатураВДокументахСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, Отказ, Истина);
	
	// Склад резерва
	ИтогоРезерв = 0;
	Если ВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка Тогда
		ИмяТЧ = "Продукция";
	Иначе
		ИмяТЧ = "Запасы";
	КонецЕсли; 
	НеПроверять = (НЕ ПолучитьФункциональнуюОпцию("РезервированиеЗапасов") И НЕ УчетПотребностиПоСкладам);
	Для каждого СтрокаТабличнойЧасти Из ЭтотОбъект[ИмяТЧ] Цикл
		Если НеПроверять Тогда
			Прервать;
		КонецЕсли; 
		Если СтрокаТабличнойЧасти.Резерв <= 0 И НЕ УчетПотребностиПоСкладам Тогда
			Продолжить;
		КонецЕсли;
		Если ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			ЗаказПокупателяСтроки = СтрокаТабличнойЧасти.ЗаказПокупателя;
		Иначе
			ЗаказПокупателяСтроки = ЗаказПокупателя;
		КонецЕсли; 
		Если НЕ ЗначениеЗаполнено(ЗаказПокупателяСтроки) И НЕ УчетПотребностиПоСкладам Тогда
			Продолжить;
		КонецЕсли;
		ИтогоРезерв = ИтогоРезерв + СтрокаТабличнойЧасти.Резерв;
		Если ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти
			И Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтруктурнаяЕдиница) Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнена колонка ""Склад""  в строке %1 списка ""%2""'"),
				СтрокаТабличнойЧасти.НомерСтроки, ИмяТЧ);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ,
				СтрокаТабличнойЧасти.НомерСтроки, "СтруктурнаяЕдиница");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
		КонецЕсли; 
	КонецЦикла;
	Если (ИтогоРезерв > 0 И ПоложениеСклада <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти) ИЛИ УчетПотребностиПоСкладам Тогда
		ПроверяемыеРеквизиты.Добавить("СтруктурнаяЕдиницаРезерв");
	КонецЕсли;
	Если УчетПотребностиПоСкладам Тогда
		ПроверяемыеРеквизиты.Добавить("СтруктурнаяЕдиницаПродукции");
	КонецЕсли; 
	
	// Сравнение таблицы материалов и распределения
	Если РучноеРаспределение Тогда
		ПроизводствоСервер.СравнитьЗапасыИРаспределение(ЭтотОбъект, Отказ);
	Иначе
		ЗаполнениеОбъектовУНФ.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "РаспределениеЗапасов.Количество");
	КонецЕсли;
	
	Если ПоложениеЗаказаПокупателя=Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		ПроверитьЗаказыТЧ(Отказ);
	КонецЕсли; 
	
	// Этапы производства
	ТипСтруктурнойЕдиницы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "ТипСтруктурнойЕдиницы");
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЭтапыПроизводства") 
		И ВидОперации <> Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка
		И ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Подразделение Тогда
		МассивСпецификаций = Новый Массив;
		Для каждого СтрокаТабличнойЧасти Из Продукция Цикл
			Если МассивСпецификаций.Найти(СтрокаТабличнойЧасти.Спецификация) = Неопределено Тогда
				МассивСпецификаций.Добавить(СтрокаТабличнойЧасти.Спецификация);
			КонецЕсли; 
			Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
				Прервать;
			КонецЕсли; 
		КонецЦикла; 
		Если МассивСпецификаций.Найти(Справочники.Спецификации.ПустаяСсылка()) = Неопределено Тогда
			МассивЭтапов = ПроизводствоСервер.ЭтапыПроизводстваСпецификаций(МассивСпецификаций);
			Если МассивЭтапов.Найти(Справочники.ЭтапыПроизводства.ПустаяСсылка()) = Неопределено Тогда
				ПроверяемыеРеквизиты.Добавить("Запасы.Этап");
				Если РучноеРаспределение Тогда
					ПроверяемыеРеквизиты.Добавить("РаспределениеЗапасов.Этап");
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли; 
	
	//Ресурсы
	ЕстьОшибки = ПланированиеРесурсовУНФ.ЕстьОшибкиЗаполнения(РесурсыПредприятия, ЭтотОбъект);
	Если ЕстьОшибки Тогда
		Отказ = Истина;
	КонецЕсли; 
	
	// Операции
	Для каждого СтрокаТабличнойЧасти Из Операции Цикл
		СтруктураОтбора = Новый Структура("КлючСвязи", СтрокаТабличнойЧасти.КлючСвязиПродукция);
		СтрокиПродукции = Продукция.НайтиСтроки(СтруктураОтбора);
		Если СтрокиПродукции.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.КлючСвязиПродукция) Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не указана продукция в строке %1 списка ""Операции""'"),
				СтрокаТабличнойЧасти.НомерСтроки);
			КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Операции",
				СтрокаТабличнойЧасти.НомерСтроки, "Номенклатура");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , КонтекстноеПоле);
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЗаполненияДокумента

// АПК:299-выкл процедуры вызываются см. ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент

Процедура ЗаполнитьПоСтруктуре(ДанныеЗаполнения) Экспорт
	
	Если ДанныеЗаполнения.Свойство("МассивЗаказовПокупателей") Тогда
		ЗаполнитьПоЗаказуПокупателя(ДанныеЗаполнения);
	КонецЕсли;
	
	Если Не ДанныеЗаполнения.Свойство("РасчетПотребностей") Тогда
		Возврат;
	КонецЕсли;
	
	СтекСпецификацийУзлов = Новый Массив;
	ЗаполнитьЗапасыПоСпецификации(СтекСпецификацийУзлов);
	Если ПолучитьФункциональнуюОпцию("ИспользоватьТехоперации") И Константы.АвтоматическиПланироватьОперацийЗаказомНаПроизводство.Получить() Тогда
		ЗаполнитьОперацииПоСпецификации();
	КонецЕсли; 
	Если РучноеРаспределение Тогда
		ПроизводствоСервер.РаспределитьМатериалы(Продукция, Запасы, РаспределениеЗапасов, , Истина, ВидОперации);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуНаПроизводство(ДанныеЗаполнения) Экспорт
	
	РучноеРаспределение = Константы.ИспользоватьРучноеРаспределениеМатериаловПоУмолчанию.Получить()<>Перечисления.ДаНет.Нет;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказНаПроизводство.Старт КАК Старт,
	|	ЗаказНаПроизводство.Старт КАК Финиш,
	|	ЗаказНаПроизводство.ВидОперации КАК ВидОперации,
	|	ЗаказНаПроизводство.Ссылка КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ЗаказНаПроизводство.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ЗаказНаПроизводство.Организация КАК Организация,
	|	ЗаказНаПроизводство.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА (ЗаказНаПроизводство.СтруктурнаяЕдиницаРезерв = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|				ИЛИ ЗаказНаПроизводство.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|					И ЗаказНаПроизводство.ПоложениеЗаказаПокупателя = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВШапке))
	|				И (ЗаказНаПроизводство.СтруктурнаяЕдиница.ИсточникПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|					ИЛИ ЗаказНаПроизводство.СтруктурнаяЕдиница.ИсточникПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение))
	|			ТОГДА ЗаказНаПроизводство.СтруктурнаяЕдиница.ИсточникПеремещения
	|		ИНАЧЕ ЗаказНаПроизводство.СтруктурнаяЕдиницаРезерв
	|	КОНЕЦ КАК СтруктурнаяЕдиницаРезерв,
	|	ЗаказНаПроизводство.ПоложениеСклада КАК ПоложениеСклада,
	|	ЗаказНаПроизводство.ПоложениеЗаказаПокупателя КАК ПоложениеЗаказаПокупателя,
	|	ЗаказНаПроизводство.Запасы.(
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ЗаказНаПроизводство.Запасы.Количество - ЗаказНаПроизводство.Запасы.Резерв КАК Количество,
	|		Спецификация КАК Спецификация,
	|		Партия КАК Партия,
	|		ВЫБОР
	|			КОГДА ЗаказНаПроизводство.Запасы.Ссылка.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|					И ЗаказНаПроизводство.Запасы.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Сборка)
	|				ТОГДА ЗаказНаПроизводство.Запасы.Ссылка.СтруктурнаяЕдиница
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|		КОНЕЦ КАК ПодразделениеЗавершающегоЭтапа,
	|		Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|		Номенклатура.СпособПополнения КАК СпособПополнения,
	|		ЗаказПокупателя КАК ЗаказПокупателя
	|	) КАК Запасы
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
	|ГДЕ
	|	ЗаказНаПроизводство.Ссылка = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	Продукция.Очистить();
	Запасы.Очистить();
	РаспределениеЗапасов.Очистить();
	Операции.Очистить();
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
		ВыборкаИзРезультатаЗапроса.Следующий();
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаИзРезультатаЗапроса);
		
		ТаблицаЗапасы = ВыборкаИзРезультатаЗапроса.Запасы.Выгрузить();
		ТаблицаЗапасы.Свернуть("Номенклатура, Характеристика, ЕдиницаИзмерения, Спецификация, Партия, ПодразделениеЗавершающегоЭтапа, ТипНоменклатуры, СпособПополнения, ЗаказПокупателя", "Количество");
		Для каждого СтрокаЗапасы Из ТаблицаЗапасы Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаЗапасы.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;
			Если СтрокаЗапасы.Количество<=0 Тогда
				Продолжить;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(СтрокаЗапасы.Спецификация) 
				И СтрокаЗапасы.СпособПополнения=Перечисления.СпособыПополненияЗапасов.Закупка Тогда
				Продолжить;
			КонецЕсли; 
			НоваяСтрока = Продукция.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗапасы);
		КонецЦикла;
		
		Если Продукция.Количество() > 0 Тогда
			ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "Продукция", "ЗаказПокупателя", "ПоложениеЗаказаПокупателя");
			СтекСпецификацийУзлов = Новый Массив;
			ЗаполнитьЗапасыПоСпецификации(СтекСпецификацийУзлов);
			Если ПолучитьФункциональнуюОпцию("ИспользоватьТехоперации") И Константы.АвтоматическиПланироватьОперацийЗаказомНаПроизводство.Получить() Тогда
				ЗаполнитьОперацииПоСпецификации();
			КонецЕсли; 
			Если РучноеРаспределение Тогда
				ПроизводствоСервер.РаспределитьМатериалы(Продукция, Запасы, РаспределениеЗапасов, , Истина, ВидОперации);
			КонецЕсли; 
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуПокупателя(ДанныеЗаполнения) Экспорт
	
	РучноеРаспределение = Константы.ИспользоватьРучноеРаспределениеМатериаловПоУмолчанию.Получить()<>Перечисления.ДаНет.Нет;
	
	Если ВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка Тогда
		ИмяТабличнойЧасти = "Запасы";
	Иначе
		ИмяТабличнойЧасти = "Продукция";
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("МассивЗаказовПокупателей") Тогда
		МассивЗаказов = ДанныеЗаполнения.МассивЗаказовПокупателей;
		ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
	Иначе
		МассивЗаказов = Новый Массив;
		МассивЗаказов.Добавить(ДанныеЗаполнения);
		Если ЭтоНовый() ИЛИ НЕ ЗначениеЗаполнено(ПоложениеЗаказаПокупателя) Тогда
			ПоложениеЗаказаПокупателя = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ПоложениеЗаказаПокупателяВДокументахПроизводства");
		КонецЕсли; 
		Если НЕ ЗначениеЗаполнено(ПоложениеЗаказаПокупателя) Тогда
			ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
		КонецЕсли;
		Если ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
			ЗаказПокупателя = ДанныеЗаполнения;
		КонецЕсли;
	КонецЕсли;
	
	// Заполнение шапки.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПокупателя.Ссылка КАК ОснованиеСсылка,
	|	ЗаказПокупателя.Проведен КАК ОснованиеПроведен,
	|	ЗаказПокупателя.СостояниеЗаказа КАК ОснованиеСостояние,
	|	ЗаказПокупателя.ВидОперации КАК ОснованиеВидОперации,
	|	ЗаказПокупателя.Ссылка КАК ЗаказПокупателя,
	|	ЗаказПокупателя.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.СтруктурнаяЕдиницаПродажи.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗаказПокупателя.СтруктурнаяЕдиницаПродажи
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.СтруктурнаяЕдиницаПродажи.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|				И (ЗаказПокупателя.СтруктурнаяЕдиницаПродажи.ИсточникПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|					ИЛИ ЗаказПокупателя.СтруктурнаяЕдиницаПродажи.ИсточникПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение))
	|			ТОГДА ЗаказПокупателя.СтруктурнаяЕдиницаПродажи.ИсточникПеремещения
	|		ИНАЧЕ ЗаказПокупателя.СтруктурнаяЕдиницаРезерв
	|	КОНЕЦ КАК СтруктурнаяЕдиницаРезерв,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.СтруктурнаяЕдиницаРезерв <> ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|			ТОГДА ЗаказПокупателя.СтруктурнаяЕдиницаРезерв
	|		КОГДА ЗаказПокупателя.СтруктурнаяЕдиницаПродажи.ПолучательПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|				ИЛИ ЗаказПокупателя.СтруктурнаяЕдиницаПродажи.ПолучательПеремещения.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗаказПокупателя.СтруктурнаяЕдиницаПродажи.ПолучательПеремещения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК СтруктурнаяЕдиницаПродукции,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд)
	|			ТОГДА ЗаказПокупателя.Старт
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ЗаказПокупателя.ДатаОтгрузки, ДЕНЬ)
	|	КОНЕЦ КАК Старт,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд)
	|			ТОГДА ЗаказПокупателя.Финиш
	|		ИНАЧЕ КОНЕЦПЕРИОДА(ЗаказПокупателя.ДатаОтгрузки, ДЕНЬ)
	|	КОНЕЦ КАК Финиш,
	|	ЗаказПокупателя.ОжидаетсяВыборВариантаКП КАК ОжидаетсяВыборВариантаКП
	|ПОМЕСТИТЬ ЗаказыПокупателей
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.Ссылка В(&ДанныеЗаполнения)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыПокупателей.ОснованиеСсылка КАК ОснованиеСсылка,
	|	ЗаказыПокупателей.ОснованиеПроведен КАК ОснованиеПроведен,
	|	ЗаказыПокупателей.ОснованиеСостояние КАК ОснованиеСостояние,
	|	ЗаказыПокупателей.ОснованиеВидОперации КАК ОснованиеВидОперации,
	|	ЗаказыПокупателей.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ЗаказыПокупателей.Организация КАК Организация,
	|	ЗаказыПокупателей.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗаказыПокупателей.СтруктурнаяЕдиницаРезерв КАК СтруктурнаяЕдиницаРезерв,
	|	ЗаказыПокупателей.СтруктурнаяЕдиницаПродукции КАК СтруктурнаяЕдиницаПродукции,
	|	ЗаказыПокупателей.Старт КАК Старт,
	|	ЗаказыПокупателей.Финиш КАК Финиш,
	|	ЗаказыПокупателей.ОжидаетсяВыборВариантаКП КАК ОжидаетсяВыборВариантаКП
	|ИЗ
	|	ЗаказыПокупателей КАК ЗаказыПокупателей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЗаказыПокупателей.ЗаказПокупателя) КАК ЗаказПокупателя,
	|	МАКСИМУМ(ЗаказыПокупателей.Организация) КАК Организация,
	|	МАКСИМУМ(ЗаказыПокупателей.СтруктурнаяЕдиница) КАК СтруктурнаяЕдиница,
	|	МАКСИМУМ(ЗаказыПокупателей.СтруктурнаяЕдиницаРезерв) КАК СтруктурнаяЕдиницаРезерв,
	|	МАКСИМУМ(ЗаказыПокупателей.СтруктурнаяЕдиницаПродукции) КАК СтруктурнаяЕдиницаПродукции,
	|	МАКСИМУМ(ЗаказыПокупателей.ОснованиеВидОперации) КАК ОснованиеВидОперации,
	|	МИНИМУМ(ЗаказыПокупателей.Старт) КАК Старт,
	|	МАКСИМУМ(ЗаказыПокупателей.Финиш) КАК Финиш
	|ИЗ
	|	ЗаказыПокупателей КАК ЗаказыПокупателей";
	
	Запрос.УстановитьПараметр("ДанныеЗаполнения", МассивЗаказов);
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗначенияПроверяемыхРеквизитов = Новый Структура(
			"ВидОперации, СостояниеЗаказа, Проведен, ОжидаетсяВыборВариантаКП",
			Выборка.ОснованиеВидОперации, Выборка.ОснованиеСостояние, Выборка.ОснованиеПроведен, Выборка.ОжидаетсяВыборВариантаКП
		);
		Документы.ЗаказПокупателя.ПроверитьВозможностьВводаНаОснованииЗаказаПокупателя(Выборка.ОснованиеСсылка, ЗначенияПроверяемыхРеквизитов);
	КонецЦикла;
	
	Выборка = Результат[2].Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	Если НЕ ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ОсновноеПодразделение");
		Если НЕ ЗначениеЗаполнено(ЗначениеНастройки) Тогда
			СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение;
		КонецЕсли;
	КонецЕсли;
	
	ОснованиеВидОперации = Выборка.ОснованиеВидОперации;
	
	// Заполнение табличной части.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыОстатки.Характеристика КАК Характеристика,
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|		ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|		ЗаказыОстатки.Характеристика КАК Характеристика,
	|		ЗаказыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.ЗаказыПокупателей.Остатки(, ЗаказПокупателя В (&ДанныеЗаполнения)) КАК ЗаказыОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗапасыОстатки.ЗаказПокупателя,
	|		ЗапасыОстатки.Номенклатура,
	|		ЗапасыОстатки.Характеристика,
	|		-ЗапасыОстатки.КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.Запасы.Остатки(, ЗаказПокупателя В (&ДанныеЗаполнения)) КАК ЗапасыОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РазмещениеОстатки.ЗаказПокупателя,
	|		РазмещениеОстатки.Номенклатура,
	|		РазмещениеОстатки.Характеристика,
	|		-РазмещениеОстатки.КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.РазмещениеЗаказов.Остатки(, ЗаказПокупателя В (&ДанныеЗаполнения)) КАК РазмещениеОстатки,
	|		Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
	|	ГДЕ
	|		ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаРазмещениеЗаказов.ЗаказПокупателя,
	|		ДвиженияДокументаРазмещениеЗаказов.Номенклатура,
	|		ДвиженияДокументаРазмещениеЗаказов.Характеристика,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаРазмещениеЗаказов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -ЕСТЬNULL(ДвиженияДокументаРазмещениеЗаказов.Количество, 0)
	|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаРазмещениеЗаказов.Количество, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РазмещениеЗаказов КАК ДвиженияДокументаРазмещениеЗаказов,
	|		Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
	|	ГДЕ
	|		ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|		И ДвиженияДокументаРазмещениеЗаказов.Регистратор = &Ссылка
	|		И ДвиженияДокументаРазмещениеЗаказов.ЗаказПокупателя В(&ДанныеЗаполнения)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗаказНаПроизводство.ДокументОснование,
	|		ЗаказНаПроизводствоПродукция.Номенклатура,
	|		ЗаказНаПроизводствоПродукция.Характеристика,
	|		-ЗаказНаПроизводствоПродукция.Количество * ВЫБОР
	|			КОГДА ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|				ТОГДА ВЫРАЗИТЬ(ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
	|			ИНАЧЕ 1
	|		КОНЕЦ
	|	ИЗ
	|		Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
	|			ПО ЗаказНаПроизводство.Ссылка = ЗаказНаПроизводствоПродукция.Ссылка,
	|		Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
	|	ГДЕ
	|		НЕ ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|		И ЗаказНаПроизводство.ДокументОснование В(&ДанныеЗаполнения)
	|		И ЗаказНаПроизводство.Ссылка <> &Ссылка
	|		И ЗаказНаПроизводство.Проведен) КАК ЗаказыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыОстатки.ЗаказПокупателя,
	|	ЗаказыОстатки.Номенклатура,
	|	ЗаказыОстатки.Характеристика
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПокупателяЗапасы.Ссылка КАК ЗаказПокупателя,
	|	МИНИМУМ(ЗаказПокупателяЗапасы.НомерСтроки) КАК НомерСтроки,
	|	ЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяЗапасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА 1
	|		ИНАЧЕ ЗаказПокупателяЗапасы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Коэффициент,
	|	ЗаказПокупателяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПокупателяЗапасы.Спецификация КАК Спецификация,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЗаказПокупателяЗапасы.Спецификация.ВидПроизводства <> ЗНАЧЕНИЕ(Справочник.ВидыПроизводства.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ, ЛОЖЬ) КАК ИспользуютсяЭтапы,
	|	ЗаказПокупателяЗапасы.Партия КАК Партия,
	|	ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	СУММА(ВЫБОР
	|			КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка)
	|				ТОГДА ЗаказПокупателяЗапасы.Количество
	|			ИНАЧЕ ЗаказПокупателяЗапасы.Количество - ЗаказПокупателяЗапасы.Резерв
	|		КОНЕЦ) КАК Количество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВЫБОР
	|						КОГДА ЗаказПокупателяЗапасы.Спецификация.ВидПроизводства <> ЗНАЧЕНИЕ(Справочник.ВидыПроизводства.ПустаяСсылка)
	|							ТОГДА ИСТИНА
	|						ИНАЧЕ ЛОЖЬ
	|					КОНЕЦ, ЛОЖЬ)
	|				И &ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка)
	|			ТОГДА ЕСТЬNULL(ЭтапыПроизводстваСтруктурныеЕдиницы.СтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка))
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК ПодразделениеЗавершающегоЭтапа
	|ИЗ
	|	ВТЗаказПокупателяЗапасы КАК ЗаказПокупателяЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства.СтруктурныеЕдиницы КАК ЭтапыПроизводстваСтруктурныеЕдиницы
	|		ПО (ЭтапыПроизводстваСтруктурныеЕдиницы.Ссылка = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ЗавершениеПроизводства))
	|			И (ЭтапыПроизводстваСтруктурныеЕдиницы.ПоУмолчанию)
	|ГДЕ
	|	(ЗаказПокупателяЗапасы.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|			ИЛИ ЗаказПокупателяЗапасы.Номенклатура.СпособПополнения = ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство)
	|			ИЛИ ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ИЛИ &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказПокупателяЗапасы.Ссылка,
	|	ЗаказПокупателяЗапасы.Номенклатура,
	|	ЗаказПокупателяЗапасы.Характеристика,
	|	ЗаказПокупателяЗапасы.ЕдиницаИзмерения,
	|	ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА 1
	|		ИНАЧЕ ЗаказПокупателяЗапасы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ,
	|	ЗаказПокупателяЗапасы.Спецификация,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЗаказПокупателяЗапасы.Спецификация.ВидПроизводства <> ЗНАЧЕНИЕ(Справочник.ВидыПроизводства.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ, ЛОЖЬ),
	|	ЗаказПокупателяЗапасы.Партия,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВЫБОР
	|						КОГДА ЗаказПокупателяЗапасы.Спецификация.ВидПроизводства <> ЗНАЧЕНИЕ(Справочник.ВидыПроизводства.ПустаяСсылка)
	|							ТОГДА ИСТИНА
	|						ИНАЧЕ ЛОЖЬ
	|					КОНЕЦ, ЛОЖЬ)
	|				И &ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка)
	|			ТОГДА ЕСТЬNULL(ЭтапыПроизводстваСтруктурныеЕдиницы.СтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка))
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка)
	|				ТОГДА ЗаказПокупателяЗапасы.Количество
	|			ИНАЧЕ ЗаказПокупателяЗапасы.Количество - ЗаказПокупателяЗапасы.Резерв
	|		КОНЕЦ) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Документы.ЗаказПокупателя.ДобавитьТаблицуЗапасыВМенеджерВременныхТаблиц(МассивЗаказов, Запрос.МенеджерВременныхТаблиц, Ложь);
	
	Если ОснованиеВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд Тогда
		
		Запрос.Текст = Запрос.Текст + "; " +
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыОстатки.Характеристика КАК Характеристика,
		|	СУММА(ЗаказыОстатки.КоличествоОстаток) КАК КоличествоОстаток
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
		|		ЗапасыОстатки.Номенклатура КАК Номенклатура,
		|		ЗапасыОстатки.Характеристика КАК Характеристика,
		|		ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток
		|	ИЗ
		|		РегистрНакопления.Запасы.Остатки(
		|				,
		|				ЗаказПокупателя В (&ДанныеЗаполнения)
		|					И Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)) КАК ЗапасыОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		РазмещениеОстатки.ЗаказПокупателя,
		|		РазмещениеОстатки.Номенклатура,
		|		РазмещениеОстатки.Характеристика,
		|		РазмещениеОстатки.КоличествоОстаток
		|	ИЗ
		|		РегистрНакопления.РазмещениеЗаказов.Остатки(
		|				,
		|				ЗаказПокупателя В (&ДанныеЗаполнения)
		|					И Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)) КАК РазмещениеОстатки,
		|		Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
		|	ГДЕ
		|		ФункциональнаяОпцияРезервированиеЗапасов.Значение
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДвиженияДокументаРазмещениеЗаказов.ЗаказПокупателя,
		|		ДвиженияДокументаРазмещениеЗаказов.Номенклатура,
		|		ДвиженияДокументаРазмещениеЗаказов.Характеристика,
		|		ВЫБОР
		|			КОГДА ДвиженияДокументаРазмещениеЗаказов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА ЕСТЬNULL(ДвиженияДокументаРазмещениеЗаказов.Количество, 0)
		|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаРазмещениеЗаказов.Количество, 0)
		|		КОНЕЦ
		|	ИЗ
		|		РегистрНакопления.РазмещениеЗаказов КАК ДвиженияДокументаРазмещениеЗаказов,
		|		Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
		|	ГДЕ
		|		ФункциональнаяОпцияРезервированиеЗапасов.Значение
		|		И ДвиженияДокументаРазмещениеЗаказов.Регистратор = &Ссылка
		|		И ДвиженияДокументаРазмещениеЗаказов.ЗаказПокупателя В(&ДанныеЗаполнения)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗаказНаПроизводство.ДокументОснование,
		|		ЗаказНаПроизводствоПродукция.Номенклатура,
		|		ЗаказНаПроизводствоПродукция.Характеристика,
		|		-ЗаказНаПроизводствоПродукция.Количество * ВЫБОР
		|			КОГДА ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
		|				ТОГДА ВЫРАЗИТЬ(ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
		|			ИНАЧЕ 1
		|		КОНЕЦ
		|	ИЗ
		|		Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
		|			ПО ЗаказНаПроизводство.Ссылка = ЗаказНаПроизводствоПродукция.Ссылка,
		|		Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
		|	ГДЕ
		|		НЕ ФункциональнаяОпцияРезервированиеЗапасов.Значение
		|		И ЗаказНаПроизводство.ДокументОснование В(&ДанныеЗаполнения)
		|		И ЗаказНаПроизводство.Ссылка <> &Ссылка
		|		И ЗаказНаПроизводство.Проведен) КАК ЗаказыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыОстатки.ЗаказПокупателя,
		|	ЗаказыОстатки.Номенклатура,
		|	ЗаказыОстатки.Характеристика
		|
		|ИМЕЮЩИЕ
		|	СУММА(ЗаказыОстатки.КоличествоОстаток) > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказПокупателяМатериалы.Ссылка КАК ЗаказПокупателя,
		|	МИНИМУМ(ЗаказПокупателяМатериалы.НомерСтроки) КАК НомерСтроки,
		|	ЗаказПокупателяМатериалы.Номенклатура КАК Номенклатура,
		|	ЗаказПокупателяМатериалы.Характеристика КАК Характеристика,
		|	ЗаказПокупателяМатериалы.Партия КАК Партия,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяМатериалы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
		|			ТОГДА 1
		|		ИНАЧЕ ЗаказПокупателяМатериалы.ЕдиницаИзмерения.Коэффициент
		|	КОНЕЦ КАК Коэффициент,
		|	ЗаказПокупателяМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЗаказПокупателяМатериалы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
		|	СУММА(ВЫБОР
		|			КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка)
		|				ТОГДА ЗаказПокупателяМатериалы.Количество
		|			ИНАЧЕ ЗаказПокупателяМатериалы.Количество
		|		КОНЕЦ) КАК Количество,
		|	ЕСТЬNULL(ЭтапыПроизводстваСтруктурныеЕдиницы.СтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)) КАК ПодразделениеЗавершающегоЭтапа
		|ИЗ
		|	Документ.ЗаказПокупателя.Материалы КАК ЗаказПокупателяМатериалы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства.СтруктурныеЕдиницы КАК ЭтапыПроизводстваСтруктурныеЕдиницы
		|		ПО (ЭтапыПроизводстваСтруктурныеЕдиницы.Ссылка = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ЗавершениеПроизводства))
		|			И (ЭтапыПроизводстваСтруктурныеЕдиницы.ПоУмолчанию)
		|ГДЕ
		|	ЗаказПокупателяМатериалы.Ссылка В(&ДанныеЗаполнения)
		|	И (ЗаказПокупателяМатериалы.Номенклатура.СпособПополнения = ЗНАЧЕНИЕ(Перечисление.СпособыПополненияЗапасов.Производство)
		|			ИЛИ &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка))
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказПокупателяМатериалы.Ссылка,
		|	ЗаказПокупателяМатериалы.Номенклатура,
		|	ЗаказПокупателяМатериалы.Характеристика,
		|	ЗаказПокупателяМатериалы.Партия,
		|	ЗаказПокупателяМатериалы.ЕдиницаИзмерения,
		|	ЗаказПокупателяМатериалы.Номенклатура.ТипНоменклатуры,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяМатериалы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
		|			ТОГДА 1
		|		ИНАЧЕ ЗаказПокупателяМатериалы.ЕдиницаИзмерения.Коэффициент
		|	КОНЕЦ,
		|	ЕСТЬNULL(ЭтапыПроизводстваСтруктурныеЕдиницы.СтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка))
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВЫБОР
		|			КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка)
		|				ТОГДА ЗаказПокупателяМатериалы.Количество
		|			ИНАЧЕ ЗаказПокупателяМатериалы.Количество - ЗаказПокупателяМатериалы.Резерв
		|		КОНЕЦ) > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		
	ИначеЕсли ОснованиеВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаПереработку Тогда
		
		Запрос.Текст = Запрос.Текст + "; " +
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыОстатки.Характеристика КАК Характеристика,
		|	СУММА(ЗаказыОстатки.КоличествоОстаток) КАК КоличествоОстаток
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
		|		ЗапасыОстатки.Номенклатура КАК Номенклатура,
		|		ЗапасыОстатки.Характеристика КАК Характеристика,
		|		ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток
		|	ИЗ
		|		РегистрНакопления.Запасы.Остатки(
		|				,
		|				ЗаказПокупателя В (&ДанныеЗаполнения)
		|					И Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)) КАК ЗапасыОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		РазмещениеОстатки.ЗаказПокупателя,
		|		РазмещениеОстатки.Номенклатура,
		|		РазмещениеОстатки.Характеристика,
		|		РазмещениеОстатки.КоличествоОстаток
		|	ИЗ
		|		РегистрНакопления.РазмещениеЗаказов.Остатки(
		|				,
		|				ЗаказПокупателя В (&ДанныеЗаполнения)
		|					И Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)) КАК РазмещениеОстатки,
		|		Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
		|	ГДЕ
		|		ФункциональнаяОпцияРезервированиеЗапасов.Значение
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗаказНаПроизводство.ДокументОснование,
		|		ЗаказНаПроизводствоПродукция.Номенклатура,
		|		ЗаказНаПроизводствоПродукция.Характеристика,
		|		-ЗаказНаПроизводствоПродукция.Количество * ВЫБОР
		|			КОГДА ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
		|				ТОГДА ВЫРАЗИТЬ(ЗаказНаПроизводствоПродукция.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
		|			ИНАЧЕ 1
		|		КОНЕЦ
		|	ИЗ
		|		Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
		|			ПО ЗаказНаПроизводство.Ссылка = ЗаказНаПроизводствоПродукция.Ссылка,
		|		Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
		|	ГДЕ
		|		НЕ ФункциональнаяОпцияРезервированиеЗапасов.Значение
		|		И ЗаказНаПроизводство.ДокументОснование В(&ДанныеЗаполнения)
		|		И ЗаказНаПроизводство.Ссылка <> &Ссылка
		|		И ЗаказНаПроизводство.Проведен
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДвиженияДокументаПотребностьВЗапасах.ЗаказПокупателя,
		|		ДвиженияДокументаПотребностьВЗапасах.Номенклатура,
		|		ДвиженияДокументаПотребностьВЗапасах.Характеристика,
		|		ДвиженияДокументаПотребностьВЗапасах.Количество
		|	ИЗ
		|		РегистрНакопления.ПотребностьВЗапасах КАК ДвиженияДокументаПотребностьВЗапасах
		|	ГДЕ
		|		ДвиженияДокументаПотребностьВЗапасах.Регистратор = &Ссылка
		|		И ДвиженияДокументаПотребностьВЗапасах.ЗаказПокупателя В(&ДанныеЗаполнения)
		|		И ДвиженияДокументаПотребностьВЗапасах.ТипДвижения = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженийЗапасов.Отгрузка)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДвиженияДокументаПотребностьВЗапасахОбороты.ЗаказПокупателя,
		|		ДвиженияДокументаПотребностьВЗапасахОбороты.Номенклатура,
		|		ДвиженияДокументаПотребностьВЗапасахОбороты.Характеристика,
		|		-ДвиженияДокументаПотребностьВЗапасахОбороты.КоличествоПриход
		|	ИЗ
		|		РегистрНакопления.ПотребностьВЗапасах.Обороты(
		|				,
		|				,
		|				Период,
		|				ЗаказПокупателя В (&ДанныеЗаполнения)
		|					И ТипДвижения = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженийЗапасов.Отгрузка)) КАК ДвиженияДокументаПотребностьВЗапасахОбороты) КАК ЗаказыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыОстатки.ЗаказПокупателя,
		|	ЗаказыОстатки.Номенклатура,
		|	ЗаказыОстатки.Характеристика
		|
		|ИМЕЮЩИЕ
		|	СУММА(ЗаказыОстатки.КоличествоОстаток) > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказПокупателяМатериалыЗаказчика.Ссылка КАК ЗаказПокупателя,
		|	МИНИМУМ(ЗаказПокупателяМатериалыЗаказчика.НомерСтроки) КАК НомерСтроки,
		|	ЗаказПокупателяМатериалыЗаказчика.Номенклатура КАК Номенклатура,
		|	ЗаказПокупателяМатериалыЗаказчика.Характеристика КАК Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка) КАК Партия,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяМатериалыЗаказчика.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
		|			ТОГДА 1
		|		ИНАЧЕ ЗаказПокупателяМатериалыЗаказчика.ЕдиницаИзмерения.Коэффициент
		|	КОНЕЦ КАК Коэффициент,
		|	ЗаказПокупателяМатериалыЗаказчика.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЗаказПокупателяМатериалыЗаказчика.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
		|	СУММА(ВЫБОР
		|			КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка)
		|				ТОГДА ЗаказПокупателяМатериалыЗаказчика.Количество
		|			ИНАЧЕ ЗаказПокупателяМатериалыЗаказчика.Количество
		|		КОНЕЦ) КАК Количество,
		|	ЕСТЬNULL(ЭтапыПроизводстваСтруктурныеЕдиницы.СтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)) КАК ПодразделениеЗавершающегоЭтапа,
		|	ЗаказПокупателяМатериалыЗаказчика.Номенклатура.Склад КАК СтруктурнаяЕдиница
		|ИЗ
		|	Документ.ЗаказПокупателя.МатериалыЗаказчика КАК ЗаказПокупателяМатериалыЗаказчика
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства.СтруктурныеЕдиницы КАК ЭтапыПроизводстваСтруктурныеЕдиницы
		|		ПО (ЭтапыПроизводстваСтруктурныеЕдиницы.Ссылка = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ЗавершениеПроизводства))
		|			И (ЭтапыПроизводстваСтруктурныеЕдиницы.ПоУмолчанию)
		|ГДЕ
		|	ЗаказПокупателяМатериалыЗаказчика.Ссылка В(&ДанныеЗаполнения)
		|	И ЗаказПокупателяМатериалыЗаказчика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказПокупателяМатериалыЗаказчика.Ссылка,
		|	ЗаказПокупателяМатериалыЗаказчика.Номенклатура,
		|	ЗаказПокупателяМатериалыЗаказчика.Характеристика,
		|	ЗаказПокупателяМатериалыЗаказчика.ЕдиницаИзмерения,
		|	ЗаказПокупателяМатериалыЗаказчика.Номенклатура.ТипНоменклатуры,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяМатериалыЗаказчика.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
		|			ТОГДА 1
		|		ИНАЧЕ ЗаказПокупателяМатериалыЗаказчика.ЕдиницаИзмерения.Коэффициент
		|	КОНЕЦ,
		|	ЕСТЬNULL(ЭтапыПроизводстваСтруктурныеЕдиницы.СтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)),
		|	ЗаказПокупателяМатериалыЗаказчика.Номенклатура.Склад
		|
		|ИМЕЮЩИЕ
		|	СУММА(ЗаказПокупателяМатериалыЗаказчика.Количество) > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДанныеЗаполнения", МассивЗаказов);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидОперации", ВидОперации);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ТаблицаОстатков = МассивРезультатов[0].Выгрузить();
	ТаблицаОстатков.Индексы.Добавить("ЗаказПокупателя, Номенклатура, Характеристика");
	
	Продукция.Очистить();
	Запасы.Очистить();
	РаспределениеЗапасов.Очистить();
	Операции.Очистить();
	Если НЕ ЗначениеЗаполнено(ВидОперации) Тогда
		ВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Сборка;
	КонецЕсли; 
	СборкаВПодразделении = (ВидОперации = Перечисления.ВидыОперацийЗаказНаПроизводство.Сборка
	И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "ТипСтруктурнойЕдиницы") = Перечисления.ТипыСтруктурныхЕдиниц.Подразделение);
	Если ТаблицаОстатков.Количество() > 0 Тогда
		
		Выборка = МассивРезультатов[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СтруктураДляПоиска = Новый Структура;
			СтруктураДляПоиска.Вставить("ЗаказПокупателя", Выборка.ЗаказПокупателя);
			СтруктураДляПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
			СтруктураДляПоиска.Вставить("Характеристика", Выборка.Характеристика);
			
			МассивСтрокОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураДляПоиска);
			Если МассивСтрокОстатков.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ЭтотОбъект[ИмяТабличнойЧасти].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.Спецификация) Тогда
				НоваяСтрока.Спецификация = Справочники.Спецификации.СпецификацияПоУмолчанию(НоваяСтрока.Номенклатура,
					НоваяСтрока.Характеристика);
			КонецЕсли;
			
			КоличествоКСписанию = Выборка.Количество * Выборка.Коэффициент;
			МассивСтрокОстатков[0].КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток - КоличествоКСписанию;
			Если МассивСтрокОстатков[0].КоличествоОстаток < 0 Тогда
				
				НоваяСтрока.Количество = (КоличествоКСписанию + МассивСтрокОстатков[0].КоличествоОстаток) / Выборка.Коэффициент;
				
			КонецЕсли;
			
			Если МассивСтрокОстатков[0].КоличествоОстаток <= 0 Тогда
				ТаблицаОстатков.Удалить(МассивСтрокОстатков[0]);
			КонецЕсли;
			
			Если ИмяТабличнойЧасти = "Продукция"
				И СборкаВПодразделении
				И Выборка.ИспользуютсяЭтапы
				И НЕ ЗначениеЗаполнено(НоваяСтрока.ПодразделениеЗавершающегоЭтапа) Тогда
				НоваяСтрока.ПодразделениеЗавершающегоЭтапа = СтруктурнаяЕдиница;
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ОснованиеВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд Тогда
		
		ТаблицаОстатков = МассивРезультатов[2].Выгрузить();
		ТаблицаОстатков.Индексы.Добавить("ЗаказПокупателя, Номенклатура, Характеристика");
		
		Выборка = МассивРезультатов[3].Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если ИмяТабличнойЧасти = "Запасы"
				И Выборка.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Запас Тогда
				Продолжить;
			КонецЕсли;
			
			СтруктураДляПоиска = Новый Структура;
			СтруктураДляПоиска.Вставить("ЗаказПокупателя", Выборка.ЗаказПокупателя);
			СтруктураДляПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
			СтруктураДляПоиска.Вставить("Характеристика", Выборка.Характеристика);
			
			МассивСтрокОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураДляПоиска);
			Если МассивСтрокОстатков.Количество() = 0 Тогда
				
				НоваяСтрока = ЭтотОбъект[ИмяТабличнойЧасти].Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				
				НоваяСтрока.Спецификация = Справочники.Спецификации.СпецификацияПоУмолчанию(НоваяСтрока.Номенклатура,
					НоваяСтрока.Характеристика);
				
			ИначеЕсли (МассивСтрокОстатков[0].КоличествоОстаток / Выборка.Коэффициент) = Выборка.Количество Тогда
				
				Продолжить;
				
			ИначеЕсли (МассивСтрокОстатков[0].КоличествоОстаток / Выборка.Коэффициент) > Выборка.Количество Тогда
				
				МассивСтрокОстатков[0].КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток - Выборка.Количество * Выборка.Коэффициент;
				Продолжить;
				
			ИначеЕсли (МассивСтрокОстатков[0].КоличествоОстаток / Выборка.Коэффициент) < Выборка.Количество Тогда
				
				КоличествоКСписанию = -1 * (МассивСтрокОстатков[0].КоличествоОстаток / Выборка.Коэффициент - Выборка.Количество);
				МассивСтрокОстатков[0].КоличествоОстаток = 0;
				
				НоваяСтрока = ЭтотОбъект[ИмяТабличнойЧасти].Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				
				НоваяСтрока.Количество = (КоличествоКСписанию + МассивСтрокОстатков[0].КоличествоОстаток) / Выборка.Коэффициент;
				
				НоваяСтрока.Спецификация = Справочники.Спецификации.СпецификацияПоУмолчанию(НоваяСтрока.Номенклатура,
					НоваяСтрока.Характеристика);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ОснованиеВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаПереработку Тогда
		
		Если ИмяТабличнойЧасти="Запасы" Тогда
			ИмяТабличнойЧастиМатериалы = "Продукция";
		Иначе
			ИмяТабличнойЧастиМатериалы = "Запасы";
		КонецЕсли; 
		
		ТаблицаОстатков = МассивРезультатов[2].Выгрузить();
		ТаблицаОстатков.Индексы.Добавить("ЗаказПокупателя, Номенклатура, Характеристика");
		
		Выборка = МассивРезультатов[3].Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СтруктураДляПоиска = Новый Структура;
			СтруктураДляПоиска.Вставить("ЗаказПокупателя", Выборка.ЗаказПокупателя);
			СтруктураДляПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
			СтруктураДляПоиска.Вставить("Характеристика", Выборка.Характеристика);
			
			МассивСтрокОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураДляПоиска);
			Если МассивСтрокОстатков.Количество() = 0 Тогда
				Остаток = 0;
			Иначе
				Остаток = МассивСтрокОстатков[0].КоличествоОстаток / Выборка.Коэффициент;
			КонецЕсли;
			Если Остаток<=0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если Остаток<Выборка.Количество Тогда
				КСписанию = Остаток;
			Иначе
				КСписанию = Выборка.Количество;
			КонецЕсли; 
			
			НоваяСтрока = ЭтотОбъект[ИмяТабличнойЧастиМатериалы].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.Спецификация = Справочники.Спецификации.СпецификацияПоУмолчанию(НоваяСтрока.Номенклатура, НоваяСтрока.Характеристика);
			НоваяСтрока.Количество = КСписанию;
			МассивСтрокОстатков[0].КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток - КСписанию * Выборка.Коэффициент;
			
		КонецЦикла;
		
		Если ИмяТабличнойЧастиМатериалы = "Запасы" Тогда
			ПроизводствоСервер.ЗаполнитьЭтапыМатериалов(Продукция.Выгрузить(), Запасы);
		КонецЕсли; 
		
	ИначеЕсли Продукция.Количество() > 0 Тогда
		
		СтекСпецификацийУзлов = Новый Массив;
		ЗаполнитьЗапасыПоСпецификации(СтекСпецификацийУзлов);
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьТехоперации") И Константы.АвтоматическиПланироватьОперацийЗаказомНаПроизводство.Получить() Тогда
		ЗаполнитьОперацииПоСпецификации();
	КонецЕсли; 
	Если РучноеРаспределение И Запасы.Количество()>0 Тогда
		ПроизводствоСервер.РаспределитьМатериалы(Продукция, Запасы, РаспределениеЗапасов, , Истина, ВидОперации);
	КонецЕсли;
	
КонецПроцедуры

// АПК:299-вкл

Процедура ЗаполнитьКолонкуРезервПоОстаткам(ОбъектЗапасы) Экспорт
	
	Если ПоложениеЗаказаПокупателя<>Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти 
		И НЕ ЗначениеЗаполнено(ЗаказПокупателя) Тогда
		// Резерв возможен только под заказ покупателя 
		Возврат;
	КонецЕсли; 
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВЫРАЗИТЬ(ТаблицаЗапасы.СтруктурнаяЕдиница КАК Справочник.СтруктурныеЕдиницы) КАК СтруктурнаяЕдиница
	|ПОМЕСТИТЬ ТабличнаяЧасть
	|ИЗ
	|	&ТаблицаЗапасы КАК ТаблицаЗапасы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Партия КАК Партия,
	|	ТабличнаяЧасть.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТабличнаяЧасть.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасы
	|ИЗ
	|	ТабличнаяЧасть КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)";
	
	ТаблицаЗапасы = ОбъектЗапасы.Выгрузить();
	Если ПоложениеСклада<>Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для каждого СтрокаТабличнойЧасти Из ТаблицаЗапасы Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = СтруктурнаяЕдиницаРезерв;
		КонецЦикла; 
	КонецЕсли; 
	Если ПоложениеЗаказаПокупателя<>Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для каждого СтрокаТабличнойЧасти Из ТаблицаЗапасы Цикл
			СтрокаТабличнойЧасти.ЗаказПокупателя = ЗаказПокупателя;
		КонецЦикла; 
	КонецЕсли; 
	Запрос.УстановитьПараметр("ТаблицаЗапасы", ТаблицаЗапасы);
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗапасыОстатки.Организация КАК Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыОстатки.Характеристика КАК Характеристика,
	|	ЗапасыОстатки.Партия КАК Партия,
	|	СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыОстатки.Организация КАК Организация,
	|		ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыОстатки.Характеристика КАК Характеристика,
	|		ЗапасыОстатки.Партия КАК Партия,
	|		ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.Запасы.Остатки(
	|				,
	|				(Организация, СтруктурнаяЕдиница, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
	|					(ВЫБРАТЬ
	|						&Организация,
	|						ТаблицаЗапасы.СтруктурнаяЕдиница,
	|						ТаблицаЗапасы.Номенклатура,
	|						ТаблицаЗапасы.Характеристика,
	|						ТаблицаЗапасы.Партия,
	|						ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка) КАК ЗаказПокупателя
	|					ИЗ
	|						ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы)) КАК ЗапасыОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаЗапасы.Организация,
	|		ДвиженияДокументаЗапасы.СтруктурнаяЕдиница,
	|		ДвиженияДокументаЗапасы.Номенклатура,
	|		ДвиженияДокументаЗапасы.Характеристика,
	|		ДвиженияДокументаЗапасы.Партия,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
	|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.Запасы КАК ДвиженияДокументаЗапасы
	|	ГДЕ
	|		ДвиженияДокументаЗапасы.Регистратор = &Ссылка
	|		И ДвиженияДокументаЗапасы.Период <= &Период
	|		И ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)) КАК ЗапасыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыОстатки.Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница,
	|	ЗапасыОстатки.Номенклатура,
	|	ЗапасыОстатки.Характеристика,
	|	ЗапасыОстатки.Партия";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Организация", Константы.УчетПоКомпании.Компания(Организация));
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураДляПоиска = Новый Структура;
		СтруктураДляПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
		СтруктураДляПоиска.Вставить("Характеристика", Выборка.Характеристика);
		СтруктураДляПоиска.Вставить("Партия", Выборка.Партия);
		Если ПоложениеСклада=Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			СтруктураДляПоиска.Вставить("СтруктурнаяЕдиница", Выборка.СтруктурнаяЕдиница);
		КонецЕсли; 
		
		ВсегоОстаток = Выборка.КоличествоОстаток;
		МассивСтрокЗапасы = ОбъектЗапасы.НайтиСтроки(СтруктураДляПоиска);
		Для каждого СтрокаЗапасы Из МассивСтрокЗапасы Цикл
			Если ПоложениеЗаказаПокупателя=Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти 
				И НЕ ЗначениеЗаполнено(СтрокаЗапасы.ЗаказПокупателя) Тогда
				Продолжить;
			КонецЕсли; 
			ВсегоОстаток = ?(ТипЗнч(СтрокаЗапасы.ЕдиницаИзмерения) = ТИП("СправочникСсылка.КлассификаторЕдиницИзмерения"), ВсегоОстаток, ВсегоОстаток / СтрокаЗапасы.ЕдиницаИзмерения.Коэффициент);
			ПотребностьВРезерве = СтрокаЗапасы.НеОтгружено;
			Если ПотребностьВРезерве >= ВсегоОстаток Тогда
				СтрокаЗапасы.Резерв = ВсегоОстаток;
				ВсегоОстаток = 0;
			Иначе
				СтрокаЗапасы.Резерв = ПотребностьВРезерве;
				ВсегоОстаток = ВсегоОстаток - ПотребностьВРезерве;
				ВсегоОстаток = ?(ТипЗнч(СтрокаЗапасы.ЕдиницаИзмерения) = ТИП("СправочникСсылка.КлассификаторЕдиницИзмерения"), ВсегоОстаток, ВсегоОстаток * СтрокаЗапасы.ЕдиницаИзмерения.Коэффициент);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьЗапасыПоСпецификации(СтекСпецификацийУзлов, ТаблицаУзлы = Неопределено) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПродукция.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПродукция.Количество КАК Количество,
	|	ТаблицаПродукция.Коэффициент КАК Коэффициент,
	|	ТаблицаПродукция.Спецификация КАК Спецификация,
	|	ТаблицаПродукция.Этап КАК Этап,
	|	ТаблицаПродукция.ЗаказПокупателя КАК ЗаказПокупателя
	|ПОМЕСТИТЬ ВременнаяТаблицаПродукция
	|ИЗ
	|	&ТаблицаПродукция КАК ТаблицаПродукция
	|ГДЕ
	|	ТаблицаПродукция.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)";
	
	Если ТаблицаУзлы = Неопределено Тогда
		Запасы.Очистить();
		ТаблицаПродукция = Продукция.Выгрузить();
		ОписаниеТиповЧисло = Новый ОписаниеТипов("Число", , ,Новый КвалификаторыЧисла(10,3));
		ТаблицаПродукция.Колонки.Добавить("Коэффициент", ОписаниеТиповЧисло);
		ТаблицаПродукция.Колонки.Добавить("Этап", Новый ОписаниеТипов("СправочникСсылка.ЭтапыПроизводства"));
		Для каждого СтрокаПродукция Из ТаблицаПродукция Цикл
			Если ЗначениеЗаполнено(СтрокаПродукция.ЕдиницаИзмерения)
				И ТипЗнч(СтрокаПродукция.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
				СтрокаПродукция.Коэффициент = СтрокаПродукция.ЕдиницаИзмерения.Коэффициент;
			Иначе
				СтрокаПродукция.Коэффициент = 1;
			КонецЕсли;
		КонецЦикла;
		ТаблицаУзлы = ТаблицаПродукция.СкопироватьКолонки("НомерСтроки, Количество, Коэффициент, Спецификация, ЗаказПокупателя, Этап");
		Запрос.УстановитьПараметр("ТаблицаПродукция", ТаблицаПродукция);
	Иначе
		Запрос.УстановитьПараметр("ТаблицаПродукция", ТаблицаУзлы);
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Запрос.УстановитьПараметр("ВидОперации", ВидОперации);
	Запрос.УстановитьПараметр("ТипСтруктурнойЕдиницы", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "ТипСтруктурнойЕдиницы"));
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МИНИМУМ(ТаблицаПродукция.НомерСтроки) КАК НомерСтрокиПродукции,
	|	ТаблицаПродукция.Спецификация КАК СпецификацияПродукции,
	|	МИНИМУМ(ТаблицаМатериалы.НомерСтроки) КАК НомерСтрокиСостава,
	|	ТаблицаМатериалы.ТипСтрокиСостава КАК ТипСтрокиСостава,
	|	ВЫБОР
	|		КОГДА НЕ ФункциональнаяОпцияИспользоватьЭтапыПроизводства.Значение
	|				ИЛИ &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка)
	|				ИЛИ &ТипСтруктурнойЕдиницы <> ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|		КОГДА ТаблицаПродукция.Этап <> ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|			ТОГДА ТаблицаПродукция.Этап
	|		ИНАЧЕ ТаблицаМатериалы.Этап
	|	КОНЕЦ КАК Этап,
	|	ТаблицаМатериалы.Номенклатура КАК Номенклатура,
	|	ТаблицаМатериалы.Номенклатура.Склад КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ИспользоватьХарактеристики.Значение
	|			ТОГДА ТаблицаМатериалы.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	СУММА(ТаблицаМатериалы.Количество / ТаблицаМатериалы.КоличествоПродукции * ТаблицаПродукция.Коэффициент * ТаблицаПродукция.Количество) КАК Количество,
	|	ТаблицаМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаМатериалы.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|				И ТИПЗНАЧЕНИЯ(ТаблицаМатериалы.ЕдиницаИзмерения) = ТИП(Справочник.ЕдиницыИзмерения)
	|				И ТаблицаМатериалы.ЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ТаблицаМатериалы.ЕдиницаИзмерения.Коэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Коэффициент,
	|	ТаблицаМатериалы.ДоляСтоимости КАК ДоляСтоимости,
	|	ВЫБОР
	|		КОГДА ТаблицаМатериалы.Спецификация = ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(СпецификацииПоУмолчанию.Спецификация, ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка))
	|		ИНАЧЕ ТаблицаМатериалы.Спецификация
	|	КОНЕЦ КАК Спецификация,
	|	ТаблицаПродукция.ЗаказПокупателя КАК ЗаказПокупателя
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ТаблицаПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Спецификации.Состав КАК ТаблицаМатериалы
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпецификацииПоУмолчанию КАК СпецификацииПоУмолчанию
	|			ПО ТаблицаМатериалы.Номенклатура = СпецификацииПоУмолчанию.Номенклатура
	|				И ТаблицаМатериалы.Характеристика = СпецификацииПоУмолчанию.Характеристика
	|		ПО ТаблицаПродукция.Спецификация = ТаблицаМатериалы.Ссылка,
	|	Константа.ФункциональнаяОпцияИспользоватьХарактеристики КАК ИспользоватьХарактеристики,
	|	Константа.ФункциональнаяОпцияИспользоватьЭтапыПроизводства КАК ФункциональнаяОпцияИспользоватьЭтапыПроизводства
	|ГДЕ
	|	ТаблицаМатериалы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПродукция.Спецификация,
	|	ТаблицаМатериалы.ТипСтрокиСостава,
	|	ВЫБОР
	|		КОГДА НЕ ФункциональнаяОпцияИспользоватьЭтапыПроизводства.Значение
	|				ИЛИ &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка)
	|				ИЛИ &ТипСтруктурнойЕдиницы <> ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|		КОГДА ТаблицаПродукция.Этап <> ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|			ТОГДА ТаблицаПродукция.Этап
	|		ИНАЧЕ ТаблицаМатериалы.Этап
	|	КОНЕЦ,
	|	ТаблицаМатериалы.Номенклатура,
	|	ТаблицаМатериалы.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаМатериалы.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|				И ТИПЗНАЧЕНИЯ(ТаблицаМатериалы.ЕдиницаИзмерения) = ТИП(Справочник.ЕдиницыИзмерения)
	|				И ТаблицаМатериалы.ЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ТаблицаМатериалы.ЕдиницаИзмерения.Коэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ТаблицаМатериалы.ДоляСтоимости,
	|	ВЫБОР
	|		КОГДА ТаблицаМатериалы.Спецификация = ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(СпецификацииПоУмолчанию.Спецификация, ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка))
	|		ИНАЧЕ ТаблицаМатериалы.Спецификация
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ИспользоватьХарактеристики.Значение
	|			ТОГДА ТаблицаМатериалы.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаПродукция.ЗаказПокупателя,
	|	ТаблицаМатериалы.Номенклатура.Склад
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиПродукции,
	|	НомерСтрокиСостава";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ТипСтрокиСостава = Перечисления.ТипыСтрокСоставаСпецификации.Узел Тогда
			ТаблицаУзлы.Очистить();
			Если НЕ СтекСпецификацийУзлов.Найти(Выборка.Спецификация) = Неопределено Тогда
				ТекстСообщения = НСтр("ru = 'При попытке заполнить табличную часть Материалы по спецификации,
									|обнаружено рекурсивное вхождение элемента'")+" "+Выборка.Номенклатура+" "+НСтр("ru = 'в спецификации'")+" "+Выборка.СпецификацияПродукции+"
									|Операция не выполнена!";
				ВызватьИсключение ТекстСообщения;
			КонецЕсли;
			СтекСпецификацийУзлов.Добавить(Выборка.Спецификация);
			НоваяСтрока = ТаблицаУзлы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			ЗаполнитьЗапасыПоСпецификации(СтекСпецификацийУзлов, ТаблицаУзлы);
		Иначе
			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЕсли;
	КонецЦикла;
	
	СтекСпецификацийУзлов.Очистить();
	Запасы.Свернуть(
		"Этап, Номенклатура, Характеристика, Партия, ЕдиницаИзмерения, Спецификация, ЗаказПокупателя, СтруктурнаяЕдиница", 
		"Количество, Резерв, ДоляСтоимости");
	
	СтатусПартии = Новый СписокЗначений;
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.СобственныеЗапасы"));
	СтатусПартии.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПартий.ДавальческоеСырье"));
	
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		
		СтрокаТабличнойЧасти.Партия = ?(ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура)
		, НоменклатураВДокументахСервер.ЗначенияПартийНоменклатурыПоУмолчанию(СтрокаТабличнойЧасти.Номенклатура,СтатусПартии)
		, Неопределено);
		
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтруктурнаяЕдиница) 
			И ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			Продолжить;
		КонецЕсли; 
		СтрокаТабличнойЧасти.СтруктурнаяЕдиница = СтруктурнаяЕдиницаРезерв;
	КонецЦикла;
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "Запасы", "ЗаказПокупателя", "ПоложениеЗаказаПокупателя");
	
КонецПроцедуры

Процедура ЗаполнитьОперацииПоСпецификации() Экспорт  
	
	Операции.Очистить();
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьТехоперации") Тогда
		Возврат;
	КонецЕсли; 
	
	ПроизводствоСервер.ЗаполнитьКлючиСвязи(Продукция);
	
	Операции.Очистить();
	Если ПоложениеИсполнителя=Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		СоставБригады.Очистить();
	КонецЕсли; 
	
	ТаблицаПродукция = Продукция.Выгрузить();
	ИспользоватьРезервирование = Константы.ФункциональнаяОпцияРезервированиеЗапасов.Получить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Запасы", ТаблицаПродукция.Скопировать(, "НомерСтроки, Номенклатура, Спецификация"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.Спецификация КАК Спецификация,
	|	ТабличнаяЧасть.Спецификация КАК ВложеннаяСпецификация,
	|	ИСТИНА КАК ЭтоУзел,
	|	ТабличнаяЧасть.Номенклатура КАК Операция,
	|	1 КАК НормаВремени,
	|	1 КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	&Запасы КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	0 КАК Уровень,
	|	МИНИМУМ(ВременнаяТаблица.НомерСтроки) КАК Порядок,
	|	0 КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка) КАК Этап,
	|	ВременнаяТаблица.Спецификация КАК Спецификация,
	|	ВременнаяТаблица.ВложеннаяСпецификация КАК ВложеннаяСпецификация,
	|	ВременнаяТаблица.ЭтоУзел КАК ЭтоУзел,
	|	ВременнаяТаблица.Операция КАК Операция,
	|	ВременнаяТаблица.НормаВремени КАК НормаВремени,
	|	ВременнаяТаблица.Количество КАК Количество
	|ПОМЕСТИТЬ ТаблицаОпераций
	|ИЗ
	|	ВременнаяТаблица КАК ВременнаяТаблица
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблица.Спецификация,
	|	ВременнаяТаблица.Операция,
	|	ВременнаяТаблица.ВложеннаяСпецификация,
	|	ВременнаяТаблица.ЭтоУзел,
	|	ВременнаяТаблица.НормаВремени,
	|	ВременнаяТаблица.Количество
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВременнаяТаблица";
	Запрос.Выполнить();
	ПроизводствоСервер.РазузловатьОперации(Запрос, Неопределено, "Продукция");
	
	Запрос.УстановитьПараметр("Продукция", ТаблицаПродукция);
	Запрос.УстановитьПараметр("ВидОперации", ВидОперации);
	Запрос.УстановитьПараметр("ТипСтруктурнойЕдиницы", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "ТипСтруктурнойЕдиницы"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Продукция.НомерСтроки КАК НомерСтроки,
	|	Продукция.КлючСвязи КАК КлючСвязиПродукция,
	|	Продукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВЫРАЗИТЬ(Продукция.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	Продукция.Спецификация КАК Спецификация,
	|	Продукция.Количество КАК Количество,
	|	Продукция.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ПОМЕСТИТЬ Продукция
	|ИЗ
	|	&Продукция КАК Продукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Продукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	Продукция.НомерСтроки КАК НомерСтроки,
	|	Продукция.КлючСвязиПродукция КАК КлючСвязиПродукция,
	|	Продукция.Количество * ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Продукция.ЕдиницаИзмерения) = ТИП(Справочник.ЕдиницыИзмерения)
	|				И Продукция.ЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(Продукция.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ * ЕСТЬNULL(ВЫБОР
	|			КОГДА ТаблицаОпераций.Количество = 0
	|				ТОГДА 1
	|			ИНАЧЕ ТаблицаОпераций.Количество
	|		КОНЕЦ, 1) КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ ФункциональнаяОпцияИспользоватьЭтапыПроизводства.Значение
	|				ИЛИ &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка)
	|				ИЛИ &ТипСтруктурнойЕдиницы <> ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаОпераций.Этап
	|	КОНЕЦ КАК Этап,
	|	ТаблицаОпераций.Операция КАК Операция,
	|	ВЫБОР
	|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ТаблицаОпераций.Операция.ЕдиницаИзмерения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаОпераций.Операция ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ТаблицаОпераций.Операция.ФиксированнаяСтоимость
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ФиксированнаяСтоимость,
	|	ЕСТЬNULL(ТаблицаОпераций.НормаВремени, 0) КАК НормаВремени,
	|	ТаблицаОпераций.Уровень КАК Уровень,
	|	ТаблицаОпераций.Порядок КАК Порядок,
	|	ТаблицаОпераций.НомерСтроки КАК НомерСтрокиОперации
	|ПОМЕСТИТЬ Операции
	|ИЗ
	|	Продукция КАК Продукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОпераций КАК ТаблицаОпераций
	|		ПО Продукция.Спецификация = ТаблицаОпераций.Спецификация,
	|	Константа.ФункциональнаяОпцияИспользоватьЭтапыПроизводства КАК ФункциональнаяОпцияИспользоватьЭтапыПроизводства
	|ГДЕ
	|	Продукция.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|	И НЕ ТаблицаОпераций.Операция ЕСТЬ NULL
	|	И ТаблицаОпераций.Операция <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Операции.ЗаказПокупателя КАК ЗаказПокупателя,
	|	Операции.НомерСтроки КАК НомерСтроки,
	|	Операции.КлючСвязиПродукция КАК КлючСвязиПродукция,
	|	Операции.Количество КАК КоличествоПлан,
	|	Операции.Этап КАК Этап,
	|	Операции.Операция КАК Операция,
	|	Операции.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
	|	Операции.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Операции.НормаВремени КАК НормаВремени
	|ИЗ
	|	Операции КАК Операции
	|
	|УПОРЯДОЧИТЬ ПО
	|	Операции.НомерСтроки,
	|	Операции.Уровень,
	|	Операции.Порядок,
	|	Операции.НомерСтрокиОперации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	КлючСвязи = 0;
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Операции.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Если НЕ ИспользоватьРезервирование Тогда
			НоваяСтрока.ЗаказПокупателя = Документы.ЗаказПокупателя.ПустаяСсылка();
		КонецЕсли;
		Если НЕ ПоложениеИсполнителя=Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			НоваяСтрока.Исполнитель = Исполнитель;
		КонецЕсли; 
		Если ПоложениеСтруктурнойЕдиницыОпераций=Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			НоваяСтрока.СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение;
		Иначе
			НоваяСтрока.СтруктурнаяЕдиница = СтруктурнаяЕдиницаОпераций;
		КонецЕсли;
		НоваяСтрока.Нормочасы = НоваяСтрока.НормаВремени * НоваяСтрока.КоличествоПлан;
		КлючСвязи = КлючСвязи + 1;
		НоваяСтрока.КлючСвязи = КлючСвязи;
	КонецЦикла;
	ЗаполнениеОбъектовУНФ.ЗаполнитьТаблицуПоШапке(ЭтотОбъект, "Операции", "Исполнитель", "ПоложениеИсполнителя");
	Если ПоложениеСтруктурнойЕдиницыОпераций<>Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для каждого СтрокаТабличнойЧасти Из Операции Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = СтруктурнаяЕдиницаОпераций;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ДозаполнитьПоУмолчанию()
	
	Если Не ЗначениеЗаполнено(СостояниеЗаказа) Тогда
		СостояниеЗаказа = ЗаполнениеОбъектовУНФ.ПолучитьСостояниеЗаказаНаПроизводство();
	КонецЕсли;
	Если ЭтоНовый() Тогда
		УчетПотребностиПоЗаказамНаПроизводство = Истина;
		УчетПотребностиПоСкладам = ПолучитьФункциональнуюОпцию("УчетПотребностиПоСкладам");
		УчетПотребностиПоЗаказам = Истина;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПривестиДанныеКСогласованномуСостоянию()
	
	Если СостояниеЗаказа <> Справочники.СостоянияЗаказовНаПроизводство.Завершен Тогда
		ВариантЗавершения = Перечисления.ВариантыЗавершенияЗаказа.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

Функция СтруктурнаяЕдиницаОперацийШапки()
	
	ТекСтруктурнаяЕдиница = СтруктурнаяЕдиницаОпераций;
	
	Если Операции.Количество()=0 Тогда
		Возврат ТекСтруктурнаяЕдиница;
	КонецЕсли; 
	
	ТекСтруктурнаяЕдиница = Операции[0].СтруктурнаяЕдиница;
	
	Если Операции.Количество()=1 Тогда
		Возврат ТекСтруктурнаяЕдиница;
	КонецЕсли; 
	
	ТаблицаСтруктурныеЕдиницы = Операции.Выгрузить(, "СтруктурнаяЕдиница");
	ТаблицаСтруктурныеЕдиницы.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	Для Каждого ТекСтрока Из ТаблицаСтруктурныеЕдиницы Цикл
		ТекСтрока.Количество = 1;
	КонецЦикла;
	ТаблицаСтруктурныеЕдиницы.Свернуть("СтруктурнаяЕдиница", "Количество");
	
	Если ТаблицаСтруктурныеЕдиницы.Количество() < 2 Тогда
		Возврат ТекСтруктурнаяЕдиница;
	КонецЕсли;
	
	ТаблицаСтруктурныеЕдиницы.Сортировать("Количество Убыв");
	
	Если ТаблицаСтруктурныеЕдиницы[0].Количество = ТаблицаСтруктурныеЕдиницы[1].Количество Тогда
		Возврат ТекСтруктурнаяЕдиница;
	КонецЕсли;
	
	ТекСтруктурнаяЕдиница = ТаблицаСтруктурныеЕдиницы[0].СтруктурнаяЕдиница;
	
	Возврат ТекСтруктурнаяЕдиница;
	
КонецФункции

Процедура ПроверитьЗаказыТЧ(Отказ)
	
	Заказы = Новый Массив;
	Для каждого СтрокаТабличнойЧасти Из Продукция Цикл
		Если Заказы.Найти(СтрокаТабличнойЧасти.ЗаказПокупателя) = Неопределено Тогда
			Заказы.Добавить(СтрокаТабличнойЧасти.ЗаказПокупателя);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		Если Заказы.Найти(СтрокаТабличнойЧасти.ЗаказПокупателя) = Неопределено Тогда
			ТекстСообщения = СтрШаблон(НСтр(
				"ru = 'В строке №%1 табл. части ""Материалы"" используется заказ покупателя, отсутствующий в табл. части ""Продукция"".'"),
				СтрокаТабличнойЧасти.НомерСтроки);
			КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы",
				СтрокаТабличнойЧасти.НомерСтроки, "ЗаказПокупателя");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаТабличнойЧасти Из Операции Цикл
		Если Заказы.Найти(СтрокаТабличнойЧасти.ЗаказПокупателя) = Неопределено Тогда
			ТекстСообщения = СтрШаблон(НСтр(
				"ru = 'В строке №%1 табл. части ""Операции"" используется заказ покупателя, отсутствующий в табл. части ""Продукция"".'"),
				СтрокаТабличнойЧасти.НомерСтроки);
			КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Операции",
				СтрокаТабличнойЧасти.НомерСтроки, "ЗаказПокупателя");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеЭтаповПроизводства(Отказ, ОтменаПроведения = Ложь)
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьЭтапыПроизводства") Тогда
		Возврат;
	КонецЕсли;
	
	// Формирование таблицы продукции, использующей этапы производства
	Если ОтменаПроведения Тогда
		ТаблицаПродукции = Продукция.ВыгрузитьКолонки();
	Иначе
		ТаблицаПродукции = Продукция.Выгрузить();
	КонецЕсли; 
	ТаблицаПродукции.Колонки.Добавить("ИмяТЧ", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(100)));
	ТаблицаПродукции.ЗаполнитьЗначения("Продукция", "ИмяТЧ");
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ВидОперации")<>Перечисления.ВидыОперацийЗаказНаПроизводство.Разборка Тогда
		ПроизводствоСервер.ДобавитьУдаленнуюПродукцию(Ссылка, ТаблицаПродукции, "Продукция");
	КонецЕсли; 
	ТаблицаПродукции.Колонки.Добавить("ЗаказНаПроизводство", Новый ОписаниеТипов("ДокументСсылка.ЗаказНаПроизводство"));
	ТаблицаПродукции.ЗаполнитьЗначения(Ссылка, "ЗаказНаПроизводство");
	Если ПоложениеЗаказаПокупателя <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		ТаблицаПродукции.ЗаполнитьЗначения(ЗаказПокупателя, "ЗаказПокупателя");
	КонецЕсли;
	МассивСпецификаций = Новый Массив;
	Для каждого СтрокаТабличнойЧасти Из ТаблицаПродукции Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Спецификация) Тогда
			Продолжить;
		КонецЕсли; 
		Если МассивСпецификаций.Найти(СтрокаТабличнойЧасти.Спецификация)=Неопределено Тогда
			МассивСпецификаций.Добавить(СтрокаТабличнойЧасти.Спецификация);
		КонецЕсли; 
	КонецЦикла; 
	МассивСпецификаций = ПроизводствоСервер.СпецификацииСПоэтапнымПроизводством(МассивСпецификаций);
	ПроверяемаяПродукция = ТаблицаПродукции.СкопироватьКолонки();
	Для каждого СтрокаПродукция Из ТаблицаПродукции Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаПродукция.Спецификация) ИЛИ МассивСпецификаций.Найти(СтрокаПродукция.Спецификация) = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		ЗаполнитьЗначенияСвойств(ПроверяемаяПродукция.Добавить(), СтрокаПродукция);
	КонецЦикла;
	
	// Поэтапное производство выполняется только на подразделениях
	ТипСтруктурнойЕдиницы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "ТипСтруктурнойЕдиницы");
	Если ПроверяемаяПродукция.Количество() > 0
		И ТипСтруктурнойЕдиницы <> Перечисления.ТипыСтруктурныхЕдиниц.Подразделение Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр(
			"ru = 'Изготовителем при поэтапном производстве может быть только подразделение.'"), ,
			"Объект.СтруктурнаяЕдиница", , Отказ);
	КонецЕсли; 
	
	// Для поэтапного производства доступен только вид операции "Сборка"
	Если ПроверяемаяПродукция.Количество() > 0
		И ВидОперации <> Перечисления.ВидыОперацийЗаказНаПроизводство.Сборка Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр(
			"ru = 'При поэтапном производстве доступен только вид операции ""Сборка"".'"), , "Объект.ВидОперации", ,
			Отказ);
	КонецЕсли; 
	
	// Запрет дублирования продукции
	ТаблицаКонтроляДублей = ПроверяемаяПродукция.Скопировать();
	ТаблицаКонтроляДублей.Колонки.Добавить("КоличествоСтрок", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5, 0)));
	ТаблицаКонтроляДублей.ЗаполнитьЗначения(1, "КоличествоСтрок");
	ТаблицаКонтроляДублей.Свернуть("ЗаказПокупателя, ЗаказНаПроизводство, Номенклатура, Характеристика, Партия", "КоличествоСтрок");
	Для каждого СтрокаПродукция Из ТаблицаКонтроляДублей Цикл
		Если СтрокаПродукция.КоличествоСтрок > 1 Тогда
			СтруктураОтбора = Новый Структура("ЗаказПокупателя, ЗаказНаПроизводство, Номенклатура, Характеристика, Партия");
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаПродукция);
			СтрокиТЧ = ТаблицаПродукции.НайтиСтроки(СтруктураОтбора);
			Для каждого НайденнаяСтрока Из СтрокиТЧ Цикл
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'Дублирующаяся продукция, использующая этапы производства, в строке %1.'"),
					НайденнаяСтрока.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция",
					НайденнаяСтрока.НомерСтроки, "Номенклатура");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
			КонецЦикла; 
		КонецЕсли; 
	КонецЦикла; 
	
	Если ПолучитьФункциональнуюОпцию("ВыполнениеЭтаповРазнымиПодразделениями") 
		И НЕ ОтменаПроведения
		И ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Подразделение Тогда
		Для каждого СтрокаПродукция Из ПроверяемаяПродукция Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаПродукция.ПодразделениеЗавершающегоЭтапа) Тогда
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'Не заполнена колонка ""Завершающее подразделение"" в строке %1 списка ""Продукция"".'"),
					СтрокаПродукция.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция",
					СтрокаПродукция.НомерСтроки, "ПодразделениеЗавершающегоЭтапа");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	
	// Проверка уникальности завершающего подразделения и количества продукции
	ПроизводствоСервер.ПроверкаПоэтапногоПроизводства(ЭтотОбъект, ПроверяемаяПродукция, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли