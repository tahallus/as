
&НаСервере
Процедура МедиапланПриИзмененииНаСервере()

	ДанныеМедиаплана = ПолучитьДанныеМедиаплана();
	Объект.СтрокиМедиаплана.Загрузить(ДанныеМедиаплана.СтрокиМедиаплана);
	Объект.СтрокиМедиапланаРазмещение.Загрузить(ДанныеМедиаплана.СтрокиМедиапланаРазмещение);
	Объект.СтрокиМедиапланаРасшифровка.Загрузить(ДанныеМедиаплана.СтрокиМедиапланаРасшифровка);
	
КонецПроцедуры

&НаКлиенте
Процедура МедиапланПриИзменении(Элемент)
	МедиапланПриИзмененииНаСервере();
	СтрокиМедиапланаРасшифровкаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеМедиаплана()
	
	Запрос = Новый Запрос;
	
	//Установка значений параметров
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Медиаплан", Объект.Медиаплан);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	_МедиапланСтрокиМедиаплана.КлючСвязи КАК КлючСвязи,
	               |	_МедиапланСтрокиМедиаплана.Номенклатура КАК Номенклатура
	               |ИЗ
	               |	Документ._Медиаплан.СтрокиМедиаплана КАК _МедиапланСтрокиМедиаплана
	               |ГДЕ
	               |	_МедиапланСтрокиМедиаплана.Ссылка = &Медиаплан
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	КлючСвязи
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	_МедиапланСтрокиМедиапланаРасшифровка.КлючСвязи КАК КлючСвязи,
	               |	_МедиапланСтрокиМедиапланаРасшифровка.ДатаНачала КАК ДатаНачала,
	               |	_МедиапланСтрокиМедиапланаРасшифровка.ДатаОкончания КАК ДатаОкончания,
	               |	_МедиапланСтрокиМедиапланаРасшифровка.Показов КАК КоличествоПлановое,
	               |	СУММА(ЕСТЬNULL(_МедиапланФактическиеДанные.Количество, 0)) КАК КоличествоОтраженноеРанее
	               |ИЗ
	               |	Документ._Медиаплан.СтрокиМедиапланаРасшифровка КАК _МедиапланСтрокиМедиапланаРасшифровка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений._МедиапланФактическиеДанные КАК _МедиапланФактическиеДанные
	               |		ПО _МедиапланСтрокиМедиапланаРасшифровка.Ссылка = _МедиапланФактическиеДанные.Медиаплан
	               |			И _МедиапланСтрокиМедиапланаРасшифровка.КлючСвязи = _МедиапланФактическиеДанные.КлючСвязи
	               |			И _МедиапланСтрокиМедиапланаРасшифровка.ДатаНачала = _МедиапланФактическиеДанные.ДатаНачала
	               |			И _МедиапланСтрокиМедиапланаРасшифровка.ДатаОкончания = _МедиапланФактическиеДанные.ДатаОкончания
	               |			И (_МедиапланФактическиеДанные.Регистратор <> &Ссылка)
	               |ГДЕ
	               |	_МедиапланСтрокиМедиапланаРасшифровка.Ссылка = &Медиаплан
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	_МедиапланСтрокиМедиапланаРасшифровка.КлючСвязи,
	               |	_МедиапланСтрокиМедиапланаРасшифровка.ДатаНачала,
	               |	_МедиапланСтрокиМедиапланаРасшифровка.ДатаОкончания,
	               |	_МедиапланСтрокиМедиапланаРасшифровка.Показов
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	КлючСвязи,
	               |	ДатаНачала
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	_МедиапланРазмещениеСтрокиМедиаплана.КлючСвязи КАК КлючСвязи,
	               |	_МедиапланРазмещениеСтрокиМедиаплана.Номенклатура КАК Номенклатура,
	               |	_МедиапланРазмещениеСтрокиМедиаплана.Площадка КАК Площадка,
	               |	_МедиапланРазмещениеСтрокиМедиаплана.НеУчитыватьФакт КАК НеУчитыватьФакт,
	               |	_МедиапланРазмещениеСтрокиМедиаплана.Баннер КАК Баннер,
	               |	0 КАК Количество
	               |ИЗ
	               |	Документ._МедиапланРазмещение.СтрокиМедиаплана КАК _МедиапланРазмещениеСтрокиМедиаплана
	               |ГДЕ
	               |	_МедиапланРазмещениеСтрокиМедиаплана.Ссылка.Медиаплан = &Медиаплан
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	КлючСвязи";
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	Результат = Новый Структура("СтрокиМедиаплана, СтрокиМедиапланаРазмещение, СтрокиМедиапланаРасшифровка", ПакетРезультатов[0].Выгрузить(), ПакетРезультатов[2].Выгрузить(), ПакетРезультатов[1].Выгрузить());
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура СтрокиМедиапланаПриАктивизацииСтроки(Элемент)
	Если Элемент.ТекущиеДанные <> Неопределено Тогда 
		Элементы.СтрокиМедиапланаРасшифровка.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", Элемент.ТекущиеДанные.КлючСвязи);
		Элементы.СтрокиМедиапланаРазмещение.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", Элемент.ТекущиеДанные.КлючСвязи);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПересчитатьСтроки()

	Для Каждого СтрокаМедиаплана Из Объект.СтрокиМедиаплана Цикл 
		ОтборСтрок = Новый Структура("КлючСвязи, НеУчитыватьФакт", СтрокаМедиаплана.КлючСвязи, Ложь);
		СтрокиРазмещения = Объект.СтрокиМедиапланаРазмещение.НайтиСтроки(ОтборСтрок);
		Количество = 0;
		Для Каждого СтрокаРазмещения Из СтрокиРазмещения Цикл
			Количество = Количество + СтрокаРазмещения.Количество;
		КонецЦикла;
		СтрокаМедиаплана.Количество = Количество;
	КонецЦикла;

КонецПроцедуры


&НаКлиенте
Процедура СтрокиМедиапланаРазмещениеПриИзменении(Элемент)
	ПересчитатьСтроки();
	РаспределитьПоказы();
КонецПроцедуры


&НаСервере
Процедура СтрокиМедиапланаРасшифровкаПриИзмененииНаСервере()

	Для Каждого СтрокаРасшифровки Из Объект.СтрокиМедиапланаРасшифровка Цикл
		СтрокаРасшифровки.Остаток = СтрокаРасшифровки.КоличествоПлановое - СтрокаРасшифровки.КоличествоОтраженноеРанее - СтрокаРасшифровки.Количество;
	КонецЦикла;
	
КонецПроцедуры





&НаКлиенте
Процедура СтрокиМедиапланаРасшифровкаПриИзменении(Элемент)
	СтрокиМедиапланаРасшифровкаПриИзмененииНаСервере();
КонецПроцедуры


&НаСервере
Процедура РаспределитьПоказы()
	
	Для Каждого СтрокаМедиаплана Из Объект.СтрокиМедиаплана Цикл
		ОсталосьРаспределить = 	СтрокаМедиаплана.Количество;
		ОтборСтрок = Новый Структура("КлючСвязи", СтрокаМедиаплана.КлючСвязи);
		СтрокиРасшифровки = Объект.СтрокиМедиапланаРасшифровка.НайтиСтроки(ОтборСтрок);
		Для Каждого СтрокаРасшифровки Из СтрокиРасшифровки Цикл
			СтрокаРасшифровки.Количество = Мин(СтрокаРасшифровки.КоличествоПлановое - СтрокаРасшифровки.КоличествоОтраженноеРанее, ОсталосьРаспределить);
			СтрокаРасшифровки.Остаток = СтрокаРасшифровки.КоличествоПлановое - СтрокаРасшифровки.КоличествоОтраженноеРанее - СтрокаРасшифровки.Количество;
			ОсталосьРаспределить = ОсталосьРаспределить - СтрокаРасшифровки.Количество;
		КонецЦикла;
	КонецЦикла;

	
КонецПроцедуры

