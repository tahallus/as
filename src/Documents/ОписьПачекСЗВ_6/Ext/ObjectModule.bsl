#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ВыборкаДокументовДляПроверки = СформироватьЗапросПоПачкамДокументовДляПроверки().Выбрать();

	Пока ВыборкаДокументовДляПроверки.Следующий() Цикл
		
		Если Не ВыборкаДокументовДляПроверки.ФайлСформирован Тогда
			ШаблонСообщения = НСтр("ru = 'У документа в строке %1 не сформирован файл.'");
			СообщитьОбОшибкеВСтроке(ШаблонСообщения, ВыборкаДокументовДляПроверки.НомерСтроки, Отказ);
		КонецЕсли;

		Если ВыборкаДокументовДляПроверки.ОтчетныйПериод <> ОтчетныйПериод Тогда
			ШаблонСообщения = НСтр("ru = 'Отчетный период документа-пачки в строке %1 отличается от указанного.'");
			СообщитьОбОшибкеВСтроке(ШаблонСообщения, ВыборкаДокументовДляПроверки.НомерСтроки, Отказ);
		КонецЕсли;

		Если ВыборкаДокументовДляПроверки.Организация <> Организация Тогда
			ШаблонСообщения = НСтр("ru = 'Документ-пачка в строке %1 оформлен на другую организацию.'");
			СообщитьОбОшибкеВСтроке(ШаблонСообщения, ВыборкаДокументовДляПроверки.НомерСтроки, Отказ);
		КонецЕсли;

		Если ВыборкаДокументовДляПроверки.НомерПачки = НомерПачки Тогда
			ШаблонСообщения = НСтр(
				"ru = 'Документ-пачка в строке %1 имеет такой же номер как и документ описи,
				|рекомендуется изменить номер пачки в документе описи.'");
			СообщитьОбОшибкеВСтроке(ШаблонСообщения, ВыборкаДокументовДляПроверки.НомерСтроки, Отказ);
		КонецЕсли;

		Если ВыборкаДокументовДляПроверки.НомерСтрокиДубль <> Null Тогда
			ШаблонСообщения = НСтр("ru = 'Документ-пачка указан дважды.'");
			СообщитьОбОшибкеВСтроке(ШаблонСообщения, ВыборкаДокументовДляПроверки.НомерСтроки, Отказ);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьЗапросПоПачкамДокументовДляПроверки()
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПачкиДокументов", ПачкиДокументов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПачкиДокументов.ПачкаДокументов,
	|	ПачкиДокументов.НомерСтроки
	|ПОМЕСТИТЬ ВТПачкиДокументов
	|ИЗ
	|	&ПачкиДокументов КАК ПачкиДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ПачкиДокументов.ПачкаДокументов.ОтчетныйПериод, ДАТАВРЕМЯ(1,1,1)) КАК ОтчетныйПериод,
	|	ЕСТЬNULL(ПачкиДокументов.ПачкаДокументов.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	|	ЕСТЬNULL(ПачкиДокументов.ПачкаДокументов.НомерПачки, 0) КАК НомерПачки,
	|	ПачкиДокументов.ПачкаДокументов КАК Документ,
	|	ЕСТЬNULL(ПачкиДокументов.ПачкаДокументов.ФайлСформирован, ЛОЖЬ) КАК ФайлСформирован,
	|	ПачкиДокументов.ПачкаДокументов,
	|	ПачкиДокументов.НомерСтроки,
	|	ПачкиДокументовДубль.НомерСтроки КАК НомерСтрокиДубль
	|ИЗ
	|	ВТПачкиДокументов КАК ПачкиДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПачкиДокументов КАК ПачкиДокументовДубль
	|		ПО ПачкиДокументов.ПачкаДокументов = ПачкиДокументовДубль.ПачкаДокументов
	|			И ПачкиДокументов.НомерСтроки > ПачкиДокументовДубль.НомерСтроки";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Процедура СообщитьОбОшибкеВСтроке(ШаблонСообщения, НомерСтроки, Отказ)
	ТекстСообщения = СтрШаблон(ШаблонСообщения, XMLСтрока(НомерСтроки));
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, СтрШаблон(
		"Объект.ПачкиДокументов[%1].ПачкаДокументов", XMLСтрока(НомерСтроки - 1)), , Отказ);
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли