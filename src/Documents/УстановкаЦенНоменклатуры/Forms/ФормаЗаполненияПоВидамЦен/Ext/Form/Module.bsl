
#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура УстановитьЗначениеПараметраВНастройкахСКД(НастройкиКомпоновкиДанных, ИмяПараметра, ЗначениеПараметра)
	
	Если НЕ ЗначениеЗаполнено(ЗначениеПараметра) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрКомпоновкиДанных = Новый ПараметрКомпоновкиДанных(ИмяПараметра);
	
	ЗначениеПараметраКД = НастройкиКомпоновкиДанных.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрКомпоновкиДанных);
	Если ЗначениеПараметраКД = Неопределено Тогда
		
		ЗначениеПараметраКД = НастройкиКомпоновкиДанных.ПараметрыДанных.Элементы.Добавить();
		ЗначениеПараметраКД.Параметр = ПараметрКомпоновкиДанных;
		
	КонецЕсли;
	
	ЗначениеПараметраКД.Значение = ЗначениеПараметра;
	ЗначениеПараметраКД.Использование = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьНоменклатуруСЦеной(АдресВременногоХранилища)
	
	ДеревоНоменклатуры = Новый ДеревоЗначений;
	
	// 1. Получим СКД
	ИмяСхемыКД = "ПоВидамЦенСЦеной";
	СхемаКомпоновкиДанных = Документы.УстановкаЦенНоменклатуры.ПолучитьМакет(ИмяСхемыКД);
	
	// 2. создаем настройки для схемы 
	НастройкиКомпоновкиДанных = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	
	// 2.1 установим значения параметров
	УстановитьЗначениеПараметраВНастройкахСКД(НастройкиКомпоновкиДанных, "МассивВидовЦен",				ВидыЦен.ВыгрузитьЗначения());
	УстановитьЗначениеПараметраВНастройкахСКД(НастройкиКомпоновкиДанных, "ИгнорироватьОтборПоПериоду",	НЕ ЗначениеЗаполнено(ОтборПоПериоду));
	УстановитьЗначениеПараметраВНастройкахСКД(НастройкиКомпоновкиДанных, "ОтборПоПериоду",				ОтборПоПериоду);
	УстановитьЗначениеПараметраВНастройкахСКД(НастройкиКомпоновкиДанных, "ИспользоватьХарактеристики",	ИспользоватьХарактеристики <> 0);
	
	// 3. готовим макет 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// 4. исполняем макет 
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет);
	ПроцессорКомпоновки.Сбросить();
	
	// 5. выводим результат 
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ДеревоНоменклатуры);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	СтруктураТаблицДанных = Документы.УстановкаЦенНоменклатуры.РазобратьДеревоНоменклатуры(ДеревоНоменклатуры, ПоказыватьНедействительныеХарактеристики);
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(СтруктураТаблицДанных);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьНоменклатуруБезЦеной(АдресВременногоХранилища)
	
	ДеревоНоменклатуры = Новый ДеревоЗначений;
	
	// 1. Получим СКД
	ИмяСхемыКД = "ПоВидамЦенБезЦен";
	СхемаКомпоновкиДанных = Документы.УстановкаЦенНоменклатуры.ПолучитьМакет(ИмяСхемыКД);
	
	// 2. создаем настройки для схемы 
	НастройкиКомпоновкиДанных = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	
	// 2.1 установим значения параметров
	УстановитьЗначениеПараметраВНастройкахСКД(НастройкиКомпоновкиДанных, "МассивВидовЦен", ВидыЦен.ВыгрузитьЗначения());
	
	Если ИспользоватьХарактеристики = 0 Тогда
		
		НастройкиКомпоновкиДанных.Структура[0].Структура[0].Использование = Ложь;
		
	КонецЕсли;
	
	// 3. готовим макет 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// 4. исполняем макет 
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет);
	ПроцессорКомпоновки.Сбросить();
	
	// 5. выводим результат 
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ДеревоНоменклатуры);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	СтруктураТаблицДанных = Документы.УстановкаЦенНоменклатуры.РазобратьДеревоНоменклатуры(ДеревоНоменклатуры, ПоказыватьНедействительныеХарактеристики);
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(СтруктураТаблицДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОтчетСПредварительнымРезультатом()
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ИмяСКД",					?(ВидЗаписейРегистраКОтбору = Истина, "ПоВидамЦенСЦеной", "ПоВидамЦенБезЦен"));
	ПараметрыОткрытия.Вставить("МассивВидовЦен", 			ВидыЦен.ВыгрузитьЗначения());
	ПараметрыОткрытия.Вставить("ВыбиратьНоменклатуру",		?(ВидЗаписейРегистраКОтбору = Истина, 1, 0));
	ПараметрыОткрытия.Вставить("ОтборПоПериоду", 			ОтборПоПериоду);
	ПараметрыОткрытия.Вставить("ИспользоватьХарактеристики",ИспользоватьХарактеристики);
	
	ОткрытьФорму("Документ.УстановкаЦенНоменклатуры.Форма.ФормаПредварительногоПросмотра", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьВХранилищеДанныеТабличнойЧасти(АдресВременногоХранилища)
	
	Если ВидЗаписейРегистраКОтбору Тогда
		
		ПолучитьНоменклатуруСЦеной(АдресВременногоХранилища);
		
	Иначе
		
		ПолучитьНоменклатуруБезЦеной(АдресВременногоХранилища);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиСообщениеОНеправильномВыборе()
	
	ТекстСообщения = НСтр("ru ='Необходимо заполнить виды цен'");
	ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ВидыЦен");
	
КонецПроцедуры

#КонецОбласти

#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("КэшЗначений", КэшЗначений);
	
	Если КэшЗначений.ВыбранныеВидыЦен <> Неопределено Тогда
		
		Для каждого ЭлементСоответствия Из КэшЗначений.ВыбранныеВидыЦен Цикл
			
			ВидыЦен.Добавить(ЭлементСоответствия.Ключ);
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Только номенклатуру(0); Номенклатуру и ее характеристики(1);
	ИспользоватьХарактеристики = ?(Параметры.ХарактеристикиВидны = Истина, 1, 0);
	Если Параметры.Свойство("ПоказыватьНедействительныеХарактеристики") Тогда
		ПоказыватьНедействительныеХарактеристики = Параметры.ПоказыватьНедействительныеХарактеристики;
	КонецЕсли;
	
	ВидЗаписейРегистраКОтбору = Истина;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		
		// Обход ошибки платформы 30163126
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияКартинка", "Ширина", 2);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияКартинка", "Высота", 0);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СобытияРеквизитовФормы

&НаКлиенте
Процедура ВидыЦенНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидыЦен",			ВидыЦен.ВыгрузитьЗначения());
	
	ОбработкаОповещения = Новый ОписаниеОповещения("ПослеВыбораВидовЦен", ЭтотОбъект);
	ОткрытьФорму("Документ.УстановкаЦенНоменклатуры.Форма.ВидыЦен", ПараметрыОткрытия, ЭтаФорма, , , , ОбработкаОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораВидовЦен(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура")
		И Результат.ВыборПроизведен Тогда
		
		ВидыЦен.Очистить();
		Для каждого ВыбранныйВидЦен Из Результат.ВыбранныеВидыЦен Цикл
			
			ВидыЦен.Добавить(ВыбранныйВидЦен.Ключ);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЗаписейРегистраКОтборуПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОтборПоПериоду", "Доступность", ВидЗаписейРегистраКОтбору);
	Если НЕ ВидЗаписейРегистраКОтбору Тогда
		
		ОтборПоПериоду = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ОК(Команда)
	
	Если ВидыЦен.Количество() > 0 Тогда
		
		АдресВременногоХранилища = "";
		ЗаписатьВХранилищеДанныеТабличнойЧасти(АдресВременногоХранилища);
		
		Закрыть(Новый Структура("ВыборПроизведен, АдресВременногоХранилища", Истина, АдресВременногоХранилища));
		
	Иначе
		
		ВывестиСообщениеОНеправильномВыборе();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотр(Команда)
	
	Если ВидыЦен.Количество() > 0 Тогда
		
		ПоказатьОтчетСПредварительнымРезультатом();
		
	Иначе
		
		ВывестиСообщениеОНеправильномВыборе();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
