#Область ОбработчикиСобытийФормы

Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	Контрагент = Параметры.Контрагент;
	ЭтаФорма.Заголовок = НСтр("ru ='Список договоров контрагента: '") + Строка(Контрагент);
	СортироватьПоДоговорам = Параметры.СортироватьПоДоговорам;

	ДоговорыКонтрагентов.Загрузить(Параметры.ДоговорыКонтрагентов.Выгрузить());

	ПроверитьЗаполнениеДоговоров = Ложь;

	РедактированиеДоступно = ПравоДоступа("Редактирование", Метаданные.Документы.СверкаВзаиморасчетов);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДоговорыКонтрагентов", "Доступность",
		РедактированиеДоступно);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СортироватьПоДоговорам", "Доступность",
		РедактированиеДоступно);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	Если ПроверитьЗаполнениеДоговоров Тогда

		Для Каждого Договор Из ДоговорыКонтрагентов Цикл

			Если Не ЗначениеЗаполнено(Договор.Договор) Тогда

				ТекстСообщения = НСтр("ru = 'В таблице присутствуют строки с незаполненным договором контрагента'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , , , Отказ);
				Прервать;

			КонецЕсли;

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)

	ПроверитьЗаполнениеДоговоров = Истина;
	Модифицированность = Ложь;
	Закрыть(Новый Структура("ДоговорыКонтрагентов, СортироватьПоДоговорам", ДоговорыКонтрагентов,
		СортироватьПоДоговорам));

КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)

	ПроверитьЗаполнениеДоговоров = Ложь;
	Модифицированность = Ложь;
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)

	Для Каждого СтрокаСписка Из ДоговорыКонтрагентов Цикл

		СтрокаСписка.Отметка = Истина;

	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)

	Для Каждого СтрокаСписка Из ДоговорыКонтрагентов Цикл

		СтрокаСписка.Отметка = Ложь;

	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ДоговорыКонтрагентовПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти
