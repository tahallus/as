#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Контрагент)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Проведение

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаУправленческий(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ЗачетПредоплаты", НСтр("ru = 'Зачет предоплаты'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	Запрос.УстановитьПараметр("ПоложительнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеДоходы);
	Запрос.УстановитьПараметр("ОтрицательнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеРасходы);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	1 КАК НомерСтроки,
	|	ТаблицаДокумента.Период КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком КАК СчетДт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаРасчетовСПоставщикомВалютный
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаРасчетовСПоставщикомВалютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалДт,
	|	ТаблицаДокумента.СчетУчетаАвансовПоставщику КАК СчетКт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаАвансовПоставщикуВалютный
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаКт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаАвансовПоставщикуВалютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалКт,
	|	ТаблицаДокумента.Сумма КАК Сумма,
	|	&ЗачетПредоплаты КАК Содержание
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаДокумента.Период КАК Период,
	|		ТаблицаДокумента.Организация КАК Организация,
	|		ТаблицаДокумента.СчетУчетаАвансовПоставщику КАК СчетУчетаАвансовПоставщику,
	|		ТаблицаДокумента.СчетУчетаАвансовПоставщикуВалютный КАК СчетУчетаАвансовПоставщикуВалютный,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПоставщикомВалютный КАК СчетУчетаРасчетовСПоставщикомВалютный,
	|		ТаблицаДокумента.ВалютаРасчетов КАК ВалютаРасчетов,
	|		СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВал,
	|		СУММА(ТаблицаДокумента.Сумма) КАК Сумма
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Период КАК Период,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.СчетУчетаАвансовПоставщику КАК СчетУчетаАвансовПоставщику,
	|			ТаблицаДокумента.СчетУчетаАвансовПоставщику.Валютный КАК СчетУчетаАвансовПоставщикуВалютный,
	|			ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|			ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком.Валютный КАК СчетУчетаРасчетовСПоставщикомВалютный,
	|			ТаблицаДокумента.ВалютаРасчетов КАК ВалютаРасчетов,
	|			ТаблицаДокумента.СуммаВал КАК СуммаВал,
	|			ТаблицаДокумента.Сумма КАК Сумма
	|		ИЗ
	|			ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.Контрагент.СчетУчетаАвансовПоставщику,
	|			ТаблицаДокумента.Контрагент.СчетУчетаАвансовПоставщику.Валютный,
	|			ТаблицаДокумента.Контрагент.СчетУчетаРасчетовСПоставщиком,
	|			ТаблицаДокумента.Контрагент.СчетУчетаРасчетовСПоставщиком.Валютный,
	|			ТаблицаДокумента.Валюта,
	|			0,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаДокумента
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаДокумента.Период,
	|		ТаблицаДокумента.Организация,
	|		ТаблицаДокумента.СчетУчетаАвансовПоставщику,
	|		ТаблицаДокумента.СчетУчетаАвансовПоставщикуВалютный,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПоставщикомВалютный,
	|		ТаблицаДокумента.ВалютаРасчетов
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаДокумента.Сумма) >= 0.005
	|			ИЛИ СУММА(ТаблицаДокумента.Сумма) <= -0.005
	|			ИЛИ СУММА(ТаблицаДокумента.СуммаВал) >= 0.005
	|			ИЛИ СУММА(ТаблицаДокумента.СуммаВал) <= -0.005)) КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	1,
	|	ТаблицаУправленческий.Дата,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА &ОтрицательнаяКурсоваяРазницаСчетУчета
	|		ИНАЧЕ ТаблицаУправленческий.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы < 0
	|				И ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаУправленческий.СчетУчета
	|		ИНАЧЕ &ПоложительнаяКурсоваяРазницаСчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|				И ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаУправленческий.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаУправленческий.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата КАК Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация КАК Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчета КАК СчетУчета,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчетаВалютный КАК СчетУчетаВалютный,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта КАК Валюта,
	|		СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) КАК СуммаКурсовойРазницы
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Дата КАК Дата,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.СчетУчета КАК СчетУчета,
	|			ТаблицаДокумента.СчетУчета.Валютный КАК СчетУчетаВалютный,
	|			ТаблицаДокумента.Валюта КАК Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы КАК СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.СчетУчета,
	|			ТаблицаДокумента.СчетУчета.Валютный,
	|			ТаблицаДокумента.Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаКурсовыхРазницРасчетыСПоставщиками
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчета,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчетаВалютный,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) >= 0.005
	|			ИЛИ СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) <= -0.005)) КАК ТаблицаУправленческий";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяПроводка = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПроводка, Выборка);
	КонецЦикла;
	
КонецПроцедуры // СформироватьТаблицаУправленческий()

#КонецОбласти

#Область Услуги

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыУслуга(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства, СуммаУслуги)
	
	Для н = 0 По СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыУслуга.Количество() - 1 Цикл
		
		СтрокаТаблицаЗапасы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыУслуга[н];
		
		// Сформируем проводки.
		Если СтрокаТаблицаЗапасы.Сумма > 0 Тогда
			СтрокаТаблицаУправленческий = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицаУправленческий, СтрокаТаблицаЗапасы);
		КонецЕсли;
		
		// Если это производство, тогда необходимо отнести полученные затраты НЗП на себестоимость продукции.
		Если СтрокаТаблицаЗапасы.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Подразделение Тогда
				
			// Приход услуг в НЗП.
			СтрокаТаблицыПриход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаТаблицаЗапасы);
				
			СтрокаТаблицыПриход.ВидДвижения = ВидДвиженияНакопления.Приход;
			
			СтрокаТаблицыПриход.КоррСтруктурнаяЕдиница = Неопределено;
			СтрокаТаблицыПриход.КоррСчетУчета = Неопределено;
			СтрокаТаблицыПриход.КоррНоменклатура = Неопределено;
			СтрокаТаблицыПриход.КоррХарактеристика = Неопределено;
			СтрокаТаблицыПриход.КоррПартия = Неопределено;
			СтрокаТаблицыПриход.КоррСпецификация = Неопределено;
			СтрокаТаблицыПриход.КоррЗаказПокупателя  = Неопределено;
			СтрокаТаблицыПриход.ФиксированнаяСтоимость = Истина;
			
			// Списание затрат.
			// Сформируем проводки.
			Если СтрокаТаблицаЗапасы.Сумма > 0 Тогда
				СтрокаТаблицаУправленческий = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицаУправленческий, СтрокаТаблицаЗапасы);
				СтрокаТаблицаУправленческий.СчетКт = СтрокаТаблицаУправленческий.СчетДт;
				СтрокаТаблицаУправленческий.ВалютаКт = Неопределено;
				СтрокаТаблицаУправленческий.СуммаВалКт = 0;
				СтрокаТаблицаУправленческий.СчетДт = СтрокаТаблицаЗапасы.КоррСчетУчета;
			КонецЕсли;
		
			СтрокаТаблицыРасход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
				
			СтрокаТаблицыРасход.ВидДвижения = ВидДвиженияНакопления.Расход;
			
			СтрокаТаблицыРасход.КоррОрганизация = СтрокаТаблицаЗапасы.КоррОрганизация;
			СтрокаТаблицыРасход.КоррСтруктурнаяЕдиница = СтрокаТаблицаЗапасы.КоррСтруктурнаяЕдиница;
			СтрокаТаблицыРасход.КоррСчетУчета = СтрокаТаблицаЗапасы.КоррСчетУчета;
			СтрокаТаблицыРасход.КоррНоменклатура = СтрокаТаблицаЗапасы.КоррНоменклатура;
			СтрокаТаблицыРасход.КоррХарактеристика = СтрокаТаблицаЗапасы.КоррХарактеристика;
			СтрокаТаблицыРасход.КоррПартия = СтрокаТаблицаЗапасы.КоррПартия;
			СтрокаТаблицыРасход.КоррСпецификация = СтрокаТаблицаЗапасы.КоррСпецификация;
			
 			СтрокаТаблицыРасход.КоррЗаказПокупателя = СтрокаТаблицаЗапасы.КоррЗаказПокупателя;
				
			СтрокаТаблицыРасход.СчетУчета = СтрокаТаблицаЗапасы.СчетУчета;
		    СтрокаТаблицыРасход.ФиксированнаяСтоимость = Ложь;
			СтрокаТаблицыРасход.ЗатратыНаВыпуск = Истина;
				
			// Отнесение затрат на себестоимость.
			СтрокаТаблицыПриход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаТаблицаЗапасы);
				
			СтрокаТаблицыПриход.ВидДвижения = ВидДвиженияНакопления.Приход;
						
			СтрокаТаблицыПриход.Организация = СтрокаТаблицаЗапасы.КоррОрганизация;
			СтрокаТаблицыПриход.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.КоррСтруктурнаяЕдиница;
			СтрокаТаблицыПриход.СчетУчета = СтрокаТаблицаЗапасы.КоррСчетУчета;
			СтрокаТаблицыПриход.Номенклатура = СтрокаТаблицаЗапасы.КоррНоменклатура;
			СтрокаТаблицыПриход.Характеристика = СтрокаТаблицаЗапасы.КоррХарактеристика;
			СтрокаТаблицыПриход.Партия = СтрокаТаблицаЗапасы.КоррПартия;
			СтрокаТаблицыПриход.Спецификация = СтрокаТаблицаЗапасы.КоррСпецификация;
			
			СтрокаТаблицыПриход.ЗаказПокупателя = СтрокаТаблицаЗапасы.КоррЗаказПокупателя;
			
			СтрокаТаблицыПриход.КоррОрганизация = СтрокаТаблицаЗапасы.Организация;
			СтрокаТаблицыПриход.КоррСтруктурнаяЕдиница = СтрокаТаблицаЗапасы.СтруктурнаяЕдиница;
			СтрокаТаблицыПриход.КоррСчетУчета = СтрокаТаблицаЗапасы.СчетУчета;
			СтрокаТаблицыПриход.КоррНоменклатура = СтрокаТаблицаЗапасы.Номенклатура;
			СтрокаТаблицыПриход.КоррХарактеристика = СтрокаТаблицаЗапасы.Характеристика;
			СтрокаТаблицыПриход.КоррПартия = СтрокаТаблицаЗапасы.Партия;
			СтрокаТаблицыПриход.КоррСпецификация = СтрокаТаблицаЗапасы.Спецификация;
			
			СтрокаТаблицыПриход.СчетУчета = СтрокаТаблицаЗапасы.КоррСчетУчета;
		    СтрокаТаблицыПриход.ФиксированнаяСтоимость = Ложь;
			
			СуммаУслуги = СуммаУслуги + СтрокаТаблицаЗапасы.Сумма;
				
		Иначе
			
			// Отнесение затрат на себестоимость.
			СтрокаТаблицыПриход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаТаблицаЗапасы);
				
			СтрокаТаблицыПриход.ВидДвижения = ВидДвиженияНакопления.Приход;
			
			
			СтрокаТаблицыПриход.Организация = СтрокаТаблицаЗапасы.КоррОрганизация;
			СтрокаТаблицыПриход.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.КоррСтруктурнаяЕдиница;
			СтрокаТаблицыПриход.СчетУчета = СтрокаТаблицаЗапасы.КоррСчетУчета;
			СтрокаТаблицыПриход.Номенклатура = СтрокаТаблицаЗапасы.КоррНоменклатура;
			СтрокаТаблицыПриход.Характеристика = СтрокаТаблицаЗапасы.КоррХарактеристика;
			СтрокаТаблицыПриход.Партия = СтрокаТаблицаЗапасы.КоррПартия;
			СтрокаТаблицыПриход.Спецификация = СтрокаТаблицаЗапасы.КоррСпецификация;
			СтрокаТаблицыПриход.ЗаказПокупателя = СтрокаТаблицаЗапасы.КоррЗаказПокупателя;
			
			СтрокаТаблицыПриход.КоррСтруктурнаяЕдиница = Неопределено;
			СтрокаТаблицыПриход.КоррСчетУчета = Неопределено;
			СтрокаТаблицыПриход.КоррНоменклатура = Неопределено;
			СтрокаТаблицыПриход.КоррХарактеристика = Неопределено;
			СтрокаТаблицыПриход.КоррПартия = Неопределено;
			СтрокаТаблицыПриход.КоррСпецификация = Неопределено;
		    СтрокаТаблицыПриход.КоррЗаказПокупателя  = Неопределено;
		    СтрокаТаблицыПриход.ФиксированнаяСтоимость = Истина;
			
			СтрокаТаблицыПриход.СчетУчета = СтрокаТаблицаЗапасы.КоррСчетУчета;
			
			СуммаУслуги = СуммаУслуги + СтрокаТаблицаЗапасы.Сумма;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // СформироватьТаблицаЗапасыЗапасы()

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеПоУслуге(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства, СуммаУслуги) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(ОтчетПереработчикаПродукция.Количество) КАК КоличествоПродукции
	|ПОМЕСТИТЬ ВТКоличествоПродукции
	|ИЗ
	|	Документ.ОтчетПереработчика.Продукция КАК ОтчетПереработчикаПродукция
	|ГДЕ
	|	ОтчетПереработчикаПродукция.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	0 КАК НомерСтроки,
	|	ОтчетПереработчика.Ссылка КАК Ссылка,
	|	ОтчетПереработчика.ВалютаДокумента КАК ВалютаДокумента,
	|	ОтчетПереработчика.Курс КАК Курс,
	|	ОтчетПереработчика.Кратность КАК Кратность,
	|	ОтчетПереработчика.Расход КАК Расход,
	|	ОтчетПереработчика.Расход.СчетУчетаЗатрат КАК СчетУчетаЗатрат,
	|	ОтчетПереработчика.Всего КАК Всего
	|ПОМЕСТИТЬ Услуги
	|ИЗ
	|	Документ.ОтчетПереработчика КАК ОтчетПереработчика
	|ГДЕ
	|	ОтчетПереработчика.Ссылка = &Ссылка
	|	И ОтчетПереработчика.Всего > 0
	|	И ОтчетПереработчика.ПоложениеРасходов <> ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетПереработчикаРасходы.НомерСтроки,
	|	ОтчетПереработчикаРасходы.Ссылка,
	|	ОтчетПереработчикаРасходы.Ссылка.ВалютаДокумента,
	|	ОтчетПереработчикаРасходы.Ссылка.Курс,
	|	ОтчетПереработчикаРасходы.Ссылка.Кратность,
	|	ОтчетПереработчикаРасходы.Расход,
	|	ОтчетПереработчикаРасходы.Расход.СчетУчетаЗатрат,
	|	ОтчетПереработчикаРасходы.Всего
	|ИЗ
	|	Документ.ОтчетПереработчика.Расходы КАК ОтчетПереработчикаРасходы
	|ГДЕ
	|	ОтчетПереработчикаРасходы.Ссылка = &Ссылка
	|	И ОтчетПереработчикаРасходы.Всего > 0
	|	И ОтчетПереработчикаРасходы.Ссылка.ПоложениеРасходов = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Услуги.НомерСтроки КАК НомерСтроки,
	|	Услуги.Ссылка КАК Ссылка,
	|	Услуги.Расход КАК Расход,
	|	Услуги.СчетУчетаЗатрат КАК СчетУчетаЗатрат,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА Услуги.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА Услуги.Всего * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ Услуги.Всего * Услуги.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * Услуги.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА Услуги.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА Услуги.Всего * КурсыРегВалюты.Курс * Услуги.Кратность / (Услуги.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ Услуги.Всего
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаВал
	|ПОМЕСТИТЬ УслугиВВалюте
	|ИЗ
	|	Услуги КАК Услуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаВалютаУчета.Значение
	|					ИЗ
	|						Константа.ВалютаУчета КАК КонстантаВалютаУчета)) КАК КурсыУпрВалюты
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаНациональнаяВалюта.Значение
	|					ИЗ
	|						Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта)) КАК КурсыРегВалюты
	|		ПО (ИСТИНА),
	|	Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УслугиВВалюте.НомерСтроки КАК НомерСтроки,
	|	СУММА(УслугиВВалюте.Сумма) КАК Сумма,
	|	СУММА(УслугиВВалюте.СуммаВал) КАК СуммаВал
	|ИЗ
	|	УслугиВВалюте КАК УслугиВВалюте
	|
	|СГРУППИРОВАТЬ ПО
	|	УслугиВВалюте.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УслугиВВалюте.НомерСтроки КАК НомерСтроки,
	|	ОтчетПереработчикаПродукция.Ссылка.Дата КАК Период,
	|	ОтчетПереработчикаПродукция.Ссылка.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	&Организация КАК Организация,
	|	&Организация КАК КоррОрганизация,
	|	ОтчетПереработчикаПродукция.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ОтчетПереработчикаПродукция.Ссылка.СтруктурнаяЕдиница КАК КоррСтруктурнаяЕдиница,
	|	УслугиВВалюте.СчетУчетаЗатрат КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ОтчетПереработчикаПродукция.Ссылка.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|			ТОГДА ОтчетПереработчикаПродукция.Номенклатура.СчетУчетаЗапасов
	|		ИНАЧЕ ОтчетПереработчикаПродукция.Номенклатура.СчетУчетаЗатрат
	|	КОНЕЦ КАК КоррСчетУчета,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ОтчетПереработчикаПродукция.Номенклатура КАК КоррНоменклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА ОтчетПереработчикаПродукция.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК КоррХарактеристика,
	|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка) КАК Партия,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПартии
	|			ТОГДА ОтчетПереработчикаПродукция.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК КоррПартия,
	|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка) КАК Спецификация,
	|	ОтчетПереработчикаПродукция.Спецификация КАК КоррСпецификация,
	|	ОтчетПереработчикаПродукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ОтчетПереработчикаПродукция.ЗаказПокупателя КАК КоррЗаказПокупателя,
	|	0 КАК Количество,
	|	ВЫРАЗИТЬ(УслугиВВалюте.Сумма * ОтчетПереработчикаПродукция.Количество / ВТКоличествоПродукции.КоличествоПродукции КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(УслугиВВалюте.СуммаВал * ОтчетПереработчикаПродукция.Количество / ВТКоличествоПродукции.КоличествоПродукции КАК ЧИСЛО(15, 2)) КАК СуммаВал,
	|	ВЫБОР
	|		КОГДА ОтчетПереработчикаПродукция.Ссылка.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА УслугиВВалюте.СчетУчетаЗатрат
	|		ИНАЧЕ ОтчетПереработчикаПродукция.Номенклатура.СчетУчетаЗапасов
	|	КОНЕЦ КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	0 КАК СуммаВалДт,
	|	ОтчетПереработчикаПродукция.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетКт,
	|	ВЫБОР
	|		КОГДА ОтчетПереработчикаПродукция.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком.Валютный
	|			ТОГДА ОтчетПереработчикаПродукция.Ссылка.Договор.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаКт,
	|	ВЫБОР
	|		КОГДА ОтчетПереработчикаПродукция.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком.Валютный
	|			ТОГДА ВЫРАЗИТЬ(УслугиВВалюте.СуммаВал * ОтчетПереработчикаПродукция.Количество / ВТКоличествоПродукции.КоличествоПродукции КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалКт,
	|	&ОтражениеРасходовПоПереработке КАК Содержание,
	|	&ОтражениеРасходовПоПереработке КАК СодержаниеПроводки
	|ИЗ
	|	Документ.ОтчетПереработчика.Продукция КАК ОтчетПереработчикаПродукция,
	|	ВТКоличествоПродукции КАК ВТКоличествоПродукции,
	|	УслугиВВалюте КАК УслугиВВалюте
	|ГДЕ
	|	ОтчетПереработчикаПродукция.Ссылка = &Ссылка
	|	И НЕ УслугиВВалюте.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	Запрос.УстановитьПараметр("МоментКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментКонтроля);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьХарактеристики);
	Запрос.УстановитьПараметр("ИспользоватьПартии", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьПартии);
	Запрос.УстановитьПараметр("ОтражениеРасходовПоПереработке", НСтр("ru = 'Отражение расходов по переработке'"));
	
	Результат = Запрос.ВыполнитьПакет();
	ТаблицаЗапасыУслуга = Результат.Получить(4).Выгрузить();
	ТаблицаПроверка = Результат.Получить(3).Выгрузить();
	
	// Корректировка округлений при распределении стоимости услуг
	СоответствиеСтрок = Новый Соответствие;
	Для каждого СтрокаТаблицы Из ТаблицаЗапасыУслуга Цикл
		СохраненнаяСтрока = СоответствиеСтрок.Получить(СтрокаТаблицы.НомерСтроки);
		Если СохраненнаяСтрока = Неопределено Тогда
			СоответствиеСтрок.Вставить(СтрокаТаблицы.НомерСтроки, СтрокаТаблицы);
		ИначеЕсли СохраненнаяСтрока.Сумма < СтрокаТаблицы.Сумма Тогда 
			СоответствиеСтрок.Вставить(СтрокаТаблицы.НомерСтроки, СтрокаТаблицы);
		КонецЕсли; 
		СтрокаПроверка = ТаблицаПроверка.Найти(СтрокаТаблицы.НомерСтроки, "НомерСтроки");
		Если СтрокаПроверка = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтрокаПроверка.Сумма = СтрокаПроверка.Сумма - СтрокаТаблицы.Сумма;
		СтрокаПроверка.СуммаВал = СтрокаПроверка.СуммаВал - СтрокаТаблицы.СуммаВал;
	КонецЦикла;
	Для каждого СтрокаПроверка Из ТаблицаПроверка Цикл
		Если СтрокаПроверка.Сумма = 0 И СтрокаПроверка.СуммаВал = 0 Тогда
			Продолжить;
		КонецЕсли; 
		СтрокаТаблицы = СоответствиеСтрок.Получить(СтрокаПроверка.НомерСтроки);
		Если СтрокаТаблицы = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтрокаТаблицы.Сумма = СтрокаТаблицы.Сумма + СтрокаПроверка.Сумма;
		СтрокаТаблицы.СуммаВал = СтрокаТаблицы.СуммаВал + СтрокаПроверка.СуммаВал;
	КонецЦикла; 
	
	// Определим таблицу для учета запасов.
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыУслуга", ТаблицаЗапасыУслуга);

	// Сформируем таблицу для учета запасов.
	СформироватьТаблицаЗапасыУслуга(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства, СуммаУслуги);
	
КонецПроцедуры // ИнициализироватьДанныеПоУслуге()

#КонецОбласти

#Область Отходы

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыОтходы(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства)
	
	Для н = 0 По СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыОтходы.Количество() - 1 Цикл
		
		СтрокаТаблицаЗапасы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыОтходы[н];
				
		СтрокаТаблицыПриход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаТаблицаЗапасы);
		
	КонецЦикла;
	
КонецПроцедуры // СформироватьТаблицаЗапасыОтходы()

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеПоОтходам(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтчетПереработчикаОтходы.НомерСтроки КАК НомерСтроки,
	|	ОтчетПереработчикаОтходы.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ОтчетПереработчикаОтходы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА &УчетПоЯчейкам
	|			ТОГДА ОтчетПереработчикаОтходы.Ссылка.Ячейка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|	КОНЕЦ КАК Ячейка,
	|	ВЫБОР
	|		КОГДА ОтчетПереработчикаОтходы.Ссылка.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	|			ТОГДА ОтчетПереработчикаОтходы.Номенклатура.СчетУчетаЗатрат
	|		ИНАЧЕ ОтчетПереработчикаОтходы.Номенклатура.СчетУчетаЗапасов
	|	КОНЕЦ КАК СчетУчета,
	|	ОтчетПереработчикаОтходы.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА ОтчетПереработчикаОтходы.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПартии
	|			ТОГДА ОтчетПереработчикаОтходы.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Партия,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ОтчетПереработчикаОтходы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА ОтчетПереработчикаОтходы.Количество
	|		ИНАЧЕ ОтчетПереработчикаОтходы.Количество * ОтчетПереработчикаОтходы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Количество,
	|	0 КАК Сумма,
	|	&ВозвратныеОтходы КАК СодержаниеПроводки
	|ИЗ
	|	Документ.ОтчетПереработчика.Отходы КАК ОтчетПереработчикаОтходы
	|ГДЕ
	|	ОтчетПереработчикаОтходы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетПереработчикаОтходы.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ОтчетПереработчикаОтходы.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ОтчетПереработчикаОтходы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА &УчетПоЯчейкам
	|			ТОГДА ОтчетПереработчикаОтходы.Ссылка.Ячейка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Ячейка,
	|	ОтчетПереработчикаОтходы.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА ОтчетПереработчикаОтходы.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПартии
	|			ТОГДА ОтчетПереработчикаОтходы.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Партия,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ОтчетПереработчикаОтходы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА ОтчетПереработчикаОтходы.Количество
	|		ИНАЧЕ ОтчетПереработчикаОтходы.Количество * ОтчетПереработчикаОтходы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Количество
	|ИЗ
	|	Документ.ОтчетПереработчика.Отходы КАК ОтчетПереработчикаОтходы
	|ГДЕ
	|	ОтчетПереработчикаОтходы.Ссылка = &Ссылка
	|	И НЕ ОтчетПереработчикаОтходы.Ссылка.СтруктурнаяЕдиница.ОрдерныйСклад";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	Запрос.УстановитьПараметр("МоментКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментКонтроля);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьХарактеристики);
	Запрос.УстановитьПараметр("ИспользоватьПартии", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьПартии);
	Запрос.УстановитьПараметр("УчетПоЯчейкам", СтруктураДополнительныеСвойства.УчетнаяПолитика.УчетПоЯчейкам);
	Запрос.УстановитьПараметр("ВозвратныеОтходы", НСтр("ru = 'Возвратные отходы'"));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// Определим таблицу для учета запасов.
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОтходы", МассивРезультатов[0].Выгрузить());

	// Сформируем таблицу для учета запасов.
	СформироватьТаблицаЗапасыОтходы(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства);

	// Дополним таблицу для запасов.
	ВыборкаРезультатов = МассивРезультатов[1].Выбрать();
	Пока ВыборкаРезультатов.Следующий() Цикл
		СтрокаТаблицыРасход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыНаСкладах.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, ВыборкаРезультатов);
	КонецЦикла;

КонецПроцедуры // ИнициализироватьДанныеПоОтходам()

#КонецОбласти

#Область Запасы

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыЗапасы(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства, СуммаСборки)
	
	ТекстСодержаниеПеремещение = НСтр("ru = 'Перемещение запасов'");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	// Установка исключительной блокировки контролируемых остатков запасов.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.ЗаказПокупателя";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Запасы");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;

	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	// Получение остатков запасов по стоимости.
	Запрос.Текст = 	
	"ВЫБРАТЬ
	|	ЗапасыОстатки.Организация КАК Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗапасыОстатки.СчетУчета КАК СчетУчета,
	|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыОстатки.Характеристика КАК Характеристика,
	|	ЗапасыОстатки.Партия КАК Партия,
	|	ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	|	СУММА(ЗапасыОстатки.СуммаОстаток) КАК СуммаОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыОстатки.Организация КАК Организация,
	|		ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗапасыОстатки.СчетУчета КАК СчетУчета,
	|		ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыОстатки.Характеристика КАК Характеристика,
	|		ЗапасыОстатки.Партия КАК Партия,
	|		ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|		СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	|		СУММА(ЗапасыОстатки.СуммаОстаток) КАК СуммаОстаток
	|	ИЗ
	|		РегистрНакопления.Запасы.Остатки(
	|				&МоментКонтроля,
	|				(Организация, СтруктурнаяЕдиница, СчетУчета, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
	|					(ВЫБРАТЬ
	|						ТаблицаЗапасы.Организация,
	|						ТаблицаЗапасы.СтруктурнаяЕдиница,
	|						ТаблицаЗапасы.СчетУчета,
	|						ТаблицаЗапасы.Номенклатура,
	|						ТаблицаЗапасы.Характеристика,
	|						ТаблицаЗапасы.Партия,
	|						ТаблицаЗапасы.ЗаказПокупателя
	|					ИЗ
	|						ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы)) КАК ЗапасыОстатки
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ЗапасыОстатки.Организация,
	|		ЗапасыОстатки.СтруктурнаяЕдиница,
	|		ЗапасыОстатки.СчетУчета,
	|		ЗапасыОстатки.Номенклатура,
	|		ЗапасыОстатки.Характеристика,
	|		ЗапасыОстатки.Партия,
	|		ЗапасыОстатки.ЗаказПокупателя
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаЗапасы.Организация,
	|		ДвиженияДокументаЗапасы.СтруктурнаяЕдиница,
	|		ДвиженияДокументаЗапасы.СчетУчета,
	|		ДвиженияДокументаЗапасы.Номенклатура,
	|		ДвиженияДокументаЗапасы.Характеристика,
	|		ДвиженияДокументаЗапасы.Партия,
	|		ДвиженияДокументаЗапасы.ЗаказПокупателя,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
	|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗапасы.Сумма, 0)
	|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗапасы.Сумма, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.Запасы КАК ДвиженияДокументаЗапасы
	|	ГДЕ
	|		ДвиженияДокументаЗапасы.Регистратор = &Ссылка
	|		И ДвиженияДокументаЗапасы.Период <= &ПериодКонтроля) КАК ЗапасыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыОстатки.Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница,
	|	ЗапасыОстатки.СчетУчета,
	|	ЗапасыОстатки.Номенклатура,
	|	ЗапасыОстатки.Характеристика,
	|	ЗапасыОстатки.Партия,
	|	ЗапасыОстатки.ЗаказПокупателя";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	Запрос.УстановитьПараметр("МоментКонтроля", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаЗапасыОстатки = РезультатЗапроса.Выгрузить();
	ТаблицаЗапасыОстатки.Индексы.Добавить("Организация, СтруктурнаяЕдиница, СчетУчета, Номенклатура, Характеристика, Партия, ЗаказПокупателя");
	
	Для н = 0 По СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыЗапасы.Количество() - 1 Цикл
		
		СтрокаТаблицаЗапасы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыЗапасы[н];
		
		СтруктураДляПоиска = Новый Структура;
		СтруктураДляПоиска.Вставить("Организация", СтрокаТаблицаЗапасы.Организация);
		СтруктураДляПоиска.Вставить("СтруктурнаяЕдиница", СтрокаТаблицаЗапасы.СтруктурнаяЕдиница);
		СтруктураДляПоиска.Вставить("СчетУчета", СтрокаТаблицаЗапасы.СчетУчета);
		СтруктураДляПоиска.Вставить("Номенклатура", СтрокаТаблицаЗапасы.Номенклатура);
		СтруктураДляПоиска.Вставить("Характеристика", СтрокаТаблицаЗапасы.Характеристика);
		СтруктураДляПоиска.Вставить("Партия", СтрокаТаблицаЗапасы.Партия);
		СтруктураДляПоиска.Вставить("ЗаказПокупателя", СтрокаТаблицаЗапасы.ЗаказПокупателя);
		
		КоличествоТребуетсяСвободныйОстаток = СтрокаТаблицаЗапасы.Количество;
						
		Если КоличествоТребуетсяСвободныйОстаток > 0 Тогда
								
			МассивСтрокОстатков = ТаблицаЗапасыОстатки.НайтиСтроки(СтруктураДляПоиска);
			
			КоличествоОстаток = 0;
			СуммаОстаток = 0;
			
			Если МассивСтрокОстатков.Количество() > 0 Тогда
				КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток;
				СуммаОстаток = МассивСтрокОстатков[0].СуммаОстаток;
			КонецЕсли;
			
			Если КоличествоОстаток > 0 И КоличествоОстаток > КоличествоТребуетсяСвободныйОстаток Тогда

				СуммаКСписанию = Окр(СуммаОстаток * КоличествоТребуетсяСвободныйОстаток / КоличествоОстаток , 2, 1);

				МассивСтрокОстатков[0].КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток - КоличествоТребуетсяСвободныйОстаток;
				МассивСтрокОстатков[0].СуммаОстаток = МассивСтрокОстатков[0].СуммаОстаток - СуммаКСписанию;

			ИначеЕсли КоличествоОстаток = КоличествоТребуетсяСвободныйОстаток Тогда

				СуммаКСписанию = СуммаОстаток;

				МассивСтрокОстатков[0].КоличествоОстаток = 0;
				МассивСтрокОстатков[0].СуммаОстаток = 0;

			Иначе
				СуммаКСписанию = 0;	
			КонецЕсли;
			
			СуммаСборки = СуммаСборки + СуммаКСписанию;
			
			// При получателе-подразделении выполняем движения материалов через промежуточное подразделение и счет учета затрат
			// для добавления возможности распределения дополнительных расходов по прямым затратам 
			ВыпускНаПодразделение = (СтрокаТаблицаЗапасы.ТипСтруктурнойЕдиницыПродукции = Перечисления.ТипыСтруктурныхЕдиниц.Подразделение);
			Если ВыпускНаПодразделение Тогда
				
				СтрокаТаблицыРасход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
				СтрокаТаблицыРасход.КоррСчетУчета = СтрокаТаблицаЗапасы.СчетУчетаЗатратЗапасов;
				СтрокаТаблицыРасход.СчетДт = СтрокаТаблицаЗапасы.СчетУчетаЗатратЗапасов;
				СтрокаТаблицыРасход.Сумма = СуммаКСписанию;
				СтрокаТаблицыРасход.Количество = КоличествоТребуетсяСвободныйОстаток;
				СтрокаТаблицыРасход.КоррНоменклатура = СтрокаТаблицыРасход.Номенклатура;
				СтрокаТаблицыРасход.КоррХарактеристика = СтрокаТаблицыРасход.Характеристика;
				СтрокаТаблицыРасход.КоррПартия = СтрокаТаблицыРасход.Партия;
				СтрокаТаблицыРасход.КоррЗаказПокупателя = СтрокаТаблицыРасход.ЗаказПокупателя;
				СтрокаТаблицыРасход.Содержание = ТекстСодержаниеПеремещение;
				СтрокаТаблицыРасход.СодержаниеПроводки = ТекстСодержаниеПеремещение;
				
				СтрокаТаблицыПриход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
				ПроизводствоСервер.ЗаполнитьДвижениеЗапасыЗеркально(СтрокаТаблицыПриход, СтрокаТаблицыРасход);
				
				// Сформируем проводки.
				Если Окр(СуммаКСписанию, 2, 1) <> 0 Тогда
					СтрокаТаблицаУправленческий = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицаУправленческий, СтрокаТаблицыПриход);
				КонецЕсли;
				
			КонецЕсли; 
			
			// Расход.
			СтрокаТаблицыРасход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
			
			СтрокаТаблицыРасход.Сумма = СуммаКСписанию;
			СтрокаТаблицыРасход.Количество = КоличествоТребуетсяСвободныйОстаток;
			СтрокаТаблицыРасход.ЗатратыНаВыпуск = Истина;
			
			Если ВыпускНаПодразделение Тогда
				
				СтрокаТаблицыРасход.СчетУчета = СтрокаТаблицаЗапасы.СчетУчетаЗатратЗапасов;
				СтрокаТаблицыРасход.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.КоррСтруктурнаяЕдиница;
				СтрокаТаблицыРасход.КоррСчетУчета = СтрокаТаблицаЗапасы.КоррСчетУчета;
				
			КонецЕсли; 
			
			// Приход.
			Если Окр(СуммаКСписанию, 2, 1) <> 0 Тогда
				
				СтрокаТаблицыПриход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаТаблицаЗапасы);
					
				СтрокаТаблицыПриход.ВидДвижения = ВидДвиженияНакопления.Приход;
								
				СтрокаТаблицыПриход.Организация = СтрокаТаблицаЗапасы.КоррОрганизация;
				СтрокаТаблицыПриход.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.КоррСтруктурнаяЕдиница;
				СтрокаТаблицыПриход.СчетУчета = СтрокаТаблицаЗапасы.КоррСчетУчета;
				СтрокаТаблицыПриход.Номенклатура = СтрокаТаблицаЗапасы.КоррНоменклатура;
				СтрокаТаблицыПриход.Характеристика = СтрокаТаблицаЗапасы.КоррХарактеристика;
				СтрокаТаблицыПриход.Партия = СтрокаТаблицаЗапасы.КоррПартия;
				СтрокаТаблицыПриход.Спецификация = СтрокаТаблицаЗапасы.КоррСпецификация;
				
				СтрокаТаблицыПриход.ЗаказПокупателя = СтрокаТаблицаЗапасы.КоррЗаказПокупателя;
				
				СтрокаТаблицыПриход.КоррОрганизация = СтрокаТаблицаЗапасы.Организация;
				
				Если ВыпускНаПодразделение Тогда
					СтрокаТаблицыПриход.КоррСтруктурнаяЕдиница = СтрокаТаблицаЗапасы.КоррСтруктурнаяЕдиница;
					СтрокаТаблицыПриход.КоррСчетУчета = СтрокаТаблицаЗапасы.СчетУчетаЗатратЗапасов;
					СтрокаТаблицыПриход.СчетКт = СтрокаТаблицаЗапасы.СчетУчетаЗатратЗапасов;
				Иначе
					СтрокаТаблицыПриход.КоррСтруктурнаяЕдиница = СтрокаТаблицаЗапасы.СтруктурнаяЕдиница;
					СтрокаТаблицыПриход.КоррСчетУчета = СтрокаТаблицаЗапасы.СчетУчета;
				КонецЕсли;
				
				СтрокаТаблицыПриход.КоррНоменклатура = СтрокаТаблицаЗапасы.Номенклатура;
				СтрокаТаблицыПриход.КоррХарактеристика = СтрокаТаблицаЗапасы.Характеристика;
				СтрокаТаблицыПриход.КоррПартия = СтрокаТаблицаЗапасы.Партия;
				СтрокаТаблицыПриход.КоррСпецификация = СтрокаТаблицаЗапасы.Спецификация;
 						
				СтрокаТаблицыПриход.Сумма = СуммаКСписанию;
				СтрокаТаблицыПриход.Количество = 0;
					
				СтрокаТаблицыПриход.СчетУчета = СтрокаТаблицаЗапасы.КоррСчетУчета;
				
				// Сформируем проводки.
				Если Окр(СуммаКСписанию, 2, 1) <> 0 Тогда
					СтрокаТаблицаУправленческий = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицаУправленческий, СтрокаТаблицыПриход);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // СформироватьТаблицаЗапасов()

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеПоЗапасам(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства, СуммаСборки) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтчетПереработчикаЗапасы.НомерСтроки КАК НомерСтроки,
	|	ОтчетПереработчикаЗапасы.Период КАК Период,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	&Организация КАК КоррОрганизация,
	|	ОтчетПереработчикаЗапасы.Контрагент КАК СтруктурнаяЕдиница,
	|	ОтчетПереработчикаЗапасы.СтруктурнаяЕдиница КАК КоррСтруктурнаяЕдиница,
	|	ОтчетПереработчикаЗапасы.ОрдерныйСклад КАК ОрдерныйСклад,
	|	ОтчетПереработчикаЗапасы.Ячейка КАК Ячейка,
	|	ОтчетПереработчикаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаЗапасов,
	|	ОтчетПереработчикаЗапасы.СтруктурнаяЕдиницаЗапасовНаСклад КАК СтруктурнаяЕдиницаЗапасовНаСклад,
	|	ОтчетПереработчикаЗапасы.ОрдерныйСкладЗапасов КАК ОрдерныйСкладЗапасов,
	|	ВЫРАЗИТЬ(ОтчетПереработчикаЗапасы.СтруктурнаяЕдиница КАК Справочник.СтруктурныеЕдиницы) КАК СтруктурнаяЕдиницаПродукции,
	|	ОтчетПереработчикаЗапасы.Ячейка КАК ЯчейкаЗапасов,
	|	ОтчетПереработчикаЗапасы.СчетУчета КАК СчетУчета,
	|	ОтчетПереработчикаЗапасы.СчетУчетаЗапасов КАК СчетУчетаЗапасов,
	|	ОтчетПереработчикаЗапасы.КоррСчетУчета КАК КоррСчетУчета,
	|	ОтчетПереработчикаЗапасы.СчетУчетаПродукции КАК СчетУчетаПродукции,
	|	ВЫРАЗИТЬ(ОтчетПереработчикаЗапасы.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ОтчетПереработчикаЗапасы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ОтчетПереработчикаЗапасы.НомерГТД КАК НомерГТД,
	|	ОтчетПереработчикаЗапасы.КоррНоменклатура КАК КоррНоменклатура,
	|	ОтчетПереработчикаЗапасы.Характеристика КАК Характеристика,
	|	ОтчетПереработчикаЗапасы.КоррХарактеристика КАК КоррХарактеристика,
	|	ОтчетПереработчикаЗапасы.Партия КАК Партия,
	|	ОтчетПереработчикаЗапасы.ПартияСтатус КАК ПартияСтатус,
	|	ОтчетПереработчикаЗапасы.КоррПартия КАК КоррПартия,
	|	ОтчетПереработчикаЗапасы.Спецификация КАК Спецификация,
	|	ОтчетПереработчикаЗапасы.КоррСпецификация КАК КоррСпецификация,
	|	ОтчетПереработчикаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ОтчетПереработчикаЗапасы.ЗаказПокупателя КАК КоррЗаказПокупателя,
	|	ОтчетПереработчикаЗапасы.ДокументОснование КАК ДокументОснование,
	|	ОтчетПереработчикаЗапасы.Контрагент КАК Контрагент,
	|	ОтчетПереработчикаЗапасы.Договор КАК Договор,
	|	ОтчетПереработчикаЗапасы.Количество КАК Количество,
	|	0 КАК Сумма,
	|	ОтчетПереработчикаЗапасы.Всего КАК Всего,
	|	ОтчетПереработчикаЗапасы.Курс КАК Курс,
	|	ОтчетПереработчикаЗапасы.Кратность КАК Кратность,
	|	ОтчетПереработчикаЗапасы.ВалютаДокумента КАК ВалютаДокумента,
	|	ОтчетПереработчикаЗапасы.СчетДт КАК СчетДт,
	|	ОтчетПереработчикаЗапасы.СчетДтПродукции КАК СчетДтПродукции,
	|	ОтчетПереработчикаЗапасы.СчетКт КАК СчетКт,
	|	ОтчетПереработчикаЗапасы.СчетКтПродукции КАК СчетКтПродукции,
	|	ВЫРАЗИТЬ(&РаспределениеЗапасов КАК СТРОКА(100)) КАК СодержаниеПроводки,
	|	ВЫРАЗИТЬ(&РаспределениеЗапасов КАК СТРОКА(100)) КАК Содержание
	|ПОМЕСТИТЬ 	ВТТаблицаРаспределенияМатериаловСборка
	|ИЗ
	|	&ТаблицаРаспределенияМатериаловСборка КАК ОтчетПереработчикаЗапасы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетПереработчикаЗапасы.НомерСтроки КАК НомерСтроки,
	|	ОтчетПереработчикаЗапасы.Период КАК Период,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	&Организация КАК КоррОрганизация,
	|	ОтчетПереработчикаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ОтчетПереработчикаЗапасы.КоррСтруктурнаяЕдиница КАК КоррСтруктурнаяЕдиница,
	|	ОтчетПереработчикаЗапасы.ОрдерныйСклад КАК ОрдерныйСклад,
	|	ОтчетПереработчикаЗапасы.Ячейка КАК Ячейка,
	|	ОтчетПереработчикаЗапасы.СтруктурнаяЕдиницаЗапасов КАК СтруктурнаяЕдиницаЗапасов,
	|	ОтчетПереработчикаЗапасы.СтруктурнаяЕдиницаЗапасовНаСклад КАК СтруктурнаяЕдиницаЗапасовНаСклад,
	|	ОтчетПереработчикаЗапасы.ОрдерныйСкладЗапасов КАК ОрдерныйСкладЗапасов,
	|	ОтчетПереработчикаЗапасы.СтруктурнаяЕдиницаПродукции КАК СтруктурнаяЕдиницаПродукции,
	|	ОтчетПереработчикаЗапасы.СтруктурнаяЕдиницаПродукции.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницыПродукции,
	|	ОтчетПереработчикаЗапасы.Ячейка КАК ЯчейкаЗапасов,
	|	ОтчетПереработчикаЗапасы.СчетУчета КАК СчетУчета,
	|	ОтчетПереработчикаЗапасы.СчетУчетаЗапасов КАК СчетУчетаЗапасов,
	|	ОтчетПереработчикаЗапасы.КоррСчетУчета КАК КоррСчетУчета,
	|	ОтчетПереработчикаЗапасы.СчетУчетаПродукции КАК СчетУчетаПродукции,
	|	ОтчетПереработчикаЗапасы.Номенклатура КАК Номенклатура,
	|	ОтчетПереработчикаЗапасы.Номенклатура.СчетУчетаЗатрат КАК СчетУчетаЗатратЗапасов,
	|	ОтчетПереработчикаЗапасы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ОтчетПереработчикаЗапасы.НомерГТД КАК НомерГТД,
	|	ОтчетПереработчикаЗапасы.КоррНоменклатура КАК КоррНоменклатура,
	|	ОтчетПереработчикаЗапасы.Характеристика КАК Характеристика,
	|	ОтчетПереработчикаЗапасы.КоррХарактеристика КАК КоррХарактеристика,
	|	ОтчетПереработчикаЗапасы.Партия КАК Партия,
	|	ОтчетПереработчикаЗапасы.ПартияСтатус КАК ПартияСтатус,
	|	ОтчетПереработчикаЗапасы.КоррПартия КАК КоррПартия,
	|	ОтчетПереработчикаЗапасы.Спецификация КАК Спецификация,
	|	ОтчетПереработчикаЗапасы.КоррСпецификация КАК КоррСпецификация,
	|	ОтчетПереработчикаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ОтчетПереработчикаЗапасы.КоррЗаказПокупателя КАК КоррЗаказПокупателя,
	|	ОтчетПереработчикаЗапасы.ДокументОснование КАК ЗаказПередачи,
	|	ОтчетПереработчикаЗапасы.Контрагент КАК Контрагент,
	|	ОтчетПереработчикаЗапасы.Договор КАК Договор,
	|	ОтчетПереработчикаЗапасы.Количество,
	|	0 КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ОтчетПереработчикаЗапасы.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА ОтчетПереработчикаЗапасы.Всего * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ОтчетПереработчикаЗапасы.Всего * ОтчетПереработчикаЗапасы.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ОтчетПереработчикаЗапасы.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаРасчетов,
	|	ОтчетПереработчикаЗапасы.Всего КАК СуммаРасчетовПереданные,
	|	ОтчетПереработчикаЗапасы.СчетДт КАК СчетДт,
	|	ОтчетПереработчикаЗапасы.СчетДтПродукции КАК СчетДтПродукции,
	|	ОтчетПереработчикаЗапасы.СчетКт КАК СчетКт,
	|	ОтчетПереработчикаЗапасы.СчетКтПродукции КАК СчетКтПродукции,
	|	ВЫРАЗИТЬ(&РаспределениеЗапасов КАК СТРОКА(100)) КАК СодержаниеПроводки,
	|	ВЫРАЗИТЬ(&РаспределениеЗапасов КАК СТРОКА(100)) КАК Содержание
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасы
	|ИЗ
	|	ВТТаблицаРаспределенияМатериаловСборка КАК ОтчетПереработчикаЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаВалютаУчета.Значение
	|					ИЗ
	|						Константа.ВалютаУчета КАК КонстантаВалютаУчета)) КАК КурсыУпрВалюты
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаНациональнаяВалюта.Значение
	|					ИЗ
	|						Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта)) КАК КурсыРегВалюты
	|		ПО (ИСТИНА),
	|	Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаЗапасы.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ТаблицаЗапасы.СценарийПланирования КАК СценарийПланирования,
	|	ТаблицаЗапасы.КоррОрганизация КАК КоррОрганизация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиницаПродукции КАК СтруктурнаяЕдиницаПродукции,
	|	ТаблицаЗапасы.ТипСтруктурнойЕдиницыПродукции КАК ТипСтруктурнойЕдиницыПродукции,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.КоррСтруктурнаяЕдиница КАК КоррСтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ТаблицаЗапасы.КоррСчетУчета КАК КоррСчетУчета,
	|	ТаблицаЗапасы.СчетУчетаЗатратЗапасов КАК СчетУчетаЗатратЗапасов,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.КоррНоменклатура КАК КоррНоменклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.КоррХарактеристика КАК КоррХарактеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.КоррПартия КАК КоррПартия,
	|	ТаблицаЗапасы.Спецификация КАК Спецификация,
	|	ТаблицаЗапасы.КоррСпецификация КАК КоррСпецификация,
	|	ТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаЗапасы.КоррЗаказПокупателя КАК КоррЗаказПокупателя,
	|	ТаблицаЗапасы.КоррСчетУчета КАК СчетДт,
	|	ТаблицаЗапасы.СчетУчета КАК СчетКт,
	|	ТаблицаЗапасы.СодержаниеПроводки КАК Содержание,
	|	ТаблицаЗапасы.СодержаниеПроводки КАК СодержаниеПроводки,
	|	ЛОЖЬ КАК ЗатратыНаВыпуск,
	|	СУММА(ТаблицаЗапасы.Количество) КАК Количество,
	|	0 КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Период,
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.СценарийПланирования,
	|	ТаблицаЗапасы.КоррОрганизация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиницаПродукции,
	|	ТаблицаЗапасы.ТипСтруктурнойЕдиницыПродукции,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.КоррСтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.КоррСчетУчета,
	|	ТаблицаЗапасы.СчетУчетаЗатратЗапасов,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.КоррНоменклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.КоррХарактеристика,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.КоррПартия,
	|	ТаблицаЗапасы.Спецификация,
	|	ТаблицаЗапасы.КоррСпецификация,
	|	ТаблицаЗапасы.ЗаказПокупателя,
	|	ТаблицаЗапасы.КоррЗаказПокупателя,
	|	ТаблицаЗапасы.СодержаниеПроводки,
	|	ТаблицаЗапасы.КоррСчетУчета,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.СодержаниеПроводки
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыПереданные.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗапасыПереданные.Период КАК Период,
	|	ТаблицаЗапасыПереданные.Организация КАК Организация,
	|	ТаблицаЗапасыПереданные.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыПереданные.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыПереданные.Партия КАК Партия,
	|	ТаблицаЗапасыПереданные.Контрагент КАК Контрагент,
	|	ТаблицаЗапасыПереданные.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасыПереданные.ЗаказПередачи <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ТаблицаЗапасыПереданные.ЗаказПередачи
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Заказ,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ПередачаВПереработку) КАК ТипПриемаПередачи,
	|	СУММА(ТаблицаЗапасыПереданные.Количество) КАК Количество,
	|	СУММА(ТаблицаЗапасыПереданные.СуммаРасчетовПереданные) КАК СуммаРасчетов
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыПереданные
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыПереданные.Период,
	|	ТаблицаЗапасыПереданные.Организация,
	|	ТаблицаЗапасыПереданные.Номенклатура,
	|	ТаблицаЗапасыПереданные.Характеристика,
	|	ТаблицаЗапасыПереданные.Партия,
	|	ТаблицаЗапасыПереданные.Контрагент,
	|	ТаблицаЗапасыПереданные.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасыПереданные.ЗаказПередачи <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ТаблицаЗапасыПереданные.ЗаказПередачи
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	Запрос.УстановитьПараметр("МоментКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментКонтроля);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьХарактеристики);
	Запрос.УстановитьПараметр("ИспользоватьПартии", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьПартии);
	Запрос.УстановитьПараметр("УчетПоЯчейкам", СтруктураДополнительныеСвойства.УчетнаяПолитика.УчетПоЯчейкам);
	Запрос.УстановитьПараметр("ТаблицаРаспределенияМатериаловСборка", СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРаспределенияМатериаловСборка);
	Запрос.УстановитьПараметр("РаспределениеЗапасов", НСтр("ru = 'Распределение запасов'"));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// Определим таблицу для учета запасов.
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыЗапасы", МассивРезультатов[2].Выгрузить());
	
	// Сформируем таблицу для учета запасов.
	СформироватьТаблицаЗапасыЗапасы(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства, СуммаСборки);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыПереданные", МассивРезультатов[3].Выгрузить());
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Удалить("ТаблицаРаспределенияМатериаловСборка");
	
КонецПроцедуры // ИнициализироватьДанныеПоЗапасам()

#КонецОбласти

#Область Продукция

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыКПоступлениюНаСклады(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ОтчетПереработчикаПродукция.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ОтчетПереработчикаПродукция.Период КАК Период,
	|	ОтчетПереработчикаПродукция.Организация КАК Организация,
	|	ОтчетПереработчикаПродукция.Номенклатура КАК Номенклатура,
	|	ОтчетПереработчикаПродукция.Характеристика КАК Характеристика,
	|	ОтчетПереработчикаПродукция.Партия КАК Партия,
	|	ОтчетПереработчикаПродукция.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	СУММА(ОтчетПереработчикаПродукция.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ОтчетПереработчикаПродукция
	|ГДЕ
	|	ОтчетПереработчикаПродукция.ОрдерныйСклад
	|	И ОтчетПереработчикаПродукция.Период >= ОтчетПереработчикаПродукция.ДатаОбновленияНаРелиз_1_2_1
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетПереработчикаПродукция.Номенклатура,
	|	ОтчетПереработчикаПродукция.Характеристика,
	|	ОтчетПереработчикаПродукция.Партия,
	|	ОтчетПереработчикаПродукция.Период,
	|	ОтчетПереработчикаПродукция.Организация,
	|	ОтчетПереработчикаПродукция.СтруктурнаяЕдиница
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МИНИМУМ(ОтчетПереработчикаОтходы.НомерСтроки),
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ОтчетПереработчикаОтходы.Ссылка.Дата,
	|	ОтчетПереработчикаОтходы.Ссылка.Организация,
	|	ОтчетПереработчикаОтходы.Номенклатура,
	|	ОтчетПереработчикаОтходы.Характеристика,
	|	ОтчетПереработчикаОтходы.Партия,
	|	ОтчетПереработчикаОтходы.Ссылка.СтруктурнаяЕдиница,
	|	СУММА(ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(ОтчетПереработчикаОтходы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|				ТОГДА ОтчетПереработчикаОтходы.Количество
	|			ИНАЧЕ ОтчетПереработчикаОтходы.Количество * ОтчетПереработчикаОтходы.ЕдиницаИзмерения.Коэффициент
	|		КОНЕЦ)
	|ИЗ
	|	Документ.ОтчетПереработчика.Отходы КАК ОтчетПереработчикаОтходы
	|ГДЕ
	|	ОтчетПереработчикаОтходы.Ссылка.СтруктурнаяЕдиница.ОрдерныйСклад
	|	И ОтчетПереработчикаОтходы.Ссылка = &Ссылка
	|	И ОтчетПереработчикаОтходы.Ссылка.Дата >= &ДатаОбновленияНаРелиз_1_2_1
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетПереработчикаОтходы.Ссылка,
	|	ОтчетПереработчикаОтходы.Номенклатура,
	|	ОтчетПереработчикаОтходы.Характеристика,
	|	ОтчетПереработчикаОтходы.Партия,
	|	ОтчетПереработчикаОтходы.Ссылка.Дата,
	|	ОтчетПереработчикаОтходы.Ссылка.Организация,
	|	ОтчетПереработчикаОтходы.Ссылка.СтруктурнаяЕдиница";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	
	// Временно: изменение движений по ордерному складу.
	Запрос.УстановитьПараметр("ДатаОбновленияНаРелиз_1_2_1", Константы.ДатаОбновленияНаРелиз_1_2_1.Получить());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыКПоступлениюНаСклады", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗапасыКПоступлениюНаСклады()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыПродукция(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства, СуммаСборки)
	
	ПоложениеЗаказаПокупателя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылкаОтчетПереработчика, "ПоложениеЗаказаПокупателя", Истина);
	ТаблицаОстатки = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказов.Скопировать();
	
	Для н = 0 По СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыПродукция.Количество() - 1 Цикл
		
		СтрокаТаблицаЗапасыПродукция = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыПродукция[н];
		
		// Сформируем выпуск продукции в количественном выражении. Если заказ покупателя указан - под заказ
		// покупателя, если нет - то под пустой заказ.
		СтрокаТаблицыПриход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаТаблицаЗапасыПродукция);
		
		// Если заказ на производство заполнен, а заказ покупателя нет, то необходимо проверить,
		// есть ли размещенные заказы покупателей в заказе поставщику.
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицаЗапасыПродукция.ЗаказПокупателя) 
		   И ЗначениеЗаполнено(СтрокаТаблицаЗапасыПродукция.ЗаказПоставщику)
		   И ПоложениеЗаказаПокупателя <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		 
			// Потом приход либо в свободный остаток либо в размещенные в заказе заказы поставщику.
			СтоимостьВыхода = СуммаСборки;
			КоличествоВыхода = СтрокаТаблицаЗапасыПродукция.Количество;
			
			СтруктураОтбора = Новый Структура("Организация, Номенклатура, Характеристика");
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТаблицаЗапасыПродукция);
			СтрокиОстатки = ТаблицаОстатки.НайтиСтроки(СтруктураОтбора);
			
			Для каждого СтрокаОстаток Из СтрокиОстатки Цикл
				
				Если КоличествоВыхода <= 0 Тогда
					Прервать;
				КонецЕсли; 
				Если СтрокаОстаток.Количество <= 0 Тогда
					Продолжить;
				КонецЕсли;
				КоличествоВыходаВРезерв = Мин(СтрокаОстаток.Количество, КоличествоВыхода);
				Если КоличествоВыхода = КоличествоВыходаВРезерв Тогда
					СтоимостьВыходаВРезерв = СтоимостьВыхода;
				Иначе
					СтоимостьВыходаВРезерв = Окр(СтоимостьВыхода * КоличествоВыходаВРезерв / КоличествоВыхода, 2, 1);
				КонецЕсли;
				КоличествоВыхода = КоличествоВыхода - КоличествоВыходаВРезерв;
				СтрокаОстаток.Количество = СтрокаОстаток.Количество - КоличествоВыходаВРезерв;
				
				СтрокаТаблицыРасход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасыПродукция);
				
				СтрокаТаблицыРасход.ВидДвижения = ВидДвиженияНакопления.Расход;
				
				СтрокаТаблицыРасход.КоррОрганизация = СтрокаТаблицаЗапасыПродукция.Организация;
				СтрокаТаблицыРасход.КоррСтруктурнаяЕдиница = СтрокаТаблицаЗапасыПродукция.СтруктурнаяЕдиница;
				СтрокаТаблицыРасход.КоррСчетУчета = СтрокаТаблицаЗапасыПродукция.СчетУчета;
				СтрокаТаблицыРасход.КоррНоменклатура = СтрокаТаблицаЗапасыПродукция.Номенклатура;
				СтрокаТаблицыРасход.КоррХарактеристика = СтрокаТаблицаЗапасыПродукция.Характеристика;
				СтрокаТаблицыРасход.КоррПартия = СтрокаТаблицаЗапасыПродукция.Партия;
				СтрокаТаблицыРасход.КоррСпецификация = СтрокаТаблицаЗапасыПродукция.Спецификация;
 								
				СтрокаТаблицыРасход.КоррЗаказПокупателя = СтрокаОстаток.ЗаказПокупателя;
				СтрокаТаблицыРасход.Количество = КоличествоВыходаВРезерв;
				СтрокаТаблицыРасход.Сумма = СтоимостьВыходаВРезерв;
				
				СтрокаТаблицыПриход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаТаблицаЗапасыПродукция);
								
				СтрокаТаблицыПриход.ВидДвижения = ВидДвиженияНакопления.Приход;
								
				СтрокаТаблицыПриход.ЗаказПокупателя = СтрокаОстаток.ЗаказПокупателя;
								
				СтрокаТаблицыРасход.КоррОрганизация = СтрокаТаблицаЗапасыПродукция.Организация;
				СтрокаТаблицыРасход.КоррСтруктурнаяЕдиница = СтрокаТаблицаЗапасыПродукция.СтруктурнаяЕдиница;
				СтрокаТаблицыРасход.КоррСчетУчета = СтрокаТаблицаЗапасыПродукция.СчетУчета;
				СтрокаТаблицыРасход.КоррНоменклатура = СтрокаТаблицаЗапасыПродукция.Номенклатура;
				СтрокаТаблицыРасход.КоррХарактеристика = СтрокаТаблицаЗапасыПродукция.Характеристика;
				СтрокаТаблицыРасход.КоррПартия = СтрокаТаблицаЗапасыПродукция.Партия;
				СтрокаТаблицыРасход.КоррСпецификация = СтрокаТаблицаЗапасыПродукция.Спецификация;

				СтрокаТаблицыПриход.КоррЗаказПокупателя = СтрокаТаблицаЗапасыПродукция.ЗаказПокупателя;
				СтрокаТаблицыПриход.Количество = КоличествоВыходаВРезерв;
				СтрокаТаблицыПриход.Сумма = СтоимостьВыходаВРезерв;
				
			КонецЦикла; 

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // СформироватьТаблицаЗапасыПродукция()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРазмещениеЗаказов(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства)
	
	ПоложениеЗаказаПокупателя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылкаОтчетПереработчика, "ПоложениеЗаказаПокупателя", Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	// Установка исключительной блокировки контролируемых размещений заказов.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаПродукция.Организация КАК Организация,
	|	ТаблицаПродукция.Номенклатура КАК Номенклатура,
	|	ТаблицаПродукция.Характеристика КАК Характеристика,
	|	ТаблицаПродукция.ИсточникОбеспечения КАК ИсточникОбеспечения
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ТаблицаПродукция
	|ГДЕ
	|	ТаблицаПродукция.ИсточникОбеспечения <> Неопределено
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПродукция.Организация,
	|	ТаблицаПродукция.Номенклатура,
	|	ТаблицаПродукция.Характеристика,
	|	ТаблицаПродукция.ИсточникОбеспечения";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РазмещениеЗаказов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;

	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	// Получение остатков.
	Запрос.Текст = 	
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаПродукция.Период КАК Период,
	|	ТаблицаПродукция.Организация КАК Организация,
	|	ТаблицаПродукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаПродукция.Номенклатура КАК Номенклатура,
	|	ТаблицаПродукция.Характеристика КАК Характеристика,
	|	ТаблицаПродукция.ИсточникОбеспечения КАК ИсточникОбеспечения,
	|	ТаблицаПродукция.Количество КАК Количество
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ТаблицаПродукция
	|ГДЕ
	|	ТаблицаПродукция.ИсточникОбеспечения <> НЕОПРЕДЕЛЕНО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РазмещениеЗаказовОстатки.Организация КАК Организация,
	|	РазмещениеЗаказовОстатки.Номенклатура КАК Номенклатура,
	|	РазмещениеЗаказовОстатки.Характеристика КАК Характеристика,
	|	РазмещениеЗаказовОстатки.ИсточникОбеспечения КАК ИсточникОбеспечения,
	|	РазмещениеЗаказовОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СУММА(РазмещениеЗаказовОстатки.КоличествоОстаток) КАК Количество
	|ИЗ
	|	(ВЫБРАТЬ
	|		РазмещениеЗаказовОстатки.Организация КАК Организация,
	|		РазмещениеЗаказовОстатки.Номенклатура КАК Номенклатура,
	|		РазмещениеЗаказовОстатки.Характеристика КАК Характеристика,
	|		РазмещениеЗаказовОстатки.ИсточникОбеспечения КАК ИсточникОбеспечения,
	|		РазмещениеЗаказовОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|		РазмещениеЗаказовОстатки.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.РазмещениеЗаказов.Остатки(
	|				&МоментКонтроля,
	|				(Организация, Номенклатура, Характеристика, ИсточникОбеспечения) В
	|					(ВЫБРАТЬ
	|						ТаблицаПродукция.Организация КАК Организация,
	|						ТаблицаПродукция.Номенклатура КАК Номенклатура,
	|						ТаблицаПродукция.Характеристика КАК Характеристика,
	|						ТаблицаПродукция.ИсточникОбеспечения КАК ИсточникОбеспечения
	|					ИЗ
	|						ВременнаяТаблицаПродукция КАК ТаблицаПродукция
	|					ГДЕ
	|						ТаблицаПродукция.ИсточникОбеспечения <> НЕОПРЕДЕЛЕНО)) КАК РазмещениеЗаказовОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаРазмещениеЗаказов.Организация,
	|		ДвиженияДокументаРазмещениеЗаказов.Номенклатура,
	|		ДвиженияДокументаРазмещениеЗаказов.Характеристика,
	|		ДвиженияДокументаРазмещениеЗаказов.ИсточникОбеспечения,
	|		ДвиженияДокументаРазмещениеЗаказов.ЗаказПокупателя,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаРазмещениеЗаказов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЕСТЬNULL(ДвиженияДокументаРазмещениеЗаказов.Количество, 0)
	|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаРазмещениеЗаказов.Количество, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РазмещениеЗаказов КАК ДвиженияДокументаРазмещениеЗаказов
	|	ГДЕ
	|		ДвиженияДокументаРазмещениеЗаказов.Регистратор = &Ссылка
	|		И ДвиженияДокументаРазмещениеЗаказов.Период <= &ПериодКонтроля) КАК РазмещениеЗаказовОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	РазмещениеЗаказовОстатки.Организация,
	|	РазмещениеЗаказовОстатки.Номенклатура,
	|	РазмещениеЗаказовОстатки.Характеристика,
	|	РазмещениеЗаказовОстатки.ЗаказПокупателя,
	|	РазмещениеЗаказовОстатки.ИсточникОбеспечения";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	Запрос.УстановитьПараметр("МоментКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментКонтроля);
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.ПериодКонтроля);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ТаблицаПродукция = РезультатЗапроса[0].Выгрузить();
	ТаблицаОстатки = РезультатЗапроса[1].Выгрузить();
	ТаблицаРазмещениеЗаказов = ТаблицаПродукция.СкопироватьКолонки();
	Для каждого СтрокаПродукция Из ТаблицаПродукция Цикл
		Если ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти
			ИЛИ ЗначениеЗаполнено(СтрокаПродукция.ЗаказПокупателя) Тогда
			Если ЗначениеЗаполнено(СтрокаПродукция.ЗаказПокупателя) Тогда
				НоваяСтрока = ТаблицаРазмещениеЗаказов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПродукция);
			КонецЕсли; 
			Продолжить;
		КонецЕсли; 
		СтруктураОтбора = Новый Структура("Организация, Номенклатура, Характеристика, ИсточникОбеспечения");
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаПродукция);
		СтрокиОстатки = ТаблицаОстатки.НайтиСтроки(СтруктураОтбора);
		Распределить = СтрокаПродукция.Количество;
		Для каждого СтрокаОстаток Из СтрокиОстатки Цикл
			Если Распределить <= 0 Тогда
				Прервать;
			КонецЕсли;
			Количество = Мин(Распределить, СтрокаОстаток.Количество);
			Если Количество <= 0 Тогда
				Продолжить;
			КонецЕсли; 
			НоваяСтрока = ТаблицаРазмещениеЗаказов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПродукция);
			НоваяСтрока.ЗаказПокупателя = СтрокаОстаток.ЗаказПокупателя;
			НоваяСтрока.Количество = Количество;
			Распределить = Распределить - Количество;
			СтрокаОстаток.Количество = СтрокаОстаток.Количество - Количество;
		КонецЦикла;
	КонецЦикла;  

	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРазмещениеЗаказов", ТаблицаРазмещениеЗаказов);
	
КонецПроцедуры // СформироватьТаблицаРазмещениеЗаказов()

#КонецОбласти

#Область РасчетыСПоставщиками

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПоставщиками(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ВозникновениеОбязательствПередПоставщиком", НСтр("ru='Возникновение обязательств перед поставщиком'"));
	Запрос.УстановитьПараметр("ЗачетАванса", НСтр("ru='Зачет предоплаты'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	Запрос.УстановитьПараметр("УчетВалютныхОпераций", СтруктураДополнительныеСвойства.УчетВалютныхОпераций);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.Период КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Контрагент КАК Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком КАК СчетУчета,
	|	ТаблицаДокумента.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ТаблицаДокумента.ВалютаРасчетов КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг) КАК ТипРасчетов,
	|	СУММА(ТаблицаДокумента.Сумма) КАК Сумма,
	|	СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВал,
	|	СУММА(ВЫБОР
	|			КОГДА &УчетВалютныхОпераций
	|				ТОГДА ТаблицаДокумента.СуммаРег
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаРег,
	|	СУММА(ТаблицаДокумента.Сумма) КАК СуммаДляОстатка,
	|	СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВалДляОстатка,
	|	ВЫРАЗИТЬ(&ВозникновениеОбязательствПередПоставщиком КАК СТРОКА(100)) КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПоставщиками
	|ИЗ
	|	ВременнаяТаблицаЗапасыДляРасчетов КАК ТаблицаДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаАвансовПоставщику,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.ТипРасчетов,
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал),
	|	СУММА(ВЫБОР
	|			КОГДА &УчетВалютныхОпераций
	|				ТОГДА ТаблицаДокумента.СуммаРег
	|			ИНАЧЕ 0
	|		КОНЕЦ),
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал),
	|	ВЫРАЗИТЬ(&ЗачетАванса КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ТипРасчетов,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.СчетУчетаАвансовПоставщику
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.ДокументКуда
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.ТипРасчетовКуда,
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал),
	|	СУММА(ВЫБОР
	|			КОГДА &УчетВалютныхОпераций
	|				ТОГДА ТаблицаДокумента.СуммаРег
	|			ИНАЧЕ 0
	|		КОНЕЦ),
	|	-СУММА(ТаблицаДокумента.Сумма),
	|	-СУММА(ТаблицаДокумента.СуммаВал),
	|	ВЫРАЗИТЬ(&ЗачетАванса КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.ДокументКуда
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.ТипРасчетовКуда
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Контрагент,
	|	Договор,
	|	Валюта,
	|	Документ,
	|	Заказ,
	|	ТипРасчетов,
	|	СчетУчета";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с контрагентами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПоставщиками.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Договор КАК Договор,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Документ КАК Документ,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Заказ КАК Заказ,
	|	ВременнаяТаблицаРасчетыСПоставщиками.ТипРасчетов КАК ТипРасчетов
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПоставщиками КАК ВременнаяТаблицаРасчетыСПоставщиками";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПоставщиками");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	РасчетыПроведениеДокументов.ПодготовитьТаблицуВзаиморасчетовСУчетомАвансов(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства, Истина);
	
	НомерЗапроса = 0;
	Запрос.Текст = КурсовыеРазницыУНФ.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыСПоставщиками(Запрос.МенеджерВременныхТаблиц, Истина, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПоставщиками", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходы(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПоложительнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеДоходы);
	Запрос.УстановитьПараметр("ОтрицательнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеРасходы);
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК СтруктурнаяЕдиница,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее) КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА &ОтрицательнаяКурсоваяРазницаСчетУчета
	|		ИНАЧЕ &ПоложительнаяКурсоваяРазницаСчетУчета
	|	КОНЕЦ КАК СчетУчета,
	|	ТаблицаДокумента.Валюта КАК Аналитика,
	|	&КурсоваяРазница КАК СодержаниеПроводки,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ КАК СуммаДоходов,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРасходов,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата КАК Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация КАК Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта,
	|		СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) КАК СуммаКурсовойРазницы
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Дата КАК Дата,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы КАК СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаКурсовыхРазницРасчетыСПоставщиками
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) >= 0.005
	|			ИЛИ СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) <= -0.005)) КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходы", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыОтложенные(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Период КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Сумма КАК СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаЗапасыДляРасчетов КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Организация КАК Организация,
	|	СУММА(ТаблицаДокумента.Сумма) КАК СуммаКСписанию
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Статья КАК Статья
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаЗапасыДоходыИРасходыОтложенные = МассивРезультатов[0].Выгрузить();
	ВыборкаРезультатаЗапроса = МассивРезультатов[1].Выбрать();
	
	ТаблицаПредоплатаДоходыИРасходыОтложенные = ТаблицаЗапасыДоходыИРасходыОтложенные.Скопировать();
	ТаблицаПредоплатаДоходыИРасходыОтложенные.Очистить();
	
	Если ВыборкаРезультатаЗапроса.Следующий() Тогда
		СуммаКСписанию = ВыборкаРезультатаЗапроса.СуммаКСписанию;
		Для каждого СтрокаЗапасыДоходыИРасходыОтложенные Из ТаблицаЗапасыДоходыИРасходыОтложенные Цикл
			Если СуммаКСписанию = 0 Тогда
				Продолжить
			ИначеЕсли СтрокаЗапасыДоходыИРасходыОтложенные.СуммаРасходов <= СуммаКСписанию Тогда
				СтрокаПредоплатаДоходыИРасходыОтложенные = ТаблицаПредоплатаДоходыИРасходыОтложенные.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПредоплатаДоходыИРасходыОтложенные, СтрокаЗапасыДоходыИРасходыОтложенные);
				СуммаКСписанию = СуммаКСписанию - СтрокаЗапасыДоходыИРасходыОтложенные.СуммаРасходов;
			ИначеЕсли СтрокаЗапасыДоходыИРасходыОтложенные.СуммаРасходов > СуммаКСписанию Тогда
				СтрокаПредоплатаДоходыИРасходыОтложенные = ТаблицаПредоплатаДоходыИРасходыОтложенные.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПредоплатаДоходыИРасходыОтложенные, СтрокаЗапасыДоходыИРасходыОтложенные);
				СтрокаПредоплатаДоходыИРасходыОтложенные.СуммаРасходов = СуммаКСписанию;
				СуммаКСписанию = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для каждого СтрокаПредоплатаДоходыИРасходыОтложенные Из ТаблицаПредоплатаДоходыИРасходыОтложенные Цикл
		СтрокаЗапасыДоходыИРасходыОтложенные = ТаблицаЗапасыДоходыИРасходыОтложенные.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗапасыДоходыИРасходыОтложенные, СтрокаПредоплатаДоходыИРасходыОтложенные);
		СтрокаЗапасыДоходыИРасходыОтложенные.ВидДвижения = ВидДвиженияНакопления.Расход;
	КонецЦикла;
	
	ВыборкаРезультатаЗапроса = МассивРезультатов[2].Выбрать();
	
	Если ВыборкаРезультатаЗапроса.Следующий() Тогда
		Статья = ВыборкаРезультатаЗапроса.Статья;
	Иначе
		Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	Таблица.Период КАК Период,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Документ КАК Документ,
	|	&Статья КАК Статья,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Таблица.СуммаРасходов КАК СуммаРасходов
	|ПОМЕСТИТЬ ВременнаяТаблицаПредоплатаДоходыИРасходыОтложенные
	|ИЗ
	|	&Таблица КАК Таблица";
	Запрос.УстановитьПараметр("Таблица", ТаблицаПредоплатаДоходыИРасходыОтложенные);
	Запрос.УстановитьПараметр("Статья", Статья);
	
	Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыОтложенные", ТаблицаЗапасыДоходыИРасходыОтложенные);
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыОтложенные()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыНераспределенные(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаДокумента.Период КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	ТаблицаДокумента.Сумма КАК СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыНераспределенные", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыНераспределенные()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыКассовыйМетод(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.ДокументДата КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	-ТаблицаДокумента.Сумма КАК СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Период,
	|	Таблица.Организация,
	|	Таблица.НаправлениеДеятельности,
	|	Таблица.Статья,
	|	Таблица.СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаПредоплатаДоходыИРасходыОтложенные КАК Таблица";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыКассовыйМетод", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыКассовыйМетод() 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗакупкиДляКУДиР(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	Запрос.УстановитьПараметр("ДатаПереходаНаНДС20", Дата('20190101'));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаПредоплата.Период КАК Период,
	|	ВременнаяТаблицаПредоплата.Период КАК ДатаДокумента,
	|	ВременнаяТаблицаПредоплата.НомерСтроки КАК НомерСтроки,
	|	ВременнаяТаблицаПредоплата.ОрганизацияВНУ КАК Организация,
	|	&Ссылка КАК ТоварныйДокумент,
	|	ВременнаяТаблицаПредоплата.Документ КАК ДенежныйДокумент,
	|	ВременнаяТаблицаПредоплата.Документ.УчитыватьВНУ КАК УчитыватьВНУ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаПредоплата.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДС)
	|			ТОГДА ВЫБОР
	|					КОГДА ВременнаяТаблицаПредоплата.Период < &ДатаПереходаНаНДС20
	|						ТОГДА 18
	|					ИНАЧЕ 20
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтавкаНДС,
	|	ЛОЖЬ КАК ЭтоТоварыКРеализации,
	|	СУММА(ВременнаяТаблицаПредоплата.СуммаВал) КАК СуммаВал,
	|	СУММА(ВременнаяТаблицаПредоплата.СуммаВал) КАК Сумма,
	|	0 СуммаНДС,
	|	ВременнаяТаблицаПредоплата.ВалютаРасчетов КАК Валюта
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ВременнаяТаблицаПредоплата
	|ГДЕ
	|	ВременнаяТаблицаПредоплата.ВестиРасчетыПоДокументам
	|	И ВременнаяТаблицаПредоплата.ОрганизацияВНУ.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаПредоплата.НомерСтроки,
	|	ВременнаяТаблицаПредоплата.Период,
	|	ВременнаяТаблицаПредоплата.ОрганизацияВНУ,
	|	ВременнаяТаблицаПредоплата.Документ,
	|	ВременнаяТаблицаПредоплата.НалогообложениеНДС,
	|	ВременнаяТаблицаПредоплата.ВалютаРасчетов,
	|	ВременнаяТаблицаПредоплата.Документ.УчитыватьВНУ
	|;
	|";
	
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	
	УчетВалюты = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
	НацВалюта = Константы.НациональнаяВалюта.Получить();
	ВалютаДокумента = ДокументСсылкаОтчетПереработчика.ВалютаДокумента;
	
	Если УчетВалюты И ВалютаДокумента <> НацВалюта Тогда
		ТекущийКурс = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ДокументСсылкаОтчетПереработчика.Дата);
		Для Каждого СтрокаОплаты Из ТаблицаРезультат Цикл
			СтрокаОплаты.Сумма = СтрокаОплаты.Сумма * ТекущийКурс.Курс / ТекущийКурс.Кратность;
			СтрокаОплаты.СуммаНДС = СтрокаОплаты.Сумма - СтрокаОплаты.Сумма /((100+ СтрокаОплаты.СтавкаНДС)/100);
			СтрокаОплаты.Сумма = СтрокаОплаты.Сумма - СтрокаОплаты.СуммаНДС;
		КонецЦикла;
	Иначе
		Для Каждого СтрокаОплаты Из ТаблицаРезультат Цикл
			СтрокаОплаты.СуммаНДС = СтрокаОплаты.Сумма - СтрокаОплаты.Сумма /((100+ СтрокаОплаты.СтавкаНДС)/100);
			СтрокаОплаты.Сумма = СтрокаОплаты.Сумма - СтрокаОплаты.СуммаНДС;
		КонецЦикла;
	КонецЕсли;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗакупкиДляКУДиР", ТаблицаРезультат);
	
КонецПроцедуры // СформироватьТаблицаЗакупкиДляКУДиР()

Процедура СформироватьТаблицаЗапасыВРазрезеГТД(СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыНаСкладах.НомерСтроки) КАК НомерСтроки
	|	,ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
	|	,ТаблицаЗапасыНаСкладах.Период КАК Период
	|	,ТаблицаЗапасыНаСкладах.Организация КАК Организация
	|	,ТаблицаЗапасыНаСкладах.Номенклатура КАК Номенклатура
	|	,ТаблицаЗапасыНаСкладах.Характеристика КАК Характеристика
	|	,ТаблицаЗапасыНаСкладах.Партия КАК Партия
	|	,ТаблицаЗапасыНаСкладах.НомерГТД КАК НомерГТД
	|	,ТаблицаЗапасыНаСкладах.СтранаПроисхождения КАК СтранаПроисхождения
	|	,СУММА(ТаблицаЗапасыНаСкладах.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыНаСкладах
	|ГДЕ
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения <> Значение(Справочник.СтраныМира.Россия)
	|	И ТаблицаЗапасыНаСкладах.СтранаПроисхождения <> Значение(Справочник.СтраныМира.ПустаяСсылка)
	|	И ТаблицаЗапасыНаСкладах.НомерГТД <> Значение(Справочник.НомераГТД.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыНаСкладах.Период
	|	,ТаблицаЗапасыНаСкладах.Организация
	|	,ТаблицаЗапасыНаСкладах.Номенклатура
	|	,ТаблицаЗапасыНаСкладах.Характеристика
	|	,ТаблицаЗапасыНаСкладах.Партия
	|	,ТаблицаЗапасыНаСкладах.НомерГТД
	|	,ТаблицаЗапасыНаСкладах.СтранаПроисхождения";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыВРазрезеГТД", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеРасчетыСПоставщиками(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ОтчетПереработчика.Договор.ЭтоДоговорОбслуживания
	|				И ОтчетПереработчика.Договор.ДоговорОбслуживанияНаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВестиУчетРасходовПоДоговорамОбслуживания,
	|	ОтчетПереработчика.Договор.ДоговорОбслуживанияНаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ВременнаяТаблицаШапка
	|ИЗ
	|	Документ.ОтчетПереработчика КАК ОтчетПереработчика
	|ГДЕ
	|	ОтчетПереработчика.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетПереработчикаЗатраты.Дата КАК Период,
	|	1 КАК НомерСтроки,
	|	&Организация КАК Организация,
	|	ОтчетПереработчикаЗатраты.Контрагент КАК Контрагент,
	|	ОтчетПереработчикаЗатраты.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	ОтчетПереработчикаЗатраты.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ОтчетПереработчикаЗатраты.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	ОтчетПереработчикаЗатраты.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	ОтчетПереработчикаЗатраты.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	ОтчетПереработчикаЗатраты.Договор КАК Договор,
	|	ОтчетПереработчикаЗатраты.Расход.СчетУчетаЗатрат КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|		ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности
	|		ИНАЧЕ ОтчетПереработчикаЗатраты.Расход.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ОтчетПереработчикаЗатраты.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	&Ссылка КАК Документ,
	|	ОтчетПереработчикаЗатраты.ДокументОснование КАК Заказ,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ОтчетПереработчикаЗатраты.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ОтчетПереработчикаЗатраты.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|						ТОГДА ОтчетПереработчикаЗатраты.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|					ИНАЧЕ ОтчетПереработчикаЗатраты.СуммаНДС * ОтчетПереработчикаЗатраты.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ОтчетПереработчикаЗатраты.Ссылка.Кратность)
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ОтчетПереработчикаЗатраты.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА ОтчетПереработчикаЗатраты.Всего * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ОтчетПереработчикаЗатраты.Всего * ОтчетПереработчикаЗатраты.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ОтчетПереработчикаЗатраты.Ссылка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ОтчетПереработчикаЗатраты.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ОтчетПереработчикаЗатраты.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|						ТОГДА ОтчетПереработчикаЗатраты.СуммаНДС * КурсыРегВалюты.Курс * ОтчетПереработчикаЗатраты.Ссылка.Кратность / (ОтчетПереработчикаЗатраты.Ссылка.Курс * КурсыРегВалюты.Кратность)
	|					ИНАЧЕ ОтчетПереработчикаЗатраты.СуммаНДС
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ОтчетПереработчикаЗатраты.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА ОтчетПереработчикаЗатраты.Всего * КурсыРегВалюты.Курс * ОтчетПереработчикаЗатраты.Ссылка.Кратность / (ОтчетПереработчикаЗатраты.Ссылка.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ОтчетПереработчикаЗатраты.Всего
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ОтчетПереработчикаЗатраты.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА ОтчетПереработчикаЗатраты.Всего
	|			ИНАЧЕ ОтчетПереработчикаЗатраты.Всего * ОтчетПереработчикаЗатраты.Ссылка.Курс / ОтчетПереработчикаЗатраты.Ссылка.Кратность
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаРег
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасыДляРасчетов
	|ИЗ
	|	Документ.ОтчетПереработчика КАК ОтчетПереработчикаЗатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаВалютаУчета.Значение
	|					ИЗ
	|						Константа.ВалютаУчета КАК КонстантаВалютаУчета)) КАК КурсыУпрВалюты
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаНациональнаяВалюта.Значение
	|					ИЗ
	|						Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта)) КАК КурсыРегВалюты
	|		ПО (ИСТИНА),
	|	Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта,
	|	ВременнаяТаблицаШапка
	|ГДЕ
	|	ОтчетПереработчикаЗатраты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаДокумента.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаДокумента.Ссылка.Контрагент КАК Контрагент,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПокупателя КАК СчетУчетаАвансовПокупателя,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПоставщику КАК СчетУчетаАвансовПоставщику,
	|	ТаблицаДокумента.Ссылка.Договор КАК Договор,
	|	ТаблицаДокумента.Ссылка.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ТаблицаДокумента.Ссылка.ДокументОснование КАК Заказ,
	|	ТаблицаДокумента.Ссылка.Организация КАК ОрганизацияВНУ,
	|	ТаблицаДокумента.Ссылка.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее) КАК НаправлениеДеятельностиПродажи,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс) КАК ТипРасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг) КАК ТипРасчетовКуда,
	|	&Ссылка КАК ДокументКуда,
	|	ТаблицаДокумента.Документ КАК Документ,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам
	|				ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Документ) = ТИП(Документ.АвансовыйОтчет)
	|				ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Документ) = ТИП(Документ.Взаимозачет)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам)
	|		ИНАЧЕ ТаблицаДокумента.Документ.Статья
	|	КОНЕЦ КАК Статья,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ.Дата
	|		ИНАЧЕ ТаблицаДокумента.Ссылка.Дата
	|	КОНЕЦ КАК ДокументДата,
	|	СУММА(ВЫРАЗИТЬ(ТаблицаДокумента.СуммаРасчетов * ТаблицаДокумента.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * ТаблицаДокумента.Кратность) КАК ЧИСЛО(15, 2))) КАК Сумма,
	|	СУММА(ТаблицаДокумента.СуммаРасчетов) КАК СуммаВал,
	|	СУММА(ВЫРАЗИТЬ(ВЫБОР КОГДА ТаблицаДокумента.Ссылка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА ТаблицаДокумента.СуммаРасчетов
	|			ИНАЧЕ ТаблицаДокумента.СуммаРасчетов * ТаблицаДокумента.Курс / ТаблицаДокумента.Кратность
	|		КОНЕЦ КАК ЧИСЛО(15, 2))) КАК СуммаРег
	|ПОМЕСТИТЬ ВременнаяТаблицаПредоплата
	|ИЗ
	|	Документ.ОтчетПереработчика.Предоплата КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаВалютаУчета.Значение
	|					ИЗ
	|						Константа.ВалютаУчета КАК КонстантаВалютаУчета)) КАК КурсыВалютУчетаСрезПоследних
	|		ПО (ИСТИНА),
	|	Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.Документ,
	|	ТаблицаДокумента.Ссылка.Дата,
	|	ТаблицаДокумента.Ссылка.Контрагент,
	|	ТаблицаДокумента.Ссылка.Договор,
	|	ТаблицаДокумента.Ссылка.ДокументОснование,
	|	ТаблицаДокумента.Ссылка.Договор.ВалютаРасчетов,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПокупателя,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПоставщику,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам
	|				ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Документ) = ТИП(Документ.АвансовыйОтчет)
	|				ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Документ) = ТИП(Документ.Взаимозачет)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам)
	|		ИНАЧЕ ТаблицаДокумента.Документ.Статья
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ.Дата
	|		ИНАЧЕ ТаблицаДокумента.Ссылка.Дата
	|	КОНЕЦ,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДоговорам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоЗаказам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	
	Запрос.ВыполнитьПакет();
	
	СформироватьТаблицаРасчетыСПоставщиками(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходы(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыОтложенные(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыНераспределенные(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыКассовыйМетод(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеРасчетыСПоставщиками()

////////////////////////////////////////////////////////////////////////////////
// ИНИЦИАЛИЗАЦИЯ ДАННЫХ

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	0 КАК НомерСтроки,
	|	ОтчетПереработчикаПродукция.Ссылка КАК Ссылка,
	|	ОтчетПереработчикаПродукция.КлючСвязи КАК КлючСвязи,
	|	ОтчетПереработчика.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК КоррОрганизация,
	|	ОтчетПереработчика.СтруктурнаяЕдиница.ОрдерныйСклад КАК ОрдерныйСклад,
	|	ЕСТЬNULL(ОтчетПереработчика.СтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК СтруктурнаяЕдиница,
	|	ЕСТЬNULL(ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка), ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК КоррСтруктурнаяЕдиница,
	|	ОтчетПереработчика.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
	|	ВЫБОР
	|		КОГДА &УчетПоЯчейкам
	|			ТОГДА ОтчетПереработчика.Ячейка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|	КОНЕЦ КАК Ячейка,
	|	ОтчетПереработчикаПродукция.Спецификация КАК Спецификация,
	|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка) КАК КоррСпецификация,
	|	ВЫБОР
	|		КОГДА ОтчетПереработчика.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|			ТОГДА ОтчетПереработчикаПродукция.Номенклатура.СчетУчетаЗапасов
	|		ИНАЧЕ ОтчетПереработчикаПродукция.Номенклатура.СчетУчетаЗатрат
	|	КОНЕЦ КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ОтчетПереработчика.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|			ТОГДА ОтчетПереработчикаПродукция.Номенклатура.СчетУчетаЗапасов
	|		ИНАЧЕ ОтчетПереработчикаПродукция.Номенклатура.СчетУчетаЗатрат
	|	КОНЕЦ КАК СчетУчетаПродукции,
	|	ВЫБОР
	|		КОГДА ОтчетПереработчика.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|			ТОГДА ОтчетПереработчикаПродукция.Номенклатура.СчетУчетаЗапасов
	|		ИНАЧЕ ОтчетПереработчикаПродукция.Номенклатура.СчетУчетаЗатрат
	|	КОНЕЦ КАК СчетДт,
	|	ВЫБОР
	|		КОГДА ОтчетПереработчика.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|			ТОГДА ОтчетПереработчикаПродукция.Номенклатура.СчетУчетаЗапасов
	|		ИНАЧЕ ОтчетПереработчикаПродукция.Номенклатура.СчетУчетаЗатрат
	|	КОНЕЦ КАК СчетДтПродукции,
	|	ВЫБОР
	|		КОГДА ОтчетПереработчикаПродукция.Партия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ОтветственноеХранение)
	|				ИЛИ ОтчетПереработчикаПродукция.Партия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварыНаКомиссии)
	|				ИЛИ ОтчетПереработчикаПродукция.Партия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ДавальческоеСырье)
	|			ТОГДА ОтчетПереработчикаПродукция.Номенклатура.СчетУчетаЗапасов
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ОтчетПереработчика.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|					ТОГДА ОтчетПереработчикаПродукция.Номенклатура.СчетУчетаЗапасов
	|				ИНАЧЕ ОтчетПереработчикаПродукция.Номенклатура.СчетУчетаЗатрат
	|			КОНЕЦ
	|	КОНЕЦ КАК СчетКтПродукции,
	|	ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка) КАК КоррСчетУчета,
	|	ОтчетПереработчикаПродукция.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК КоррНоменклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА ОтчетПереработчикаПродукция.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК КоррХарактеристика,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПартии
	|			ТОГДА ОтчетПереработчикаПродукция.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Партия,
	|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка) КАК КоррПартия,
	|	ОтчетПереработчикаПродукция.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА ОтчетПереработчика.ДокументОснование ССЫЛКА Документ.ЗаказПоставщику
	|			ТОГДА ОтчетПереработчика.ДокументОснование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаказПоставщику,
	|	ВЫБОР
	|		КОГДА ОтчетПереработчика.ДокументОснование ССЫЛКА Документ.ЗаказПоставщику
	|			ТОГДА ВЫРАЗИТЬ(ОтчетПереработчика.ДокументОснование КАК Документ.ЗаказПоставщику).УчетПотребностиПоСкладам
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК УчетПотребностиПоСкладам,
	|	ВЫБОР
	|		КОГДА ОтчетПереработчика.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ОтчетПереработчика.ДокументОснование
	|	КОНЕЦ КАК ИсточникОбеспечения,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ОтчетПереработчикаПродукция.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА ОтчетПереработчикаПродукция.Количество
	|		ИНАЧЕ ОтчетПереработчикаПродукция.Количество * ОтчетПереработчикаПродукция.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Количество,
	|	0 КАК Сумма,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка) КАК КоррЗаказПокупателя,
	|	ВЫРАЗИТЬ(&СборкаЗапасов КАК СТРОКА(100)) КАК СодержаниеПроводки,
	|	ВЫРАЗИТЬ(&СборкаЗапасов КАК СТРОКА(100)) КАК Содержание,
	|	&ДатаОбновленияНаРелиз_1_2_1 КАК ДатаОбновленияНаРелиз_1_2_1,
	|	ЕСТЬNULL(ПолитикиУчетаСерий.ПолитикаУчетаСерий, ОтчетПереработчикаПродукция.Номенклатура.ПолитикаУчетаСерий) КАК ПолитикаУчетаСерий
	|ПОМЕСТИТЬ ВременнаяТаблицаПродукция
	|ИЗ
	|	Документ.ОтчетПереработчика.Продукция КАК ОтчетПереработчикаПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|		ПО ОтчетПереработчикаПродукция.Номенклатура = ПолитикиУчетаСерий.Владелец
	|			И (&СтруктурнаяЕдиница = ПолитикиУчетаСерий.СтруктурнаяЕдиница)
	|			И (&Организация = ПолитикиУчетаСерий.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК ОтчетПереработчика
	|		ПО ОтчетПереработчикаПродукция.Ссылка = ОтчетПереработчика.Ссылка
	|ГДЕ
	|	ОтчетПереработчикаПродукция.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаЗапасы.Период КАК Период,
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК СценарийПланирования,
	|	ТаблицаЗапасы.КоррОрганизация КАК КоррОрганизация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.КоррСтруктурнаяЕдиница КАК КоррСтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ТаблицаЗапасы.КоррСчетУчета КАК КоррСчетУчета,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.КоррНоменклатура КАК КоррНоменклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.КоррХарактеристика КАК КоррХарактеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.КоррПартия КАК КоррПартия,
	|	ТаблицаЗапасы.Спецификация КАК Спецификация,
	|	ТаблицаЗапасы.КоррСпецификация КАК КоррСпецификация,
	|	ТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаЗапасы.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ТаблицаЗапасы.КоррЗаказПокупателя КАК КоррЗаказПокупателя,
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	ТаблицаЗапасы.СодержаниеПроводки КАК СодержаниеПроводки,
	|	ТаблицаЗапасы.Содержание КАК Содержание,
	|	ЛОЖЬ КАК ЗатратыНаВыпуск,
	|	СУММА(ТаблицаЗапасы.Количество) КАК Количество,
	|	СУММА(ТаблицаЗапасы.Сумма) КАК Сумма,
	|	ЛОЖЬ КАК ФиксированнаяСтоимость
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ТаблицаЗапасы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Период,
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.КоррОрганизация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.КоррСтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.КоррСчетУчета,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.КоррНоменклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.КоррХарактеристика,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.КоррПартия,
	|	ТаблицаЗапасы.Спецификация,
	|	ТаблицаЗапасы.КоррСпецификация,
	|	ТаблицаЗапасы.ЗаказПокупателя,
	|	ТаблицаЗапасы.ЗаказПоставщику,
	|	ТаблицаЗапасы.КоррЗаказПокупателя,
	|	ТаблицаЗапасы.СодержаниеПроводки,
	|	ТаблицаЗапасы.Содержание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыНаСкладах.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаЗапасыНаСкладах.Период КАК Период,
	|	ТаблицаЗапасыНаСкладах.Организация КАК Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия КАК Партия,
	|	ТаблицаЗапасыНаСкладах.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасыНаСкладах.Ячейка КАК Ячейка,
	|	СУММА(ТаблицаЗапасыНаСкладах.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ТаблицаЗапасыНаСкладах
	|ГДЕ
	|	ТаблицаЗапасыНаСкладах.Период < ТаблицаЗапасыНаСкладах.ДатаОбновленияНаРелиз_1_2_1
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыНаСкладах.Период,
	|	ТаблицаЗапасыНаСкладах.Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия,
	|	ТаблицаЗапасыНаСкладах.СтруктурнаяЕдиница,
	|	ТаблицаЗапасыНаСкладах.Ячейка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыНаСкладах.НомерСтроки),
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаЗапасыНаСкладах.Период,
	|	ТаблицаЗапасыНаСкладах.Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия,
	|	ТаблицаЗапасыНаСкладах.СтруктурнаяЕдиница,
	|	ТаблицаЗапасыНаСкладах.Ячейка,
	|	СУММА(ТаблицаЗапасыНаСкладах.Количество)
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ТаблицаЗапасыНаСкладах
	|ГДЕ
	|	НЕ ТаблицаЗапасыНаСкладах.ОрдерныйСклад
	|	И ТаблицаЗапасыНаСкладах.Период >= ТаблицаЗапасыНаСкладах.ДатаОбновленияНаРелиз_1_2_1
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыНаСкладах.Период,
	|	ТаблицаЗапасыНаСкладах.Организация,
	|	ТаблицаЗапасыНаСкладах.Номенклатура,
	|	ТаблицаЗапасыНаСкладах.Характеристика,
	|	ТаблицаЗапасыНаСкладах.Партия,
	|	ТаблицаЗапасыНаСкладах.СтруктурнаяЕдиница,
	|	ТаблицаЗапасыНаСкладах.Ячейка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаВыпускПродукции.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаВыпускПродукции.Период КАК Период,
	|	ТаблицаВыпускПродукции.Организация КАК Организация,
	|	ТаблицаВыпускПродукции.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаВыпускПродукции.Номенклатура КАК Номенклатура,
	|	ТаблицаВыпускПродукции.Характеристика КАК Характеристика,
	|	ТаблицаВыпускПродукции.Партия КАК Партия,
	|	ТаблицаВыпускПродукции.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаВыпускПродукции.Спецификация КАК Спецификация,
	|	СУММА(ТаблицаВыпускПродукции.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ТаблицаВыпускПродукции
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаВыпускПродукции.Период,
	|	ТаблицаВыпускПродукции.Организация,
	|	ТаблицаВыпускПродукции.СтруктурнаяЕдиница,
	|	ТаблицаВыпускПродукции.Номенклатура,
	|	ТаблицаВыпускПродукции.Характеристика,
	|	ТаблицаВыпускПродукции.Партия,
	|	ТаблицаВыпускПродукции.ЗаказПокупателя,
	|	ТаблицаВыпускПродукции.Спецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПродукция.Ссылка.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаПродукция.Ссылка.Дата КАК ДатаСобытия,
	|	ЗНАЧЕНИЕ(Перечисление.ОперацииСерийНоменклатуры.Приход) КАК Операция,
	|	&Организация КАК Организация,
	|	ТаблицаСерииНоменклатуры.Серия КАК Серия,
	|	ТаблицаПродукция.Номенклатура КАК Номенклатура,
	|	ТаблицаПродукция.Характеристика КАК Характеристика,
	|	ТаблицаПродукция.Партия КАК Партия,
	|	ТаблицаПродукция.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаПродукция.Ссылка.Ячейка КАК Ячейка,
	|	ВЫБОР
	|		КОГДА ТаблицаСерииНоменклатуры.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ ТаблицаСерииНоменклатуры.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ТаблицаПродукция.ПолитикаУчетаСерий = ЗНАЧЕНИЕ(Справочник.ПолитикаУчетаСерий.ПустаяСсылка)
	|			ТОГДА &ОстаткиСерийНоменклатуры
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаПродукция.ПолитикаУчетаСерий.ТипПолитики = ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.УправлениеОстаткамиСерий)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|	КОНЕЦ КАК ОстаткиСерийНоменклатуры
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ТаблицаПродукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика.СерииНоменклатуры КАК ТаблицаСерииНоменклатуры
	|		ПО ТаблицаПродукция.Ссылка = ТаблицаСерииНоменклатуры.Ссылка
	|			И ТаблицаПродукция.КлючСвязи = ТаблицаСерииНоменклатуры.КлючСвязи
	|ГДЕ
	|	ТаблицаПродукция.Ссылка = &Ссылка
	|	И &ИспользоватьСерииНоменклатуры
	|	И НЕ ТаблицаПродукция.Ссылка.СтруктурнаяЕдиница.ОрдерныйСклад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаПродукция.НомерСтроки) КАК КоррНомерСтроки,
	|	ТаблицаПродукция.ЗаказПокупателя КАК КоррЗаказПокупателя,
	|	ТаблицаПродукция.Номенклатура КАК КоррНоменклатура,
	|	ТаблицаПродукция.Характеристика КАК КоррХарактеристика,
	|	ТаблицаПродукция.Партия КАК КоррПартия,
	|	ТаблицаПродукция.Спецификация КАК КоррСпецификация,
	|	ТаблицаПродукция.СчетУчета КАК КоррСчетУчета,
	|	ТаблицаПродукция.СчетУчетаПродукции КАК СчетУчетаПродукции,
	|	ТаблицаПродукция.СчетДт КАК СчетДт,
	|	ТаблицаПродукция.СчетДтПродукции КАК СчетДтПродукции,
	|	ТаблицаПродукция.СчетКтПродукции КАК СчетКтПродукции,
	|	СУММА(ТаблицаПродукция.Количество) КАК КоррКоличество,
	|	ЛОЖЬ КАК Распределено
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ТаблицаПродукция
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПродукция.Номенклатура,
	|	ТаблицаПродукция.Характеристика,
	|	ТаблицаПродукция.Партия,
	|	ТаблицаПродукция.Спецификация,
	|	ТаблицаПродукция.СчетУчета,
	|	ТаблицаПродукция.СчетУчетаПродукции,
	|	ТаблицаПродукция.СчетДт,
	|	ТаблицаПродукция.СчетДтПродукции,
	|	ТаблицаПродукция.СчетКтПродукции,
	|	ТаблицаПродукция.ЗаказПокупателя
	|
	|УПОРЯДОЧИТЬ ПО
	|	КоррНомерСтроки";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьХарактеристики);
	Запрос.УстановитьПараметр("ИспользоватьПартии",  СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьПартии);
	Запрос.УстановитьПараметр("УчетПоЯчейкам",  СтруктураДополнительныеСвойства.УчетнаяПолитика.УчетПоЯчейкам);
	
	Запрос.УстановитьПараметр("ИспользоватьСерииНоменклатуры", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьСерииНоменклатуры);
	Запрос.УстановитьПараметр("ОстаткиСерийНоменклатуры", СтруктураДополнительныеСвойства.УчетнаяПолитика.ОстаткиСерийНоменклатуры);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", ДокументСсылкаОтчетПереработчика.СтруктурнаяЕдиница);
	
	// Временно: изменение движений по ордерному складу.
	Запрос.УстановитьПараметр("ДатаОбновленияНаРелиз_1_2_1", Константы.ДатаОбновленияНаРелиз_1_2_1.Получить());
		
	Запрос.УстановитьПараметр("СборкаЗапасов", НСтр("ru = 'Сборка запасов'"));

	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыПродукция", МассивРезультатов[1].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасы", СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыПродукция.СкопироватьКолонки());
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыНаСкладах", МассивРезультатов[2].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВыпускПродукции", МассивРезультатов[3].Выгрузить());
	
	// Серии номенклатуры
	РезультатЗапроса5 = МассивРезультатов[4].Выгрузить();
	Если Не СтруктураДополнительныеСвойства.УчетнаяПолитика.МиграцияСерийНоменклатурыВыполнена Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерииНоменклатурыГарантии", РезультатЗапроса5);
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДвиженияСерииНоменклатуры", Новый ТаблицаЗначений);
	Иначе
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДвиженияСерииНоменклатуры", РезультатЗапроса5);
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерииНоменклатурыГарантии", Новый ТаблицаЗначений);
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура("ОстаткиСерийНоменклатуры", Истина);
	ОстаткиСерийНоменклатурыСтроки = РезультатЗапроса5.НайтиСтроки(ПараметрыОтбора);
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерииНоменклатуры", РезультатЗапроса5.Скопировать(ОстаткиСерийНоменклатурыСтроки));
	
	// Формирование проводок документа.
	ПроведениеДокументовУНФ.СформироватьТаблицуПроводок(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства);
	
	// Расчеты с поставщиками.
	ИнициализироватьДанныеРасчетыСПоставщиками(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства);
	
	// Сформируем таблицу по размещению заказов.
	СформироватьТаблицаРазмещениеЗаказов(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства);
	
	// Сформируем таблицу распределения материалов.
	ТаблицаПродукция = МассивРезультатов[5].Выгрузить();
	СформироватьТаблицуРаспределенияМатериалов(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства, ТаблицаПродукция);
	
	// Запасы.
	СуммаСборки = 0;
	ИнициализироватьДанныеПоЗапасам(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства, СуммаСборки);
	
	СформироватьТаблицаЗапасыВРазрезеГТД(СтруктураДополнительныеСвойства);
	
	// Услуги.
	СуммаУслуги = 0;
	ИнициализироватьДанныеПоУслуге(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства, СуммаУслуги);
	
	// Продукция.
	СуммаСборки = СуммаСборки + СуммаУслуги;
	СформироватьТаблицаЗапасыПродукция(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства, СуммаСборки);
	
	// Отходы.
	ИнициализироватьДанныеПоОтходам(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства);
	
	// Запасы к поступлению.
	СформироватьТаблицаЗапасыКПоступлениюНаСклады(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаУправленческий(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаЗакупкиДляКУДиР(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаЗаказыПоставщикам(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицуРаспределенияМатериалов(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства, ТаблицаПродукция) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПродукция.КоррНомерСтроки КАК КоррНомерСтроки,
	|	ТаблицаПродукция.КоррЗаказПокупателя КАК КоррЗаказПокупателя,
	|	ТаблицаПродукция.КоррНоменклатура КАК КоррНоменклатура,
	|	ТаблицаПродукция.КоррХарактеристика КАК КоррХарактеристика,
	|	ТаблицаПродукция.КоррПартия КАК КоррПартия,
	|	ТаблицаПродукция.КоррСпецификация КАК КоррСпецификация,
	|	ТаблицаПродукция.КоррСчетУчета КАК КоррСчетУчета,
	|	ТаблицаПродукция.СчетУчетаПродукции КАК СчетУчетаПродукции,
	|	ТаблицаПродукция.СчетДт КАК СчетДт,
	|	ТаблицаПродукция.СчетДтПродукции КАК СчетДтПродукции,
	|	ТаблицаПродукция.СчетКтПродукции КАК СчетКтПродукции,
	|	ТаблицаПродукция.КоррКоличество КАК КоррКоличество
	|ПОМЕСТИТЬ ВременнаяТаблицаТЗ
	|ИЗ
	|	&ТаблицаПродукция КАК ТаблицаПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСоставПродукции.КоррНомерСтроки КАК КоррНомерСтроки,
	|	ТаблицаСоставПродукции.КоррЗаказПокупателя КАК КоррЗаказПокупателя,
	|	ТаблицаСоставПродукции.КоррНоменклатура КАК КоррНоменклатура,
	|	ТаблицаСоставПродукции.КоррХарактеристика КАК КоррХарактеристика,
	|	ТаблицаСоставПродукции.КоррПартия КАК КоррПартия,
	|	ТаблицаСоставПродукции.КоррСпецификация КАК КоррСпецификация,
	|	ТаблицаСоставПродукции.КоррСчетУчета КАК КоррСчетУчета,
	|	ТаблицаСоставПродукции.СчетУчетаПродукции КАК СчетУчетаПродукции,
	|	ТаблицаСоставПродукции.СчетДт КАК СчетДт,
	|	ТаблицаСоставПродукции.СчетДтПродукции КАК СчетДтПродукции,
	|	ТаблицаСоставПродукции.СчетКтПродукции КАК СчетКтПродукции,
	|	ТаблицаСоставПродукции.КоррКоличество КАК КоррКоличество,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаМатериалы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаМатериалы.Количество = 0
	|						ТОГДА 1
	|					ИНАЧЕ ТаблицаМатериалы.Количество
	|				КОНЕЦ / ТаблицаМатериалы.КоличествоПродукции * ТаблицаСоставПродукции.КоррКоличество
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаМатериалы.Количество = 0
	|					ТОГДА 1
	|				ИНАЧЕ ТаблицаМатериалы.Количество
	|			КОНЕЦ * ТаблицаМатериалы.ЕдиницаИзмерения.Коэффициент / ТаблицаМатериалы.КоличествоПродукции * ТаблицаСоставПродукции.КоррКоличество
	|	КОНЕЦ КАК ТМКоличество,
	|	ТаблицаМатериалы.ТипСтрокиСостава КАК ТМТипСтрокиСостава,
	|	ТаблицаМатериалы.Номенклатура КАК ТМНоменклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА ТаблицаМатериалы.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК ТМХарактеристика,
	|	ТаблицаМатериалы.Спецификация КАК ТМСпецификация,
	|	ЛОЖЬ КАК Распределено
	|ИЗ
	|	ВременнаяТаблицаТЗ КАК ТаблицаСоставПродукции
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Спецификации.Состав КАК ТаблицаМатериалы
	|		ПО ТаблицаСоставПродукции.КоррСпецификация = ТаблицаМатериалы.Ссылка
	|			И (ТаблицаМатериалы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас))
	|
	|УПОРЯДОЧИТЬ ПО
	|	КоррНомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СборкаЗапасовЗапасы.НомерСтроки КАК НомерСтроки,
	|	СборкаЗапасовЗапасы.КлючСвязи КАК КлючСвязи,
	|	СборкаЗапасовЗапасы.Ссылка КАК Ссылка,
	|	СборкаЗапасовЗапасы.ЗаказПокупателя КАК КоррЗаказПокупателя,
	|	СборкаЗапасовЗапасы.Ссылка.ДокументОснование КАК ДокументОснование,
	|	СборкаЗапасовЗапасы.Ссылка.Контрагент КАК Контрагент,
	|	СборкаЗапасовЗапасы.Ссылка.Договор КАК Договор,
	|	СборкаЗапасовЗапасы.Ссылка.Курс КАК Курс,
	|	СборкаЗапасовЗапасы.Ссылка.Кратность КАК Кратность,
	|	СборкаЗапасовЗапасы.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
	
	|	СборкаЗапасовЗапасы.Ссылка.Дата КАК Период,
	|	СборкаЗапасовЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	СборкаЗапасовЗапасы.ЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	|	СборкаЗапасовЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СборкаЗапасовЗапасы.Ссылка.СтруктурнаяЕдиница.ОрдерныйСклад КАК ОрдерныйСклад,
	|	ВЫБОР
	|		КОГДА &УчетПоЯчейкам
	|			ТОГДА СборкаЗапасовЗапасы.Ссылка.Ячейка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|	КОНЕЦ КАК Ячейка,
	|	ВЫБОР
	|		КОГДА СборкаЗапасовЗапасы.Ссылка.СтруктурнаяЕдиница = СборкаЗапасовЗапасы.Ссылка.СтруктурнаяЕдиница
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|		ИНАЧЕ СборкаЗапасовЗапасы.Ссылка.СтруктурнаяЕдиница
	|	КОНЕЦ КАК СтруктурнаяЕдиницаЗапасов,
	|	СборкаЗапасовЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаЗапасовНаСклад,
	|	СборкаЗапасовЗапасы.Ссылка.СтруктурнаяЕдиница.ОрдерныйСклад КАК ОрдерныйСкладЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка) КАК СтруктурнаяЕдиницаПродукции,
	|	СборкаЗапасовЗапасы.Номенклатура.СчетУчетаЗапасов КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА СборкаЗапасовЗапасы.Партия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ОтветственноеХранение)
	|				ИЛИ СборкаЗапасовЗапасы.Партия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварыНаКомиссии)
	|				ИЛИ СборкаЗапасовЗапасы.Партия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ДавальческоеСырье)
	|			ТОГДА СборкаЗапасовЗапасы.Номенклатура.СчетУчетаЗапасов
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СборкаЗапасовЗапасы.Ссылка.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|					ТОГДА СборкаЗапасовЗапасы.Номенклатура.СчетУчетаЗапасов
	|				ИНАЧЕ СборкаЗапасовЗапасы.Номенклатура.СчетУчетаЗатрат
	|			КОНЕЦ
	|	КОНЕЦ КАК СчетУчетаЗапасов,
	|	ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка) КАК КоррСчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка) КАК СчетУчетаПродукции,
	|	СборкаЗапасовЗапасы.Номенклатура КАК Номенклатура,
	|	СборкаЗапасовЗапасы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СборкаЗапасовЗапасы.НомерГТД КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК КоррНоменклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА СборкаЗапасовЗапасы.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК КоррХарактеристика,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПартии
	|			ТОГДА СборкаЗапасовЗапасы.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Партия,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПартии
	|			ТОГДА СборкаЗапасовЗапасы.Партия.Статус
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ПустаяСсылка)
	|	КОНЕЦ КАК ПартияСтатус,
	|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка) КАК КоррПартия,
	|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка) КАК КоррСпецификация,
	|	СборкаЗапасовЗапасы.Спецификация КАК Спецификация,
	|	СборкаЗапасовЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(СборкаЗапасовЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА СборкаЗапасовЗапасы.Количество
	|		ИНАЧЕ СборкаЗапасовЗапасы.Количество * СборкаЗапасовЗапасы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Количество,
	|	СборкаЗапасовЗапасы.Всего КАК Всего,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(СборкаЗапасовЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА СборкаЗапасовЗапасы.Количество
	|		ИНАЧЕ СборкаЗапасовЗапасы.Количество * СборкаЗапасовЗапасы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК ИсходноеКоличество,
	|	ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка) КАК СчетДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка) КАК СчетДтПродукции,
	|	ВЫБОР
	|		КОГДА СборкаЗапасовЗапасы.Партия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ОтветственноеХранение)
	|				ИЛИ СборкаЗапасовЗапасы.Партия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварыНаКомиссии)
	|				ИЛИ СборкаЗапасовЗапасы.Партия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ДавальческоеСырье)
	|			ТОГДА СборкаЗапасовЗапасы.Номенклатура.СчетУчетаЗапасов
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СборкаЗапасовЗапасы.Ссылка.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	|					ТОГДА СборкаЗапасовЗапасы.Номенклатура.СчетУчетаЗапасов
	|				ИНАЧЕ СборкаЗапасовЗапасы.Номенклатура.СчетУчетаЗатрат
	|			КОНЕЦ
	|	КОНЕЦ КАК СчетКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка) КАК СчетКтПродукции,
	|	ЛОЖЬ КАК Распределено
	|ИЗ
	|	Документ.ОтчетПереработчика.Запасы КАК СборкаЗапасовЗапасы
	|ГДЕ
	|	СборкаЗапасовЗапасы.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ТаблицаПродукция", ТаблицаПродукция);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетПереработчика);
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьХарактеристики);
	Запрос.УстановитьПараметр("ИспользоватьПартии", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьПартии);
	Запрос.УстановитьПараметр("УчетПоЯчейкам", СтруктураДополнительныеСвойства.УчетнаяПолитика.УчетПоЯчейкам);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаСоставПродукции = МассивРезультатов[1].Выгрузить();
	ТаблицаМатериалов = МассивРезультатов[2].Выгрузить();
	
	КоличествоПродукции = ТаблицаСоставПродукции.Количество();
	Инд = 0;
	Пока Инд < КоличествоПродукции Цикл
		СтрокаПродукции = ТаблицаСоставПродукции[Инд];
		Если СтрокаПродукции.ТМТипСтрокиСостава = Перечисления.ТипыСтрокСоставаСпецификации.Узел Тогда
			СтекСпецификацийУзлов = Новый Массив();
			ЗаполнитьТаблицуПродукцииСоставомУзлов(СтрокаПродукции, ТаблицаСоставПродукции, СтекСпецификацийУзлов);
			ТаблицаСоставПродукции.Удалить(СтрокаПродукции);
			Если Инд + 1 = КоличествоПродукции Тогда
				Прервать;
			КонецЕсли;
		Иначе
			Инд = Инд + 1;
		КонецЕсли;
	КонецЦикла;
	ИменаКолонок = Новый Массив;
	ИменаКолонок.Добавить("КоррЗаказПокупателя, КоррНоменклатура, КоррХарактеристика, КоррПартия, КоррСпецификация, КоррСчетУчета");
	ИменаКолонок.Добавить("СчетУчетаПродукции, СчетДт, СчетДтПродукции, СчетКтПродукции, КоррКоличество, ТМНоменклатура");
	ИменаКолонок.Добавить("ТМХарактеристика, Распределено");
	ТаблицаСоставПродукции.Свернуть(СтрСоединить(ИменаКолонок, ", "), "ТМКоличество");
	ТаблицаСоставПродукции.Индексы.Добавить("ТМНоменклатура, ТМХарактеристика");
	ТаблицаСоставПродукции.Индексы.Добавить("КоррЗаказПокупателя, ТМНоменклатура, ТМХарактеристика");
	
	РаспределеноМатериалов = 0;
	КоличествоПродукции = ТаблицаСоставПродукции.Количество();
	КоличествоМатериалов = ТаблицаМатериалов.Количество();
	Для н = 0 По КоличествоМатериалов - 1 Цикл
		
		СтрокаМатериалы = ТаблицаМатериалов[н];
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ТМНоменклатура", СтрокаМатериалы.Номенклатура);
		СтруктураПоиска.Вставить("ТМХарактеристика", СтрокаМатериалы.Характеристика);
		Если ЗначениеЗаполнено(СтрокаМатериалы.ЗаказПокупателя) Тогда
			СтруктураПоиска.Вставить("КоррЗаказПокупателя", СтрокаМатериалы.ЗаказПокупателя);
		КонецЕсли; 
		
		РезультатПоиска = ТаблицаСоставПродукции.НайтиСтроки(СтруктураПоиска);
		Если РезультатПоиска.Количество() <> 0 Тогда
			РаспределитьМатериалыПоНормативам(СтрокаМатериалы, РезультатПоиска, ТаблицаМатериалов);
			РаспределеноМатериалов = РаспределеноМатериалов + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	РаспределеноПродукции = 0;
	Для каждого СтрокаСоставПродукции Из ТаблицаСоставПродукции Цикл
		Если СтрокаСоставПродукции.Распределено Тогда
			РаспределеноПродукции = РаспределеноПродукции + 1;
		КонецЕсли;
	КонецЦикла;
	
	Если РаспределеноМатериалов < КоличествоМатериалов Тогда
		Если РаспределеноПродукции = КоличествоПродукции Тогда
			БазаРаспределения = ТаблицаПродукция.Итог("КоррКоличество");
			РаспределитьМатериалыПоКоличеству(ТаблицаПродукция, ТаблицаМатериалов, БазаРаспределения);
		Иначе
			РаспределитьМатериалыПоКоличеству(ТаблицаСоставПродукции, ТаблицаМатериалов);
		КонецЕсли;
	КонецЕсли;
	
	ИсходнаяТаблицаМатериалов = МассивРезультатов[2].Выгрузить();
	ВсегоПоСтрокеЗапасов = Новый Структура;
	// сохраняем исходные суммы "Всего" в структуру по строкам
	Для каждого стр Из ИсходнаяТаблицаМатериалов Цикл
		ВсегоПоСтрокеЗапасов.Вставить("НомерСтроки" + стр.НомерСтроки, стр.Всего);
	КонецЦикла;
	
	Для каждого стр Из ТаблицаМатериалов Цикл
		стр.Всего = стр.Всего*стр.Количество/стр.ИсходноеКоличество;
		ВсегоПоСтрокеЗапасов["НомерСтроки" + стр.НомерСтроки] = ВсегоПоСтрокеЗапасов["НомерСтроки" + стр.НомерСтроки] - стр.Всего;
	КонецЦикла;
	
	// разнесение остатка от округления по строкам
	Для каждого стр Из ВсегоПоСтрокеЗапасов Цикл
		Если стр.Значение <> 0 Тогда
			НомерСтроки = Число(СтрЗаменить(стр.Ключ, "НомерСтроки", ""));
			СтрокаСОстатком = ТаблицаМатериалов.Найти(НомерСтроки, "НомерСтроки");
			Если СтрокаСОстатком <> Неопределено Тогда
			
				СтрокаСОстатком.Всего = СтрокаСОстатком.Всего + стр.Значение;
			
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаПродукция = Неопределено;
	ТаблицаСоставПродукции = Неопределено;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРаспределенияМатериаловСборка", ТаблицаМатериалов);
	ТаблицаМатериалов = Неопределено;
	
КонецПроцедуры // СформироватьТаблицуРаспределенияМатериалов()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗаказыПоставщикам(ДокументСсылкаОтчетПереработчика, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗаказыПоставщикам.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗаказыПоставщикам.Период КАК Период,
	|	ТаблицаЗаказыПоставщикам.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаЗаказыПоставщикам.УчетПотребностиПоСкладам
	|			ТОГДА ТаблицаЗаказыПоставщикам.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК Склад,
	|	ТаблицаЗаказыПоставщикам.Номенклатура КАК Номенклатура,
	|	ТаблицаЗаказыПоставщикам.Характеристика КАК Характеристика,
	|	ТаблицаЗаказыПоставщикам.ЗаказПоставщику КАК ЗаказПоставщику,
	|	СУММА(ТаблицаЗаказыПоставщикам.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ТаблицаЗаказыПоставщикам
	|ГДЕ
	|	ТаблицаЗаказыПоставщикам.ЗаказПоставщику <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗаказыПоставщикам.Период,
	|	ТаблицаЗаказыПоставщикам.Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаЗаказыПоставщикам.УчетПотребностиПоСкладам
	|			ТОГДА ТаблицаЗаказыПоставщикам.СтруктурнаяЕдиница
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаЗаказыПоставщикам.Номенклатура,
	|	ТаблицаЗаказыПоставщикам.Характеристика,
	|	ТаблицаЗаказыПоставщикам.ЗаказПоставщику";
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЗаказыПоставщикам = РезультатЗапроса.Выгрузить();
	РегистрыНакопления.ЗаказыПоставщикам.ДобавитьДвиженияСУчетомСкладов(ДокументСсылкаОтчетПереработчика, ТаблицаЗаказыПоставщикам);
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗаказыПоставщикам", ТаблицаЗаказыПоставщикам);
	
КонецПроцедуры // СформироватьТаблицаЗаказыПоставщикам()

// Процедура распределяет материалы по спецификациям продукции.
//
Процедура РаспределитьМатериалыПоНормативам(СтрокаМатериалы, БазоваяТаблица, ТаблицаМатериалов)
	
	СтрокаМатериалы.Распределено = Истина;
	
	БазаРаспределения = 0;
	Для каждого СтрокаБазы Из БазоваяТаблица Цикл
		БазаРаспределения = БазаРаспределения + СтрокаБазы.ТМКоличество;
		СтрокаБазы.Распределено = Истина;
	КонецЦикла;
	
	РаспределитьСтрокуТабличнойЧастиМатериалы(СтрокаМатериалы, БазоваяТаблица, ТаблицаМатериалов, БазаРаспределения, Истина);
	
КонецПроцедуры // РаспределитьМатериалыПоНормативам()

// Процедура распределяет материалы пропорционально количеству продукции.
//
Процедура РаспределитьМатериалыПоКоличеству(БазоваяТаблица, ТаблицаМатериалов, БазаРаспределения = 0)
	
	ИсключатьРаспределенные = Ложь;
	Если БазаРаспределения = 0 Тогда
		ИсключатьРаспределенные = Истина;
		Для каждого СтрокаБазы Из БазоваяТаблица Цикл
			Если НЕ СтрокаБазы.Распределено Тогда
				БазаРаспределения = БазаРаспределения + СтрокаБазы.КоррКоличество;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для н = 0 По ТаблицаМатериалов.Количество() - 1 Цикл
		
		СтрокаМатериалы = ТаблицаМатериалов[н];
		
		Если НЕ СтрокаМатериалы.Распределено И ЗначениеЗаполнено(СтрокаМатериалы.ЗаказПокупателя) Тогда
			// Если заполнен заказ покупателя - в первую очередь распределяем материал на продукцию с таким же заказом
			СтрокиПоЗаказу = Новый Массив;
			БазаРаспределенияПоЗаказу = 0;
			Для каждого СтрокаБазы Из БазоваяТаблица Цикл
				Если НЕ СтрокаБазы.Распределено И СтрокаБазы.КоррЗаказПокупателя = СтрокаМатериалы.ЗаказПокупателя Тогда
					СтрокиПоЗаказу.Добавить(СтрокаБазы);
					БазаРаспределенияПоЗаказу = БазаРаспределенияПоЗаказу + СтрокаБазы.КоррКоличество;
				КонецЕсли; 
			КонецЦикла;
			Если СтрокиПоЗаказу.Количество() > 0 Тогда
				РаспределитьСтрокуТабличнойЧастиМатериалы(СтрокаМатериалы, СтрокиПоЗаказу, ТаблицаМатериалов, БазаРаспределения, Ложь, ИсключатьРаспределенные);
			КонецЕсли; 
		КонецЕсли; 
		
		Если НЕ СтрокаМатериалы.Распределено Тогда
			РаспределитьСтрокуТабличнойЧастиМатериалы(СтрокаМатериалы, БазоваяТаблица, ТаблицаМатериалов, БазаРаспределения, Ложь, ИсключатьРаспределенные);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // РаспределитьМатериалыПоКоличеству()

// Процедура распределяет строку материалов.
//
Процедура РаспределитьСтрокуТабличнойЧастиМатериалы(СтрокаМатериалы, БазоваяТаблица, ТаблицаМатериалов, БазаРаспределения, ПоНормативам, ИсключатьРаспределенные = Ложь)
	
	ИсхКоличество = 0;
	КоличествоКСписанию = СтрокаМатериалы.Количество;
	БазаРаспределенияКоличество = БазаРаспределения;
	
	Для каждого СтрокаБазовойТаблицы Из БазоваяТаблица Цикл
		
		Если ИсключатьРаспределенные И СтрокаБазовойТаблицы.Распределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИсхКоличество = КоличествоКСписанию Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаМатериалы.КоррНоменклатура) Тогда
			НоваяСтрока = ТаблицаМатериалов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаМатериалы);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаБазовойТаблицы);
			СтрокаМатериалы = НоваяСтрока;
		Иначе
			ЗаполнитьЗначенияСвойств(СтрокаМатериалы, СтрокаБазовойТаблицы);
		КонецЕсли;
		
		Если ПоНормативам Тогда
			КоличествоБазовойТаблицы = СтрокаБазовойТаблицы.ТМКоличество;
		Иначе
			КоличествоБазовойТаблицы = СтрокаБазовойТаблицы.КоррКоличество
		КонецЕсли;
		
		// Количество.
		СтрокаМатериалы.Количество = Окр((КоличествоКСписанию - ИсхКоличество) * КоличествоБазовойТаблицы / БазаРаспределенияКоличество, 3, 1);
		
		Если (ИсхКоличество + СтрокаМатериалы.Количество) > КоличествоКСписанию Тогда
			СтрокаМатериалы.Количество = КоличествоКСписанию - ИсхКоличество;
			ИсхКоличество = КоличествоКСписанию;
		Иначе
			БазаРаспределенияКоличество = БазаРаспределенияКоличество - КоличествоБазовойТаблицы;
			ИсхКоличество = ИсхКоличество + СтрокаМатериалы.Количество;
		КонецЕсли;
		
		СтрокаМатериалы.Распределено = Истина;
		
	КонецЦикла;
	
	Если ИсхКоличество < КоличествоКСписанию Тогда
		СтрокаМатериалы.Количество = СтрокаМатериалы.Количество + (КоличествоКСписанию - ИсхКоличество);
	КонецЕсли;
	
КонецПроцедуры // РаспределитьСтрокуТабличнойЧастиМатериалы()

// Процедура формирует состав узлов.
//
Процедура ЗаполнитьТаблицуПродукцииСоставомУзлов(СтрокаПродукция, ТаблицаПродукция, СтекСпецификацийУзлов)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаМатериалы.НомерСтроки) КАК НомерСтрокиСостава,
	|	ТаблицаМатериалы.ТипСтрокиСостава КАК ТипСтрокиСостава,
	|	ТаблицаМатериалы.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ИспользоватьХарактеристики.Значение
	|			ТОГДА ТаблицаМатериалы.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	СУММА(ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(ТаблицаМатериалы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|				ТОГДА ТаблицаМатериалы.Количество / ТаблицаМатериалы.КоличествоПродукции * &КоличествоПродукции
	|			ИНАЧЕ ТаблицаМатериалы.Количество * ТаблицаМатериалы.ЕдиницаИзмерения.Коэффициент / ТаблицаМатериалы.КоличествоПродукции * &КоличествоПродукции
	|		КОНЕЦ) КАК НормаРасхода,
	|	ТаблицаМатериалы.Спецификация КАК Спецификация
	|ИЗ
	|	Справочник.Спецификации.Состав КАК ТаблицаМатериалы,
	|	Константа.ФункциональнаяОпцияИспользоватьХарактеристики КАК ИспользоватьХарактеристики
	|ГДЕ
	|	ТаблицаМатериалы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|	И ТаблицаМатериалы.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаМатериалы.ТипСтрокиСостава,
	|	ТаблицаМатериалы.Номенклатура,
	|	ТаблицаМатериалы.ДоляСтоимости,
	|	ТаблицаМатериалы.Спецификация,
	|	ВЫБОР
	|		КОГДА ИспользоватьХарактеристики.Значение
	|			ТОГДА ТаблицаМатериалы.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиСостава";
	
	Запрос.УстановитьПараметр("Ссылка", СтрокаПродукция.ТМСпецификация);
	Запрос.УстановитьПараметр("КоличествоПродукции", СтрокаПродукция.ТМКоличество);
	
	СтекСпецификацийУзлов.Добавить(СтрокаПродукция.ТМСпецификация);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ТипСтрокиСостава = Перечисления.ТипыСтрокСоставаСпецификации.Узел Тогда
			Если НЕ СтекСпецификацийУзлов.Найти(Выборка.Спецификация) = Неопределено Тогда
				ТекстСообщения = НСтр("ru = 'Обнаружено рекурсивное вхождение элемента'")+" "+Выборка.Номенклатура+" "+НСтр("ru = 'в спецификации'")+" "+СтрокаПродукция.КоррСпецификация+"
									|Операция не выполнена!";
				ВызватьИсключение ТекстСообщения;
			КонецЕсли;
			СтекСпецификацийУзлов.Добавить(Выборка.Спецификация);
			СтрокаПродукция.ТМКоличество = Выборка.НормаРасхода;
			СтрокаПродукция.ТМСпецификация = Выборка.Спецификация;
			ЗаполнитьТаблицуПродукцииСоставомУзлов(СтрокаПродукция, ТаблицаПродукция, СтекСпецификацийУзлов);
		Иначе
			НоваяСтрока = ТаблицаПродукция.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПродукция);
			НоваяСтрока.ТМТипСтрокиСостава = Выборка.ТипСтрокиСостава;
			НоваяСтрока.ТМНоменклатура = Выборка.Номенклатура;
			НоваяСтрока.ТМХарактеристика = Выборка.Характеристика;
			НоваяСтрока.ТМКоличество = Выборка.НормаРасхода;
			НоваяСтрока.ТМСпецификация = Выборка.Спецификация;
		КонецЕсли;
	КонецЦикла;
	
	СтекСпецификацийУзлов.Очистить();
	
КонецПроцедуры // ЗаполнитьТаблицуПродукцииСоставомУзлов()

// Выполняет контроль возникновения отрицательных остатков.
//
Процедура ВыполнитьКонтроль(ДокументСсылкаОтчетПереработчика, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт
	
	Если ПроведениеДокументовУНФ.КонтрольОстатковВыключен() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Если временные таблицы "ДвиженияЗапасыИзменение", "ДвиженияЗапасыНаСкладахИзменение", 
	// "ДвиженияЗапасыПереданныеИзменение", "ДвиженияЗаказыПоставщикамИзменение", 
	// "ДвиженияРазмещениеЗаказовИзменение", содержат записи, необходимо выполнить контроль 
	// реализации товаров.
	Если СтруктураВременныеТаблицы.ДвиженияЗапасыИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗапасыНаСкладахИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗапасыПереданныеИзменение 
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗаказыПоставщикамИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияРазмещениеЗаказовИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияРасчетыСПоставщикамиИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗапасыВРазрезеГТДИзменение
		Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияЗапасыНаСкладахИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыНаСкладахИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница) КАК СтруктурнаяЕдиницаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыНаСкладахИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыНаСкладахИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыНаСкладахИзменение.Партия) КАК ПартияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыНаСкладахИзменение.Ячейка) КАК ЯчейкаПредставление,
		|	ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗапасыНаСкладахОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыНаСкладахИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыНаСкладах,
		|	ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыНаСкладах
		|ИЗ
		|	ДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыНаСкладах.Остатки(
		|				&МоментКонтроля,
		|				(Организация, СтруктурнаяЕдиница, Номенклатура, Характеристика, Партия, Ячейка) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыНаСкладахИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|						ДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыНаСкладахИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыНаСкладахИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыНаСкладахИзменение.Ячейка КАК Ячейка
		|					ИЗ
		|						ДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение)) КАК ЗапасыНаСкладахОстатки
		|		ПО ДвиженияЗапасыНаСкладахИзменение.Организация = ЗапасыНаСкладахОстатки.Организация
		|			И ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница = ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница
		|			И ДвиженияЗапасыНаСкладахИзменение.Номенклатура = ЗапасыНаСкладахОстатки.Номенклатура
		|			И ДвиженияЗапасыНаСкладахИзменение.Характеристика = ЗапасыНаСкладахОстатки.Характеристика
		|			И ДвиженияЗапасыНаСкладахИзменение.Партия = ЗапасыНаСкладахОстатки.Партия
		|			И ДвиженияЗапасыНаСкладахИзменение.Ячейка = ЗапасыНаСкладахОстатки.Ячейка
		|ГДЕ
		|	ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыИзменение.СтруктурнаяЕдиница) КАК СтруктурнаяЕдиницаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыИзменение.СчетУчета) КАК СчетУчетаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыИзменение.Партия) КАК ПартияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыИзменение.ЗаказПокупателя) КАК ЗаказПокупателяПредставление,
		|	ЗапасыОстатки.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗапасыОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасы,
		|	ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасы,
		|	ЕСТЬNULL(ЗапасыОстатки.СуммаОстаток, 0) КАК СуммаОстатокЗапасы
		|ИЗ
		|	ДвиженияЗапасыИзменение КАК ДвиженияЗапасыИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Запасы.Остатки(
		|				&МоментКонтроля,
		|				(Организация, СтруктурнаяЕдиница, СчетУчета, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|						ДвиженияЗапасыИзменение.СчетУчета КАК СчетУчета,
		|						ДвиженияЗапасыИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыИзменение.ЗаказПокупателя КАК ЗаказПокупателя
		|					ИЗ
		|						ДвиженияЗапасыИзменение КАК ДвиженияЗапасыИзменение)) КАК ЗапасыОстатки
		|		ПО ДвиженияЗапасыИзменение.Организация = ЗапасыОстатки.Организация
		|			И ДвиженияЗапасыИзменение.СтруктурнаяЕдиница = ЗапасыОстатки.СтруктурнаяЕдиница
		|			И ДвиженияЗапасыИзменение.СчетУчета = ЗапасыОстатки.СчетУчета
		|			И ДвиженияЗапасыИзменение.Номенклатура = ЗапасыОстатки.Номенклатура
		|			И ДвиженияЗапасыИзменение.Характеристика = ЗапасыОстатки.Характеристика
		|			И ДвиженияЗапасыИзменение.Партия = ЗапасыОстатки.Партия
		|			И ДвиженияЗапасыИзменение.ЗаказПокупателя = ЗапасыОстатки.ЗаказПокупателя
		|ГДЕ
		|	ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыПереданныеИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.Партия) КАК ПартияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.Контрагент) КАК КонтрагентПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.Договор) КАК ДоговорПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.Заказ) КАК ЗаказПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.ТипПриемаПередачи) КАК ТипПриемаПередачиПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗапасыПереданныеОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыПереданныеИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыПереданныеОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыПереданные,
		|	ЕСТЬNULL(ЗапасыПереданныеОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыПереданные,
		|	ЕСТЬNULL(ДвиженияЗапасыПереданныеИзменение.СуммаРасчетовИзменение, 0) + ЕСТЬNULL(ЗапасыПереданныеОстатки.СуммаРасчетовОстаток, 0) КАК СуммаРасчетовЗапасыПереданные,
		|	ЕСТЬNULL(ЗапасыПереданныеОстатки.СуммаРасчетовОстаток, 0) КАК СуммаРасчетовОстатокЗапасыПереданные
		|ИЗ
		|	ДвиженияЗапасыПереданныеИзменение КАК ДвиженияЗапасыПереданныеИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыПереданные.Остатки(
		|				&МоментКонтроля,
		|				(Организация, Номенклатура, Характеристика, Партия, Контрагент, Договор, Заказ, ТипПриемаПередачи) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыПереданныеИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыПереданныеИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыПереданныеИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыПереданныеИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыПереданныеИзменение.Контрагент КАК Контрагент,
		|						ДвиженияЗапасыПереданныеИзменение.Договор КАК Договор,
		|						ДвиженияЗапасыПереданныеИзменение.Заказ КАК Заказ,
		|						ДвиженияЗапасыПереданныеИзменение.ТипПриемаПередачи КАК ТипПриемаПередачи
		|					ИЗ
		|						ДвиженияЗапасыПереданныеИзменение КАК ДвиженияЗапасыПереданныеИзменение)) КАК ЗапасыПереданныеОстатки
		|		ПО ДвиженияЗапасыПереданныеИзменение.Организация = ЗапасыПереданныеОстатки.Организация
		|			И ДвиженияЗапасыПереданныеИзменение.Номенклатура = ЗапасыПереданныеОстатки.Номенклатура
		|			И ДвиженияЗапасыПереданныеИзменение.Характеристика = ЗапасыПереданныеОстатки.Характеристика
		|			И ДвиженияЗапасыПереданныеИзменение.Партия = ЗапасыПереданныеОстатки.Партия
		|			И ДвиженияЗапасыПереданныеИзменение.Контрагент = ЗапасыПереданныеОстатки.Контрагент
		|			И ДвиженияЗапасыПереданныеИзменение.Договор = ЗапасыПереданныеОстатки.Договор
		|			И ДвиженияЗапасыПереданныеИзменение.Заказ = ЗапасыПереданныеОстатки.Заказ
		|			И ДвиженияЗапасыПереданныеИзменение.ТипПриемаПередачи = ЗапасыПереданныеОстатки.ТипПриемаПередачи
		|ГДЕ
		|	(ЕСТЬNULL(ЗапасыПереданныеОстатки.КоличествоОстаток, 0) < 0
		|			ИЛИ ЕСТЬNULL(ЗапасыПереданныеОстатки.СуммаРасчетовОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗаказыПоставщикамИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗаказыПоставщикамИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗаказыПоставщикамИзменение.Склад) КАК СкладПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗаказыПоставщикамИзменение.ЗаказПоставщику) КАК ЗаказПоставщикуПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗаказыПоставщикамИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗаказыПоставщикамИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗаказыПоставщикамОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗаказыПоставщикамИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗаказыПоставщикамОстатки.КоличествоОстаток, 0) КАК ОстатокЗаказыПоставщикам,
		|	ЕСТЬNULL(ЗаказыПоставщикамОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗаказыПоставщикам
		|ИЗ
		|	ДвиженияЗаказыПоставщикамИзменение КАК ДвиженияЗаказыПоставщикамИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПоставщикам.Остатки(
		|				&МоментКонтроля,
		|				(Организация, Склад, ЗаказПоставщику, Номенклатура, Характеристика) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗаказыПоставщикамИзменение.Организация КАК Организация,
		|						ДвиженияЗаказыПоставщикамИзменение.Склад КАК Склад,
		|						ДвиженияЗаказыПоставщикамИзменение.ЗаказПоставщику КАК ЗаказПоставщику,
		|						ДвиженияЗаказыПоставщикамИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗаказыПоставщикамИзменение.Характеристика КАК Характеристика
		|					ИЗ
		|						ДвиженияЗаказыПоставщикамИзменение КАК ДвиженияЗаказыПоставщикамИзменение)) КАК ЗаказыПоставщикамОстатки
		|		ПО ДвиженияЗаказыПоставщикамИзменение.Организация = ЗаказыПоставщикамОстатки.Организация
		|			И ДвиженияЗаказыПоставщикамИзменение.Склад = ЗаказыПоставщикамОстатки.Склад
		|			И ДвиженияЗаказыПоставщикамИзменение.ЗаказПоставщику = ЗаказыПоставщикамОстатки.ЗаказПоставщику
		|			И ДвиженияЗаказыПоставщикамИзменение.Номенклатура = ЗаказыПоставщикамОстатки.Номенклатура
		|			И ДвиженияЗаказыПоставщикамИзменение.Характеристика = ЗаказыПоставщикамОстатки.Характеристика
		|ГДЕ
		|	ЕСТЬNULL(ЗаказыПоставщикамОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияРазмещениеЗаказовИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРазмещениеЗаказовИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРазмещениеЗаказовИзменение.ЗаказПокупателя) КАК ЗаказПокупателяПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРазмещениеЗаказовИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРазмещениеЗаказовИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРазмещениеЗаказовИзменение.ИсточникОбеспечения) КАК ИсточникОбеспеченияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(РазмещениеЗаказовОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияРазмещениеЗаказовИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(РазмещениеЗаказовОстатки.КоличествоОстаток, 0) КАК ОстатокРазмещениеЗаказов,
		|	ЕСТЬNULL(РазмещениеЗаказовОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокРазмещениеЗаказов
		|ИЗ
		|	ДвиженияРазмещениеЗаказовИзменение КАК ДвиженияРазмещениеЗаказовИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РазмещениеЗаказов.Остатки(
		|				&МоментКонтроля,
		|				(Организация, ЗаказПокупателя, Номенклатура, Характеристика, ИсточникОбеспечения) В
		|					(ВЫБРАТЬ
		|						ДвиженияРазмещениеЗаказовИзменение.Организация КАК Организация,
		|						ДвиженияРазмещениеЗаказовИзменение.ЗаказПокупателя КАК ЗаказПокупателя,
		|						ДвиженияРазмещениеЗаказовИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияРазмещениеЗаказовИзменение.Характеристика КАК Характеристика,
		|						ДвиженияРазмещениеЗаказовИзменение.ИсточникОбеспечения КАК ИсточникОбеспечения
		|					ИЗ
		|						ДвиженияРазмещениеЗаказовИзменение КАК ДвиженияРазмещениеЗаказовИзменение)) КАК РазмещениеЗаказовОстатки
		|		ПО ДвиженияРазмещениеЗаказовИзменение.Организация = РазмещениеЗаказовОстатки.Организация
		|			И ДвиженияРазмещениеЗаказовИзменение.ЗаказПокупателя = РазмещениеЗаказовОстатки.ЗаказПокупателя
		|			И ДвиженияРазмещениеЗаказовИзменение.Номенклатура = РазмещениеЗаказовОстатки.Номенклатура
		|			И ДвиженияРазмещениеЗаказовИзменение.Характеристика = РазмещениеЗаказовОстатки.Характеристика
		|			И ДвиженияРазмещениеЗаказовИзменение.ИсточникОбеспечения = РазмещениеЗаказовОстатки.ИсточникОбеспечения
		|ГДЕ
		|	ЕСТЬNULL(РазмещениеЗаказовОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияРасчетыСПоставщикамиИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРасчетыСПоставщикамиИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРасчетыСПоставщикамиИзменение.Контрагент) КАК КонтрагентПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРасчетыСПоставщикамиИзменение.Договор) КАК ДоговорПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРасчетыСПоставщикамиИзменение.Договор.ВалютаРасчетов) КАК ВалютаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРасчетыСПоставщикамиИзменение.Документ) КАК ДокументПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРасчетыСПоставщикамиИзменение.Заказ) КАК ЗаказПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов) КАК ТипРасчетовПредставление,
		|	ЛОЖЬ КАК ДвиженияДенежныхДокументов,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалИзменение КАК СуммаВалИзменение,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалПриЗаписи - ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) КАК СуммаВыданныхАвансов,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалИзменение + ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) КАК СуммаНепогашеннойЗадолженности,
		|	ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
		|	ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов КАК ТипРасчетов
		|ИЗ
		|	ДвиженияРасчетыСПоставщикамиИзменение КАК ДвиженияРасчетыСПоставщикамиИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками.Остатки(
		|				&МоментКонтроля,
		|				(Организация, Контрагент, Договор, Документ, Заказ, ТипРасчетов) В
		|					(ВЫБРАТЬ
		|						ДвиженияРасчетыСПоставщикамиИзменение.Организация КАК Организация,
		|						ДвиженияРасчетыСПоставщикамиИзменение.Контрагент КАК Контрагент,
		|						ДвиженияРасчетыСПоставщикамиИзменение.Договор КАК Договор,
		|						ДвиженияРасчетыСПоставщикамиИзменение.Документ КАК Документ,
		|						ДвиженияРасчетыСПоставщикамиИзменение.Заказ КАК Заказ,
		|						ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов КАК ТипРасчетов
		|					ИЗ
		|						ДвиженияРасчетыСПоставщикамиИзменение КАК ДвиженияРасчетыСПоставщикамиИзменение)) КАК РасчетыСПоставщикамиОстатки
		|		ПО ДвиженияРасчетыСПоставщикамиИзменение.Организация = РасчетыСПоставщикамиОстатки.Организация
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Контрагент = РасчетыСПоставщикамиОстатки.Контрагент
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Договор = РасчетыСПоставщикамиОстатки.Договор
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Документ = РасчетыСПоставщикамиОстатки.Документ
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Заказ = РасчетыСПоставщикамиОстатки.Заказ
		|			И ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов = РасчетыСПоставщикамиОстатки.ТипРасчетов
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
		|				ТОГДА ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) > 0
		|			ИНАЧЕ ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) < 0
		|		КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыВРазрезеГТДИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД) КАК НомерГТДПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Партия) КАК ПартияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения) КАК СтранаПроисхожденияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗапасыВРазрезеГТДОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыВРазрезеГТДИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыВРазрезеГТД,
		|	ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыВРазрезеГТД
		|ИЗ
		|	ДвиженияЗапасыВРазрезеГТДИзменение КАК ДвиженияЗапасыВРазрезеГТДИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыВРазрезеГТД.Остатки(
		|				&МоментКонтроля,
		|				(Организация, НомерГТД, Номенклатура, Характеристика, Партия, СтранаПроисхождения) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД КАК НомерГТД,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения КАК СтранаПроисхождения
		|					ИЗ
		|						ДвиженияЗапасыВРазрезеГТДИзменение КАК ДвиженияЗапасыВРазрезеГТДИзменение)) КАК ЗапасыВРазрезеГТДОстатки
		|		ПО ДвиженияЗапасыВРазрезеГТДИзменение.Организация = ЗапасыВРазрезеГТДОстатки.Организация
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД = ЗапасыВРазрезеГТДОстатки.НомерГТД
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура = ЗапасыВРазрезеГТДОстатки.Номенклатура
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика = ЗапасыВРазрезеГТДОстатки.Характеристика
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Партия = ЗапасыВРазрезеГТДОстатки.Партия
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения = ЗапасыВРазрезеГТДОстатки.СтранаПроисхождения
		|ГДЕ
		|	ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки");
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("МоментКонтроля", ДополнительныеСвойства.ДляПроведения.МоментКонтроля);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		Если НЕ МассивРезультатов[0].Пустой()
			ИЛИ НЕ МассивРезультатов[1].Пустой()
			ИЛИ НЕ МассивРезультатов[2].Пустой()
			ИЛИ НЕ МассивРезультатов[3].Пустой()
			ИЛИ НЕ МассивРезультатов[4].Пустой()
			ИЛИ НЕ МассивРезультатов[5].Пустой()
			ИЛИ НЕ МассивРезультатов[6].Пустой()
			Тогда
			
			ДокументОбъектОтчетПереработчика = ДокументСсылкаОтчетПереработчика.ПолучитьОбъект();
			
		КонецЕсли;
		
		// Отрицательный остаток запасов на складе.
		Если НЕ МассивРезультатов[0].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[0].Выбрать();
			КонтрольОстатковУНФ.ЗапасыНаСкладах(ДокументОбъектОтчетПереработчика, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток учета запасов.
		Если НЕ МассивРезультатов[1].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[1].Выбрать();
			КонтрольОстатковУНФ.Запасы(ДокументОбъектОтчетПереработчика, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток запасов переданных.
		Если НЕ МассивРезультатов[2].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[2].Выбрать();
			КонтрольОстатковУНФ.ЗапасыПереданные(ДокументОбъектОтчетПереработчика, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по заказу поставщику.
		Если НЕ МассивРезультатов[3].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[3].Выбрать();
			КонтрольОстатковУНФ.ЗаказыПоставщикам(
				ДокументОбъектОтчетПереработчика, ВыборкаИзРезультатаЗапроса, "", Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по размещению запасов.
		Если НЕ МассивРезультатов[4].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[4].Выбрать();
			КонтрольОстатковУНФ.РазмещениеЗаказов(ДокументОбъектОтчетПереработчика, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по расчетам с поставщиками.
		Если НЕ МассивРезультатов[5].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[5].Выбрать();
			КонтрольОстатковУНФ.РасчетыСПоставщиками(ДокументОбъектОтчетПереработчика, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по остаткам запасов в разрезе номеров ГТД.
		Если Константы.КонтролироватьОстаткиПоНомерамГТД.Получить()
			И НЕ МассивРезультатов[6].Пустой() Тогда
			
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[6].Выбрать();
			КонтрольОстатковУНФ.ЗапасыВРазрезеГТД(ДокументОбъектОтчетПереработчика, ВыборкаИзРезультатаЗапроса, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтроль()

#КонецОбласти

#Область ИнтерфейсПечати

Функция ДанныеПолученныхДокументовРегУчет(МассивОбъектов, ИспользоватьФаксимиле, ПечатнаяФормаТолькоВРублях = Истина, Ошибки = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ИспользоватьФаксимиле", ИспользоватьФаксимиле);
	Запрос.УстановитьПараметр("ПечатнаяФормаТолькоВРублях", ПечатнаяФормаТолькоВРублях);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтчетПереработчика.Ссылка КАК Ссылка
	|	,Неопределено КАК ДатаДокумента
	|	,Неопределено КАК Номер
	|	,Неопределено КАК ЦифровойИндексОбособленногоПодразделения
	|	,Неопределено КАК НомерИсправления
	|	,Неопределено КАК ДатаИсправления
	|	,Неопределено КАК ФаксимилеПустая
	|	,Значение(Перечисление.ВидыОперацийСчетФактура.Продажа) КАК ВидОперации
	|	,Выбор КОГДА ОтчетПереработчика.Контрагент.ГоловнойКонтрагент <> Значение(Справочник.Контрагенты.ПустаяСсылка)
	|		ТОГДА ОтчетПереработчика.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ ОтчетПереработчика.Контрагент КОНЕЦ КАК Организация
	|	,ОтчетПереработчика.Контрагент КАК ОбособленноеПодразделениеПоставщика
	|	,ОтчетПереработчика.Контрагент КАК Грузоотправитель
	|	,Неопределено КАК Префикс
	|	,Неопределено КАК Логотип
	|	,Неопределено КАК ФаксимилеПечати
	|	,Выбор КОГДА &ИспользоватьФаксимиле = ИСТИНА
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ДаНет.Да)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ДаНет.Нет) КОНЕЦ КАК ИспользоватьФаксимиле
	|	,Выбор КОГДА ОтчетПереработчика.Контрагент.ВидКонтрагента = Значение(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ КОНЕЦ КАК ЭтоФизическоеЛицо
	|	,Выбор КОГДА ОтчетПереработчика.НалогообложениеНДС = Значение(Перечисление.ТипыНалогообложенияНДС.НеОблагаетсяНДС)
	|		ТОГДА 2
	|		ИНАЧЕ 1 КОНЕЦ КАК СтатусУПД
	|	,Ложь КАК ЭтоСводныйСчетФактура
	|	,Ложь КАК ЭтоКорректировка
	|	,ОтчетПереработчика.Контрагент.БанковскийСчетПоУмолчанию КАК БанковскийСчет
	|	,Неопределено КАК ПредставлениеПодразделения
	|	,Неопределено КАК ПредставлениеСкладаСписания
	|	,Выбор КОГДА ОтчетПереработчика.Организация.ГоловнаяОрганизация <> Значение(Справочник.Организации.ПустаяСсылка)
	|		ТОГДА ОтчетПереработчика.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ ОтчетПереработчика.Организация КОНЕЦ КАК Контрагент
	|	,ОтчетПереработчика.Организация КАК ОбособленноеПодразделениеПокупателя
	|	,ОтчетПереработчика.Организация КАК Грузополучатель
	|	,ОтчетПереработчика.Организация.БанковскийСчетПоУмолчанию КАК БанковскийСчетКонтрагента
	|	,Неопределено КАК АдресДоставки
	|	,ОтчетПереработчика.Организация.ПодписьРуководителя.РасшифровкаПодписи КАК РасшифровкаПодписиКонтрагента
	|	,Неопределено КАК ДоверенностьНомер
	|	,Неопределено КАК ДоверенностьДата
	|	,Неопределено КАК ДоверенностьВыдана
	|	,Неопределено КАК ДоверенностьЛицо
	|	,ОтчетПереработчика.Договор.Представление КАК Основание
	|	,ОтчетПереработчика.Договор.Представление КАК ПредставлениеОснования
	|	,ОтчетПереработчика.Договор КАК ОснованиеПечатиСсылка
	|	,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА 
	|		ТОГДА НациональнаяВалюта.Значение
	|		ИНАЧЕ ОтчетПереработчика.ВалютаДокумента КОНЕЦ КАК ВалютаДокумента
	|	,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА 
	|		ТОГДА НациональнаяВалюта.Значение.НаименованиеПолное
	|		ИНАЧЕ ОтчетПереработчика.ВалютаДокумента.НаименованиеПолное КОНЕЦ КАК ВалютаНаименование
	|	,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА 
	|		ТОГДА НациональнаяВалюта.Значение.Код
	|		ИНАЧЕ ОтчетПереработчика.ВалютаДокумента.Код КОНЕЦ КАК ВалютаКод
	|	,ОтчетПереработчика.СуммаВключаетНДС КАК СуммаВключаетНДС
	|	,ОтчетПереработчика.НДСВключатьВСтоимость КАК НДСВключатьВСтоимость
	|	,ОтчетПереработчика.Курс КАК Курс
	|	,ОтчетПереработчика.Кратность КАК Кратность
	|	,ОтчетПереработчика.Договор.НомерДоговора КАК ОснованиеНомер
	|	,ОтчетПереработчика.Договор.ДатаДоговора КАК ОснованиеДата
	|	,Неопределено КАК ТранспортнаяНакладнаяНомер
	|	,Неопределено КАК ТранспортнаяНакладнаяДата
	|	,Неопределено КАК ДолжностьРуководителя
	|	,Неопределено КАК РасшифровкаПодписиРуководителя
	|	,Неопределено КАК ФаксимилеРуководителя
	|	,Неопределено КАК РасшифровкаПодписиГлавногоБухгалтера
	|	,Неопределено КАК ФаксимилеГлавногоБухгалтера
	|	,Неопределено КАК ДолжностьКладовщика
	|	,Неопределено КАК РасшифровкаПодписиКладовщика
	|	,Неопределено КАК ФаксимилеКладовщика
	|	,0 КАК Вес
	|	,0 КАК Объем
	|	,Неопределено КАК НоменклатураДоставки
	|	,Неопределено КАК ПредставлениеНоменклатурыДоставки
	|	,Неопределено КАК КодНоменклатурыДоставки
	|	,Неопределено КАК АртикулНоменклатурыДоставки
	|	,Неопределено КАК СтоимостьДоставки
	|	,Неопределено КАК СтавкаНДСДоставки
	|	,Неопределено КАК СуммаНДСДоставки
	|	,Неопределено КАК ИдентификаторГосКонтракта
	|
	// ::: Табличная часть Расходы
	|	,ПоложениеРасходов КАК ПоложениеРасходов
	|	,ОтчетПереработчика.Расходы.(
	|		НомерСтроки КАК НомерСтроки
	|		,Расход КАК Номенклатура
	|		,Неопределено КАК Содержание
	|		,Выбор КОГДА ВЫРАЗИТЬ(ОтчетПереработчика.Расходы.Расход.НаименованиеПолное КАК СТРОКА(1000)) = """"
	|			ТОГДА ОтчетПереработчика.Расходы.Расход.Наименование
	|			ИНАЧЕ ВЫРАЗИТЬ(ОтчетПереработчика.Расходы.Расход.НаименованиеПолное КАК СТРОКА(1000)) КОНЕЦ КАК ПредставлениеНоменклатуры
	|		,Значение(Перечисление.ТипыНоменклатуры.Услуга) КАК ТипНоменклатуры
	|		,Расход.Код КАК ЗапасКод
	|		,Расход.Код КАК Код
	|		,Расход.Артикул КАК Артикул
	|		,Расход.ТоварнаяНоменклатураВЭД.Код КАК КодТНВЭД
	|		,Расход.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|		,Расход.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияПоОКЕИ_Наименование
	|		,Расход.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияПоОКЕИ_Код
	|		,Неопределено КАК Характеристика
	|		,Неопределено КАК ВидУпаковки
	|		,1 КАК КоэффициентЕдиницыИзмерения
	|		,1 КАК Количество
	|		,Неопределено КАК КоличествоВОдномМесте
	|		,Неопределено КАК КоличествоМест
	|		,СтавкаНДС КАК СтавкаНДС
	|		,0 КАК МассаБрутто
	|		,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|				ИЛИ (Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА 
	|						И Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение)
	|			ТОГДА ВЫРАЗИТЬ(
	|				ВЫБОР КОГДА ОтчетПереработчика.СуммаВключаетНДС = ИСТИНА И ОтчетПереработчика.Расходы.СтавкаНДС <> Значение(Справочник.СтавкиНДС.ПустаяСсылка)
	|					ТОГДА ОтчетПереработчика.Расходы.Сумма / ((ОтчетПереработчика.Расходы.СтавкаНДС.Ставка + 100) / 100)
	|					ИНАЧЕ ОтчетПереработчика.Расходы.Сумма КОНЕЦ * ОтчетПереработчика.Ссылка.Курс / ОтчетПереработчика.Ссылка.Кратность
	|			КАК Число(15, 2)) 
	|			ИНАЧЕ ВЫРАЗИТЬ(
	|				ВЫБОР КОГДА ОтчетПереработчика.СуммаВключаетНДС = ИСТИНА И ОтчетПереработчика.Расходы.СтавкаНДС <> Значение(Справочник.СтавкиНДС.ПустаяСсылка)
	|					ТОГДА ОтчетПереработчика.Расходы.Сумма / ((ОтчетПереработчика.Расходы.СтавкаНДС.Ставка + 100) / 100)
	|					ИНАЧЕ ОтчетПереработчика.Расходы.Сумма КОНЕЦ
	|			КАК Число(15, 2)) КОНЕЦ КАК Цена
	|		,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|				ИЛИ (Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА 
	|						И Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение)
	|			ТОГДА ВЫРАЗИТЬ(
	|				ВЫБОР КОГДА ОтчетПереработчика.СуммаВключаетНДС = ИСТИНА И ОтчетПереработчика.Расходы.СтавкаНДС <> Значение(Справочник.СтавкиНДС.ПустаяСсылка)
	|					ТОГДА ОтчетПереработчика.Расходы.Сумма / ((ОтчетПереработчика.Расходы.СтавкаНДС.Ставка + 100) / 100)
	|					ИНАЧЕ ОтчетПереработчика.Расходы.Сумма КОНЕЦ * ОтчетПереработчика.Ссылка.Курс / ОтчетПереработчика.Ссылка.Кратность
	|			КАК Число(15, 2)) 
	|			ИНАЧЕ ВЫРАЗИТЬ(
	|				ВЫБОР КОГДА ОтчетПереработчика.СуммаВключаетНДС = ИСТИНА И ОтчетПереработчика.Расходы.СтавкаНДС <> Значение(Справочник.СтавкиНДС.ПустаяСсылка)
	|					ТОГДА ОтчетПереработчика.Расходы.Сумма / ((ОтчетПереработчика.Расходы.СтавкаНДС.Ставка + 100) / 100)
	|					ИНАЧЕ ОтчетПереработчика.Расходы.Сумма КОНЕЦ
	|			КАК Число(15, 2)) КОНЕЦ КАК Сумма
	|		,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|				ИЛИ (Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА 
	|						И Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение)
	|			ТОГДА ВЫРАЗИТЬ(ОтчетПереработчика.Расходы.СуммаНДС * ОтчетПереработчика.Ссылка.Курс / ОтчетПереработчика.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ ОтчетПереработчика.Расходы.СуммаНДС КОНЕЦ КАК СуммаНДС
	|		,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|				ИЛИ (Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА 
	|						И Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение)
	|			ТОГДА ВЫРАЗИТЬ(ОтчетПереработчика.Расходы.Всего * ОтчетПереработчика.Ссылка.Курс / ОтчетПереработчика.Ссылка.Кратность КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ ОтчетПереработчика.Расходы.Всего КОНЕЦ КАК Всего
	|		,Ссылка КАК Ссылка
	|		,Неопределено КАК НоменклатураНабора
	|		,неопределено КАК ХарактеристикаНабора
	|		,ЛОЖЬ КАК ЭтоНабор
	|		,ЛОЖЬ КАК НеобходимоВыделитьКакСоставНабора
	|		,Неопределено КАК СтранаСсылка
	|		,Неопределено КАК СтранаКод
	|		,Неопределено КАК СтранаПредставление
	|		,Неопределено КАК ПредставлениеГТД
	|	) КАК ТаблицаЗапасы
	|	,1 КАК НомерСтроки
	|	,ОтчетПереработчика.Расход КАК Номенклатура
	|	,Неопределено КАК Содержание
	|	,Выбор КОГДА ВЫРАЗИТЬ(ОтчетПереработчика.Расход.НаименованиеПолное КАК СТРОКА(1000)) = """"
	|		ТОГДА ОтчетПереработчика.Расход.Наименование
	|		ИНАЧЕ ВЫРАЗИТЬ(ОтчетПереработчика.Расход.НаименованиеПолное КАК СТРОКА(1000)) КОНЕЦ КАК ПредставлениеНоменклатуры
	|	,ОтчетПереработчика.Расход.ТипНоменклатуры КАК ТипНоменклатуры
	|	,ОтчетПереработчика.Расход.Код КАК ЗапасКод
	|	,ОтчетПереработчика.Расход.Код КАК Код
	|	,ОтчетПереработчика.Расход.Артикул КАК Артикул
	|	,ОтчетПереработчика.Расход.ТоварнаяНоменклатураВЭД.Код КАК КодТНВЭД
	|	,ОтчетПереработчика.Расход.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияПоОКЕИ_Наименование
	|	,ОтчетПереработчика.Расход.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияПоОКЕИ_Код
	|	,Неопределено КАК Характеристика
	|	,ОтчетПереработчика.Расход.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,Неопределено КАК ВидУпаковки
	|	,1 КАК КоэффициентЕдиницыИзмерения
	|	,1 КАК Количество
	|	,Неопределено КАК КоличествоВОдномМесте
	|	,Неопределено КАК КоличествоМест
	|	,ОтчетПереработчика.СтавкаНДС КАК СтавкаНДС
	|	,0 КАК МассаБрутто
	
	// ::: Цена * Курс * Кратность ЛИБО Цена
	|	,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|			ИЛИ (Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА 
	|					И Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение)
	|		ТОГДА
	|			ВЫРАЗИТЬ(
	|				ВЫБОР КОГДА ОтчетПереработчика.СуммаВключаетНДС = ИСТИНА И ОтчетПереработчика.СтавкаНДС <> Значение(Справочник.СтавкиНДС.ПустаяСсылка)
	|					ТОГДА ОтчетПереработчика.Сумма / ((ОтчетПереработчика.СтавкаНДС.Ставка + 100) / 100)
	|					ИНАЧЕ ОтчетПереработчика.Сумма КОНЕЦ * Ссылка.Курс / Ссылка.Кратность
	|			КАК Число(15, 2))
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(
	|				ВЫБОР КОГДА ОтчетПереработчика.СуммаВключаетНДС = ИСТИНА И ОтчетПереработчика.СтавкаНДС <> Значение(Справочник.СтавкиНДС.ПустаяСсылка)
	|					ТОГДА ОтчетПереработчика.Сумма / ((ОтчетПереработчика.СтавкаНДС.Ставка + 100) / 100)
	|					ИНАЧЕ ОтчетПереработчика.Сумма КОНЕЦ
	|			КАК Число(15, 2))
	|	КОНЕЦ КАК Цена
	
	// ::: Сумма * Курс * Кратность ЛИБО Сумма
	|	,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|			ИЛИ (Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА 
	|					И Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение)
	|		ТОГДА
	|			ВЫРАЗИТЬ(
	|				ВЫБОР КОГДА ОтчетПереработчика.СуммаВключаетНДС = ИСТИНА И ОтчетПереработчика.СтавкаНДС <> Значение(Справочник.СтавкиНДС.ПустаяСсылка)
	|					ТОГДА ОтчетПереработчика.Сумма / ((ОтчетПереработчика.СтавкаНДС.Ставка + 100) / 100)
	|					ИНАЧЕ ОтчетПереработчика.Сумма КОНЕЦ * Ссылка.Курс / Ссылка.Кратность 
	|			КАК Число(15, 2))
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(
	|				ВЫБОР КОГДА ОтчетПереработчика.СуммаВключаетНДС = ИСТИНА И ОтчетПереработчика.СтавкаНДС <> Значение(Справочник.СтавкиНДС.ПустаяСсылка)
	|					ТОГДА ОтчетПереработчика.Сумма / ((ОтчетПереработчика.СтавкаНДС.Ставка + 100) / 100)
	|					ИНАЧЕ ОтчетПереработчика.Сумма КОНЕЦ
	|			КАК Число(15, 2))
	|		КОНЕЦ КАК Сумма
	
	// ::: СуммаНДС * Курс * Кратность ЛИБО СуммаНДС
	|	,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|			ИЛИ (Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА 
	|					И Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение)
	|		ТОГДА ВЫРАЗИТЬ(ОтчетПереработчика.СуммаНДС * Ссылка.Курс / Ссылка.Кратность КАК Число(15, 2))
	|		ИНАЧЕ ОтчетПереработчика.СуммаНДС КОНЕЦ КАК СуммаНДС
	
	// ::: Всего * Курс * Кратность ЛИБО Всего
	|	,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|			ИЛИ (Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА 
	|					И Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение)
	|		ТОГДА
	|			ВЫРАЗИТЬ(
	|				ВЫБОР КОГДА ОтчетПереработчика.СуммаВключаетНДС = ИСТИНА И ОтчетПереработчика.СтавкаНДС <> Значение(Справочник.СтавкиНДС.ПустаяСсылка)
	|					ТОГДА ОтчетПереработчика.Сумма / ((ОтчетПереработчика.СтавкаНДС.Ставка + 100) / 100) + ОтчетПереработчика.СуммаНДС
	|					ИНАЧЕ ОтчетПереработчика.Сумма + ОтчетПереработчика.СуммаНДС КОНЕЦ * Ссылка.Курс / Ссылка.Кратность
	|			КАК Число(15, 2))
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(
	|				ВЫБОР КОГДА ОтчетПереработчика.СуммаВключаетНДС = ИСТИНА И ОтчетПереработчика.СтавкаНДС <> Значение(Справочник.СтавкиНДС.ПустаяСсылка)
	|					ТОГДА ОтчетПереработчика.Сумма / ((ОтчетПереработчика.СтавкаНДС.Ставка + 100) / 100) + ОтчетПереработчика.СуммаНДС
	|					ИНАЧЕ ОтчетПереработчика.Сумма + ОтчетПереработчика.СуммаНДС КОНЕЦ
	|			КАК Число(15, 2))
	|		КОНЕЦ КАК Всего
	|	,Неопределено КАК СтранаСсылка
	|	,Неопределено КАК СтранаКод
	|	,Неопределено КАК СтранаПредставление
	|	,Неопределено КАК ПредставлениеГТД
	|
	// ::: Табличная часть Платежные документы
	|	,Неопределено КАК ПлатежныеДокументы
	|
	|ИЗ Документ.ОтчетПереработчика КАК ОтчетПереработчика
	|	,Константа.НациональнаяВалюта КАК НациональнаяВалюта
	|ГДЕ ОтчетПереработчика.Ссылка В(&МассивОбъектов)";
	
	ДанныеПечати = Запрос.Выполнить().Выгрузить();
	
	ИменаКолонокТЧ =
	"НомерСтроки
	|,Номенклатура
	|,Содержание
	|,ПредставлениеНоменклатуры
	|,ТипНоменклатуры
	|,ЗапасКод
	|,Код
	|,Артикул
	|,КодТНВЭД
	|,ЕдиницаИзмерения
	|,ЕдиницаИзмеренияПоОКЕИ_Наименование
	|,ЕдиницаИзмеренияПоОКЕИ_Код
	|,Характеристика
	|,ВидУпаковки
	|,КоэффициентЕдиницыИзмерения
	|,Количество
	|,КоличествоВОдномМесте
	|,КоличествоМест
	|,СтавкаНДС
	|,МассаБрутто
	|,Цена
	|,Сумма
	|,СуммаНДС
	|,Всего
	|,СтранаСсылка
	|,СтранаКод
	|,СтранаПредставление
	|,ПредставлениеГТД";
	
	ТабличнаяЧасть = ДанныеПечати.Скопировать(, ИменаКолонокТЧ);
	
	Для каждого ДанныеОбъекта Из ДанныеПечати Цикл
		
		Если ДанныеОбъекта.ПоложениеРасходов = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			Продолжить;
		КонецЕсли; 
		
		ТабличнаяЧасть.Очистить();
		ЗаполнитьЗначенияСвойств(ТабличнаяЧасть.Добавить(), ДанныеОбъекта);
		
		ДанныеОбъекта.ТаблицаЗапасы = ТабличнаяЧасть.Скопировать();
		
	КонецЦикла;
	
	Возврат ДанныеПечати;
	
КонецФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Обработка.ПечатьСчетФактура.СчетФактураПолученный";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура (полученный)'");;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 1;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли