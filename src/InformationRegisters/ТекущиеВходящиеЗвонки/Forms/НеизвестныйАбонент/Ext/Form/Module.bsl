
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МенеджерЗаписи = РегистрыСведений.ТекущиеВходящиеЗвонки.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ПользовательКому = Параметры.ПользовательКому;
	МенеджерЗаписи.Прочитать();
	
	ЗначениеВРеквизитФормы(МенеджерЗаписи, "Запись");
	
	Если НЕ ЗначениеЗаполнено(МенеджерЗаписи.Событие) И ЗначениеЗаполнено(МенеджерЗаписи.ИдентификаторЗвонкаВАТС) Тогда
		ДанныеЗвонка = ТелефонияСервер.НовыйДанныеЗвонка();
		ДанныеЗвонка.ИдентификаторЗвонкаВАТС = МенеджерЗаписи.ИдентификаторЗвонкаВАТС;
		Событие = ТелефонияСервер.НайтиСобытиеПоДаннымЗвонка(ДанныеЗвонка);
	Иначе
		Событие = МенеджерЗаписи.Событие;
	КонецЕсли;
	
	ИспользуетсяМобильнаяТелефония = ПолучитьФункциональнуюОпцию("ИспользоватьМобильнуюТелефонию");
	ИспользуетсяОблачнаяТелефония = ПолучитьФункциональнуюОпцию("ИспользоватьОблачнуюТелефонию");
	
	Элементы.Игнорировать.Видимость = ИспользуетсяМобильнаяТелефония И НЕ ИспользуетсяОблачнаяТелефония;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьКонтрагента(Команда)
	
	УдалитьЗаписьТекущегоВходящегоЗвонка();
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Покупатель", Истина);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ПараметрыФормы.Вставить(
		"КонтактКакСвязаться",
		Новый Структура(
			"ВидКонтакта,Контакт,КакСвязаться,ТипКИ",
			"КонтактноеЛицо", "", Запись.НомерТелефонаАбонента, ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон")));
	
	Закрыть();
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокКонтрагентов(Команда)
	
	УдалитьЗаписьТекущегоВходящегоЗвонка();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Новый Структура("Покупатель", Истина));
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "СписокПокупателей");
	
	Закрыть();
	ОткрытьФорму("Справочник.Контрагенты.ФормаСписка", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиСобытие(Команда)
	
	УдалитьЗаписьТекущегоВходящегоЗвонка();
	
	Закрыть();
	ПоказатьЗначение(, Событие);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Игнорировать(Команда)
	
	УдалитьЗаписьТекущегоВходящегоЗвонка();
	ДобавитьНеобрабатываемыйНомер(Запись.НомерТелефонаАбонента);
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЗаписьТекущегоВходящегоЗвонка()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТекущиеВходящиеЗвонки.ПользовательКому КАК ПользовательКому
	|ИЗ
	|	РегистрСведений.ТекущиеВходящиеЗвонки КАК ТекущиеВходящиеЗвонки
	|ГДЕ
	|	ТекущиеВходящиеЗвонки.Событие = &Событие";
	Запрос.УстановитьПараметр("Событие", Запись.Событие);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.ТекущиеВходящиеЗвонки.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ПользовательКому = Выборка.ПользовательКому;
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.Удалить();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьНеобрабатываемыйНомер(Номер)
	
	МенеджерЗаписи = РегистрыСведений.НеобрабатываемыеТелефонныеНомера.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.НомерТелефонаАбонента = Номер;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти
