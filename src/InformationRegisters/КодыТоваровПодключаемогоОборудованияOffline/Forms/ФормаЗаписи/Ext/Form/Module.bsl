
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КодыТоваровПодключаемогоОборудованияOffline.Код КАК Код
	|ИЗ
	|	РегистрСведений.КодыТоваровПодключаемогоОборудованияOffline КАК КодыТоваровПодключаемогоОборудованияOffline
	|ГДЕ
	|	КодыТоваровПодключаемогоОборудованияOffline.Код = &Код
	|	И КодыТоваровПодключаемогоОборудованияOffline.ПравилоОбмена = &ПравилоОбмена
	|	И КодыТоваровПодключаемогоОборудованияOffline.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|");
	
	Запрос.УстановитьПараметр("Код", ТекущийОбъект.Код);
	Запрос.УстановитьПараметр("ПравилоОбмена", ТекущийОбъект.ПравилоОбмена);
	
	Если Не Запрос.Выполнить().Пустой() Тогда
		УдаляемаяЗапись = РегистрыСведений.КодыТоваровПодключаемогоОборудованияOffline.СоздатьМенеджерЗаписи();
		УдаляемаяЗапись.Код = ТекущийОбъект.Код;
		УдаляемаяЗапись.ПравилоОбмена = ТекущийОбъект.ПравилоОбмена;
		УдаляемаяЗапись.Удалить();
	КонецЕсли;
	
	Код = ПолучитьМаксимальныйКод(Запись.ПравилоОбмена)+1;
	Пока Код < ТекущийОбъект.Код Цикл
		УдалитьКод(ТекущийОбъект.ПравилоОбмена, Код);
		Код = Код+1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Запись.ИсходныйКлючЗаписи.Код <> ТекущийОбъект.Код Тогда
		УдалитьКод(ТекущийОбъект.ПравилоОбмена, Запись.ИсходныйКлючЗаписи.Код);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КодыТоваровПодключаемогоОборудованияOffline.Код КАК Код,
	|	КодыТоваровПодключаемогоОборудованияOffline.Номенклатура КАК Номенклатура,
	|	КодыТоваровПодключаемогоОборудованияOffline.Характеристика КАК Характеристика,
	|	КодыТоваровПодключаемогоОборудованияOffline.Партия КАК Партия,
	|	КодыТоваровПодключаемогоОборудованияOffline.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	КодыТоваровПодключаемогоОборудованияOffline.Номенклатура.Наименование КАК НоменклатураПредставление,
	|	КодыТоваровПодключаемогоОборудованияOffline.Характеристика.Наименование КАК ХарактеристикаПредставление,
	|	КодыТоваровПодключаемогоОборудованияOffline.Партия.Наименование КАК ПартияПредставление,
	|	КодыТоваровПодключаемогоОборудованияOffline.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияПредставление
	|ИЗ
	|	РегистрСведений.КодыТоваровПодключаемогоОборудованияOffline КАК КодыТоваровПодключаемогоОборудованияOffline
	|ГДЕ
	|	КодыТоваровПодключаемогоОборудованияOffline.Код = &Код
	|	И КодыТоваровПодключаемогоОборудованияOffline.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|");
	
	Запрос.УстановитьПараметр("Код", Запись.Код);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() // Штрихкод уже записан в БД
		И Запись.ИсходныйКлючЗаписи.Код <> Запись.Код Тогда
		
		ОписаниеОшибки = НСтр("ru='Такой код уже назначен для номенклатуры %Номенклатура%'");
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Номенклатура%", """" + Выборка.НоменклатураПредставление + """"
						+ ?(ЗначениеЗаполнено(Выборка.Характеристика), " " + НСтр("ru='с характеристикой'") + " """ + Выборка.ХарактеристикаПредставление + """", "")
						+ ?(ЗначениеЗаполнено(Выборка.Партия), " " + НСтр("ru='с партией'") + " """ + Выборка.ПартияПредставление + """", "")
						+ ?(ЗначениеЗаполнено(Выборка.ЕдиницаИзмерения), " " + НСтр("ru='ед. изм.'") + " """ + Выборка.ЕдиницаИзмеренияПредставление + """", ""));
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ОписаниеОшибки;
		Сообщение.Поле = "Запись.Код";
		Сообщение.Сообщить();
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивКодов = ПолучитьСвободныеКоды().ВыгрузитьКолонку("Код");
	Элементы.Код.СписокВыбора.ЗагрузитьЗначения(МассивКодов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьСвободныеКоды()
	
	СвободныеКоды = ПолучитьСвободныеКодыПоКоличеству(Запись.ПравилоОбмена, 20);
	НоваяСтрока = СвободныеКоды.Добавить();
	НоваяСтрока.Код = ПолучитьМаксимальныйКод(Запись.ПравилоОбмена)+1;
	
	Возврат СвободныеКоды;
	
КонецФункции

Функция ПолучитьСвободныеКодыПоКоличеству(ПравилоОбмена, Количество = 0) 
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ //ПЕРВЫЕ
	|	КодыТоваровPLUНаОборудовании.КодТовараPLU КАК Код
	|ИЗ
	|	РегистрСведений.КодыТоваровPLUНаОборудовании КАК КодыТоваровPLUНаОборудовании
	|ГДЕ
	|	КодыТоваровPLUНаОборудовании.ПравилоОбмена = &ПравилоОбмена
	|	И КодыТоваровPLUНаОборудовании.КодТовараSKU = 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	КодыТоваровPLUНаОборудовании.КодТовараPLU ВОЗР");
	
	Запрос.УстановитьПараметр("ПравилоОбмена", ПравилоОбмена);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ПЕРВЫЕ", ?(Количество = 0,"","ПЕРВЫЕ" + " " + Формат(Количество, "ЧГ=0")));
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции


Функция ПолучитьМаксимальныйКод(ПравилоОбмена) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(КодыТоваровPLUНаОборудовании.КодТовараPLU), 0) КАК Код
	|ИЗ
	|	РегистрСведений.КодыТоваровPLUНаОборудовании КАК КодыТоваровPLUНаОборудовании
	|ГДЕ
	|	КодыТоваровPLUНаОборудовании.ПравилоОбмена = &ПравилоОбмена");
	
	Запрос.УстановитьПараметр("ПравилоОбмена", ПравилоОбмена);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Код;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Процедура УдалитьКод(ПравилоОбмена, Код) Экспорт
	
	ЗаписатьКод(Новый Структура("Номенклатура, Характеристика, Партия, ЕдиницаИзмерения"), ПравилоОбмена, Код , Ложь);
	
КонецПроцедуры

Процедура ЗаписатьКод(Данные, ПравилоОбмена, Код, Используется) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.КодыТоваровPLUНаОборудовании.СоздатьМенеджерЗаписи();
	
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Данные);
	
	МенеджерЗаписи.КодТовараPLU             = Код;
	МенеджерЗаписи.ПравилоОбмена   = ПравилоОбмена;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры




#КонецОбласти
