
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ЗаказСервисаПоДокументуОснования(ДокументОснование) Экспорт
	
	ЗаказСервиса = Новый Структура;
	ЗаказСервиса.Вставить("ИдентификаторЗаказа", "");
	ЗаказСервиса.Вставить("ТрекНомер", "");
	ЗаказСервиса.Вставить("ТипГрузоперевозки", "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказыСервисовДоставки.ДокументОснование КАК ДокументОснование,
	|	ЗаказыСервисовДоставки.ИдентификаторЗаказа КАК ИдентификаторЗаказа,
	|	ЗаказыСервисовДоставки.ТрекНомер КАК ТрекНомер,
	|	ЗаказыСервисовДоставки.ТипГрузоперевозки КАК ТипГрузоперевозки
	|ИЗ
	|	РегистрСведений.ЗаказыСервисовДоставки КАК ЗаказыСервисовДоставки
	|ГДЕ
	|	ЗаказыСервисовДоставки.ДокументОснование = &Основание";
	
	Запрос.УстановитьПараметр("Основание", ДокументОснование);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат ЗаказСервиса;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(ЗаказСервиса, Выборка);
	Возврат ЗаказСервиса;
	
КонецФункции

Процедура ПроверитьЗаписатьДанныеПоДоставке(ДокументОснование, ТипГрузоперевозки, ИдентификаторЗаказа = "", ТрекНомер = "") Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказыСервисовДоставки.ДокументОснование КАК ДокументОснование,
	|	ЗаказыСервисовДоставки.ИдентификаторЗаказа КАК ИдентификаторЗаказа,
	|	ЗаказыСервисовДоставки.ТрекНомер КАК ТрекНомер,
	|	ЗаказыСервисовДоставки.ТипГрузоперевозки КАК ТипГрузоперевозки,
	|	ЗаказыСервисовДоставки.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ЗаказыСервисовДоставки КАК ЗаказыСервисовДоставки
	|ГДЕ
	|	ЗаказыСервисовДоставки.ДокументОснование = &Основание";
	
	Запрос.УстановитьПараметр("Основание", ДокументОснование);
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	НоваяЗаписьРегистра = РегистрыСведений.ЗаказыСервисовДоставки.СоздатьМенеджерЗаписи();
	НоваяЗаписьРегистра.ДокументОснование = ДокументОснование;
	НоваяЗаписьРегистра.ТрекНомер = "";
	НоваяЗаписьРегистра.ИдентификаторЗаказа = ИдентификаторЗаказа;
	Если ТипЗнч(ТипГрузоперевозки) = Тип("Число") Тогда
		НоваяЗаписьРегистра.ТипГрузоперевозки = ТипГрузоперевозки;
	Иначе
		Если ТипГрузоперевозки = Перечисления.СервисыДоставки.Сервис1СДоставка Тогда
			НоваяЗаписьРегистра.ТипГрузоперевозки = 1;
		ИначеЕсли ТипГрузоперевозки = Перечисления.СервисыДоставки.Сервис1СКурьер Тогда
			НоваяЗаписьРегистра.ТипГрузоперевозки = 2;
		КонецЕсли;
	КонецЕсли;
	
	НоваяЗаписьРегистра.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли