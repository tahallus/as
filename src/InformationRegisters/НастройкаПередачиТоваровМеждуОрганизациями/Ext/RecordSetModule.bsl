#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Количество() = 0 Тогда
		// Проверки удаления записей
		Если ЗначениеЗаполнено(Отбор.ОрганизацияВладелец.Значение)
			И ЗначениеЗаполнено(Отбор.ОрганизацияПродавец.Значение)
			И РегистрыСведений.НастройкаПередачиТоваровМеждуОрганизациями.ЕстьНезакрытыеРезервы(Отбор.ОрганизацияВладелец.Значение, Отбор.ОрганизацияПродавец.Значение) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'По указанным организациям есть остатки резервов для передачи, удаление настроек невозможно.'"), , , , Отказ);
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Количество() > 0 И ЭтотОбъект[0].СпособПередачиТоваров <> Перечисления.СпособыПередачиТоваров.ПередачаНаКомиссию Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Валюта");
	КонецЕсли;
	
	Если Количество() > 0 И ЭтотОбъект[0].СпособПередачиТоваров = Перечисления.СпособыПередачиТоваров.НеПередается Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Договор");
		МассивНепроверяемыхРеквизитов.Добавить("ВидЦены");
	КонецЕсли; 
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(
		ПроверяемыеРеквизиты,
		МассивНепроверяемыхРеквизитов);
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли
