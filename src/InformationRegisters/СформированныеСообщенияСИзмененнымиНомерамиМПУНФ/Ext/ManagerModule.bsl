#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьНомерСообщения() Экспорт 
	
	НомерСообщенияОбмена = 1;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СформированныеСообщенияСИзмененнымиНомерамиМПУНФ.Номер КАК Номер
		|ИЗ
		|	РегистрСведений.СформированныеСообщенияСИзмененнымиНомерамиМПУНФ КАК СформированныеСообщенияСИзмененнымиНомерамиМПУНФ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номер УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		НомерСообщенияОбмена = ВыборкаДетальныеЗаписи.Номер + НомерСообщенияОбмена;
	КонецЕсли;
	
	Возврат НомерСообщенияОбмена;
	
КонецФункции

Функция ПолучитьСообщенияДляОтправки(КодУзла) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СформированныеСообщенияСИзмененнымиНомерамиМПУНФ.Номер КАК Номер,
		|	СформированныеСообщенияСИзмененнымиНомерамиМПУНФ.Сообщение КАК Сообщение
		|ИЗ
		|	РегистрСведений.СформированныеСообщенияСИзмененнымиНомерамиМПУНФ КАК СформированныеСообщенияСИзмененнымиНомерамиМПУНФ
		|ГДЕ
		|	СформированныеСообщенияСИзмененнымиНомерамиМПУНФ.КодУзла = &КодУзла";
	
	Запрос.УстановитьПараметр("КодУзла", КодУзла);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	МассивССообщениями = Новый Массив;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивССообщениями.Добавить(Новый Структура("Номер, Сообщение", ВыборкаДетальныеЗаписи.Номер, ВыборкаДетальныеЗаписи.Сообщение));
	КонецЦикла;
	
	Возврат Новый Структура("МассивССообщениями", МассивССообщениями)
	
КонецФункции // ()

Процедура УдалитьСообщение(Номер, КодУзла) Экспорт
	
	НаборЗаписей = РегистрыСведений.СформированныеСообщенияСИзмененнымиНомерамиМПУНФ.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Номер.Установить(Номер);
	НаборЗаписей.Отбор.КодУзла.Установить(КодУзла);
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
