
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекСтрока = Элемент.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуРедактирования(ТекСтрока);
	
КонецПроцедуры


&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Для каждого стр Из Строки Цикл
		стр.Значение.Данные.УслугаРесурс = стр.Ключ.Ресурс;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)

	ТекСтрока = Элемент.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	ОткрытьФормуРедактирования(ТекСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаРедактированияОбработкаВыбора(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуРедактирования(ТекСтрока = Неопределено)
	
	Если НЕ (ТекСтрока.Свойство("Услуга") ИЛИ ТекСтрока.Свойство("УслугаРесурс")) Тогда
		Возврат;
	КонецЕсли; 
	
	Если ТекСтрока.РодительскаяГруппировкаСтроки = Неопределено Тогда
		ПараметрыФормы = Новый Структура("Услуга, ИсточникЗаписи, УзелОбмена", 
			ТекСтрока.Услуга, 
			ТекСтрока.ИсточникЗаписи, 
			ТекСтрока.УзелОбмена);
	Иначе	
   		ПараметрыФормы = Новый Структура("Услуга, ИсточникЗаписи, УзелОбмена", 
			ТекСтрока.РодительскаяГруппировкаСтроки.Ключ, 
			ТекСтрока.ИсточникЗаписи, 
			ТекСтрока.УзелОбмена);
	КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("ФормаРедактированияОбработкаВыбора", ЭтотОбъект);	
	ОткрытьФорму("РегистрСведений.НастройкиЗаписьНаУслуги.Форма.ФормаРедактирования", ПараметрыФормы,,,,,ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

