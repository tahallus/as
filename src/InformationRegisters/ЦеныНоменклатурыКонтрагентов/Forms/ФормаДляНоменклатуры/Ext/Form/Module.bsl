
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Отбор") 
		И Параметры.Отбор.Свойство("Номенклатура") Тогда
		
		ОтборНоменклатура = Параметры.Отбор.Номенклатура;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,			"Номенклатура", ОтборНоменклатура, ВидСравненияКомпоновкиДанных.Равно, "Номенклатура", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокИстория,	"Номенклатура", ОтборНоменклатура, ВидСравненияКомпоновкиДанных.Равно, "Номенклатура", Истина);
		
		Если (ОтборНоменклатура.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Запас)
			И (ОтборНоменклатура.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.ПодарочныйСертификат)
			И (ОтборНоменклатура.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Услуга)
			И (ОтборНоменклатура.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Работа)
			Тогда
			
			АвтоЗаголовок = Ложь;
			Заголовок = НСтр("ru = 'Цены контрагентов можно вводить только для номенклатуры с типами Запас, Услуга, Работа, Сертификат'");
			
			Элементы.Список.ТолькоПросмотр = Истина;
			
		КонецЕсли;
		
		ИнформацияОМинимальнойЦене();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Наборы
	Если НЕ Элементы.Список.ТолькоПросмотр И ТипЗнч(ВладелецФормы)=Тип("УправляемаяФорма") И ВладелецФормы.ИмяФормы="Справочник.Номенклатура.Форма.ФормаЭлемента" Тогда
		
		ОбъектНоменклатура = ВладелецФормы.Объект;
		
		Если ОбъектНоменклатура.ЭтоНабор И ОбъектНоменклатура.СпособРасчетаЦеныНабора=ПредопределенноеЗначение("Перечисление.СпособыРасчетаЦеныНабора.СкладыватьИзЦенКомплектующих") Тогда
			
			АвтоЗаголовок = Ложь;
			Заголовок = НСтр("ru = 'Цены контрагентов недоступны для наборов со способом расчета цен по комплектующим'");
			Элементы.Список.ТолькоПросмотр = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	// Конец Наборы
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьИнформациюОМинимальнойЦене(Команда)
	
	ИнформацияОМинимальнойЦене();
	
КонецПроцедуры

&НаКлиенте
Процедура Хронология(Команда)
	
	СоответствиеСтраниц = Новый Соответствие;
	СоответствиеСтраниц.Вставить(Элементы.СтраницаСписок, Элементы.СтраницаСписокИстория);
	СоответствиеСтраниц.Вставить(Элементы.СтраницаСписокИстория, Элементы.СтраницаСписок);
	
	Элементы.Страницы.ТекущаяСтраница = СоответствиеСтраниц.Получить(Элементы.Страницы.ТекущаяСтраница);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнформацияОМинимальнойЦене()
	
	Если НЕ ЗначениеЗаполнено(ОтборНоменклатура) Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОбновитьИнформациюОМинимальнойЦене", "Видимость", Ложь);
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОтборНоменклатура", ОтборНоменклатура);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЦеныКонтрагентов.ВидЦенКонтрагента КАК ВидЦенКонтрагента,
	|	ЦеныКонтрагентов.Период КАК Период,
	|	ЦеныКонтрагентов.Характеристика КАК ХарактеристикаНоменклатуры,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ЦеныКонтрагентов.ВидЦенКонтрагента.ВалютаЦены = НациональнаяВалюта.Значение
	|				ТОГДА ЦеныКонтрагентов.Цена
	|			ИНАЧЕ ЦеныКонтрагентов.Цена * КурсВалютыВидЦен.Курс / КурсВалютыВидЦен.Кратность
	|		КОНЕЦ) КАК Цена
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатурыКонтрагентов.СрезПоследних(, Номенклатура = &ОтборНоменклатура) КАК ЦеныКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(, ) КАК КурсВалютыВидЦен
	|		ПО ЦеныКонтрагентов.ВидЦенКонтрагента.ВалютаЦены = КурсВалютыВидЦен.Валюта,
	|	Константа.НациональнаяВалюта КАК НациональнаяВалюта
	|
	|СГРУППИРОВАТЬ ПО
	|	ЦеныКонтрагентов.ВидЦенКонтрагента,
	|	ЦеныКонтрагентов.Период,
	|	ЦеныКонтрагентов.Характеристика
	|
	|УПОРЯДОЧИТЬ ПО
	|	Цена";
	
	РезультатЗапроса = Запрос.Выполнить();
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОбновитьИнформациюОМинимальнойЦене", "Видимость", НЕ РезультатЗапроса.Пустой());
	
	Если Элементы.ОбновитьИнформациюОМинимальнойЦене.Видимость Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			
			Тело = НСтр("ru ='Минимальная цена <b>%1</b> зарегистрирована <b>%2</b> и составляет <b>%3</b> руб. %4'");
			Параметр1 = Строка(Выборка.ВидЦенКонтрагента);
			Параметр2 = Формат(Выборка.Период, "ДФ=dd.MM.yy");
			Параметр3 = Формат(Выборка.Цена, "ЧДЦ=2; ЧРГ=,; ЧН=0,00; ЧГ=0");
			
			Если ЗначениеЗаполнено(Выборка.ХарактеристикаНоменклатуры) Тогда
				
				Параметр4 = СтрШаблон(НСтр("ru ='(хар-ка:%1)'"), Строка(Выборка.ХарактеристикаНоменклатуры));
				
			Иначе
				
				Параметр4 = "";
				
			КонецЕсли;
			
			СтрокаЗаголовка = СокрЛП(СтрШаблон(Тело, Параметр1, Параметр2, Параметр3, Параметр4));
			
			Элементы.ОбновитьИнформациюОМинимальнойЦенеРасширеннаяПодсказка.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(СтрокаЗаголовка);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти