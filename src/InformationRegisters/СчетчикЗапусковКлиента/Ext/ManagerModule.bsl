#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Увеличивает счетчик запусков клиента
// 
// Параметры:
// 	ЭтоМобильныйКлиент - Булево - .
Процедура УвеличитьСчетчик(ЭтоМобильныйКлиент) Экспорт
	
	Если РаботаВМоделиСервиса.РазделениеВключено()
		И РаботаВМоделиСервиса.СеансЗапущенБезРазделителей() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗначенияСчетчика = ЗначенияСчетчика();
	ИдентификаторКлиента = УникальныйИдентификаторКлиента();
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ИдентификаторКлиента = ИдентификаторКлиента;
	Если ЗначениеЗаполнено(ЗначенияСчетчика) Тогда
		МенеджерЗаписи.КоличествоЗапусков = ЗначенияСчетчика.КоличествоЗапусков + 1;
	Иначе
		МенеджерЗаписи.КоличествоЗапусков = 1;
	КонецЕсли;
	МенеджерЗаписи.ДатаПоследнегоЗапуска = ТекущаяДатаСеанса();
	МенеджерЗаписи.ЭтоМобильныйКлиент = ЭтоМобильныйКлиент;
	
	Если ЗначениеЗаполнено(ЗначенияСчетчика) Тогда
		Если МенеджерЗаписи.ДатаПоследнегоЗапуска - ЗначенияСчетчика.ДатаПоследнегоЗапуска <= 60 * 60 * 24 * 30 Тогда
			МенеджерЗаписи.ЭтоАктивный = Истина;
		Иначе
			МенеджерЗаписи.ЭтоАктивный = Ложь;
		КонецЕсли;
		МенеджерЗаписи.ЗапретОткрытияОпроса = ЗначенияСчетчика.ЗапретОткрытияОпроса;
		МенеджерЗаписи.ДатаПервогоЗапуска = ЗначенияСчетчика.ДатаПервогоЗапуска;
	Иначе
		МенеджерЗаписи.ДатаПервогоЗапуска = ТекущаяДатаСеанса();
	КонецЕсли;
	
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры


// Значения счетчика запусков клиента.
// Если счетчик не увеличивался, либо приложение в сервисе запущено без разделителей, возвращается пустая структура.
// 
// Возвращаемое значение:
//  Структура - Описание:
//   * ЭтоМобильныйБраузер - Булево - это мобильный браузер
//   * ЗапретОткрытияФормыПереходаВМК - Булево - запрет открытия формы перехода в мобильный клиент
//   * ТолькоМобильныеКлиенты - Булево - только мобильные клиента
//   * ДатаПервогоЗапуска - Дата - дата первого запуска
//   * ЗапретОткрытияОпроса - Булево - запрет открытия опроса
//   * ЭтоМобильныйКлиент - Булево - это мобильный клиент
//   * ЭтоАктивный - это активный пользователь
//   * КоличествоЗапусков - Число - количество запусков
//   * ДатаПоследнегоЗапуска - Дата - дата последнего запуска
//   * ИдентификаторКлиента - УникальныйИдентификатор - идентификатор клиента
Функция ЗначенияСчетчика() Экспорт
	
	Результат = Новый Структура;
	
	Если РаботаВМоделиСервиса.РазделениеВключено()
		И РаботаВМоделиСервиса.СеансЗапущенБезРазделителей() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат.Вставить("ИдентификаторКлиента", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	Результат.Вставить("ДатаПоследнегоЗапуска", '00010101');
	Результат.Вставить("КоличествоЗапусков", 0);
	Результат.Вставить("ЭтоАктивный", Ложь);
	Результат.Вставить("ЭтоМобильныйКлиент", Ложь);
	Результат.Вставить("ЗапретОткрытияОпроса", Ложь);
	Результат.Вставить("ДатаПервогоЗапуска", '00010101');
	Результат.Вставить("ТолькоМобильныеКлиенты", Ложь);
	Результат.Вставить("ЗапретОткрытияФормыПереходаВМК", Ложь);
	Результат.Вставить("ЭтоМобильныйБраузер", Ложь);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторКлиента = УникальныйИдентификаторКлиента();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетчикЗапусковКлиента.ИдентификаторКлиента КАК ИдентификаторКлиента,
	|	СчетчикЗапусковКлиента.ДатаПоследнегоЗапуска КАК ДатаПоследнегоЗапуска,
	|	СчетчикЗапусковКлиента.КоличествоЗапусков КАК КоличествоЗапусков,
	|	СчетчикЗапусковКлиента.ЭтоАктивный КАК ЭтоАктивный,
	|	СчетчикЗапусковКлиента.ЭтоМобильныйКлиент КАК ЭтоМобильныйКлиент,
	|	СчетчикЗапусковКлиента.ЗапретОткрытияОпроса КАК ЗапретОткрытияОпроса,
	|	СчетчикЗапусковКлиента.ДатаПервогоЗапуска КАК ДатаПервогоЗапуска,
	|	СчетчикЗапусковКлиента.ЗапретОткрытияФормыПереходаВМК КАК ЗапретОткрытияФормыПереходаВМК
	|ИЗ
	|	РегистрСведений.СчетчикЗапусковКлиента КАК СчетчикЗапусковКлиента
	|ГДЕ
	|	СчетчикЗапусковКлиента.ИдентификаторКлиента = &ИдентификаторКлиента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетчикЗапусковКлиента.ИдентификаторКлиента КАК ИдентификаторКлиента
	|ИЗ
	|	РегистрСведений.СчетчикЗапусковКлиента КАК СчетчикЗапусковКлиента
	|ГДЕ
	|	СчетчикЗапусковКлиента.ЭтоМобильныйКлиент = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("ИдентификаторКлиента", ИдентификаторКлиента);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если РезультатЗапроса[0].Пустой() Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	Выборка = РезультатЗапроса[0].Выбрать();
	ВыборкаТолькоМобильныеКлиенты = РезультатЗапроса[1].Выбрать();
	
	Выборка.Следующий();
	ТолькоМобильныеКлиенты = НЕ ВыборкаТолькоМобильныеКлиенты.Следующий();
	
	ЗаполнитьЗначенияСвойств(Результат, Выборка);
	Результат.ТолькоМобильныеКлиенты = ТолькоМобильныеКлиенты;
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ИнформацияПрограммыПросмотра = НРег(СистемнаяИнформация.ИнформацияПрограммыПросмотра);
	
	Если СтрНайти(ИнформацияПрограммыПросмотра, "iphone") <> 0
		ИЛИ СтрНайти(ИнформацияПрограммыПросмотра, "ipod") <> 0 
		ИЛИ СтрНайти(ИнформацияПрограммыПросмотра, "android") <> 0 Тогда
		Результат.ЭтоМобильныйБраузер = Истина;
	Иначе
		Результат.ЭтоМобильныйБраузер = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


// Устанавливает запрет открытия формы опроса
// 
// Параметры:
// 	ЗапретОткрытияОпроса
Процедура УстановитьЗапретОткрытияФормыОпроса(ЗапретОткрытияОпроса) Экспорт
	
	Если РаботаВМоделиСервиса.СеансЗапущенБезРазделителей() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗначениеСчетчика = ЗначенияСчетчика();
	Если ЗначениеСчетчика = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЗначениеСчетчика);
	МенеджерЗаписи.ЗапретОткрытияОпроса = ЗапретОткрытияОпроса;
	
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция УникальныйИдентификаторКлиента()
	
	СисИнфо = Новый СистемнаяИнформация;
	Возврат СисИнфо.ИдентификаторКлиента;
	
КонецФункции

#КонецОбласти

#КонецЕсли
