#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиЗаписи
	
// Производит запись в служебный регистр сведений данных по остатками и резервам номенклатуры
//
Процедура РассчитатьОстатокТовараДляБыстрогоОтображения(ДополнительныеСвойства, ЧислоДвижений, ИмяТаблицыЗапасы = "ДвиженияЗапасыНаСкладахИзменение") Экспорт
	
	Если НЕ ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы[ИмяТаблицыЗапасы] Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		
	Запрос.Текст = ТекстЗапросаОстатки();
	
	Если НЕ ИмяТаблицыЗапасы = "ДвиженияЗапасыНаСкладахИзменение" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ДвиженияЗапасыНаСкладахИзменение", ИмяТаблицыЗапасы)
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	НаборЗаписей = РегистрыСведений.ОстаткиТоваров.СоздатьНаборЗаписей();
	Пока Выборка.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(Выборка.Номенклатура) Тогда Продолжить КонецЕсли;
		
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Отбор.СтруктурнаяЕдиница.Установить(Выборка.СтруктурнаяЕдиница);
		НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
		НаборЗаписей.Отбор.Характеристика.Установить(Выборка.Характеристика);
		НаборЗаписей.Отбор.Партия.Установить(Выборка.Партия);
		
		НовСтр = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
		НовСтр.Количество = НовСтр.Количество - НовСтр.Резерв;
		
		Если НовСтр.Количество = 0 И НовСтр.Резерв = 0 Тогда
			НаборЗаписей.Очистить();
		КонецЕсли;
		
		НаборЗаписей.Записать(Истина);
		НаборЗаписей.Очистить();
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаОстатки()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК Номенклатура,
	|	ДвиженияЗапасыНаСкладахИзменение.Организация КАК Организация,
	|	ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ДвиженияЗапасыНаСкладахИзменение.Характеристика КАК Характеристика,
	|	ДвиженияЗапасыНаСкладахИзменение.Партия КАК Партия
	|ПОМЕСТИТЬ ВТДвиженияЗапасыНаСкладахИзменение
	|ИЗ
	|	ДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияЗапасыНаСкладахИзменение.Организация,
	|	ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница,
	|	ДвиженияЗапасыНаСкладахИзменение.Партия,
	|	ДвиженияЗапасыНаСкладахИзменение.Характеристика,
	|	ДвиженияЗапасыНаСкладахИзменение.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК Номенклатура,
	|	ВТДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВТДвиженияЗапасыНаСкладахИзменение.Организация КАК Организация,
	|	ВТДвиженияЗапасыНаСкладахИзменение.Партия КАК Партия,
	|	ВТДвиженияЗапасыНаСкладахИзменение.Характеристика КАК Характеристика,
	|	СУММА(ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0)) КАК КоличествоФинКонтур,
	|	ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя
	|ПОМЕСТИТЬ ВтФинКонтур
	|ИЗ
	|	ВТДвиженияЗапасыНаСкладахИзменение КАК ВТДвиженияЗапасыНаСкладахИзменение
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Запасы.Остатки(
	|				,
	|				(Номенклатура, Организация, СтруктурнаяЕдиница, Характеристика, Партия) В
	|					(ВЫБРАТЬ
	|						ВТДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК Номенклатура,
	|						ВТДвиженияЗапасыНаСкладахИзменение.Организация КАК Организация,
	|						ВТДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|						ВТДвиженияЗапасыНаСкладахИзменение.Характеристика КАК Характеристика,
	|						ВТДвиженияЗапасыНаСкладахИзменение.Партия КАК Партия
	|					ИЗ
	|						ВТДвиженияЗапасыНаСкладахИзменение КАК ВТДвиженияЗапасыНаСкладахИзменение)) КАК ЗапасыОстатки
	|		ПО ВТДвиженияЗапасыНаСкладахИзменение.Номенклатура = ЗапасыОстатки.Номенклатура
	|			И ВТДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница = ЗапасыОстатки.СтруктурнаяЕдиница
	|			И ВТДвиженияЗапасыНаСкладахИзменение.Организация = ЗапасыОстатки.Организация
	|			И ВТДвиженияЗапасыНаСкладахИзменение.Партия = ЗапасыОстатки.Партия
	|			И ВТДвиженияЗапасыНаСкладахИзменение.Характеристика = ЗапасыОстатки.Характеристика
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДвиженияЗапасыНаСкладахИзменение.Номенклатура,
	|	ВТДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница,
	|	ВТДвиженияЗапасыНаСкладахИзменение.Организация,
	|	ВТДвиженияЗапасыНаСкладахИзменение.Характеристика,
	|	ВТДвиженияЗапасыНаСкладахИзменение.Партия,
	|	ЗапасыОстатки.ЗаказПокупателя
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтФинКонтур.Номенклатура КАК Номенклатура,
	|	ВтФинКонтур.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВтФинКонтур.Организация КАК Организация,
	|	ВтФинКонтур.Партия КАК Партия,
	|	ВтФинКонтур.Характеристика КАК Характеристика,
	|	СУММА(ЕСТЬNULL(ВтФинКонтур.КоличествоФинКонтур, 0)) КАК КоличествоФинКонтурРезерв
	|ПОМЕСТИТЬ ВтФинКонтурРезерв
	|ИЗ
	|	ВтФинКонтур КАК ВтФинКонтур
	|ГДЕ
	|	НЕ ВтФинКонтур.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтФинКонтур.Номенклатура,
	|	ВтФинКонтур.СтруктурнаяЕдиница,
	|	ВтФинКонтур.Организация,
	|	ВтФинКонтур.Партия,
	|	ВтФинКонтур.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК Номенклатура,
	|	ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ДвиженияЗапасыНаСкладахИзменение.Организация КАК Организация,
	|	ДвиженияЗапасыНаСкладахИзменение.Партия КАК Партия,
	|	ДвиженияЗапасыНаСкладахИзменение.Характеристика КАК Характеристика,
	|	СУММА(ВтФинКонтур.КоличествоФинКонтур) КАК Количество,
	|	ВтФинКонтурРезерв.КоличествоФинКонтурРезерв КАК Резерв
	|ИЗ
	|	ВТДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтФинКонтур КАК ВтФинКонтур
	|		ПО ДвиженияЗапасыНаСкладахИзменение.Номенклатура = ВтФинКонтур.Номенклатура
	|			И ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница = ВтФинКонтур.СтруктурнаяЕдиница
	|			И ДвиженияЗапасыНаСкладахИзменение.Организация = ВтФинКонтур.Организация
	|			И ДвиженияЗапасыНаСкладахИзменение.Партия = ВтФинКонтур.Партия
	|			И ДвиженияЗапасыНаСкладахИзменение.Характеристика = ВтФинКонтур.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтФинКонтурРезерв КАК ВтФинКонтурРезерв
	|		ПО ДвиженияЗапасыНаСкладахИзменение.Номенклатура = ВтФинКонтурРезерв.Номенклатура
	|			И ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница = ВтФинКонтурРезерв.СтруктурнаяЕдиница
	|			И ДвиженияЗапасыНаСкладахИзменение.Организация = ВтФинКонтурРезерв.Организация
	|			И ДвиженияЗапасыНаСкладахИзменение.Партия = ВтФинКонтурРезерв.Партия
	|			И ДвиженияЗапасыНаСкладахИзменение.Характеристика = ВтФинКонтурРезерв.Характеристика
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияЗапасыНаСкладахИзменение.Номенклатура,
	|	ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница,
	|	ДвиженияЗапасыНаСкладахИзменение.Организация,
	|	ДвиженияЗапасыНаСкладахИзменение.Характеристика,
	|	ДвиженияЗапасыНаСкладахИзменение.Партия,
	|	ВтФинКонтурРезерв.КоличествоФинКонтурРезерв";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Производит запись в служебный регистр сведений данных по всем остатками и резервам номенклатуры
//
Функция ВыполнитьЗаданиеПоЗаписиОстатковВТехническийРегистр() Экспорт
	
	НаборЗаписей = РегистрыСведений.ОстаткиТоваров.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОстаткиТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗапасыОстатки.Организация КАК Организация,
		|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
		|	ЗапасыОстатки.Характеристика КАК Характеристика,
		|	ЗапасыОстатки.Партия КАК Партия,
		|	СУММА(ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0)) КАК Резерв,
		|	СУММА(0) КАК Количество
		|ПОМЕСТИТЬ Итог
		|ИЗ
		|	РегистрНакопления.Запасы.Остатки(
		|			,
		|			НЕ ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|				И ТИПЗНАЧЕНИЯ(СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)) КАК ЗапасыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Партия,
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.Номенклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Номенклатура,
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Партия,
		|	СУММА(0),
		|	СУММА(ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0))
		|ИЗ
		|	РегистрНакопления.Запасы.Остатки(
		|			,
		|			ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|				И ТИПЗНАЧЕНИЯ(СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)) КАК ЗапасыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Партия,
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Итог.Организация КАК Организация,
		|	Итог.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	Итог.Номенклатура КАК Номенклатура,
		|	Итог.Характеристика КАК Характеристика,
		|	Итог.Партия КАК Партия,
		|	СУММА(Итог.Резерв) КАК Резерв,
		|	СУММА(Итог.Количество) КАК Количество
		|ИЗ
		|	Итог КАК Итог
		|ГДЕ
		|	НЕ Итог.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	И ТИПЗНАЧЕНИЯ(Итог.СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)
		|
		|СГРУППИРОВАТЬ ПО
		|	Итог.Характеристика,
		|	Итог.Организация,
		|	Итог.Номенклатура,
		|	Итог.Партия,
		|	Итог.СтруктурнаяЕдиница
		|ИТОГИ ПО
		|	Номенклатура";
		
		Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если Не Выборка.Количество() Тогда
			ОтменитьТранзакцию();
			Возврат Истина 
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.ОстаткиТоваров.СоздатьНаборЗаписей();
		
		Пока Выборка.Следующий() Цикл
			
			ВыборкаДетали = Выборка.Выбрать();
			
			НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
			
			Пока ВыборкаДетали.Следующий() Цикл
				НовСтр = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетали);
			КонецЦикла;
			
			НаборЗаписей.Записать(Истина);
			НаборЗаписей.Очистить();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
		Возврат Истина;
		
	Исключение
		
		ОтменитьТранзакцию();
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

// Производит пересчет записи в служебном регистре сведений данных по номенклатуре
//
Функция ВыполнитьЗаданиеПоПересчетуОстатковВТехническомРегистре(Номенклатура) Экспорт
	
	НаборЗаписей = РегистрыСведений.ОстаткиТоваров.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура);
	НаборЗаписей.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОстаткиТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗапасыОстатки.Организация КАК Организация,
		|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
		|	ЗапасыОстатки.Характеристика КАК Характеристика,
		|	ЗапасыОстатки.Партия КАК Партия,
		|	СУММА(ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0)) КАК Резерв,
		|	СУММА(0) КАК Количество
		|ПОМЕСТИТЬ Итог
		|ИЗ
		|	РегистрНакопления.Запасы.Остатки(
		|			,
		|			Номенклатура = &Номенклатура
		|				И НЕ ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|				И ТИПЗНАЧЕНИЯ(СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)) КАК ЗапасыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Партия,
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.Номенклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Номенклатура,
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Партия,
		|	СУММА(0),
		|	СУММА(ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0))
		|ИЗ
		|	РегистрНакопления.Запасы.Остатки(
		|			,
		|			Номенклатура = &Номенклатура
		|				И ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|				И ТИПЗНАЧЕНИЯ(СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)) КАК ЗапасыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Партия,
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Итог.Организация КАК Организация,
		|	Итог.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	Итог.Номенклатура КАК Номенклатура,
		|	Итог.Характеристика КАК Характеристика,
		|	Итог.Партия КАК Партия,
		|	СУММА(Итог.Резерв) КАК Резерв,
		|	СУММА(Итог.Количество) КАК Количество
		|ИЗ
		|	Итог КАК Итог
		|ГДЕ
		|	НЕ Итог.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	И ТИПЗНАЧЕНИЯ(Итог.СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)
		|
		|СГРУППИРОВАТЬ ПО
		|	Итог.Характеристика,
		|	Итог.Организация,
		|	Итог.Номенклатура,
		|	Итог.Партия,
		|	Итог.СтруктурнаяЕдиница
		|ИТОГИ ПО
		|	Номенклатура";
		
		Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если Не Выборка.Количество() Тогда
			ОтменитьТранзакцию();
			Возврат Истина 
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.ОстаткиТоваров.СоздатьНаборЗаписей();
		
		Пока Выборка.Следующий() Цикл
			
			ВыборкаДетали = Выборка.Выбрать();
			
			НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
			
			Пока ВыборкаДетали.Следующий() Цикл
				НовСтр = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетали);
			КонецЦикла;
			
			НаборЗаписей.Записать(Истина);
			НаборЗаписей.Очистить();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
		Возврат Истина;
		
	Исключение
		
		ОтменитьТранзакцию();
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

// Обновляет остатки и резервы номенклатуры в служебном регистре
//
Функция ОбновитьЗаписиОстатковВТехническомРегистре(ДополнительныеПараметры = Неопределено) Экспорт
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОстаткиТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЕстьОтборПоНоменклатуре = Ложь;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		Если ДополнительныеПараметры.Свойство("ТаблицаНоменклатуры") Тогда
			ЭлементБлокировки.ИсточникДанных = ДополнительныеПараметры.ТаблицаНоменклатуры;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
			ЕстьОтборПоНоменклатуре = Истина;
		Иначе
			НаборЗаписей = РегистрыСведений.ОстаткиТоваров.СоздатьНаборЗаписей();
			НаборЗаписей.Записать();
		КонецЕсли;
	Иначе
		НаборЗаписей = РегистрыСведений.ОстаткиТоваров.СоздатьНаборЗаписей();
		НаборЗаписей.Записать();
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗапасыОстатки.Организация КАК Организация,
		|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
		|	ЗапасыОстатки.Характеристика КАК Характеристика,
		|	ЗапасыОстатки.Партия КАК Партия,
		|	СУММА(ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0)) КАК Резерв,
		|	СУММА(0) КАК Количество
		|ПОМЕСТИТЬ Итог
		|ИЗ
		|	РегистрНакопления.Запасы.Остатки(
		|			,
		|			НЕ ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|				И ТИПЗНАЧЕНИЯ(СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)
		|				И Номенклатура В (&ТаблицаНоменклатуры)) КАК ЗапасыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Партия,
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.Номенклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Номенклатура,
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Партия,
		|	СУММА(0),
		|	СУММА(ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0))
		|ИЗ
		|	РегистрНакопления.Запасы.Остатки(
		|			,
		|			ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|				И ТИПЗНАЧЕНИЯ(СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)
		|				И Номенклатура В (&ТаблицаНоменклатуры)) КАК ЗапасыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Партия,
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Итог.Организация КАК Организация,
		|	Итог.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	Итог.Номенклатура КАК Номенклатура,
		|	Итог.Характеристика КАК Характеристика,
		|	Итог.Партия КАК Партия,
		|	СУММА(Итог.Резерв) КАК Резерв,
		|	СУММА(Итог.Количество) КАК Количество
		|ИЗ
		|	Итог КАК Итог
		|ГДЕ
		|	НЕ Итог.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	И ТИПЗНАЧЕНИЯ(Итог.СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)
		|
		|СГРУППИРОВАТЬ ПО
		|	Итог.Характеристика,
		|	Итог.Организация,
		|	Итог.Номенклатура,
		|	Итог.Партия,
		|	Итог.СтруктурнаяЕдиница
		|ИТОГИ ПО
		|	Номенклатура";
		
		Если ЕстьОтборПоНоменклатуре Тогда
			Запрос.УстановитьПараметр("ТаблицаНоменклатуры", ДополнительныеПараметры.ТаблицаНоменклатуры);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Номенклатура В (&ТаблицаНоменклатуры)", "");
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если Не Выборка.Количество() Тогда
			ОтменитьТранзакцию();
			Возврат Истина 
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.ОстаткиТоваров.СоздатьНаборЗаписей();
		
		Пока Выборка.Следующий() Цикл
			
			ВыборкаДетали = Выборка.Выбрать();
			
			НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
			НаборЗаписей.Очистить();
			
			Пока ВыборкаДетали.Следующий() Цикл
				НовСтр = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетали);
			КонецЦикла;
			
			НаборЗаписей.Записать(Истина);
			НаборЗаписей.Очистить();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
		Возврат Истина;
		
	Исключение
		
		ОтменитьТранзакцию();
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#КонецЕсли