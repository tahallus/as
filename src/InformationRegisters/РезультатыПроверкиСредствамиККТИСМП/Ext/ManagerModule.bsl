#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ДанныеРезультатовПроверкиДляПроверкиИПодбора(СсылкаНаДокумент, ВидПродукции) Экспорт
	
	ВозвращаемоеЗначение = Новый Соответствие();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РезультатыПроверкиСредствамиККТИСМП.ШтрихкодУпаковки,
		|	РезультатыПроверкиСредствамиККТИСМП.КодРезультатаПроверки,
		|	РезультатыПроверкиСредствамиККТИСМП.КодМаркировкиПроверен,
		|	РезультатыПроверкиСредствамиККТИСМП.КодОбработкиЗапроса,
		|	РезультатыПроверкиСредствамиККТИСМП.ПредставлениеРезультатаПроверки,
		|	РезультатыПроверкиСредствамиККТИСМП.РезультатПроверки,
		|	РезультатыПроверкиСредствамиККТИСМП.РезультаПроверкиОИСМ,
		|	РезультатыПроверкиСредствамиККТИСМП.СтатусТовара,
		|	РезультатыПроверкиСредствамиККТИСМП.ТребуетсяПолныйКодМаркировки
		|ИЗ
		|	РегистрСведений.РезультатыПроверкиСредствамиККТИСМП КАК РезультатыПроверкиСредствамиККТИСМП
		|ГДЕ
		|	РезультатыПроверкиСредствамиККТИСМП.Документ       = &Документ
		|	И РезультатыПроверкиСредствамиККТИСМП.ВидПродукции = &ВидПродукции";
	
	Запрос.УстановитьПараметр("ВидПродукции", ВидПродукции);
	Запрос.УстановитьПараметр("Документ",     СсылкаНаДокумент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ДанныеПроверки = ШтрихкодированиеИСМПКлиентСервер.ДанныеПредставленияРезультатовПроверкиСредствамиККТ(
			ВыборкаДетальныеЗаписи, Ложь);
		
		Если Не ДанныеПроверки.ЕстьОшибки Тогда
			Продолжить;
		КонецЕсли;
		
		ВозвращаемоеЗначение.Вставить(
			СокрЛП(ВыборкаДетальныеЗаписи.ШтрихкодУпаковки),
			ДанныеПроверки);
		
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#КонецЕсли