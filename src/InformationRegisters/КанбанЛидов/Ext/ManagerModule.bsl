#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ДобавитьЛидВКонец(Состояние, Лид) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.КанбанЛидов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Состояние = Состояние;
	МенеджерЗаписи.Лид = Лид;
	МенеджерЗаписи.Порядок = НовыйКрайнийПорядковыйНомер(Состояние);
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура ПереместитьЛид(СостояниеПредыдущее, СостояниеНовое, Лид, Порядок = Неопределено) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		МенеджерЗаписи = РегистрыСведений.КанбанЛидов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Состояние = СостояниеПредыдущее;
		МенеджерЗаписи.Лид = Лид;
		МенеджерЗаписи.Прочитать();
		МенеджерЗаписи.Удалить();
		
		Если Порядок = Неопределено Тогда
			Порядок = НовыйКрайнийПорядковыйНомер(СостояниеНовое);
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.КанбанЛидов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Состояние.Установить(СостояниеНовое);
		НаборЗаписей.Прочитать();
		
		Для каждого Запись Из НаборЗаписей Цикл
			Если Запись.Порядок < Порядок Тогда
				Продолжить;
			КонецЕсли;
			Запись.Порядок = Запись.Порядок + 1;
		КонецЦикла;
		
		НоваяСтрока = НаборЗаписей.Добавить();
		НоваяСтрока.Состояние = СостояниеНовое;
		НоваяСтрока.Лид = Лид;
		НоваяСтрока.Порядок = Порядок;
		
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;
	
КонецПроцедуры

Процедура ПоменятьПорядкиЛидов(ДанныеПеретаскиваемыхЛидов) Экспорт
	
	МенеджерЗаписиПервого = РегистрыСведений.КанбанЛидов.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписиПервого.Лид = ДанныеПеретаскиваемыхЛидов.СсылкаНаПервыйЛид;
	МенеджерЗаписиПервого.Порядок = ДанныеПеретаскиваемыхЛидов.ПорядокВторогоЛида;
	МенеджерЗаписиПервого.Состояние = ДанныеПеретаскиваемыхЛидов.Состояние;
	МенеджерЗаписиПервого.Записать();
	
	МенеджерЗаписиВторого = РегистрыСведений.КанбанЛидов.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписиВторого.Лид = ДанныеПеретаскиваемыхЛидов.СсылкаНаВторойЛид;
	МенеджерЗаписиВторого.Порядок = ДанныеПеретаскиваемыхЛидов.ПорядокПервогоЛида;
	МенеджерЗаписиВторого.Состояние = ДанныеПеретаскиваемыхЛидов.Состояние;
	МенеджерЗаписиВторого.Записать();
	
КонецПроцедуры

Процедура ПереместитьЛиды(СостояниеПредыдущее, СостояниеНовое, Лиды, Порядок = Неопределено) Экспорт
	
	Если Порядок = Неопределено Тогда
		Порядок = НовыйКрайнийПорядковыйНомер(СостояниеНовое);
	КонецЕсли;
	
	ПорядокПервогоЛида = Порядок;
	
	Для Каждого Лид Из Лиды Цикл
		
		МенеджерЗаписи = РегистрыСведений.КанбанЛидов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Состояние = СостояниеПредыдущее;
		МенеджерЗаписи.Лид = Лид;
		МенеджерЗаписи.Прочитать();
		МенеджерЗаписи.Удалить();
		
	КонецЦикла;
	
	НаборЗаписей = РегистрыСведений.КанбанЛидов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Состояние.Установить(СостояниеНовое);
	НаборЗаписей.Прочитать();
	
	Для Каждого Лид Из Лиды Цикл
		НоваяСтрока = НаборЗаписей.Добавить();
		НоваяСтрока.Состояние = СостояниеНовое;
		НоваяСтрока.Лид = Лид;
		НоваяСтрока.Порядок = Порядок;
		Порядок = Порядок + 1;
	КонецЦикла;
	
	Для каждого Запись Из НаборЗаписей Цикл
		НайденныйЛид = Лиды.Найти(Запись.Лид);
		Если Запись.Порядок < ПорядокПервогоЛида ИЛИ НайденныйЛид <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Запись.Порядок = Запись.Порядок + Лиды.Количество();
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйКрайнийПорядковыйНомер(Состояние) Экспорт
	
	Порядок = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КанбанЛидов.Порядок КАК Порядок
	|ИЗ
	|	РегистрСведений.КанбанЛидов КАК КанбанЛидов
	|ГДЕ
	|	КанбанЛидов.Состояние = &Состояние
	|
	|УПОРЯДОЧИТЬ ПО
	|	КанбанЛидов.Порядок УБЫВ";
	Запрос.Параметры.Вставить("Состояние", Состояние);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Порядок = Выборка.Порядок + 1;
	КонецЕсли;
	
	Возврат Порядок;
	
КонецФункции

#КонецОбласти

#КонецЕсли
