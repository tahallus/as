#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СвойстваЗаказа(Знач ЗаказПокупателя) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторЗаказа", "");
	Результат.Вставить("НомерЗаказа", "");
	Результат.Вставить("Статус", Перечисления.СтатусыЯндексДоставки.ПустаяСсылка());
	
	Если Не ЗначениеЗаполнено(ЗаказПокупателя) Тогда
		Возврат Результат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЯндексДоставка.ИдентификаторЗаказа,
	|	ЯндексДоставка.НомерЗаказа,
	|	ЯндексДоставка.Статус
	|ИЗ
	|	РегистрСведений.ЯндексДоставка КАК ЯндексДоставка
	|ГДЕ
	|	ЯндексДоставка.ЗаказПокупателя = &ЗаказПокупателя");
	Запрос.УстановитьПараметр("ЗаказПокупателя", ЗаказПокупателя);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(Результат, Выборка);
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаписатьСвойстваЗаказа(Знач ЗаказПокупателя, Знач СпособОтгрузки, Знач Свойства) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ЗаказПокупателя = ЗаказПокупателя;
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.ЗаказПокупателя = ЗаказПокупателя;
	МенеджерЗаписи.ИдентификаторЗаказа = Свойства["id"];
	МенеджерЗаписи.НомерЗаказа = Свойства["full_num"];
	МенеджерЗаписи.СпособОтгрузки = СпособОтгрузки;
	МенеджерЗаписи.Статус = ЯндексДоставкаКлиентСервер.СтатусПоКоду(Свойства["status"]);
	МенеджерЗаписи.ИзмененЛокально = ТекущаяУниверсальнаяДата();
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура ОбновитьСтатусЗаказа(Знач ЗаказПокупателя, Знач Статус) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ЗаказПокупателя = ЗаказПокупателя;
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.ЗаказПокупателя = ЗаказПокупателя;
	МенеджерЗаписи.Статус = Статус;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура УстановитьОтметкуИзмененЛокально(Знач ЗаказПокупателя) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ЗаказПокупателя = ЗаказПокупателя;
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.ЗаказПокупателя = ЗаказПокупателя;
	МенеджерЗаписи.ИзмененЛокально = ТекущаяУниверсальнаяДата();
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
