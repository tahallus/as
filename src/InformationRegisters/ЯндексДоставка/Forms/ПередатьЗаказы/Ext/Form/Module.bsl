
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиентСервер.Проверить(
	Параметры.Свойство("Заказы"),
	НСтр("ru = 'Не задан параметр ""Заказы"".'"),
	"РС.ЯндексДоставка.ПередатьЗаказы");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗаказыПокупателя.Ссылка КАК ЗаказПокупателя,
	|	ЗаказыПокупателя.СлужбаДоставки КАК СлужбаДоставки,
	|	ЗаказыПокупателя.СостояниеЗаказа КАК СостояниеЗаказа,
	|	ЯндексДоставка.НомерЗаказа КАК НомерВЯндексДоставке,
	|	ЯндексДоставка.ИдентификаторЗаказа КАК ИдентификаторЯндексДоставки
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказыПокупателя
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЯндексДоставка КАК ЯндексДоставка
	|		ПО ЗаказыПокупателя.Ссылка = ЯндексДоставка.ЗаказПокупателя
	|ГДЕ
	|	ЗаказыПокупателя.Ссылка В(&Заказы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказыПокупателя.Дата");
	Запрос.УстановитьПараметр("Заказы", Параметры.Заказы);
	
	ПередаваемыеЗаказы.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Элементы.СтраницыСтатусФоновогоЗадания.ТекущаяСтраница = Элементы.ЗаданиеВыполняется;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("НачатьПередачуЗаказов", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачатьПередачуЗаказов()
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения("ОбработатьПрогресс", ЭтотОбъект);
	
	Задание = ЗаданиеПередатьЗаказыВЯндексДоставку();
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
	Задание,
	Новый ОписаниеОповещения("ОбработатьЗавершениеЗадания", ЭтотОбъект),
	ПараметрыОжидания);

	
КонецПроцедуры

&НаСервере
Функция ЗаданиеПередатьЗаказыВЯндексДоставку()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
	"ЯндексДоставка.ЗаданиеПередатьЗаказы",
	ПередаваемыеЗаказы.Выгрузить(),
	ПараметрыВыполнения);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьПрогресс(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Результат.Прогресс) = Тип("Структура") Тогда
		ОтобразитьРезультатыОтправки(Результат.Прогресс.ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗавершениеЗадания(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатыОтправки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		ОтобразитьРезультатыОтправки(РезультатыОтправки);
		Элементы.СтраницыСтатусФоновогоЗадания.Видимость = Ложь;
		ПоказатьОповещениеПользователя(НСтр("ru = 'Заказы выгружены в Яндекс.Доставку'"));
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		Элементы.СтраницыСтатусФоновогоЗадания.ТекущаяСтраница = Элементы.ЗаданиеЗавершеноСОшибкой;
		Элементы.ПодробноеПредставлениеОшибки.Заголовок = Результат.ПодробноеПредставлениеОшибки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьРезультатыОтправки(РезультатыОтправки)
	
	Если ТипЗнч(РезультатыОтправки) <> Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекРезультат Из РезультатыОтправки Цикл
		
		ОтборЗаказПокупателя = Новый Структура("ЗаказПокупателя", ТекРезультат.Ключ);
		НайденныеСтроки = ПередаваемыеЗаказы.НайтиСтроки(ОтборЗаказПокупателя);
		Если Не ЗначениеЗаполнено(НайденныеСтроки) Тогда
			Продолжить;
		КонецЕсли;
		
		НайденныеСтроки[0].РезультатПередачи = ТекРезультат.Значение["status"];
		
		Если ТекРезультат.Значение["status"] = "ok" Тогда
			
			НайденныеСтроки[0].НомерВЯндексДоставке = ТекРезультат.Значение["data"]["order"]["full_num"];
			НайденныеСтроки[0].ИдентификаторЯндексДоставки = ТекРезультат.Значение["data"]["order"]["id"];
			НайденныеСтроки[0].РезультатПередачи = ТекРезультат.Значение["status"];
			
		ИначеЕсли ТекРезультат.Значение["status"] = "error" Тогда
			
			Ошибки = Новый Массив;
			Для Каждого ТекОшибка Из ТекРезультат.Значение["data"]["errors"] Цикл
				Если ТипЗнч(ТекОшибка) = Тип("Строка") Тогда
					Ошибки.Добавить(ТекОшибка);
				Иначе
					Ошибки.Добавить(СтрШаблон("%1: %2", ТекОшибка.Ключ, ТекОшибка.Значение));
				КонецЕсли;
			КонецЦикла;
			НайденныеСтроки[0].РезультатПередачи =  СтрСоединить(Ошибки, Символы.ПС);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередаваемыеЗаказыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ПередаваемыеЗаказыНомерВЯндексДоставке Тогда
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(
			СтрШаблон("https://delivery.yandex.ru/order/create?id=%1", Элемент.ТекущиеДанные.ИдентификаторЯндексДоставки));
	Иначе
		ПоказатьЗначение(, Элемент.ТекущиеДанные.ЗаказПокупателя);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти