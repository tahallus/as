#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция Значения(Номенклатура, ЕдиницаИзмерения, Коэффициент) Экспорт
	
	Если ТипЗнч(ЕдиницаИзмерения) <> Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
		Возврат ВесИОбъемБазовойЕдиницы(Номенклатура, Коэффициент);
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВесИОбъемЕдиницТоваров.Вес,
	|	ВесИОбъемЕдиницТоваров.Объем
	|ИЗ
	|	РегистрСведений.ВесИОбъемЕдиницТоваров КАК ВесИОбъемЕдиницТоваров
	|ГДЕ
	|	ВесИОбъемЕдиницТоваров.Номенклатура = &Номенклатура
	|	И ВесИОбъемЕдиницТоваров.ЕдиницаИзмерения = &ЕдиницаИзмерения");
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ЕдиницаИзмерения", ЕдиницаИзмерения);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ВесИОбъемБазовойЕдиницы(Номенклатура, Коэффициент);
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат НовыйРезультат(Выборка.Вес, Выборка.Объем);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВесИОбъемБазовойЕдиницы(Номенклатура, Коэффициент)
	
	ВесИОбъемБазовойЕдиницы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "Вес,Объем");
	Возврат НовыйРезультат(ВесИОбъемБазовойЕдиницы.Вес * Коэффициент, ВесИОбъемБазовойЕдиницы.Объем * Коэффициент)
	
КонецФункции

Функция НовыйРезультат(ВесЕдиницыТовара, ОбъемЕдиницыТовара)
	
	Результат = Новый Структура;
	Результат.Вставить("ВесЕдиницыТовара", ВесЕдиницыТовара);
	Результат.Вставить("ОбъемЕдиницыТовара", ОбъемЕдиницыТовара);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли