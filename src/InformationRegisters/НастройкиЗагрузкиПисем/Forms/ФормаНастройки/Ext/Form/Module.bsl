
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("УчетнаяЗапись", УчетнаяЗапись);
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ВызватьИсключение НСтр("ru = 'Не указана учетная запись для настройки почтовых ящиков.'");
	КонецЕсли;
	
	Если Не НастройкиСуществуют(УчетнаяЗапись) Тогда
		ЭлектроннаяПочтаУНФ.ОбновитьПочтовыеПапки(УчетнаяЗапись);
	КонецЕсли;
	
	ЗаполнитьТаблицуНастроек();
	
	Если Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиЗагрузкиПисем) Тогда
		Элементы.ФормаОбновитьПочтовыеПапки.Доступность = Ложь;
		Элементы.НастройкиЗагрузкиПисем.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗагрузкиПисемПриИзмененииОбщий(Элемент)
	
	ТекущиеДанные = Элементы.НастройкиЗагрузкиПисем.ТекущиеДанные;
	
	ЗаписатьНастройкуПочтовогоЯщика(
	УчетнаяЗапись,
	ТекущиеДанные.ПочтоваяПапка,
	ТекущиеДанные.ВариантЗагрузки,
	ТекущиеДанные.ВариантСинхронизации,
	ТекущиеДанные.ВариантСостояния,
	ТекущиеДанные.ОтветственныйДляНовыхПисем);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьПочтовыеПапки(Команда)
	
	ОбновитьПочтовыеЯщикиНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция НастройкиСуществуют(Знач УчетнаяЗапись)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиЗагрузкиПисем.УчетнаяЗапись КАК УчетнаяЗапись
	|ИЗ
	|	РегистрСведений.НастройкиЗагрузкиПисем КАК НастройкиЗагрузкиПисем
	|ГДЕ
	|	НастройкиЗагрузкиПисем.УчетнаяЗапись = &УчетнаяЗапись");
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуНастроек()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкиЗагрузкиПисем.ПочтоваяПапка КАК ПочтоваяПапка,
	|	НастройкиЗагрузкиПисем.ВариантЗагрузки КАК ВариантЗагрузки,
	|	НастройкиЗагрузкиПисем.ВариантСинхронизации КАК ВариантСинхронизации,
	|	НастройкиЗагрузкиПисем.ВариантСостояния КАК ВариантСостояния,
	|	НастройкиЗагрузкиПисем.ОтветственныйДляНовыхПисем КАК ОтветственныйДляНовыхПисем
	|ИЗ
	|	РегистрСведений.НастройкиЗагрузкиПисем КАК НастройкиЗагрузкиПисем
	|ГДЕ
	|	НастройкиЗагрузкиПисем.УчетнаяЗапись = &УчетнаяЗапись");
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		НастройкиЗагрузкиПисем.Загрузить(РезультатЗапроса.Выгрузить());
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПочтовыеЯщикиНаСервере()
	
	ЭлектроннаяПочтаУНФ.ОбновитьПочтовыеПапки(УчетнаяЗапись);
	ЗаполнитьТаблицуНастроек();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьНастройкуПочтовогоЯщика(
	Знач УчетнаяЗапись,
	Знач ПочтоваяПапка,
	Знач ВариантЗагрузки,
	Знач ВариантСинхронизации,
	Знач ВариантСостояния,
	Знач ОтветственныйДляНовыхПисем)
	
	МенеджерЗаписи = РегистрыСведений.НастройкиЗагрузкиПисем.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
	МенеджерЗаписи.ПочтоваяПапка = ПочтоваяПапка;
	МенеджерЗаписи.ВариантЗагрузки = ВариантЗагрузки;
	МенеджерЗаписи.ВариантСинхронизации = ВариантСинхронизации;
	МенеджерЗаписи.ВариантСостояния = ВариантСостояния;
	МенеджерЗаписи.ОтветственныйДляНовыхПисем = ОтветственныйДляНовыхПисем;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти