
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	МассивНоменклатуры = Параметры.МассивНоменклатуры;
	ТекШтрихкод = ОбщегоНазначения.СкопироватьРекурсивно(Параметры.ТекШтрихкод);
	
	Для Каждого ЭлементМассива Из МассивНоменклатуры Цикл
		
		НоваяСтрока = ШтрихкодыНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементМассива);
		
		Если  НоваяСтрока.Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Запас Тогда 
			ВариантКартинки = 0;
		ИначеЕсли НоваяСтрока.Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.ПодарочныйСертификат Тогда
			ВариантКартинки = 4
			Иначе
			ВариантКартинки = 2
		КонецЕсли;
		
		Если НоваяСтрока.Номенклатура.ИспользоватьХарактеристики Тогда 
			ВариантКартинки = ВариантКартинки + 1;
		КонецЕсли;
		
		Если НоваяСтрока.Номенклатура.ПометкаУдаления Тогда 
			ВариантКартинки = ВариантКартинки + 6;
			НоваяСтрока.ПометкаУдаления = Истина;
		КонецЕсли;
		
		Если НоваяСтрока.Номенклатура.ЭтоНабор Тогда 
			ВариантКартинки = ВариантКартинки + 12;
		КонецЕсли;
		
		НоваяСтрока.ВариантКартинки = ВариантКартинки;
		
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ШтрихкодыНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ПеренестиДанныеВДокумент();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	ПеренестиДанныеВДокумент();
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	ЭтаФорма.Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПеренестиДанныеВДокумент()
	
	ТекущиеДанные = Элементы.ШтрихкодыНоменклатуры.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат КонецЕсли;
	
	Если ТекущиеДанные.ПометкаУдаления Тогда
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Номенклатура помечена на удаление. Продолжить?'"), Режим, 0);
	Иначе
		ПеренестиДанныеВДокументЗавершение();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиДанныеВДокументЗавершение()
	
	ТекущиеДанные = Элементы.ШтрихкодыНоменклатуры.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат КонецЕсли;
	
	МассивСтрок = Новый Массив;
	СтруктураШК = Новый Структура;
	
	СтруктураШК.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
	СтруктураШК.Вставить("Характеристика", ТекущиеДанные.Характеристика);
	СтруктураШК.Вставить("Партия", ТекущиеДанные.Партия);
	СтруктураШК.Вставить("ЕдиницаИзмерения", ТекущиеДанные.ЕдиницаИзмерения);
	СтруктураШК.Вставить("Серия", ТекущиеДанные.Серия);
	СтруктураШК.Вставить("СтруктураДанныеНоменклатуры", ТекущиеДанные.СтруктураДанныеНоменклатуры);
	
	МассивСтрок.Добавить(СтруктураШК);
	
	СтруктураЗакрытия = Новый Структура("МассивНоменклатуры, ТекШтрихкод", МассивСтрок , ТекШтрихкод);
	
	ЭтаФорма.Закрыть(СтруктураЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ПеренестиДанныеВДокументЗавершение();

КонецПроцедуры

#КонецОбласти