
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ВладелецФайлов" , ВладелецФайлов);
	
	ОбновитьТаблицуФайлов();
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецФайловПриИзменении(Элемент)
	
	ОбновитьТаблицуФайлов();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаФайловПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаФайловПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаФайловВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЗначение(,Элементы.ТаблицаФайлов.ТекущиеДанные.Файл);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИзменения(Команда)
	
	ЗаписатьИзмененияНаСервере();
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьВсе(Команда)
	УстановитьСнятьПометкуНаКлиенте(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометкиВсе(Команда)
	УстановитьСнятьПометкуНаКлиенте(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьИзмененияНаСервере()
	
	НачатьТранзакцию();
	
	Попытка
		
		Сортировка = 1;
		Для Каждого Стр Из ТаблицаФайлов Цикл
			Стр.Сортировка = Сортировка;
			Сортировка = Сортировка + 1;
			
			МЗ = РегистрыСведений.СортировкаФайловДляСайта.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МЗ , Стр);
			МЗ.Записать();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ОбщегоНазначения.СообщитьПользователю(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицуФайлов()
	
	СтруктураОсновныхПараметровОбмена = ОбменССайтом.ПолучитьСтруктуруОсновныхПараметровОбмена();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураПрисоединенныеФайлы.Ссылка КАК Файл,
	|	ЕСТЬNULL(СортировкаФайловДляСайта.Сортировка, 0) КАК Сортировка,
	|	ЕСТЬNULL(СортировкаФайловДляСайта.ВыгружатьНаСайт, ИСТИНА) КАК ВыгружатьНаСайт,
	|	&ВладелецФайлов КАК ВладелецФайлов,
	|	НоменклатураПрисоединенныеФайлы.ДатаСоздания КАК ДатаСоздания,
	|	НоменклатураПрисоединенныеФайлы.ДатаМодификацииУниверсальная КАК ДатаМодификацииУниверсальная
	|ИЗ
	|	Справочник.НоменклатураПрисоединенныеФайлы КАК НоменклатураПрисоединенныеФайлы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СортировкаФайловДляСайта КАК СортировкаФайловДляСайта
	|		ПО ((ВЫРАЗИТЬ(СортировкаФайловДляСайта.Файл КАК Справочник.НоменклатураПрисоединенныеФайлы)) = НоменклатураПрисоединенныеФайлы.Ссылка)
	|ГДЕ
	|	НоменклатураПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайлов
	|	И НЕ НоменклатураПрисоединенныеФайлы.ПометкаУдаления
	|	И НоменклатураПрисоединенныеФайлы.Расширение В(&РазрешенныеТипыКартинок)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХарактеристикиНоменклатурыПрисоединенныеФайлы.Ссылка,
	|	ЕСТЬNULL(СортировкаФайловДляСайта.Сортировка, 0),
	|	ЕСТЬNULL(СортировкаФайловДляСайта.ВыгружатьНаСайт, ИСТИНА),
	|	&ВладелецФайлов,
	|	ХарактеристикиНоменклатурыПрисоединенныеФайлы.ДатаСоздания,
	|	ХарактеристикиНоменклатурыПрисоединенныеФайлы.ДатаМодификацииУниверсальная
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатурыПрисоединенныеФайлы КАК ХарактеристикиНоменклатурыПрисоединенныеФайлы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СортировкаФайловДляСайта КАК СортировкаФайловДляСайта
	|		ПО ((ВЫРАЗИТЬ(СортировкаФайловДляСайта.Файл КАК Справочник.ХарактеристикиНоменклатурыПрисоединенныеФайлы)) = ХарактеристикиНоменклатурыПрисоединенныеФайлы.Ссылка)
	|ГДЕ
	|	ХарактеристикиНоменклатурыПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайлов
	|	И НЕ ХарактеристикиНоменклатурыПрисоединенныеФайлы.ПометкаУдаления
	|	И ХарактеристикиНоменклатурыПрисоединенныеФайлы.Расширение В(&РазрешенныеТипыКартинок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сортировка,
	|	ДатаСоздания,
	|	ДатаМодификацииУниверсальная";
	
	Запрос.УстановитьПараметр("ВладелецФайлов", ВладелецФайлов);
	Запрос.УстановитьПараметр("РазрешенныеТипыКартинок", СтруктураОсновныхПараметровОбмена.РазрешенныеТипыКартинок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаФайлов.Загрузить(РезультатЗапроса.Выгрузить());
	
	МаксЗначениеСортировки = ТаблицаФайлов.Количество();
	
	Для Каждого Стр Из ТаблицаФайлов Цикл
		ДвоичныеДанныеФайла = РаботаСФайлами.ДвоичныеДанныеФайла(Стр.Файл, Ложь);
		Стр.Картинка = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, Новый УникальныйИдентификатор);
	КонецЦикла;
	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСнятьПометкуНаКлиенте(Пометка)
	Для Каждого Стр Из ТаблицаФайлов Цикл
		Стр.ВыгружатьНаСайт = Пометка;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти