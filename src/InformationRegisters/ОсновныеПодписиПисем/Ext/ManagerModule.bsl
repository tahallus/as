#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область ПрограммныйИнтерфейс

// Очистка записей
//
// Параметры:
//  Подпись - СправочникСсылка.ПодписиПисем
Процедура ОчиститьЗаписиСПодписью(Подпись) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОсновныеПодписиПисем.Пользователь КАК Пользователь,
	|	ОсновныеПодписиПисем.УчетнаяЗапись КАК УчетнаяЗапись,
	|	ОсновныеПодписиПисем.Подпись КАК Подпись
	|ИЗ
	|	РегистрСведений.ОсновныеПодписиПисем КАК ОсновныеПодписиПисем
	|ГДЕ
	|	ОсновныеПодписиПисем.Подпись = &Подпись";
	
	Запрос.УстановитьПараметр("Подпись", Подпись);
	
	НачатьТранзакцию();
	Попытка
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОсновныеПодписиПисем");
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Пользователь", "ВладелецПодписи");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("УчетнаяЗапись", "УчетнаяЗапись");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		
		Блокировка.Заблокировать();
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = РегистрыСведений.ОсновныеПодписиПисем.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Пользователь.Установить(Выборка.Пользователь);
			НаборЗаписей.Отбор.УчетнаяЗапись.Установить(Выборка.УчетнаяЗапись);
			НаборЗаписей.Записать(Истина);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ОсновныеПодписиПисем.ОчиститьЗаписиСПодписью'",
		ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Ошибка,
		Неопределено,
		Неопределено,
		ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
КонецПроцедуры

#КонецОбласти

#КонецЕсли
