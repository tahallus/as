#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ДобавитьСообщение(НомерСообщенияОбмена, СообщениеОбмена, КодУзла, Версия) Экспорт
	
	НаборЗаписей = РегистрыСведений.СформированныеСообщенияДляОтправкиМП.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.НомерСообщения.Установить(НомерСообщенияОбмена);
	НаборЗаписей.Отбор.КодУзла.Установить(КодУзла);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.НомерСообщения = НомерСообщенияОбмена;
	НоваяЗапись.КодУзла = КодУзла;
	НоваяЗапись.СообщениеОбмена = СообщениеОбмена;
	НоваяЗапись.Версия = Версия;
	
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры 

Процедура УдалитьСообщение(Знач НомерСообщения, Знач КодКлиентскогоУзла) Экспорт
	
	НаборЗаписей = РегистрыСведений.СформированныеСообщенияДляОтправкиМП.СоздатьНаборЗаписей();
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	НаборЗаписей.Отбор.НомерСообщения.Установить(НомерСообщения);
	НаборЗаписей.Отбор.КодУзла.Установить(КодКлиентскогоУзла);
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

Функция ПолучитьСообщение(КодУзла, Версия, СообщениеОбмена, НомерСообщения) Экспорт //Для серверного узла

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СформированныеСообщенияДляОтправкиМП.НомерСообщения КАК НомерСообщения,
		|	СформированныеСообщенияДляОтправкиМП.КодУзла КАК КодУзла,
		|	СформированныеСообщенияДляОтправкиМП.Версия КАК Версия,
		|	СформированныеСообщенияДляОтправкиМП.СообщениеОбмена КАК СообщениеОбмена
		|ИЗ
		|	РегистрСведений.СформированныеСообщенияДляОтправкиМП КАК СформированныеСообщенияДляОтправкиМП
		|ГДЕ
		|	СформированныеСообщенияДляОтправкиМП.КодУзла = &КодУзла
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСообщения";
	
	Запрос.УстановитьПараметр("КодУзла", КодУзла);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Версия = ВыборкаДетальныеЗаписи.Версия;
		СообщениеОбмена = ВыборкаДетальныеЗаписи.СообщениеОбмена;
		НомерСообщения = ВыборкаДетальныеЗаписи.НомерСообщения;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСообщения() Экспорт //Для клиентского узла
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СформированныеСообщенияДляОтправкиМП.НомерСообщения КАК НомерСообщения,
		|	СформированныеСообщенияДляОтправкиМП.СообщениеОбмена КАК СообщениеОбмена,
		|	СформированныеСообщенияДляОтправкиМП.Версия КАК Версия		
		|ИЗ
		|	РегистрСведений.СформированныеСообщенияДляОтправкиМП КАК СформированныеСообщенияДляОтправкиМП
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСообщения";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	

	Возврат ВыборкаДетальныеЗаписи;

КонецФункции 

#КонецОбласти

#КонецЕсли
