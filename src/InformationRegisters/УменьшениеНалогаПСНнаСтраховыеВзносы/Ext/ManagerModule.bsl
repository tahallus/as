#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает данные об уменьшениях налога по патентной системе налогооблоожения на суммы
// страховых взносов и больничных пособий
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация, по которой осуществляется отбор данных
//  Дата - Дата - определяет год, за который будут получены данные
//  Патент - СправочникСсылка.Патенты - если заполнен, то данные будут получены с отбором по патенту
//
// Возвращаемое значение:
//  Соответствие - * Ключ - СправочникСсылка.Патенты
//                 * Значение - Структура - см. НовыеСведенияУменьшениеНалогаПоПатенту
//
Функция СведенияУменьшениеНалогаЗаГод(Организация, Дата, Патент = Неопределено) Экспорт
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(
		"ВЫБРАТЬ
		|	УменьшениеНалоговПСН.Патент КАК Патент,
		|	УменьшениеНалоговПСН.Уведомление КАК Уведомление,
		|	УменьшениеНалоговПСН.Сумма КАК Сумма,
		|	УменьшениеНалоговПСН.Уведомление.ДатаПодписи КАК Дата
		|ИЗ
		|	РегистрСведений.УменьшениеНалогаПСНнаСтраховыеВзносы КАК УменьшениеНалоговПСН
		|ГДЕ
		|	УменьшениеНалоговПСН.Организация = &Организация
		|	И &ОтборПоПатенту
		|	И НЕ УменьшениеНалоговПСН.Уведомление.ПометкаУдаления
		|ИТОГИ
		|	СУММА(Сумма)
		|ПО
		|	Патент");
	УсловияЗапроса = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор;
	Если ЗначениеЗаполнено(Патент) Тогда
		УсловияЗапроса.Установить(1, Новый ВыражениеСхемыЗапроса("УменьшениеНалоговПСН.Патент = &Патент"));
	Иначе
		УсловияЗапроса.Установить(1, Новый ВыражениеСхемыЗапроса("УменьшениеНалоговПСН.Патент.ДатаНачала МЕЖДУ &ДатаНачала И &ДатаОкончания"));
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Патент", Патент);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоГода(Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецГода(Дата));
	
	ВыборкаПатент = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	СведенияУменьшениеНалогаОбщие = Новый Соответствие;
	
	Пока ВыборкаПатент.Следующий() Цикл
		СведенияУменьшениеНалогаПоПатенту = НовыеСведенияУменьшениеНалогаПоПатенту();
		СведенияУменьшениеНалогаПоПатенту.Сумма = ВыборкаПатент.Сумма;
		ВыборкаДетальная = ВыборкаПатент.Выбрать();
		Пока ВыборкаДетальная.Следующий() Цикл
			РеквизитыУведомления = НовыеСведенияОбУведомлении();
			ЗаполнитьЗначенияСвойств(РеквизитыУведомления, ВыборкаДетальная);
			СведенияУменьшениеНалогаПоПатенту.Уведомления.Добавить(РеквизитыУведомления);
		КонецЦикла;
		СведенияУменьшениеНалогаОбщие.Вставить(ВыборкаПатент.Патент, СведенияУменьшениеНалогаПоПатенту);
	КонецЦикла;
	
	Возврат СведенияУменьшениеНалогаОбщие;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыеСведенияУменьшениеНалогаПоПатенту()
	
	Сведения = Новый Структура;
	Сведения.Вставить("Сумма", 0);
	Сведения.Вставить("Уведомления", Новый Массив);
	Возврат Сведения;
	
КонецФункции

Функция НовыеСведенияОбУведомлении()
	
	Сведения = Новый Структура;
	
	Сведения.Вставить("Уведомление", Документы.УведомлениеОСпецрежимахНалогообложения.ПустаяСсылка());
	Сведения.Вставить("Сумма", 0);
	Сведения.Вставить("Дата", Дата(1, 1, 1));
	
	Возврат Сведения;
	
КонецФункции

#КонецОбласти

#КонецЕсли