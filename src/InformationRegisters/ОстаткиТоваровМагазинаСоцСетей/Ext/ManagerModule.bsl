#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиЗаписи
	
// Производит запись в служебный регистр сведений данных по остатками и резервам номенклатуры
//
Процедура РассчитатьОстатокТовараДляБыстрогоОтображения(ДополнительныеСвойства, ЧислоДвижений, ИмяТаблицыЗапасы = "ДвиженияЗапасыНаСкладахИзменение") Экспорт
	
	Если НЕ ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы[ИмяТаблицыЗапасы] Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		
		Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
			Запрос.Текст = ТекстЗапросаФайловыйРежим();
		Иначе
			Запрос.Текст = ТекстЗапросаКлиентСерверныйРежим();
		КонецЕсли;
	
	Если НЕ ИмяТаблицыЗапасы = "ДвиженияЗапасыНаСкладахИзменение" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ДвиженияЗапасыНаСкладахИзменение", ИмяТаблицыЗапасы)
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	НаборЗаписей = РегистрыСведений.ОстаткиТоваров.СоздатьНаборЗаписей();
	Пока Выборка.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(Выборка.Номенклатура) Тогда Продолжить КонецЕсли;
		
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Отбор.СтруктурнаяЕдиница.Установить(Выборка.СтруктурнаяЕдиница);
		НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
		НаборЗаписей.Отбор.Характеристика.Установить(Выборка.Характеристика);
		НаборЗаписей.Отбор.Партия.Установить(Выборка.Партия);
		
		НовСтр = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
		НовСтр.Количество = НовСтр.Количество - НовСтр.Резерв;
		
		Если НовСтр.Количество = 0 И НовСтр.Резерв = 0 Тогда
			НаборЗаписей.Очистить();
		КонецЕсли;
		
		НаборЗаписей.Записать(Истина);
		НаборЗаписей.Очистить();
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаФайловыйРежим()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК Номенклатура,
	|	ДвиженияЗапасыНаСкладахИзменение.Организация КАК Организация,
	|	ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ДвиженияЗапасыНаСкладахИзменение.Характеристика КАК Характеристика,
	|	ДвиженияЗапасыНаСкладахИзменение.Партия КАК Партия
	|ПОМЕСТИТЬ ВременнаяТаблица1
	|ИЗ
	|	ДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияЗапасыНаСкладахИзменение.Организация,
	|	ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница,
	|	ДвиженияЗапасыНаСкладахИзменение.Партия,
	|	ДвиженияЗапасыНаСкладахИзменение.Характеристика,
	|	ДвиженияЗапасыНаСкладахИзменение.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблица1.Номенклатура КАК Номенклатура,
	|	ВременнаяТаблица1.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВременнаяТаблица1.Организация КАК Организация,
	|	ВременнаяТаблица1.Партия КАК Партия,
	|	ВременнаяТаблица1.Характеристика КАК Характеристика,
	|	СУММА(ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0)) КАК КоличествоФинКонтур,
	|	ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя
	|ПОМЕСТИТЬ ВтФинКонтур
	|ИЗ
	|	ВременнаяТаблица1 КАК ВременнаяТаблица1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Запасы.Остатки(, ТИПЗНАЧЕНИЯ(СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)) КАК ЗапасыОстатки
	|		ПО ВременнаяТаблица1.Номенклатура = ЗапасыОстатки.Номенклатура
	|			И ВременнаяТаблица1.СтруктурнаяЕдиница = ЗапасыОстатки.СтруктурнаяЕдиница
	|			И ВременнаяТаблица1.Организация = ЗапасыОстатки.Организация
	|			И ВременнаяТаблица1.Партия = ЗапасыОстатки.Партия
	|			И ВременнаяТаблица1.Характеристика = ЗапасыОстатки.Характеристика
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблица1.Номенклатура,
	|	ВременнаяТаблица1.СтруктурнаяЕдиница,
	|	ВременнаяТаблица1.Организация,
	|	ВременнаяТаблица1.Характеристика,
	|	ВременнаяТаблица1.Партия,
	|	ЗапасыОстатки.ЗаказПокупателя
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтФинКонтур.Номенклатура КАК Номенклатура,
	|	ВтФинКонтур.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВтФинКонтур.Организация КАК Организация,
	|	ВтФинКонтур.Партия КАК Партия,
	|	ВтФинКонтур.Характеристика КАК Характеристика,
	|	СУММА(ЕСТЬNULL(ВтФинКонтур.КоличествоФинКонтур, 0)) КАК КоличествоФинКонтурРезерв
	|ПОМЕСТИТЬ ВтФинКонтурРезерв
	|ИЗ
	|	ВтФинКонтур КАК ВтФинКонтур
	|ГДЕ
	|	НЕ ВтФинКонтур.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтФинКонтур.Номенклатура,
	|	ВтФинКонтур.СтруктурнаяЕдиница,
	|	ВтФинКонтур.Организация,
	|	ВтФинКонтур.Партия,
	|	ВтФинКонтур.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблица1.Номенклатура КАК Номенклатура,
	|	ВременнаяТаблица1.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВременнаяТаблица1.Организация КАК Организация,
	|	ВременнаяТаблица1.Партия КАК Партия,
	|	ВременнаяТаблица1.Характеристика КАК Характеристика,
	|	СУММА(ВтФинКонтур.КоличествоФинКонтур) КАК Количество,
	|	ВтФинКонтурРезерв.КоличествоФинКонтурРезерв КАК Резерв
	|ИЗ
	|	ВременнаяТаблица1 КАК ВременнаяТаблица1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтФинКонтур КАК ВтФинКонтур
	|		ПО ВременнаяТаблица1.Номенклатура = ВтФинКонтур.Номенклатура
	|			И ВременнаяТаблица1.СтруктурнаяЕдиница = ВтФинКонтур.СтруктурнаяЕдиница
	|			И ВременнаяТаблица1.Организация = ВтФинКонтур.Организация
	|			И ВременнаяТаблица1.Партия = ВтФинКонтур.Партия
	|			И ВременнаяТаблица1.Характеристика = ВтФинКонтур.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтФинКонтурРезерв КАК ВтФинКонтурРезерв
	|		ПО ВременнаяТаблица1.Номенклатура = ВтФинКонтурРезерв.Номенклатура
	|			И ВременнаяТаблица1.СтруктурнаяЕдиница = ВтФинКонтурРезерв.СтруктурнаяЕдиница
	|			И ВременнаяТаблица1.Организация = ВтФинКонтурРезерв.Организация
	|			И ВременнаяТаблица1.Партия = ВтФинКонтурРезерв.Партия
	|			И ВременнаяТаблица1.Характеристика = ВтФинКонтурРезерв.Характеристика
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблица1.Номенклатура,
	|	ВременнаяТаблица1.СтруктурнаяЕдиница,
	|	ВременнаяТаблица1.Организация,
	|	ВременнаяТаблица1.Характеристика,
	|	ВременнаяТаблица1.Партия,
	|	ВтФинКонтурРезерв.КоличествоФинКонтурРезерв";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаКлиентСерверныйРежим()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК Номенклатура,
	|	ДвиженияЗапасыНаСкладахИзменение.Организация КАК Организация,
	|	ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ДвиженияЗапасыНаСкладахИзменение.Характеристика КАК Характеристика,
	|	ДвиженияЗапасыНаСкладахИзменение.Партия КАК Партия
	|ПОМЕСТИТЬ ВТДвиженияЗапасыНаСкладахИзменение
	|ИЗ
	|	ДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияЗапасыНаСкладахИзменение.Организация,
	|	ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница,
	|	ДвиженияЗапасыНаСкладахИзменение.Партия,
	|	ДвиженияЗапасыНаСкладахИзменение.Характеристика,
	|	ДвиженияЗапасыНаСкладахИзменение.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(ЗапасыОстаткиРезерв.КоличествоОстаток, 0)) КАК Резерв,
	|	ВТДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК Номенклатура,
	|	ВТДвиженияЗапасыНаСкладахИзменение.Организация КАК Организация,
	|	ВТДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВТДвиженияЗапасыНаСкладахИзменение.Характеристика КАК Характеристика,
	|	ВТДвиженияЗапасыНаСкладахИзменение.Партия КАК Партия,
	|	СУММА(ЕСТЬNULL(ЗапасыОстаткиКоличествоФинКонтур.КоличествоОстаток, 0)) КАК Количество
	|ИЗ
	|	ВТДвиженияЗапасыНаСкладахИзменение КАК ВТДвиженияЗапасыНаСкладахИзменение
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Запасы.Остатки(
	|				,
	|				НЕ ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|					И ТИПЗНАЧЕНИЯ(СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)) КАК ЗапасыОстаткиРезерв
	|		ПО ВТДвиженияЗапасыНаСкладахИзменение.Организация = ЗапасыОстаткиРезерв.Организация
	|			И ВТДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница = ЗапасыОстаткиРезерв.СтруктурнаяЕдиница
	|			И ВТДвиженияЗапасыНаСкладахИзменение.Номенклатура = ЗапасыОстаткиРезерв.Номенклатура
	|			И ВТДвиженияЗапасыНаСкладахИзменение.Характеристика = ЗапасыОстаткиРезерв.Характеристика
	|			И ВТДвиженияЗапасыНаСкладахИзменение.Партия = ЗапасыОстаткиРезерв.Партия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Запасы.Остатки(, ТИПЗНАЧЕНИЯ(СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)) КАК ЗапасыОстаткиКоличествоФинКонтур
	|		ПО ВТДвиженияЗапасыНаСкладахИзменение.Организация = ЗапасыОстаткиКоличествоФинКонтур.Организация
	|			И ВТДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница = ЗапасыОстаткиКоличествоФинКонтур.СтруктурнаяЕдиница
	|			И ВТДвиженияЗапасыНаСкладахИзменение.Номенклатура = ЗапасыОстаткиКоличествоФинКонтур.Номенклатура
	|			И ВТДвиженияЗапасыНаСкладахИзменение.Характеристика = ЗапасыОстаткиКоличествоФинКонтур.Характеристика
	|			И ВТДвиженияЗапасыНаСкладахИзменение.Партия = ЗапасыОстаткиКоличествоФинКонтур.Партия
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДвиженияЗапасыНаСкладахИзменение.Характеристика,
	|	ВТДвиженияЗапасыНаСкладахИзменение.Номенклатура,
	|	ВТДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница,
	|	ВТДвиженияЗапасыНаСкладахИзменение.Партия,
	|	ВТДвиженияЗапасыНаСкладахИзменение.Организация";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Производит запись в служебный регистр сведений данных по всем остатками и резервам номенклатуры
//
Функция ВыполнитьЗаданиеПоЗаписиОстатковВТехническийРегистр() Экспорт
	
	НаборЗаписей = РегистрыСведений.ОстаткиТоваров.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОстаткиТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗапасыОстатки.Организация КАК Организация,
		|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
		|	ЗапасыОстатки.Характеристика КАК Характеристика,
		|	ЗапасыОстатки.Партия КАК Партия,
		|	СУММА(ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0)) КАК Резерв,
		|	СУММА(0) КАК Количество
		|ПОМЕСТИТЬ Итог
		|ИЗ
		|	РегистрНакопления.Запасы.Остатки(
		|			,
		|			НЕ ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|				И ТИПЗНАЧЕНИЯ(СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)) КАК ЗапасыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Партия,
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.Номенклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Номенклатура,
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Партия,
		|	СУММА(0),
		|	СУММА(ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0))
		|ИЗ
		|	РегистрНакопления.Запасы.Остатки(
		|			,
		|			ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|				И ТИПЗНАЧЕНИЯ(СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)) КАК ЗапасыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Партия,
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Итог.Организация КАК Организация,
		|	Итог.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	Итог.Номенклатура КАК Номенклатура,
		|	Итог.Характеристика КАК Характеристика,
		|	Итог.Партия КАК Партия,
		|	СУММА(Итог.Резерв) КАК Резерв,
		|	СУММА(Итог.Количество) КАК Количество
		|ИЗ
		|	Итог КАК Итог
		|ГДЕ
		|	НЕ Итог.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	И ТИПЗНАЧЕНИЯ(Итог.СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)
		|
		|СГРУППИРОВАТЬ ПО
		|	Итог.Характеристика,
		|	Итог.Организация,
		|	Итог.Номенклатура,
		|	Итог.Партия,
		|	Итог.СтруктурнаяЕдиница
		|ИТОГИ ПО
		|	Номенклатура";
		
		Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если Не Выборка.Количество() Тогда
			ОтменитьТранзакцию();
			Возврат Истина 
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.ОстаткиТоваров.СоздатьНаборЗаписей();
		
		Пока Выборка.Следующий() Цикл
			
			ВыборкаДетали = Выборка.Выбрать();
			
			НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
			
			Пока ВыборкаДетали.Следующий() Цикл
				НовСтр = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетали);
			КонецЦикла;
			
			НаборЗаписей.Записать(Истина);
			НаборЗаписей.Очистить();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
		Возврат Истина;
		
	Исключение
		
		ОтменитьТранзакцию();
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

// Производит пересчет записи в служебном регистре сведений данных по номенклатуре
//
Функция ВыполнитьЗаданиеПоПересчетуОстатковВТехническомРегистре(Номенклатура) Экспорт
	
	НаборЗаписей = РегистрыСведений.ОстаткиТоваров.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура);
	НаборЗаписей.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОстаткиТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗапасыОстатки.Организация КАК Организация,
		|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
		|	ЗапасыОстатки.Характеристика КАК Характеристика,
		|	ЗапасыОстатки.Партия КАК Партия,
		|	СУММА(ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0)) КАК Резерв,
		|	СУММА(0) КАК Количество
		|ПОМЕСТИТЬ Итог
		|ИЗ
		|	РегистрНакопления.Запасы.Остатки(
		|			,
		|			Номенклатура = &Номенклатура
		|				И НЕ ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|				И ТИПЗНАЧЕНИЯ(СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)) КАК ЗапасыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Партия,
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.Номенклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Номенклатура,
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Партия,
		|	СУММА(0),
		|	СУММА(ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0))
		|ИЗ
		|	РегистрНакопления.Запасы.Остатки(
		|			,
		|			Номенклатура = &Номенклатура
		|				И ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|				И ТИПЗНАЧЕНИЯ(СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)) КАК ЗапасыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Партия,
		|	ЗапасыОстатки.Характеристика,
		|	ЗапасыОстатки.Организация,
		|	ЗапасыОстатки.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Итог.Организация КАК Организация,
		|	Итог.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	Итог.Номенклатура КАК Номенклатура,
		|	Итог.Характеристика КАК Характеристика,
		|	Итог.Партия КАК Партия,
		|	СУММА(Итог.Резерв) КАК Резерв,
		|	СУММА(Итог.Количество) КАК Количество
		|ИЗ
		|	Итог КАК Итог
		|ГДЕ
		|	НЕ Итог.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	И ТИПЗНАЧЕНИЯ(Итог.СтруктурнаяЕдиница) = ТИП(Справочник.СтруктурныеЕдиницы)
		|
		|СГРУППИРОВАТЬ ПО
		|	Итог.Характеристика,
		|	Итог.Организация,
		|	Итог.Номенклатура,
		|	Итог.Партия,
		|	Итог.СтруктурнаяЕдиница
		|ИТОГИ ПО
		|	Номенклатура";
		
		Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если Не Выборка.Количество() Тогда
			ОтменитьТранзакцию();
			Возврат Истина 
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.ОстаткиТоваров.СоздатьНаборЗаписей();
		
		Пока Выборка.Следующий() Цикл
			
			ВыборкаДетали = Выборка.Выбрать();
			
			НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
			
			Пока ВыборкаДетали.Следующий() Цикл
				НовСтр = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетали);
			КонецЦикла;
			
			НаборЗаписей.Записать(Истина);
			НаборЗаписей.Очистить();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
		Возврат Истина;
		
	Исключение
		
		ОтменитьТранзакцию();
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#КонецЕсли