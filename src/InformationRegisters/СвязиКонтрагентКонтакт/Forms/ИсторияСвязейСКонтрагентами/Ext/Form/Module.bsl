#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Контакт", ВладелецКонтакт);

	Если ЗначениеЗаполнено(ВладелецКонтакт) Тогда
		
		УстановитьОтборыСвязей(Справочники.Контрагенты.ПустаяСсылка());
		
		Элементы.Контакт.Видимость = Ложь;

		Список.Параметры.УстановитьЗначениеПараметра("Контакт", ВладелецКонтакт);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "КонтрагентНедействителен",
			Ложь, , , Не Элементы.ПоказыватьНедействительных.Пометка);
		
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_Контрагент" ИЛИ ИмяСобытия = "Запись_Контакт" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	Если ТипЗнч(Элемент.ТекущиеДанные) <> ТипЗнч(Неопределено) 
		И ТипЗнч(Элементы.Список.ТекущаяСтрока) <> Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		УстановитьОтборыСвязей(Элемент.ТекущиеДанные.Контрагент);
	Иначе
		МассивОтбора = Новый Массив;
		МассивОтбора.Добавить(Элемент.ТекущиеДанные);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СвязьКонтактКонтрагент, "Контрагент",
			МассивОтбора, ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ",Элемент.ТекущиеДанные.Контрагент);
	ПараметрыФормы.Вставить("ТекущийКонтакт", ВладелецКонтакт);
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта",ПараметрыФормы);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокСвязиКонтактКонтрагент

&НаКлиенте
Процедура СвязьКонтактКонтрагентПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СвязьКонтактКонтрагентПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СвязьКонтактКонтрагентПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СвязьКонтактКонтрагентВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Изменить(Команда)
	
	Если ТипЗнч(Элементы.Список.ТекущиеДанные) = ТипЗнч(Неопределено) 
		ИЛИ ТипЗнч(Элементы.Список.ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ",Элементы.Список.ТекущиеДанные.Контрагент);
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта",ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьНедействительных(Команда)
	
	Элементы.ПоказыватьНедействительных.Пометка = Не Элементы.ПоказыватьНедействительных.Пометка;
	Элементы.СписокКонтекстноеМенюПоказыватьНедействительных.Пометка = Не Элементы.СписокКонтекстноеМенюПоказыватьНедействительных.Пометка;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
	Список,
	"КонтрагентНедействителен",
	Ложь,
	,
	,
	Не Элементы.ПоказыватьНедействительных.Пометка);
	Элементы.КонтрагентНедействителен.Видимость = Элементы.ПоказыватьНедействительных.Пометка;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// 1. Недействительные связи выделяем серым
	// a)общий список
	// б)список со связями
	НовоеУсловноеОформлениеСписок = Список.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы.Добавить();
	
	Оформление = НовоеУсловноеОформлениеСписок.Оформление.Элементы.Найти("ЦветТекста");
	Оформление.Значение 		= ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет;
	Оформление.Использование 	= Истина;
	
	Отбор = НовоеУсловноеОформлениеСписок.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование 	= Истина;
	Отбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("СвязьНедействительна");
	Отбор.ПравоеЗначение 	= Истина;
	
	НовоеУсловноеОформлениеИсторияСвязей = СвязьКонтактКонтрагент.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы.Добавить();
	
	Оформление = НовоеУсловноеОформлениеИсторияСвязей.Оформление.Элементы.Найти("ЦветТекста");
	Оформление.Значение 		= ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет;
	Оформление.Использование 	= Истина;
	
	Отбор = НовоеУсловноеОформлениеИсторияСвязей.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование 	= Истина;
	Отбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("СвязьНедействительна");
	Отбор.ПравоеЗначение 	= Истина;
	
	// 1. Недействительные контакты/контрагентов выделяем серым
	НовоеУсловноеОформлениеСписок = Список.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы.Добавить();
	
	Оформление = НовоеУсловноеОформлениеСписок.Оформление.Элементы.Найти("ЦветТекста");
	Оформление.Значение 		= ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет;
	Оформление.Использование 	= Истина;
	
	Отбор = НовоеУсловноеОформлениеСписок.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование 	= Истина;
	Если ЗначениеЗаполнено(ВладелецКонтрагент) Тогда
		Отбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("КонтактНедействителен");
	ИначеЕсли ЗначениеЗаполнено(ВладелецКонтакт) Тогда
		Отбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("КонтрагентНедействителен");
	КонецЕсли;
	Отбор.ПравоеЗначение 	= Истина;
	

КонецПроцедуры

&НаСервере
Процедура УстановитьОтборыСвязей(Контрагент)
	
	МассивОтбора = Новый Массив;
	МассивОтбора.Добавить(ВладелецКонтакт);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СвязьКонтактКонтрагент, "Контакт",
		МассивОтбора, ВидСравненияКомпоновкиДанных.ВСписке);
	
	МассивОтбораКонтрагент = Новый Массив;
	МассивОтбораКонтрагент.Добавить(Контрагент);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СвязьКонтактКонтрагент, "Контрагент",
		МассивОтбораКонтрагент, ВидСравненияКомпоновкиДанных.ВСписке);
	
КонецПроцедуры

#КонецОбласти
