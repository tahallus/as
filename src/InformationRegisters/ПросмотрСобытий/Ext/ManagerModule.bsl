#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ДобавитьОжиданиеПросмотра(Событие) Экспорт
	
	Если Не ЗначениеЗаполнено(Событие) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ПросмотрСобытий) Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Событие = Событие;
	МенеджерЗаписи.Просмотрено = Ложь;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура ОтметитьКакПросмотрено(Событие) Экспорт
	
	Если Не ЗначениеЗаполнено(Событие) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПросмотрСобытий.Просмотрено КАК Просмотрено
	|ИЗ
	|	РегистрСведений.ПросмотрСобытий КАК ПросмотрСобытий
	|ГДЕ
	|	ПросмотрСобытий.Событие = &Событие
	|	И НЕ ПросмотрСобытий.Просмотрено");
	Запрос.УстановитьПараметр("Событие", Событие);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ПросмотрСобытий) Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Событие = Событие;
	МенеджерЗаписи.Просмотрено = Истина;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Очистка записей
//
// Параметры:
//  События - Массив - Массив ссылок на документ "Событие"
Процедура ОтметитьКакПросмотреноСписком(События) Экспорт
	
	Если Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ПросмотрСобытий) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ТипЗнч(События) = Тип("Массив") И ЗначениеЗаполнено(События) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Событие Из События Цикл
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Событие = Событие;
		МенеджерЗаписи.Просмотрено = Истина;
		МенеджерЗаписи.Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьЗапись(Событие) Экспорт
	
	Если Не ЗначениеЗаполнено(Событие) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ПросмотрСобытий) Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Событие = Событие;
	МенеджерЗаписи.Удалить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли