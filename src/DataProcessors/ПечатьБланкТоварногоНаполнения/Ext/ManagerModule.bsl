#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция КлючПараметровПечати() Экспорт

	Возврат "ПАРАМЕТРЫ_ПЕЧАТИ_Универсальные_БланкТоварногоНаполнения";

КонецФункции

Функция ПолныйПутьКМакету() Экспорт

	Возврат "Обработка.ПечатьБланкТоварногоНаполнения.ПФ_MXL_БланкТоварногоНаполнения";

КонецФункции

Функция ПредставлениеПФ() Экспорт

	Возврат НСтр("ru ='Бланк товарного наполнения'");

КонецФункции

Функция СформироватьПФ(ОписаниеПечатнойФормы, ДанныеОбъектовПечати, ОбъектыПечати) Экспорт
	Перем ПервыйДокумент, НомерСтрокиНачало, Ошибки;

	Макет = УправлениеПечатью.МакетПечатнойФормы(ОписаниеПечатнойФормы.ПолныйПутьКМакету);
	ТабличныйДокумент = ОписаниеПечатнойФормы.ТабличныйДокумент;
	ДанныеПечати = Новый Структура;
	ЕстьТЧЗапасы = (ДанныеОбъектовПечати.Колонки.Найти("ТаблицаЗапасы") <> Неопределено);
	ЕстьТЧМатериалы = (ДанныеОбъектовПечати.Колонки.Найти("ТаблицаМатериалы") <> Неопределено);
	ЕстьПоложениеСклада = (ДанныеОбъектовПечати.Колонки.Найти("ПоложениеСклада") <> Неопределено);

	Для Каждого ДанныеОбъекта Из ДанныеОбъектовПечати Цикл

		СкладВШапке = (Не ЕстьПоложениеСклада
					   Или ДанныеОбъекта.ПоложениеСклада <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти);
		ПечатьДокументовУНФ.ПередНачаломФормированияДокумента(ТабличныйДокумент, ПервыйДокумент, НомерСтрокиНачало,
			ДанныеПечати);

		МассивОбластейМакета = Новый Массив;
		Если СкладВШапке Тогда
			МассивОбластейМакета.Добавить("Склад");
		КонецЕсли;
		МассивОбластейМакета.Добавить("ВремяПечати");

		НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(ДанныеОбъекта.ДатаДокумента,
			ДанныеОбъекта.Номер, ДанныеОбъекта.Префикс);
		ТекстЗаголовка = СтрШаблон(НСтр("ru = '%1 № %2 от %3'"), ДанныеОбъекта.ПредставлениеРегистратора,
			НомерДокумента, Формат(ДанныеОбъекта.ДатаДокумента, "ДЛФ=DD"));
		ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
		ДанныеПечати.Вставить("ПредставлениеСклада", ДанныеОбъекта.ПредставлениеСклада);

		Если СкладВШапке И ПолучитьФункциональнуюОпцию("УчетПоЯчейкам") Тогда
			МассивОбластейМакета.Вставить(2, "Ячейка");
			ДанныеПечати.Вставить("ПредставлениеЯчейки", ДанныеОбъекта.ПредставлениеЯчейки);
		КонецЕсли;

		ВремяПечати = СтрШаблон(НСтр("ru ='Дата и время печати: %1. Пользователь: %2'"), ТекущаяДатаСеанса(),
			Пользователи.АвторизованныйПользователь());
		ДанныеПечати.Вставить("ВремяПечати", ВремяПечати);

		ОбластьЗаголовок = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Заголовок", "", Ошибки);
		Если ОбластьЗаголовок <> Неопределено Тогда

			ОбластьЗаголовок.Параметры.Заполнить(ДанныеПечати);
			ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьЗаголовок,
				ДанныеОбъекта.Ссылка);
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);

		КонецЕсли;

		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл

			ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, ИмяОбласти, "", Ошибки);
			Если ОбластьМакета <> Неопределено Тогда

				ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);

			КонецЕсли;

		КонецЦикла;

		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Строка", "", Ошибки);
		ОбластьМакетаШапка = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "ШапкаТаблицы", "", Ошибки);
		Если ОбластьМакета <> Неопределено И ОбластьМакетаШапка <> Неопределено Тогда

			ОбластьМакетаИтого = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Итого", "", Ошибки);
			ТаблицаСерииНоменклатуры = ДанныеОбъекта.ТаблицаСерииНоменклатуры;
			ПараметрыНоменклатуры = Новый Структура;

			Если ЕстьТЧЗапасы Тогда

				ДанныеПечати.Вставить("ТипНоменклатурыТаблицы", НСтр("ru ='Запасы'"));
				ОбластьМакетаШапка.Параметры.Заполнить(ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакетаШапка);

				Для Каждого СтрокаТаблицыЗапасы Из ДанныеОбъекта.ТаблицаЗапасы Цикл

					Если Не СтрокаТаблицыЗапасы.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Запас Тогда
						Продолжить;
					КонецЕсли;

					Если СтрокаТаблицыЗапасы.Количество = 0 Тогда
						Продолжить;
					КонецЕсли;

					ЗаполнитьДанныеПечатиПоСтрокеТабличнойЧасти(СтрокаТаблицыЗапасы, ДанныеПечати,
						ПараметрыНоменклатуры, ТаблицаСерииНоменклатуры);

					ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
					ТабличныйДокумент.Вывести(ОбластьМакета);

				КонецЦикла;

				Если ОбластьМакетаИтого <> Неопределено Тогда

					ТабличныйДокумент.Вывести(ОбластьМакетаИтого);

				КонецЕсли;

			КонецЕсли;

			Если ЕстьТЧМатериалы Тогда

				ДанныеПечати.Вставить("ТипНоменклатурыТаблицы", НСтр("ru ='Материалы'"));
				ОбластьМакетаШапка.Параметры.Заполнить(ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакетаШапка);

				Для Каждого СтрокаТаблицыМатериалов Из ДанныеОбъекта.ТаблицаМатериалы Цикл

					Если Не СтрокаТаблицыМатериалов.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Запас Тогда

						Продолжить;

					КонецЕсли;

					ЗаполнитьДанныеПечатиПоСтрокеТабличнойЧасти(СтрокаТаблицыМатериалов, ДанныеПечати,
						ПараметрыНоменклатуры, ТаблицаСерииНоменклатуры);

					ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
					ТабличныйДокумент.Вывести(ОбластьМакета);

				КонецЦикла;

				Если ОбластьМакетаИтого <> Неопределено Тогда

					ТабличныйДокумент.Вывести(ОбластьМакетаИтого);

				КонецЕсли;

			КонецЕсли;

		КонецЕсли;

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати,
			ДанныеОбъекта.Ссылка);

	КонецЦикла;

	Возврат ТабличныйДокумент;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДанныеПечатиПоСтрокеТабличнойЧасти(СтрокаТабличнойЧасти, ДанныеПечати, ПараметрыНоменклатуры,
	ТаблицаСерииНоменклатуры)

	ДанныеПечати.Очистить();

	ДанныеПечати.Вставить("НомерСтроки", СтрокаТабличнойЧасти.НомерСтроки);

	СтрокаСерииНоменклатуры = ПечатьДокументовУНФ.ПредставлениеСерииНоменклатурыПоКлючу(ТаблицаСерииНоменклатуры,
		СтрокаТабличнойЧасти.КлючСвязи);

	ПараметрыНоменклатуры.Очистить();
	ПараметрыНоменклатуры.Вставить("Содержание", СтрокаТабличнойЧасти.Содержание);
	ПараметрыНоменклатуры.Вставить("ПредставлениеНоменклатуры", СтрокаТабличнойЧасти.ПредставлениеНоменклатуры);
	ПараметрыНоменклатуры.Вставить("ПредставлениеХарактеристики", СтрокаТабличнойЧасти.Характеристика);
	ПараметрыНоменклатуры.Вставить("ПредставлениеСерииНоменклатуры", СтрокаСерииНоменклатуры);

	ДанныеПечати.Вставить("ПредставлениеНоменклатуры", ПечатьДокументовУНФ.ПредставлениеНоменклатуры(
		ПараметрыНоменклатуры));
	ДанныеПечати.Вставить("ПредставлениеКодаНоменклатуры", ПечатьДокументовУНФ.ПредставлениеКодаНоменклатуры(
		СтрокаТабличнойЧасти));
	ДанныеПечати.Вставить("Количество", СтрокаТабличнойЧасти.Количество);
	ДанныеПечати.Вставить("ЕдиницаИзмерения", СтрокаТабличнойЧасти.ЕдиницаИзмерения);
	ДанныеПечати.Вставить("Склад", СтрокаТабличнойЧасти.Склад);
	ДанныеПечати.Вставить("Ячейка", СтрокаТабличнойЧасти.Ячейка);

КонецПроцедуры

#КонецОбласти 
	
#КонецЕсли