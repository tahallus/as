#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ДанныеСтроки);
	УстановитьВидимостьИДоступность();
	ЗаполнитьЗначенияРеквизитов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СуммаРасчетовПриИзменении(Элемент)
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуПлатежаНаКлиенте(ЭтотОбъект, ЭтотОбъект,, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПлатежаПриИзменении(Элемент)
	
	//РасчетыРаботаСФормамиКлиент.РасшифровкаПлатежаРасчетыПоКредитамСуммаПлатежаПриИзменении(ЭтотОбъект, ЭтотОбъект, Истина);
	ЛожьРассчитыватьАванс = Ложь;
	РасчетыРаботаСФормамиКлиент.РасшифровкаПлатежаСуммаПлатежаПриИзмененииЗавершениеНаКлиенте(ЭтотОбъект,
		ДатаДокумента, ВалютаДенежныхСредств, Курс, Кратность, СтавкаНДСПоУмолчанию,, ЛожьРассчитыватьАванс
	);
	
КонецПроцедуры

&НаКлиенте
Процедура КурсПриИзменении(Элемент)
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуПлатежаНаКлиенте(ЭтотОбъект, ЭтотОбъект, "Курс", Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуНДСНаКлиенте(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если СписокСтавокИСуммНДСВДокументе.Количество() > 0 Тогда
		Элемент.СписокВыбора.Очистить();
		
		Если ЗначениеЗаполнено(Документ) Тогда
			Элемент.СписокВыбора.Добавить(Неопределено, НСтр("ru = 'В документе:'"));
		Иначе
			Элемент.СписокВыбора.Добавить(Неопределено, НСтр("ru = 'В заказе:'"));
		КонецЕсли;
		
		Для Каждого ЭлементСписка Из СписокСтавокИСуммНДСВДокументе Цикл
			Элемент.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		СтандартнаяОбработка = Ложь;
		СтавкаНДС = ВыбранноеЗначение.СтавкаНДС;
		
		Если ВалютаДенежныхСредств = ВыбранноеЗначение.ВалютаДокумента Тогда
			СуммаНДС = ВыбранноеЗначение.СуммаНДС;
			СуммаРасчетов = 0;
			СуммаПлатежа = ВыбранноеЗначение.Всего;
			СуммаПлатежаПриИзменении(Элементы.СуммаПлатежа);
		ИначеЕсли ВалютаРасчетов = ВыбранноеЗначение.ВалютаДокумента Тогда
			СуммаПлатежа = 0;
			СуммаРасчетов = ВыбранноеЗначение.Всего;
			СуммаРасчетовПриИзменении(Элементы.СуммаРасчетов);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ДанныеСтроки = РасчетыРаботаСФормамиКлиент.ПолучитьСтруктуруСтрокиДополнительно();
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, ЭтотОбъект);
	Закрыть(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКурсВТекущейСтрокеРасшифровки(Команда)
	
	СтруктураДанных = РасчетыРаботаСФормамиВызовСервера.ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(ДатаДокумента, Договор);
	Курс = СтруктураДанных.ДоговорВалютаКурсКратность.Курс;
	Кратность = СтруктураДанных.ДоговорВалютаКурсКратность.Кратность;
	
	Если СуммаПлатежа = 0 И СуммаРасчетов = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РасчетыРаботаСФормамиКлиент.РассчитатьСуммуПлатежаНаКлиенте(ЭтотОбъект, ЭтотОбъект, "Курс", Истина, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьЗначенияРеквизитов()
	
	Если ЭтоЗачетПредоплаты Тогда
		Комментарий = ?(Документ.Метаданные().Реквизиты.Найти("Комментарий") = Неопределено, "", Документ.Комментарий);
		НазначениеПлатежа = ?(Документ.Метаданные().Реквизиты.Найти("НазначениеПлатежа") = Неопределено, "", Документ.НазначениеПлатежа);
	Иначе
		ВалютаРасчетов = Договор.ВалютаРасчетов;
		ЗаполнитьСписокСтавокИСуммНДСВДокументе();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСтавокИСуммНДСВДокументе()
	
	СписокСтавокИСуммНДСВДокументеСтрока = "";
	СписокСтавокИСуммНДСВДокументе.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктВыполненныхРаботРаботыИУслуги.СтавкаНДС,
		|	СУММА(АктВыполненныхРаботРаботыИУслуги.СуммаНДС) КАК СуммаНДС,
		|	АктВыполненныхРаботРаботыИУслуги.СтавкаНДС.Представление,
		|	АктВыполненныхРаботРаботыИУслуги.Ссылка.ВалютаДокумента.СимвольноеПредставление,
		|	СУММА(АктВыполненныхРаботРаботыИУслуги.Сумма) КАК Сумма,
		|	СУММА(АктВыполненныхРаботРаботыИУслуги.Всего) КАК Всего,
		|	АктВыполненныхРаботРаботыИУслуги.Ссылка.ВалютаДокумента КАК ВалютаДокумента
		|ИЗ
		|	Документ.АктВыполненныхРабот.РаботыИУслуги КАК АктВыполненныхРаботРаботыИУслуги
		|ГДЕ
		|	АктВыполненныхРаботРаботыИУслуги.Ссылка = &Ссылка
		|	И (&ЗаказНеВыбран
		|			ИЛИ АктВыполненныхРаботРаботыИУслуги.ЗаказПокупателя = &Заказ)
		|
		|СГРУППИРОВАТЬ ПО
		|	АктВыполненныхРаботРаботыИУслуги.СтавкаНДС,
		|	АктВыполненныхРаботРаботыИУслуги.СтавкаНДС.Представление,
		|	АктВыполненныхРаботРаботыИУслуги.Ссылка.ВалютаДокумента.СимвольноеПредставление,
		|	АктВыполненныхРаботРаботыИУслуги.Ссылка.ВалютаДокумента";
	
	Если ЗначениеЗаполнено(Документ) Тогда
		СсылкаНаДокумент = Документ;
	Иначе
		СсылкаНаДокумент = Заказ;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	
	ЕстьЗаказВТЧ = Истина;
	Если СсылкаНаДокумент.Метаданные().ТабличныеЧасти.Найти("Запасы") <> Неопределено Тогда
		Если СсылкаНаДокумент.Метаданные().ТабличныеЧасти.Запасы.Реквизиты.Найти("Заказ") = Неопределено Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИЛИ АктВыполненныхРаботРаботыИУслуги.ЗаказПокупателя = &Заказ", "");
			ЕстьЗаказВТЧ = Ложь;
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИЛИ АктВыполненныхРаботРаботыИУслуги.ЗаказПокупателя = &Заказ", "ИЛИ АктВыполненныхРаботРаботыИУслуги.Заказ = &Заказ");
		КонецЕсли;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РаботыИУслуги", "Запасы");
	ИначеЕсли СсылкаНаДокумент.Метаданные().ТабличныеЧасти.Найти("РасшифровкаПлатежа") <> Неопределено Тогда
		Если СсылкаНаДокумент.Метаданные().ТабличныеЧасти.РасшифровкаПлатежа.Реквизиты.Найти("Заказ") = Неопределено Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИЛИ АктВыполненныхРаботРаботыИУслуги.ЗаказПокупателя = &Заказ", "");
			ЕстьЗаказВТЧ = Ложь;
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИЛИ АктВыполненныхРаботРаботыИУслуги.ЗаказПокупателя = &Заказ", "ИЛИ АктВыполненныхРаботРаботыИУслуги.Заказ = &Заказ");
		КонецЕсли;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РаботыИУслуги", "РасшифровкаПлатежа");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ".Сумма ", ".СуммаПлатежа ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ".Всего ", ".СуммаПлатежа ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ".ВалютаДокумента", ".ВалютаДенежныхСредств");
	ИначеЕсли СсылкаНаДокумент.Метаданные().ТабличныеЧасти.Найти("РаботыИУслуги") = Неопределено Тогда
		// Не обрабатываем такую ситуацию.
		Возврат;
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "АктВыполненныхРабот", СсылкаНаДокумент.Метаданные().Имя);
	
	Если ЗначениеЗаполнено(Заказ) Тогда
		Запрос.УстановитьПараметр("ЗаказНеВыбран", Ложь ИЛИ НЕ ЕстьЗаказВТЧ);
		Запрос.УстановитьПараметр("Заказ", Заказ);
	Иначе
		Запрос.УстановитьПараметр("ЗаказНеВыбран", Истина);
		Если ЭтоРасчетыСПоставщиком Тогда
			Запрос.УстановитьПараметр("Заказ", Документы.ЗаказПоставщику.ПустаяСсылка());
		Иначе
			Запрос.УстановитьПараметр("Заказ", Документы.ЗаказПокупателя.ПустаяСсылка());
		КонецЕсли;
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СтрокаДляСтавкиНДС = ВыборкаДетальныеЗаписи.СтавкаНДСПредставление+" ("+ВыборкаДетальныеЗаписи.Всего + " / " + ВыборкаДетальныеЗаписи.СуммаНДС+" "+ВыборкаДетальныеЗаписи.ВалютаДокументаСимвольноеПредставление+")";
		
		СтруктураДляВставки = Новый Структура("СтавкаНДС, СуммаНДС, Сумма, Всего, ВалютаДокумента",
			ВыборкаДетальныеЗаписи.СтавкаНДС,
			ВыборкаДетальныеЗаписи.СуммаНДС,
			ВыборкаДетальныеЗаписи.Сумма,
			ВыборкаДетальныеЗаписи.Всего,
			ВыборкаДетальныеЗаписи.ВалютаДокумента
		);
		СписокСтавокИСуммНДСВДокументе.Добавить(
			СтруктураДляВставки,
			СтрокаДляСтавкиНДС
		);
		
		СписокСтавокИСуммНДСВДокументеСтрока = СписокСтавокИСуммНДСВДокументеСтрока + ?(СписокСтавокИСуммНДСВДокументеСтрока = "", "", "; ");
		СписокСтавокИСуммНДСВДокументеСтрока = СписокСтавокИСуммНДСВДокументеСтрока + СтрокаДляСтавкиНДС;
	КонецЦикла;
	
КонецПроцедуры

// Процедура определяет видимость и доступность реквизитов формы.
//
&НаСервере
Процедура УстановитьВидимостьИДоступность()
	
	Если ЭтоЗачетПредоплаты Тогда
		
		Элементы.СчетНаОплату.Видимость = Ложь;
		Элементы.СуммаКолонкиСправа.Видимость = Ложь;
		Элементы.ГруппаДополнительно.Видимость = Ложь;
		
		Если Документ = Неопределено Тогда
			Элементы.Комментарий.Видимость = Ложь;
			Элементы.НазначениеПлатежа.Видимость = Ложь;
		Иначе
			Элементы.Комментарий.Видимость = (Документ.Метаданные().Реквизиты.Найти("Комментарий") <> Неопределено);
			Элементы.НазначениеПлатежа.Видимость = (Документ.Метаданные().Реквизиты.Найти("НазначениеПлатежа") <> Неопределено);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Элементы.ДекорацияРазделитель1.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры // УстановитьВидимостьИДоступность()

#КонецОбласти
