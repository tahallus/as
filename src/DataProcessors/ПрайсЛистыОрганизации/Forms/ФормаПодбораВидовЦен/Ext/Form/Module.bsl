

#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ПериодЦен", 					Объект.ПериодЦен);
	Параметры.Свойство("НазваниеЦеныПредставлением",	Объект.НазваниеЦеныПредставлением);
	Параметры.Свойство("ВариантПечатиПрайсЛиста",		Объект.ПечатьПрайсЛиста);
	
	ЗаголовкиКолонокСЦенами = (Объект.НазваниеЦеныПредставлением = 1);
	
	Если НЕ Параметры.Свойство("Валюта") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаВалютаКурс", "Видимость", Ложь);
		
	Иначе
		
		Параметры.Свойство("Валюта",					Объект.Валюта);
		Параметры.Свойство("ДатаРасчетаКурсаВалюты",	Объект.ДатаРасчетаКурсаВалюты);
		
	КонецЕсли;
	
	Если Параметры.Свойство("МассивВидыЦен") Тогда
		
		Для каждого ЭлементаМассива Из Параметры.МассивВидыЦен Цикл
				
			НоваяСтрока = Объект.ВидыЦен.Добавить();
			НоваяСтрока.ВидЦен = ЭлементаМассива;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЭтоПрайсЛистПолотно = (Объект.ПечатьПрайсЛиста = Перечисления.ВариантыПечатиПрайсЛиста.Полотно);
	Если НЕ ЭтоПрайсЛистПолотно
		И Объект.ВидыЦен.Количество() = 0 Тогда
		
		Объект.ВидыЦен.Добавить(); // Нужна пустая строка, для выбора одного вида цен
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Группа1", "Видимость", ЭтоПрайсЛистПолотно);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Группа2", "Видимость", НЕ ЭтоПрайсЛистПолотно);
	
КонецПроцедуры //ПриСозданииНаСервере()

&НаКлиенте
// Процедура - обработчик события ОбработкаВыбора формы.
//
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если НЕ ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		
		ЗначениеВыбора 		= ВыбранноеЗначение;
		ВыбранноеЗначение	= Новый Массив;
		ВыбранноеЗначение.Добавить(ЗначениеВыбора);
		
	КонецЕсли;
	
	ДобавитьЭлементыВТабличнуюЧасть(ВыбранноеЗначение);
	
КонецПроцедуры //ОбработкаВыбора()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗаголовкиКолонокСЦенамиПриИзменении(Элемент)
	
	Объект.НазваниеЦеныПредставлением = (ЗаголовкиКолонокСЦенами = 1);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Процедура - обработчик команды ОК.
//
Процедура ОК(Команда)
	
	Результат = Новый Структура;
	Результат.Вставить("ПериодЦен",						Объект.ПериодЦен);
	Результат.Вставить("НазваниеЦеныПредставлением",	Объект.НазваниеЦеныПредставлением);
	Результат.Вставить("Валюта",						Объект.Валюта);
	Результат.Вставить("ДатаРасчетаКурсаВалюты",		Объект.ДатаРасчетаКурсаВалюты);
	Результат.Вставить("МассивВидовЦен",				Новый Массив);
	
	Для каждого СтрокаТаблицы Из Объект.ВидыЦен Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ВидЦен) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Результат.МассивВидовЦен.Добавить(СтрокаТаблицы.ВидЦен);
		
	КонецЦикла;
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры //ОК()

&НаКлиенте
// Процедура - обработчик команды Подбор.
//
Процедура Подбор(Команда)
	
	ОткрытьФорму("Справочник.ВидыЦен.Форма.ФормаВыбора", Новый Структура("МножественныйВыбор, РежимВыбора, ЗакрыватьПриВыборе", Истина, Истина, Ложь), ЭтаФорма);
	
КонецПроцедуры //Подбор()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Добавляет элементы массива в табличную часть.
// Предварительно проверяем на наличие данного элемента в табличной части.
//
Процедура ДобавитьЭлементыВТабличнуюЧасть(МассивЭлементов)
	
	Если НЕ ТипЗнч(МассивЭлементов) = Тип("Массив") 
		ИЛИ НЕ МассивЭлементов.Количество() > 0 Тогда 
		
		Возврат;
		
	КонецЕсли;
	
	ОтборВидаЦен = Новый Структура("ВидЦен", Неопределено);
	
	Для каждого ЭлементМассива Из МассивЭлементов Цикл
		
		ОтборВидаЦен.ВидЦен = ЭлементМассива;
		Если Объект.ВидыЦен.НайтиСтроки(ОтборВидаЦен).Количество() > 0 Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Элемент [%1] уже присутствует в таблице.'"), ЭлементМассива);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Продолжить;
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Объект.ВидыЦен.Добавить(), ОтборВидаЦен);
		
	КонецЦикла;
	
КонецПроцедуры //ДобавитьЭлементыВТабличнуюЧасть()

#КонецОбласти



