
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПрисозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПользователиИнформационнойБазы.ТекущийПользователь().Имя = "" Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("РегистрацияИнформационнойБазыСистемыВзаимодействия", Метаданные) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Сервер = "wss://1cdialog.com:443";
	ИмяИнформационнойБазы = Метаданные.КраткаяИнформация;
	
	Если СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаУжеЗарегистрирован;
	Иначе
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаНачалоРегистрации;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура EMail1ИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Элементы.ПолучитьКодРегистрации.Доступность = НЕ ПустаяСтрока(Текст);
	
КонецПроцедуры

&НаКлиенте
Процедура EMailИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Элементы.Зарегистрировать.Доступность = 
		НЕ ПустаяСтрока(Текст) И
		НЕ ПустаяСтрока(ИмяИнформационнойБазы) И
		НЕ ПустаяСтрока(КодРегистрации);
		
КонецПроцедуры

&НаКлиенте
Процедура ИБИмяИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Элементы.Зарегистрировать.Доступность = 
		НЕ ПустаяСтрока(АдресЭлектроннойПочты) И
		НЕ ПустаяСтрока(Текст) И
		НЕ ПустаяСтрока(КодРегистрации);

КонецПроцедуры

&НаКлиенте
Процедура КодРегистрацииИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)

	Элементы.Зарегистрировать.Доступность = 
		НЕ ПустаяСтрока(АдресЭлектроннойПочты) И
		НЕ ПустаяСтрока(ИмяИнформационнойБазы) И
		НЕ ПустаяСтрока(Текст);
	
КонецПроцедуры

&НаКлиенте
Процедура ФлагОтменитьРегистрациюПриИзменении(Элемент)
	
	Элементы.ОтменитьРегистрацию.Доступность = ФлагОтменитьРегистрацию;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтменитьРегистрацию(Команда)
	
	СистемаВзаимодействия.НачатьОтменуРегистрацииИнформационнойБазы(Новый ОписаниеОповещения("ОтменаРегистрацииВыполнена", ЭтотОбъект, , "ОтменаРегистрацииОшибка", ЭтотОбъект));
	
	ЭтаФорма.Доступность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСтраницуРегистрации(Команда)
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПолучитьКодРегистрации;

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКодРегистрации(Команда)
	
	ПараметрыВзаимодействия = Новый ПараметрыРегистрацииИнформационнойБазыСистемыВзаимодействия();
	ПараметрыВзаимодействия.АдресСервера = Сервер;
	ПараметрыВзаимодействия.АдресЭлектроннойПочты = АдресЭлектроннойПочты;
	
	Оповещение = Новый ОписаниеОповещения("КодРегистрацииПолучен", ЭтотОбъект, , "КодРегистрацииОшибка", ЭтотОбъект);
	
	СистемаВзаимодействия.BeginInfoBaseRegistration(Оповещение, ПараметрыВзаимодействия);
	
	ЭтаФорма.Доступность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЕстьКодРегистрации(Команда)
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.ГлавнаяСтраница;
	
КонецПроцедуры

&НаКлиенте
Процедура Зарегистрировать(Команда)
	
	ПараметрыВзаимодействия = Новый ПараметрыРегистрацииИнформационнойБазыСистемыВзаимодействия();
	ПараметрыВзаимодействия.АдресСервера = Сервер;
	ПараметрыВзаимодействия.АдресЭлектроннойПочты = АдресЭлектроннойПочты;
	ПараметрыВзаимодействия.ИмяИнформационнойБазы = ИмяИнформационнойБазы;
	ПараметрыВзаимодействия.КодАктивации = СокрЛП(КодРегистрации);
	
	Оповещение = Новый ОписаниеОповещения("РегистрацияВыполнена", ЭтотОбъект, , "РегистрацияОшибка", ЭтотОбъект);
	
	СистемаВзаимодействия.НачатьРегистрациюИнформационнойБазы(Оповещение, ПараметрыВзаимодействия);
	
	ЭтаФорма.Доступность = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РегистрацияВыполнена(Результат, ТекстСообщения, ДопПараметры) Экспорт

	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаУспешно;
	
	ЭтаФорма.Доступность = Истина;
	
	Оповестить("РегистрацияВзаимодействия", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияОшибка(ИнформацияОбОшибке, СтандартнаяОбработка, ДопПараметры) Экспорт

	СтандартнаяОбработка = Ложь;
	ПоказатьИнформациюОбОшибке(ИнформацияОбОшибке);

	ЭтаФорма.Доступность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменаРегистрацииВыполнена(ДопПараметры) Экспорт

	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРегистрацияОтменена;
	
	ЭтаФорма.Доступность = Истина;
	
	Оповестить("РегистрацияВзаимодействия", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменаРегистрацииОшибка(ИнформацияОбОшибке, СтандартнаяОбработка, ДопПараметры) Экспорт

	СтандартнаяОбработка = Ложь;
	ПоказатьИнформациюОбОшибке(ИнформацияОбОшибке);

	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаУжеЗарегистрирован;
	
	ЭтаФорма.Доступность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КодРегистрацииПолучен(Результат, ТекстСообщения, ДопПараметры) Экспорт

	Оповещение = Новый ОписаниеОповещения("КодРегистрацииПолученЗавершение", ЭтотОбъект);
	ПоказатьПредупреждение(Оповещение, ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура КодРегистрацииПолученЗавершение(ДопПараметры) Экспорт
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.ГлавнаяСтраница;
	
	ЭтаФорма.Доступность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КодРегистрацииОшибка(ИнформацияОбОшибке, СтандартнаяОбработка, ДопПараметры) Экспорт

	СтандартнаяОбработка = Ложь;
	
	ПоказатьИнформациюОбОшибке(ИнформацияОбОшибке);
	
	ЭтаФорма.Доступность = Истина;

КонецПроцедуры

#КонецОбласти

