
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Операторы = Перечисления.ДоступныеАТС.ДоступныеАТСУниверсальногоВиджетаItoolabs();
	
	ИспользуемаяАТСДополнительно = Константы.ИспользуемаяАТСДополнительно.Получить();
	
	Для каждого ОписаниеОператора Из Операторы Цикл
		НоваяСтрока = СписокОператоров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеОператора, "Представление,Идентификатор,Ссылка");
		НоваяСтрока.Используется = (ОписаниеОператора.Идентификатор = ИспользуемаяАТСДополнительно);
	КонецЦикла;
	
	СписокОператоров.Сортировать("Представление Возр");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокОператоровИспользуетсяПриИзменении(Элемент)
	
	ЗаписатьВыбраннуюАТС(Элементы.СписокОператоров.ТекущиеДанные.Идентификатор);
	ОповеститьОВыборе(ПредопределенноеЗначение("Перечисление.ДоступныеАТС.УниверсальныйItoolabs"));
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОператоровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(Элемент.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьВыбраннуюАТС(Идентификатор)
	
	Константы.ИспользуемаяАТСДополнительно.Установить(Идентификатор);
	
КонецПроцедуры

#КонецОбласти