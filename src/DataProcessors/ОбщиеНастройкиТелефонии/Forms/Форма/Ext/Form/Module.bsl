
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПрочитатьНастройки();
	
	ОбновитьАдресПубликации();
	
	СтрокаАвторизацииVRDФайла = ТелефонияСервер.СтрокаАвторизацииФайлVRDТекстПодсказки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Оповестить("Запись_НаборКонстант", , "АдресУНФ");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовУправленияФормы

&НаКлиенте
Процедура АдресСервераПриИзменении(Элемент)
	
	Если СтрЗаканчиваетсяНа(АдресСервераОсновнойПубликации, "/") Тогда
		АдресСервераОсновнойПубликации = Лев(АдресСервераОсновнойПубликации, СтрДлина(АдресСервераОсновнойПубликации) - 1);
	КонецЕсли;
	
	ОбновитьАдресПубликации();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресРесурсаОсновнойПубликацииMangoПриИзменении(Элемент)
	ОбновитьАдресПубликации();
КонецПроцедуры

&НаКлиенте
Процедура АдресРесурсаОсновнойПубликацииДомRuПриИзменении(Элемент)
	ОбновитьАдресПубликации();
КонецПроцедуры

&НаКлиенте
Процедура АдресРесурсаОсновнойПубликацииЯндексПриИзменении(Элемент)
	ОбновитьАдресПубликации();
КонецПроцедуры

&НаКлиенте
Процедура АдресРесурсаОсновнойПубликацииРостелекомПриИзменении(Элемент)
	ОбновитьАдресПубликации();
КонецПроцедуры

&НаКлиенте
Процедура АдресРесурсаОсновнойПубликацииВестКоллСПбПриИзменении(Элемент)
	ОбновитьАдресПубликации();
КонецПроцедуры

&НаКлиенте
Процедура АдресРесурсаОсновнойПубликацииДеловаяСетьИркутск(Элемент)
	ОбновитьАдресПубликации();
КонецПроцедуры

&НаКлиенте
Процедура АдресРесурсаОсновнойПубликацииУниверсальныйItoolabsПриИзменении(Элемент)
	ОбновитьАдресПубликации();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	СохранитьНастройки();
	Оповестить("Запись_НаборКонстант", Новый Структура, "ОбщиеНастройкиТелефонии");
	Закрыть(Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьНастройки()
	
	Настройки = Константы.ОбщиеНастройкиТелефонии.Получить().Получить();
	
	Если ТипЗнч(Настройки) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Настройки.Свойство("АдресСервераОсновнойПубликации", АдресСервераОсновнойПубликации);
	
	НастройкиПубликации = Неопределено;
	Настройки.Свойство("НастройкиПубликации", НастройкиПубликации);
	
	Если НастройкиПубликации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Mango
	НастройкиПубликацииMango = НастройкиПубликации.Получить(Перечисления.ДоступныеАТС.MangoOffice);
	Если ТипЗнч(НастройкиПубликацииMango) = Тип("Структура") Тогда
		НастройкиПубликацииMango.Свойство("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликацииMango);
	КонецЕсли;
	
	// ДомRu
	НастройкиПубликацииДомRu = НастройкиПубликации.Получить(Перечисления.ДоступныеАТС.ДомRu);
	Если ТипЗнч(НастройкиПубликацииДомRu) = Тип("Структура") Тогда
		НастройкиПубликацииДомRu.Свойство("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликацииДомRu);
	КонецЕсли;
	
	// Яндекс
	НастройкиПубликацииЯндекс = НастройкиПубликации.Получить(Перечисления.ДоступныеАТС.Яндекс);
	Если ТипЗнч(НастройкиПубликацииЯндекс) = Тип("Структура") Тогда
		НастройкиПубликацииЯндекс.Свойство("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликацииЯндекс);
	КонецЕсли;
	
	// Ростелеком
	НастройкиПубликацииРостелеком = НастройкиПубликации.Получить(Перечисления.ДоступныеАТС.Ростелеком);
	Если ТипЗнч(НастройкиПубликацииРостелеком) = Тип("Структура") Тогда
		НастройкиПубликацииРостелеком.Свойство("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликацииРостелеком);
	КонецЕсли;
	
	// ВестКоллСПб
	НастройкиПубликацииВестКоллСПб = НастройкиПубликации.Получить(Перечисления.ДоступныеАТС.ВестКоллСПб);
	Если ТипЗнч(НастройкиПубликацииВестКоллСПб) = Тип("Структура") Тогда
		НастройкиПубликацииВестКоллСПб.Свойство("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликацииВестКоллСПб);
	КонецЕсли;
	
	// ДеловаяСетьИркутск
	НастройкиПубликацииДеловаяСетьИркутск = НастройкиПубликации.Получить(Перечисления.ДоступныеАТС.ДеловаяСетьИркутск);
	Если ТипЗнч(НастройкиПубликацииДеловаяСетьИркутск) = Тип("Структура") Тогда
		НастройкиПубликацииДеловаяСетьИркутск.Свойство("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликацииДеловаяСетьИркутск);
	КонецЕсли;
	
	// УниверсальныйItoolabs
	НастройкиПубликацииУниверсальныйItoolabs = НастройкиПубликации.Получить(Перечисления.ДоступныеАТС.УниверсальныйItoolabs);
	Если ТипЗнч(НастройкиПубликацииУниверсальныйItoolabs) = Тип("Структура") Тогда
		НастройкиПубликацииУниверсальныйItoolabs.Свойство("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликацииУниверсальныйItoolabs);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	Если Не ЗначениеЗаполнено(АдресСервераОсновнойПубликации)
		И Не ЗначениеЗаполнено(АдресРесурсаОсновнойПубликацииMango)
		И Не ЗначениеЗаполнено(АдресРесурсаОсновнойПубликацииДомRu)
		И Не ЗначениеЗаполнено(АдресРесурсаОсновнойПубликацииЯндекс)
		И Не ЗначениеЗаполнено(АдресРесурсаОсновнойПубликацииРостелеком)
		И Не ЗначениеЗаполнено(АдресРесурсаОсновнойПубликацииВестКоллСПб)
		И Не ЗначениеЗаполнено(АдресРесурсаОсновнойПубликацииДеловаяСетьИркутск)
		И Не ЗначениеЗаполнено(АдресРесурсаОсновнойПубликацииУниверсальныйItoolabs) Тогда
		
		Настройки = Неопределено;
		
	Иначе
		
		Настройки = Новый Структура;
		Настройки.Вставить("АдресСервераОсновнойПубликации", АдресСервераОсновнойПубликации);
		
		НастройкиПубликации = Новый Соответствие;
		НастройкиПубликации.Вставить(Перечисления.ДоступныеАТС.MangoOffice, Новый Структура("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликацииMango));
		НастройкиПубликации.Вставить(Перечисления.ДоступныеАТС.ДомRu, Новый Структура("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликацииДомRu));
		НастройкиПубликации.Вставить(Перечисления.ДоступныеАТС.Яндекс, Новый Структура("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликацииЯндекс));
		НастройкиПубликации.Вставить(Перечисления.ДоступныеАТС.Ростелеком, Новый Структура("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликацииРостелеком));
		НастройкиПубликации.Вставить(Перечисления.ДоступныеАТС.ВестКоллСПб, Новый Структура("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликацииВестКоллСПб));
		НастройкиПубликации.Вставить(Перечисления.ДоступныеАТС.ДеловаяСетьИркутск, Новый Структура("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликацииДеловаяСетьИркутск));
		НастройкиПубликации.Вставить(Перечисления.ДоступныеАТС.УниверсальныйItoolabs, Новый Структура("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликацииУниверсальныйItoolabs));
		
		Настройки.Вставить("НастройкиПубликации", НастройкиПубликации);
		
	КонецЕсли;
	
	Константы.ОбщиеНастройкиТелефонии.Установить(Новый ХранилищеЗначения(Настройки));
	
КонецПроцедуры

&НаСервере
Функция ПолныйПутьПубликации(АТС, АдресПубликации)
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Шаблон = "[АдресСервера]/[АдресПубликации]/[ОбластьДанных]/hs/[КорневойURLСервиса]";
	Иначе
		Шаблон = "[АдресСервера]/[АдресПубликации]/hs/[КорневойURLСервиса]";
	КонецЕсли;
	
	ПараметрыСтроки = Новый Структура;
	Если ЗначениеЗаполнено(АдресСервераОсновнойПубликации) Тогда
		ПараметрыСтроки.Вставить("АдресСервера", АдресСервераОсновнойПубликации);
	КонецЕсли;
	Если ЗначениеЗаполнено(АдресПубликации) Тогда
		ПараметрыСтроки.Вставить("АдресПубликации", АдресПубликации);
	КонецЕсли;
	ПараметрыСтроки.Вставить("КорневойURLСервиса", ТелефонияСервер.КорневойURLСервисаОсновнойПубликации(АТС));
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, ПараметрыСтроки);
	
КонецФункции

&НаСервере
Процедура ОбновитьАдресПубликации()
	
	ПолныйПутьПубликацииMango                 = ПолныйПутьПубликации(Перечисления.ДоступныеАТС.MangoOffice, АдресРесурсаОсновнойПубликацииMango);
	ПолныйПутьПубликацииДомRu                 = ПолныйПутьПубликации(Перечисления.ДоступныеАТС.ДомRu, АдресРесурсаОсновнойПубликацииДомRu);
	ПолныйПутьПубликацииЯндекс                = ПолныйПутьПубликации(Перечисления.ДоступныеАТС.Яндекс, АдресРесурсаОсновнойПубликацииЯндекс);
	ПолныйПутьПубликацииРостелеком            = ПолныйПутьПубликации(Перечисления.ДоступныеАТС.Ростелеком, АдресРесурсаОсновнойПубликацииРостелеком);
	ПолныйПутьПубликацииВестКоллСПб           = ПолныйПутьПубликации(Перечисления.ДоступныеАТС.ВестКоллСПб, АдресРесурсаОсновнойПубликацииВестКоллСПб);
	ПолныйПутьПубликацииДеловаяСетьИркутск    = ПолныйПутьПубликации(Перечисления.ДоступныеАТС.ДеловаяСетьИркутск, АдресРесурсаОсновнойПубликацииДеловаяСетьИркутск);
	ПолныйПутьПубликацииУниверсальныйItoolabs = ПолныйПутьПубликации(Перечисления.ДоступныеАТС.УниверсальныйItoolabs, АдресРесурсаОсновнойПубликацииУниверсальныйItoolabs);
	
КонецПроцедуры

#КонецОбласти