#Область ОписаниеПеременных

&НаКлиенте
Перем ТекущийКонтрагент; // Для проверки в событии "ПриАктивизацииСтроки", что текущая строка была изменена

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ОбработатьАктивизациюСтрокиСписка()
	
	ТекущаяСтрока = Элементы.БанковскиеВыписки.ТекущиеДанные;
	
	РасчетыРаботаСФормамиКлиент.ОбновлениеИтоговПриАктивизацииСтрокиСписка(ЭтаФорма, ТекущаяСтрока, "БанкИКасса");
	
КонецПроцедуры // ОбработатьАктивизациюСтрокиСписка()

&НаСервере
Процедура НастройкаСортировкиИГруппировкиВДинамическихСписках()
	
	// Рекомендуется ограничивать сортировку и группировку по "сложным" полям, которые получены
	// с помощью оператора ВЫБОР и могут содержать значения различных типов.
	МассивПолей = Новый Массив;
	МассивПолей.Добавить("ВидОперации");
	МассивПолей.Добавить("Аналитика");
	
	БанковскиеВыписки.УстановитьОграниченияИспользованияВГруппировке(МассивПолей);
	БанковскиеВыписки.УстановитьОграниченияИспользованияВПорядке(МассивПолей);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкаСортировкиИГруппировкиВДинамическихСписках();
	
	СписокОрганизаций = ДвижениеДенежныхСредствСервер.ПолучитьСписокВыбораОрганизаций();
	
	РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям = ПолучитьФункциональнуюОпцию("РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям");
	ВестиУчетРасчетовСБанкомЭквайрером = ПолучитьФункциональнуюОпцию("ВестиУчетРасчетовСБанкомЭквайрером");
	
	// Отборы
	// Банк и касса
	ПрочитатьРасчетныеСчетаИКассы();
	// Конец Отборы
	
	// Итоги
	РасчетыРаботаСФормамиВызовСервера.НастроитьПанельИтоговПриСозданииНаСервере(ЭтаФорма, "БанкИКасса");
	// Конец Итоги
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Документы.ПоступлениеНаСчет);
	Элементы.ЗагрузитьИзФайла.Видимость = МожноРедактировать;
	Элементы.ФормаВыгрузитьВКлиентБанк.Видимость = МожноРедактировать;
	
	// Установим видимость закладки в зависимости от функциональной опции.
	Элементы.СтраницаОперацииПоПлатежнымКартам.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьОплатуКартами");
	
	ДинамическиеСпискиУНФ.ОтображатьТолькоВремяДляТекущейДаты(СписокПлатежныхПоручений);
	ДинамическиеСпискиУНФ.ОтображатьТолькоВремяДляТекущейДаты(БанковскиеВыписки);
	ДинамическиеСпискиУНФ.ОтображатьТолькоВремяДляТекущейДаты(СписокОперацииПоПлатежнымКартам);
	
	//УНФ.ОтборыСписка
	РаботаСОтборами.ОпределитьПорядокПредопределенныхОтборов(ЭтотОбъект);
	РаботаСОтборами.ОпределитьПорядокПредопределенныхОтборов(ЭтотОбъект, "ГруппаОтборыПП", "ПредопределенныеОтборыПоУмолчаниюПП");
	РаботаСОтборами.ОпределитьПорядокПредопределенныхОтборов(ЭтотОбъект, "ГруппаОтборыЭ", "ПредопределенныеОтборыПоУмолчаниюЭ");
	
	// установим пустые значения параметров запроса списка
	СписокКассИСчетов = Новый Массив;
	БанковскиеВыписки.Параметры.УстановитьЗначениеПараметра("БезОтбора", Истина);
	БанковскиеВыписки.Параметры.УстановитьЗначениеПараметра("СписокКассИСчетов", СписокКассИСчетов);
	
	СтруктураИменЭлементовПП = Новый Структура;
	СтруктураИменЭлементовПП.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоПП");
	СтруктураИменЭлементовПП.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыПП");
	СтруктураИменЭлементовПП.Вставить("ПраваяПанель", "ПраваяПанельПП");
	СтруктураИменЭлементовПП.Вставить("ОтборПериод", "ОтборПериодПП");
	СтруктураИменЭлементовПП.Вставить("ПредставлениеПериода", "ПредставлениеПериодаПП");
	
	СтруктураИменЭлементовЭ = Новый Структура;
	СтруктураИменЭлементовЭ.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоЭ");
	СтруктураИменЭлементовЭ.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыЭ");
	СтруктураИменЭлементовЭ.Вставить("ПраваяПанель", "ПраваяПанельЭ");
	СтруктураИменЭлементовЭ.Вставить("ОтборПериод", "ОтборПериодЭ");
	СтруктураИменЭлементовЭ.Вставить("ПредставлениеПериода", "ПредставлениеПериодаЭ");
	
	Если Параметры.Свойство("ЭтоНачальнаяСтраница") Тогда
		РаботаСОтборами.ВосстановитьНастройкиОтборов(ЭтотОбъект, БанковскиеВыписки, "БанковскиеВыписки");
		РаботаСОтборами.ВосстановитьНастройкиОтборов(ЭтотОбъект, СписокПлатежныхПоручений, "СписокПлатежныхПоручений", СтруктураИменЭлементовПП,,,, "ДанныеМетокПП",,"ДанныеОтборовПП", "ГруппаОтборыПП");
		РаботаСОтборами.ВосстановитьНастройкиОтборов(ЭтотОбъект, СписокОперацииПоПлатежнымКартам, "СписокОперацииПоПлатежнымКартам", СтруктураИменЭлементовЭ,,,, "ДанныеМетокОплатаКартами",, "ДанныеОтборовОплатаКартами", "ГруппаОтборыЭ");
		
		ЭтоНачальнаяСтраница = Ложь;
	Иначе
		РаботаСОтборами.СвернутьРазвернутьОтборыНаСервере(ЭтотОбъект, Ложь);
		РаботаСОтборами.СвернутьРазвернутьОтборыНаСервере(ЭтотОбъект, Ложь, СтруктураИменЭлементовЭ);
		РаботаСОтборами.СвернутьРазвернутьОтборыНаСервере(ЭтотОбъект, Ложь, СтруктураИменЭлементовПП);
		ПредставлениеПериода	= РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(Неопределено);
		ПредставлениеПериодаПП	= РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(Неопределено);
		ПредставлениеПериодаЭ   = РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(Неопределено);
		
		ЭтоНачальнаяСтраница = Истина;
	КонецЕсли;
	
	НастроитьПанелиОтборов();
	//Конец УНФ.ОтборыСписка
	
	// ИнтернетПоддержкаПользователей.Новости
	ОбработкаНовостей.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"УНФ.Обработка.БанкИКасса",
		"ФормаСписка",
		Неопределено,
		НСтр("ru='Новости: Банк и касса'"),
		Ложь,
		Новый Структура("ПолучатьНовостиНаСервере, ХранитьМассивНовостейТолькоНаСервере", Истина, Истина),
		"ПриОткрытии"
	);
	// Конец ИнтернетПоддержкаПользователей.Новости
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.Источники = СписокПлатежныхПоручений.Отбор.ДоступныеПоляОтбора.НайтиПоле(Новый ПолеКомпоновкиДанных("Ссылка")).ТипЗначения;
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаВажныеКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПечатьДокументовУНФ.УстановитьОтображениеПодменюПечати(Элементы.ПодменюПечать);
	
	ИспользуютсяПереводыВПути = РегистрыСведений.ПрименениеПереводовВПути.ИспользуютсяПереводыВПути();
	Если ИспользуютсяПереводыВПути Тогда
		Элементы.ПодменюПеремещение.Видимость = Истина;
		Элементы.ПодменюПеремещениеБез57Счета.Видимость = Ложь;
	Иначе
		Элементы.ПодменюПеремещение.Видимость = Ложь;
		Элементы.ПодменюПеремещениеБез57Счета.Видимость = Истина;
	КонецЕсли;
	
	ОтборСчетИКасса = Справочники.Кассы.ПустаяСсылка();
	
	ДвижениеДенежныхСредствСервер.СформироватьСписокКомандСозданияДокументов(ЭтотОбъект, "ПодменюБанкПоступление", "ПоступлениеНаСчет", "БанковскиеВыписки");
	ДвижениеДенежныхСредствСервер.СформироватьСписокКомандСозданияДокументов(ЭтотОбъект, "ПодменюКассаПоступление", "ПоступлениеВКассу", "БанковскиеВыписки");
	ДвижениеДенежныхСредствСервер.СформироватьСписокКомандСозданияДокументов(ЭтотОбъект, "ПодменюБанкРасход", "РасходСоСчета", "БанковскиеВыписки");
	ДвижениеДенежныхСредствСервер.СформироватьСписокКомандСозданияДокументов(ЭтотОбъект, "ПодменюКассаРасход", "РасходИзКассы", "БанковскиеВыписки");
	ДвижениеДенежныхСредствСервер.СформироватьСписокКомандСозданияДокументов(ЭтотОбъект, "ПодменюСоздатьПлатежноеПоручение", "ПлатежноеПоручение", "СписокПлатежныхПоручений");
	ДвиженияДенежныхСредствВызовСервера.ЗаполнитьСписокВыбораВидовОпераций("ПлатежноеПоручение", Элементы.ОтборВидОперацииПП.СписокВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.Новости
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	// Флаги над списком на закладке "Поступление и расход".
	ОтборБанк			= Настройки.Получить("ОтборБанк");
	ОтборКасса			= Настройки.Получить("ОтборКасса");
	НастроитьОтборДинамическогоСпискаБанкКасса();
	
	ОтборПоступления	= Настройки.Получить("ОтборПоступления");
	НастроитьОтборДинамическогоСпискаПоВидуДвижения("БанковскиеВыписки", "ЭтоПоступление", ОтборПоступления);
	
	ОтборРасход			= Настройки.Получить("ОтборРасход");
	НастроитьОтборДинамическогоСпискаПоВидуДвижения("БанковскиеВыписки", "ЭтоРасход", ОтборРасход);
	
	ОтборПеремещения	= Настройки.Получить("ОтборПеремещения");
	НастроитьОтборДинамическогоСпискаПоВидуДвижения("БанковскиеВыписки", "ЭтоПеремещение", ОтборПеремещения);
	
	//Итоги
	РасчетыРаботаСФормамиВызовСервера.ПередЗагрузкойНастроекИтогов(ЭтаФорма, Настройки);
	//Конец Итоги
	
КонецПроцедуры // ПриЗагрузкеДанныхИзНастроекНаСервере()

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	//Итоги
	РасчетыРаботаСФормамиВызовСервера.ПриЗагрузкеНастроекИтогов(ЭтаФорма, Настройки);
	//Конец Итоги
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.БанковскиеСчета") Тогда
		ЗагрузитьИзФайлаФрагмент(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокПлатежныхПоручений

&НаКлиенте
Процедура СписокПлатежныхПорученийПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

&НаКлиенте
Процедура БанковскиеВыпискиПриАктивизацииСтроки(Элемент)
	
	Если ВариантОтображенияИтогов = ВедомостьЗаДень Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбработатьАктивизациюСтрокиСписка", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры // БанковскиеВыпискиПриАктивизацииСтроки()

&НаКлиенте
Процедура БанковскиеВыпискиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Параметр) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ИмяДокумента = РаботаСФормойДокументаКлиент.ИмяДокументаПоТипу(Параметр);
	Если ИмяДокумента = "ПеремещениеДС" Тогда
		ДвиженияДенежныхСредствКлиент.СоздатьДокумент(ИмяДокумента, "", ДанныеМеток, "СписокПлатежныхПоручений", ПолучитьПараметрыПеремещенияДС("ПеремещениеИзКассыНаСчет"));
	ИначеЕсли ИмяДокумента = "ОперацияПоПлатежнымКартам" Тогда
		ДвиженияДенежныхСредствКлиент.СоздатьДокумент(ИмяДокумента, "", ДанныеМеток, "СписокОперацииПоПлатежнымКартам");
	Иначе
		ДвиженияДенежныхСредствКлиент.СоздатьДокумент(ИмяДокумента, "", ДанныеМеток, "БанковскиеВыписки");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОповещениеОбИзмененииДолга" Тогда
		Если ВариантОтображенияИтогов = ВедомостьЗаДень Тогда
			Подключаемый_ОбработатьАктивизациюСтрокиСписка();
		ИначеЕсли ВариантОтображенияИтогов <> НеОтображатьИтоги Тогда // Итоги по валюте (за период) и остаток в валюте.
			НастройкаИтоговЗавершениеНаСервере();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "Запись_ПеремещениеДС" ИЛИ
		ИмяСобытия = "Запись_ПоступлениеВКассу" ИЛИ
		ИмяСобытия = "Запись_ПоступлениеНаСчет" ИЛИ
		ИмяСобытия = "Запись_РасходИзКассы" ИЛИ
		ИмяСобытия = "Запись_РасходСоСчета" Тогда
		Если Параметр.Свойство("Ссылка") Тогда
			Элементы.БанковскиеВыписки.ТекущаяСтрока = Параметр.Ссылка;
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "Запись_ПлатежноеПоручение" Тогда
		Если Параметр.Свойство("Ссылка") Тогда
			Элементы.СписокПлатежныхПоручений.ТекущаяСтрока = Параметр.Ссылка;
		КонецЕсли;		
	ИначеЕсли ИмяСобытия = "Запись_ОперацияПоПлатежнымКартам" Тогда
		Если Параметр.Свойство("Ссылка") Тогда
			Элементы.СписокОперацииПоПлатежнымКартам.ТекущаяСтрока = Параметр.Ссылка;
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "НастройкиИтоговИзменены" Тогда
		РасчетыРаботаСФормамиКлиент.ОбработкаОповещенияОбИзмененииНастроекИтогов(ЭтаФорма, Параметр);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаОповещения()

&НаКлиенте
Процедура ВедомостьПерейти(Команда)
	
	ОткрытьФорму("Отчет.ДенежныеСредства.Форма", Новый Структура("КлючВарианта, СформироватьПриОткрытии", "Ведомость", Истина));
	
КонецПроцедуры // ВедомостьПерейти()

&НаКлиенте
Процедура ВедомостьПерейтиКОтчетуПоЭквайрингу(Команда)
	
	Если ВестиУчетРасчетовСБанкомЭквайрером Тогда
		Если РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям Тогда
			ОткрытьФорму("Отчет.КонтрольОперацийЭквайринга.Форма", Новый Структура("СформироватьПриОткрытии", Истина));
		Иначе
			ОткрытьФорму("Отчет.РасчетыПоЭквайрингу.Форма", Новый Структура("КлючВарианта, СформироватьПриОткрытии", "ОсновнойБезРазнесенияПоОперациямОплаты", Истина));
		КонецЕсли;
	Иначе
		ОткрытьФорму("Отчет.ОплатаПлатежнымиКартами.Форма", Новый Структура("СформироватьПриОткрытии", Истина));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик команды ВыгрузитьВКлиентБанк.
//
&НаКлиенте
Процедура ВыгрузитьВКлиентБанк(Команда)
	
	ПараметрыОткрытия = Новый Структура("НачПериода, КонПериода, БанковскийСчетОрганизации", 
		ОтборПериодПП.ДатаНачала, ОтборПериодПП.ДатаОкончания, );
	ТекущиеДанные = Элементы.СписокПлатежныхПоручений.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.БанковскийСчет) Тогда
		БанковскийСчетОрганизации = ТекущиеДанные.БанковскийСчет;
		ПараметрыОткрытия.Вставить("БанковскийСчетОрганизации", БанковскийСчетОрганизации);
	КонецЕсли;
	Если ТекущиеДанные <> Неопределено И ОтборПериодПП.ДатаНачала = '00010101' И ОтборПериодПП.ДатаОкончания = '00010101' Тогда
		ПараметрыОткрытия.Вставить("НачПериода", НачалоДня(ТекущиеДанные.Дата));
		ПараметрыОткрытия.Вставить("КонПериода", КонецДня(ТекущиеДанные.Дата));
	КонецЕсли;
		
	ОткрытьФорму(
		"Обработка.КлиентБанк.Форма.ФормаВыгрузка", 
		ПараметрыОткрытия
	);
	
КонецПроцедуры // ВыгрузитьВКлиентБанк()

&НаКлиенте
Процедура СоздатьПоШаблону(Команда)
	
	ЗаполнениеОбъектовУНФКлиент.ПоказатьВыборШаблонаДляСозданияДокументаИзСписка(
	"Документ.ПлатежноеПоручение",
	СписокПлатежныхПоручений.КомпоновщикНастроек.Настройки.Отбор.Элементы,
	Элементы.СписокПлатежныхПоручений.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПоШаблонуОперациюПоПлатежнойКарте(Команда)
	
	ЗаполнениеОбъектовУНФКлиент.ПоказатьВыборШаблонаДляСозданияДокументаИзСписка(
	"Документ.ОперацияПоПлатежнымКартам",
	СписокОперацииПоПлатежнымКартам.КомпоновщикНастроек.Настройки.Отбор.Элементы,
	Элементы.СписокОперацииПоПлатежнымКартам.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ЗагрузитьИзФайлаФрагмент();
	
КонецПроцедуры // ЗагрузитьИзФайла()

&НаКлиенте
Процедура ЗагрузитьИзФайлаФрагмент(ПараметрБанковскийСчет = Неопределено)
	
	Если ПараметрБанковскийСчет = Неопределено Тогда
		СтруктураПрямогоОбмена = КлиентБанкВызовСервера.ПолучитьСтруктуруПрямогоОбмена();
		Если СтруктураПрямогоОбмена.БанковскийСчет = Неопределено Тогда // Есть несколько счетов, нужно выбирать.
			Отбор = Новый Структура("ЭтоСчетОрганизации", Истина);
			ОткрытьФорму("Справочник.БанковскиеСчета.Форма.ФормаВыбораБезВладельца", Новый Структура("ТекущаяСтрока, Отбор", СтруктураПрямогоОбмена.БанковскийСчет, Отбор), ЭтотОбъект);
		ИначеЕсли СтруктураПрямогоОбмена.БанковскийСчет.Пустая() Тогда  // Нет ни одной настройки обмена с банками, можно сразу выбирать файл загрузки.
			УправлениеНебольшойФирмойКлиент.ЗагрузитьДанныеИзФайлаВыписки(УникальныйИдентификатор);
		Иначе                                                           // Один счет, можно сразу подставить его.
			ОткрытьФорму(
				"Обработка.КлиентБанк.Форма.ФормаЗагрузка",
				СтруктураПрямогоОбмена
			);
		КонецЕсли;
	Иначе
		СтруктураОбмена = КлиентБанкВызовСервера.ПолучитьСтруктуруОбмена(ПараметрБанковскийСчет);
		Если НЕ СтруктураОбмена.ПрямойОбмен Тогда
			КлиентБанкКлиент.ЗагрузитьДанныеИзФайлаВыписки(УникальныйИдентификатор, СтруктураОбмена);
		Иначе
			ОткрытьФорму(
				"Обработка.КлиентБанк.Форма.ФормаЗагрузка",
				СтруктураОбмена
			);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбработчикКомандСозданияДокумента(Команда)
	
	ДвиженияДенежныхСредствКлиент.ВыполнитьКомандуСозданияДокумента(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область ЗамерыПроизводительности

&НаКлиенте
Процедура СписокПлатежныхПорученийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("СозданиеФормыПлатежноеПоручение");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("ОткрытиеФормыПлатежноеПоручение");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// ИнтернетПоддержкаПользователей.Новости
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, "ПриОткрытии");
	
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.Новости


// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.СписокПлатежныхПоручений);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.СписокПлатежныхПоручений, Результат);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.СписокПлатежныхПоручений);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область Отборы

&НаКлиенте
Процедура ОтборПоступленияПриИзменении(Элемент)
	
	НастроитьОтборДинамическогоСпискаПоВидуДвижения("БанковскиеВыписки", "ЭтоПоступление", ОтборПоступления);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСписанияПриИзменении(Элемент)
	
	НастроитьОтборДинамическогоСпискаПоВидуДвижения("БанковскиеВыписки", "ЭтоРасход", ОтборРасход);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПеремещенияПриИзменении(Элемент)
	
	НастроитьОтборДинамическогоСпискаПоВидуДвижения("БанковскиеВыписки", "ЭтоПеремещение", ОтборПеремещения);
	
КонецПроцедуры

// Процедура установки отбора в динамическом списке.
//
&НаСервере
Процедура НастроитьОтборДинамическогоСпискаПоВидуДвижения(ИмяСписка, ИмяПоля, Использование)
	
	ЧислоИзмененных = ОбщегоНазначенияКлиентСервер.ИзменитьЭлементыОтбора(
		ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
		ИмяПоля,
		,
		Истина,
		ВидСравненияКомпоновкиДанных.Равно,
		Использование,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
	Если ЧислоИзмененных = 0 Тогда
		
		ГруппаДеловыеОтношения = ОбщегоНазначенияКлиентСервер.НайтиЭлементОтбораПоПредставлению(
			ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы, ИмяСписка);
		
		Если ГруппаДеловыеОтношения = Неопределено Тогда
			ГруппаДеловыеОтношения = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
				ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы,
				ИмяСписка,
				ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИЛИ);
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ГруппаДеловыеОтношения,
			ИмяПоля,
			ВидСравненияКомпоновкиДанных.Равно,
			Истина,
			,
			Использование,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборБанкПриИзменении(Элемент)
	
	НастроитьОтборДинамическогоСпискаБанкКасса();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКассаПриИзменении(Элемент)
	
	НастроитьОтборДинамическогоСпискаБанкКасса();
	
КонецПроцедуры

// Процедура установки отбора в динамическом списке.
//
&НаСервере
Процедура НастроитьОтборДинамическогоСпискаБанкКасса()
	
	Если ОтборБанк И Не ОтборКасса Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			БанковскиеВыписки,
			"ЭтоБанк",
			Истина,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Истина
		);
	ИначеЕсли Не ОтборБанк И ОтборКасса Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			БанковскиеВыписки,
			"ЭтоКасса",
			Истина,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Истина
		);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			БанковскиеВыписки,
			"ЭтоБанк",
			Истина,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Ложь
		);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			БанковскиеВыписки,
			"ЭтоКасса",
			Истина,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Ложь
		);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьРасчетныеСчетаИКассы()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Кассы.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Кассы.Ссылка) КАК Представление
		|ИЗ
		|	Справочник.Кассы КАК Кассы
		|
		|УПОРЯДОЧИТЬ ПО
		|	Кассы.Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	БанковскиеСчета.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(БанковскиеСчета.Ссылка) КАК Представление
		|ИЗ
		|	Справочник.БанковскиеСчета КАК БанковскиеСчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО БанковскиеСчета.Владелец = Организации.Ссылка";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаКасс = МассивРезультатов[0].Выбрать();
	ВыборкаСчетов = МассивРезультатов[1].Выбрать();
	
	Элементы.ОтборСчетИКасса.СписокВыбора.Очистить();
	Элементы.ОтборСчетПП.СписокВыбора.Очистить();
	
	Пока ВыборкаСчетов.Следующий() Цикл
		Элементы.ОтборСчетИКасса.СписокВыбора.Добавить(ВыборкаСчетов.Ссылка, ВыборкаСчетов.Представление,, БиблиотекаКартинок.Банк);
	КонецЦикла;
	ВыборкаСчетов.Сбросить();
	Пока ВыборкаСчетов.Следующий() Цикл
		Элементы.ОтборСчетПП.СписокВыбора.Добавить(ВыборкаСчетов.Ссылка,,, БиблиотекаКартинок.Банк);
	КонецЦикла;
	Пока ВыборкаКасс.Следующий() Цикл
		Элементы.ОтборСчетИКасса.СписокВыбора.Добавить(ВыборкаКасс.Ссылка, ВыборкаКасс.Представление,, БиблиотекаКартинок.Касса);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПанелиОтборов()
	
	ОтборПП = "ОтборВидОперацииПП,ОтборКонтрагентПП,ОтборСчетПП,ОтборВалютаПП,ОтборАвторПП,ОтборОрганизацияПП";
	ОтборЭ  = "ОтборВидОперацииЭ,ОтборБанкЭквайрерЭ,ОтборКонтрагентЭ,ОтборЭквайринговыйТерминалЭ,ОтборАвторЭ,ОтборОрганизацияЭ";
	
	РаботаСОтборами.НастроитьПанельОтборовМобильныйКлиент(ЭтаФорма, "ПраваяПанельПП", "ДанныеМетокПП", ОтборПП, "ДекорацияРазвернутьОтборыПП,СвернутьОтборыПП",Истина);
	РаботаСОтборами.НастроитьПанельОтборовМобильныйКлиент(ЭтаФорма, "ПраваяПанельЭ", "ДанныеМетокОплатаКартами", ОтборЭ, "ДекорацияРазвернутьОтборыЭ,СвернутьОтборыЭ",Истина);
	
КонецПроцедуры
 

#Область ОтборПоПериоду

&НаКлиенте
Процедура НастройкаИнтервала(Команда)
	
	УстановитьПериодНаКлиенте("БанковскиеВыписки");
	
КонецПроцедуры

// Процедура настраивает период динамического списка.
//
&НаКлиенте
Процедура УстановитьПериодНаКлиенте(ИмяСписка, ПараметрСтандартныйПериод = Неопределено)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьПериодЗавершение", ЭтотОбъект, Новый Структура("ИмяСписка", ИмяСписка));
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = ЭтотОбъект.Элементы[ИмяСписка].Период;
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

// Процедура настраивает период динамического списка (если требуется интерактивный выбор периода).
//
&НаКлиенте
Процедура УстановитьПериодЗавершение(Результат, Параметры) Экспорт
	
	УстановитьПериодЗавершениеНаСервере(Результат, Параметры);
	
КонецПроцедуры

// Процедура настривает период динамического списка на сервере (если требуется интерактивный выбор периода).
//
&НаСервере
Процедура УстановитьПериодЗавершениеНаСервере(Результат, Параметры)
	
	Если Результат <> Неопределено Тогда
		
		Элементы[Параметры.ИмяСписка].Период.Вариант = Результат.Вариант;
		Элементы[Параметры.ИмяСписка].Период.ДатаНачала = Результат.ДатаНачала;
		Элементы[Параметры.ИмяСписка].Период.ДатаОкончания = Результат.ДатаОкончания;
		Элементы[Параметры.ИмяСписка].Обновить();
		
		ВидПериодаЖурнала = ПолучитьПредставлениеПериода(Результат, " - ");
		
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает представление стандартного периода.
//
&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПредставлениеПериода(СтандартныйПериод, Разделитель = " по ")
	
	ДатаНачала = СтандартныйПериод.ДатаНачала;
	ДатаОкончания = СтандартныйПериод.ДатаОкончания;
	Если Не ЗначениеЗаполнено(ДатаНачала) И Не ЗначениеЗаполнено(ДатаОкончания) Тогда
		Возврат "Интервал: <За все время>";
	ИначеЕсли СтандартныйПериод.Вариант <> ВариантСтандартногоПериода.ПроизвольныйПериод Тогда
		Возврат "Интервал: <" + СтандартныйПериод.Вариант + ">";
	ИначеЕсли Год(ДатаНачала) = Год(ДатаОкончания) И Месяц(ДатаНачала) = Месяц(ДатаОкончания) Тогда
		Возврат "Интервал: " + Формат(ДатаНачала, "ДФ=dd")+Разделитель+Формат(ДатаОкончания, "ДФ=dd.MM.yyyy");
	ИначеЕсли Год(ДатаНачала) = Год(ДатаОкончания) Тогда
		Возврат "Интервал: " + Формат(ДатаНачала, "ДФ=dd.MM")+Разделитель+Формат(ДатаОкончания, "ДФ=dd.MM.yyyy");
	Иначе
		Возврат "Интервал: " + Формат(ДатаНачала, "ДФ=dd.MM.yyyy")+Разделитель+Формат(ДатаОкончания, "ДФ=dd.MM.yyyy");
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

&НаКлиенте
Процедура НастройкаИтогов(Команда)
	
	СтруктураПараметров = Новый Структура();
	
	СтруктураПараметров.Вставить("ВариантОтображенияИтогов", ВариантОтображенияИтогов);
	СтруктураПараметров.Вставить("ВалютаВедомости", ОтборВалютаВедомости);
	СтруктураПараметров.Вставить("ВалютаОстатков", ОтборВалютаОстатков);
	
	ОткрытьФорму("ОбщаяФорма.ФормаНастройкиИтоговБанкИКасса", 
		СтруктураПараметров, 
		ЭтотОбъект, 
		УникальныйИдентификатор,,,
		Новый ОписаниеОповещения("НастройкаИтоговЗавершение", ЭтотОбъект), 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаИтоговЗавершение(РезультатЗавершения, ПараметрыЗавершения) Экспорт
	
	Если ТипЗнч(РезультатЗавершения) = Тип("Структура") Тогда
		
		ВариантОтображенияИтогов = РезультатЗавершения.ВариантОтображенияИтогов;
		ОтборВалютаВедомости = РезультатЗавершения.ВалютаВедомости;
		ОтборВалютаОстатков = РезультатЗавершения.ВалютаОстатков;
		
		НастройкаИтоговЗавершениеНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастройкаИтоговЗавершениеНаСервере()

	РасчетыРаботаСФормамиВызовСервера.НастройкаИтоговЗавершениеНаСервере(ЭтаФорма);

КонецПроцедуры
 

&НаКлиенте
Процедура ПерейтиКВалютам(Команда)
	
	ОткрытьФорму("Справочник.Валюты.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьИтогиКонтекстноеМеню(Команда)
	
	РасчетыРаботаСФормамиКлиент.ОткрытьФормуИтоговКонтекстноеМеню(ЭтаФорма, "БанкИКасса");
	
КонецПроцедуры


#Область ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ДИНАМИЧЕСКОГО СПИСКА

// Выбор значения отбора в поле отбора
&НаКлиенте
Процедура ОтборКонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("БанковскиеВыписки", "Контрагент", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("БанковскиеВыписки", "ОрганизацияДляОтбора", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСчетИКассаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("БанковскиеВыписки", "СчетКассаДляОтбора", Элемент.Родитель.Имя, ВыбранноеЗначение,, "СписокКассИСчетов");
	ВыбранноеЗначение = ПредопределенноеЗначение("Справочник.Кассы.ПустаяСсылка");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборАвторОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("БанковскиеВыписки", "Автор", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборАвторППОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокПлатежныхПоручений", "Автор", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборАвторЭОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокОперацииПоПлатежнымКартам", "Автор", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСчетИКассаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если НЕ ПустаяСтрока(Текст) Тогда
		ДанныеВыбора = Новый СписокЗначений;
		Для каждого ЭлементСписка Из Элементы.ОтборСчетИКасса.СписокВыбора Цикл
			Позиция = Найти(НРег(ЭлементСписка.Представление), НРег(Текст));
			Если Позиция=0 Тогда
				Продолжить;
			КонецЕсли;
			МассивСоставляющих = Новый Массив;
			МассивСоставляющих.Добавить(Лев(ЭлементСписка.Представление, Позиция-1));
			МассивСоставляющих.Добавить(Новый ФорматированнаяСтрока(Сред(ЭлементСписка.Представление, Позиция, СтрДлина(Текст)), Новый Шрифт(Новый Шрифт, , , Истина), Новый Цвет(0, 150, 70)));
			МассивСоставляющих.Добавить(Сред(ЭлементСписка.Представление, Позиция+СтрДлина(Текст)));
			ДанныеВыбора.Добавить(ЭлементСписка.Значение, Новый ФорматированнаяСтрока(МассивСоставляющих), , ЭлементСписка.Картинка);
		КонецЦикла; 
		Если ДанныеВыбора.Количество()>0 Тогда
			СтандартнаяОбработка = Ложь;
		КонецЕсли; 
	КонецЕсли; 		
	
КонецПроцедуры

#КонецОбласти

#Область Отборы

&НаСервере
Процедура УстановитьМеткуИОтборСписка(СписокДляОтбора, ИмяПоляОтбораСписка, ГруппаРодительМетки, ВыбранноеЗначение, ПредставлениеЗначения="", ИмяПараметраЗапроса="")
	
	Если ПредставлениеЗначения="" Тогда
		ПредставлениеЗначения=Строка(ВыбранноеЗначение);
	КонецЕсли; 
	
	Если СписокДляОтбора = "СписокПлатежныхПоручений" Тогда
		ИмяТЧДанныеМеток = "ДанныеМетокПП";
	ИначеЕсли СписокДляОтбора = "СписокОперацииПоПлатежнымКартам" Тогда
		ИмяТЧДанныеМеток = "ДанныеМетокОплатаКартами";
	Иначе
		ИмяТЧДанныеМеток = "ДанныеМеток";
	КонецЕсли;
	
	РаботаСОтборами.ПрикрепитьМеткуОтбора(ЭтотОбъект, ИмяПоляОтбораСписка, ГруппаРодительМетки, ВыбранноеЗначение, ПредставлениеЗначения, СписокДляОтбора, ИмяПараметраЗапроса, ИмяТЧДанныеМеток);
	
	Если ИмяПараметраЗапроса="" Тогда
		
		РаботаСОтборами.УстановитьОтборСписка(ЭтотОбъект, ЭтотОбъект[СписокДляОтбора], ИмяПоляОтбораСписка, СписокДляОтбора,, ИмяТЧДанныеМеток);
		
	Иначе
		
		СтруктураОтбораМеток = Новый Структура("ИмяПараметраЗапроса", ИмяПараметраЗапроса);
		НайденныеСтроки = ЭтотОбъект[ИмяТЧДанныеМеток].НайтиСтроки(СтруктураОтбораМеток);
		МассивОтбора = Новый Массив;
		Для каждого стр Из НайденныеСтроки Цикл
			МассивОтбора.Добавить(стр.Метка);
		КонецЦикла;
		ЭтотОбъект[СписокДляОтбора].Параметры.УстановитьЗначениеПараметра("БезОтбора", НайденныеСтроки.Количество()=0);
		ЭтотОбъект[СписокДляОтбора].Параметры.УстановитьЗначениеПараметра(ИмяПараметраЗапроса, МассивОтбора);
		
	КонецЕсли;
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_МеткаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяГруппыРодителя = СокрЛП(Элемент.Родитель.Имя);
	Если СтрНачинаетсяС(ИмяГруппыРодителя, "МеткаПП_") Тогда
		ИмяРеквизитаСписка = "СписокПлатежныхПоручений";
		ИмяТЧДанныеМеток = "ДанныеМетокПП";
		ИмяТЧДанныеОтборов = "ДанныеОтборовПП";
		НачалоИмениМетки = "МеткаПП_";
	ИначеЕсли СтрНачинаетсяС(ИмяГруппыРодителя, "МеткаОплатаКартами_") Тогда
		ИмяРеквизитаСписка = "СписокОперацииПоПлатежнымКартам";
		ИмяТЧДанныеМеток = "ДанныеМетокОплатаКартами";
		НачалоИмениМетки = "МеткаОплатаКартами_";
		ИмяТЧДанныеОтборов = "ДанныеОтборовОплатаКартами";
	Иначе
		ИмяРеквизитаСписка = "БанковскиеВыписки";
		ИмяТЧДанныеМеток = "ДанныеМеток";
		ИмяТЧДанныеОтборов = "ДанныеОтборов";
		НачалоИмениМетки = "Метка_";
	КонецЕсли;
	
	МеткаИД = Сред(Элемент.Имя, СтрДлина(НачалоИмениМетки)+1);
	
	УдалитьМеткуОтбора(МеткаИД, ИмяРеквизитаСписка, ИмяТЧДанныеМеток, ИмяТЧДанныеОтборов);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьМеткуОтбора(МеткаИД, ИмяРеквизитаСписка, ИмяТЧДанныеМеток, ИмяТЧДанныеОтборов)
	
	РаботаСОтборами.УдалитьМеткуОтбораСервер(ЭтотОбъект, ЭтотОбъект[ИмяРеквизитаСписка], МеткаИД, ИмяРеквизитаСписка, ИмяТЧДанныеМеток,,,,ИмяТЧДанныеОтборов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаНажатие(Элемент, СтандартнаяОбработка)
	
	СтруктураИменЭлементов = Новый Структура("ОтборПериод, ПредставлениеПериода, СобытиеОповещения", "ОтборПериод", "ПредставлениеПериода", "ОповещениеОбИзмененииДолга");
	
	СтандартнаяОбработка = Ложь;
	РаботаСОтборамиКлиент.ПредставлениеПериодаВыбратьПериод(ЭтотОбъект, "БанковскиеВыписки", "Дата", СтруктураИменЭлементов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаППНажатие(Элемент, СтандартнаяОбработка)
	
	СтруктураИменЭлементов = Новый Структура("ОтборПериод, ПредставлениеПериода", "ОтборПериодПП", "ПредставлениеПериодаПП");
	
	СтандартнаяОбработка = Ложь;
	РаботаСОтборамиКлиент.ПредставлениеПериодаВыбратьПериод(ЭтотОбъект, "СписокПлатежныхПоручений", "Дата", СтруктураИменЭлементов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаЭНажатие(Элемент, СтандартнаяОбработка)
	
	СтруктураИменЭлементов = Новый Структура("ОтборПериод, ПредставлениеПериода", "ОтборПериодЭ", "ПредставлениеПериодаЭ");
	
	СтандартнаяОбработка = Ложь;
	РаботаСОтборамиКлиент.ПредставлениеПериодаВыбратьПериод(ЭтотОбъект, "СписокОперацииПоПлатежнымКартам", "Дата", СтруктураИменЭлементов);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиОтборов()
	
	Если НЕ ЭтоНачальнаяСтраница Тогда
	
		РаботаСОтборами.СохранитьНастройкиОтборов(ЭтотОбъект, "БанковскиеВыписки");
		
		СтруктураИменЭлементовПП = Новый Структура;
		СтруктураИменЭлементовПП.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоПП");
		СтруктураИменЭлементовПП.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыПП");
		СтруктураИменЭлементовПП.Вставить("ПраваяПанель", "ПраваяПанельПП");
		СтруктураИменЭлементовПП.Вставить("ОтборПериод", "ОтборПериодПП");
		
		РаботаСОтборами.СохранитьНастройкиОтборов(ЭтотОбъект, "СписокПлатежныхПоручений", СтруктураИменЭлементовПП,,, "ДанныеМетокПП", "ДанныеОтборовПП", "ГруппаОтборыПП");
		
		СтруктураИменЭлементовЭ = Новый Структура;
		СтруктураИменЭлементовЭ.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоЭ");
		СтруктураИменЭлементовЭ.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыЭ");
		СтруктураИменЭлементовЭ.Вставить("ПраваяПанель", "ПраваяПанельЭ");
		СтруктураИменЭлементовЭ.Вставить("ОтборПериод", "ОтборПериодЭ");
		
		РаботаСОтборами.СохранитьНастройкиОтборов(ЭтотОбъект, "СписокОперацииПоПлатежнымКартам", СтруктураИменЭлементовЭ,,, "ДанныеМетокОплатаКартами", "ДанныеОтборовОплатаКартами", "ГруппаОтборыЭ");
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьРазвернутьПанельОтборов(Элемент)
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфо.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость);
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьОтборыППНажатие(Элемент)
	
	СтруктураИменЭлементов = Новый Структура;
	СтруктураИменЭлементов.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоПП");
	СтруктураИменЭлементов.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыПП");
	СтруктураИменЭлементов.Вставить("ПраваяПанель", "ПраваяПанельПП");
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфоПП.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость, СтруктураИменЭлементов);
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьОтборыЭНажатие(Элемент)
	
	СтруктураИменЭлементов = Новый Структура;
	СтруктураИменЭлементов.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоЭ");
	СтруктураИменЭлементов.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыЭ");
	СтруктураИменЭлементов.Вставить("ПраваяПанель", "ПраваяПанельЭ");
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфоЭ.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость, СтруктураИменЭлементов);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРазвернутьОтборыППНажатие(Элемент)
	
	СтруктураИменЭлементов = Новый Структура;
	СтруктураИменЭлементов.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоПП");
	СтруктураИменЭлементов.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыПП");
	СтруктураИменЭлементов.Вставить("ПраваяПанель", "ПраваяПанельПП");
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфоПП.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость, СтруктураИменЭлементов);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРазвернутьОтборыЭНажатие(Элемент)
	
	СтруктураИменЭлементов = Новый Структура;
	СтруктураИменЭлементов.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоЭ");
	СтруктураИменЭлементов.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыЭ");
	СтруктураИменЭлементов.Вставить("ПраваяПанель", "ПраваяПанельЭ");
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфоЭ.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость, СтруктураИменЭлементов);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОтборы(Команда)
	
	ПоискМетки= ЭтаФорма.ТекущийЭлемент.Имя;
	Если СтрЗаканчиваетсяНа(ПоискМетки, "ПП") Тогда
		ИмяРеквизитаСписка = "СписокПлатежныхПоручений";
		ИмяТЧДанныеМеток = "ДанныеМетокПП";
		ИмяТЧДанныеОтборов = "ДанныеОтборовПП";
		ИмяГруппыОтборов = "ГруппаОтборыПП";
		ИмяПредопределенныеОтборыПоУмолчанию = "ПредопределенныеОтборыПоУмолчаниюПП";
	ИначеЕсли СтрЗаканчиваетсяНа(ПоискМетки, "Э") Тогда
		ИмяРеквизитаСписка = "СписокОперацииПоПлатежнымКартам";
		ИмяТЧДанныеМеток = "ДанныеМетокОплатаКартами";
		ИмяТЧДанныеОтборов = "ДанныеОтборовОплатаКартами";
		ИмяГруппыОтборов = "ГруппаОтборыЭ";
		ИмяПредопределенныеОтборыПоУмолчанию = "ПредопределенныеОтборыПоУмолчаниюЭ";
	Иначе
		ИмяРеквизитаСписка = "БанковскиеВыписки";
		ИмяТЧДанныеМеток = "ДанныеМеток";
		ИмяТЧДанныеОтборов = "ДанныеОтборов";
		ИмяГруппыОтборов = "ГруппаОтборы";
		ИмяПредопределенныеОтборыПоУмолчанию = "ПредопределенныеОтборыПоУмолчанию";
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяРеквизитаСписка", ИмяРеквизитаСписка);
	ДополнительныеПараметры.Вставить("ИмяТЧДанныеМеток", ИмяТЧДанныеМеток);
	ДополнительныеПараметры.Вставить("ИмяТЧДанныеОтборов", ИмяТЧДанныеОтборов);
	ДополнительныеПараметры.Вставить("ИмяГруппыОтборов", ИмяГруппыОтборов);
	ДополнительныеПараметры.Вставить("ИмяПредопределенныеОтборыПоУмолчанию", ИмяПредопределенныеОтборыПоУмолчанию);
	
	РаботаСОтборамиКлиент.НастроитьОтборыНажатие(ЭтотОбъект, ПараметрыОткрытияФормыСНастройкамиОтборов(ДополнительныеПараметры), ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыОткрытияФормыСНастройкамиОтборов(ДополнительныеПараметры)
	
	Возврат РаботаСОтборами.ПараметрыДляОткрытияФормыСНастройкамиОтборов(ЭтотОбъект, ДополнительныеПараметры);
	
КонецФункции

&НаКлиенте
Процедура НастройкаОтборовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаОтборовЗавершениеНаСервере(Результат.АдресВыбранныеОтборы, Результат.АдресУдаленныеОтборы, ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура НастройкаОтборовЗавершениеНаСервере(АдресВыбранныеОтборы, АдресУдаленныеОтборы, ДополнительныеПараметры)
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ИмяРеквизитаСписка = "Список";
		ИмяТЧДанныеМеток = "ДанныеМеток";
		ИмяТЧДанныеОтборов = "ДанныеОтборов";
	Иначе
		ИмяРеквизитаСписка = ДополнительныеПараметры.ИмяРеквизитаСписка;
		ИмяТЧДанныеМеток = ДополнительныеПараметры.ИмяТЧДанныеМеток;
		ИмяТЧДанныеОтборов = ДополнительныеПараметры.ИмяТЧДанныеОтборов;
	КонецЕсли;
	
	РаботаСОтборами.НастройкаОтборовЗавершение(ЭтотОбъект, АдресВыбранныеОтборы, АдресУдаленныеОтборы, ДополнительныеПараметры);
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОтборПриИзменении(Элемент)
	
	Подключаемый_ОтборПриИзмененииНаСервере(Элемент.Имя, Элемент.Родитель.Имя)
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ОтборПриИзмененииНаСервере(ЭлементИмя, ЭлементРодительИмя)
	
	ИмяРеквизитаСписка = Прав(ЭлементИмя, СтрДлина(ЭлементИмя)-СтрНайти(ЭлементИмя, "_", НаправлениеПоиска.СНачала));
	ИмяРеквизитаСписка = Лев(ИмяРеквизитаСписка, СтрНайти(ИмяРеквизитаСписка,"_", НаправлениеПоиска.СНачала)-1);
	
	Если ИмяРеквизитаСписка = "СписокПлатежныхПоручений" Тогда
		//ИмяРеквизитаСписка = "СписокПлатежныхПоручений";
		ИмяТЧДанныеМеток = "ДанныеМетокПП";
		ИмяТЧДанныеОтборов = "ДанныеОтборовПП";
	ИначеЕсли ИмяРеквизитаСписка = "СписокОперацииПоПлатежнымКартам" Тогда
		//ИмяРеквизитаСписка = "СписокОперацииПоПлатежнымКартам";
		ИмяТЧДанныеМеток = "ДанныеМетокОплатаКартами";
		ИмяТЧДанныеОтборов = "ДанныеОтборовОплатаКартами";
	Иначе
		//ИмяРеквизитаСписка = "БанковскиеВыписки";
		ИмяТЧДанныеМеток = "ДанныеМеток";
		ИмяТЧДанныеОтборов = "ДанныеОтборов";
	КонецЕсли;
	
	РаботаСОтборами.Подключаемый_ОтборПриИзменении(ЭтотОбъект, ЭлементИмя, ЭлементРодительИмя, ИмяРеквизитаСписка, ИмяТЧДанныеМеток, ИмяТЧДанныеОтборов);
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОтборОчистка(Элемент)
	
	Подключаемый_ОтборОчисткаНаСервере(Элемент.Имя, Элемент.Родитель.Имя)
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ОтборОчисткаНаСервере(ЭлементИмя, ЭлементРодительИмя)

	ИмяРеквизитаСписка = Прав(ЭлементИмя, СтрДлина(ЭлементИмя)-СтрНайти(ЭлементИмя, "_", НаправлениеПоиска.СНачала));
	ИмяРеквизитаСписка = Лев(ИмяРеквизитаСписка, СтрНайти(ИмяРеквизитаСписка,"_", НаправлениеПоиска.СНачала)-1);
	
	Если ИмяРеквизитаСписка = "СписокПлатежныхПоручений" Тогда
		//ИмяРеквизитаСписка = "СписокПлатежныхПоручений";
		ИмяТЧДанныеМеток = "ДанныеМетокПП";
		ИмяТЧДанныеОтборов = "ДанныеОтборовПП";
	ИначеЕсли ИмяРеквизитаСписка = "СписокОперацииПоПлатежнымКартам" Тогда
		//ИмяРеквизитаСписка = "СписокОперацииПоПлатежнымКартам";
		ИмяТЧДанныеМеток = "ДанныеМетокОплатаКартами";
		ИмяТЧДанныеОтборов = "ДанныеОтборовОплатаКартами";
	Иначе
		//ИмяРеквизитаСписка = "БанковскиеВыписки";
		ИмяТЧДанныеМеток = "ДанныеМеток";
		ИмяТЧДанныеОтборов = "ДанныеОтборов";
	КонецЕсли;
	
	РаботаСОтборами.Подключаемый_ОтборОчистка(ЭтотОбъект, ЭлементИмя, ИмяРеквизитаСписка, ИмяТЧДанныеМеток, ИмяТЧДанныеОтборов);

КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Дата1Нажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элементы.БанковскиеВыписки.ТекущиеДанные <> Неопределено Тогда
		ПериодОтчета = Новый СтандартныйПериод;
		ПериодОтчета.ДатаНачала = Элементы.БанковскиеВыписки.ТекущиеДанные.Дата;
		ПериодОтчета.ДатаОкончания = Элементы.БанковскиеВыписки.ТекущиеДанные.Дата;
		ПараметрыОтчета = Новый Структура("СтПериод", ПериодОтчета);
	Иначе
		ПериодОтчета = Новый СтандартныйПериод;
		ПериодОтчета.ДатаНачала = '00010101';
		ПериодОтчета.ДатаОкончания = '00010101';
		ПараметрыОтчета = Новый Структура("СтПериод", ПериодОтчета);
	КонецЕсли;
	
	ОткрытьФорму("Отчет.ДенежныеСредства.Форма", Новый Структура("КлючВарианта, СформироватьПриОткрытии, Отбор", "ВедомостьВВалюте", Истина, ПараметрыОтчета));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВалютаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НастройкаИтогов(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВалюта1Нажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НастройкаИтогов(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкаВыводаИтоговНажатие(Элемент)
	
	НастройкаИтогов(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОстатокВВалютеНажатие(Элемент)
	
	ПериодОтчета = Новый СтандартныйПериод;
	ПериодОтчета.ДатаНачала = ТекущаяДата();
	ПериодОтчета.ДатаОкончания = ТекущаяДата();
	ПараметрыОтчета = Новый Структура("СтПериод, Валюта", ПериодОтчета, ОтборВалютаОстатков);
	
	ОткрытьФорму("Отчет.ДенежныеСредства.Форма", Новый Структура("КлючВарианта, СформироватьПриОткрытии, Отбор", "ОстаткиВВалюте", Истина, ПараметрыОтчета));
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВедомостьПоВалютеНажатие(Элемент)
	
	ПараметрыОтчета = Новый Структура("СтПериод, Валюта", ОтборПериод, ОтборВалютаВедомости);
	ОткрытьФорму("Отчет.ДенежныеСредства.Форма", Новый Структура("КлючВарианта, СформироватьПриОткрытии, Отбор", "ВедомостьВВалюте", Истина, ПараметрыОтчета));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВалютаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("БанковскиеВыписки", "Валюта", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОткрытьФормуНастройкиИтоговНажатие(Элемент)
	
	НастройкаИтогов(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПоШаблонуДокументДвиженияДС(Команда)
	
	ЗаполнениеОбъектовУНФКлиент.ПоказатьВыборШаблонаДляСозданияДокументаИзСписка(
	"ЖурналДокументов.ДокументыПоБанкуИКассе",
	БанковскиеВыписки.КомпоновщикНастроек.Настройки.Отбор.Элементы,
	Элементы.БанковскиеВыписки.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборХозОперацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Обработка.БанкИКасса.Форма.ФормаВыбораВидаОперации",,,,,,
		Новый ОписаниеОповещения("ОтборХозОперацияНачалоВыбораЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборХозОперацияНачалоВыбораЗавершение(РезультатЗавершения, ПараметрыЗавершения) Экспорт
	
	Если ЗначениеЗаполнено(РезультатЗавершения) Тогда
		Если ТипЗнч(РезультатЗавершения) = Тип("Строка") Тогда
			УстановитьМеткуИОтборСписка("БанковскиеВыписки", "ХозяйственнаяОперация", "ГруппаОтборХозОперация",
				"Перемещение. " + РезультатЗавершения);
		Иначе
			УстановитьМеткуИОтборСписка("БанковскиеВыписки", "ХозяйственнаяОперация", "ГруппаОтборХозОперация",
				РезультатЗавершения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидОперацииППОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокПлатежныхПоручений", "ВидОперации", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидОперацииЭОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокОперацииПоПлатежнымКартам", "ВидОперации", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентППОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокПлатежныхПоручений", "Контрагент", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборБанкЭквайрерОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокОперацииПоПлатежнымКартам", "Эквайрер", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентЭОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокОперацииПоПлатежнымКартам", "Контрагент", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСчетППОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокПлатежныхПоручений", "БанковскийСчет", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВалютаППОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокПлатежныхПоручений", "ВалютаДокумента", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияППОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокПлатежныхПоручений", "Организация", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияЭОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокОперацииПоПлатежнымКартам", "Организация", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборЭквайринговыйТерминалОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокОперацииПоПлатежнымКартам", "ЭквайринговыйТерминал", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВКлиентБанкаТекущийДокумент(Команда)
	
	ТекущиеДанные = Элементы.СписокПлатежныхПоручений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(Неопределено, "Необходимо выделить документ, который хотите выгрузить в клиент-банка.");
		Возврат;
	КонецЕсли;
	
	СписокВыгрузки = Новый СписокЗначений;
	СписокВыгрузки.Добавить(ТекущиеДанные.Ссылка);
	ПараметрыОткрытия = Новый Структура("СписокПлатежек, БанковскийСчет", СписокВыгрузки, ТекущиеДанные.БанковскийСчет);
	ОткрытьФорму("Обработка.КлиентБанк.Форма.СохранениеПлатежек", ПараметрыОткрытия,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НЕ ЗавершениеРаботы Тогда
		//УНФ.ОтборыСписка
		СохранитьНастройкиОтборов();
		//Конец УНФ.ОтборыСписка
	КонецЕсли;
	
КонецПроцедуры

#Область ПеремещениеДС

&НаСервереБезКонтекста
Функция ПолучитьПараметрыПеремещенияДС(ИмяКоманды)
	
	Если ИмяКоманды = "ПеремещениеИзКассыНаСчет" Тогда
		ТипДенежныхСредств = Перечисления.ТипыДенежныхСредств.Наличные;
		ТипДенежныхСредствПолучатель = Перечисления.ТипыДенежныхСредств.Безналичные;
	ИначеЕсли ИмяКоманды = "ПеремещениеСоСчетаВКассу" Тогда
		ТипДенежныхСредств = Перечисления.ТипыДенежныхСредств.Безналичные;
		ТипДенежныхСредствПолучатель = Перечисления.ТипыДенежныхСредств.Наличные;
	ИначеЕсли ИмяКоманды = "ПеремещениеМеждуСчетами" Тогда
		ТипДенежныхСредств = Перечисления.ТипыДенежныхСредств.Безналичные;
		ТипДенежныхСредствПолучатель = Перечисления.ТипыДенежныхСредств.Безналичные;
	ИначеЕсли ИмяКоманды = "ПеремещениеМеждуКассами" Тогда
		ТипДенежныхСредств = Перечисления.ТипыДенежныхСредств.Наличные;
		ТипДенежныхСредствПолучатель = Перечисления.ТипыДенежныхСредств.Наличные;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", Новый Структура("ТипДенежныхСредств, ТипДенежныхСредствПолучатель", 
		ТипДенежныхСредств, ТипДенежныхСредствПолучатель));
		
	Возврат ПараметрыОткрытия;
	
КонецФункции

&НаКлиенте
Процедура ПеремещениеИзКассыНаСчет(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходИзКассы", "ВзносНаличнымиВБанк", ДанныеМеток);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеремещениеСоСчетаВКассу(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеВКассу", "ПолучениеНаличныхВБанке", ДанныеМеток);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеремещениеМеждуСчетами(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "ПереводНаДругойСчет", ДанныеМеток);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеремещениеМеждуКассами(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПеремещениеДС", "ПеремещениеДС", ДанныеМеток, "БанковскиеВыписки",
		ПолучитьПараметрыПеремещенияДС(Команда.Имя));
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеВзносНаличнымиБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеНаСчет", "ВзносНаличными", ДанныеМеток);
	
КонецПроцедуры

&НаКлиенте
Процедура РасходСнятиеНаличныхБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "СнятиеНаличных", ДанныеМеток);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеПереводСДругогоСчетаБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеНаСчет", "ПереводСДругогоСчета", ДанныеМеток);
	
КонецПроцедуры

#КонецОбласти

#Область ОперацияПоПлатежнымКартам

&НаКлиенте
Процедура ОплатаКартойПоступлениеОплатыОтПокупателя(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ОперацияПоПлатежнымКартам", "ПоступлениеОплатыОтПокупателя",
		ДанныеМетокОплатаКартами, "СписокОперацииПоПлатежнымКартам");
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаКартойВозвратОплатыПокупателю(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ОперацияПоПлатежнымКартам", "ВозвратОплатыПокупателю",
		ДанныеМетокОплатаКартами, "СписокОперацииПоПлатежнымКартам");
	
КонецПроцедуры

#КонецОбласти
