
#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("МассивВидыЦенКонтрагентов") Тогда
		
		Для каждого ЭлементаМассива Из Параметры.МассивВидыЦенКонтрагентов Цикл
			
			НоваяСтрока = Объект.ОтборВидовЦенКонтрагентов.Добавить();
			НоваяСтрока.ВидЦенКонтрагента = ЭлементаМассива.Ссылка;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры //ПриСозданииНаСервере()

&НаКлиенте
// Процедура - обработчик события ОбработкаВыбора формы.
//
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если НЕ ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		
		ЗначениеВыбора 		= ВыбранноеЗначение;
		ВыбранноеЗначение	= Новый Массив;
		ВыбранноеЗначение.Добавить(ЗначениеВыбора);
		
	КонецЕсли;
	
	ДобавитьЭлементыВТабличнуюЧасть(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Процедура - обработчик команды ОК.
//
Процедура ОК(Команда)
	
	Результат = Новый Структура;
	Результат.Вставить("МассивВидыЦенКонтрагентов",		Новый Массив);
	
	Для каждого СтрокаТаблицы Из Объект.ОтборВидовЦенКонтрагентов Цикл
		
		Результат.МассивВидыЦенКонтрагентов.Добавить(СтрокаТаблицы.ВидЦенКонтрагента);
		
	КонецЦикла;
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);

	ОткрытьФорму("Справочник.ВидыЦенКонтрагентов.Форма.ФормаБыстрогоВыбора", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Добавляет элементы массива в табличную часть.
// Предварительно проверяем на наличие данного элемента в табличной части.
//
Процедура ДобавитьЭлементыВТабличнуюЧасть(МассивЭлементов)

	Если Не ТипЗнч(МассивЭлементов) = Тип("Массив") Или Не МассивЭлементов.Количество() > 0 Тогда

		Возврат;

	КонецЕсли;

	Для Каждого ЭлементМассива Из МассивЭлементов Цикл

		Если Объект.ОтборВидовЦенКонтрагентов.НайтиСтроки(Новый Структура("ВидЦенКонтрагента",
			ЭлементМассива)).Количество() > 0 Тогда

			ТекстСообщения = СтрШаблон(НСтр("ru = 'Элемент [%1] присутствует в отборе.'"), ЭлементМассива);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Продолжить;

		КонецЕсли;

		НоваяСтрока 					= Объект.ОтборВидовЦенКонтрагентов.Добавить();
		НоваяСтрока.ВидЦенКонтрагента	= ЭлементМассива;

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

