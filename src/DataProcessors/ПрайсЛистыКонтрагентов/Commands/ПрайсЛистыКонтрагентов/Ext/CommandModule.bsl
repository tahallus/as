#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	КонтекстОткрытия = КонтекстОткрытияПрайсЛиста();
	
	ИмяОткрываемойФормы = "Справочник.ПрайсЛистыКонтрагентов.ФормаСписка";
	Если КонтекстОткрытия.ЕстьПравоДобавленияНового
		И (КонтекстОткрытия.КоличествоПрайсЛистов < 2
			ИЛИ КонтекстОткрытия.ОткрыватьПрайсЛистКонтрагентовКакСписок = Ложь) Тогда
		
		ИмяОткрываемойФормы = "Обработка.ПрайсЛистыКонтрагентов.Форма";
		
	КонецЕсли;
	
	ОткрытьФорму(ИмяОткрываемойФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция КонтекстОткрытияПрайсЛиста()
	
	КонтекстОткрытия = Новый Структура;
	КонтекстОткрытия.Вставить("КоличествоПрайсЛистов",
		Справочники.ПрайсЛистыКонтрагентов.КоличествоПрайсЛистовКонтрагентовПользователя());
	КонтекстОткрытия.Вставить("ОткрыватьПрайсЛистКонтрагентовКакСписок",
		УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ОткрыватьПрайсЛистКонтрагентовКакСписок"));
	КонтекстОткрытия.Вставить("ЕстьПравоДобавленияНового", ПравоДоступа("Добавление",
		Метаданные.Справочники.ПрайсЛистыКонтрагентов));
	
	Возврат КонтекстОткрытия;
	
КонецФункции

#КонецОбласти
