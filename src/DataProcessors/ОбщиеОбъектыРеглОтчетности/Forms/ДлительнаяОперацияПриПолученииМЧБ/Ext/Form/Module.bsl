&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ПолученОтветПользователя;

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайлаОтчета = Параметры.ИмяФайлаОтчета;
	
	Если НЕ ЗначениеЗаполнено(ИмяФайлаОтчета) Тогда
		
		ИмяФайлаОтчета = "" + Новый УникальныйИдентификатор() + ".pdf";
		
	КонецЕсли;
	
	ПараметрыВызоваЭкспортнойПроцедуры = Параметры.ПараметрыВызоваЭкспортнойПроцедуры;
	
	РасчетноеВремяПолученияОтчета = ТекущаяДатаСеанса() + ПараметрыВызоваЭкспортнойПроцедуры.ВремяОжиданияДоПолученияОтчета;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПолученОтветПользователя = Ложь;
	
	ПараметрыОбработчикаОжидания = Новый Структура;
	ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 5);   // минимальный интервал для проверок  - 5 сек.
	ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 10); // максимальный интервал для проверок - 10 сек.
	ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);
	
	Если ПараметрыВызоваЭкспортнойПроцедуры.ВремяОжиданияДоПолученияОтчета > 60 Тогда
		Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = "Пожалуйста, подождите до "
			+ Формат(РасчетноеВремяПолученияОтчета + 60, "ДФ=Ч:мм; ДЛФ=T") + "...";
	Иначе
		Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = "Пожалуйста, подождите...";
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьГотовностьОтчетаНаВебСервисе", ПараметрыОбработчикаОжидания.МинимальныйИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьГотовностьОтчетаНаВебСервисе()
	
	Попытка
		
		Если ПроверитьГотовностьОтчетаНаВебСервисе() Тогда
			
			ВывестиМашиночитаемуюФорму();
			
			Возврат;
			
		КонецЕсли;
		
	Исключение
		
		Закрыть();
		
		ВызватьИсключение;
		
	КонецПопытки;
	
	ПараметрыОбработчикаОжидания.ТекущийИнтервал = 
		ПараметрыОбработчикаОжидания.ТекущийИнтервал * ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
	Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
	КонецЕсли;
	
	Если НЕ ПолученОтветПользователя И (ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал И РасчетноеВремяПолученияОтчета < ТекущаяДатаНаСервере()) Тогда
	
		СогласиеПользователяНаПродлениеОжиданияИлиЗакрытие(ПараметрыОбработчикаОжидания);
		Возврат;
		
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьГотовностьОтчетаНаВебСервисе", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаСервере
Функция ПроверитьГотовностьОтчетаНаВебСервисе()
	
	Если РегламентированнаяОтчетностьВызовСервера.ПолучитьМашиночитаемуюФормуСВебСервиса(ПараметрыВызоваЭкспортнойПроцедуры) Тогда
		
		Если ЗначениеЗаполнено(ПараметрыВызоваЭкспортнойПроцедуры.АдресОтчетаВоВременномХранилище) Тогда
			
			Возврат Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура ВывестиМашиночитаемуюФорму()
	
	АдресОтчетаВоВременномХранилище = ПараметрыВызоваЭкспортнойПроцедуры.АдресОтчетаВоВременномХранилище;
	
	#Если ВебКлиент Тогда
		
		ПолучитьФайл(АдресОтчетаВоВременномХранилище, ИмяФайлаОтчета, Истина);
		
		Закрыть();
		
	#Иначе
		
		КаталогВременногоФайла = ПолучитьИмяВременногоФайла(""); // удаляется в процедуре "УдалитьФайлИЗакрытьФорму"
		КаталогВременногоФайла = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогВременногоФайла);
		СоздатьКаталог(КаталогВременногоФайла);
		
		ПолноеИмяФайлаОтчета = КаталогВременногоФайла + ИмяФайлаОтчета;
		
		ПередаваемыйФайл = Новый ОписаниеПередаваемогоФайла;
		ПередаваемыйФайл.Имя      = ПолноеИмяФайлаОтчета;
		ПередаваемыйФайл.Хранение = АдресОтчетаВоВременномХранилище;
		
		МассивПередаваемыхФайлов = Новый Массив;
		МассивПередаваемыхФайлов.Добавить(ПередаваемыйФайл);
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения(
			"ВывестиМашиночитаемуюФормуНеВебклиентОбработатьПолучениеФайлов", ЭтотОбъект,
			Новый Структура("КаталогВременногоФайла, ПолноеИмяФайлаОтчета",
							 КаталогВременногоФайла, ПолноеИмяФайлаОтчета));
		НачатьПолучениеФайлов(ОповещениеОЗавершении, МассивПередаваемыхФайлов, , Ложь);
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиМашиночитаемуюФормуНеВебклиентОбработатьПолучениеФайлов(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	
	КаталогВременногоФайла = ДополнительныеПараметры.КаталогВременногоФайла;
	
	ДополнительныеПараметры.Вставить("ИмяФайлаОтчета", КаталогВременногоФайла);
	
	Если ПолученныеФайлы = Неопределено Тогда
		УдалитьФайлИЗакрытьФорму(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ПолноеИмяФайлаОтчета = ДополнительныеПараметры.ПолноеИмяФайлаОтчета;
	
	КодВозврата = Неопределено;
	ВремяОткрытияФайла = ТекущаяДатаНаСервере();
	ЗапуститьПриложение("""" + ПолноеИмяФайлаОтчета + """", , Истина, КодВозврата);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"УдалитьФайлИЗакрытьФорму", ЭтотОбъект, ДополнительныеПараметры);
	
	Если КодВозврата = Неопределено ИЛИ ТекущаяДатаНаСервере() < ВремяОткрытияФайла + 10 Тогда
		Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = 
			НСтр("ru = 'Файл отчета открыт ассоциированным приложением...'");
		ПоказатьПредупреждение(ОписаниеОповещения,
			НСтр("ru = 'Сформированный PDF-файл отчета будет удален после закрытия формы ожидания'"), 10);
	Иначе
		УдалитьФайлИЗакрытьФорму(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьФайлИЗакрытьФорму(ДополнительныеПараметры) Экспорт
	
	Попытка
		УдалитьФайлы(ДополнительныеПараметры.ИмяФайлаОтчета);
	Исключение
		Закрыть();
		Возврат;
	КонецПопытки;
	
	Если ЭтотОбъект.Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СогласиеПользователяНаПродлениеОжиданияИлиЗакрытие(ПараметрыОбработчикаОжидания)
	
	ТекстВопроса = "Продолжить процесс получения машиночитаемой формы, сформированной веб-сервисом?";
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СогласиеПользователяНаПродлениеОжиданияИлиЗакрытиеЗавершение", ЭтотОбъект, ПараметрыОбработчикаОжидания);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура СогласиеПользователяНаПродлениеОжиданияИлиЗакрытиеЗавершение(Ответ, ПараметрыОбработчикаОжидания) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		
		Закрыть();
		
	Иначе
		
		ПолученОтветПользователя = Истина;
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьГотовностьОтчетаНаВебСервисе", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекущаяДатаНаСервере()
	
	Возврат ТекущаяДатаСеанса();
	
КонецФункции

#КонецОбласти
