
&НаКлиенте
Перем ТекущаяСтрокаРазделовОтчета;

&НаКлиенте
Перем ТекущиеДанныеОтчета;

&НаКлиенте
Перем СтраницаПечатнойФормыМодифицирована;

&НаКлиенте
Перем АдресаЛистовВоВременномХранилище;

#Область ПрограммныйИнтерфейс

&НаСервере
Функция ТабличныйДокументПечатногоБланкаРегламентированногоОтчета(ПечатныйБланкРегламентированногоОтчета) Экспорт
	
	ТаблДок = ПечатныйБланкРегламентированногоОтчета.ПечатныйБланк.Получить();
	
	// ПроцессыОбработкиДокументов
	//
	Если ОбщегоНазначения.ПодсистемаСуществует("ПроцессыОбработкиДокументов") Тогда
		
		МодульПроцессыОбработкиДокументов = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументов");
	
		МодульПроцессыОбработкиДокументов.ПриВыводеВыбранногоЛистаРегламентированногоОтчетаВТабличныйДокумент(
			ЭтотОбъект, ПечатныйБланкРегламентированногоОтчета.УникальныйИдентификатор, ТаблДок);
			
	КонецЕсли;
	//
	// ПроцессыОбработкиДокументов
	
	Возврат ТаблДок;
	
КонецФункции

&НаСервере
Функция ПечатныйБланкРегламентированногоОтчета(Идентификатор) Экспорт
		
	Запрос = Новый Запрос;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПечатныеБланкиРегламентированныхОтчетов.РегламентированныйОтчет КАК РегламентированныйОтчет,
	               |	ПечатныеБланкиРегламентированныхОтчетов.УникальныйИдентификатор КАК УникальныйИдентификатор,
	               |	ПечатныеБланкиРегламентированныхОтчетов.ПечатныйБланк КАК ПечатныйБланк
	               |ИЗ
	               |	РегистрСведений.ПечатныеБланкиРегламентированныхОтчетов КАК ПечатныеБланкиРегламентированныхОтчетов
	               |ГДЕ
	               |	ПечатныеБланкиРегламентированныхОтчетов.РегламентированныйОтчет = &РегламентированныйОтчет
	               |	И ПечатныеБланкиРегламентированныхОтчетов.УникальныйИдентификатор = &УникальныйИдентификатор";
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("РегламентированныйОтчет", СписокПечатаемыхЛистов[0].Значение);
	Запрос.УстановитьПараметр("УникальныйИдентификатор", Идентификатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
						
		Выборка = РезультатЗапроса.Выбрать();
				
		Если Выборка.Следующий() Тогда
			
			СтруктураВыборки = Новый Структура;
			
			СтруктураВыборки.Вставить("РегламентированныйОтчет", Выборка.РегламентированныйОтчет);
			СтруктураВыборки.Вставить("УникальныйИдентификатор", Выборка.УникальныйИдентификатор);
			СтруктураВыборки.Вставить("ПечатныйБланк", Выборка.ПечатныйБланк);
			
			Возврат СтруктураВыборки;
				
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СписокПечатаемыхЛистов  = Параметры.СписокПечатаемыхЛистов;
	ВидПечати               = Параметры.ВидПечати;
	
	Параметры.Свойство("ДополнительныеПараметрыПечати", ДополнительныеПараметрыПечати);
	
	Если СписокПечатаемыхЛистов.Количество() = 0 Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не выбраны листы для вывода на печать.'");
		Сообщение.Сообщить();
		
		Отказ = Истина;
		
	КонецЕсли;
		
	Инициализация();
	
	Если Параметры.Свойство("ЕстьВыходЗаГраницы") И Параметры.ЕстьВыходЗаГраницы = Истина Тогда 
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Некоторые поля адреса не уместились, рекомендуется воспользоваться печатной формой PDF417'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура Инициализация()
	
	ТабличныйДокумент.АвтоМасштаб        = Истина;
	ТабличныйДокумент.ЧерноБелаяПечать   = Истина;
	ТабличныйДокумент.Область().ЦветФона = Новый Цвет();
	ТабличныйДокумент.Область().Защита   = Константы.ЗапрещатьРедактированиеФормРеглОтчетности.Получить();
	ТабличныйДокумент.ЧерноБелыйПросмотр = Истина;
		
	ЗаголовокФормы = Параметры.ЗаголовокФормы;
					
	СохрКоличествоКопий = ХранилищеНастроекДанныхФорм.Загрузить(
		"Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ПечатьРегламентированныхОтчетов",
		"ПредпросмотрРегламентированнойОтчетностиКоличествоКопий");
	СохрРазобратьПоКопиям = ХранилищеНастроекДанныхФорм.Загрузить(
		"Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ПечатьРегламентированныхОтчетов",
		"ПредпросмотрРегламентированнойОтчетностиРазобратьПоКопиям");
	
	КоличествоКопий   = СохрКоличествоКопий;
	РазобратьПоКопиям = СохрРазобратьПоКопиям;
	
	Если РазобратьПоКопиям = Неопределено Тогда
		РазобратьПоКопиям = Истина;
	КонецЕсли;
		
	ДеревоЛистов = РеквизитФормыВЗначение("СписокЛистов");
	
	ДеревоЛистов.Строки.Очистить();
	
	Если СписокПечатаемыхЛистов.Количество() > 0
		И ВРег(СокрЛП(СписокПечатаемыхЛистов[0].Представление)) = ВРег(СокрЛП("СсылкаНаРегламентированныйОтчет")) Тогда
		
		УникальныеИдентификаторы = СписокПечатаемыхЛистов.ВыгрузитьЗначения();
		УникальныеИдентификаторы.Удалить(0);
		
		СсылкаНаРегламентированныйОтчет = СписокПечатаемыхЛистов.Получить(0);
		
		СписокПечатаемыхЛистов.Очистить();
		СписокПечатаемыхЛистов.Добавить(СсылкаНаРегламентированныйОтчет.Значение,
										СсылкаНаРегламентированныйОтчет.Представление);
				
		Запрос = Новый Запрос;
		
		ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	ПечатныеБланкиРегламентированныхОтчетов.УникальныйИдентификатор КАК УникальныйИдентификатор,
		               |	ПечатныеБланкиРегламентированныхОтчетов.НомерСтраницы КАК НомерСтраницы,
		               |	ПечатныеБланкиРегламентированныхОтчетов.ИмяСтраницы КАК ИмяСтраницы
		               |ИЗ
		               |	РегистрСведений.ПечатныеБланкиРегламентированныхОтчетов КАК ПечатныеБланкиРегламентированныхОтчетов
		               |ГДЕ
		               |	ПечатныеБланкиРегламентированныхОтчетов.РегламентированныйОтчет = &РегламентированныйОтчет
		               |	И ПечатныеБланкиРегламентированныхОтчетов.УникальныйИдентификатор В(&УникальныеИдентификаторы)
		               |УПОРЯДОЧИТЬ ПО
		               |	НомерСтраницы";
		
		Запрос.Текст = ТекстЗапроса;
										
		Запрос.УстановитьПараметр("РегламентированныйОтчет", СписокПечатаемыхЛистов[0].Значение);
		Запрос.УстановитьПараметр("УникальныеИдентификаторы", УникальныеИдентификаторы);
				
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				НовСтр = ДеревоЛистов.Строки.Добавить();
				
				НовСтр.Наименование = Выборка.ИмяСтраницы;
				НовСтр.Идентификатор = Выборка.УникальныйИдентификатор;
				НовСтр.АдресВоВременномХранилище = Выборка.УникальныйИдентификатор;
				НовСтр.КоличествоСтраниц = 1;
				НовСтр.Пометка = 1;
				
			КонецЦикла;
			
		КонецЕсли;   
		
	Иначе
		
		Для Каждого Элемент Из СписокПечатаемыхЛистов Цикл
			Элемент.Значение[0] = ПоместитьВоВременноеХранилище(ПолучитьИзВременногоХранилища(
				Элемент.Значение[0]), ЭтаФорма.УникальныйИдентификатор);
		КонецЦикла;
		
		Элемент = СписокПечатаемыхЛистов[0];
		
		ТаблДок = ПолучитьИзВременногоХранилища(Элемент.Значение[0]);
		
		ТабличныйДокумент.Вывести(ТаблДок);
		
		Для Каждого Сч Из СписокПечатаемыхЛистов Цикл
			
			НовСтр = ДеревоЛистов.Строки.Добавить();
			НовСтр.Пометка                   = 1;
			НовСтр.Наименование              = Сч.Представление;
			НовСтр.Идентификатор             = Сч.Значение[1];
			НовСтр.АдресВоВременномХранилище = Сч.Значение[0];
			
			ТекТабДок = ПолучитьИзВременногоХранилища(НовСтр.АдресВоВременномХранилище);
			КоличествоСтраниц = 0;
			НомПоследнейСтрокиСРазделителем = ТекТабДок.ВысотаТаблицы;
			
			Для НомСтроки = 1 По ТекТабДок.ВысотаТаблицы Цикл
				Если ТекТабДок.Область(НомСтроки, , НомСтроки, ).КонецСтраницы Тогда
					КоличествоСтраниц = КоличествоСтраниц + 1;
					НомПоследнейСтрокиСРазделителем = НомСтроки;
				КонецЕсли;
			КонецЦикла;
			
			Если НомПоследнейСтрокиСРазделителем < ТекТабДок.ВысотаТаблицы Тогда
				КоличествоСтраниц = КоличествоСтраниц + 1;
			КонецЕсли;
			
			НовСтр.КоличествоСтраниц = ?(КоличествоСтраниц > 0, КоличествоСтраниц, 1);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ДеревоЛистов, "СписокЛистов");
	
	ДобавитьШтампыЭЦПКСодержимомуТабличныхДокументовЕслиВозможно();
	
	Если ДеревоЛистов.Строки.Количество() <= 1 Тогда
		
		Элементы.СписокЛистов.Видимость = Ложь;
		Элементы.СписокЛистовУстановитьФлажки.Видимость = Ложь;
		Элементы.СписокЛистовСнятьФлажки.Видимость      = Ложь;
		
		ТабличныйДокумент.АвтоМасштаб        = Истина;
		ТабличныйДокумент.ЧерноБелаяПечать   = Истина;
		ТабличныйДокумент.Область().ЦветФона = Новый Цвет();
		ТабличныйДокумент.Область().Защита   = Константы.ЗапрещатьРедактированиеФормРеглОтчетности.Получить();
		ТабличныйДокумент.ЧерноБелыйПросмотр = Истина;
		
		Если СписокПечатаемыхЛистов.Количество() > 0
		   И ВРег(СокрЛП(СписокПечатаемыхЛистов[0].Представление)) = ВРег(СокрЛП("СсылкаНаРегламентированныйОтчет")) Тогда
			
			ВывестиВыбранныйЛистВТабличныйДокумент(ДеревоЛистов.Строки[0].Идентификатор);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// ПроцессыОбработкиДокументов
	//
	Если ОбщегоНазначения.ПодсистемаСуществует("ПроцессыОбработкиДокументов") Тогда
		
		МодульПроцессыОбработкиДокументов = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументов");
		
		МодульПроцессыОбработкиДокументов.ПриИнициализацииФормыПечатиРегламентированныхОтчетов(ЭтотОбъект, ДеревоЛистов);
		
	КонецЕсли;
	//
	// ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ПечататьСразу() Экспорт
	
	ПакетТаблДок = Новый ПакетОтображаемыхДокументов;
		
	Для Каждого Эл Из СписокПечатаемыхЛистов Цикл
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Идентификатор", Эл.Значение[1]);
		СтруктураПараметров.Вставить("АдресВоВременномХранилище", Эл.Значение[0]);
		
		ДобавитьТабличныеДокументыВПакетОтображаемыхДокументов(ПакетТаблДок, СтруктураПараметров);
		
	КонецЦикла;
	
	ПакетТаблДок.Напечатать(РежимИспользованияДиалогаПечати.НеИспользовать);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБланкPDF() Экспорт
	
	АдресаФайловВоВременномХранилище = Новый Массив();
	
	СформироватьPDFФайлыИзВсехЛистовНаСервере(АдресаФайловВоВременномХранилище);
	
	#Если ВебКлиент Тогда
	
	ПолучитьФайл(АдресаФайловВоВременномХранилище[0].АдресВоВременномХранилище,
				 АдресаФайловВоВременномХранилище[0].Наименование,
				 Истина);
	
	#Иначе
	
	ПутьДляВыгрузки = РегламентированнаяОтчетностьКлиент.ПолучитьПутьВыгрузки();
	
	Если ПутьДляВыгрузки = Неопределено
	 ИЛИ ПустаяСтрока(ПутьДляВыгрузки)
	 ИЛИ ПутьДляВыгрузки = Ложь Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПередаваемыйФайл = Новый ОписаниеПередаваемогоФайла;
	ПередаваемыйФайл.Имя      = ПутьДляВыгрузки + АдресаФайловВоВременномХранилище[0].Наименование;
	ПередаваемыйФайл.Хранение = АдресаФайловВоВременномХранилище[0].АдресВоВременномХранилище;
	
	МассивПередаваемыхФайлов = Новый Массив;
	МассивПередаваемыхФайлов.Добавить(ПередаваемыйФайл);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПолучитьИОткрытьБланкPDF", ЭтотОбъект,
		Новый Структура("ПолноеИмяФайла", ПередаваемыйФайл.Имя));
	
	НачатьПолучениеФайлов(ОповещениеОЗавершении, МассивПередаваемыхФайлов, , Ложь);
	
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьБланк() Экспорт
	
	АдресПакетаДокументов = СформироватьПакетИзВсехЛистовНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИОткрытьБланкPDF(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ПолученныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПолноеИмяФайла = ДополнительныеПараметры.ПолноеИмяФайла;
	
	КодВозврата = Неопределено;
	ЗапуститьПриложение("""" + ПолноеИмяФайла + """", , Истина, КодВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Заголовок = ЗаголовокФормы;
	
	ОтработатьИзмененияПараметровПечати();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьТекстПодсказки()
	
	КоличествоТаблДок = КоличествоВыбранныхТабличныхДокументов();
	
	КоличествоЛистовВсего = 0;
	
	Для Каждого Стр1 Из СписокЛистов.ПолучитьЭлементы() Цикл
		КоличествоЛистовВсего = КоличествоЛистовВсего + Стр1.КоличествоСтраниц;
	КонецЦикла;
	
	КоличествоЛистовТребуется = КоличествоТаблДок * КоличествоКопий;

	ТекстЛистовВсего = СтрЗаменить(ЧислоПрописью(КоличествоЛистовВсего, "НП=Истина, НД=Ложь", "листа, листов, листов, м, , , , ,0"),
	                               ЧислоПрописью(КоличествоЛистовВсего, "НП=Ложь, НД=Ложь", " , , , , , , , ,0"),
	                               "");

	ТекстЛистовТребуется = СтрЗаменить(ЧислоПрописью(КоличествоЛистовТребуется, "НП=Истина, НД=Ложь", "лист, листа, листов, м, , , , ,0"),
	                                   ЧислоПрописью(КоличествоЛистовТребуется, "НП=Ложь, НД=Ложь", " , , , , , , , ,0"),
	                                   "");

	ТекстСтатистики = НСтр("ru='Всего: '") + КоличествоЛистовТребуется + " " + ТекстЛистовТребуется + НСтр("ru=' бумаги'");
	
КонецПроцедуры

&НаКлиенте
Функция КоличествоВыбранныхТабличныхДокументов()
	
	Кол = 0;
	
	Для Каждого Стр1 Из СписокЛистов.ПолучитьЭлементы() Цикл
		
		Кол = Кол + ?(Стр1.Пометка, Стр1.КоличествоСтраниц, 0);
		
	КонецЦикла;
		
	Возврат Кол;
	
КонецФункции

&НаКлиенте
Процедура ОтработатьИзмененияПараметровПечати()
	
	Если КоличествоКопий = 0 ИЛИ КоличествоКопий = Неопределено Тогда
		КоличествоКопий = 1;
	КонецЕсли;
	
	Если КоличествоКопий <> 1 Тогда
		Элементы.РазобратьПоКопиям.Доступность = Истина;
	Иначе
		Элементы.РазобратьПоКопиям.Доступность = Ложь;
	КонецЕсли;
	
	СформироватьТекстПодсказки();
	
	СохранитьЗначениеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЛистовПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущаяСтрока = ТекущаяСтрокаРазделовОтчета Тогда
		Возврат;
	КонецЕсли;
	
	СохранениеМодификацийВПечатнойФорме();
	
	ТекущаяСтрокаРазделовОтчета = Элемент.ТекущаяСтрока;
	ТекущиеДанныеОтчета = Элемент.ТекущиеДанные;
	
	Если СписокПечатаемыхЛистов.Количество() > 0
	   И ВРег(СокрЛП(СписокПечатаемыхЛистов[0].Представление)) = ВРег(СокрЛП("СсылкаНаРегламентированныйОтчет")) Тогда
	   
    	ВывестиВыбранныйЛистВТабличныйДокумент(Элемент.ТекущиеДанные.Идентификатор);
	   
	Иначе   
	
		ВывестиВыбранныйЛистВТабличныйДокумент(Элемент.ТекущиеДанные.АдресВоВременномХранилище);
		
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура КоличествоКопийПриИзменении(Элемент)
	
	ОтработатьИзмененияПараметровПечати();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИлиСнятьФлажки(Пометка)
	
	Для Каждого Эл Из СписокЛистов.ПолучитьЭлементы() Цикл
		
		Эл.Пометка = Пометка;
		
	КонецЦикла;
	
	СформироватьТекстПодсказки();	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	УстановитьИлиСнятьФлажки(1);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	УстановитьИлиСнятьФлажки(0);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьЗначениеНаСервере()
		
	ХранилищеНастроекДанныхФорм.Сохранить("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ПечатьРегламентированныхОтчетов", "ПредпросмотрРегламентированнойОтчетностиКоличествоКопий",     КоличествоКопий);
	ХранилищеНастроекДанныхФорм.Сохранить("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ПечатьРегламентированныхОтчетов", "ПредпросмотрРегламентированнойОтчетностиРазобратьПоКопиям",   РазобратьПоКопиям);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЛистовПометкаПриИзменении(Элемент)
	
	Если Элементы.СписокЛистов.ТекущиеДанные.Пометка = 2 Тогда
		Элементы.СписокЛистов.ТекущиеДанные.Пометка = 0;
	КонецЕсли;
		
	Для Каждого Стр Из Элементы.СписокЛистов.ТекущиеДанные.ПолучитьЭлементы() Цикл
		Стр.Пометка = Элементы.СписокЛистов.ТекущиеДанные.Пометка;
	КонецЦикла;
	
	СформироватьТекстПодсказки();
	
КонецПроцедуры

&НаКлиенте
Процедура Печатать(Команда)
	
	СохранениеМодификацийВПечатнойФорме();
	
	ПакетТаблДок = Новый ПакетОтображаемыхДокументов;
	
	Если РазобратьПоКопиям Тогда
		
		Для Сч = 1 По КоличествоКопий Цикл
			
			Для Каждого Стр1 Из СписокЛистов.ПолучитьЭлементы() Цикл
				
				Если НЕ Стр1.Пометка Тогда
					Продолжить;
				КонецЕсли;
				
				СтруктураПараметров = Новый Структура;
				СтруктураПараметров.Вставить("Идентификатор", Стр1.Идентификатор);
				СтруктураПараметров.Вставить("АдресВоВременномХранилище", Стр1.АдресВоВременномХранилище);
				
				ДобавитьТабличныеДокументыВПакетОтображаемыхДокументов(ПакетТаблДок, СтруктураПараметров);
				
			КонецЦикла;
			
		КонецЦикла;
		
	Иначе
						
		Для Каждого Стр1 Из СписокЛистов.ПолучитьЭлементы() Цикл
				
			Если НЕ Стр1.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Для Сч = 1 По КоличествоКопий Цикл
				
				СтруктураПараметров = Новый Структура;
				СтруктураПараметров.Вставить("Идентификатор", Стр1.Идентификатор);
				СтруктураПараметров.Вставить("АдресВоВременномХранилище", Стр1.АдресВоВременномХранилище);
				
				ДобавитьТабличныеДокументыВПакетОтображаемыхДокументов(ПакетТаблДок, СтруктураПараметров);
				
			КонецЦикла;
				
		КонецЦикла;
		
	КонецЕсли;
	
	Если ПакетТаблДок.Состав.Количество() > 0 Тогда
		
		ПакетТаблДок.Напечатать(РежимИспользованияДиалогаПечати.НеИспользовать);
						
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВФорматеPDFДокумента(Команда)
	
	#Если ВебКлиент Тогда
		
		ВыгрузитьНаДискВВебКлиенте(ТипФайлаТабличногоДокумента.PDF);
		
	#Иначе
		
		ВыгрузитьНаДиск(ТипФайлаТабличногоДокумента.PDF);
		
	#КонецЕсли	
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВФорматеMicrosoftExcel(Команда)
	
	#Если ВебКлиент Тогда
		
		ВыгрузитьНаДискВВебКлиенте(ТипФайлаТабличногоДокумента.XLS);
		
	#Иначе
		
		ВыгрузитьНаДиск(ТипФайлаТабличногоДокумента.XLS);
		
	#КонецЕсли	
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВВидеТабличныхДокументов(Команда)
	
	#Если ВебКлиент Тогда
		
		ВыгрузитьНаДискВВебКлиенте(ТипФайлаТабличногоДокумента.MXL);
		
	#Иначе
		
		ВыгрузитьНаДиск(ТипФайлаТабличногоДокумента.MXL);
		
	#КонецЕсли	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьНаДиск(ВФормате)
	
	СохранениеМодификацийВПечатнойФорме();
	
	СоответствиеФорматаРасширению = Новый Соответствие;
	СоответствиеФорматаРасширению.Вставить(ТипФайлаТабличногоДокумента.MXL, "mxl");
	СоответствиеФорматаРасширению.Вставить(ТипФайлаТабличногоДокумента.XLS, "xls");
	СоответствиеФорматаРасширению.Вставить(ТипФайлаТабличногоДокумента.PDF, "pdf");
	
	Если КоличествоВыбранныхТабличныхДокументов() = 0 Тогда
		
		ПоказатьПредупреждение(, НСтр("ru='Выберите листы в дереве печатаемых листов.'"));
		
		Возврат;
		
	КонецЕсли;
	
	ПутьДляВыгрузки = РегламентированнаяОтчетностьКлиент.ПолучитьПутьВыгрузки();
	
	Если ПутьДляВыгрузки = Неопределено
	 ИЛИ ПустаяСтрока(ПутьДляВыгрузки)
	 ИЛИ ПутьДляВыгрузки = Ложь Тогда
	 
		Возврат;
		
	КонецЕсли;
	
	ПризнакОшибки = Ложь;
	
	Если ВФормате = ТипФайлаТабличногоДокумента.PDF Тогда
				
		ПакетТаблДок = Новый ПакетОтображаемыхДокументов;
		
		Для Каждого Лист Из СписокЛистов.ПолучитьЭлементы() Цикл
			
			Если Лист.Пометка Тогда
												
				СтруктураПараметров = Новый Структура;
				СтруктураПараметров.Вставить("Идентификатор", Лист.Идентификатор);
				СтруктураПараметров.Вставить("АдресВоВременномХранилище", Лист.АдресВоВременномХранилище);
				
				ДобавитьТабличныеДокументыВПакетОтображаемыхДокументов(ПакетТаблДок, СтруктураПараметров);
				
				Если СписокПечатаемыхЛистов.Количество() > 0
					И ВРег(СокрЛП(СписокПечатаемыхЛистов[0].Представление))
					= ВРег(СокрЛП("СсылкаНаРегламентированныйОтчет"))
					И ПакетТаблДок.Состав.Количество() > 0 Тогда
					
					ИмяФайла = ПутьДляВыгрузки + СформироватьИмяФайла(Лист.Наименование
						+ "." + СоответствиеФорматаРасширению[ВФормате]);
					
					Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Сохранение %1'"), ИмяФайла), , , БиблиотекаКартинок.Записать);
					
					Попытка
						
						ПакетТаблДок.ЗаписатьФайлДляПечати(ИмяФайла);
						
					Исключение
						
						ПризнакОшибки = Истина;
						
					КонецПопытки;
					
					ПакетТаблДок.Состав.Очистить();
					
				КонецЕсли;
												
			КонецЕсли;
			
		КонецЦикла;
				
		Если ПакетТаблДок.Состав.Количество() > 0 Тогда
									
			ИмяФайла = ПутьДляВыгрузки + СформироватьИмяФайла(ЗаголовокФормы
				+ "." + СоответствиеФорматаРасширению[ВФормате]);
			
			Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Сохранение %1'"), ИмяФайла), , , БиблиотекаКартинок.Записать);
			
			Попытка
				
				ПакетТаблДок.ЗаписатьФайлДляПечати(ИмяФайла);
				
			Исключение
				
				ПризнакОшибки = Истина;
				
			КонецПопытки;
			
			ПакетТаблДок.Состав.Очистить();
			
		КонецЕсли;
						
	Иначе
	
		Для Каждого Стр1 Из СписокЛистов.ПолучитьЭлементы() Цикл
			
			Если Стр1.Пометка Тогда
				
				Попытка
					
					Если СписокПечатаемыхЛистов.Количество() > 0
						И ВРег(СокрЛП(СписокПечатаемыхЛистов[0].Представление))
						= ВРег(СокрЛП("СсылкаНаРегламентированныйОтчет")) Тогда
						
						ТаблДок =
						ТабличныйДокументПечатногоБланкаРегламентированногоОтчета(
						ПечатныйБланкРегламентированногоОтчета(Стр1.Идентификатор));
						
					Иначе
						
						ТаблДок = ПолучитьИзВременногоХранилища(Стр1.АдресВоВременномХранилище);
						
					КонецЕсли;
					
					ТаблДок.Область().ЦветФона = Новый Цвет();
					
					ИмяФайла = ПутьДляВыгрузки + СформироватьИмяФайла(Стр1.Наименование + "." + СоответствиеФорматаРасширению[ВФормате]);
					
					Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Сохранение %1'"), ИмяФайла), , , БиблиотекаКартинок.Записать);
					
					ТаблДок.Записать(ИмяФайла, ВФормате);
					
				Исключение
					
					ПризнакОшибки = Истина;
					
				КонецПопытки;
				
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЕсли;
			
	Состояние();
				
	Если ПризнакОшибки Тогда
		
		ПоказатьПредупреждение(, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Не удалось сохранить печатный бланк в указанный каталог.%1Возможно, нет прав для записи в выбранный каталог, нет доступа к файлу, т. к. файл открыт в другой программе, недостаточно места на диске.'"), Символы.ПС), , НСтр("ru='Ошибка записи на диск.'"));
			
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьНаДискВВебКлиенте(ВФормате)
	
	СохранениеМодификацийВПечатнойФорме();
	
	СоответствиеФорматаРасширению = Новый Соответствие;
	СоответствиеФорматаРасширению.Вставить(ТипФайлаТабличногоДокумента.MXL, "mxl");
	СоответствиеФорматаРасширению.Вставить(ТипФайлаТабличногоДокумента.XLS, "xls");
	СоответствиеФорматаРасширению.Вставить(ТипФайлаТабличногоДокумента.PDF, "pdf");
	
	Если КоличествоВыбранныхТабличныхДокументов() = 0 Тогда
		
		ПоказатьПредупреждение(, НСтр("ru='Выберите листы в дереве печатаемых листов.'"));
		
		Возврат;
		
	КонецЕсли;
		
	АдресаЛистовВоВременномХранилище = Новый Массив();
	
	ПоместитьЛистыВоВременноеХранилищеНаСервере(ВФормате, АдресаЛистовВоВременномХранилище, СоответствиеФорматаРасширению);
	
	ПодключитьОбработчикОжидания("Подключаемый_ПолучитьФайл", 1);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьPDFФайлыИзВсехЛистовНаСервере(АдресаФайловВоВременномХранилище)
	
	ПакетТаблДок = СформироватьПакетИзВсехЛистовНаСервере();
	
	Если ТипЗнч(ПакетТаблДок) <> Тип("ПакетОтображаемыхДокументов")  Тогда
	
		Возврат;
	
	КонецЕсли;
	
	Если ПакетТаблДок.Состав.Количество() > 0 Тогда
		
		ИмяФайла = ПолучитьИмяВременногоФайла("pdf");
		
		ПакетТаблДок.ЗаписатьФайлДляПечати(ИмяФайла);
		
		АдресаФайловВоВременномХранилище.Добавить(Новый Структура("АдресВоВременномХранилище, Наименование",
			ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяФайла), ЭтаФорма.УникальныйИдентификатор), 
			СформироватьИмяФайла(ЗаголовокФормы + ".pdf")));
		
		УдалитьФайлы(ИмяФайла);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДобавитьШтампыЭЦПКСодержимомуТабличногоДокумента(ТабДокШтампыЭЦП)
	
	Если ТабличныйДокумент.Области.Найти("СтрокаШтампов") <> Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПоследняяСтрокаСтраницы = ТабличныйДокумент.ПолучитьОбласть(
	ТабличныйДокумент.ВысотаТаблицы, , ТабличныйДокумент.ВысотаТаблицы);
	ТабличныйДокумент.УдалитьОбласть(ТабличныйДокумент.Область(
	ТабличныйДокумент.ВысотаТаблицы, ,ТабличныйДокумент.ВысотаТаблицы), ТипСмещенияТабличногоДокумента.ПоВертикали);
	
	ПоследняяСтрокаСтраницы.Область(1, ,ПоследняяСтрокаСтраницы.ВысотаТаблицы).СоздатьФорматСтрок();
	ПоследняяСтрокаСтраницы.Область(1, ,ПоследняяСтрокаСтраницы.ВысотаТаблицы).КонецСтраницы = Ложь;
	ПоследняяСтрокаСтраницы.Область(1, ,ПоследняяСтрокаСтраницы.ВысотаТаблицы).РастягиватьПоГоризонтали = Ложь;
	
	Отступ = ПоследняяСтрокаСтраницы.ПолучитьОбласть(1, 1, ПоследняяСтрокаСтраницы.ВысотаТаблицы, 1);
	Отступ.Область().Очистить( , , Истина);
	
	ТабличныйДокумент.Вывести(ПоследняяСтрокаСтраницы.ПолучитьОбласть(1, 1,
	ПоследняяСтрокаСтраницы.ВысотаТаблицы, ПоследняяСтрокаСтраницы.ШиринаТаблицы));
	
	КоличествоСтраниц = 0;
	ПринтерДоступен = Истина;
	Попытка
		КоличествоСтраниц = ТабличныйДокумент.КоличествоСтраниц();
	Исключение
		ПринтерДоступен = Ложь;
	КонецПопытки;
	
	Разделитель = Новый ТабличныйДокумент;
	
	СерыйЦвет = Новый Цвет(192, 192, 192);
	Граница  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
	
	СекцияОгр = Разделитель.ПолучитьОбласть(1, 1, 2, 1);
	СекцияОгр.Область().ВысотаСтроки = 7.5;
	СекцияОгр.Область().ЦветРамки = СерыйЦвет;
	СекцияОгр.Область().РастягиватьПоГоризонтали = Ложь;
	СекцияОгр.Область().ШиринаКолонки = Отступ.Область(1, 1).ШиринаКолонки;
	
	СекцияКол = СекцияОгр.ПолучитьОбласть();
	СекцияКол.Область(1, 1).ГраницаСнизу = Граница;
	
	Разделитель.Вывести(СекцияОгр);
	Для НомКол = 2 По ПоследняяСтрокаСтраницы.ШиринаТаблицы Цикл
		Секция = ?(НомКол = ПоследняяСтрокаСтраницы.ШиринаТаблицы, СекцияОгр, СекцияКол);
		Секция.Область().ШиринаКолонки = ПоследняяСтрокаСтраницы.Область(1, НомКол).ШиринаКолонки;
		Разделитель.Присоединить(Секция);
	КонецЦикла;
	Разделитель.Область(1, ,Разделитель.ВысотаТаблицы).СоздатьФорматСтрок();
	
	СтрокаШтампов = ТабДокШтампыЭЦП.ПолучитьОбласть("СтрокаШтампов");
	СтрокаШтампов.Область(1, 1, СтрокаШтампов.ВысотаТаблицы, 1).ШиринаКолонки = Отступ.Область(1, 1).ШиринаКолонки;
	
	НачальнаяВысотаТаблицы = ТабличныйДокумент.ВысотаТаблицы;
	
	Если ПринтерДоступен Тогда
		ПроверяемыеТабДок = Новый Массив();
		ПроверяемыеТабДок.Добавить(Разделитель);
		ПроверяемыеТабДок.Добавить(СтрокаШтампов);
		ПроверяемыеТабДок.Добавить(Разделитель); // не выводится
		
		ИндДопЯчейки = 0;
		Пока НЕ ТабличныйДокумент.ПроверитьВывод(ПроверяемыеТабДок) Цикл
			ТабличныйДокумент.Присоединить(Отступ);
			ИндДопЯчейки = ИндДопЯчейки + 1;
			Если ИндДопЯчейки > 100 Тогда
				Прервать;
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(Разделитель);
	ТабличныйДокумент.Вывести(СтрокаШтампов);
	
	ТабличныйДокумент.Область(НачальнаяВысотаТаблицы, ,ТабличныйДокумент.ВысотаТаблицы).ВместеСоСледующим = Истина;
	ТабличныйДокумент.Область(НачальнаяВысотаТаблицы, ,ТабличныйДокумент.ВысотаТаблицы).НачалоСтраницы = Ложь;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ДобавитьТабличныеДокументыВПакетОтображаемыхДокументов(ПакетТаблДок, СтруктураПараметров)
	
	НаименованиеЛиста = "";
	СтруктураПараметров.Свойство("Наименование", НаименованиеЛиста);
	
	УИДВременногоХранилища = "";
	СтруктураПараметров.Свойство("УИДВременногоХранилища", УИДВременногоХранилища);
	Если НЕ ЗначениеЗаполнено(УИДВременногоХранилища) Тогда
		УИДВременногоХранилища = ЭтаФорма.УникальныйИдентификатор;
	КонецЕсли;
	
	Если СписокПечатаемыхЛистов.Количество() > 0
		И ВРег(СокрЛП(СписокПечатаемыхЛистов[0].Представление))
		= ВРег(СокрЛП("СсылкаНаРегламентированныйОтчет")) Тогда
		
		ПакетТаблДок.Состав.Добавить(
			ПоместитьВоВременноеХранилище(ТабличныйДокументПечатногоБланкаРегламентированногоОтчета(
			ПечатныйБланкРегламентированногоОтчета(СтруктураПараметров.Идентификатор)),
			УИДВременногоХранилища)).Наименование = СокрЛП(НаименованиеЛиста);
		
	Иначе
		
		АдресДокументаВСоставеПакета = СтруктураПараметров.АдресВоВременномХранилище;
		Если УИДВременногоХранилища <> ЭтаФорма.УникальныйИдентификатор Тогда
			АдресДокументаВСоставеПакета = ПоместитьВоВременноеХранилище(ПолучитьИзВременногоХранилища(
				СтруктураПараметров.АдресВоВременномХранилище), УИДВременногоХранилища);
		КонецЕсли;
		
		ПакетТаблДок.Состав.Добавить(АдресДокументаВСоставеПакета).Наименование = СокрЛП(НаименованиеЛиста);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьПакетИзВсехЛистовНаСервере()
	
	ПакетТаблДок = Новый ПакетОтображаемыхДокументов;
	ПакетТаблДок.ТочностьПечати = ТочностьПечати.Точная; 
	
	АдресПакетаДокументов = Неопределено;
	УИДВременногоХранилища = Неопределено;
	
	Если ТипЗнч(ДополнительныеПараметрыПечати) = Тип("Структура") Тогда
		
		ДополнительныеПараметрыПечати.Свойство("АдресПакетаДокументов", АдресПакетаДокументов);
		ДополнительныеПараметрыПечати.Свойство("УИДВременногоХранилища", УИДВременногоХранилища);
		
		ДобавитьШтампыЭЦПКСодержимомуТабличныхДокументовЕслиВозможно();
		
	КонецЕсли;
	
	Для Каждого Лист Из СписокЛистов.ПолучитьЭлементы() Цикл
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Наименование", Лист.Наименование);
		СтруктураПараметров.Вставить("Идентификатор", Лист.Идентификатор);
		СтруктураПараметров.Вставить("АдресВоВременномХранилище", Лист.АдресВоВременномХранилище);
		СтруктураПараметров.Вставить("УИДВременногоХранилища", УИДВременногоХранилища);
		
		ДобавитьТабличныеДокументыВПакетОтображаемыхДокументов(ПакетТаблДок, СтруктураПараметров);
		
	КонецЦикла;
	
	Если ЭтоАдресВременногоХранилища(АдресПакетаДокументов) Тогда
		
		ПакетТаблДокСтр = Новый Структура("Заголовок,КоличествоЭкземпляров,РазборПоКопиям,ТочностьПечати");
		ЗаполнитьЗначенияСвойств(ПакетТаблДокСтр, ПакетТаблДок);
		
		СоставПакета = Новый Массив;
		Для Каждого ЭлементПакета Из ПакетТаблДок.Состав Цикл
			СоставПакета.Добавить(Новый Структура("Данные,Наименование",
				Строка(ЭлементПакета.Данные), ЭлементПакета.Наименование));
		КонецЦикла;
		ПакетТаблДокСтр.Вставить("Состав", СоставПакета);
		
		Возврат ПоместитьВоВременноеХранилище(ПакетТаблДокСтр, АдресПакетаДокументов);
		
	КонецЕсли;
	
	Возврат ПакетТаблДок;
	
КонецФункции

&НаСервере
Процедура ДобавитьШтампыЭЦПКСодержимомуТабличныхДокументовЕслиВозможно()
	
	Если ТипЗнч(ДополнительныеПараметрыПечати) = Тип("Структура") Тогда
		
		ТабДокШтампыЭЦП = Неопределено;
		
		Если ДополнительныеПараметрыПечати.Свойство("ТабДокШтампыЭЦП", ТабДокШтампыЭЦП)
			И ТипЗнч(ТабДокШтампыЭЦП) = Тип("ТабличныйДокумент") Тогда
			
			ЭлементыСпискаЛистов = СписокЛистов.ПолучитьЭлементы();
			МаксИндексСпискаЛистов = ЭлементыСпискаЛистов.Количество() - 1;
			
			ПрерватьЦикл = Ложь;
			ПодписьНайдена = Ложь;
			Для Инд = 0 По МаксИндексСпискаЛистов Цикл
				
				Если СписокПечатаемыхЛистов.Количество() > 0
					И ВРег(СокрЛП(СписокПечатаемыхЛистов[0].Представление)) = ВРег(СокрЛП("СсылкаНаРегламентированныйОтчет")) Тогда
					ВывестиВыбранныйЛистВТабличныйДокумент(ЭлементыСпискаЛистов[Инд].Идентификатор);
				Иначе
					ВывестиВыбранныйЛистВТабличныйДокумент(ЭлементыСпискаЛистов[Инд].АдресВоВременномХранилище);
				КонецЕсли;
				
				Если Инд = 0 И ЭтоМЧБ(ТабличныйДокумент) Тогда      // титульный МЧБ
					ПрерватьЦикл = Истина;
				ИначеЕсли ЭтоЛистСПодписью(ТабличныйДокумент) Тогда // все остальные печатные формы
					ПодписьНайдена = Истина;
				ИначеЕсли ПодписьНайдена ИЛИ Инд <> МаксИндексСпискаЛистов Тогда
					Продолжить;
				КонецЕсли;
				
				Если ДобавитьШтампыЭЦПКСодержимомуТабличногоДокумента(ТабДокШтампыЭЦП) Тогда
					
					Если СписокПечатаемыхЛистов.Количество() > 0
						И ВРег(СокрЛП(СписокПечатаемыхЛистов[0].Представление)) = ВРег(СокрЛП("СсылкаНаРегламентированныйОтчет")) Тогда
						СохранитьМодифицированныйПечатныйБланк(ЭлементыСпискаЛистов[Инд].Идентификатор);
					Иначе
						НовТаблДок = Новый ТабличныйДокумент;  // обязательно нужно формировать новый табличный документ,
						НовТаблДок.Вывести(ТабличныйДокумент); // чтобы различались внутренние идентификаторы во временном хранилище.
						ЗаполнитьЗначенияСвойств(НовТаблДок, ТабличныйДокумент);
						
						ПоместитьВоВременноеХранилище(НовТаблДок, ЭлементыСпискаЛистов[Инд].АдресВоВременномХранилище);
					КонецЕсли;
					
				КонецЕсли;
				
				Если ПрерватьЦикл Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьЛистыВоВременноеХранилищеНаСервере(ВФормате,
													АдресаЛистовВоВременномХранилище,
													СоответствиеФорматаРасширению) Экспорт
			
	Если ВФормате = ТипФайлаТабличногоДокумента.PDF Тогда
		
		ПакетТаблДок = Новый ПакетОтображаемыхДокументов;
		
		Для Каждого Лист Из СписокЛистов.ПолучитьЭлементы() Цикл
			
			Если Лист.Пометка Тогда
				
				СтруктураПараметров = Новый Структура;
				СтруктураПараметров.Вставить("Идентификатор", Лист.Идентификатор);
				СтруктураПараметров.Вставить("АдресВоВременномХранилище", Лист.АдресВоВременномХранилище);
				
				ДобавитьТабличныеДокументыВПакетОтображаемыхДокументов(ПакетТаблДок, СтруктураПараметров);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ПакетТаблДок.Состав.Количество() > 0 Тогда
			
			ИмяФайла = ПолучитьИмяВременногоФайла(СоответствиеФорматаРасширению[ВФормате]);
			
			ПакетТаблДок.ЗаписатьФайлДляПечати(ИмяФайла);
			
			ФайлЛиста = Новый ДвоичныеДанные(ИмяФайла);
			
			АдресаЛистовВоВременномХранилище.Добавить(Новый Структура("АдресВоВременномХранилище, Наименование",
				ПоместитьВоВременноеХранилище(ФайлЛиста, ЭтаФорма.УникальныйИдентификатор), 
				СформироватьИмяФайла(ЗаголовокФормы + "." + СоответствиеФорматаРасширению[ВФормате])));
			
			УдалитьФайлы(ИмяФайла);
			
		КонецЕсли;
		
	Иначе
		
		Для Каждого Лист Из СписокЛистов.ПолучитьЭлементы() Цикл
			
			Если Лист.Пометка Тогда
				
				ИмяФайла = ПолучитьИмяВременногоФайла(СоответствиеФорматаРасширению[ВФормате]);
				
				Если СписокПечатаемыхЛистов.Количество() > 0
					И ВРег(СокрЛП(СписокПечатаемыхЛистов[0].Представление))
					= ВРег(СокрЛП("СсылкаНаРегламентированныйОтчет")) Тогда
					
					ТаблДок =
						ТабличныйДокументПечатногоБланкаРегламентированногоОтчета(
							ПечатныйБланкРегламентированногоОтчета(Лист.Идентификатор));
					
				Иначе
					
					ТаблДок = ПолучитьИзВременногоХранилища(Лист.АдресВоВременномХранилище);
					
				КонецЕсли;
				
				ТаблДок.Область().ЦветФона = Новый Цвет();
				
				ТаблДок.Записать(ИмяФайла, ВФормате);
				
				ФайлЛиста = Новый ДвоичныеДанные(ИмяФайла);
				
				АдресаЛистовВоВременномХранилище.Добавить(Новый Структура("АдресВоВременномХранилище, Наименование", 
					ПоместитьВоВременноеХранилище(ФайлЛиста, ЭтаФорма.УникальныйИдентификатор), 
					СформироватьИмяФайла(Лист.Наименование + "." + СоответствиеФорматаРасширению[ВФормате])));
				
				УдалитьФайлы(ИмяФайла);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьИмяФайла(ИсходноеИмяФайла)
	
	ИмяФайла = СтрЗаменить(ИсходноеИмяФайла, "/", "_");
	ИмяФайла = СтрЗаменить(ИмяФайла, "\", "_");
	ИмяФайла = СтрЗаменить(ИмяФайла, ":", "_");
	ИмяФайла = СтрЗаменить(ИмяФайла, "*", "_");
	ИмяФайла = СтрЗаменить(ИмяФайла, "?", "_");
	ИмяФайла = СтрЗаменить(ИмяФайла, """","_");
	ИмяФайла = СтрЗаменить(ИмяФайла, "<", "_");
	ИмяФайла = СтрЗаменить(ИмяФайла, ">", "_");
	
	ИмяФайла = СтрЗаменить(ИмяФайла, Символы.ПС, " ");
	
	Возврат СтрЗаменить(ИмяФайла, "|", "_");
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПолучитьФайл()
	
	Если АдресаЛистовВоВременномХранилище.Количество() = 0 Тогда
		
		ОтключитьОбработчикОжидания("Подключаемый_ПолучитьФайл");
		
		Возврат;
		
	КонецЕсли;
	
	ПолучитьФайл(АдресаЛистовВоВременномХранилище[0].АдресВоВременномХранилище, АдресаЛистовВоВременномХранилище[0].Наименование);
	
	АдресаЛистовВоВременномХранилище.Удалить(0);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЛистовПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЛистовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиВыбранныйЛистВТабличныйДокумент(АдресВоВременномХранилище)
	
	Если СписокПечатаемыхЛистов.Количество() > 0
	   И ВРег(СокрЛП(СписокПечатаемыхЛистов[0].Представление)) = ВРег(СокрЛП("СсылкаНаРегламентированныйОтчет")) Тогда
		
		ТаблДок =
			ТабличныйДокументПечатногоБланкаРегламентированногоОтчета(
				ПечатныйБланкРегламентированногоОтчета(АдресВоВременномХранилище));
		
	Иначе
		
		ТаблДок = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
		
	КонецЕсли;	
		
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(ТаблДок);
	
	ЗаполнитьЗначенияСвойств(ТабличныйДокумент, ТаблДок);
	
	ТабличныйДокумент.АвтоМасштаб        = Истина;
	ТабличныйДокумент.ЧерноБелаяПечать   = Истина;
	ТабличныйДокумент.Область().ЦветФона = Новый Цвет();
	ТабличныйДокумент.Область().Защита   = Константы.ЗапрещатьРедактированиеФормРеглОтчетности.Получить();
	ТабличныйДокумент.ЧерноБелыйПросмотр = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличныйДокументПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	СтраницаПечатнойФормыМодифицирована = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранениеМодификацийВПечатнойФорме()
	
	СтраницаПечатнойФормыМодифицирована = ?(СтраницаПечатнойФормыМодифицирована <> Неопределено,
											СтраницаПечатнойФормыМодифицирована, Ложь);
	
	Если СтраницаПечатнойФормыМодифицирована Тогда
		
		Если СписокПечатаемыхЛистов.Количество() > 0
			И ВРег(СокрЛП(СписокПечатаемыхЛистов[0].Представление)) = ВРег(СокрЛП("СсылкаНаРегламентированныйОтчет")) Тогда
			
			Если ТекущиеДанныеОтчета = Неопределено Тогда
				
				СохранитьМодифицированныйПечатныйБланк(СписокЛистов.ПолучитьЭлементы()[0].Идентификатор);
				
			Иначе
				
				СохранитьМодифицированныйПечатныйБланк(ТекущиеДанныеОтчета.Идентификатор);
				
			КонецЕсли;
			
			// ПроцессыОбработкиДокументов
			//
			Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ПроцессыОбработкиДокументов") Тогда
				
				МодульПроцессыОбработкиДокументовКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовКлиент");
				
				МодульПроцессыОбработкиДокументовКлиент.ПриСохраненииМодификацийВПечатнойФормеРегламентированногоОтчета(
					ЭтотОбъект, ТекущиеДанныеОтчета);
					
			КонецЕсли;
			//
			// ПроцессыОбработкиДокументов
	
		Иначе
			
			Если ТекущиеДанныеОтчета = Неопределено Тогда
				
				ПоместитьВоВременноеХранилище(ТабличныйДокумент, СписокЛистов.ПолучитьЭлементы()[0].АдресВоВременномХранилище);
				
			Иначе
				
				ПоместитьВоВременноеХранилище(ТабличныйДокумент, ТекущиеДанныеОтчета.АдресВоВременномХранилище);
				
			КонецЕсли;
			
		КонецЕсли;
		
		СтраницаПечатнойФормыМодифицирована = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьМодифицированныйПечатныйБланк(Идентификатор)
	
	ПечатныйБланкРегламентированногоОтчета = ПечатныйБланкРегламентированногоОтчета(Идентификатор);
	
	ЗаписьРегистраСведений = РегистрыСведений.ПечатныеБланкиРегламентированныхОтчетов.СоздатьМенеджерЗаписи();
	
	ЗаписьРегистраСведений.РегламентированныйОтчет = ПечатныйБланкРегламентированногоОтчета.РегламентированныйОтчет;
	ЗаписьРегистраСведений.УникальныйИдентификатор = ПечатныйБланкРегламентированногоОтчета.УникальныйИдентификатор;
	
	ЗаписьРегистраСведений.ПечатныйБланк = Новый ХранилищеЗначения(ТабличныйДокумент);
	
	ЗаписьРегистраСведений.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура РазобратьПоКопиямПриИзменении(Элемент)
	
	СохранитьЗначениеНаСервере();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоМЧБ(ТаблДок, ПараметрыБланка = Неопределено)
	
	ТаблДокумент = ТаблДок.ПолучитьОбласть();
	
	Если ТаблДокумент.Рисунки.Количество() >= 3 Тогда
		
		КоличествоКартинок = 0;
		
		ОграничениеСлева = 999;
		ОграничениеСправа = 0;
		ОграничениеСверху = 999;
		ОграничениеСнизу = 0;
		
		ОграничениеСправаОбластьПечати = 0;
		ОбластьПервойСтроки = ТаблДокумент.ПолучитьОбласть(1, , 1);
		РисунокОбластиПечати = ТаблДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Прямоугольник);
		РисунокОбластиПечати.Расположить(ТаблДокумент.Область(1, ОбластьПервойСтроки.ШиринаТаблицы, 1, ОбластьПервойСтроки.ШиринаТаблицы));
		
		Для Каждого РисунокОбласти Из ТаблДокумент.Рисунки Цикл
			Если РисунокОбласти.ТипРисунка = ТипРисункаТабличногоДокумента.Картинка ИЛИ РисунокОбласти.Имя = РисунокОбластиПечати.Имя Тогда
				
				
				ШиринаРисункаОбласти = РисунокОбласти.Ширина;
				Если ШиринаРисункаОбласти < 0 Тогда
					// Обходим ошибку платформы (ширина рисунка при созданном формате строки не соответствует реальной)
					ТестовыйРисунок = ТаблДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Прямоугольник);
					
					НомерСтрокиРисункаВерх = ТаблДокумент.ВысотаТаблицы;
					НомерСтрокиРисункаНиз = ТаблДокумент.ВысотаТаблицы + 1;
					ВерхНайден = Ложь;
					Для НомСтр = 1 По ТаблДокумент.ВысотаТаблицы + 1 Цикл
						ТестовыйРисунок.Расположить(ТаблДокумент.Область(НомСтр, 1, НомСтр, 1));
						Если НЕ ВерхНайден И ТестовыйРисунок.Верх > РисунокОбласти.Верх Тогда
							НомерСтрокиРисункаВерх = НомСтр - 1;
							ВерхНайден = Истина;
						КонецЕсли;
						Если ВерхНайден И ТестовыйРисунок.Верх > РисунокОбласти.Верх + РисунокОбласти.Высота Тогда
							НомерСтрокиРисункаНиз = НомСтр - 1;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					СмещениеРисункаПраво = 0;
					НомерСтолбцаРисункаПраво = ТаблДокумент.ШиринаТаблицы;
					Для НомСтолбца = 1 По ТаблДокумент.ШиринаТаблицы + 1 Цикл
						ТестовыйРисунок.Расположить(ТаблДокумент.Область(НомерСтрокиРисункаНиз, НомСтолбца, НомерСтрокиРисункаНиз, НомСтолбца));
						Если ТестовыйРисунок.Лево > РисунокОбласти.Лево + РисунокОбласти.Ширина Тогда
							НомерСтолбцаРисункаПраво = НомСтолбца - 1;
							Прервать;
						КонецЕсли;
						СмещениеРисункаПраво = РисунокОбласти.Лево + РисунокОбласти.Ширина - ТестовыйРисунок.Лево;
					КонецЦикла;
					ТестовыйРисунок.Расположить(ТаблДокумент.Область(НомерСтрокиРисункаВерх, НомерСтолбцаРисункаПраво, НомерСтрокиРисункаВерх, НомерСтолбцаРисункаПраво));
					ШиринаРисункаОбласти = ТестовыйРисунок.Лево - РисунокОбласти.Лево + СмещениеРисункаПраво;
					
					ТаблДокумент.Рисунки.Удалить(ТестовыйРисунок);
				КонецЕсли;
				
				Если РисунокОбласти.Имя = РисунокОбластиПечати.Имя Тогда
					ОграничениеСправаОбластьПечати = РисунокОбласти.Лево + ШиринаРисункаОбласти;
				Иначе
					Если РисунокОбласти.Высота < 15 Тогда
						ОграничениеСверху  = Мин(ОграничениеСверху, РисунокОбласти.Верх);
						ОграничениеСнизу   = Макс(ОграничениеСнизу, РисунокОбласти.Верх + РисунокОбласти.Высота);
						ОграничениеСлева   = Мин(ОграничениеСлева, РисунокОбласти.Лево);
						ОграничениеСправа  = Макс(ОграничениеСправа, РисунокОбласти.Лево + ШиринаРисункаОбласти);
					КонецЕсли; 
					КоличествоКартинок = КоличествоКартинок + 1;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		ТаблДокумент.Рисунки.Удалить(РисунокОбластиПечати);
		
		Если КоличествоКартинок >= 3
		   И ОграничениеСправа - ОграничениеСлева > 40
		   И ОграничениеСнизу - ОграничениеСверху > 70 Тогда
			
			ПараметрыБланка = Новый Структура;
			ПараметрыБланка.Вставить("ОграничениеСправаОбластьПечати", ОграничениеСправаОбластьПечати);
			
			ПараметрыБланка.Вставить("ОграничениеСлева",    ОграничениеСлева);
			ПараметрыБланка.Вставить("ОграничениеСправа",   ОграничениеСправа);
			ПараметрыБланка.Вставить("ОграничениеСверху",   ОграничениеСверху);
			ПараметрыБланка.Вставить("ОграничениеСнизу",    ОграничениеСнизу);
			ПараметрыБланка.Вставить("КоличествоКартинок",  КоличествоКартинок);
			
			Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоЛистСПодписью(ТаблДок)
	
	ТекстДляПоиска = "подпись)";
	
	Возврат ТаблДок.НайтиТекст(ТекстДляПоиска,,,,,, Истина) <> Неопределено;
	
КонецФункции

#КонецОбласти