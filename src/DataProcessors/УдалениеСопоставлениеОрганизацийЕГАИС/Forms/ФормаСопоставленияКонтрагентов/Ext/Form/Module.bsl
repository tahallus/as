
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ОрганизацияЕГАИС, Контрагент");
	
	ФактическийАдресКонтрагента = ПечатьДокументовУНФ.СведенияОЮрФизЛице(Контрагент, ТекущаяДатаСеанса()).ФактическийАдрес;
	
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьСоответствие(Команда)
	
	Если ЕстьПредупреждения() Тогда
		ОповещениеПриЗавершении = Новый ОписаниеОповещения("ЗаписатьСоответствие_Завершение", ЭтотОбъект);
		ПоказатьВопрос(ОповещениеПриЗавершении, НСтр("ru = 'Между связываемыми элементами есть расхождения. Продолжить?'"), РежимДиалогаВопрос.ОКОтмена,, КодВозвратаДиалога.Отмена);
	Иначе
		ЗаписатьСоответствие_Завершение(КодВозвратаДиалога.ОК, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьСоответствие_Завершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьСоответствиеНаСервере();
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСоответствиеНаСервере()
	
	Спр = ОрганизацияЕГАИС.ПолучитьОбъект();
	Спр.СоответствуетОрганизации = ТипЗнч(Контрагент) = Тип("СправочникСсылка.Организации");
	Спр.Контрагент = Контрагент;
	Спр.ТорговыйОбъект = Неопределено;
	Спр.Сопоставлено = Истина;
	Спр.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	Элементы.ПредупреждениеИНН.Видимость = НЕ ПустаяСтрока(Контрагент.ИНН) И НЕ ПустаяСтрока(ОрганизацияЕГАИС.ИНН) И СокрЛП(Контрагент.ИНН) <> СокрЛП(ОрганизацияЕГАИС.ИНН);
	Элементы.ПредупреждениеКПП.Видимость = НЕ ПустаяСтрока(Контрагент.КПП) И НЕ ПустаяСтрока(ОрганизацияЕГАИС.КПП) И СокрЛП(Контрагент.КПП) <> СокрЛП(ОрганизацияЕГАИС.КПП);
	
КонецПроцедуры

&НаКлиенте
Функция ЕстьПредупреждения()
	
	Возврат Элементы.ПредупреждениеИНН.Видимость ИЛИ Элементы.ПредупреждениеКПП.Видимость;
	
КонецФункции

#КонецОбласти