#Область ОписаниеПеременных

&НаКлиенте
Перем СоответствиеШтрихкодовСтрокДерева Экспорт;

&НаКлиенте
Перем Ссылка Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ИнициализироватьДанныеФормы();
	
	СформироватьПараметрыСверкиКодовМаркировки();
	
	ОбработатьИПроверитьПереданныеПараметры(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// варианты открытия:
	//1. акт поступления - основание: результаты проверки по регистру, дополнить упаковками эдо расхождениями
	//2. акт реализации  - основание: дерево по кодам из реализации, дополнить деревом ИСМП по ТОРГ-2
	//3. корр.поступл    - основание: дерево ИСМП по кодам УПДи/УКД, дополнить деревом ИСМП из акта
	
	ВосстановитьСохраненныеРезультатыПроверки();
	Если ВосстановленыСохраненныеРезультатыПроверки Тогда
		РассчитатьИтогиУстановитьВидимость();
	КонецЕсли;
	
	Если ПараметрыСверкиКодовМаркировки.ПроверятьПодключениеИСМП Тогда
		ТребуетсяОбновлениеКлючаСессии = ИнтерфейсАвторизацииИСМПВызовСервера.ТребуетсяОбновлениеКлючаСессии(
			ИнтерфейсИСМПКлиентСервер.ПараметрыЗапросаКлючаСессии(Организация));
	КонецЕсли;
	
	ШтрихкодированиеИСКлиентСервер.ОтобразитьСохраненныйВыборПоМаркируемойПродукции(ЭтотОбъект);
	Если Не СохраненВыборПоМаркируемойПродукции Тогда
		ИнициализироватьДанныеВыбораПоМаркируемойПродукции(ДанныеВыбораПоМаркируемойПродукции);
		СверкаКодовМаркировкиИСМПКлиентСервер.ОтобразитьФильтрПоМаркируемойПродукции(ЭтотОбъект, ДанныеВыбораПоМаркируемойПродукции);
	КонецЕсли;
	
	СобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВосстановленыСохраненныеРезультатыПроверки Тогда
		СоответствиеШтрихкодовСтрокДерева = СоответствиеШтрихкодовСтрокДерева(АдресСоответствиеШтрихкодыИдентификаторыСтрок);
		ТребуетсяОбновлениеКлючаСессии = Ложь;
	ИначеЕсли ПараметрыСверкиКодовМаркировки.ПроверятьПодключениеИСМП Тогда
		Если ТребуетсяОбновлениеКлючаСессии Тогда
			ПодключитьОбработчикОжидания("ОбработкаОжиданияЗапросаКлючаСессии", 0.5, Истина);
		Иначе
			ЗагрузитьДанныеДокумента();
		КонецЕсли;
	Иначе
		ПоказатьПредупреждение(, "Работа с формой сверки невозможна: не найдены сохраненные результаты сверки по документу");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредставлениеСохраненногоВыбораОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИнтеграцияИСКлиент.ПредставлениеСохраненногоВыбораОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки);
	
	ИнициализироватьДанныеВыбораПоМаркируемойПродукции(ДанныеВыбораПоМаркируемойПродукции);
	
	ОбновитьДанныеВыбораПоМаркируемойПродукции();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУстановитьОтборОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СохранитьДанныеВыбораПоМаркируемойПродукции" Тогда
		
		СохраненВыборПоМаркируемойПродукции = Не СохраненВыборПоМаркируемойПродукции;
		
		СкрытьСтрокиДереваСОтборомПоНоменклатуре(ЭтотОбъект);
		
		ШтрихкодированиеИСКлиентСервер.ОтобразитьСохраненныйВыборПоМаркируемойПродукции(ЭтотОбъект);
		СверкаКодовМаркировкиИСМПКлиентСервер.ОтобразитьФильтрПоМаркируемойПродукции(ЭтотОбъект);
		
		ОбновитьОтображениеДанных();
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьНоменклатуру" Тогда
		
		ПоказатьЗначение(, ДанныеВыбораПоМаркируемойПродукции.Номенклатура);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьХарактеристику" Тогда
		
		ПоказатьЗначение(, ДанныеВыбораПоМаркируемойПродукции.Характеристика);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

#Область ОбработчикиСобытийЭлементовДерева

&НаКлиенте
Процедура ДеревоРезультатовЭДОПриАктивизацииСтроки(Элемент)
	
	Если СохраненВыборПоМаркируемойПродукции Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьПредставлениеФильтраПоМаркируемойПродукции(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоМаркированнойПродукцииПриАктивизацииСтроки(Элемент)
	
	Если СохраненВыборПоМаркируемойПродукции Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьПредставлениеФильтраПоМаркируемойПродукции(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоРезультатовЭДОВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле = Элементы.ДеревоРезультатовЭДОПредставление
		И ЗначениеЗаполнено(ТекущиеДанные.ЗначениеШтрихкода) Тогда
		СтандартнаяОбработка = Ложь;
		
		НайденнаяСтрокаДерева = Неопределено;
		
		Если ОтображатьРезультатыФизПроверки = "ЭДОРасхождения" Тогда
			ИдентификаторСтроки = СоответствиеШтрихкодовСтрокДерева.Получить(ТекущиеДанные.ЗначениеШтрихкода + "ЭДО");
			Если ИдентификаторСтроки <> Неопределено Тогда
				НайденнаяСтрокаДерева = НайтиПоИдентификатору(ДеревоМаркированнойПродукции, ИдентификаторСтроки);
			КонецЕсли;
		КонецЕсли;
		
		Если НайденнаяСтрокаДерева = Неопределено Тогда
			
			ИдентификаторСтроки = СоответствиеШтрихкодовСтрокДерева.Получить(ТекущиеДанные.ЗначениеШтрихкода);
			Если ИдентификаторСтроки <> Неопределено Тогда
				НайденнаяСтрокаДерева = НайтиПоИдентификатору(ДеревоМаркированнойПродукции, ИдентификаторСтроки);
			КонецЕсли;
		КонецЕсли;
		
		Если НайденнаяСтрокаДерева = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Элементы.ДеревоМаркированнойПродукции.ТекущаяСтрока = НайденнаяСтрокаДерева.ПолучитьИдентификатор();
		Элементы.СтраницыПродукция.ТекущаяСтраница = Элементы.СтраницаРезультатыСверки;
		
		ОбновитьТекущуюСтрокуДереваПоОтборам(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОтображатьРезультатыФизПроверкиПриИзменении(Элемент)
	
	ОбновитьТекущуюСтрокуДереваПоОтборам(ЭтотОбъект);
	
	СверкаКодовМаркировкиИСМПКлиентСервер.ПересчитатьИтогиПоПроверкеСодержимогоУпаковок(ЭтотОбъект.ДеревоМаркированнойПродукции,
		ЭтотОбъект.ОтображатьРезультатыФизПроверки,
		ЭтотОбъект.ЭтоПродукцияМОТП);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьДействиеПризнать(Команда)
	
	УстановитьДействиеДляВыделенныхСтрок(
		Элементы.ДеревоМаркированнойПродукции.ВыделенныеСтроки,
		ИнтеграцияИСМПКлиентСервер.ВариантДействийПоРасхождениямКодовМаркировкиИСМППризнать());
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДействиеНеПризнать(Команда)
	
	УстановитьДействиеДляВыделенныхСтрок(
		Элементы.ДеревоМаркированнойПродукции.ВыделенныеСтроки,
		ИнтеграцияИСМПКлиентСервер.ВариантДействийПоРасхождениямКодовМаркировкиИСМПНеПризнать());
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаЗавершена(Команда)
	
	Если ДоступноСогласованиеРасхождений И Не ТолькоПросмотр Тогда
		ОценкаПроизводительностиКлиент.ЗамерВремени("Обработка.РезультатыСверкиКодовМаркировкиТОРГ2.Форма.РезультатыСверки.ЗавершитьПроверку");
		
		ПараметрыОкончанияПроверки = Новый Структура;
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Сохранение результатов сверки'");
		
		ДлительнаяОперация = НачатьЗавершениеПроверки(ПараметрыОкончанияПроверки);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершениеПроверкиОкончание", ЭтотОбъект);
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
#Область ОтборыДеревоМаркируемойПродукции
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукцииПредставлениеПроверкиПодчиненных.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.СтатусПроверки");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыПроверкиНаличияПродукцииИС.Отложена;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукции.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтображатьРезультатыФизПроверки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "Все";
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.НеСоответствуетОтборуВсе");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукции.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтображатьРезультатыФизПроверки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "ФактическиеРасхождения";
	
	ГруппаОтбораИЛИ = ГруппаОтбора.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбораИЛИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИЛИ;
	
	ОтборЭлемента = ГруппаОтбораИЛИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.НеСоответствуетОтбору");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаОтбораИЛИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.НеСоответствуетОтборуВсе");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукции.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтображатьРезультатыФизПроверки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "ЭДОРасхождения";
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.НеСоответствуетОтборуЭДО");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукции.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СохраненВыборПоМаркируемойПродукции");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.НеСоответствуетОтборуНоменклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
#КонецОбласти
	
#Область ДеревоУпаковокЭДО
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоРезультатовЭДОЗначениеШтрихкода.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтображатьРезультатыЭДО");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "ТолькоРасхождения";
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоРезультатовЭДО.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтображатьРезультатыЭДО");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "ТолькоРасхождения";
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоРезультатовЭДО.НеСоответствуетОтбору");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
#КонецОбласти
	
#Область ПредставлениеСтатусов
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукцииСтатусПроверки.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.ТипУпаковки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПродукцияБезУпаковки;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукцииСтатусПроверки.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.ТипУпаковки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПачкиБезБлока;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукцииСтатусПроверки.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.ТипУпаковки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.БлокиБезКоробки;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукцииСтатусПроверки.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.СтатусПроверки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыПроверкиНаличияПродукцииИС.НеПроверялась;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДоступноСогласованиеРасхождений");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПроверкаЭлектронногоДокумента");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Неизвестен'"));
	
#Область ПредставлениеСтатусовСверкиКорректировочногоДокумента
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукцииСтатусПроверки.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
		
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СверкаПоДаннымКорректировки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.СтатусПроверки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыПроверкиНаличияПродукцииИС.НеЧислилась;
		
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПроблемаГосИС);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Излишек'"));

	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукцииСтатусПроверки.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СверкаПоДаннымКорректировки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.СтатусПроверки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыПроверкиНаличияПродукцииИС.ВНаличии;
		
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Соответствует'"));
	
#КонецОбласти
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукцииСтатусПроверки.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.НедопустимыйКодМаркировки");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.ПредставлениеПроверкиКодаМаркировки"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПроблемаГосИС);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
#КонецОбласти

#Область ПредставлениеСтатусовЭДО
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоРезультатовЭДОСтатусПроверки.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ДеревоРезультатовЭДО.НедопустимыйКодМаркировки");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПроблемаГосИС);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Брак'"));
	
#КонецОбласти

#Область ДоступноСогласованиеРасхождений
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукцииДействиеПоРасхождениям.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.СтатусПроверки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыПроверкиНаличияПродукцииИС.ВНаличии;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.СтатусПроверки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыПроверкиНаличияПродукцииИС.НеПроверялась;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукцииСтатусПроверки.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДоступноСогласованиеРасхождений");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.СтатусПроверки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыПроверкиНаличияПродукцииИС.ВНаличии;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Принята'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукцииСтатусПроверки.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента =ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДоступноСогласованиеРасхождений");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.СтатусПроверки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыПроверкиНаличияПродукцииИС.НеЧислилась;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Излишек'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукцииСтатусПроверки.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДоступноСогласованиеРасхождений");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.СтатусПроверки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыПроверкиНаличияПродукцииИС.Отсутствует;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.НедопустимыйКодМаркировки");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Недостача'"));
	
#КонецОбласти

#Область СодержимоеУпаковки

	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоМаркированнойПродукцииПредставлениеСодержимоеУпаковки.Имя);

	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.КоличествоПодчиненныхУпаковок");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.КоличествоПодчиненныхБлоков");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.КоличествоПодчиненнойПродукции");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ДеревоМаркированнойПродукции.ТипУпаковки");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыУпаковок.МаркированныйТовар;
		
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	
#КонецОбласти


КонецПроцедуры

#Область НачальноеЗаполнение

&НаСервере
Процедура ИнициализироватьДанныеФормы(РежимОчистки = Ложь)
	
	ИндексКартинкиЭДОПоУмолчанию = 7;
	
	ЦветГиперссылки           = ЦветаСтиля.ЦветГиперссылкиГосИС;
	ЦветТекстаПоля            = ЦветаСтиля.ЦветТекстаПоля;
	ЦветТекстаТребуетВнимания = ЦветаСтиля.ЦветТекстаТребуетВниманияГосИС;
	
	ОтображатьРезультатыФизПроверки = "ФактическиеРасхождения";
	ОтображатьРезультатыЭДО         = "ТолькоРасхождения";
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИПроверитьПереданныеПараметры(Отказ)
	
	Организация = Параметры.Организация;
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'В форму не передана организация'"),,,,
			Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроверяемыйДокумент = Параметры.ПроверяемыйДокумент;
	
	ИнициализироватьДанныеВыбораПоМаркируемойПродукции(ДанныеВыбораПоМаркируемойПродукции);
	
	Если Параметры.Свойство("ДанныеВыбораПоМаркируемойПродукции") Тогда
		Если Параметры.Свойство("СохраненВыборПоМаркируемойПродукции")
			И Параметры.СохраненВыборПоМаркируемойПродукции Тогда
			ДанныеВыбораПоМаркируемойПродукции  = Параметры.ДанныеВыбораПоМаркируемойПродукции;
			СохраненВыборПоМаркируемойПродукции = Параметры.СохраненВыборПоМаркируемойПродукции;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ПараметрыСверкиКодовМаркировки, Параметры);
	ПараметрыСверкиКодовМаркировки.ПроверятьПодключениеИСМП = Не Параметры.ВосстановитьПоДаннымПроверкиПодбора;
	
	ПроверкаЭлектронногоДокумента   = Параметры.ПроверкаЭлектронногоДокумента;
	ДоступноСогласованиеРасхождений = Параметры.ДоступноСогласованиеРасхождений;
	СверкаПоДаннымКорректировки     = Не (Параметры.ВосстановитьПоДаннымПроверкиПодбора Или ДоступноСогласованиеРасхождений);
	
	Элементы.ДеревоМаркированнойПродукцииДействиеПоРасхождениям.Видимость = ДоступноСогласованиеРасхождений;
	Элементы.ПодменюПризнатьРасхождения.Видимость                         = ДоступноСогласованиеРасхождений И Не Параметры.РедактированиеФормыНедоступно;
	
	Заголовок = Параметры.ЗаголовокФормы;
	
	Если ДоступноСогласованиеРасхождений Тогда
		Элементы.ДеревоМаркированнойПродукцииСтатусПроверки.Заголовок = НСтр("ru = 'Статус проверки покупателем'");
	Иначе
		Если СверкаПоДаннымКорректировки Тогда
			Элементы.ДеревоМаркированнойПродукцииСтатусПроверки.Заголовок = НСтр("ru = 'УКД'");
			Элементы.ДеревоМаркированнойПродукцииЭДО.Заголовок = НСтр("ru = 'ТОРГ-2'");
		Иначе
			Элементы.ДеревоМаркированнойПродукцииСтатусПроверки.Заголовок = НСтр("ru = 'Статус проверки'");
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.РедактированиеФормыНедоступно Тогда
		ТолькоПросмотр = Истина;
		Элементы.ФормаПеренестиВДокумент.Заголовок = НСтр("ru = 'Закрыть'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПараметрыСверкиКодовМаркировки()
	
	ПараметрыСверкиКодовМаркировки = Новый Структура();
	ПараметрыСверкиКодовМаркировки.Вставить("ОтсутствуетПодключениеИСМП", Ложь);
	ПараметрыСверкиКодовМаркировки.Вставить("ПроверятьПодключениеИСМП", Ложь);
	ПараметрыСверкиКодовМаркировки.Вставить("ДоступноСогласованиеРасхождений", Ложь);
	ПараметрыСверкиКодовМаркировки.Вставить("ИмяТабличнойЧастиШтрихкодыУпаковокФакт", "");
	ПараметрыСверкиКодовМаркировки.Вставить("ИмяТабличнойЧастиШтрихкодыУпаковокРасхождения", "");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоответствиеШтрихкодовСтрокДерева(АдресСоответствиеШтрихкодыИдентификаторыСтрок)
	
	Если ЭтоАдресВременногоХранилища(АдресСоответствиеШтрихкодыИдентификаторыСтрок) Тогда
		Возврат ПолучитьИзВременногоХранилища(АдресСоответствиеШтрихкодыИдентификаторыСтрок);
	Иначе
		Возврат Новый Соответствие;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаОжиданияЗапросаКлючаСессии()
	
	ЗапроситьКлючСессииНачало();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьКлючСессииНачало(ПовторныйЗапрос = Ложь)
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ПовторныйЗапрос", ПовторныйЗапрос);
	
	ОповещениеПриЗапросеКлючаСессии = Новый ОписаниеОповещения("ЗапроситьКлючСессииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ИнтерфейсАвторизацииИСМПКлиент.ЗапроситьКлючСессии(
		ИнтерфейсИСМПКлиентСервер.ПараметрыЗапросаКлючаСессии(Организация),
		ОповещениеПриЗапросеКлючаСессии);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьКлючСессииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОтказОтАвторизации = Ложь;
	ОшибкаАвторизации  = Ложь;
	
	Если ТипЗнч(Результат) <> Тип("Соответствие") Тогда
		ОтказОтАвторизации = Истина;
	Иначе
		РезультатАвторизации = Результат[Организация];
		
		Если РезультатАвторизации = Неопределено Тогда
			ОшибкаАвторизации = Истина;
			ТекстОшибки = НСтр("ru = 'Произошла ошибка при авторизации в ИС МП'");
		ИначеЕсли РезультатАвторизации <> Истина Тогда
			ОшибкаАвторизации = Истина;
			ТекстОшибки = РезультатАвторизации;
		КонецЕсли;
	КонецЕсли;
	
	ПовторныйЗапрос = Ложь;
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		ДополнительныеПараметры.Свойство("ПовторныйЗапрос", ПовторныйЗапрос);
	КонецЕсли;
	
	Если ПовторныйЗапрос Тогда
		Если ОтказОтАвторизации ИЛИ ОшибкаАвторизации Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Иначе
			ПриПодключенииКСервисуИСМП();
		КонецЕсли;
	ИначеЕсли ОтказОтАвторизации Тогда
		ЗакрытьФорму();
	ИначеЕсли ОшибкаАвторизации Тогда
		ПриОшибкеПодключенияКСервисуИСМП(ТекстОшибки);
	Иначе
		ЗагрузитьДанныеДокумента();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПодключенииКСервисуИСМП()
	
	ПараметрыСверкиКодовМаркировки.ОтсутствуетПодключениеИСМП = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОшибкеПодключенияКСервисуИСМП(ТекстОшибки)
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	ПараметрыСверкиКодовМаркировки.ОтсутствуетПодключениеИСМП = Истина;
	Если Открыта() Тогда
		ПоказатьПредупреждение(Новый ОписаниеОповещения("ЗакрытьФорму", ЭтотОбъект), ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьСохраненныеРезультатыПроверки()
	
	ВыборкаРесурсовПроверки = ПолучитьДанныеПроверкиПоДокументу(ПроверяемыйДокумент);
	
	Если ВыборкаРесурсовПроверки = Неопределено Тогда
		НачальныйСтатусПроверки = Перечисления.СтатусыПроверкиНаличияПродукцииИС.Отсутствует;
		Возврат;
	КонецЕсли;
	
	ДанныеДокумента = Новый Структура;
	ДанныеДокумента.Вставить("ДоступноСогласованиеРасхождений", Ложь);
	ДанныеДокумента.Вставить("ДетализацияСтруктурыХранения", Неопределено);
	ДанныеДокумента.Вставить("СтрокаПродукцияБезУпаковки", Неопределено);
	ДанныеДокумента.Вставить("СтрокаПачкиБезБлока", Неопределено);
	ДанныеДокумента.Вставить("СтрокаБлокиБезКоробки", Неопределено);
	
	ДанныеДокумента.Вставить("ДеревоМаркированнойПродукции", Обработки.РезультатыСверкиКодовМаркировкиТОРГ2.ДеревоМаркированнойПродукции());
	
	РезультатыСверкиПланФакт = Новый Структура;
	РезультатыСверкиПланФакт.Вставить("ШтрихкодыУпаковокПланЭДО", СверкаКодовМаркировкиИСМП.ИнициализацияТаблицыПриемки());
	РезультатыСверкиПланФакт.Вставить("ШтрихкодыУпаковокФактЭДО", СверкаКодовМаркировкиИСМП.ИнициализацияТаблицыПриемки());
	
	ДанныеДокумента.Вставить("ТаблицыТОРГ2", РезультатыСверкиПланФакт);
	
	ДетализацияСтруктурыХранения = Неопределено;
	
	Пока ВыборкаРесурсовПроверки.Следующий() Цикл
		
		Если ТипЗнч(ВыборкаРесурсовПроверки.ДанныеПроверкиИПодбора) <> Тип("ХранилищеЗначения") Тогда
			Возврат;
		КонецЕсли;
		
		ДанныеПроверкиИПодбора = ВыборкаРесурсовПроверки.ДанныеПроверкиИПодбора.Получить();
		
		Если ТипЗнч(ДанныеПроверкиИПодбора) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		
		ЭтоПродукцияМОТП = ЭтоПродукцияМОТП Или ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(ВыборкаРесурсовПроверки.ВидМаркируемойПродукции);
		
		Если ДанныеПроверкиИПодбора.Свойство("ДетализацияСтруктурыХранения") Тогда
			ДетализацияСтруктурыХранения = ДанныеПроверкиИПодбора.ДетализацияСтруктурыХранения;
		ИначеЕсли ДанныеПроверкиИПодбора.Свойство("ДеревоМаркированнойПродукции") Тогда
			ДетализацияСтруктурыХранения = ДетализацияСтруктурыХраненияДерева(ДанныеПроверкиИПодбора.ДеревоМаркированнойПродукции, ЭтоПродукцияМОТП);
		КонецЕсли;
		
		Если ДетализацияСтруктурыХранения <> Неопределено Тогда
			ДанныеДокумента.ДетализацияСтруктурыХранения = ДетализацияСтруктурыХранения;
		КонецЕсли;
		
		Если ДанныеПроверкиИПодбора.Свойство("ДеревоМаркированнойПродукции") Тогда
			ДополнитьДеревоМаркированнойПродукции(ДанныеПроверкиИПодбора.ДеревоМаркированнойПродукции, ДанныеДокумента);
		КонецЕсли;
		
		Если ДанныеПроверкиИПодбора.Свойство("ТаблицыТОРГ2") Тогда
			ДополнитьТаблицыРезультатовСверкиПланФакт(ДанныеПроверкиИПодбора.ТаблицыТОРГ2, ДанныеДокумента);
		КонецЕсли;
		
	КонецЦикла;
	
	ВывестиДанныеПроверкиИПодбораНаФорму(ДанныеДокумента);
	
	ВосстановленыСохраненныеРезультатыПроверки = Истина;
	
	ВывестиРезультатыПроверкиЭДО();
	
КонецПроцедуры

&НаСервере
Процедура ВывестиДанныеПроверкиИПодбораНаФорму(ДанныеПроверкиИПодбора)
	
	Если ДанныеПроверкиИПодбора.Свойство("ТаблицаРасхожденийКодовМаркировки") Тогда
		ТаблицаРасхожденийКодовМаркировкиОбъект = ДанныеПроверкиИПодбора.ТаблицаРасхожденийКодовМаркировки;
	Иначе
		Если ДанныеПроверкиИПодбора.Свойство("ТаблицыТОРГ2") Тогда
			ТаблицаРасхожденийКодовМаркировкиОбъект = СверкаКодовМаркировкиИСМП.ТаблицаРасхожденийШтриховыхКодовПолная(ДанныеПроверкиИПодбора.ТаблицыТОРГ2);
		Иначе
			ТаблицаРасхожденийКодовМаркировкиОбъект = СверкаКодовМаркировкиИСМП.ИнициализацияТаблицыШтрихкодыУпаковокПринятоИзлишекНедостача();
		КонецЕсли;
		
	КонецЕсли;
	
	Попытка
		ЗначениеВРеквизитФормы(ТаблицаРасхожденийКодовМаркировкиОбъект, "ТаблицаРасхожденийКодовМаркировки");
	Исключение
		ПроверкаИПодборПродукцииИС.ПроверитьКолонкиИсточникаИПриемникаНаСовместимость(
			ТаблицаРасхожденийКодовМаркировкиОбъект, РеквизитФормыВЗначение("ТаблицаРасхожденийКодовМаркировки"));
		Возврат;
	КонецПопытки;
		
	Если ДанныеПроверкиИПодбора.Свойство("ДеревоМаркированнойПродукции") Тогда
		
		ДеревоМаркированнойПродукцииОбъект = ДанныеПроверкиИПодбора.ДеревоМаркированнойПродукции;
		
		ДобавитьКолонкиПриВосстановленииДерева(ДеревоМаркированнойПродукцииОбъект);
		
		ПараметрыЗаполнения = Новый Структура();
		ПараметрыЗаполнения.Вставить("СоответствуетОтбору", Ложь);
		ПараметрыЗаполнения.Вставить("ПерваяИтерация", Истина);
		ПараметрыЗаполнения.Вставить("ИндексКартинкиЭДОПоУмолчанию", ИндексКартинкиЭДОПоУмолчанию);
		ПараметрыЗаполнения.Вставить("СохраненВыборПоМаркируемойПродукции", СохраненВыборПоМаркируемойПродукции);
		
		Если СохраненВыборПоМаркируемойПродукции Тогда
			
			ПараметрыЗаполнения.Вставить("СоответствуетОтборуНоменклатура", Ложь);
			
			ПараметрыЗаполнения.Вставить("Номенклатура", ДанныеВыбораПоМаркируемойПродукции.Номенклатура);
			ПараметрыЗаполнения.Вставить("Характеристика", ДанныеВыбораПоМаркируемойПродукции.Характеристика);
			
		КонецЕсли;
		
		ЗаполнитьСлужебныеРеквизитыКоллекцииСтрок(ДеревоМаркированнойПродукцииОбъект.Строки, ПараметрыЗаполнения);
		
		ДополнитьУпаковкиПоДаннымЭДО(
			ДеревоМаркированнойПродукцииОбъект,
			ТаблицаРасхожденийКодовМаркировкиОбъект,
			ДанныеПроверкиИПодбора);
		
		Попытка
			ЗначениеВРеквизитФормы(ДеревоМаркированнойПродукцииОбъект, "ДеревоМаркированнойПродукции");
		Исключение
			ПроверкаИПодборПродукцииИС.ПроверитьКолонкиИсточникаИПриемникаНаСовместимость(
				ДеревоМаркированнойПродукцииОбъект, РеквизитФормыВЗначение("ДеревоМаркированнойПродукции"));
			Возврат;
		КонецПопытки;
		
		СоответствиеШтрихкодовСтрокДерева = Новый Соответствие;
		ДополнитьСоответствиеШтрихкодовСтрокДереваДляКоллекции(ДеревоМаркированнойПродукции.ПолучитьЭлементы(), СоответствиеШтрихкодовСтрокДерева);
		
		Если ПустаяСтрока(АдресСоответствиеШтрихкодыИдентификаторыСтрок) Тогда
			АдресСоответствиеШтрихкодыИдентификаторыСтрок = ПоместитьВоВременноеХранилище(
				СоответствиеШтрихкодовСтрокДерева,
				УникальныйИдентификатор);
		Иначе
			АдресСоответствиеШтрихкодыИдентификаторыСтрок = ПоместитьВоВременноеХранилище(
				СоответствиеШтрихкодовСтрокДерева,
				АдресСоответствиеШтрихкодыИдентификаторыСтрок);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеПроверкиПоДокументу(ДокументДляОтбора) 
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СтатусыПроверкиИПодбораДокументовИСМП.ВидМаркируемойПродукции КАК ВидМаркируемойПродукции,
		|	СтатусыПроверкиИПодбораДокументовИСМП.ДанныеПроверкиИПодбора КАК ДанныеПроверкиИПодбора,
		|	СтатусыПроверкиИПодбораДокументовИСМП.СтатусПроверкиИПодбора КАК СтатусПроверкиИПодбора,
		|	СтатусыПроверкиИПодбораДокументовИСМП.Документ КАК Документ
		|ИЗ
		|	РегистрСведений.СтатусыПроверкиИПодбораДокументовИСМП КАК СтатусыПроверкиИПодбораДокументовИСМП
		|ГДЕ
		|	СтатусыПроверкиИПодбораДокументовИСМП.Документ В(&Документ)");
	
	Запрос.УстановитьПараметр("Документ", ДокументДляОтбора);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Возврат РезультатЗапроса.Выбрать();
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ДобавитьКолонкиПриВосстановленииДерева(ДеревоМаркированнойПродукцииОбъект)
	
	ИмяКолонки = "ВидУпаковки";
	Если ДеревоМаркированнойПродукцииОбъект.Колонки.Найти(ИмяКолонки) = Неопределено Тогда
		ДеревоМаркированнойПродукцииОбъект.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("ПеречислениеСсылка.ВидыУпаковокИС"));
	КонецЕсли;
	
	ИмяКолонки = "ЭДО";
	Если ДеревоМаркированнойПродукцииОбъект.Колонки.Найти(ИмяКолонки) = Неопределено Тогда
		ДеревоМаркированнойПродукцииОбъект.Колонки.Добавить(ИмяКолонки, ОбщегоНазначения.ОписаниеТипаЧисло(1));
	КонецЕсли;
	
	ИмяКолонки = "НеСоответствуетОтборуЭДО";
	Если ДеревоМаркированнойПродукцииОбъект.Колонки.Найти(ИмяКолонки) = Неопределено Тогда
		ДеревоМаркированнойПродукцииОбъект.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	ИмяКолонки = "НеСоответствуетОтборуВсе";
	Если ДеревоМаркированнойПродукцииОбъект.Колонки.Найти(ИмяКолонки) = Неопределено Тогда
		ДеревоМаркированнойПродукцииОбъект.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	ИмяКолонки = "НеСоответствуетОтборуНоменклатура";
	Если ДеревоМаркированнойПродукцииОбъект.Колонки.Найти(ИмяКолонки) = Неопределено Тогда
		ДеревоМаркированнойПродукцииОбъект.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	ИмяКолонки = "ДействиеПоРасхождениям";
	Если ДеревоМаркированнойПродукцииОбъект.Колонки.Найти(ИмяКолонки) = Неопределено Тогда
		ДеревоМаркированнойПродукцииОбъект.Колонки.Добавить(ИмяКолонки, Метаданные.ОпределяемыеТипы.ВариантДействийПоРасхождениямКодовМаркировкиИСМП.Тип);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСлужебныеРеквизитыКоллекцииСтрок(КоллекцияСтрок, 
	ПараметрыЗаполнения, СоответствуетОтбору = Ложь, ПерваяИтерация = Истина)
	
	ПараметрыЗаполненияВложенныхСтрок = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыЗаполнения);
	
	Для Каждого СтрокаДерева Из КоллекцияСтрок Цикл
		
		Если СтрокаДерева.ТипУпаковки = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПродукцияБезУпаковки 
			Или СтрокаДерева.ТипУпаковки = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПачкиБезБлока
			Или СтрокаДерева.ТипУпаковки = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.БлокиБезКоробки Тогда
			СтрокаДерева.ИндексКартинкиСтатусПроверки = ПараметрыЗаполнения.ИндексКартинкиЭДОПоУмолчанию;
			СтрокаДерева.ЭДО = ПараметрыЗаполнения.ИндексКартинкиЭДОПоУмолчанию;
			Если СтрокаДерева.Строки.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ТекущаяСтрокаСоответствуетОтбору = Ложь;
		ТекущаяСтрокаСоответствуетОтборуНоменклатура = Ложь;
		
		Если ПараметрыЗаполнения.СохраненВыборПоМаркируемойПродукции Тогда
			ПараметрыЗаполненияВложенныхСтрок.СоответствуетОтборуНоменклатура = Ложь;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаДерева.ВидУпаковки) Тогда
			Если ИнтеграцияИСКлиентСервер.ЭтоУпаковка(СтрокаДерева.ТипУпаковки) Тогда
				СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая;
			Иначе
				СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская;
			КонецЕсли;
		КонецЕсли;
		
		Если ПараметрыЗаполнения.СохраненВыборПоМаркируемойПродукции Тогда
			
			Если (СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская
				Или СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая)
				И СтрокаДерева.Номенклатура = ПараметрыЗаполнения.Номенклатура 
				И СтрокаДерева.Характеристика = ПараметрыЗаполнения.Характеристика Тогда
				ТекущаяСтрокаСоответствуетОтборуНоменклатура = Истина;
			Иначе
				ТекущаяСтрокаСоответствуетОтборуНоменклатура = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		СоответствуетОтбору = Ложь;
		ПараметрыЗаполненияВложенныхСтрок.СоответствуетОтбору = Ложь;
		Если СтрокаДерева.Строки.Количество() = 0 Тогда
			
			ТекущаяСтрокаСоответствуетОтбору = СтрокаДерева.СтатусПроверки <> Перечисления.СтатусыПроверкиНаличияПродукцииИС.ВНаличии;
			
		Иначе
			ЗаполнитьСлужебныеРеквизитыКоллекцииСтрок(СтрокаДерева.Строки, 
				ПараметрыЗаполненияВложенныхСтрок, СоответствуетОтбору);
			Если ПараметрыЗаполненияВложенныхСтрок.СоответствуетОтбору Тогда
				ТекущаяСтрокаСоответствуетОтбору = Истина;
			КонецЕсли;
			Если Не ТекущаяСтрокаСоответствуетОтбору Тогда
				ТекущаяСтрокаСоответствуетОтбору = СтрокаДерева.СтатусПроверки <> Перечисления.СтатусыПроверкиНаличияПродукцииИС.ВНаличии;
			КонецЕсли;
			
			Если ПараметрыЗаполнения.СохраненВыборПоМаркируемойПродукции Тогда
				Если ПараметрыЗаполненияВложенныхСтрок.СоответствуетОтборуНоменклатура Тогда
					ТекущаяСтрокаСоответствуетОтборуНоменклатура = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		ПараметрыЗаполнения.СоответствуетОтбору = ТекущаяСтрокаСоответствуетОтбору Или ПараметрыЗаполнения.СоответствуетОтбору;
		СтрокаДерева.НеСоответствуетОтбору      = Не ТекущаяСтрокаСоответствуетОтбору;
		
		Если ПараметрыЗаполнения.СохраненВыборПоМаркируемойПродукции Тогда
			
			ПараметрыЗаполнения.СоответствуетОтборуНоменклатура = ТекущаяСтрокаСоответствуетОтборуНоменклатура
				Или ПараметрыЗаполнения.СоответствуетОтборуНоменклатура;
			СтрокаДерева.НеСоответствуетОтборуНоменклатура     = Не ТекущаяСтрокаСоответствуетОтборуНоменклатура;
			
		КонецЕсли;
		
		СтрокаДерева.НеСоответствуетОтборуЭДО = Истина;
		СтрокаДерева.ЭДО = ПараметрыЗаполнения.ИндексКартинкиЭДОПоУмолчанию;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьТаблицыРезультатовСверкиПланФакт(ТаблицыТОРГ2, ДанныеДокумента)
	
	Для Каждого СтрокаТаблицы Из ТаблицыТОРГ2.ШтрихкодыУпаковокПланЭДО Цикл
		ЗаполнитьЗначенияСвойств(ДанныеДокумента.ТаблицыТОРГ2.ШтрихкодыУпаковокПланЭДО.Добавить(), СтрокаТаблицы);
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ТаблицыТОРГ2.ШтрихкодыУпаковокФактЭДО Цикл
		ЗаполнитьЗначенияСвойств(ДанныеДокумента.ТаблицыТОРГ2.ШтрихкодыУпаковокФактЭДО.Добавить(), СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьДеревоМаркированнойПродукции(ДеревоУпаковокДокумента, ДанныеДокумента)
	
	КоллекцияСтрокПриемника = ДанныеДокумента.ДеревоМаркированнойПродукции.Строки;

	Если ЭтоПродукцияМОТП Тогда
		
		Для Каждого СтрокаДереваУпаковок Из ДеревоУпаковокДокумента.Строки Цикл
			ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузкеМОТП(СтрокаДереваУпаковок, КоллекцияСтрокПриемника, ДанныеДокумента);
		КонецЦикла;
		
		Если ДанныеДокумента.СтрокаПачкиБезБлока = Неопределено
			И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки Тогда
			ДобавленнаяСтрокаПачкиБезБлока(ДанныеДокумента);
		КонецЕсли;
		
		Если ДанныеДокумента.СтрокаБлокиБезКоробки = Неопределено
			И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки
			И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими Тогда
			ДобавленнаяСтрокаБлокиБезКоробки(ДанныеДокумента);
		КонецЕсли;
		
	Иначе
		
		Для Каждого СтрокаДереваУпаковок Из ДеревоУпаковокДокумента.Строки Цикл
			ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузке(СтрокаДереваУпаковок, КоллекцияСтрокПриемника, ДанныеДокумента);
		КонецЦикла;
		
		Если ДанныеДокумента.СтрокаПродукцияБезУпаковки = Неопределено
			И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковки Тогда
			ДобавленнаяСтрокаПродукцияБезУпаковки(ДанныеДокумента);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузке(СтрокаИсточника, КоллекцияСтрокПриемника, ДанныеДокумента)
	
	Если СтрокаИсточника.ТипУпаковки = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПродукцияБезУпаковки Тогда
		
		Если ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковки Тогда
			
			НоваяСтрока = ДанныеДокумента.СтрокаПродукцияБезУпаковки;
			
			Если НоваяСтрока = Неопределено Тогда
				НоваяСтрока = ДобавленнаяСтрокаПродукцияБезУпаковки(ДанныеДокумента);
			КонецЕсли;
			
		Иначе
			
			Для Каждого СтрокаПродукции Из СтрокаИсточника.Строки Цикл
				ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузке(СтрокаПродукции, КоллекцияСтрокПриемника, ДанныеДокумента);
			КонецЦикла;
			
			Возврат;
			
		КонецЕсли;
	
	ИначеЕсли СтрокаИсточника.Родитель = Неопределено
		И СтрокаИсточника.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар
		И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковки Тогда
		
		СтрокаПродукцияБезУпаковки = ДанныеДокумента.СтрокаПродукцияБезУпаковки;
		
		Если СтрокаПродукцияБезУпаковки = Неопределено Тогда
			СтрокаПродукцияБезУпаковки = ДобавленнаяСтрокаПродукцияБезУпаковки(ДанныеДокумента);
		КонецЕсли;
		
		НоваяСтрока = СтрокаПродукцияБезУпаковки.Строки.Добавить();
		
	Иначе
		
		НоваяСтрока = КоллекцияСтрокПриемника.Добавить();
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаИсточника);
	
	Если НоваяСтрока.ТипУпаковки = Перечисления.ТипыУпаковок.ПустаяСсылка() Тогда
		НоваяСтрока.ТипУпаковки = Перечисления.ТипыУпаковок.МонотоварнаяУпаковка;
	КонецЕсли;
	
	ПроверкаИПодборПродукцииИСМПКлиентСервер.СформироватьПредставлениеДляСтрокиДереваМаркированнойПродукции(НоваяСтрока);
	
	КоллекцияСтрокНовойСтроки = НоваяСтрока.Строки;
	
	Для Каждого ПодчиненнаяСтрокаИсточника Из СтрокаИсточника.Строки Цикл
		ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузке(ПодчиненнаяСтрокаИсточника, КоллекцияСтрокНовойСтроки, ДанныеДокумента);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузкеМОТП(СтрокаИсточника, КоллекцияСтрокПриемника, ДанныеДокумента)
	
	Если СтрокаИсточника.ТипУпаковки = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПачкиБезБлока Тогда
		
		Если ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки Тогда
			
			НоваяСтрока = ДанныеДокумента.СтрокаПачкиБезБлока;
			
			Если НоваяСтрока = Неопределено Тогда
				НоваяСтрока = ДобавленнаяСтрокаПачкиБезБлока(ДанныеДокумента);
			КонецЕсли;
			
		Иначе
			
			Для Каждого СтрокаИсточникаПачка Из СтрокаИсточника.Строки Цикл
				ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузкеМОТП(СтрокаИсточникаПачка, КоллекцияСтрокПриемника, ДанныеДокумента);
			КонецЦикла;
			
			Возврат;
			
		КонецЕсли;
		
	ИначеЕсли СтрокаИсточника.ТипУпаковки = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.БлокиБезКоробки Тогда
		
		Если ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки
			И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими Тогда
			
			НоваяСтрока = ДанныеДокумента.СтрокаБлокиБезКоробки;
			
			Если НоваяСтрока = Неопределено Тогда
				НоваяСтрока = ДобавленнаяСтрокаБлокиБезКоробки(ДанныеДокумента);
			КонецЕсли;
			
		Иначе
			
			Для Каждого СтрокаИсточникаБлок Из СтрокаИсточника.Строки Цикл
				Если ДанныеДокумента.ДетализацияСтруктурыХранения = Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими Тогда
					ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузкеМОТП(СтрокаИсточникаБлок, КоллекцияСтрокПриемника, ДанныеДокумента);
				Иначе
					Для Каждого СтрокаИсточникаПачка Из СтрокаИсточникаБлок.Строки Цикл
						ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузкеМОТП(СтрокаИсточникаПачка, КоллекцияСтрокПриемника, ДанныеДокумента);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			
			Возврат;
		
		КонецЕсли;
		
	ИначеЕсли СтрокаИсточника.Родитель = Неопределено
		И СтрокаИсточника.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар
		И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки Тогда
		
		СтрокаПачкиБезБлока = ДанныеДокумента.СтрокаПачкиБезБлока;
		
		Если СтрокаПачкиБезБлока = Неопределено Тогда
			СтрокаПачкиБезБлока = ДобавленнаяСтрокаПачкиБезБлока(ДанныеДокумента);
		КонецЕсли;
				
	ИначеЕсли СтрокаИсточника.Родитель = Неопределено
		И ПроверкаИПодборПродукцииМОТПКлиентСервер.ЭтоУпаковкаБлока(СтрокаИсточника)
		И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки
		И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими Тогда
			
		СтрокаБлокиБезКоробки = ДанныеДокумента.СтрокаБлокиБезКоробки;
		
		Если СтрокаБлокиБезКоробки = Неопределено Тогда
			СтрокаБлокиБезКоробки = ДобавленнаяСтрокаБлокиБезКоробки(ДанныеДокумента);
		КонецЕсли;
		
		НоваяСтрока = СтрокаБлокиБезКоробки.Строки.Добавить();
		
	ИначеЕсли СтрокаИсточника.Родитель <> Неопределено
		И ПроверкаИПодборПродукцииМОТПКлиентСервер.ЭтоУпаковкаБлока(СтрокаИсточника.Родитель)
		И СтрокаИсточника.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар
		И ДанныеДокумента.ДетализацияСтруктурыХранения = Перечисления.ДетализацияСтруктурыХраненияИС.КоробаСГрупповымиУпаковками Тогда
		
		Возврат;
		
	Иначе
		
		НоваяСтрока = КоллекцияСтрокПриемника.Добавить();
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаИсточника);
	НоваяСтрока.КоличествоПодчиненнойПродукции = СтрокаИсточника.КоличествоПодчиненныхПачек;
	Если НоваяСтрока.ТипУпаковки = Перечисления.ТипыУпаковок.ПустаяСсылка() Тогда
		НоваяСтрока.ТипУпаковки = Перечисления.ТипыУпаковок.МонотоварнаяУпаковка;		
	КонецЕсли;
	
	Если НоваяСтрока.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
		И СтрокаИсточника.Строки.Количество() = 0 Тогда
		НоваяСтрока.НеПересчитыватьКоличествоПачек = Истина;
	КонецЕсли;
	
	Если НоваяСтрока.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая
		И ЗначениеЗаполнено(НоваяСтрока.GTIN)
		И СтрокаИсточника.Строки.Количество() = 0 Тогда
		НоваяСтрока.НеПересчитыватьКоличествоПачек = Истина;
	КонецЕсли;
	
	ПроверкаИПодборПродукцииИСМПКлиентСервер.СформироватьПредставлениеДляСтрокиДереваМаркированнойПродукции(НоваяСтрока);
	
	КоллекцияСтрокНовойСтроки = НоваяСтрока.Строки;
	
	Для Каждого ПодчиненнаяСтрокаИсточника Из СтрокаИсточника.Строки Цикл
		ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузкеМОТП(ПодчиненнаяСтрокаИсточника, КоллекцияСтрокНовойСтроки, ДанныеДокумента);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ДобавленнаяСтрокаПродукцияБезУпаковки(ДанныеДокумента)
	
	СтрокаПродукцияБезУпаковки = ДанныеДокумента.ДеревоМаркированнойПродукции.Строки.Найти(
		Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПродукцияБезУпаковки,
		"ТипУпаковки");
	
	Если СтрокаПродукцияБезУпаковки <> Неопределено Тогда
		ДанныеДокумента.СтрокаПродукцияБезУпаковки = СтрокаПродукцияБезУпаковки;
		Возврат СтрокаПродукцияБезУпаковки;
	КонецЕсли;
	
	НоваяСтрока = ДанныеДокумента.ДеревоМаркированнойПродукции.Строки.Вставить(0);
	
	ПроверкаИПодборПродукцииИСМПКлиентСервер.ЗаполнитьСтрокуПродукцияБезУпаковки(НоваяСтрока);
	
	Если ДанныеДокумента <> Неопределено Тогда
		ДанныеДокумента.СтрокаПродукцияБезУпаковки = НоваяСтрока;
	КонецЕсли;
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаСервере
Функция ДобавленнаяСтрокаПачкиБезБлока(ДанныеДокумента)
	
	СтрокаПачкиБезБлока = ДанныеДокумента.ДеревоМаркированнойПродукции.Строки.Найти(
		Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПачкиБезБлока,
		"ТипУпаковки");
	
	Если СтрокаПачкиБезБлока <> Неопределено Тогда
		ДанныеДокумента.СтрокаПачкиБезБлока = СтрокаПачкиБезБлока;
		Возврат СтрокаПачкиБезБлока;
	КонецЕсли;
	
	НоваяСтрока = ДанныеДокумента.ДеревоМаркированнойПродукции.Строки.Вставить(0);
	
	ПроверкаИПодборПродукцииМОТПКлиентСервер.ЗаполнитьСтрокуПачкиБезБлока(НоваяСтрока);
	
	ДанныеДокумента.СтрокаПачкиБезБлока = НоваяСтрока;
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаСервере
Функция ДобавленнаяСтрокаБлокиБезКоробки(ДанныеДокумента)
	
	СтрокаБлокиБезКоробки = ДанныеДокумента.ДеревоМаркированнойПродукции.Строки.Найти(
		Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.БлокиБезКоробки,
		"ТипУпаковки");
	
	Если СтрокаБлокиБезКоробки <> Неопределено Тогда
		ДанныеДокумента.СтрокаБлокиБезКоробки = СтрокаБлокиБезКоробки;
		Возврат СтрокаБлокиБезКоробки;
	КонецЕсли;

	СтрокиДерева = ДанныеДокумента.ДеревоМаркированнойПродукции.Строки;
	
	Если СтрокиДерева.Количество() = 0 Тогда
		НоваяСтрока = СтрокиДерева.Вставить(0);
	ИначеЕсли СтрокиДерева[0].ТипУпаковки <> Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПачкиБезБлока Тогда
		НоваяСтрока = СтрокиДерева.Вставить(0);
	Иначе
		НоваяСтрока = СтрокиДерева.Вставить(1);
	КонецЕсли;
	
	ПроверкаИПодборПродукцииМОТПКлиентСервер.ЗаполнитьСтрокуБлокиБезКоробки(НоваяСтрока);
	
	ДанныеДокумента.СтрокаБлокиБезКоробки = НоваяСтрока;
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаКлиенте
Функция ДобавленнаяСтрокаПродукцияБезУпаковкиНаКлиенте()
	
	НоваяСтрока = ДеревоМаркированнойПродукции.ПолучитьЭлементы().Вставить(0);
	
	ПроверкаИПодборПродукцииИСМПКлиентСервер.ЗаполнитьСтрокуПродукцияБезУпаковки(НоваяСтрока);
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаКлиенте
Функция ДобавленнаяСтрокаПачкиБезБлокаНаКлиенте()
	
	НоваяСтрока = ДеревоМаркированнойПродукции.ПолучитьЭлементы().Вставить(0);
	
	ПроверкаИПодборПродукцииМОТПКлиентСервер.ЗаполнитьСтрокуПачкиБезБлока(НоваяСтрока);
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаКлиенте
Функция ДобавленнаяСтрокаБлокиБезКоробкиНаКлиенте()
	
	ЭлементыДерева = ДеревоМаркированнойПродукции.ПолучитьЭлементы();
	
	Если ЭлементыДерева.Количество() = 0 Тогда
		НоваяСтрока = ЭлементыДерева.Вставить(0);
	ИначеЕсли ЭлементыДерева[0].ТипУпаковки <> ПредопределенноеЗначение("Перечисление.ПрочиеЗоныПересчетаПродукцииИСМП.ПачкиБезБлока") Тогда
		НоваяСтрока = ЭлементыДерева.Вставить(0);
	Иначе
		НоваяСтрока = ЭлементыДерева.Вставить(1);
	КонецЕсли;
	
	ПроверкаИПодборПродукцииМОТПКлиентСервер.ЗаполнитьСтрокуБлокиБезКоробки(НоваяСтрока);
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаСервереБезКонтекста
Функция СохраненнаяДетализацияСтруктурыХранения(ЭтоПродукцияМОТП, РежимПодбораСуществующихУпаковок)
	
	Возврат Обработки.РезультатыСверкиКодовМаркировкиТОРГ2.СохраненнаяДетализацияСтруктурыХранения(ЭтоПродукцияМОТП, РежимПодбораСуществующихУпаковок);
	
КонецФункции

&НаСервере
Процедура ДополнитьУпаковкиПоДаннымЭДО(ДеревоМаркированнойПродукцииОбъект, ТаблицаРасхожденийПолная, ДанныеПроверкиИПодбора)
	
	Детализация = ДанныеПроверкиИПодбора.ДетализацияСтруктурыХранения;
	
	Если ЭтоПродукцияМОТП Тогда
		СтрокаБлокиБезКоробки =  ДеревоМаркированнойПродукцииОбъект.Строки.Найти(
			Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.БлокиБезКоробки,
			"ТипУпаковки");
		
		СтрокаПродукцияБезУпаковки = ДеревоМаркированнойПродукцииОбъект.Строки.Найти(
			Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПачкиБезБлока,
			"ТипУпаковки");
		
	Иначе
		СтрокаПродукцияБезУпаковки = ДеревоМаркированнойПродукцииОбъект.Строки.Найти(
			Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПродукцияБезУпаковки,
			"ТипУпаковки");
	КонецЕсли;
	
	ТаблицаРасхождений = ТаблицаРасхожденийПолная.Скопировать(,"ЗначениеШтрихкода, ТипРасхождения");
	ТаблицаРасхождений.Свернуть("ЗначениеШтрихкода, ТипРасхождения");
	
	Для Каждого СтрокаРасхождений Из ТаблицаРасхождений Цикл
		НайденнаяСтрокаДерева = ДеревоМаркированнойПродукцииОбъект.Строки.Найти(
			СтрокаРасхождений.ЗначениеШтрихкода,
			"Штрихкод", Истина);
		
		Если НайденнаяСтрокаДерева = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаРасхождений.ТипРасхождения = ИнтеграцияИСМП.ТипРасхожденияИСМПИзлишек() Тогда
			ИндексКартинкиЭДО = 4;
		ИначеЕсли СтрокаРасхождений.ТипРасхождения = ИнтеграцияИСМП.ТипРасхожденияИСМПНедостача()
			Или СтрокаРасхождений.ТипРасхождения = ИнтеграцияИСМП.ТипРасхожденияИСМПБрак() Тогда
			ИндексКартинкиЭДО = 1;
		Иначе
			ИндексКартинкиЭДО = ИндексКартинкиЭДОПоУмолчанию;
		КонецЕсли;
		
		НайденнаяСтрокаДерева.ЭДО = ИндексКартинкиЭДО;
		Если ИндексКартинкиЭДО = 4 Тогда
			Если НайденнаяСтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская Тогда
				Если Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки
					Или Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковки Тогда
					НайденнаяСтрокаДерева.НеСоответствуетОтборуЭДО = Ложь;
				Иначе
					СтрокаПродукцияБезУпаковки.НеСоответствуетОтборуЭДО = Ложь;
					Если НайденнаяСтрокаДерева.Родитель = СтрокаПродукцияБезУпаковки Тогда
						НайденнаяСтрокаДерева.НеСоответствуетОтборуЭДО = Ложь;
					Иначе
						НайденнаяСтрокаДерева.НеСоответствуетОтборуЭДО = Истина;
						ДобавитьСтрокуИзлишекЭДО(СтрокаПродукцияБезУпаковки, НайденнаяСтрокаДерева);
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли ЭтоПродукцияМОТП
				И ПроверкаИПодборПродукцииМОТПКлиентСервер.ЭтоУпаковкаБлока(НайденнаяСтрокаДерева)
				И Детализация <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки
				И Детализация <> Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими Тогда
				
				Если СтрокаБлокиБезКоробки <> Неопределено Тогда
					СтрокаБлокиБезКоробки.НеСоответствуетОтборуЭДО = Ложь;
					Если НайденнаяСтрокаДерева.Родитель = СтрокаБлокиБезКоробки Тогда
						НайденнаяСтрокаДерева.НеСоответствуетОтборуЭДО = Ложь;
					Иначе
						НайденнаяСтрокаДерева.НеСоответствуетОтборуЭДО = Истина;
						НоваяСтрока = ДобавитьСтрокуИзлишекЭДО(СтрокаБлокиБезКоробки, НайденнаяСтрокаДерева);
						Для Каждого ВложеннаяСтрока Из НайденнаяСтрокаДерева.Строки Цикл
							ЗаполнитьВложенныеСтрокиИзлишекЭДО(ВложеннаяСтрока, НоваяСтрока);
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			Иначе //логистическая
				НоваяСтрока = ДобавитьСтрокуИзлишекЭДО(ДеревоМаркированнойПродукцииОбъект, НайденнаяСтрокаДерева);
				Для Каждого ВложеннаяСтрока Из НайденнаяСтрокаДерева.Строки Цикл
					ЗаполнитьВложенныеСтрокиИзлишекЭДО(ВложеннаяСтрока, НоваяСтрока);
				КонецЦикла;
			КонецЕсли;
			
		ИначеЕсли ИндексКартинкиЭДО = 1 Тогда
			НайденнаяСтрокаДерева.НеСоответствуетОтборуЭДО = Ложь;
			Если НайденнаяСтрокаДерева.Родитель = СтрокаПродукцияБезУпаковки
				И СтрокаПродукцияБезУпаковки <> Неопределено
				И (Детализация <> Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковки
				   Или Детализация <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки)
				Тогда
				СтрокаПродукцияБезУпаковки.НеСоответствуетОтборуЭДО = Ложь;
			КонецЕсли;
			Если ЭтоПродукцияМОТП 
				И Детализация <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки
				И Детализация <> Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими
				И НайденнаяСтрокаДерева.Родитель = СтрокаБлокиБезКоробки 
				И СтрокаБлокиБезКоробки <> Неопределено Тогда
				СтрокаБлокиБезКоробки.НеСоответствуетОтборуЭДО = Ложь;
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ДобавитьСтрокуИзлишекЭДО(СтрокаДерева, СтрокаИсточник)
	
	НоваяСтрока = СтрокаДерева.Строки.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаИсточник);
	НоваяСтрока.НеСоответствуетОтборуВсе = Истина;
	НоваяСтрока.НеСоответствуетОтборуЭДО = Ложь;
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьВложенныеСтрокиИзлишекЭДО(Источник, СтрокаПриемник)
	
	НоваяСтрока = СтрокаПриемник.Строки.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Источник);
	НоваяСтрока.НеСоответствуетОтборуВсе = Истина;
	НоваяСтрока.НеСоответствуетОтборуЭДО = Ложь;
	НоваяСтрока.НеСоответствуетОтборуНоменклатура = Истина;
	
	Если Источник.ВидУпаковки <> Перечисления.ВидыУпаковокИС.Потребительская Тогда
		Для Каждого ВложеннаяСтрока Из Источник.Строки Цикл
			ЗаполнитьВложенныеСтрокиИзлишекЭДО(ВложеннаяСтрока, НоваяСтрока);
		КонецЦикла;
	КонецЕсли;
	
Конецпроцедуры

&НаСервере
Процедура ВывестиРезультатыПроверкиЭДО()
	
	ДеревоРезультатовЭДООбъект = ДеревоРезультатовЭДО();
	
	ЗаполнитьДанныеДереваПродукцииЭДО(ТаблицаРасхожденийКодовМаркировки, ДеревоРезультатовЭДООбъект);
	
	Попытка
		ЗначениеВРеквизитФормы(ДеревоРезультатовЭДООбъект, "ДеревоРезультатовЭДО");
	Исключение
		ПроверкаИПодборПродукцииИС.ПроверитьКолонкиИсточникаИПриемникаНаСовместимость(
			ДеревоРезультатовЭДООбъект, РеквизитФормыВЗначение("ДеревоРезультатовЭДО"));
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция ДеревоРезультатовЭДО()
	
	ДеревоЗначенийРезультатовЭДО = Новый ДеревоЗначений();
	
	ДеревоЗначенийРезультатовЭДО.Колонки.Добавить("СтатусПроверки",                      Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыПроверкиНаличияПродукцииИС"));
	ДеревоЗначенийРезультатовЭДО.Колонки.Добавить("ЗначениеШтрихкода",                   Новый ОписаниеТипов("Строка"));
	ДеревоЗначенийРезультатовЭДО.Колонки.Добавить("ИндексКартинкиТипУпаковки",           Новый ОписаниеТипов("Число"));
	ДеревоЗначенийРезультатовЭДО.Колонки.Добавить("ИндексКартинкиСтатусПроверки",        Новый ОписаниеТипов("Число"));
	ДеревоЗначенийРезультатовЭДО.Колонки.Добавить("Представление",                       Новый ОписаниеТипов("Строка"));
	ДеревоЗначенийРезультатовЭДО.Колонки.Добавить("НеСоответствуетОтбору",               Новый ОписаниеТипов("Булево"));
	ДеревоЗначенийРезультатовЭДО.Колонки.Добавить("НеСоответствуетОтборуНоменклатура",   Новый ОписаниеТипов("Булево"));
	ДеревоЗначенийРезультатовЭДО.Колонки.Добавить("КоличествоПодчиненныхНеЧислилось",    Новый ОписаниеТипов("Число"));
	ДеревоЗначенийРезультатовЭДО.Колонки.Добавить("КоличествоПодчиненныхВНаличии",       Новый ОписаниеТипов("Число"));
	ДеревоЗначенийРезультатовЭДО.Колонки.Добавить("КоличествоПодчиненныхОтсутствует",    Новый ОписаниеТипов("Число"));
	ДеревоЗначенийРезультатовЭДО.Колонки.Добавить("НедопустимыйКодМаркировки",           Новый ОписаниеТипов("Булево"));
	
	ДеревоЗначенийРезультатовЭДО.Колонки.Добавить("Номенклатура",                        Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
	ДеревоЗначенийРезультатовЭДО.Колонки.Добавить("Характеристика",                      Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
	ДеревоЗначенийРезультатовЭДО.Колонки.Добавить("ИдентификаторСтроки",                 Новый ОписаниеТипов("Строка"));
	
	Возврат ДеревоЗначенийРезультатовЭДО;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеДереваПродукцииЭДО(ТаблицаРасхождений, ДеревоРезультатовЭДО)
	
	ТекНоменклатура = ИнтеграцияИС.ПустоеЗначениеОпределяемогоТипа("Номенклатура");
	ТекХарактеристика = ИнтеграцияИС.ПустоеЗначениеОпределяемогоТипа("ХарактеристикаНоменклатуры");
	
	НоваяГруппировка = Истина;
	ГруппировкаНеСоответствуетОтбору = Истина;
	
	КоличествоВНаличии  = 0;
	КоличествоИзлишек   = 0;
	КоличествоНедостача = 0;
	НоваяСтрокаПродукции = Неопределено;
	Для Каждого СтрокаРасхождений Из ТаблицаРасхождений Цикл
		Если СтрокаРасхождений.Номенклатура <> ТекНоменклатура
			Или СтрокаРасхождений.Характеристика <> ТекХарактеристика Тогда
			ТекНоменклатура = СтрокаРасхождений.Номенклатура;
			ТекХарактеристика = СтрокаРасхождений.Характеристика;
			НоваяГруппировка = Истина;
			Если НоваяСтрокаПродукции <> Неопределено Тогда
				НоваяСтрокаПродукции.КоличествоПодчиненныхВНаличии = КоличествоВНаличии;
				НоваяСтрокаПродукции.КоличествоПодчиненныхОтсутствует = КоличествоНедостача;
				НоваяСтрокаПродукции.КоличествоПодчиненныхНеЧислилось = КоличествоИзлишек;
				НоваяСтрокаПродукции.НеСоответствуетОтбору  = ГруппировкаНеСоответствуетОтбору;
			КонецЕсли;
		КонецЕсли;
		Если НоваяГруппировка Тогда 
			ГруппировкаНеСоответствуетОтбору = Истина;
			
			КоличествоВНаличии  = 0;
			КоличествоИзлишек   = 0;
			КоличествоНедостача = 0;
			
			НоваяСтрокаПродукции = ДеревоРезультатовЭДО.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаПродукции,СтрокаРасхождений); 
			НоваяСтрокаПродукции.ИндексКартинкиСтатусПроверки = 5;
			НоваяСтрокаПродукции.СтатусПроверки = Перечисления.СтатусыПроверкиНаличияПродукцииИС.ПустаяСсылка();
			НоваяСтрокаПродукции.Представление = ИнтеграцияИС.ПредставлениеНоменклатуры(
				СтрокаРасхождений.Номенклатура, СтрокаРасхождений.Характеристика);
			
			НоваяГруппировка = Ложь;
		КонецЕсли;
		
		Если СтрокаРасхождений.КоличествоВНаличии + СтрокаРасхождений.КоличествоИзлишек + СтрокаРасхождений.КоличествоНедостача > 0 Тогда
			
			НоваяСтрокаШтрихкод = НоваяСтрокаПродукции.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаШтрихкод,СтрокаРасхождений); 
			Если СтрокаРасхождений.ТипРасхождения = ИнтеграцияИСМП.ТипРасхожденияИСМПИзлишек() Тогда
				ИндексКартинкиЭДО = 4;
				СтатусПроверки = Перечисления.СтатусыПроверкиНаличияПродукцииИС.НеЧислилась;
				ГруппировкаНеСоответствуетОтбору = Ложь;
				НоваяСтрокаШтрихкод.НеСоответствуетОтбору = Ложь;
			ИначеЕсли СтрокаРасхождений.ТипРасхождения = ИнтеграцияИСМП.ТипРасхожденияИСМПНедостача()
				Или СтрокаРасхождений.ТипРасхождения = ИнтеграцияИСМП.ТипРасхожденияИСМПБрак() Тогда
				ИндексКартинкиЭДО = 1;
				СтатусПроверки = Перечисления.СтатусыПроверкиНаличияПродукцииИС.Отсутствует;
				ГруппировкаНеСоответствуетОтбору = Ложь;
				НоваяСтрокаШтрихкод.НеСоответствуетОтбору = Ложь;
				Если СтрокаРасхождений.ТипРасхождения = ИнтеграцияИСМП.ТипРасхожденияИСМПБрак() Тогда
					НоваяСтрокаШтрихкод.НедопустимыйКодМаркировки = Истина;
				КонецЕсли;
			Иначе
				СтатусПроверки = Перечисления.СтатусыПроверкиНаличияПродукцииИС.ВНаличии;
				ИндексКартинкиЭДО = 0;
				НоваяСтрокаШтрихкод.НеСоответствуетОтбору = Истина;
			КонецЕсли;
			
			Если СтрокаРасхождений.ТипРасхождения = ИнтеграцияИСМП.ТипРасхожденияИСМПНедостача()
				Или СтрокаРасхождений.ТипРасхождения = ИнтеграцияИСМП.ТипРасхожденияИСМПБрак() Тогда
				КоличествоНедостача = КоличествоНедостача + СтрокаРасхождений.КоличествоНедостача;
				НоваяСтрокаШтрихкод.КоличествоПодчиненныхОтсутствует = СтрокаРасхождений.КоличествоНедостача;
			Иначе
				КоличествоВНаличии = КоличествоВНаличии + СтрокаРасхождений.КоличествоВНаличии;
				НоваяСтрокаШтрихкод.КоличествоПодчиненныхВНаличии = СтрокаРасхождений.КоличествоВНаличии;
				
				КоличествоИзлишек = КоличествоИзлишек + СтрокаРасхождений.КоличествоИзлишек; 
				НоваяСтрокаШтрихкод.КоличествоПодчиненныхНеЧислилось = СтрокаРасхождений.КоличествоИзлишек;
			КонецЕсли;
			
			НоваяСтрокаШтрихкод.ИндексКартинкиСтатусПроверки = ИндексКартинкиЭДО;
			НоваяСтрокаШтрихкод.Представление = СокрЛП(СтрокаРасхождений.ЗначениеШтрихкода);
			НоваяСтрокаШтрихкод.СтатусПроверки = СтатусПроверки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НоваяСтрокаПродукции <> Неопределено Тогда
		НоваяСтрокаПродукции.КоличествоПодчиненныхВНаличии    = КоличествоВНаличии;
		НоваяСтрокаПродукции.КоличествоПодчиненныхОтсутствует = КоличествоНедостача;
		НоваяСтрокаПродукции.КоличествоПодчиненныхНеЧислилось = КоличествоИзлишек;
		НоваяСтрокаПродукции.НеСоответствуетОтбору            = ГруппировкаНеСоответствуетОтбору;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДополнитьСоответствиеШтрихкодовСтрокДереваДляКоллекции(КоллекцияСтрок, СоответствиеШтрихкодовСтрокДерева)
	
	Для Каждого СтрокаДерева Из КоллекцияСтрок Цикл
		
		Если СтрокаДерева.ТипУпаковки <> ПредопределенноеЗначение("Перечисление.ПрочиеЗоныПересчетаПродукцииИСМП.ПродукцияБезУпаковки") Тогда
			Если СтрокаДерева.НеСоответствуетОтборуВсе Тогда
				СоответствиеШтрихкодовСтрокДерева.Вставить(СтрокаДерева.Штрихкод + "ЭДО", СтрокаДерева.ПолучитьИдентификатор());
			Иначе
				СоответствиеШтрихкодовСтрокДерева.Вставить(СтрокаДерева.Штрихкод, СтрокаДерева.ПолучитьИдентификатор());
			КонецЕсли;
		КонецЕсли;
			
		ДополнитьСоответствиеШтрихкодовСтрокДереваДляКоллекции(СтрокаДерева.ПолучитьЭлементы(), СоответствиеШтрихкодовСтрокДерева);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеДокумента()
	
	ДлительнаяОперация = НачатьЗагрузкуДанныхДокумента(ПараметрыСканированияКодовМаркировки());
	
	Если ДлительнаяОперация <> Неопределено Тогда
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузкаДанныхДокументаЗавершение", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Получение структуры упаковок'");
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		
	Иначе
		
		ДетализацияСтруктурыХранения = СохраненнаяДетализацияСтруктурыХранения(ЭтоПродукцияМОТП, Истина);
		Если Не ЗначениеЗаполнено(ДетализацияСтруктурыХранения) Тогда
			Если ЭтоПродукцияМОТП Тогда
				ДетализацияСтруктурыХранения = ПредопределенноеЗначение("Перечисление.ДетализацияСтруктурыХраненияИС.Полная");
			Иначе
				ДетализацияСтруктурыХранения = ПредопределенноеЗначение("Перечисление.ДетализацияСтруктурыХраненияИС.Полная");
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоПродукцияМОТП И
			ДетализацияСтруктурыХранения <> ПредопределенноеЗначение("Перечисление.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки") Тогда
			ДобавленнаяСтрокаПачкиБезБлокаНаКлиенте();
			Если ДетализацияСтруктурыХранения <> ПредопределенноеЗначение("Перечисление.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими") Тогда
				ДобавленнаяСтрокаБлокиБезКоробкиНаКлиенте();
			КонецЕсли;
		ИначеЕсли ДетализацияСтруктурыХранения <> ПредопределенноеЗначение("Перечисление.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковки") Тогда
			ДобавленнаяСтрокаПродукцияБезУпаковкиНаКлиенте();
		КонецЕсли;
		
		Результат = Новый Структура();
		Результат.Вставить("Статус",          "Выполнено");
		Результат.Вставить("АдресРезультата", "");
		
		ЗагрузкаДанныхДокументаЗавершение(Результат, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НачатьЗагрузкуДанныхДокумента(ПараметрыСканирования)
	
	ВыполнитьДлительнуюОперацию = Истина;
	
	ТаблицаМаркируемойПродукции = СверкаКодовМаркировкиИСМП.ТаблицаМаркируемойПродукцииДокумента(ПроверяемыйДокумент);
	ТаблицаМаркируемойПродукции.Индексы.Добавить("ВидПродукции");
	
	Если ТаблицаМаркируемойПродукции.Количество() = 0 Тогда
		ВыполнитьДлительнуюОперацию = Ложь;
	КонецЕсли;
	
	Если Не ВыполнитьДлительнуюОперацию Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ВыполнитьДлительнуюОперацию Тогда
		ПараметрыПроцедуры = Новый Структура;
		ПараметрыПроцедуры.Вставить("ПроверяемыйДокумент",              ПроверяемыйДокумент);
		ПараметрыПроцедуры.Вставить("НачальныйСтатусПроверки",          НачальныйСтатусПроверки);
		ПараметрыПроцедуры.Вставить("ДетализацияСтруктурыХранения",     ПредопределенноеЗначение("Перечисление.ДетализацияСтруктурыХраненияИС.Полная"));	
		ПараметрыПроцедуры.Вставить("ДоступноСогласованиеРасхождений",  ДоступноСогласованиеРасхождений);
		ПараметрыПроцедуры.Вставить("ПараметрыСканирования",            ПараметрыСканирования);
		ПараметрыПроцедуры.Вставить("ПроверкаЭлектронногоДокумента",    ПроверкаЭлектронногоДокумента);
		ПараметрыПроцедуры.Вставить("ПараметрыПроверкиКодовМаркировки", ПараметрыСверкиКодовМаркировки);
		ПараметрыПроцедуры.Вставить("ТаблицаМаркируемойПродукции", 		ТаблицаМаркируемойПродукции);
		ПараметрыПроцедуры.Вставить("СверкаПоДаннымКорректировки",      СверкаПоДаннымКорректировки);
		
		Если Не ПараметрыСверкиКодовМаркировки.ОтсутствуетПодключениеИСМП Тогда
			ПараметрыПроцедуры.Вставить("ДанныеКлючаСессииИСМП", ПараметрыСеанса.ДанныеКлючаСессииИСМП);
		КонецЕсли;
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ЭтотОбъект.УникальныйИдентификатор);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Фоновое заполнение данных сверки кодов маркировки'");
		ПараметрыВыполнения.ЗапуститьВФоне = Истина;
		
		ИнтеграцияИСПереопределяемый.НастроитьДлительнуюОперацию(ПараметрыПроцедуры, ПараметрыВыполнения);
		
		Возврат ДлительныеОперации.ВыполнитьВФоне(
			"Обработки.РезультатыСверкиКодовМаркировкиТОРГ2.ЗагрузитьДанныеДокументаДлительнаяОперация",
			ПараметрыПроцедуры, ПараметрыВыполнения);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузкаДанныхДокументаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда  // отменено пользователем
		ЗакрытьФорму();
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(Новый ОписаниеОповещения("ЗакрытьФорму", ЭтотОбъект), Результат.КраткоеПредставлениеОшибки);
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		ЗагрузкаДанныхДокументаЗавершениеНаСервере(Результат.АдресРезультата);
		СоответствиеШтрихкодовСтрокДерева = СоответствиеШтрихкодовСтрокДерева(АдресСоответствиеШтрихкодыИдентификаторыСтрок);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузкаДанныхДокументаЗавершениеНаСервере(АдресДанныхДокумента)
	
	Если ЭтоАдресВременногоХранилища(АдресДанныхДокумента) Тогда
		ДанныеДокумента = ПолучитьИзВременногоХранилища(АдресДанныхДокумента);
	Иначе
		ДанныеДокумента = Новый Структура();
	КонецЕсли;
	
	Если ТипЗнч(ДанныеДокумента) = Тип("Структура") Тогда
		ЭтоПродукцияМОТП = ДанныеДокумента.Свойство("ЭтоПродукцияМОТП") И ДанныеДокумента.ЭтоПродукцияМОТП;
		ВывестиДанныеПроверкиИПодбораНаФорму(ДанныеДокумента);
		ВывестиРезультатыПроверкиЭДО();
		РассчитатьИтогиУстановитьВидимость();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтогиУстановитьВидимость()
	
	ПересчитатьВсеИтогиФормыНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область УстановкаОтбораСкрытьНоменклатуру

&НаКлиентеНаСервереБезКонтекста
Процедура СкрытьСтрокиДереваСОтборомПоНоменклатуре(Форма)
	
	Если Форма.СохраненВыборПоМаркируемойПродукции Тогда
		
		ТекущиеДанные = Форма.Элементы.ДеревоМаркированнойПродукции.ТекущиеДанные;
		СтрокиДерева  = Форма.ДеревоМаркированнойПродукции.ПолучитьЭлементы();
		
		Для Каждого СтрокаДерева Из СтрокиДерева Цикл
			
			СоответствуетОтбору = Ложь;
			СкрытьСОтборомПоНоменклатуреВСтрокеДерева(Форма, СтрокаДерева, Форма.ДанныеВыбораПоМаркируемойПродукции, СоответствуетОтбору);
			
		КонецЦикла;
		
		Если ТекущиеДанные = Неопределено Или ТекущиеДанные.НеСоответствуетОтбору Тогда
			Для Каждого СтрокаДерева Из СтрокиДерева Цикл
				Если Не СтрокаДерева.НеСоответствуетОтборуНоменклатура Тогда
					Форма.Элементы.ДеревоМаркированнойПродукции.ТекущаяСтрока = СтрокаДерева.ПолучитьИдентификатор();
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СкрытьСОтборомПоНоменклатуреВСтрокеДерева(Форма, СтрокаДерева, ПараметрыОтбора, СоответствуетОтбору)
	
	ПодчиненныеСтроки = СтрокаДерева.ПолучитьЭлементы();
	
	ТекущаяСтрокаСоответствуетОтбору = Ложь;
	
	Для Каждого ПодчиненнаяСтрока Из ПодчиненныеСтроки Цикл
		
		СоответствуетОтбору = Ложь;
		
		СкрытьСОтборомПоНоменклатуреВСтрокеДерева(Форма, ПодчиненнаяСтрока, ПараметрыОтбора, СоответствуетОтбору);
		
		Если СоответствуетОтбору Тогда
			ТекущаяСтрокаСоответствуетОтбору = Истина;
		КонецЕсли;
		
	КонецЦикла;

	Если Не ТекущаяСтрокаСоответствуетОтбору Тогда
		
		Если (СтрокаДерева.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Потребительская")
			Или СтрокаДерева.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Групповая"))
			И СтрокаДерева.Номенклатура = ПараметрыОтбора.Номенклатура 
			И СтрокаДерева.Характеристика = ПараметрыОтбора.Характеристика Тогда
			ТекущаяСтрокаСоответствуетОтбору = Истина;
		Иначе
			ТекущаяСтрокаСоответствуетОтбору = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	СоответствуетОтбору = ТекущаяСтрокаСоответствуетОтбору Или СоответствуетОтбору;
	СтрокаДерева.НеСоответствуетОтборуНоменклатура = Не ТекущаяСтрокаСоответствуетОтбору;
	
КонецПроцедуры

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Процедура ИнициализироватьДанныеВыбораПоМаркируемойПродукции(ДанныеВыбораПоМаркируемойПродукции)
	
	ДанныеВыбораПоМаркируемойПродукции = Новый Структура;
	ДанныеВыбораПоМаркируемойПродукции.Вставить("Номенклатура",   Неопределено);
	ДанныеВыбораПоМаркируемойПродукции.Вставить("Характеристика", Неопределено);
	ДанныеВыбораПоМаркируемойПродукции.Вставить("Представление",  "");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредставлениеФильтраПоМаркируемойПродукции(Элемент)
	
	ОбновитьДанныеВыбораПоМаркируемойПродукции();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеВыбораПоМаркируемойПродукции()
	
	ТекущиеДанные = Элементы.ДеревоМаркированнойПродукции.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) 
		И ЗначениеЗаполнено(ДанныеВыбораПоМаркируемойПродукции.Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбораПоМаркируемойПродукции.Номенклатура = ТекущиеДанные.Номенклатура;
	ДанныеВыбораПоМаркируемойПродукции.Характеристика = ТекущиеДанные.Характеристика;
	
	СверкаКодовМаркировкиИСМПКлиентСервер.ОтобразитьФильтрПоМаркируемойПродукции(ЭтотОбъект, ДанныеВыбораПоМаркируемойПродукции);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТекущуюСтрокуДереваПоОтборам(Форма)
	
	ТекущиеДанные = Форма.Элементы.ДеревоМаркированнойПродукции.ТекущиеДанные;
	СтрокиДерева  = Форма.ДеревоМаркированнойПродукции.ПолучитьЭлементы();
	
	Если ОтображатьРезультатыФизПроверки = "Все" Тогда
		КолонкаОтбора = "НеСоответствуетОтборуВсе";
		КолонкаОтбораДополнительная = КолонкаОтбора;
	ИначеЕсли ОтображатьРезультатыФизПроверки = "ФактическиеРасхождения" Тогда
		КолонкаОтбора = "НеСоответствуетОтбору";
		КолонкаОтбораДополнительная = "НеСоответствуетОтборуВсе";
	ИначеЕсли ОтображатьРезультатыФизПроверки = "ЭДОРасхождения" Тогда
		КолонкаОтбора = "НеСоответствуетОтборуЭДО";
		КолонкаОтбораДополнительная = КолонкаОтбора;
	КонецЕсли;
	Если ТекущиеДанные = Неопределено
		 Или ТекущиеДанные[КолонкаОтбора]
		 Или ТекущиеДанные[КолонкаОтбораДополнительная] Тогда
			Для Каждого СтрокаДерева Из СтрокиДерева Цикл
				Если НЕ СтрокаДерева[КолонкаОтбора] Тогда
					Форма.Элементы.ДеревоМаркированнойПродукции.ТекущаяСтрока = СтрокаДерева.ПолучитьИдентификатор();
					Прервать;
				КонецЕсли;
			КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#Область ПересчетИтогов

&НаСервере
Процедура ПересчитатьВсеИтогиФормыНаСервере()
	
	ПересчитатьВсеИтогиФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПересчитатьВсеИтогиФормы(Форма)
	
	СверкаКодовМаркировкиИСМПКлиентСервер.ПересчитатьИтогиПоПроверкеСодержимогоУпаковок(Форма.ДеревоМаркированнойПродукции, Форма.ОтображатьРезультатыФизПроверки, Форма.ЭтоПродукцияМОТП);
	
	ШтрихкодированиеИСКлиентСервер.ОтобразитьСохраненныйВыборПоМаркируемойПродукции(Форма);
	
	Если Не Форма.СохраненВыборПоМаркируемойПродукции Тогда
		ИнициализироватьДанныеВыбораПоМаркируемойПродукции(Форма.ДанныеВыбораПоМаркируемойПродукции);
		СверкаКодовМаркировкиИСМПКлиентСервер.ОтобразитьФильтрПоМаркируемойПродукции(Форма, Форма.ДанныеВыбораПоМаркируемойПродукции);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Функция ПараметрыСканированияКодовМаркировки()
	
	ПараметрыСканирования = ШтрихкодированиеИСКлиент.ПараметрыСканирования(
		ВладелецФормы, Неопределено);
	
	ПараметрыСканирования.СоздаватьШтрихкодУпаковки                          = Ложь;
	ПараметрыСканирования.КэшМаркируемойПродукции                            = Неопределено;
	ПараметрыСканирования.ДанныеВыбораПоМаркируемойПродукции                 = ДанныеВыбораПоМаркируемойПродукции;
	ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции     = Истина;
	ПараметрыСканирования.ПроверятьДублиКодовМаркировки                      = Истина;
	ПараметрыСканирования.ВозможнаЗагрузкаТСД                                = Ложь;
	ПараметрыСканирования.ИспользуетсяСоответствиеШтрихкодовСтрокДерева      = Истина;
	ПараметрыСканирования.ДоступнаПечатьЭтикеток                             = Ложь;
	ПараметрыСканирования.КонтрольУникальностиКодовМаркировки                = Ложь;
	ПараметрыСканирования.КонтрольПустыхУпаковок                             = Ложь;
	ПараметрыСканирования.СсылкаНаОбъект                                     = ПроверяемыйДокумент;
	ПараметрыСканирования.РазрешенаОбработкаНеНайденныхЛогистическихУпаковок = Истина;
	ПараметрыСканирования.ОтборПоВидуПродукции = Ложь;
	
	ПараметрыСканирования.ЗапрашиватьДанныеНеизвестныхУпаковокИСМП = Истина;
	ПараметрыСканирования.ЗапрашиватьДанныеСервисаИСМП             = Истина;
	ПараметрыСканирования.КонтролироватьРасхождениеСоставаУпаковокМеждуИБиИСМП = Ложь;
	ПараметрыСканирования.Владелец                                 = Неопределено;
	
	Если ПараметрыСверкиКодовМаркировки.ОтсутствуетПодключениеИСМП Тогда
		ПараметрыСканирования.ЗапрашиватьДанныеНеизвестныхУпаковокИСМП = Ложь;
		ПараметрыСканирования.КонтролироватьРасхождениеСоставаУпаковокМеждуИБиИСМП = Ложь;
		ПараметрыСканирования.ЗапрашиватьДанныеСервисаИСМП             = Ложь;
	КонецЕсли;
	
	Возврат ПараметрыСканирования;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НайтиПоИдентификатору(ДанныеФормыДерево, Идентификатор)
	
	// Сейчас есть проблема в веб-клиенте: метод НайтиПоИдентификатору, может вернуть ДанныеФормыДерево
	
	Если Идентификатор = -1 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеФормыЭлементДерева = ДанныеФормыДерево.НайтиПоИдентификатору(Идентификатор);
	
	Если ТипЗнч(ДанныеФормыЭлементДерева) = Тип("ДанныеФормыЭлементДерева") Тогда
		Возврат ДанныеФормыЭлементДерева;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ЗакрытьФорму(ДополнительныеПараметры = Неопределено) Экспорт
	
	Закрыть();
	
КонецПроцедуры

#Область РезультатыПроверки

&НаКлиенте
Процедура ЗавершениеПроверкиОкончание(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда  // отменено пользователем
		
		Возврат;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		ПоказатьПредупреждение(, Результат.КраткоеПредставлениеОшибки);
		
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		
		Сообщить("Успешно");
		ЗакрытьФорму();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НачатьЗавершениеПроверки(ПараметрыОкончанияПроверки)
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ПроверяемыйДокумент",          ПроверяемыйДокумент);
	ПараметрыПроцедуры.Вставить("ДеревоМаркированнойПродукции",  РеквизитФормыВЗначение("ДеревоМаркированнойПродукции"));
	ПараметрыПроцедуры.Вставить("ЭтоПродукцияМОТП", ЭтоПродукцияМОТП);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ЭтотОбъект.УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Перенос результатов сверки кодов маркировки в документ'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ИнтеграцияИСПереопределяемый.НастроитьДлительнуюОперацию(ПараметрыПроцедуры, ПараметрыВыполнения);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.РезультатыСверкиКодовМаркировкиТОРГ2.ЗафиксироватьРезультатПроверкиИПодбора",
		ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти

#Область ДействиеПриРасхождениях

&НаКлиенте
Процедура УстановитьДействиеДляВыделенныхСтрок(ВыделенныеСтроки, НовоеДействие)
	
	КоличествоКОбработке = ВыделенныеСтроки.Количество();
	КоличествоОбработанныхСтрок = 0;
	
	Для Каждого ИдентификаторВыделеннойСтроки Из ВыделенныеСтроки Цикл
		
		ДанныеСтроки = НайтиПоИдентификатору(ДеревоМаркированнойПродукции, ИдентификаторВыделеннойСтроки);
		
		Если ДанныеСтроки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
				
		Если ДанныеСтроки.ДействиеПоРасхождениям = НовоеДействие Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДанныеСтроки.ТипУпаковки = ПредопределенноеЗначение("Перечисление.ПрочиеЗоныПересчетаПродукцииИСМП.ПродукцияБезУпаковки")
			Или ДанныеСтроки.ТипУпаковки = ПредопределенноеЗначение("Перечисление.ПрочиеЗоныПересчетаПродукцииИСМП.ПачкиБезБлока")
			Или ДанныеСтроки.ТипУпаковки = ПредопределенноеЗначение("Перечисление.ПрочиеЗоныПересчетаПродукцииИСМП.БлокиБезКоробки") Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДанныеСтроки.СтатусПроверки <> ПредопределенноеЗначение("Перечисление.СтатусыПроверкиНаличияПродукцииИС.НеЧислилась") 
			И ДанныеСтроки.СтатусПроверки <> ПредопределенноеЗначение("Перечисление.СтатусыПроверкиНаличияПродукцииИС.Отсутствует") Тогда 
			Продолжить;
		КонецЕсли;
		
		УстановитьДействиеДляПодчиненных(ЭтотОбъект, ДанныеСтроки, НовоеДействие);
		
		ДанныеСтроки.ДействиеПоРасхождениям = НовоеДействие;
		
		КоличествоОбработанныхСтрок = КоличествоОбработанныхСтрок + 1;
		
	КонецЦикла;
	
	ОповеститьПользователяОИзмененииДействияПоРасхождениям(НовоеДействие, КоличествоОбработанныхСтрок, КоличествоКОбработке);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДействиеДляПодчиненных(Форма, СтрокаДерева, НовоеДействие)
	 
	Для Каждого ПодчиненнаяСтрока Из СтрокаДерева.ПолучитьЭлементы() Цикл
		Если ПодчиненнаяСтрока.СтатусПроверки = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиНаличияПродукцииИС.НеЧислилась") 
			Или ПодчиненнаяСтрока.СтатусПроверки = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиНаличияПродукцииИС.Отсутствует") Тогда 
			ПодчиненнаяСтрока.ДействиеПоРасхождениям = НовоеДействие;
		КонецЕсли;
		
		Если ПодчиненнаяСтрока.ТипУпаковки <> ПредопределенноеЗначение("Перечисление.ТипыУпаковок.МаркированныйТовар") Тогда
			УстановитьДействиеДляПодчиненных(Форма, ПодчиненнаяСтрока, НовоеДействие);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьПользователяОИзмененииДействияПоРасхождениям(НовоеДействие, КоличествоОбработанных, КоличествоВсего)
	
	ШаблонЗаголовкаОбработано   = НСтр("ru = 'Действие по расхождениям ""%1"" установлено'");
	ШаблонСообщенияОбработано   = НСтр("ru = 'Для %1 из %2 выделенных в списке строк установлено действие по расхождениям ""%3""'");
	ШаблонЗаголовкаНеОбработано = НСтр("ru = 'Действие по расхождениям ""%1"" не установлено'");
	ШаблонСообщенияНеОбработано = НСтр("ru = 'Действие по расхождениям ""%1"" не установлено ни для одной строки'");
	
	Если КоличествоОбработанных > 0 Тогда
		
		ТекстЗаголовка = СтрШаблон(ШаблонЗаголовкаОбработано, НовоеДействие);
		ТекстСообщения = СтрШаблон(
			ШаблонСообщенияОбработано,
			КоличествоОбработанных,
			КоличествоВсего,
			НовоеДействие);
		
	Иначе
		
		ТекстЗаголовка = СтрШаблон(ШаблонЗаголовкаНеОбработано, НовоеДействие);
		ТекстСообщения = СтрШаблон(ШаблонСообщенияНеОбработано, НовоеДействие);
		
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Функция ДетализацияСтруктурыХраненияДерева(ДеревоУпаковок, ЭтоПродукцияМОТП)
	Если ЭтоПродукцияМОТП Тогда
		Возврат Обработки.ПроверкаИПодборТабачнойПродукцииМОТП.ДетализацияСтруктурыХраненияДерева(ДеревоУпаковок);
	Иначе
		Возврат Обработки.ПроверкаИПодборПродукцииИСМП.ДетализацияСтруктурыХраненияДерева(ДеревоУпаковок);
	КонецЕсли;
КонецФункции

#КонецОбласти
