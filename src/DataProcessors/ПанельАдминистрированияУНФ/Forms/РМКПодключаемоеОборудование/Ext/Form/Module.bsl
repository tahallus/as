
#Область ОписаниеПеременных

&НаКлиенте
Перем ОбновитьИнтерфейс;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РежимРаботы = Новый ФиксированнаяСтруктура(УправлениеНебольшойФирмойПовтИсп.РежимРаботыПрограммы());
	
	Элементы.ГруппаИспользоватьОбменСКассойMinikassir.Видимость = РежимРаботы.МодельСервиса
		И НаборКонстант.ИспользоватьОбменССерверомШтрихМ;
	
	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФункциональнаяОпцияИспользоватьПодключаемоеОборудованиеПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОбменСПодключаемымОборудованиемOfflineПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОбменСКассойMinikassirПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодробнееОПодключенииКассКФискальномуРегистраторуНажатие(Элемент)
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("http://v8.1c.ru/cnt.jsp/:kd_unf:/https://portal.1c.ru/applications/56");
КонецПроцедуры

&НаКлиенте
Процедура ПодробнееО54ФЗНажатие(Элемент)
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("http://v8.1c.ru/cnt.jsp/:kd_unf:/http://v8.1c.ru/kkt/");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкаДиапазоновSKUВесовогоТовара(Команда)
	ОткрытьФорму("ОбщаяФорма.НастройкаДиапазоновSKU");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СтандартноеСохранениеЗначенийКонстант

////////////////////////////////////////////////////////////////////////////////
// Клиент

&НаКлиенте
Процедура Подключаемый_ПриИзмененииРеквизита(Элемент, ОбновлятьИнтерфейс = Истина)
	
	ИмяКонстанты = ПриИзмененииРеквизитаСервер(Элемент.Имя);
	ОбновитьПовторноИспользуемыеЗначения();
	
	Если ОбновлятьИнтерфейс Тогда
		ОбновитьИнтерфейс = Истина;
		ПодключитьОбработчикОжидания("ОбновитьИнтерфейсПрограммы", 2, Истина);
	КонецЕсли;
	
	Если ИмяКонстанты <> "" Тогда
		Оповестить("Запись_НаборКонстант", Новый Структура, ИмяКонстанты);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнтерфейсПрограммы()
	
	Если ОбновитьИнтерфейс = Истина Тогда
		ОбновитьИнтерфейс = Ложь;
		ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Вызов сервера

&НаСервере
Функция ПриИзмененииРеквизитаСервер(ИмяЭлемента)
	
	РеквизитПутьКДанным = Элементы[ИмяЭлемента].ПутьКДанным;
	ИмяКонстанты = СохранитьЗначениеРеквизита(РеквизитПутьКДанным);
	УстановитьДоступность(РеквизитПутьКДанным);
	ОбновитьПовторноИспользуемыеЗначения();
	Возврат ИмяКонстанты;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Сервер

&НаСервере
Функция СохранитьЗначениеРеквизита(РеквизитПутьКДанным)
	
	// Сохранение значений реквизитов, не связанных с константами напрямую (в отношении один-к-одному).
	
	ЧастиИмени = СтрРазделить(РеквизитПутьКДанным, ".");
	Если ЧастиИмени.Количество() <> 2 Тогда
		Возврат "";
	КонецЕсли;
	
	ИмяКонстанты = ЧастиИмени[1];
	КонстантаМенеджер = Константы[ИмяКонстанты];
	НовоеЗначение = НаборКонстант[ИмяКонстанты];
	СтароеЗначение = КонстантаМенеджер.Получить();
	
	Если СтароеЗначение <> НовоеЗначение Тогда
		
		ТекстОшибки = ПриПроверкеВозможностиУстановкиЗначенияКонстанты(РеквизитПутьКДанным, НовоеЗначение);
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , РеквизитПутьКДанным);
			НаборКонстант[ИмяКонстанты] = СтароеЗначение;
		Иначе
			КонстантаМенеджер.Установить(НовоеЗначение);
			ПриУстановкеЗначенияКонстанты(РеквизитПутьКДанным, НовоеЗначение);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИмяКонстанты;
	
КонецФункции

#КонецОбласти

&НаСервере
Функция ПриПроверкеВозможностиУстановкиЗначенияКонстанты(РеквизитПутьКДанным, НовоеЗначение)
	
	ТекстОшибки = "";
	
	Если РеквизитПутьКДанным = "НаборКонстант.ФункциональнаяОпцияИспользоватьПодключаемоеОборудование" Тогда
		
		Если Не НовоеЗначение И Константы.ФункциональнаяОпцияИспользоватьПодключаемоеОборудование.Получить() Тогда
			ТекстОшибки = НастройкиУНФ.ОтказСнятьФункциональнаяОпцияИспользоватьПодключаемоеОборудование();
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции

&НаСервере
Процедура ПриУстановкеЗначенияКонстанты(РеквизитПутьКДанным, НовоеЗначение)
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьОбменСКассойMinikassir"
		И НовоеЗначение Тогда
		Константы.ФункциональнаяОпцияУчетРозничныхПродаж.Установить(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступность(РеквизитПутьКДанным = "")
	
	Если РеквизитПутьКДанным = "НаборКонстант.ФункциональнаяОпцияИспользоватьПодключаемоеОборудование"
		ИЛИ РеквизитПутьКДанным = "" Тогда
		Элементы.ГруппаНастройкиПодключаемогоОборудования.Доступность =
			НаборКонстант.ФункциональнаяОпцияИспользоватьПодключаемоеОборудование;
		Элементы.ГруппаИспользоватьОбменСПодключаемымОборудованиемOffline.Доступность =
			НаборКонстант.ФункциональнаяОпцияИспользоватьПодключаемоеОборудование;
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ФункциональнаяОпцияИспользоватьПодключаемоеОборудование"
		Или РеквизитПутьКДанным = "НаборКонстант.ИспользоватьОбменСПодключаемымОборудованиемOffline"
		Или РеквизитПутьКДанным = "" Тогда
		Элементы.ГруппаНастройкиПодключаемогоОборудованияOffline.Доступность =
			НаборКонстант.ФункциональнаяОпцияИспользоватьПодключаемоеОборудование
			И НаборКонстант.ИспользоватьОбменСПодключаемымОборудованиемOffline;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
