
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПолучитьДеревоВидовЦен(ДеревоРезультата, МассивВидовЦен, ВключаяЦеныНоменклатуры = Истина, ВключаяЦеныКонтрагентов = Истина) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВидыЦен.Ссылка КАК ВидЦен,
	|	НЕОПРЕДЕЛЕНО КАК Использование,
	|	ПРЕДСТАВЛЕНИЕ(ВидыЦен.Ссылка) КАК Представление,
	|	ИСТИНА КАК ЭтоВидЦенНоменклатуры,
	|	0 КАК Картинка,
	|	ЛОЖЬ КАК РазрешитьВыбирать,
	|	ВидыЦен.БазовыйВидЦен КАК БазовыйВидЦен,
	|	ВЫБОР
	|		КОГДА ВидыЦен.ТипВидаЦен = ЗНАЧЕНИЕ(Перечисление.ТипыВидовЦен.ДинамическийФормула)
	|			ТОГДА ""Формула""
	|		КОГДА ВидыЦен.ТипВидаЦен = ЗНАЧЕНИЕ(Перечисление.ТипыВидовЦен.ДинамическийПроцент)
	|			ТОГДА ""Динамический""
	|		ИНАЧЕ ""Статический""
	|	КОНЕЦ КАК Вид,
	|	ВидыЦен.ИдентификаторФормул КАК ИдентификаторФормул
	|ИЗ
	|	Справочник.ВидыЦен КАК ВидыЦен
	|ГДЕ
	|	НЕ ВидыЦен.ТипВидаЦен = ЗНАЧЕНИЕ(Перечисление.ТипыВидовЦен.Статический)
	|ИТОГИ ПО
	|	БазовыйВидЦен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВидыЦен.Ссылка КАК ВидЦен,
	|	ВЫБОР
	|		КОГДА ВидыЦен.Ссылка В (&МассивВидовЦен)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Использование,
	|	ПРЕДСТАВЛЕНИЕ(ВидыЦен.Ссылка) КАК Представление,
	|	ИСТИНА КАК ЭтоВидЦенНоменклатуры,
	|	0 КАК Картинка,
	|	ИСТИНА КАК РазрешитьВыбирать,
	|	ВЫРАЗИТЬ(""Статический"" КАК СТРОКА(15)) КАК Вид,
	|	ВидыЦен.ИдентификаторФормул КАК ИдентификаторФормул
	|ИЗ
	|	Справочник.ВидыЦен КАК ВидыЦен
	|ГДЕ
	|	ВидыЦен.ТипВидаЦен = ЗНАЧЕНИЕ(Перечисление.ТипыВидовЦен.Статический)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыЦенКонтрагентов.Ссылка,
	|	ВЫБОР
	|		КОГДА ВидыЦенКонтрагентов.Ссылка В (&МассивВидовЦен)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ПРЕДСТАВЛЕНИЕ(ВидыЦенКонтрагентов.Ссылка),
	|	ЛОЖЬ,
	|	0,
	|	ИСТИНА,
	|	""Статический"",
	|	ВидыЦенКонтрагентов.ИдентификаторФормул
	|ИЗ
	|	Справочник.ВидыЦенКонтрагентов КАК ВидыЦенКонтрагентов
	|ИТОГИ
	|	ВЫБОР
	|		КОГДА ЭтоВидЦенНоменклатуры = ИСТИНА
	|			ТОГДА &ЦеныНоменклатуры
	|		ИНАЧЕ &ЦеныКонтрагентов
	|	КОНЕЦ КАК Представление,
	|	1 КАК Картинка,
	|	ИСТИНА КАК РазрешитьВыбирать
	|ПО
	|	ЭтоВидЦенНоменклатуры");
	
	Запрос.УстановитьПараметр("МассивВидовЦен", МассивВидовЦен);
	Запрос.УстановитьПараметр("ЦеныНоменклатуры", НСтр("ru = 'ЦЕНЫ НОМЕНКЛАТУРЫ'"));
	Запрос.УстановитьПараметр("ЦеныКонтрагентов", НСтр("ru = 'ЦЕНЫ КОНТРАГЕНТОВ'"));
	
	РезультатЗапроса	= Запрос.ВыполнитьПакет();
	ПодчиненныеВидыЦен	= РезультатЗапроса[0].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ДеревоРезультата	= РезультатЗапроса[1].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Для каждого СтрокаГруппы Из ПодчиненныеВидыЦен.Строки Цикл
		
		Если ЗначениеЗаполнено(СтрокаГруппы.БазовыйВидЦен) Тогда
			
			СтрокаДереваЗначений = ДеревоРезультата.Строки.Найти(СтрокаГруппы.БазовыйВидЦен, "ВидЦен", Истина);
			Если ТипЗнч(СтрокаДереваЗначений) = Тип("СтрокаДереваЗначений") Тогда
				
				Для каждого СтрокаВидаЦен Из СтрокаГруппы.Строки Цикл
					
					ПодчиненныйВидЦен = СтрокаДереваЗначений.Строки.Добавить();
					ЗаполнитьЗначенияСвойств(ПодчиненныйВидЦен, СтрокаВидаЦен);
					
					ПодчиненныйВидЦен.Использование = СтрокаДереваЗначений.Использование;
					
				КонецЦикла;
				
			КонецЕсли;
			
		Иначе
			
			// Формулы
			// 1. Получим таблицу видов цен, которые участвуют в формуле
			// 2. Спозиционируемся в дереве видов цен на родительском элементе
			// 3. Запишем в родительские виды цен формульный вид цен как подчиненный
			
			Для каждого СтрокаФормульногоВидаЦен Из СтрокаГруппы.Строки Цикл
				
				ТаблицаОперандов = ЦенообразованиеФормулыСервер.ПолучитьТаблицуОперандовФормулы(ТекущаяДатаСеанса(), СтрокаФормульногоВидаЦен.ВидЦен.Формула);
				Для каждого СтрокаОперанда Из ТаблицаОперандов Цикл
					
					СтрокаДереваЗначений = ДеревоРезультата.Строки.Найти(СтрокаОперанда.ВидЦен, "ВидЦен", Истина);
					Если ТипЗнч(СтрокаДереваЗначений) = Тип("СтрокаДереваЗначений") Тогда
						
						ПодчиненныйВидЦен = СтрокаДереваЗначений.Строки.Добавить();
						ЗаполнитьЗначенияСвойств(ПодчиненныйВидЦен, СтрокаФормульногоВидаЦен);
						
						ПодчиненныйВидЦен.Использование = СтрокаДереваЗначений.Использование;
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВключаяЦеныНоменклатуры = Ложь Тогда
		
		НайденнаяСтрока = ДеревоРезультата.Строки.Найти("ЦЕНЫ НОМЕНКЛАТУРЫ", "Представление", Ложь);
		Если НайденнаяСтрока <> Неопределено Тогда
			
			ДеревоРезультата.Строки.Удалить(НайденнаяСтрока);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВключаяЦеныКонтрагентов = Ложь Тогда
		
		НайденнаяСтрока = ДеревоРезультата.Строки.Найти("ЦЕНЫ КОНТРАГЕНТОВ", "Представление", Ложь);
		Если НайденнаяСтрока <> Неопределено Тогда
			
			ДеревоРезультата.Строки.Удалить(НайденнаяСтрока);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#Область ВводНаОсновании

Процедура ПолучитьНоменклатуруПоПриходнымНакладным(СтруктураПараметров, АдресВременногоХранилища) Экспорт
	
	// 1. Получим СКД
	ИмяСхемыКД = "ПоПриходнымНакладным";
	СхемаКомпоновкиДанных = Обработки.ФормированиеЦенНоменклатуры.ПолучитьМакет(ИмяСхемыКД);
	
	Запрос = Новый Запрос(СхемаКомпоновкиДанных.НаборыДанных.Номенклатура.Запрос);
	Запрос.УстановитьПараметр("МассивПриходныхНакладных", СтруктураПараметров.МассивПриходныхНакладных);
	ТаблицаНоменклатуры = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос(СхемаКомпоновкиДанных.НаборыДанных.ХарактеристикиНоменклатуры.Запрос);
	Запрос.УстановитьПараметр("МассивПриходныхНакладных", СтруктураПараметров.МассивПриходныхНакладных);
	ТаблицаХарактеристик = Запрос.Выполнить().Выгрузить();
	
	Если СтруктураПараметров.ИспользоватьХарактеристики = 0 Тогда
		
		ТаблицаХарактеристик.Очистить();
		
	КонецЕсли;
	
	ЛокальныйКлючСвязи = 0;
	Для каждого СтрокаНоменклатуры Из ТаблицаНоменклатуры Цикл
		
		ЛокальныйКлючСвязи = ЛокальныйКлючСвязи + 1;
		СтрокаНоменклатуры.КлючСвязи = ЛокальныйКлючСвязи;
		
		ПараметрыОтбора = Новый Структура("Номенклатура", СтрокаНоменклатуры.Номенклатура);
		СтрокиХарактеристик = ТаблицаХарактеристик.НайтиСтроки(ПараметрыОтбора);
		Для каждого СтрокаХарактеристики Из СтрокиХарактеристик Цикл
			
			СтрокаХарактеристики.КлючСвязи = ЛокальныйКлючСвязи;
			
		КонецЦикла;
		
	КонецЦикла;
	
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(Новый Структура("ТаблицаНоменклатуры, ТаблицаХарактеристик", ТаблицаНоменклатуры, ТаблицаХарактеристик));
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСФормулами

Процедура ПолучитьТаблицуПоследнихЦенПоступлений(ПараметрыРасчета, ТаблицаСЦенамиПоступления)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаФормыНоменклатуры", ПараметрыРасчета.КоллекцииДанныхФормы.ТаблицаФормыНоменклатуры);
	Запрос.УстановитьПараметр("ТаблицаФормыХарактеристик", ПараметрыРасчета.КоллекцииДанныхФормы.ТаблицаФормыХарактеристик);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаФормыНоменклатуры.Номенклатура КАК Номенклатура
	|	,Значение(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика
	|ПОМЕСТИТЬ ТаблицаНоменклатура
	|ИЗ &ТаблицаФормыНоменклатуры КАК ТаблицаФормыНоменклатуры
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаФормыХарактеристик.Номенклатура КАК Номенклатура
	|	,ТаблицаФормыХарактеристик.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ТаблицаХарактеристики
	|ИЗ &ТаблицаФормыХарактеристик КАК ТаблицаФормыХарактеристик
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатура.Номенклатура КАК Номенклатура
	|	,ТаблицаНоменклатура.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ТаблицаНоменклатураХарактеристики
	|ИЗ ТаблицаНоменклатура КАК ТаблицаНоменклатура
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ
	|	ТаблицаХарактеристики.Номенклатура КАК Номенклатура
	|	,ТаблицаХарактеристики.Характеристика КАК Характеристика
	|ИЗ ТаблицаХарактеристики КАК ТаблицаХарактеристики
	|Индексировать ПО Номенклатура, Характеристика
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПН.Номенклатура КАК Номенклатура
	|	,ПН.Характеристика КАК Характеристика
	|	,Максимум(ПН.Ссылка.Дата) КАК Период
	|ПОМЕСТИТЬ ЗапасыПриходныхНакладныхСПериодами
	|ИЗ
	|	ТаблицаНоменклатураХарактеристики КАК ТаблицаНоменклатураХарактеристики
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходнаяНакладная.Запасы КАК ПН 
	|		ПО ТаблицаНоменклатураХарактеристики.Номенклатура = ПН.Номенклатура
	|			И ТаблицаНоменклатураХарактеристики.Характеристика = ПН.Характеристика
	|			И ПН.Ссылка.Проведен
	|ГДЕ 
	|	НЕ ПН.Номенклатура ЕСТЬ NULL
	|СГРУППИРОВАТЬ ПО
	|	ПН.Номенклатура, ПН.Характеристика
	|Индексировать ПО Номенклатура, Характеристика, Период
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПН.Номенклатура КАК Номенклатура
	|	,Максимум(ПН.Ссылка.Дата) КАК Период
	|ПОМЕСТИТЬ УслугиПриходныхНакладныхСПериодами
	|ИЗ
	|	ТаблицаНоменклатураХарактеристики КАК ТаблицаНоменклатураХарактеристики
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходнаяНакладная.Расходы КАК ПН 
	|		ПО ТаблицаНоменклатураХарактеристики.Номенклатура = ПН.Номенклатура
	|		И ПН.Ссылка.Проведен
	|ГДЕ 
	|	НЕ ПН.Номенклатура ЕСТЬ NULL
	|СГРУППИРОВАТЬ ПО
	|	ПН.Номенклатура
	|Индексировать ПО Номенклатура, Период
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПН.Номенклатура КАК Номенклатура
	|	,ПН.Характеристика КАК Характеристика
	|	,ПН.Период КАК Период
	|	,ЦеныИзНакладных.Цена КАК Значение
	|	,ЦеныИзНакладных.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,ЦеныИзНакладных.Ссылка.Курс КАК Курс
	|	,ЦеныИзНакладных.Ссылка.Кратность КАК Кратность
	|ПОМЕСТИТЬ ЦеныИзНакладныхЗапасы
	|ИЗ ЗапасыПриходныхНакладныхСПериодами КАК ПН
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходнаяНакладная.Запасы КАК ЦеныИзНакладных
	|		ПО ПН.Период = ЦеныИзНакладных.Ссылка.Дата И ПН.Номенклатура = ЦеныИзНакладных.Номенклатура И ПН.Характеристика = ЦеныИзНакладных.Характеристика
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПН.Номенклатура КАК Номенклатура
	|	,Значение(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика
	|	,ПН.Период КАК Период
	|	,ЦеныИзНакладных.Цена КАК Значение
	|	,ЦеныИзНакладных.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,ЦеныИзНакладных.Ссылка.Курс КАК Курс
	|	,ЦеныИзНакладных.Ссылка.Кратность КАК Кратность
	|ПОМЕСТИТЬ ЦеныИзНакладныхУслуги
	|ИЗ УслугиПриходныхНакладныхСПериодами КАК ПН
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходнаяНакладная.Расходы КАК ЦеныИзНакладных
	|		ПО ПН.Период = ЦеныИзНакладных.Ссылка.Дата И ПН.Номенклатура = ЦеныИзНакладных.Номенклатура
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныИзНакладныхЗапасы.Номенклатура КАК Номенклатура
	|	,ЦеныИзНакладныхЗапасы.Характеристика КАК Характеристика
	|	,ЦеныИзНакладныхЗапасы.Период КАК Период
	|	,ЦеныИзНакладныхЗапасы.Значение КАК Значение
	|	,ЦеныИзНакладныхЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,ЦеныИзНакладныхЗапасы.Курс КАК Курс
	|	,ЦеныИзНакладныхЗапасы.Кратность КАК Кратность
	|ИЗ ЦеныИзНакладныхЗапасы КАК ЦеныИзНакладныхЗапасы
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ
	|	ЦеныИзНакладныхУслуги.Номенклатура
	|	,ЦеныИзНакладныхУслуги.Характеристика
	|	,ЦеныИзНакладныхУслуги.Период
	|	,ЦеныИзНакладныхУслуги.Значение
	|	,ЦеныИзНакладныхУслуги.ЕдиницаИзмерения
	|	,ЦеныИзНакладныхУслуги.Курс
	|	,ЦеныИзНакладныхУслуги.Кратность
	|ИЗ ЦеныИзНакладныхУслуги КАК ЦеныИзНакладныхУслуги";
	
	ТаблицаСЦенамиПоступления = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура ПолучитьТаблицуПоследнихЦенРеализаций(ПараметрыРасчета, ТаблицаСЦенамиРеализации)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаФормыНоменклатуры", ПараметрыРасчета.КоллекцииДанныхФормы.ТаблицаФормыНоменклатуры);
	Запрос.УстановитьПараметр("ТаблицаФормыХарактеристик", ПараметрыРасчета.КоллекцииДанныхФормы.ТаблицаФормыХарактеристик);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаФормыНоменклатуры.Номенклатура КАК Номенклатура
	|	,Значение(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика
	|ПОМЕСТИТЬ ТаблицаНоменклатура
	|ИЗ &ТаблицаФормыНоменклатуры КАК ТаблицаФормыНоменклатуры
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаФормыХарактеристик.Номенклатура КАК Номенклатура
	|	,ТаблицаФормыХарактеристик.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ТаблицаХарактеристики
	|ИЗ &ТаблицаФормыХарактеристик КАК ТаблицаФормыХарактеристик
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатура.Номенклатура КАК Номенклатура
	|	,ТаблицаНоменклатура.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ТаблицаНоменклатураХарактеристики
	|ИЗ ТаблицаНоменклатура КАК ТаблицаНоменклатура
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ
	|	ТаблицаХарактеристики.Номенклатура КАК Номенклатура
	|	,ТаблицаХарактеристики.Характеристика КАК Характеристика
	|ИЗ ТаблицаХарактеристики КАК ТаблицаХарактеристики
	|Индексировать ПО Номенклатура, Характеристика
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РН.Номенклатура КАК Номенклатура
	|	,РН.Характеристика КАК Характеристика
	|	,Максимум(РН.Ссылка.Дата) КАК Период
	|ПОМЕСТИТЬ НоменклатураРасходныхНакладныхСПериодами
	|ИЗ
	|	ТаблицаНоменклатураХарактеристики КАК ТаблицаНоменклатураХарактеристики
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходнаяНакладная.Запасы КАК РН 
	|		ПО ТаблицаНоменклатураХарактеристики.Номенклатура = РН.Номенклатура
	|			И ТаблицаНоменклатураХарактеристики.Характеристика = РН.Характеристика
	|			И РН.Ссылка.Проведен
	|ГДЕ 
	|	НЕ РН.Номенклатура ЕСТЬ NULL
	|СГРУППИРОВАТЬ ПО
	|	РН.Номенклатура, РН.Характеристика
	|Индексировать ПО Номенклатура, Характеристика, Период
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РН.Номенклатура КАК Номенклатура
	|	,РН.Характеристика КАК Характеристика
	|	,РН.Период КАК Период
	|	,ЦеныИзНакладных.Цена КАК Цена
	|	,ЦеныИзНакладных.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,ЦеныИзНакладных.Ссылка.Курс КАК Курс
	|	,ЦеныИзНакладных.Ссылка.Кратность КАК Кратность
	|ПОМЕСТИТЬ НоменклатураРасходныхНакладныхСПериодамиИЦенами
	|ИЗ НоменклатураРасходныхНакладныхСПериодами КАК РН
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходнаяНакладная.Запасы КАК ЦеныИзНакладных
	|		ПО РН.Период = ЦеныИзНакладных.Ссылка.Дата И РН.Номенклатура = ЦеныИзНакладных.Номенклатура И РН.Характеристика = ЦеныИзНакладных.Характеристика
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктВР.Номенклатура КАК Номенклатура
	|	,АктВР.Характеристика КАК Характеристика
	|	,Максимум(АктВР.Ссылка.Дата) КАК Период
	|ПОМЕСТИТЬ НоменклатураАктаСПериодами
	|ИЗ
	|	ТаблицаНоменклатураХарактеристики КАК ТаблицаНоменклатураХарактеристики
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктВыполненныхРабот.РаботыИУслуги КАК АктВР 
	|		ПО ТаблицаНоменклатураХарактеристики.Номенклатура = АктВР.Номенклатура
	|			И ТаблицаНоменклатураХарактеристики.Характеристика = АктВР.Характеристика
	|			И АктВР.Ссылка.Проведен
	|ГДЕ 
	|	НЕ АктВР.Номенклатура ЕСТЬ NULL
	|СГРУППИРОВАТЬ ПО
	|	АктВР.Номенклатура, АктВР.Характеристика
	|Индексировать ПО Номенклатура, Характеристика, Период
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктВР.Номенклатура КАК Номенклатура
	|	,АктВР.Характеристика КАК Характеристика
	|	,АктВР.Период КАК Период
	|	,ЦеныИзАктовВР.Цена КАК Цена
	|	,ЦеныИзАктовВР.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,ЦеныИзАктовВР.Ссылка.Курс КАК Курс
	|	,ЦеныИзАктовВР.Ссылка.Кратность КАК Кратность
	|ПОМЕСТИТЬ НоменклатураАктовСПериодамиИЦенами
	|ИЗ НоменклатураАктаСПериодами КАК АктВР
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктВыполненныхРабот.РаботыИУслуги КАК ЦеныИзАктовВР
	|		ПО АктВР.Период = ЦеныИзАктовВР.Ссылка.Дата И АктВР.Номенклатура = ЦеныИзАктовВР.Номенклатура И АктВР.Характеристика = ЦеныИзАктовВР.Характеристика
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ РН.Номенклатура КАК Номенклатура
	|	,РН.Характеристика КАК Характеристика
	|	,РН.Период КАК Период
	|	,РН.Цена
	|	,РН.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,РН.Курс КАК Курс
	|	,РН.Кратность КАК Кратность
	|ПОМЕСТИТЬ ВсяНоменклатура
	|ИЗ НоменклатураРасходныхНакладныхСПериодамиИЦенами КАК РН
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ АктВР.Номенклатура КАК Номенклатура
	|	,АктВР.Характеристика КАК Характеристика
	|	,АктВР.Период КАК Период
	|	,АктВР.Цена
	|	,АктВР.ЕдиницаИзмерения
	|	,АктВР.Курс КАК Курс
	|	,АктВР.Кратность КАК Кратность
	|ИЗ НоменклатураАктовСПериодамиИЦенами КАК АктВР
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсяНоменклатура.Номенклатура КАК Номенклатура
	|	,ВсяНоменклатура.Характеристика КАК Характеристика
	|	,Максимум(ВсяНоменклатура.Период) КАК Период
	|	,ВсяНоменклатура.Цена КАК Значение
	|	,ВсяНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,ВсяНоменклатура.Курс КАК Курс
	|	,ВсяНоменклатура.Кратность КАК Кратность
	|ИЗ ВсяНоменклатура КАК ВсяНоменклатура
	|СГРУППИРОВАТЬ ПО ВсяНоменклатура.Номенклатура, ВсяНоменклатура.Характеристика, ВсяНоменклатура.Цена, ВсяНоменклатура.ЕдиницаИзмерения, ВсяНоменклатура.Курс, ВсяНоменклатура.Кратность";
	
	ТаблицаСЦенамиРеализации = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура ПолучитьТаблицуСебестоимости(ТаблицаССебестоимостью)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗапасыОстаткиИОбороты.Номенклатура КАК Номенклатура
	|	,ЗапасыОстаткиИОбороты.Характеристика КАК Характеристика
	|	,ЗапасыОстаткиИОбороты.СуммаКонечныйОстаток
	|	,ЗапасыОстаткиИОбороты.КоличествоКонечныйОстаток
	|	,ВЫБОР
	|		КОГДА ЗапасыОстаткиИОбороты.СуммаКонечныйОстаток = 0 					// Себестоимость ниже 0 для цен безразлична
	|				ИЛИ ЗапасыОстаткиИОбороты.КоличествоКонечныйОстаток = 0			// Деление на 0 - это ошибка
	|				ИЛИ ЗапасыОстаткиИОбороты.КоличествоКонечныйОстаток ЕСТЬ NULL	// Операция не возможна
	|			ТОГДА 0
	|		ИНАЧЕ ЗапасыОстаткиИОбороты.СуммаКонечныйОстаток / ЗапасыОстаткиИОбороты.КоличествоКонечныйОстаток
	|	КОНЕЦ КАК Значение
	|	,ЗапасыОстаткиИОбороты.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	РегистрНакопления.Запасы.ОстаткиИОбороты(, , Авто, , СчетУчета.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Запасы)) КАК ЗапасыОстаткиИОбороты");
	
	ТаблицаССебестоимостью = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура ПолучитьТаблицуЦенОперандовФормулы(ПараметрыРасчета, ТаблицаОперандов)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаФормыНоменклатуры",	ПараметрыРасчета.КоллекцииДанныхФормы.ТаблицаФормыНоменклатуры);
	Запрос.УстановитьПараметр("ТаблицаФормыХарактеристик", ПараметрыРасчета.КоллекцииДанныхФормы.ТаблицаФормыХарактеристик);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаФормыНоменклатуры.Номенклатура КАК Номенклатура
	|	,Значение(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика
	|ПОМЕСТИТЬ ТаблицаНоменклатура
	|ИЗ &ТаблицаФормыНоменклатуры КАК ТаблицаФормыНоменклатуры
	|
	|;Выбрать
	|	ТаблицаФормыХарактеристик.Номенклатура КАК Номенклатура
	|	,ТаблицаФормыХарактеристик.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ТаблицаХарактеристики
	|ИЗ &ТаблицаФормыХарактеристик КАК ТаблицаФормыХарактеристик
	|
	|;Выбрать ТаблицаНоменклатура.Номенклатура, ТаблицаНоменклатура.Характеристика, ИСТИНА КАК ТребуетсяНовыйРасчет ИЗ ТаблицаНоменклатура КАК ТаблицаНоменклатура
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ ТаблицаХарактеристики.Номенклатура, ТаблицаХарактеристики.Характеристика, ИСТИНА ИЗ ТаблицаХарактеристики КАК ТаблицаХарактеристики";
	
	ТаблицаНоменклатураИХарактеристики = Запрос.Выполнить().Выгрузить();
	
	ЦенообразованиеСервер.ЗначенияЦенВТаблицуОперандов(НачалоДня(ТекущаяДатаСеанса()), ТаблицаОперандов, ТаблицаНоменклатураИХарактеристики);
	
	ТаблицаНоменклатураИХарактеристики = Неопределено;
	
КонецПроцедуры

Процедура ПодготовитьОперандыИДанные(ПараметрыРасчета, ТаблицаОперандов) Экспорт
	Перем ПодготовленнаяТаблицаДанных;
	
	Формула = ПараметрыРасчета.Формула;
	
	Если ПараметрыРасчета.Свойство("ПериодРасчета") Тогда
		
		ПериодРасчета = НачалоДня(?(ЗначениеЗаполнено(ПараметрыРасчета.ПериодРасчета), ПараметрыРасчета.ПериодРасчета, ТекущаяДатаСеанса()));
		
	КонецЕсли;
	
	ТаблицаОперандов = ЦенообразованиеФормулыСервер.ПолучитьТаблицуОперандовФормулы(ПериодРасчета, Формула);
	ТаблицаОперандов.Колонки.Добавить("Значение");
	
	СтрокаТаблицы = ТаблицаОперандов.Найти("[ПоследняяЦенаВПриходе]", "Операнд");
	Если СтрокаТаблицы <> Неопределено Тогда
		
		ПолучитьТаблицуПоследнихЦенПоступлений(ПараметрыРасчета, ПодготовленнаяТаблицаДанных);
		СтрокаТаблицы.Значение = ПодготовленнаяТаблицаДанных;
		
	КонецЕсли;
	
	СтрокаТаблицы = ТаблицаОперандов.Найти("[ПоследняяЦенаВРасходе]", "Операнд");
	Если СтрокаТаблицы <> Неопределено Тогда
		
		ПолучитьТаблицуПоследнихЦенРеализаций(ПараметрыРасчета, ПодготовленнаяТаблицаДанных);
		СтрокаТаблицы.Значение = ПодготовленнаяТаблицаДанных;
		
	КонецЕсли;
	
	СтрокаТаблицыСебестоимость 			= ТаблицаОперандов.Найти("[Себестоимость]", "Операнд");
	СтрокаТаблицыСебестоимостьНацВалюта = ТаблицаОперандов.Найти("[СебестоимостьНацВалюта]", "Операнд");
	Если СтрокаТаблицыСебестоимость <> Неопределено 
		ИЛИ СтрокаТаблицыСебестоимостьНацВалюта <> Неопределено Тогда
		
		ПолучитьТаблицуСебестоимости(ПодготовленнаяТаблицаДанных);
		Если СтрокаТаблицыСебестоимость <> Неопределено Тогда
			
			СтрокаТаблицыСебестоимость.Значение = ПодготовленнаяТаблицаДанных;
			
		КонецЕсли;
		
		Если СтрокаТаблицыСебестоимостьНацВалюта <> Неопределено Тогда
			
			СтрокаТаблицыСебестоимостьНацВалюта.Значение = ПодготовленнаяТаблицаДанных;
			
			КурсВалютыУчета			= РаботаСКурсамиВалют.ПолучитьКурсВалюты(Константы.ВалютаУчета.Получить(), ПериодРасчета);
			
			НоваяСтрока				= ТаблицаОперандов.Добавить();
			НоваяСтрока.Операнд		= "[КурсВалютыУчета]";
			НоваяСтрока.Значение	= КурсВалютыУчета.Курс / КурсВалютыУчета.Кратность;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтрокаТаблицы = ТаблицаОперандов.Найти("[КурсДоллара]", "Операнд");
	Если СтрокаТаблицы <> Неопределено Тогда
		
		ДанныеКурса = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Справочники.Валюты.НайтиПоКоду("840"), ПериодРасчета);
		СтрокаТаблицы.Значение = ДанныеКурса.Курс / ДанныеКурса.Кратность;
		
	КонецЕсли;
		
	СтрокаТаблицы = ТаблицаОперандов.Найти("[КурсЕвро]", "Операнд");
	Если СтрокаТаблицы <> Неопределено Тогда
		
		ДанныеКурса = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Справочники.Валюты.НайтиПоКоду("978"), ПериодРасчета);
		СтрокаТаблицы.Значение = ДанныеКурса.Курс / ДанныеКурса.Кратность;
		
	КонецЕсли;
	
	МассивЦеныНоменклатуры = ТаблицаОперандов.НайтиСтроки(Новый Структура("ЭтоЦеныНоменклатуры", Истина));
	МассивЦеныКонтрагентов = ТаблицаОперандов.НайтиСтроки(Новый Структура("ЭтоЦеныНоменклатуры", Ложь));
	Если МассивЦеныНоменклатуры.Количество() > 0
		ИЛИ МассивЦеныКонтрагентов.Количество() > 0 Тогда
		
		ПолучитьТаблицуЦенОперандовФормулы(ПараметрыРасчета, ТаблицаОперандов);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СлияниеДанныхВОбщуюТаблицуЗначений(ПараметрыРасчета, КоллекцияНоменклатуры, ВидЦен) Экспорт
	
	#Region ПланТаблицы
	//	КоллекцияНоменклатуры.Период 					- не используется
	//	,КоллекцияНоменклатуры.ВидЦен					- не используется
	//	,КоллекцияНоменклатуры.Номенклатура 			- штатно
	//	,КоллекцияНоменклатуры.Характеристика 			- штатно
	//	,КоллекцияНоменклатуры.ТекущееЗначение			- штатно
	//	,КоллекцияНоменклатуры.Цена						- новое значение, до обработки равно 0
	//	,КоллекцияНоменклатуры.Актуальность				- не используется
	//	,КоллекцияНоменклатуры.ЕдиницаИзмерения			- заполнять, если заполнено
	//	,КоллекцияНоменклатуры.ВключаяХарактеристики	- не используется
	//	,КоллекцияНоменклатуры.Автор					- не используется
	//	,КоллекцияНоменклатуры.ПересчетВыполнен			- не используется
	#EndRegion
	
	КоллекцияНоменклатуры = Новый ТаблицаЗначений;
	КоллекцияНоменклатуры.Колонки.Добавить("КлючСвязиНоменклатура",		Новый ОписаниеТипов("Число"));
	КоллекцияНоменклатуры.Колонки.Добавить("КлючСвязиХарактеристика",	Новый ОписаниеТипов("Число"));
	КоллекцияНоменклатуры.Колонки.Добавить("Период", 					Новый ОписаниеТипов("Дата"));
	КоллекцияНоменклатуры.Колонки.Добавить("ВидЦен", 					Новый ОписаниеТипов("СправочникСсылка.ВидыЦен"));
	КоллекцияНоменклатуры.Колонки.Добавить("Номенклатура",				Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	КоллекцияНоменклатуры.Колонки.Добавить("Характеристика",			Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	КоллекцияНоменклатуры.Колонки.Добавить("Цена",						Новый ОписаниеТипов("Число"));
	КоллекцияНоменклатуры.Колонки.Добавить("ЕдиницаИзмерения",			Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения, СправочникСсылка.КлассификаторЕдиницИзмерения"));
	КоллекцияНоменклатуры.Колонки.Добавить("ТекущееЗначение",			Новый ОписаниеТипов("Число"));
	КоллекцияНоменклатуры.Колонки.Добавить("Актуальность",				Новый ОписаниеТипов("Булево"));
	КоллекцияНоменклатуры.Колонки.Добавить("ВключаяХарактеристики",		Новый ОписаниеТипов("Булево"));
	КоллекцияНоменклатуры.Колонки.Добавить("Автор",						Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	КоллекцияНоменклатуры.Колонки.Добавить("Формула",					Новый ОписаниеТипов("Строка"));
	КоллекцияНоменклатуры.Колонки.Добавить("ПересчетВыполнен",			Новый ОписаниеТипов("Булево"));
	
	ПустаяХарактеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	ИдентификаторВидаЦен = "ТЧНоменклатура" + ВидЦен.ИдентификаторФормул;
	
	Для каждого СтрокаНоменклатуры Из ПараметрыРасчета.КоллекцииДанныхФормы.ТаблицаФормыНоменклатуры Цикл
		
		НоваяСтрока = КоллекцияНоменклатуры.Добавить();
		НоваяСтрока.КлючСвязиНоменклатура	= СтрокаНоменклатуры.КлючСвязи;
		НоваяСтрока.КлючСвязиХарактеристика	= -1;
		НоваяСтрока.Номенклатура			= СтрокаНоменклатуры.Номенклатура;
		НоваяСтрока.Характеристика			= ПустаяХарактеристика;
		НоваяСтрока.ВключаяХарактеристики	= ПараметрыРасчета.УстанавливатьХарактеристикамБезЦен;
		НоваяСтрока.ТекущееЗначение			= СтрокаНоменклатуры[ИдентификаторВидаЦен + "_ЦенаНовая"];
		НоваяСтрока.ЕдиницаИзмерения		= СтрокаНоменклатуры[ИдентификаторВидаЦен + "_ЕдИзм"];
		НоваяСтрока.ВидЦен 					= ВидЦен;
		НоваяСтрока.Формула					= ПараметрыРасчета.Формула;
		
	КонецЦикла;
	
	ИдентификаторВидаЦен = "ТЧХарактеристики" + ВидЦен.ИдентификаторФормул;
	
	Для каждого СтрокаХарактеристики Из ПараметрыРасчета.КоллекцииДанныхФормы.ТаблицаФормыХарактеристик Цикл
		
		НоваяСтрока = КоллекцияНоменклатуры.Добавить();
		НоваяСтрока.КлючСвязиНоменклатура	= -1;
		НоваяСтрока.КлючСвязиХарактеристика	= СтрокаХарактеристики.КлючСвязи;
		НоваяСтрока.Номенклатура			= СтрокаХарактеристики.Номенклатура;
		НоваяСтрока.Характеристика			= СтрокаХарактеристики.Характеристика;
		НоваяСтрока.ВключаяХарактеристики	= ПараметрыРасчета.УстанавливатьХарактеристикамБезЦен;
		НоваяСтрока.ТекущееЗначение			= СтрокаХарактеристики[ИдентификаторВидаЦен + "_ЦенаНовая"];
		НоваяСтрока.ЕдиницаИзмерения		= СтрокаХарактеристики[ИдентификаторВидаЦен + "_ЕдИзм"];
		НоваяСтрока.ВидЦен 					= ВидЦен;
		НоваяСтрока.Формула					= ПараметрыРасчета.Формула;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПересчитанныеСтрокиВТаблицыДанных(ПараметрыРасчета, КоллекцияНоменклатуры, ВидЦен)
	
	ИдентификаторВидаЦен = ВидЦен.ИдентификаторФормул;
	ПараметрыОтбора = Новый Структура;
	
	Для каждого СтрокаКоллекции Из КоллекцияНоменклатуры Цикл
		
		ПараметрыОтбора.Очистить();
		Если СтрокаКоллекции.КлючСвязиХарактеристика < 0 Тогда
			
			Идентификатор = "ТЧНоменклатура" + ИдентификаторВидаЦен;
			ИмяТаблицы = "ТаблицаФормыНоменклатуры";
			ПараметрыОтбора.Вставить("КлючСвязи", СтрокаКоллекции.КлючСвязиНоменклатура);
			ПараметрыОтбора.Вставить("Номенклатура", СтрокаКоллекции.Номенклатура);
			
		Иначе
			
			Идентификатор = "ТЧХарактеристики" + ИдентификаторВидаЦен;
			ИмяТаблицы = "ТаблицаФормыХарактеристик";
			ПараметрыОтбора.Вставить("КлючСвязи", СтрокаКоллекции.КлючСвязиХарактеристика);
			ПараметрыОтбора.Вставить("Номенклатура", СтрокаКоллекции.Номенклатура);
			ПараметрыОтбора.Вставить("Характеристика", СтрокаКоллекции.Характеристика);
			
		КонецЕсли;
		
		НайденныеСтроки = ПараметрыРасчета.КоллекцииДанныхФормы[ИмяТаблицы].НайтиСтроки(ПараметрыОтбора);
		Для каждого СтрокаТабличнойЧасти Из НайденныеСтроки Цикл
			
			СтрокаТабличнойЧасти[Идентификатор + "_ЦенаНовая"] = СтрокаКоллекции.Цена;
			СтрокаТабличнойЧасти[Идентификатор + "_ЕдИзм"] = СтрокаКоллекции.ЕдиницаИзмерения;
			СтрокаТабличнойЧасти[Идентификатор + "_Дельта"] = СтрокаТабличнойЧасти[Идентификатор + "_ЦенаНовая"] - СтрокаТабличнойЧасти[Идентификатор + "_ЦенаДо"];
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура РассчитатьНовыеЦеныПоФормуле(ПараметрыРасчета) Экспорт
	Перем КоллекцияНоменклатуры, ТаблицаОперандов;
	
	ПараметрыРасчетаКоллекции = Новый Структура("ВидЦен, Курс, Кратность");
	
	ПодготовитьОперандыИДанные(ПараметрыРасчета, ТаблицаОперандов);
	Для каждого ЭлементСоответствия Из ПараметрыРасчета.ВидыЦенКПересчету Цикл
		
		КоллекцияНоменклатуры = Неопределено;
		
		ПараметрыРасчетаКоллекции.ВидЦен = ЭлементСоответствия.Ключ;
		Если ЗначениеЗаполнено(ПараметрыРасчетаКоллекции.ВидЦен) Тогда
			
			КурсВалюты = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ПараметрыРасчетаКоллекции.ВидЦен.ВалютаЦены, ТекущаяДатаСеанса());
			ПараметрыРасчетаКоллекции.Курс = ?(ЗначениеЗаполнено(КурсВалюты.Курс), КурсВалюты.Курс, 1);
			ПараметрыРасчетаКоллекции.Кратность = ?(ЗначениеЗаполнено(КурсВалюты.Кратность), КурсВалюты.Кратность, 1);
			
		Иначе
			
			ПараметрыРасчетаКоллекции.Курс = 1;
			ПараметрыРасчетаКоллекции.Кратность = 1;
			
		КонецЕсли;
		
		СлияниеДанныхВОбщуюТаблицуЗначений(ПараметрыРасчета, КоллекцияНоменклатуры, ПараметрыРасчетаКоллекции.ВидЦен);
		ЦенообразованиеФормулыСервер.РассчитатьДанныеКоллекции(КоллекцияНоменклатуры, ТаблицаОперандов, ПараметрыРасчетаКоллекции, Истина, Истина);
		ПересчитанныеСтрокиВТаблицыДанных(ПараметрыРасчета, КоллекцияНоменклатуры, ПараметрыРасчетаКоллекции.ВидЦен);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура РассчитатьНовыеЦеныПоФормулеДлОперация(ПараметрыРасчета, ФоновоеЗаданиеАдресХранилища = "") Экспорт
	
	РассчитатьНовыеЦеныПоФормуле(ПараметрыРасчета);
	ПоместитьВоВременноеХранилище(ПараметрыРасчета.КоллекцииДанныхФормы, ФоновоеЗаданиеАдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСТаблицамиЗначений

Процедура ДобавитьКолонкиТекущихЦен(ВыбранныеВидыЦен, ИмяТабличнойЧасти, ТаблицаИсточник, ТаблицаИсточникСЦеной)
	
	ПодчиненныеВидыЦен = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаИсточник", ТаблицаИсточник);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.Номенклатура КАК Номенклатура
	|	,ТаблицаИсточник.Характеристика КАК Характеристика
	|	,ТаблицаИсточник.ЕдиницаИзмерения КАК ЕдиницаИзмеренияШаблон
	|	,ТаблицаИсточник.ЦенаНоваяШаблон КАК ЦенаНоваяШаблон
	|	,ТаблицаИсточник.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ТаблицаИсточник
	|ИЗ &ТаблицаИсточник КАК ТаблицаИсточник
	|Индексировать ПО Номенклатура, Характеристика
	|;
	|////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИсточник.Номенклатура КАК Номенклатура
	|	,ТаблицаИсточник.Характеристика КАК Характеристика
	|	,ТаблицаИсточник.ЕдиницаИзмеренияШаблон КАК ЕдиницаИзмеренияШаблон
	|	,ТаблицаИсточник.ЦенаНоваяШаблон КАК ЦенаНоваяШаблон
	|	,ТаблицаИсточник.КлючСвязи КАК КлючСвязи
	|	,&ПолеИменаКолонокТекущихЦен
	|	,&ПолеИменаКолонокЕдиницИзмерения
	|	,&ПолеИменаКолонокДельта
	|	,&ПолеИменаКолонокНовыхЦен
	|ИЗ ТаблицаИсточник КАК ТаблицаИсточник";
	
	ШаблонТекстаЗапросаВидЦен =
	"
	|	Левое Соединение РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, Актуальность И ВидЦен  = &_ШаблонИмяВидаЦен) КАК _ШаблонИмяТаблицыЗапроса
	|	По ТаблицаИсточник.Номенклатура = _ШаблонИмяТаблицыЗапроса.Номенклатура";
	
	ШаблонТекстаЗапросаВидЦен = ШаблонТекстаЗапросаВидЦен +
		?(ИмяТабличнойЧасти = "ТЧНоменклатура",
			Символы.ПС + "		И _ШаблонИмяТаблицыЗапроса.Характеристика = Значение(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)",
			Символы.ПС + "		И ТаблицаИсточник.Характеристика = _ШаблонИмяТаблицыЗапроса.Характеристика");
	
	ПолеИменаКолонокТекущихЦен = "";
	ПолеИменаКолонокЕдиницИзмерения = "";
	ПолеИменаКолонокДельта = "";
	ПолеИменаКолонокНовыхЦен = "";
	
	Для каждого ЭлементСоответствия Из ВыбранныеВидыЦен Цикл
		
		_ШаблонИмяТаблицыЗапроса = ЭлементСоответствия.Ключ.ИдентификаторФормул;
		
		ПолеИменаКолонокТекущихЦен = ПолеИменаКолонокТекущихЦен
			+ ?(ПустаяСтрока(ПолеИменаКолонокТекущихЦен), "", ", ") 
			+ _ШаблонИмяТаблицыЗапроса + ".Цена КАК " + ИмяТабличнойЧасти + _ШаблонИмяТаблицыЗапроса + "_ЦенаДо";
		
		ПолеИменаКолонокЕдиницИзмерения = ПолеИменаКолонокЕдиницИзмерения 
			+ ?(ПустаяСтрока(ПолеИменаКолонокЕдиницИзмерения), "", ", ") 
			+ "Выбор Когда ЕстьNULL(ТаблицаИсточник.ЕдиницаИзмеренияШаблон, 0) <> 0 Тогда ТаблицаИсточник.ЕдиницаИзмеренияШаблон Иначе ЕстьNULL(" + _ШаблонИмяТаблицыЗапроса + ".ЕдиницаИзмерения, ТаблицаИсточник.Номенклатура.ЕдиницаИзмерения) Конец КАК " + ИмяТабличнойЧасти + _ШаблонИмяТаблицыЗапроса + "_ЕдИзм";
		
		ПолеИменаКолонокДельта = ПолеИменаКолонокДельта 
			+ ?(ПустаяСтрока(ПолеИменаКолонокДельта), "", ", ") 
			+ "Выбор Когда ЕстьNULL(ТаблицаИсточник.ЦенаНоваяШаблон, 0) <> 0 Тогда ТаблицаИсточник.ЦенаНоваяШаблон - " + _ШаблонИмяТаблицыЗапроса + ".Цена Иначе 0 Конец КАК " + ИмяТабличнойЧасти + _ШаблонИмяТаблицыЗапроса + "_Дельта";
		
		ПолеИменаКолонокНовыхЦен = ПолеИменаКолонокНовыхЦен
			+ ?(ПустаяСтрока(ПолеИменаКолонокНовыхЦен), "", ", ") 
			+ "Выбор Когда ЕстьNULL(ТаблицаИсточник.ЦенаНоваяШаблон, 0) <> 0 Тогда ТаблицаИсточник.ЦенаНоваяШаблон Иначе " + _ШаблонИмяТаблицыЗапроса + ".Цена Конец КАК " + ИмяТабличнойЧасти + _ШаблонИмяТаблицыЗапроса + "_ЦенаНовая";
			
		ТекстЗапросаЛевоеСоединение = СтрЗаменить(ШаблонТекстаЗапросаВидЦен, "_ШаблонИмяТаблицыЗапроса", _ШаблонИмяТаблицыЗапроса);
		ТекстЗапросаЛевоеСоединение = СтрЗаменить(ТекстЗапросаЛевоеСоединение, "_ШаблонИмяВидаЦен", _ШаблонИмяТаблицыЗапроса);
		
		Запрос.Текст = Запрос.Текст + ТекстЗапросаЛевоеСоединение;
		Запрос.УстановитьПараметр(_ШаблонИмяТаблицыЗапроса, ЭлементСоответствия.Ключ);
		
		Для каждого ПодчиненныйВидЦен Из ЭлементСоответствия.Значение Цикл
			
			Если ПодчиненныеВидыЦен.Найти(ПодчиненныйВидЦен) = Неопределено Тогда
				
				_ШаблонИмяТаблицыЗапроса = ПодчиненныйВидЦен.ИдентификаторФормул;
				
				ПолеИменаКолонокТекущихЦен = ПолеИменаКолонокТекущихЦен + ", "
					+ _ШаблонИмяТаблицыЗапроса + ".Цена КАК " + _ШаблонИмяТаблицыЗапроса + "_ЦенаДо";
				
				ТекстЗапросаЛевоеСоединение = СтрЗаменить(ШаблонТекстаЗапросаВидЦен, "_ШаблонИмяТаблицыЗапроса", _ШаблонИмяТаблицыЗапроса);
				ТекстЗапросаЛевоеСоединение = СтрЗаменить(ТекстЗапросаЛевоеСоединение, "_ШаблонИмяВидаЦен", _ШаблонИмяТаблицыЗапроса);
				
				Запрос.Текст = Запрос.Текст + ТекстЗапросаЛевоеСоединение;
				Запрос.УстановитьПараметр(_ШаблонИмяТаблицыЗапроса, ПодчиненныйВидЦен);
				
				ПодчиненныеВидыЦен.Добавить(ПодчиненныйВидЦен);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПолеИменаКолонокТекущихЦен", ПолеИменаКолонокТекущихЦен);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПолеИменаКолонокЕдиницИзмерения", ПолеИменаКолонокЕдиницИзмерения);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПолеИменаКолонокДельта", ПолеИменаКолонокДельта);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПолеИменаКолонокНовыхЦен", ПолеИменаКолонокНовыхЦен);
	
	Запрос.Текст = Запрос.Текст + Символы.ПС + "УПОРЯДОЧИТЬ ПО ТаблицаИсточник.Номенклатура.Наименование, ТаблицаИсточник.Характеристика";
	
	ТаблицаИсточникСЦеной = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура КопироватьДобавлениемСтрокТаблицЗначений(ТаблицаИсточник, ТаблицаПриемник, ПараметрыКопирования) Экспорт
	Перем ИмяТабличнойЧасти, ВыбранныеВидыЦен, ЗаполнитьТекущиеЦены, ИменаКолонокКопирования, ИменаКолонокИсключений, МаксимальныйКлючСвязи, МаксимальныйКлючСвязиПослеДобавления;
	
	Если ТипЗнч(ТаблицаИсточник) = Тип("ТаблицаЗначений")
		И ТипЗнч(ТаблицаПриемник) = Тип("ТаблицаЗначений") Тогда
		
		ТаблицаКопирования = ТаблицаИсточник.Скопировать();
		
		ПараметрыКопирования.Свойство("МаксимальныйКлючСвязи", МаксимальныйКлючСвязи);
		ПараметрыКопирования.Свойство("МаксимальныйКлючСвязиПослеДобавления", МаксимальныйКлючСвязиПослеДобавления);
		
		ПараметрыКопирования.Свойство("ИменаКолонокКопирования", ИменаКолонокКопирования);
		ПараметрыКопирования.Свойство("ИменаКолонокИсключений", ИменаКолонокИсключений);
		
		ЕстьПолеИндексКартинки = ТаблицаПриемник.Колонки.Найти("ИндексКартинки") <> Неопределено;
		
		ПараметрыКопирования.Свойство("ЗаполнитьТекущиеЦены", ЗаполнитьТекущиеЦены);
		ЗаполнитьТекущиеЦены = ?(ЗаполнитьТекущиеЦены = Неопределено, Ложь, ЗаполнитьТекущиеЦены);
		
		Если ЗаполнитьТекущиеЦены Тогда
			
			ПараметрыКопирования.Свойство("ИмяТабличнойЧасти", ИмяТабличнойЧасти);
			ПараметрыКопирования.Свойство("ВыбранныеВидыЦен", ВыбранныеВидыЦен);
			ДобавитьКолонкиТекущихЦен(ВыбранныеВидыЦен, ИмяТабличнойЧасти, ТаблицаИсточник, ТаблицаКопирования);
			
		КонецЕсли;
		
		Для каждого СтрокаИсточник Из ТаблицаКопирования Цикл
			
			НоваяСтрока = ТаблицаПриемник.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаИсточник, ИменаКолонокКопирования, ИменаКолонокИсключений);
			
			НоваяСтрока.КлючСвязи = МаксимальныйКлючСвязи + СтрокаИсточник.КлючСвязи;
			МаксимальныйКлючСвязиПослеДобавления = МАКС(МаксимальныйКлючСвязиПослеДобавления, МаксимальныйКлючСвязи + СтрокаИсточник.КлючСвязи);
			
			Если ЕстьПолеИндексКартинки Тогда
				
				НоваяСтрока.ИндексКартинки =
					?(СтрокаИсточник.Номенклатура.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Запас, 2, 0)
					+ ?(СтрокаИсточник.Номенклатура.ИспользоватьХарактеристики, 1, 0)
					+ ?(СтрокаИсточник.Номенклатура.ПометкаУдаления, 4, 0)
					+ ?(СтрокаИсточник.Номенклатура.ЭтоНабор, 8, 0)
					
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ПараметрыКопирования.МаксимальныйКлючСвязиПослеДобавления = МаксимальныйКлючСвязиПослеДобавления;
	
КонецПроцедуры

Процедура КопироватьДобавлениемСтрокТаблицЗначенийПакетно(ПараметрыДлОперации, ФоновоеЗаданиеАдресХранилища = "") Экспорт
	Перем ПараметрыКопирования;
	
	ПараметрыДлОперации.Свойство("ПараметрыКопирования", ПараметрыКопирования);
	
	// :::Номенклатура
	ПараметрыКопирования.ИмяТабличнойЧасти = "ТЧНоменклатура";
	ТаблицаНоменклатуры = ПараметрыДлОперации.СтруктураТаблицДанных.ТаблицаНоменклатуры;
	ТаблицаФормыНоменклатуры = ПараметрыДлОперации.КоллекцииДанныхФормы.ТаблицаФормыНоменклатуры;
	
	КопироватьДобавлениемСтрокТаблицЗначений(ТаблицаНоменклатуры, ТаблицаФормыНоменклатуры, ПараметрыКопирования);
	
	// :::Характеристики
	ПараметрыКопирования.ИмяТабличнойЧасти = "ТЧХарактеристики";
	ТаблицаХарактеристик = ПараметрыДлОперации.СтруктураТаблицДанных.ТаблицаХарактеристик;
	ТаблицаФормыХарактеристик = ПараметрыДлОперации.КоллекцииДанныхФормы.ТаблицаФормыХарактеристик;
	
	КопироватьДобавлениемСтрокТаблицЗначений(ТаблицаХарактеристик, ТаблицаФормыХарактеристик, ПараметрыКопирования);
	
	СтруктураКоллекцийДанныхФормы = Новый Структура("ТаблицаФормыНоменклатуры, ТаблицаФормыХарактеристик", ТаблицаФормыНоменклатуры, ТаблицаФормыХарактеристик);
	ПоместитьВоВременноеХранилище(СтруктураКоллекцийДанныхФормы, ФоновоеЗаданиеАдресХранилища);
	
КонецПроцедуры

Функция СтруктуруДереваЗначенийВТаблицуЗначений(ДеревоЗначений)
	
	Результат = Новый ТаблицаЗначений;
	
	Для Каждого Колонка Из ДеревоЗначений.Колонки Цикл
		
		Результат.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
		
	КонецЦикла;
	
	Результат.Колонки.Добавить("КлючСвязи", Новый ОписаниеТипов("Число"));
	
	Возврат Результат;
КонецФункции

Функция РазобратьДеревоНоменклатуры(ДеревоНоменклатуры, ПоказыватьНедействительныеХарактеристики = Ложь) Экспорт
	
	ТаблицаНоменклатуры = СтруктуруДереваЗначенийВТаблицуЗначений(ДеревоНоменклатуры);
	ТаблицаХарактеристик = СтруктуруДереваЗначенийВТаблицуЗначений(ДеревоНоменклатуры);
	
	Если ТаблицаНоменклатуры.Колонки.Найти("ЕдиницаИзмеренияШаблон") = Неопределено Тогда
		
		ТаблицаНоменклатуры.Колонки.Добавить("ЕдиницаИзмеренияШаблон", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения, СправочникСсылка.КлассификаторЕдиницИзмерения"));
		
	КонецЕсли;
	
	Если ТаблицаНоменклатуры.Колонки.Найти("ЦенаНоваяШаблон") = Неопределено Тогда
		
		ТаблицаНоменклатуры.Колонки.Добавить("ЦенаНоваяШаблон", Новый ОписаниеТипов("Число"));
		
	КонецЕсли;
	
	Если ТаблицаНоменклатуры.Колонки.Найти("Характеристика") = Неопределено Тогда
		
		ТаблицаНоменклатуры.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		
	КонецЕсли;
	
	Если ТаблицаХарактеристик.Колонки.Найти("ЕдиницаИзмеренияШаблон") = Неопределено Тогда
		
		ТаблицаХарактеристик.Колонки.Добавить("ЕдиницаИзмеренияШаблон", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения, СправочникСсылка.КлассификаторЕдиницИзмерения"));
		
	КонецЕсли;
	
	Если ТаблицаХарактеристик.Колонки.Найти("ЦенаНоваяШаблон") = Неопределено Тогда
		
		ТаблицаХарактеристик.Колонки.Добавить("ЦенаНоваяШаблон", Новый ОписаниеТипов("Число"));
		
	КонецЕсли;
	
	Если ТаблицаХарактеристик.Колонки.Найти("Характеристика") = Неопределено Тогда
		
		ТаблицаХарактеристик.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		
	КонецЕсли;
	
	Если ТаблицаХарактеристик.Колонки.Найти("ХарактеристикаНедействителен") = Неопределено Тогда
		
		ТаблицаХарактеристик.Колонки.Добавить("ХарактеристикаНедействителен", Новый ОписаниеТипов("Булево"));
		
	КонецЕсли;
	
	ИспользоватьХарактеристики = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики");
	
	ЛокальныйКлючСвязи = 0;
	Для каждого СтрокаДереваНоменклатура Из ДеревоНоменклатуры.Строки Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаДереваНоменклатура.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		
		ЛокальныйКлючСвязи = ЛокальныйКлючСвязи + 1;
		
		НоваяСтрокаНоменклатура = ТаблицаНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаНоменклатура, СтрокаДереваНоменклатура);
		НоваяСтрокаНоменклатура.КлючСвязи = ЛокальныйКлючСвязи;
		
		Для каждого СтрокаДереваХарактеристика Из СтрокаДереваНоменклатура.Строки Цикл
			
			Если ИспользоватьХарактеристики
				И Не ПоказыватьНедействительныеХарактеристики
				И СтрокаДереваХарактеристика.ХарактеристикаНедействителен Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрокаХарактеристика = ТаблицаХарактеристик.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаХарактеристика, СтрокаДереваХарактеристика);
			НоваяСтрокаХарактеристика.КлючСвязи = ЛокальныйКлючСвязи;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Новый Структура("ТаблицаНоменклатуры, ТаблицаХарактеристик", ТаблицаНоменклатуры, ТаблицаХарактеристик);
	
КонецФункции

Функция РазобратьМассивНоменклатуры(МассивНоменклатуры, УстанавливатьХарактеристикамБезЦен, ПоказыватьНедействительныеХарактеристики = Ложь) Экспорт
	
	ДеревоНоменклатуры = Новый ДеревоЗначений;
	
	// 1. Получим СКД
	ИмяСхемыКД = "ПоНоменклатуре";
	СхемаКомпоновкиДанных = Обработки.ФормированиеЦенНоменклатуры.ПолучитьМакет(ИмяСхемыКД);
	
	// 2. создаем настройки для схемы 
	НастройкиКомпоновкиДанных = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
		
		НастройкиКомпоновкиДанных.Структура[0].Структура[0].Использование = Ложь;
		
	КонецЕсли;
	
	// 2.1 установим значения параметров
	ПараметрКД = СхемаКомпоновкиДанных.Параметры.Найти("МассивНоменклатуры");
	ПараметрКД.Значение = МассивНоменклатуры;
	
	// 3. готовим макет 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// 4. исполняем макет 
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет);
	ПроцессорКомпоновки.Сбросить();
	
	// 5. выводим результат 
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ДеревоНоменклатуры);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	СтруктураТаблицДанных = РазобратьДеревоНоменклатуры(ДеревоНоменклатуры, ПоказыватьНедействительныеХарактеристики);
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(СтруктураТаблицДанных);
	
	Возврат АдресВременногоХранилища;
	
КонецФункции

#КонецОбласти

#Область ЗаписьНовыхЦен

Процедура ОпределитьЗаписываемыеЦеныКоллекцииНоменклатуры(КоллекцияНоменклатуры, МассивВидовЦен, ПараметрыКоллекции)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КоллекцияНоменклатуры", КоллекцияНоменклатуры);
	Запрос.УстановитьПараметр("ПериодЗаписи", ПараметрыКоллекции.ПериодЗаписи);
	Запрос.УстановитьПараметр("МассивВидовЦен", МассивВидовЦен);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РСЦены.Период КАК Период
	|	,РСЦены.ВидЦен КАК ВидЦен
	|	,РСЦены.Номенклатура КАК Номенклатура
	|	,РСЦены.Характеристика КАК Характеристика
	|	,РСЦены.Цена КАК Цена
	|	,РСЦены.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,РСЦены.Актуальность КАК Актуальность
	|	,РСЦены.Автор КАК Автор
	|ПОМЕСТИТЬ ТекущиеЗаписиРегистраЦен
	|ИЗ РегистрСведений.ЦеныНоменклатуры КАК РСЦены
	|ГДЕ РСЦены.Период = &ПериодЗаписи И РСЦены.ВидЦен В(&МассивВидовЦен) И РСЦены.Цена <> 0
	|Индексировать ПО ВидЦен, Номенклатура, Характеристика
	|;/////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КоллекцияНоменклатуры.Период КАК Период
	|	,КоллекцияНоменклатуры.ВидЦен КАК ВидЦен
	|	,КоллекцияНоменклатуры.Номенклатура КАК Номенклатура
	|	,КоллекцияНоменклатуры.Характеристика КАК Характеристика
	|	,КоллекцияНоменклатуры.Цена КАК Цена
	|	,КоллекцияНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,КоллекцияНоменклатуры.Актуальность КАК Актуальность
	|	,КоллекцияНоменклатуры.Автор КАК Автор
	|ПОМЕСТИТЬ КоллекцияНоменклатуры
	|ИЗ &КоллекцияНоменклатуры КАК КоллекцияНоменклатуры
	|ГДЕ КоллекцияНоменклатуры.Цена <> 0
	|Индексировать ПО ВидЦен, Номенклатура, Характеристика
	|;/////////////////////////////////////////////////////////////////////
	|";
	
	ТекстЗапросаТекущиеЦены = 
	"
	|// Если по ключевым полям в регистре уже имеется запись – оставляем ее
	|// 
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТекущиеЗаписиРегистраЦен.Период, КоллекцияНоменклатуры.Период) КАК Период
	|	,ЕстьNULL(ТекущиеЗаписиРегистраЦен.Актуальность, КоллекцияНоменклатуры.Актуальность) КАК Актуальность
	|	,ЕстьNULL(ТекущиеЗаписиРегистраЦен.ВидЦен, КоллекцияНоменклатуры.ВидЦен) КАК ВидЦен
	|	,ЕстьNULL(ТекущиеЗаписиРегистраЦен.Номенклатура, КоллекцияНоменклатуры.Номенклатура) КАК Номенклатура
	|	,ЕстьNULL(ТекущиеЗаписиРегистраЦен.Характеристика, КоллекцияНоменклатуры.Характеристика) КАК Характеристика
	|	,ЕстьNULL(ТекущиеЗаписиРегистраЦен.Цена, КоллекцияНоменклатуры.Цена) КАК Цена
	|	,ЕстьNULL(ТекущиеЗаписиРегистраЦен.ЕдиницаИзмерения, КоллекцияНоменклатуры.ЕдиницаИзмерения) КАК ЕдиницаИзмерения
	|	,ЕстьNULL(ТекущиеЗаписиРегистраЦен.Автор, КоллекцияНоменклатуры.Автор) КАК Автор
	|ИЗ ТекущиеЗаписиРегистраЦен КАК ТекущиеЗаписиРегистраЦен
	|	ПОЛНОЕ СОЕДИНЕНИЕ КоллекцияНоменклатуры КАК КоллекцияНоменклатуры
	|		ПО ТекущиеЗаписиРегистраЦен.ВидЦен = КоллекцияНоменклатуры.ВидЦен 
	|			И ТекущиеЗаписиРегистраЦен.Номенклатура = КоллекцияНоменклатуры.Номенклатура 
	|			И ТекущиеЗаписиРегистраЦен.Характеристика = КоллекцияНоменклатуры.Характеристика";
	
	ТекстЗапросаНовыеЦены = 
	"
	|// Записываем новые данные в любом случае и добавляем существующие только тогда, когда в таблице нет записей с такими ключами
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(КоллекцияНоменклатуры.Период, ТекущиеЗаписиРегистраЦен.Период) КАК Период
	|	,ЕстьNULL(КоллекцияНоменклатуры.Актуальность, ТекущиеЗаписиРегистраЦен.Актуальность) КАК Актуальность
	|	,ЕстьNULL(КоллекцияНоменклатуры.ВидЦен, ТекущиеЗаписиРегистраЦен.ВидЦен) КАК ВидЦен
	|	,ЕстьNULL(КоллекцияНоменклатуры.Номенклатура, ТекущиеЗаписиРегистраЦен.Номенклатура) КАК Номенклатура
	|	,ЕстьNULL(КоллекцияНоменклатуры.Характеристика, ТекущиеЗаписиРегистраЦен.Характеристика) КАК Характеристика
	|	,ЕстьNULL(КоллекцияНоменклатуры.Цена, ТекущиеЗаписиРегистраЦен.Цена) КАК Цена
	|	,Выбор КОГДА КоллекцияНоменклатуры.Цена = ТекущиеЗаписиРегистраЦен.Цена
	|		ТОГДА ЕСТЬNULL(ТекущиеЗаписиРегистраЦен.ЕдиницаИзмерения, КоллекцияНоменклатуры.ЕдиницаИзмерения)
	|		ИНАЧЕ ЕСТЬNULL(КоллекцияНоменклатуры.ЕдиницаИзмерения, ТекущиеЗаписиРегистраЦен.ЕдиницаИзмерения) КОНЕЦ КАК ЕдиницаИзмерения
	|	,Выбор КОГДА КоллекцияНоменклатуры.Цена = ТекущиеЗаписиРегистраЦен.Цена
	|		ТОГДА ЕСТЬNULL(ТекущиеЗаписиРегистраЦен.Автор, КоллекцияНоменклатуры.Автор)
	|		ИНАЧЕ ЕСТЬNULL(КоллекцияНоменклатуры.Автор, ТекущиеЗаписиРегистраЦен.Автор) КОНЕЦ КАК Автор
	|ИЗ ТекущиеЗаписиРегистраЦен КАК ТекущиеЗаписиРегистраЦен
	|	ПОЛНОЕ СОЕДИНЕНИЕ КоллекцияНоменклатуры КАК КоллекцияНоменклатуры
	|		ПО ТекущиеЗаписиРегистраЦен.ВидЦен = КоллекцияНоменклатуры.ВидЦен 
	|			И ТекущиеЗаписиРегистраЦен.Номенклатура = КоллекцияНоменклатуры.Номенклатура 
	|			И ТекущиеЗаписиРегистраЦен.Характеристика = КоллекцияНоменклатуры.Характеристика";
	
	Запрос.Текст = ТекстЗапроса + ?(ПараметрыКоллекции.ЗаписыватьНовыеЦеныПоверхУстановленных, ТекстЗапросаНовыеЦены, ТекстЗапросаТекущиеЦены);
	
	КоллекцияНоменклатуры = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура ДобавитьСтрокиХарактеристикНоменклатуры(КоллекцияНоменклатуры)
	
	ТекстЗапроса = 
	"
	|ВЫБРАТЬ 
	|	КоллекцияНоменклатуры.Период КАК Период
	|	,КоллекцияНоменклатуры.ВидЦен КАК ВидЦен
	|	,КоллекцияНоменклатуры.Номенклатура КАК Номенклатура
	|	,КоллекцияНоменклатуры.Характеристика КАК Характеристика
	|	,КоллекцияНоменклатуры.Цена КАК Цена
	|	,КоллекцияНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,КоллекцияНоменклатуры.Актуальность КАК Актуальность
	|	,КоллекцияНоменклатуры.Автор КАК Автор
	|	,КоллекцияНоменклатуры.УстанавливатьХарактеристикамБезЦен КАК УстанавливатьХарактеристикамБезЦен
	|	,КоллекцияНоменклатуры.КатегорияНоменклатуры КАК КатегорияНоменклатуры
	|	,КоллекцияНоменклатуры.ИспользоватьХарактеристики КАК ИспользоватьХарактеристики
	|ПОМЕСТИТЬ КоллекцияНоменклатуры
	|ИЗ &КоллекцияНоменклатуры КАК КоллекцияНоменклатуры
	|ГДЕ
	|	КоллекцияНоменклатуры.Характеристика = Значение(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика, КатегорияНоменклатуры
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КоллекцияНоменклатуры.*
	|	,СпрХарактеристики.Ссылка КАК Характеристика
	|ПОМЕСТИТЬ НоменклатураХарактеристики
	|ИЗ КоллекцияНоменклатуры КАК КоллекцияНоменклатуры
	|	СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК СпрХарактеристики
	|	ПО КоллекцияНоменклатуры.Номенклатура = СпрХарактеристики.Владелец И КоллекцияНоменклатуры.ИспользоватьХарактеристики = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КоллекцияНоменклатуры.*
	|	,СпрХарактеристики.Ссылка КАК Характеристика
	|ПОМЕСТИТЬ КатегорияНоменклатурыХарактеристики
	|ИЗ КоллекцияНоменклатуры КАК КоллекцияНоменклатуры
	|	СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК СпрХарактеристики
	|	ПО КоллекцияНоменклатуры.КатегорияНоменклатуры = СпрХарактеристики.Владелец И КоллекцияНоменклатуры.ИспользоватьХарактеристики = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	КоллекцияНоменклатуры.Период КАК Период
	|	,КоллекцияНоменклатуры.ВидЦен КАК ВидЦен
	|	,КоллекцияНоменклатуры.Номенклатура КАК Номенклатура
	|	,КоллекцияНоменклатуры.Характеристика КАК Характеристика
	|	,КоллекцияНоменклатуры.Цена КАК Цена
	|	,КоллекцияНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,КоллекцияНоменклатуры.Актуальность КАК Актуальность
	|	,КоллекцияНоменклатуры.Автор КАК Автор
	|	,КоллекцияНоменклатуры.УстанавливатьХарактеристикамБезЦен КАК УстанавливатьХарактеристикамБезЦен
	|	,КоллекцияНоменклатуры.КатегорияНоменклатуры КАК КатегорияНоменклатуры
	|	,КоллекцияНоменклатуры.ИспользоватьХарактеристики КАК ИспользоватьХарактеристики
	|ПОМЕСТИТЬ ВсяНоменклатураИХарактеристики
	|ИЗ КоллекцияНоменклатуры КАК КоллекцияНоменклатуры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НоменклатураХарактеристики.Период КАК Период
	|	,НоменклатураХарактеристики.ВидЦен КАК ВидЦен
	|	,НоменклатураХарактеристики.Номенклатура КАК Номенклатура
	|	,НоменклатураХарактеристики.Характеристика КАК Характеристика
	|	,НоменклатураХарактеристики.Цена КАК Цена
	|	,НоменклатураХарактеристики.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,НоменклатураХарактеристики.Актуальность КАК Актуальность
	|	,НоменклатураХарактеристики.Автор КАК Автор
	|	,Ложь КАК УстанавливатьХарактеристикамБезЦен
	|	,НоменклатураХарактеристики.КатегорияНоменклатуры КАК КатегорияНоменклатуры
	|	,НоменклатураХарактеристики.ИспользоватьХарактеристики КАК ИспользоватьХарактеристики
	|ИЗ НоменклатураХарактеристики КАК НоменклатураХарактеристики
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КатегорияНоменклатурыХарактеристики.Период КАК Период
	|	,КатегорияНоменклатурыХарактеристики.ВидЦен КАК ВидЦен
	|	,КатегорияНоменклатурыХарактеристики.Номенклатура КАК Номенклатура
	|	,КатегорияНоменклатурыХарактеристики.Характеристика КАК Характеристика
	|	,КатегорияНоменклатурыХарактеристики.Цена КАК Цена
	|	,КатегорияНоменклатурыХарактеристики.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	,КатегорияНоменклатурыХарактеристики.Актуальность КАК Актуальность
	|	,КатегорияНоменклатурыХарактеристики.Автор КАК Автор
	|	,Ложь КАК УстанавливатьХарактеристикамБезЦен
	|	,КатегорияНоменклатурыХарактеристики.КатегорияНоменклатуры КАК КатегорияНоменклатуры
	|	,КатегорияНоменклатурыХарактеристики.ИспользоватьХарактеристики КАК ИспользоватьХарактеристики
	|ИЗ КатегорияНоменклатурыХарактеристики КАК КатегорияНоменклатурыХарактеристики
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ВсяНоменклатураИХарактеристики.*
	|ИЗ ВсяНоменклатураИХарактеристики КАК ВсяНоменклатураИХарактеристики
	|УПОРЯДОЧИТЬ ПО ВидЦен, Номенклатура, Характеристика";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("КоллекцияНоменклатуры", КоллекцияНоменклатуры);
	КоллекцияНоменклатуры = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура ДобавитьСтрокуДанныхВКоллекцию(КоллекцияНоменклатуры, МассивВидовЦен, СтрокаДереваДанныхНоменклатура, ПараметрыКоллекции)
	
	Для каждого ВидЦен Из МассивВидовЦен Цикл
		
		Идентификатор 						= ПараметрыКоллекции.ИмяТабличнойЧасти + ВидЦен.ИдентификаторФормул;
		
		НоваяСтрока 						= КоллекцияНоменклатуры.Добавить();
		НоваяСтрока.Период					= ПараметрыКоллекции.ПериодЗаписи;
		НоваяСтрока.ВидЦен					= ВидЦен;
		НоваяСтрока.Номенклатура			= СтрокаДереваДанныхНоменклатура.Номенклатура;
		
		Если ПараметрыКоллекции.ЕстьКолонкаХарактеристика Тогда
			
			НоваяСтрока.Характеристика		= СтрокаДереваДанныхНоменклатура.Характеристика;
			
		КонецЕсли;
		
		НоваяСтрока.Цена					= СтрокаДереваДанныхНоменклатура[Идентификатор + "_ЦенаНовая"];
		НоваяСтрока.ЕдиницаИзмерения		= СтрокаДереваДанныхНоменклатура[Идентификатор + "_ЕдИзм"];
		НоваяСтрока.Актуальность			= Истина;
		НоваяСтрока.Автор					= ПараметрыКоллекции.АвторизированныйПользователь;
		НоваяСтрока.УстанавливатьХарактеристикамБезЦен	= ПараметрыКоллекции.УстанавливатьХарактеристикамБезЦен;
		НоваяСтрока.КатегорияНоменклатуры	= СтрокаДереваДанныхНоменклатура.Номенклатура.КатегорияНоменклатуры;
		НоваяСтрока.ИспользоватьХарактеристики = СтрокаДереваДанныхНоменклатура.Номенклатура.ИспользоватьХарактеристики;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьКоллекциюНоменклатурыПоДеревуДанных(СтруктураТаблицДанных, КоллекцияНоменклатуры, МассивВидовЦен, ПараметрыКоллекции)
	
	ТаблицаНоменклатуры = СтруктураТаблицДанных.ТаблицаНоменклатуры;
	ТаблицаХарактеристик = СтруктураТаблицДанных.ТаблицаХарактеристик;
	
	КоллекцияНоменклатуры = Новый ТаблицаЗначений;
	КоллекцияНоменклатуры.Колонки.Добавить("Период", 					Новый ОписаниеТипов("Дата"));
	КоллекцияНоменклатуры.Колонки.Добавить("ВидЦен", 					Новый ОписаниеТипов("СправочникСсылка.ВидыЦен"));
	КоллекцияНоменклатуры.Колонки.Добавить("Номенклатура",				Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	КоллекцияНоменклатуры.Колонки.Добавить("Характеристика",			Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	КоллекцияНоменклатуры.Колонки.Добавить("Цена",						Новый ОписаниеТипов("Число"));
	КоллекцияНоменклатуры.Колонки.Добавить("ЕдиницаИзмерения",			Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения, СправочникСсылка.КлассификаторЕдиницИзмерения"));
	КоллекцияНоменклатуры.Колонки.Добавить("Актуальность",				Новый ОписаниеТипов("Булево"));
	КоллекцияНоменклатуры.Колонки.Добавить("Автор",						Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	КоллекцияНоменклатуры.Колонки.Добавить("УстанавливатьХарактеристикамБезЦен", Новый ОписаниеТипов("Булево"));
	// ::: Дополнительные поля
	КоллекцияНоменклатуры.Колонки.Добавить("КатегорияНоменклатуры",		Новый ОписаниеТипов("СправочникСсылка.КатегорииНоменклатуры"));
	КоллекцияНоменклатуры.Колонки.Добавить("ИспользоватьХарактеристики",Новый ОписаниеТипов("Булево"));
	
	ПараметрыКоллекции.Вставить("ЕстьКолонкаХарактеристика", Ложь);
	ПараметрыКоллекции.Вставить("ИмяТабличнойЧасти", "ТЧНоменклатура");
	
	Для каждого СтрокаДереваДанныхНоменклатура Из ТаблицаНоменклатуры Цикл
		
		ДобавитьСтрокуДанныхВКоллекцию(КоллекцияНоменклатуры, МассивВидовЦен, СтрокаДереваДанныхНоменклатура, ПараметрыКоллекции);
		
	КонецЦикла;
	
	Если ПараметрыКоллекции.ПоказыватьХарактеристики Тогда
		
		ПараметрыКоллекции.Вставить("ИмяТабличнойЧасти", "ТЧХарактеристики");
		ПараметрыКоллекции.Вставить("ЕстьКолонкаХарактеристика", Истина);
		
		Для каждого СтрокаДереваДанныхХарактеристика Из ТаблицаХарактеристик Цикл
			
			Если ЗначениеЗаполнено(СтрокаДереваДанныхХарактеристика.Характеристика) Тогда // Доп. заглушка
				
				ДобавитьСтрокуДанныхВКоллекцию(КоллекцияНоменклатуры, МассивВидовЦен, СтрокаДереваДанныхХарактеристика, ПараметрыКоллекции);
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ПараметрыКоллекции.УстанавливатьХарактеристикамБезЦен Тогда
		
		ДобавитьСтрокиХарактеристикНоменклатуры(КоллекцияНоменклатуры);
		
	КонецЕсли;
	
	Если ПараметрыКоллекции.ЗаписьМенеджером <> Истина Тогда
		
		ОпределитьЗаписываемыеЦеныКоллекцииНоменклатуры(КоллекцияНоменклатуры, МассивВидовЦен, ПараметрыКоллекции);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьНовыеЦены(СтруктураДанных, ФоновоеЗаданиеАдресХранилища = "") Экспорт
	Перем КоллекцияНоменклатуры;
	
	ПараметрыКоллекции = Новый Структура;
	ПараметрыКоллекции.Вставить("ПериодЗаписи", СтруктураДанных.ПериодЗаписи);
	ПараметрыКоллекции.Вставить("АвторизированныйПользователь", СтруктураДанных.КэшЗначений.АвторизированныйПользователь);
	ПараметрыКоллекции.Вставить("ПоказыватьХарактеристики", СтруктураДанных.ПоказыватьХарактеристики);
	ПараметрыКоллекции.Вставить("УстанавливатьХарактеристикамБезЦен", СтруктураДанных.УстанавливатьХарактеристикамБезЦен);
	ПараметрыКоллекции.Вставить("ЗаписыватьНовыеЦеныПоверхУстановленных", СтруктураДанных.ЗаписыватьНовыеЦеныПоверхУстановленных);
	
	ЗаписьМенеджером = (СтруктураДанных.СтруктураТаблицДанных.ТаблицаНоменклатуры.Количество() + СтруктураДанных.СтруктураТаблицДанных.ТаблицаХарактеристик.Количество()) < 500;
	ПараметрыКоллекции.Вставить("ЗаписьМенеджером", ЗаписьМенеджером);
	
	МассивВидовЦен = Новый Массив;
	Для каждого ЭлементСоответствия Из СтруктураДанных.КэшЗначений.ВыбранныеВидыЦен Цикл
		
		Если СтруктураДанных.КэшЗначений.ИсключенныеВидыЦен.Найти(ЭлементСоответствия.Ключ) = Неопределено Тогда
			
			МассивВидовЦен.Добавить(ЭлементСоответствия.Ключ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СоздатьКоллекциюНоменклатурыПоДеревуДанных(СтруктураДанных.СтруктураТаблицДанных, КоллекцияНоменклатуры, МассивВидовЦен, ПараметрыКоллекции);
	
	Если ПараметрыКоллекции.ЗаписьМенеджером = Истина Тогда
		
		ЗаписатьНовыеЦеныМенеджером(КоллекцияНоменклатуры, ПараметрыКоллекции);
		
	Иначе
		
		ЗаписатьНовыеЦеныНабором(КоллекцияНоменклатуры, МассивВидовЦен, ПараметрыКоллекции);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьНовыеЦеныМенеджером(КоллекцияНоменклатуры, ПараметрыКоллекции)
	
	Для Каждого СтрокаКоллекции Из КоллекцияНоменклатуры Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаКоллекции.Цена) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ЗаписьРегистра = РегистрыСведений.ЦеныНоменклатуры.СоздатьМенеджерЗаписи();
		ЗаписьРегистра.Период = ПараметрыКоллекции.ПериодЗаписи;
		ЗаписьРегистра.ВидЦен = СтрокаКоллекции.ВидЦен;
		ЗаписьРегистра.Номенклатура = СтрокаКоллекции.Номенклатура;
		ЗаписьРегистра.Характеристика = СтрокаКоллекции.Характеристика;
		ЗаписьРегистра.Прочитать();
		
		Если ЗначениеЗаполнено(ЗаписьРегистра.Цена)
			И ПараметрыКоллекции.ЗаписыватьНовыеЦеныПоверхУстановленных = Истина Тогда
			
			ЗаписьРегистра.Цена = СтрокаКоллекции.Цена;
			Если ЗначениеЗаполнено(СтрокаКоллекции.ЕдиницаИзмерения) Тогда
				ЗаписьРегистра.ЕдиницаИзмерения = СтрокаКоллекции.ЕдиницаИзмерения;
			КонецЕсли;
			
		ИначеЕсли НЕ ЗначениеЗаполнено(ЗаписьРегистра.Цена) Тогда
			
			ЗаполнитьЗначенияСвойств(ЗаписьРегистра, СтрокаКоллекции);
			ЗаписьРегистра.Период = ПараметрыКоллекции.ПериодЗаписи;
			
		Иначе
			
			Продолжить;
			
		КонецЕсли;
		
		ЗаписьРегистра.Записать(Истина);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьНовыеЦеныНабором(КоллекцияНоменклатуры, МассивВидовЦен, ПараметрыКоллекции)
	
	СтруктураОтбора = Новый Структура;
	Для каждого ВидЦен Из МассивВидовЦен Цикл
		
		СтруктураОтбора.Вставить("ВидЦен", ВидЦен);
		СтрокиПоВидуЦен = КоллекцияНоменклатуры.НайтиСтроки(СтруктураОтбора);
		
		ЗаписиРегистра = КоллекцияНоменклатуры.Скопировать(СтрокиПоВидуЦен);
		
		Если ЗаписиРегистра.Количество() > 0 Тогда
			
			РСЦены = РегистрыСведений.ЦеныНоменклатуры.СоздатьНаборЗаписей();
			РСЦены.Отбор.Период.Установить(ПараметрыКоллекции.ПериодЗаписи, Истина);
			РСЦены.Отбор.ВидЦен.Установить(ВидЦен, Истина);
			РСЦены.Загрузить(ЗаписиРегистра);
			РСЦены.ДополнительныеСвойства.Вставить("РегистрацияНаУзлах", Ложь); 				
			РСЦены.Записать(Истина);
			
			Товары = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ЗаписиРегистра.ВыгрузитьКолонку("Номенклатура"));		
			ЦенообразованиеСервер.ЗарегистрироватьНоменклатуруНаУзлахСайтаИШтрихМ(Товары);				
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ИнтерфейсПечати

Процедура ДобавитьПолеКД(НаборПолейДанных, Поле, Заголовок, ПутьКДанным)
	
	Если НаборПолейДанных.Найти(ПутьКДанным) = Неопределено Тогда
		
		НовоеПолеКД				= НаборПолейДанных.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
		НовоеПолеКД.Поле 		= Поле;
		НовоеПолеКД.Заголовок	= Заголовок;
		НовоеПолеКД.ПутьКДанным	= ПутьКДанным;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПолеИтогаКД(ПоляИтогов, Выражение, ПутьКДанным)
	
	Если ПоляИтогов.Найти(ПутьКДанным) = Неопределено Тогда
		
		ПолеРесурса 			= ПоляИтогов.Добавить();
		ПолеРесурса.Выражение	= Выражение;
		ПолеРесурса.ПутьКДанным = ПутьКДанным;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьОписаниеВыбранныхПолей(ТаблицаОписанияПолей, ВидЦен, ПутьКДанным, Заголовок, Использование = Истина)
	
	НоваяСтрока					= ТаблицаОписанияПолей.Добавить();
	НоваяСтрока.Поле			= Новый ПолеКомпоновкиДанных(ПутьКДанным);
	НоваяСтрока.Заголовок		= Заголовок;
	НоваяСтрока.Использование	= Использование;
	НоваяСтрока.Родитель		= ВидЦен;
	
КонецПроцедуры

Процедура ДобавитьПоляТекущихЦен(ВидЦен, НаборПолейДанных, ПоляИтогов, ТаблицаОписанияПолей)
	
	ОписаниеПоля	= ВидЦен.ИдентификаторФормул + "_ЦенаДо";
	Заголовок		= НСтр("ru ='Текущая цена'");
	
	ДобавитьПолеКД(НаборПолейДанных, ОписаниеПоля, Заголовок, ОписаниеПоля);
	ДобавитьПолеИтогаКД(ПоляИтогов, ОписаниеПоля, ОписаниеПоля);
	ДобавитьОписаниеВыбранныхПолей(ТаблицаОписанияПолей, ВидЦен, ОписаниеПоля, Заголовок);
	
КонецПроцедуры

Процедура ДобавитьПоляОтклоненийЦен(ВидЦен, НаборПолейДанных, ПоляИтогов, ТаблицаОписанияПолей)
	
	ОписаниеПоля	= ВидЦен.ИдентификаторФормул + "_Дельта";
	Заголовок		= НСтр("ru ='Дельта'");
	
	ДобавитьПолеКД(НаборПолейДанных, ОписаниеПоля, Заголовок, ОписаниеПоля);
	ДобавитьПолеИтогаКД(ПоляИтогов, ОписаниеПоля, ОписаниеПоля);
	ДобавитьОписаниеВыбранныхПолей(ТаблицаОписанияПолей, ВидЦен, ОписаниеПоля, Заголовок);
	
КонецПроцедуры

Процедура ДобавитьПоляНовыхЦен(ВидЦен, НаборПолейДанных, ПоляИтогов, ТаблицаОписанияПолей)
	
	ОписаниеПоля	= ВидЦен.ИдентификаторФормул + "_ЦенаНовая";
	Заголовок		= НСтр("ru ='Новая цена'");
	
	ДобавитьПолеКД(НаборПолейДанных, ОписаниеПоля, Заголовок, ОписаниеПоля);
	ДобавитьПолеИтогаКД(ПоляИтогов, ОписаниеПоля, ОписаниеПоля);
	ДобавитьОписаниеВыбранныхПолей(ТаблицаОписанияПолей, ВидЦен, ОписаниеПоля, Заголовок);
	
КонецПроцедуры

Процедура ДобавитьПоляЕдиницИзмерения(ВидЦен, НаборПолейДанных, ПоляИтогов, ТаблицаОписанияПолей)
	
	ОписаниеПоля	= ВидЦен.ИдентификаторФормул + "_ЕдИзм";
	Заголовок		= НСтр("ru ='Единица измерения'");
	
	ДобавитьПолеКД(НаборПолейДанных, ОписаниеПоля, Заголовок, ОписаниеПоля);
	ДобавитьПолеИтогаКД(ПоляИтогов, ОписаниеПоля, ОписаниеПоля);
	ДобавитьОписаниеВыбранныхПолей(ТаблицаОписанияПолей, ВидЦен, ОписаниеПоля, Заголовок);
	
КонецПроцедуры

Процедура ДобавитьПоляЗависимыеЦены(ВидЦен, ЗависимыеЦены, НаборПолейДанных, ПоляИтогов, ТаблицаОписанияПолей)
	
	Для каждого ЗависимаяЦена Из ЗависимыеЦены Цикл
		
		ОписаниеПоля	= ЗависимаяЦена.ИдентификаторФормул + "_ЦенаДо";
		Заголовок		= ЗависимаяЦена.Наименование;
		
		ДобавитьПолеКД(НаборПолейДанных, ОписаниеПоля, Заголовок, ОписаниеПоля);
		ДобавитьПолеИтогаКД(ПоляИтогов, ОписаниеПоля, ОписаниеПоля);
		ДобавитьОписаниеВыбранныхПолей(ТаблицаОписанияПолей, ВидЦен, ОписаниеПоля, Заголовок);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьПоляСКД(СхемаКомпоновкиДанных, ПараметрыПечати, ТаблицаОписанияПолей)
	
	ВыбранныеВидыЦен	= ПараметрыПечати.ВыбранныеВидыЦен;
	НаборПолейДанных	= СхемаКомпоновкиДанных.НаборыДанных["КоллекцияНоменклатуры"].Поля;
	ПоляИтогов			= СхемаКомпоновкиДанных.ПоляИтога;
	
	Для каждого ЭлементСоответствия Из ВыбранныеВидыЦен Цикл
		
		ВидЦен = ЭлементСоответствия.Ключ;
		Если ПараметрыПечати.ПоказыватьДействующуюЦену Тогда
			
			ДобавитьПоляТекущихЦен(ВидЦен, НаборПолейДанных, ПоляИтогов, ТаблицаОписанияПолей);
			
		КонецЕсли;
		
		Если ПараметрыПечати.ПоказыватьОтклонениеЦен Тогда
			
			ДобавитьПоляОтклоненийЦен(ВидЦен, НаборПолейДанных, ПоляИтогов, ТаблицаОписанияПолей);
			
		КонецЕсли;
		
		// Выводим всегда
		ДобавитьПоляНовыхЦен(ВидЦен, НаборПолейДанных, ПоляИтогов, ТаблицаОписанияПолей);
		
		Если ПараметрыПечати.ПоказыватьЕдиницыИзмерения Тогда
			
			ДобавитьПоляЕдиницИзмерения(ВидЦен, НаборПолейДанных, ПоляИтогов, ТаблицаОписанияПолей);
			
		КонецЕсли;
		
		Если ПараметрыПечати.ПоказыватьЗависимыеЦены Тогда
			
			Если ЭлементСоответствия.Значение.Количество() > 0 Тогда
				
				ДобавитьПоляЗависимыеЦены(ВидЦен, ЭлементСоответствия.Значение, НаборПолейДанных, ПоляИтогов, ТаблицаОписанияПолей);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьВыбранныеЭлементыКД(ТаблицаОписанияПолей, СтруктураКД)
	
	Для каждого ЭлементНастройкиКД Из СтруктураКД Цикл
		
		Если ЭлементНастройкиКД.Имя = "Подпись" Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ТекущийРодитель 			= Неопределено;
		КоллекцияВыбранныхПолейКД	= ЭлементНастройкиКД.Выбор.Элементы;
		Для каждого СтрокаТаблицы Из ТаблицаОписанияПолей Цикл
			
			Если ТекущийРодитель <> СтрокаТаблицы.Родитель Тогда
				
				ТекущийРодитель					= СтрокаТаблицы.Родитель;
				
				ГруппаВыбранныхПолей			= КоллекцияВыбранныхПолейКД.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
				ГруппаВыбранныхПолей.Заголовок	= ТекущийРодитель.Наименование;
				
			КонецЕсли;
			
			ВыбранноеПоле 			= ГруппаВыбранныхПолей.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ЗаполнитьЗначенияСвойств(ВыбранноеПоле, СтрокаТаблицы);
			
		КонецЦикла;
		
		Если ЭлементНастройкиКД.Структура.Количество() > 0 Тогда
			
			ДобавитьВыбранныеЭлементыКД(ТаблицаОписанияПолей, ЭлементНастройкиКД.Структура)
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СоздатьЕдиныйНаборДанных(ПараметрыПечати)
	
	КоллекцияНоменклатуры = ПараметрыПечати.КоллекцииДанныхФормы.ТаблицаФормыНоменклатуры.Скопировать();
	КоллекцияНоменклатуры.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	
	Для каждого КолонкаТаблицы Из КоллекцияНоменклатуры.Колонки Цикл
		
		КолонкаТаблицы.Имя = СтрЗаменить(КолонкаТаблицы.Имя, "ТЧНоменклатура", "");
		
	КонецЦикла;
	
	Если ПараметрыПечати.ПоказыватьХарактеристики Тогда
		
		Для каждого КолонкаТаблицы Из ПараметрыПечати.КоллекцииДанныхФормы.ТаблицаФормыХарактеристик.Колонки Цикл
			
			КолонкаТаблицы.Имя = СтрЗаменить(КолонкаТаблицы.Имя, "ТЧХарактеристики", "");
			
		КонецЦикла;
		
		Для каждого СтрокаТаблицы Из ПараметрыПечати.КоллекцииДанныхФормы.ТаблицаФормыХарактеристик Цикл
			
			ЗаполнитьЗначенияСвойств(КоллекцияНоменклатуры.Добавить(), СтрокаТаблицы);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат КоллекцияНоменклатуры;
	
КонецФункции

Функция СформироватьТабличныйДокументПоДеревуДанных(ПараметрыПечати) Экспорт
	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	
	КоллекцияНоменклатуры = СоздатьЕдиныйНаборДанных(ПараметрыПечати);
	
	// 1. Получим СКД
	СхемаКомпоновкиДанных = Обработки.ФормированиеЦенНоменклатуры.ПолучитьМакет("ПечатьТабличнойЧасти");
	
	ТаблицаОписанияПолей = Новый ТаблицаЗначений;
	ТаблицаОписанияПолей.Колонки.Добавить("Поле");
	ТаблицаОписанияПолей.Колонки.Добавить("Заголовок");
	ТаблицаОписанияПолей.Колонки.Добавить("Использование");
	ТаблицаОписанияПолей.Колонки.Добавить("Родитель"); // Содержит вид цен (ссылка)
	
	ДобавитьПоляСКД(СхемаКомпоновкиДанных, ПараметрыПечати, ТаблицаОписанияПолей);
	
	// 2. создаем настройки для схемы 
	НастройкиКомпоновкиДанных = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	
	СтруктураКД						= НастройкиКомпоновкиДанных.Структура;
	ДобавитьВыбранныеЭлементыКД(ТаблицаОписанияПолей, СтруктураКД);
	
	// 3. готовим макет 
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных, ДанныеРасшифровки);
	
	// 4. исполняем макет 
	ВнешниеНаборыДанных = Новый Структура("КоллекцияНоменклатуры", КоллекцияНоменклатуры);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	ПроцессорКомпоновки.Сбросить();
	
	// 5. выводим результат 
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ТабличныйДокумент);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	// 6. Запишем СКД и данные расшифровки
	// не требуется
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#КонецЕсли