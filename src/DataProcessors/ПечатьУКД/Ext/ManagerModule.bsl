#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Сформировать печатную форму
// Параметры:
//   МассивОбъектов - Массив - массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - структура дополнительных параметров печати
//   КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы
//   ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
//   ПараметрыВывода - Структура - .
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	НачалоИспользованияУПД534 = '20210701';
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УКДСтатус1") Тогда
			
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УКДСтатус1", "УКД (статус 1)",
			ПечатьУниверсальныхКорректировочныхДокументов(МассивОбъектов, ОбъектыПечати, 
			Документы.СчетФактура.ТекстЗапросаПечатьКорректировочныхСчетовФактур(Истина)),,
			"Обработка.ПечатьУКД.ПФ_MXL_УниверсальныйКорректировочныйДокумент");
			
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УКДСтатус2") Тогда
		ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов = "";
		Для каждого Объект Из МассивОбъектов Цикл
			Если ТипЗнч(Объект) <> Тип("ДокументСсылка.СчетФактура") Тогда
				ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов = 
					Документы[Объект.Метаданные().Имя].ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов();
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НЕ ПустаяСтрока(ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов) Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УКДСтатус2", "УКД (статус 2)",
				ПечатьУниверсальныхКорректировочныхДокументов(МассивОбъектов, ОбъектыПечати, 
				ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов, Истина),,
				"Обработка.ПечатьУКД.ПФ_MXL_УниверсальныйКорректировочныйДокумент");
		КонецЕсли;
	КонецЕсли;
		
	// параметры отправки печатных форм по электронной почте
	ЭлектроннаяПочтаУНФ.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов,
		КоллекцияПечатныхФорм);
	
КонецПроцедуры

// Печать универсальных корректировочных документов
// 
// Параметры:
// 	МассивОбъектов - Массив - .
// 	ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
// 	ТекстЗапросаПоДокументам - Строка - .
// 	ТолькоПередаточныйДокумент - Булево - .
// Возвращаемое значение:
// 	ТабличныйДокумент - .
Функция ПечатьУниверсальныхКорректировочныхДокументов(МассивОбъектов, ОбъектыПечати, ТекстЗапросаПоДокументам,
	ТолькоПередаточныйДокумент = Ложь) Экспорт
	Перем ПервыйДокумент, НомерСтрокиНачало;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб        = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ЭкземпляровНаСтранице = 1;
	
	ОписаниеПечатнойФормы = Новый Структура;
	ОписаниеПечатнойФормы.Вставить("ТабличныйДокумент", ТабличныйДокумент);
	ОписаниеПечатнойФормы.Вставить("ПолныйПутьКМакету", Неопределено);
	
	Если ТолькоПередаточныйДокумент Тогда
		ДанныеУниверсальныхКорректировочныхДокументов = ПолучитьДанныеДляПечатиУниверсальногоКорректировочногоДокумента(
			МассивОбъектов, ТекстЗапросаПоДокументам);
	Иначе
		ДанныеУниверсальныхКорректировочныхДокументов = Документы.СчетФактура.ПолучитьДанныеДляПечатиКорректировочныхСчетовФактур(
			МассивОбъектов, ТекстЗапросаПоДокументам, Истина);
	КонецЕсли;
	
	ПервыйДокумент = Истина;
	
	Для Каждого ВыборкаУКД Из ДанныеУниверсальныхКорректировочныхДокументов Цикл
		
		ТаблицаДокумента = ВыборкаУКД.ТаблицаДокумента;
		Если ТаблицаДокумента = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПечатьДокументовУНФ.ПередНачаломФормированияДокумента(ОписаниеПечатнойФормы.ТабличныйДокумент, ПервыйДокумент,
			НомерСтрокиНачало);

		Документы.СчетФактура.ВывестиКорректировочныйСчетФактуруВТабличныйДокумент(ОписаниеПечатнойФормы, ВыборкаУКД,
			Истина);

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ОписаниеПечатнойФормы.ТабличныйДокумент, НомерСтрокиНачало,
			ОбъектыПечати, ВыборкаУКД.Ссылка);
		
	КонецЦикла;
	
	Возврат ОписаниеПечатнойФормы.ТабличныйДокумент;
	
КонецФункции

Функция ПолучитьДанныеДляПечатиУниверсальногоКорректировочногоДокумента(МассивОбъектов, ТекстЗапросаДокументам) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = ТекстЗапросаДокументам;
	
	ТаблицаСчетовФактур = Новый ТаблицаЗначений();
	ТаблицаСчетовФактур.Колонки.Добавить("ДанныеШапки");
	ТаблицаСчетовФактур.Колонки.Добавить("ТаблицаДокумента");
	ТаблицаСчетовФактур.Колонки.Добавить("ДокументыОснования");
	ТаблицаСчетовФактур.Колонки.Добавить("ВидСчетаФактуры");
	ТаблицаСчетовФактур.Колонки.Добавить("СчетФактура");
	ТаблицаСчетовФактур.Колонки.Добавить("СчетФактураБезНДС");
	ТаблицаСчетовФактур.Колонки.Добавить("Дата");
	ТаблицаСчетовФактур.Колонки.Добавить("Ссылка");
	
	ДанныеСчетаФактуры = Новый Структура;
	ДанныеСчетаФактуры.Вставить("СчетФактура");
	ДанныеСчетаФактуры.Вставить("ВидСчетаФактуры");
	ДанныеСчетаФактуры.Вставить("Контрагент");
	ДанныеСчетаФактуры.Вставить("ДоговорКонтрагента");
	ДанныеСчетаФактуры.Вставить("ИспользуетсяПостановлениеНДС1137");
	ДанныеСчетаФактуры.ИспользуетсяПостановлениеНДС1137 = Истина;
	
	ТаблицаСчетовФактур.Колонки.Добавить("ЕстьПрослеживаемыеТовары");
	
	ВыборкаПоОснованиям = Запрос.Выполнить().Выбрать();
	Пока ВыборкаПоОснованиям.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(ВыборкаПоОснованиям.ДокументОснование) Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументыОснования = Новый Массив;
		ДокументыОснования.Добавить(ВыборкаПоОснованиям.ДокументОснование);
		
		ЗаполнитьЗначенияСвойств(ДанныеСчетаФактуры, ВыборкаПоОснованиям);
		ПараметрыОснования = ПодготовитьДанныеДляПечатиУниверсальныхКорректировочныхДокументов(
			ВыборкаПоОснованиям.ДокументОснование, ДанныеСчетаФактуры);
		
		Если ПараметрыОснования.Реквизиты = Неопределено ИЛИ ПараметрыОснования.ТаблицаДокумента = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		ТаблицаДокумента = ПараметрыОснования.ТаблицаДокумента;
		ТаблицаСведенияПрослеживаемости = ПараметрыОснования.ТаблицаСведенияПрослеживаемости;
		ТаблицаСведенияПрослеживаемостиДоИзменения = ПараметрыОснования.ТаблицаСведенияПрослеживаемостиДоИзменения;
		
		ДанныеШапки = ПодготовитьДанныеШапкиУниверсальногоКорректировочногоДокумента(ВыборкаПоОснованиям,
			ПараметрыОснования.Реквизиты[0]);
		
		СчетФактура = ТаблицаСчетовФактур.Добавить();
		СчетФактура.Дата               = ВыборкаПоОснованиям.Дата;
		СчетФактура.СчетФактура        = ВыборкаПоОснованиям.СчетФактура;
		СчетФактура.ВидСчетаФактуры    = ВыборкаПоОснованиям.ВидСчетаФактуры;
		СчетФактура.СчетФактураБезНДС  = ВыборкаПоОснованиям.СчетФактураБезНДС;
		СчетФактура.ДанныеШапки        = ДанныеШапки;
		СчетФактура.ТаблицаДокумента   = ТаблицаДокумента;
		СчетФактура.ДокументыОснования = ДокументыОснования;
		СчетФактура.Ссылка             = ВыборкаПоОснованиям.ДокументОснование;
		
		// Прослеживаемость
		СчетФактура.ЕстьПрослеживаемыеТовары = (ТаблицаДокумента.Итог("ПрослеживаемыйТоварЧисло") > 0);
		Если СчетФактура.ЕстьПрослеживаемыеТовары Тогда
			Документы.СчетФактура.ДополнитьСведениямиОПрослеживаемости(ТаблицаСведенияПрослеживаемости, ТаблицаСведенияПрослеживаемостиДоИзменения, СчетФактура.ТаблицаДокумента);
		КонецЕсли;
		// Конец Прослеживаемость
		
		ПервичныйДокумент = ВыборкаПоОснованиям.ДокументОснование.ИсправляемыйДокументРеализации;
		НомерНаПечать	= ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ПервичныйДокумент.Номер, Истина, Ложь);
		ДатаНаПечать	= Формат(ПервичныйДокумент.Дата, "ДЛФ=D");
		РеквизитыПервичногоДокумента	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '№ %1 от %2'"),
			НомерНаПечать, ДатаНаПечать);
		
		СчетФактура.ДанныеШапки.Вставить("РеквизитыПередаточныхДокументов", СтрШаблон(
			НСтр("ru = 'Универсальный передаточный документ %1'"), РеквизитыПервичногоДокумента));
		
	КонецЦикла;
	
	Возврат ТаблицаСчетовФактур;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПодготовитьДанныеДляПечатиУниверсальныхКорректировочныхДокументов(ДокументОснование, ДанныеСчетаФактуры)
	
	ТекстПустойПоказатель = "--";
	
	ДанныеДляПечати = Документы.СчетФактура.ПодготовитьДанныеДляПечатиКорректировочныхСчетовФактур(ДокументОснование,
		ДанныеСчетаФактуры.СчетФактура, ДанныеСчетаФактуры.ВидСчетаФактуры, Истина);
		
	// Поля, которые в УПД со статусом 2 должны быть пустыми
	ПустыеПоляУКД = Новый Структура;
	ПустыеПоляУКД.Вставить("Акциз",                  ТекстПустойПоказатель);
	ПустыеПоляУКД.Вставить("СтавкаНДС",              Справочники.СтавкиНДС.ПустаяСсылка());
	
	Если ДанныеДляПечати.ТаблицаДокумента <> Неопределено Тогда
		Для Каждого СтрокаДокумента Из ДанныеДляПечати.ТаблицаДокумента Цикл
			ЗаполнитьЗначенияСвойств(СтрокаДокумента, ПустыеПоляУКД);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДанныеДляПечати;
	
КонецФункции

Функция ПодготовитьДанныеШапкиУниверсальногоКорректировочногоДокумента(ВыборкаСФ, Реквизиты) 

	Возврат Документы.СчетФактура.ПодготовитьДанныеШапкиКорректировочногоСчетаФактуры(ВыборкаСФ, Реквизиты, Истина);

КонецФункции

#КонецОбласти

#КонецЕсли