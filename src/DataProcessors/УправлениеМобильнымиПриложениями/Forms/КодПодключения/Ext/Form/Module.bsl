#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Параметры.Свойство("УзелОбмена", УзелОбмена);
	Параметры.Свойство("ТекущийПользователь", ТекущийПользователь);
	
	Заголовок = УзелОбмена.МобильноеПриложение.Наименование;
	
	Если ТекущийПользователь.Пустая() Тогда
		Элементы.НадписьИнформация.Заголовок = НСтр("ru = 'Передайте коды подключения сотрудникам'");
		Элементы.ФормаОтправитьПоПочте.Видимость = Ложь;
		МассивПользователей = УзелОбмена.ГруппаПользователей.Состав.ВыгрузитьКолонку("Пользователь");
	Иначе
		Элементы.НадписьИнформация.Заголовок = НСтр("ru = 'Откройте приложение и отсканируйте QR-код'");
		МассивПользователей = Новый Массив;
		МассивПользователей.Добавить(ТекущийПользователь);
	КонецЕсли;
	УправлениеМобильнымиПриложениями.ЗаполнитьКодыПодключения(
		КодыПодключения, УзелОбмена, МассивПользователей, ТабличныйДокумент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьПоПочте(Команда)
	
	ОчиститьСообщения();
	ОтправитьПоПочтеСервер();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтправитьПоПочтеСервер()
	
	ОтправкаЗавершенаУспешно = УправлениеМобильнымиПриложениями.ОтправитьКодыПодключения(КодыПодключения, УзелОбмена);
	Если Не ОтправкаЗавершенаУспешно Тогда
		Возврат;
	КонецЕсли;
	Для Каждого Строка Из КодыПодключения Цикл
		Если Строка.ПисьмоОтправлено Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Отправка завершена'"));
		Иначе
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не указан адрес электронной почты пользователя %1.'"), Строка.Пользователь);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти