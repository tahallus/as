#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "УзелОбмена");
	МассивПользователей = УзелОбмена.ГруппаПользователей.Состав.ВыгрузитьКолонку("Пользователь");
	УправлениеМобильнымиПриложениями.ЗаполнитьКодыПодключения(КодыПодключения, УзелОбмена, МассивПользователей);
	ОтрисоватьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияНажатие(Элемент)
	
	МассивСтрок = КодыПодключения.НайтиСтроки(Новый Структура("ИмяЭлемента", Элемент.Имя));
	Если МассивСтрок.Количество() > 0 Тогда
		ТекущийПользователь = МассивСтрок[0].Пользователь;
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("УзелОбмена", УзелОбмена);
		ПараметрыОткрытия.Вставить("ТекущийПользователь", ТекущийПользователь);
		ОткрытьФорму("Обработка.УправлениеМобильнымиПриложениями.Форма.КодПодключения",
			ПараметрыОткрытия, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьПоПочте(Команда)
	
	ОчиститьСообщения();
	ОтправитьПоПочтеСервер();

КонецПроцедуры

&НаКлиенте
Процедура СформироватьПечатнуюФорму(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("УзелОбмена", УзелОбмена);
	ОткрытьФорму("Обработка.УправлениеМобильнымиПриложениями.Форма.КодПодключения",
		ПараметрыОткрытия, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтрисоватьФорму()
	
	Сч = 1;
	ШаблонЗаголовка = НСтр("ru = 'QR-код (%1)'");
	Для Каждого Строка Из КодыПодключения Цикл
		
		ИмяГруппы = "ГруппаПользователь" + Строка(Сч);
		НоваяГруппа = Элементы.Добавить(ИмяГруппы, Тип("ГруппаФормы"), Элементы.ГруппаПараметрыПодключения);
		НоваяГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		НоваяГруппа.ОтображатьЗаголовок = Ложь;
		НоваяГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		
		ИмяЭлемента = "ДекорацияПользователь" + Строка(Сч);
		НоваяДекорация = Элементы.Добавить(ИмяЭлемента, Тип("ДекорацияФормы"), НоваяГруппа);
		НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
		НоваяДекорация.Заголовок =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, Строка.Пользователь);
		НоваяДекорация.Гиперссылка = Истина;
		НоваяДекорация.УстановитьДействие("Нажатие", "ДекорацияНажатие");
		Строка.ИмяЭлемента = ИмяЭлемента;
		
		ИмяЭлемента = "ДекорацияСостояниеОтправки" + Строка(Сч);
		НоваяДекорация = Элементы.Добавить(ИмяЭлемента, Тип("ДекорацияФормы"), НоваяГруппа);
		НоваяДекорация.Вид = ВидДекорацииФормы.Надпись;
		
		Сч = Сч + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтправитьПоПочтеСервер()
	
	ОтправкаЗавершенаУспешно = УправлениеМобильнымиПриложениями.ОтправитьКодыПодключения(КодыПодключения, УзелОбмена);
	Если Не ОтправкаЗавершенаУспешно Тогда
		Возврат;
	КонецЕсли;
	
	Сч = 1;
	Для Каждого Строка Из КодыПодключения Цикл
		ИмяЭлемента = "ДекорацияСостояниеОтправки" + Строка(Сч);
		ДекорацияСостояниеОтправки = Элементы.Найти(ИмяЭлемента);
		Если ДекорацияСостояниеОтправки <> Неопределено Тогда
			Если Строка.ПисьмоОтправлено Тогда
				ДекорацияСостояниеОтправки.Заголовок = НСтр("ru = 'код отправлен'");
				ДекорацияСостояниеОтправки.ЦветТекста = Новый Цвет(191, 191, 191);
			Иначе
				ДекорацияСостояниеОтправки.Заголовок = НСтр("ru = 'не указан e-mail'");
				ДекорацияСостояниеОтправки.ЦветТекста = Новый Цвет(255, 0, 0);
			КонецЕсли;
		КонецЕсли;
		Сч = Сч + 1;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти









