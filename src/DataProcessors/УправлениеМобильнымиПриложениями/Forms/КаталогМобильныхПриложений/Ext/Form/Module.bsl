
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновитьСписокПриложений();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМобильныеПриложения

&НаКлиенте
Процедура МобильныеПриложенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеСтроки = МобильныеПриложения.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("Ключ", ДанныеСтроки.Приложение);
	ОткрытьФорму("Справочник.МобильныеПриложения.Форма.ФормаЭлемента",
		ПараметрыОткрытияФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСписокПриложений()
	
	Выборка = Справочники.МобильныеПриложения.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ПометкаУдаления Тогда
			Продолжить;
		КонецЕсли;
		Приложение = МобильныеПриложения.Добавить();
		Приложение.Приложение = Выборка.Ссылка;
		Приложение.Превью = УправлениеМобильнымиПриложениями.ПревьюПриложения(Выборка.Ссылка, УникальныйИдентификатор);
		Приложение.НаименованиеПриложения = Выборка.Наименование;
		Приложение.КраткаяИнформация = Выборка.КраткаяИнформация;
		Приложение.НаименованиеВерсии = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Разработчик %1, Версия %2'"), Выборка.Разработчик, Выборка.ВерсияПриложения);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти


