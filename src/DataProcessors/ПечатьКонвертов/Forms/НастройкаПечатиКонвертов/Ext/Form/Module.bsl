
#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура СообщитьПользователюОсобенностиПечатиВВебБраузере(Форма)
	
	РазмерБумаги = РазмерБумаги(Форма);
	
	Если Форма.НаименованиеБраузера = "CHROME" Тогда
		ТекстСообщения = НСтр("ru='На следующем шаге, в окне предпросмотра, выберите размер бумаги'") + " " + РазмерБумаги;
		
	ИначеЕсли Форма.НаименованиеБраузера = "MSIE" Тогда
		ТекстСообщения = НСтр("ru='На следующем шаге выберите в Параметрах страницы размер бумаги'") + " " + РазмерБумаги;
		
	Иначе
		ТекстСообщения = НСтр("ru='На следующем шаге выберите в Свойствах принтера размер бумаги'") + " " + РазмерБумаги;
		
	КонецЕсли;
	
	Форма.Элементы.ТекстОсобенностиПечатиВВебКлиенте.Заголовок = ТекстСообщения;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РазмерБумаги(Форма)
	
	Если Форма.ФорматКонверта = Форма.КэшЗначений.ФорматыПочтовыхКонвертов.C4 Тогда
		Результат = "Envelope C4";
	ИначеЕсли Форма.ФорматКонверта = Форма.КэшЗначений.ФорматыПочтовыхКонвертов.C5 Тогда
		Результат = "Envelope C5";
	Иначе
		Результат = "Envelope DL";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВидимостьПодсказкиАдресаВПроизвольнойФорме()
	
	СпособОтображениеПодсказки = ?(РазрешитьАдресаВПроизвольнойФорме, ОтображениеПодсказки.ОтображатьСнизу, ОтображениеПодсказки.Нет);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаАдресВПроизвольнойФорме", "ОтображениеПодсказки", СпособОтображениеПодсказки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ФорматКонвертаПриИзменении(Форма)
	
	ФорматыПочтовыхКонвертов = Форма.КэшЗначений.ФорматыПочтовыхКонвертов;
	ФорматКонверта           = Форма.ФорматКонверта;
	Элементы                 = Форма.Элементы;
	
	Для Каждого Формат Из ФорматыПочтовыхКонвертов Цикл
		ЭтоВыбранныйФормат = Формат.Значение = ФорматКонверта;
		Элементы["Конверт" + Формат.Значение].Видимость = Не ЭтоВыбранныйФормат;
		Элементы["Конверт" + Формат.Значение + "Выбран"].Видимость = ЭтоВыбранныйФормат;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Форма.НаименованиеБраузера) Тогда
		СообщитьПользователюОсобенностиПечатиВВебБраузере(Форма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьВозможностьПечати(Знач ОбъектыПечати)
	
	Если ОбъектыПечати = Неопределено Тогда
		
		Возврат НСтр("ru ='Непосредственное открытие этой формы не предусмотрено.'");
		
	КонецЕсли;
	
	ОбъектыПечати = УдалитьГруппыИзОбъектовПечати(ОбъектыПечати);
	Если ОбъектыПечати.Количество() = 0 Тогда
		
		Возврат НСтр("ru ='Конверт может быть напечатан только для элементов справочника.'");
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

&НаСервере
Функция УдалитьГруппыИзОбъектовПечати(ОбъектыПечати)
	
	Если ТипЗнч(ОбъектыПечати[0]) <> Тип("СправочникСсылка.Контрагенты") Тогда
		Возврат ОбъектыПечати;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбъектыПечати", ОбъектыПечати);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	НЕ Контрагенты.ЭтоГруппа
	|	И Контрагенты.Ссылка В(&ОбъектыПечати)";
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса.ВыгрузитьКолонку("Ссылка");
	
КонецФункции

&НаСервере
Функция ПолучитьОбъектыПечати()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПолучитьИзВременногоХранилища(АдресОбъектовПечати);
	
КонецФункции

&НаСервере
Процедура РаботаСРеквизитомОрганизация()
	
	Если КэшЗначений.ФОИспользоватьНесколькоОрганизаций Тогда
		
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ СпрОрганизаций.Ссылка КАК Ссылка, Представление(СпрОрганизаций.Ссылка) КАК Представление ИЗ Справочник.Организации КАК СпрОрганизаций ГДЕ НЕ СпрОрганизаций.ПометкаУдаления");
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Элементы.Организация.СписокВыбора.Добавить(Выборка.Ссылка, Выборка.Представление);
			
		КонецЦикла;
		
	КонецЕсли;
	
	КэшЗначений.ВОбъектеПечатиЕстьОрганизация = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Параметры.ОбъектыПечати[0], "Организация");
	Если КэшЗначений.ВОбъектеПечатиЕстьОрганизация Тогда
		
		Элементы.Организация.СписокВыбора.Добавить(Справочники.Организации.ПустаяСсылка(), "<Указана в документе>");
		Организация = Справочники.Организации.ПустаяСсылка();
		
	Иначе
		
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТиповаяДлинаСтрокКонвертов()
	
	КэшЗначений.Вставить("ДлиныСтрок", Новый Соответствие);
	
	ПараметрыДлиныСтрокиC4 = Новый Структура;
	ПараметрыДлиныСтрокиC4.Вставить("ДлинаПервойСтрокиОтКого",	36);
	ПараметрыДлиныСтрокиC4.Вставить("ДлинаВторойСтрокиОтКого",	42);
	ПараметрыДлиныСтрокиC4.Вставить("ДлинаПервойСтрокиОткуда",	36);
	ПараметрыДлиныСтрокиC4.Вставить("ДлинаВторойСтрокиОткуда",	42);
	ПараметрыДлиныСтрокиC4.Вставить("ДлинаПервойСтрокиКому",	39);
	ПараметрыДлиныСтрокиC4.Вставить("ДлинаВторойСтрокиКому",	47);
	ПараметрыДлиныСтрокиC4.Вставить("ДлинаПервойСтрокиКуда",	39);
	ПараметрыДлиныСтрокиC4.Вставить("ДлинаВторойСтрокиКуда",	47);
	КэшЗначений.ДлиныСтрок.Вставить(КэшЗначений.ФорматыПочтовыхКонвертов.C4, ПараметрыДлиныСтрокиC4);
	
	ПараметрыДлиныСтрокиC5 = Новый Структура;
	ПараметрыДлиныСтрокиC5.Вставить("ДлинаПервойСтрокиОтКого",	26);
	ПараметрыДлиныСтрокиC5.Вставить("ДлинаВторойСтрокиОтКого",	32);
	ПараметрыДлиныСтрокиC5.Вставить("ДлинаПервойСтрокиОткуда",	26);
	ПараметрыДлиныСтрокиC5.Вставить("ДлинаВторойСтрокиОткуда",	32);
	ПараметрыДлиныСтрокиC5.Вставить("ДлинаПервойСтрокиКому",	31);
	ПараметрыДлиныСтрокиC5.Вставить("ДлинаВторойСтрокиКому",	47);
	ПараметрыДлиныСтрокиC5.Вставить("ДлинаПервойСтрокиКуда",	31);
	ПараметрыДлиныСтрокиC5.Вставить("ДлинаВторойСтрокиКуда",	47);
	КэшЗначений.ДлиныСтрок.Вставить(КэшЗначений.ФорматыПочтовыхКонвертов.C5, ПараметрыДлиныСтрокиC5);
	
	ПараметрыДлиныСтрокиDL = Новый Структура;
	ПараметрыДлиныСтрокиDL.Вставить("ДлинаПервойСтрокиОтКого",	33);
	ПараметрыДлиныСтрокиDL.Вставить("ДлинаВторойСтрокиОтКого",	41);
	ПараметрыДлиныСтрокиDL.Вставить("ДлинаПервойСтрокиОткуда", 33);
	ПараметрыДлиныСтрокиDL.Вставить("ДлинаВторойСтрокиОткуда", 41);
	ПараметрыДлиныСтрокиDL.Вставить("ДлинаПервойСтрокиКому",	43);
	ПараметрыДлиныСтрокиDL.Вставить("ДлинаВторойСтрокиКому",	47);
	ПараметрыДлиныСтрокиDL.Вставить("ДлинаПервойСтрокиКуда",	43);
	ПараметрыДлиныСтрокиDL.Вставить("ДлинаВторойСтрокиКуда",	47);
	КэшЗначений.ДлиныСтрок.Вставить(КэшЗначений.ФорматыПочтовыхКонвертов.DL, ПараметрыДлиныСтрокиDL);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьЗначенияПоУмолчанию()
	
	КэшЗначений = Новый Структура;
	КэшЗначений.Вставить("ФОИспользоватьНесколькоОрганизаций", ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций"));
	КэшЗначений.Вставить("ВОбъектеПечатиЕстьОрганизация", Ложь);
	КэшЗначений.Вставить("ВыполняетсяКомандаПродолжить", Ложь);
	
	ФорматыПочтовыхКонвертов = Новый Структура;
	ФорматыПочтовыхКонвертов.Вставить("C4", Перечисления.ФорматыПочтовыхКонвертов.C4);
	ФорматыПочтовыхКонвертов.Вставить("C5", Перечисления.ФорматыПочтовыхКонвертов.C5);
	ФорматыПочтовыхКонвертов.Вставить("DL", Перечисления.ФорматыПочтовыхКонвертов.DL);
	КэшЗначений.Вставить("ФорматыПочтовыхКонвертов", ФорматыПочтовыхКонвертов);
	
	ФорматКонверта = КэшЗначений.ФорматыПочтовыхКонвертов.C5;
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
	РаботаСРеквизитомОрганизация();
	
	РазрешитьАдресаВПроизвольнойФорме	= Ложь;
	ДополнительныеСтрокиВЗаголовках		= Ложь;
	ПечататьЛоготип						= Ложь;
	
	ТиповаяДлинаСтрокКонвертов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекстОшибки = ПроверитьВозможностьПечати(Параметры.ОбъектыПечати);
	Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияПоУмолчанию();
	
	АдресОбъектовПечати = ПоместитьВоВременноеХранилище(Параметры.ОбъектыПечати, УникальныйИдентификатор);
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Организация", "ОтображениеПодсказки", ОтображениеПодсказки.Нет);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияМобильныйКлиент", "Видимость", Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КонвертC4", "Ширина", 8);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КонвертC4Выбран", "Ширина", 8);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КонвертC5", "Ширина", 8);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КонвертC5Выбран", "Ширина", 8);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КонвертDL", "Ширина", 8);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КонвертDLВыбран", "Ширина", 8);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НаименованиеБраузера = РегламентированнаяОтчетностьКлиент.ВебБраузер();
	Если ЗначениеЗаполнено(НаименованиеБраузера) Тогда
		
		Элементы.ГруппаОсобенностиПечатиВВебКлиенте.Видимость = Истина;
		СообщитьПользователюОсобенностиПечатиВВебБраузере(ЭтотОбъект);
		
	КонецЕсли;
	
	Если НЕ КэшЗначений.ФОИспользоватьНесколькоОрганизаций Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Организация", "Видимость", Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если КэшЗначений.ВыполняетсяКомандаПродолжить = Ложь Тогда
		
		ЭтаФорма.СохраняемыеВНастройкахДанныеМодифицированы = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	Настройки.Вставить("ДлиныСтрок", КэшЗначений.ДлиныСтрок);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если НЕ КэшЗначений.ВОбъектеПечатиЕстьОрганизация Тогда
		
		Если НЕ ЗначениеЗаполнено(Организация) Тогда
			
			Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если РазрешитьАдресаВПроизвольнойФорме = Истина Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаАдресВПроизвольнойФорме", "ОтображениеПодсказки", ОтображениеПодсказки.ОтображатьСнизу);
		
	КонецЕсли;
	
	Если ФорматКонверта <> КэшЗначений.ФорматыПочтовыхКонвертов.C5 Тогда
		
		Если НЕ ЗначениеЗаполнено(ФорматКонверта) Тогда
			
			ФорматКонверта = КэшЗначений.ФорматыПочтовыхКонвертов.C5;
			
		КонецЕсли;
		
		ФорматКонвертаПриИзменении(ЭтотОбъект);
		
	КонецЕсли;
	
	ЗначениеИзНастроек = Настройки.Получить("ДлиныСтрок");
	Если ЗначениеИзНастроек <> Неопределено Тогда
		
		КэшЗначений.ДлиныСтрок = ЗначениеИзНастроек;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЭлементыФормы

&НаКлиенте
Процедура ФорматКонвертаС4ПриИзменении(Элемент)
	
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматКонвертаС5ПриИзменении(Элемент)
	
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматКонвертаDLПриИзменении(Элемент)
	
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонвертC4Нажатие(Элемент)
	
	ФорматКонверта = КэшЗначений.ФорматыПочтовыхКонвертов.C4;
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонвертC5Нажатие(Элемент)
	
	ФорматКонверта = КэшЗначений.ФорматыПочтовыхКонвертов.C5;
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонвертDLНажатие(Элемент)
	
	ФорматКонверта = КэшЗначений.ФорматыПочтовыхКонвертов.DL;
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечататьЛоготипРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, Организация);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьАдресаВПроизвольнойФормеПриИзменении(Элемент)
	
	ВидимостьПодсказкиАдресаВПроизвольнойФорме();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПродолжить(Команда)
	
	ОбъектыПечати = ПолучитьОбъектыПечати();
	
	Если Не ЗначениеЗаполнено(ОбъектыПечати) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПечати = Новый Структура();
	ПараметрыПечати.Вставить("Организация",						Организация);
	ПараметрыПечати.Вставить("ФорматКонверта",					ФорматКонверта);
	ПараметрыПечати.Вставить("ПечататьЛоготип",					ПечататьЛоготип);
	ПараметрыПечати.Вставить("ВидАдресаКонтрагента",			ВидАдресаКонтрагента);
	ПараметрыПечати.Вставить("КомандаПечати",					НСтр("ru='Конверт'"));
	ПараметрыПечати.Вставить("ЗаголовокФормы",					НСтр("ru='Печать конверта'"));
	ПараметрыПечати.Вставить("ДополнительныеСтрокиВЗаголовках", ДополнительныеСтрокиВЗаголовках);
	ПараметрыПечати.Вставить("ДлинаСтрок",						КэшЗначений.ДлиныСтрок);
	ПараметрыПечати.Вставить("РазрешитьАдресаВПроизвольнойФорме",РазрешитьАдресаВПроизвольнойФорме);
	
	Если ФорматКонверта = КэшЗначений.ФорматыПочтовыхКонвертов.C4 Тогда
		
		ПараметрыПечати.Вставить("РазмерСтраницы", "Envelope C4");
		
	ИначеЕсли ФорматКонверта = КэшЗначений.ФорматыПочтовыхКонвертов.C5 Тогда
		
		ПараметрыПечати.Вставить("РазмерСтраницы", "Envelope C5");
		
	ИначеЕсли ФорматКонверта = КэшЗначений.ФорматыПочтовыхКонвертов.DL Тогда
		
		ПараметрыПечати.Вставить("РазмерСтраницы", "Envelope DL");
		
	КонецЕсли;
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Обработка.ПечатьКонвертов", "Конверт", ОбъектыПечати, ЭтотОбъект, ПараметрыПечати);
	
	КэшЗначений.ВыполняетсяКомандаПродолжить = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаАдресВПроизвольнойФормеРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Обработка.ПечатьКонвертов.Форма.РазъяснениеТребованийЗаконодательства");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаДлиныСтрок(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ДлиныСтрок", КэшЗначений.ДлиныСтрок);
	ПараметрыОткрытия.Вставить("ФорматыПочтовыхКонвертов", КэшЗначений.ФорматыПочтовыхКонвертов);
	ПараметрыОткрытия.Вставить("ФорматКонверта", ФорматКонверта);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеРедактированияДлиныСтрок", ЭтотОбъект);
	ОткрытьФорму("Обработка.ПечатьКонвертов.Форма.НастройкаДлиныСтрок", ПараметрыОткрытия, ЭтаФорма, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеРедактированияДлиныСтрок(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура")
		И Результат.ОперацияВыполнена = Истина Тогда
		
		Результат.Свойство("ДлиныСтрок", КэшЗначений.ДлиныСтрок);
		ЭтаФорма.СохраняемыеВНастройкахДанныеМодифицированы = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
