
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗначениеВРеквизитФормы(Справочники.ЗаписиКалендаряСотрудника.ОписаниеРасширенногоВводаЗаписей(), 
		"ОписаниеРасширенногоВвода");
	
	Если Параметры.Свойство("ЗначенияЗаполнения") Тогда
		Параметры.ЗначенияЗаполнения.Свойство("Календарь", Календарь);
		Параметры.ЗначенияЗаполнения.Свойство("Начало", Начало);
		Параметры.ЗначенияЗаполнения.Свойство("Окончание", Окончание);
		Параметры.ЗначенияЗаполнения.Свойство("КолонкаКалендаря", КолонкаКалендаря);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КолонкаКалендаря) Тогда
		КолонкаКалендаря = Справочники.КолонкиКалендарейСотрудников.КолонкаНеобработанное(Календарь);
	КонецЕсли;
	
	СтрокаЗадачи = ОписаниеРасширенногоВвода.Вставить(0);
	СтрокаЗадачи.Представление = НСтр("ru = 'Задача'");
	СтрокаЗадачи.ИмяФормы = "Документ.ЗадачаСотрудника.Форма.ФормаДокумента";
	СтрокаЗадачи.ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", Новый Структура);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОписаниеРасширенногоВводаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыборТипаДела();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	ВыборТипаДела();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыборТипаДела()
	
	ПараметрыФормы = Новый Структура;
	СтрокаОписания = Элементы.ОписаниеРасширенногоВвода.ТекущиеДанные;

	ДанныеЗаписиКалендаря = Новый Структура;
	ДанныеЗаписиКалендаря.Вставить("Календарь",    Календарь);
	ДанныеЗаписиКалендаря.Вставить("Начало",       Начало);
	ДанныеЗаписиКалендаря.Вставить("Окончание",    Окончание);
	ДанныеЗаписиКалендаря.Вставить("Наименование", "");
	ДанныеЗаписиКалендаря.Вставить("Описание",     "");
	ДанныеЗаписиКалендаря.Вставить("ИмяФормы",     СтрокаОписания.ИмяФормы);
	ДанныеЗаписиКалендаря.Вставить("КолонкаКалендаря", КолонкаКалендаря);
	
	Если СтрокаОписания.ПараметрыФормы.Свойство("ЗначенияЗаполнения") Тогда
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", СтрокаОписания.ПараметрыФормы.ЗначенияЗаполнения);
	Иначе
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура);
	КонецЕсли;
	Если СтрокаОписания.Представление = "Задача" Тогда
		ПараметрыФормы.Вставить("СозданиеЗадачиКонтактЦентр", Истина);
	КонецЕсли;
	
	ПараметрыФормы.ЗначенияЗаполнения.Вставить("ДанныеЗаписиКалендаря", ДанныеЗаписиКалендаря);
	Закрыть(ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти
