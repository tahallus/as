
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("Отбор") Тогда
		ВызватьИсключение НСтр("ru = 'Обработка не предназначена для непосредственного использования.'");
	КонецЕсли;
	
	ОтборыДокументов = Новый ФиксированноеСоответствие(Обработки.ДокументыПоКритериюОтбора.ОтборыДокументов());
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	УстановитьКлючНастроек();
	
	ИмяКритерияОтбора = ИмяКритерияОтбора(Параметры);
	
	НастроитьЭлементыФормы(ИмяКритерияОтбора, Параметры);
	
	ЗаполнитьТаблицуЗапросов(ОбработкаОбъект, ИмяКритерияОтбора);
	
	ОбновитьСписокВидовДокументов();
	
	Параметры.Отбор.Свойство("Контрагент", Контрагент);
	Параметры.Отбор.Свойство("Договор", Договор);
	Параметры.Отбор.Свойство("Номенклатура", Номенклатура);
	
	ВосстановитьНастройки();
	
	ОбновитьТекстЗапроса();
	
	Если Параметры.Свойство("СформироватьПриОткрытии") И Параметры.СформироватьПриОткрытии Тогда
		
		ОбновитьТаблицуДокументовНаСервере();
		
	КонецЕсли;
	
	УстановитьВидимостьИДоступностьРМК();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВыполнитьОбработкуИзмененияДанных();
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	СоздатьДокументВыбранногоТипа(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборЗаказыИСчетаПриИзменении(Элемент)
	
	УстановитьОтборПоГруппе();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборНакладныеПриИзменении(Элемент)
	
	УстановитьОтборПоГруппе();

КонецПроцедуры

&НаКлиенте
Процедура ОтборОплатыПриИзменении(Элемент)
	
	УстановитьОтборПоГруппе();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборРозницаПриИзменении(Элемент)
	
	УстановитьОтборПоГруппе();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСобытияПриИзменении(Элемент)
	
	УстановитьОтборПоГруппе();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизвольныйОтборПриИзменении(Элемент)
	
	Если ПроизвольныйОтбор Тогда
		ОтборЗаказыИСчета = Ложь;
		ОтборНакладные = Ложь;
		ОтборОплаты = Ложь;
		ОтборРозница = Ложь;
		ОтборСобытия = Ложь;
	Иначе
		УстановитьОтборПоГруппе();
	КонецЕсли;
	
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьКоличествоДокументовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДополнитьСписок();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаДокументов

&НаКлиенте
Процедура ТаблицаДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	Если Копирование Тогда
		СкопироватьДокумент();
	Иначе
		СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ИзменитьДокумент();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	ПометитьНаУдалениеДокумент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастроитьСоставДокументов(Команда)
	
	РедактироватьСоставДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура SMS(Команда)
	
	СоздатьСобытиеПоКонтрагенту("СообщениеSMS");
	
КонецПроцедуры

&НаКлиенте
Процедура ЛичнаяВстреча(Команда)
	
	СоздатьСобытиеПоКонтрагенту("ЛичнаяВстреча");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписьСобытие(Команда)
	СоздатьСобытиеПоКонтрагенту("Запись");
КонецПроцедуры

&НаКлиенте
Процедура Прочее(Команда)
	
	СоздатьСобытиеПоКонтрагенту("Прочее");
	
КонецПроцедуры

&НаКлиенте
Процедура ТелефонныйЗвонок(Команда)
	
	СоздатьСобытиеПоКонтрагенту("ТелефонныйЗвонок");
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлектронноеПисьмо(Команда)
	
	СоздатьСобытиеПоКонтрагенту("ЭлектронноеПисьмо");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	
	ОбновитьТаблицуДокументовНаСервере(ВсеСтрокиОтображены);
	
КонецПроцедуры

&НаКлиенте
Процедура Провести(Команда)
	
	ВыполнитьГрупповоеПроведение(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменаПроведения(Команда)
	
	ВыполнитьГрупповоеПроведение(РежимЗаписиДокумента.ОтменаПроведения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИмяКритерияОтбора(Знач Параметры)
	
	Если Параметры.Отбор.Свойство("Договор") Тогда
		
		Возврат ДокументыПоДоговору();
		
	ИначеЕсли Параметры.Отбор.Свойство("Контрагент") Тогда
		
		Возврат ДокументыПоКонтрагенту();
		
	ИначеЕсли Параметры.Отбор.Свойство("Номенклатура") Тогда
		
		Возврат ДокументыПоНоменклатуре();
		
	Иначе
		
		ВызватьИсключение
		НСтр("ru = 'Параметр отбора установлен не верно: ожидается ""Контрагент"", ""Договор"" или ""Номенклатура"".'");
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура НастроитьЭлементыФормы(Знач ИмяКритерияОтбора, Знач Параметры)
	
	Если ИмяКритерияОтбора = ДокументыПоДоговору() Тогда
		
		Заголовок = НСтр("ru = 'Документы по договору'");
		ВключитьВидимостьГруппыИОтключитьВидимостьОстальных("ПродатьКупитьПоДоговору");
		
		ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВидДоговора");
		Если ВидДоговора = Перечисления.ВидыДоговоров.СПокупателем Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			ЭтотОбъект.Элементы,
			"ПодменюКупитьПоДоговору",
			"Видимость",
			Ложь);
		ИначеЕсли ВидДоговора = Перечисления.ВидыДоговоров.СПоставщиком Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			ЭтотОбъект.Элементы,
			"ПодменюПродатьПоДоговору",
			"Видимость",
			Ложь);
		КонецЕсли;
		
	ИначеЕсли ИмяКритерияОтбора = ДокументыПоКонтрагенту() Тогда
		
		Заголовок = НСтр("ru = 'Документы по контрагенту'");
		ВключитьВидимостьГруппыИОтключитьВидимостьОстальных("ПродатьКупитьПоКонтрагенту");
		
	ИначеЕсли ИмяКритерияОтбора = ДокументыПоНоменклатуре() Тогда
		
		Заголовок = НСтр("ru = 'Документы по номенклатуре'");
		ВключитьВидимостьГруппыИОтключитьВидимостьОстальных("ПродатьКупитьПоНоменклатуре");
		
	Иначе
		ВызватьИсключение
		НСтр("ru = 'Имя критерия отбора указано не верно.
		|Ожидается ""ДокументыПоДоговору"", ""ДокументыПоКонтрагенту"" или ""ДокументыПоНоменклатуре"".'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВключитьВидимостьГруппыИОтключитьВидимостьОстальных(Знач ВключитьВидимостьЭтойГруппы)
	
	ГруппыКоманд = Новый Массив;
	ГруппыКоманд.Добавить("ПродатьКупитьПоДоговору");
	ГруппыКоманд.Добавить("ПродатьКупитьПоКонтрагенту");
	ГруппыКоманд.Добавить("ПродатьКупитьПоНоменклатуре");
	
	Для Каждого ТекГруппаКоманд Из ГруппыКоманд Цикл
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		ЭтотОбъект.Элементы, ТекГруппаКоманд, "Видимость", ТекГруппаКоманд = ВключитьВидимостьЭтойГруппы);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокумент()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормы.Вставить("СписокДокументовДляСозданияНового", ПолучитьСписокДокументовДляСозданияНового());
	Если Элементы.ТаблицаДокументов.ТекущиеДанные <> Неопределено Тогда
		ПараметрыФормы.Вставить("ТипТекущегоДокумента", ТипЗнч(Элементы.ТаблицаДокументов.ТекущиеДанные.Документ));
	КонецЕсли;
	ОткрытьФорму("Обработка.ДокументыПоКритериюОтбора.Форма.СписокДокументовДляСозданияНового", ПараметрыФормы,
		ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СкопироватьДокумент()
	
	ДокументСсылка = Элементы.ТаблицаДокументов.ТекущиеДанные.Документ;
	ФормаОбъекта = "Документ." + ИмяОбъектаПоСсылке(ДокументСсылка) + ".ФормаОбъекта";
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначениеКопирования", ДокументСсылка);
	
	ОткрытьФорму(ФормаОбъекта, ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументВыбранногоТипа(ВыбранныйЭлемент)
	
	Если ТипЗнч(ВыбранныйЭлемент) <> Тип("ДанныеФормыСтруктура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ВыбранныйЭлемент.Свойство("Значение") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		НоменклатураВДокументахКлиент.СоздатьИзНоменклатуры(ЭтотОбъект, ВыбранныйЭлемент.Значение);
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "";
	
	Если ЗначениеЗаполнено(Договор) И Не ПроверитьКорректностьДоговора(ТекстВопроса, ВыбранныйЭлемент.Значение) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьЗавершение", ЭтотОбъект, ВыбранныйЭлемент);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;
	
	СоздатьЗавершение(КодВозвратаДиалога.Да, ВыбранныйЭлемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗавершение(Ответ, ВыбранныйЭлемент) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = ПараметрыФормыСоздаваемогоДокумента(ВыбранныйЭлемент.Значение);
	
	ФормаОбъекта = "Документ." + ИмяМетаданныхПоВидуДокумента(ВыбранныйЭлемент.Значение) + ".ФормаОбъекта";
	ОткрытьФорму(ФормаОбъекта, ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ПроверитьКорректностьДоговора(ТекстСообщения, ВидДокумента)
	Отказ = Ложь;
	Справочники.ДоговорыКонтрагентов.ПроверитьСоответствиеДоговораУсловиямДокумента(
		ТекстСообщения, 
		Договор,
		Документы[ИмяМетаданныхПоВидуДокумента(ВидДокумента)].ПустаяСсылка(), 
		Договор.Организация, 
		Договор.Владелец, 
		Отказ);
	Если Не ПустаяСтрока(ТекстСообщения) Тогда
		ТекстСообщения = ТекстСообщения + "
		| Хотите продолжить?";
	КонецЕсли;
	
	Возврат Не Отказ;
КонецФункции

&НаКлиенте
Процедура ИзменитьДокумент()
	
	ТекущиеДанные = Элементы.ТаблицаДокументов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(Неопределено, ТекущиеДанные.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеДокумент()
	
	ТекущиеДанные = Элементы.ТаблицаДокументов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСсылка = ТекущиеДанные.Документ;
	
	ПометкаУдаленияУстановлена = ТекущиеДанные.ПометкаУдаления;
	
	Если ПометкаУдаленияУстановлена Тогда
		ТекстВопроса = НСтр("ru='Снять с ""%1"" пометку на удаление?'");
	Иначе
		ТекстВопроса = НСтр("ru='Пометить ""%1"" на удаление?'");
	КонецЕсли;
	
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "%1", Строка(ТекущаяСсылка)); 
	
	ПараметрыВопроса = Новый Структура("ТекущаяСсылка, ПометкаУдаленияУстановлена", ТекущаяСсылка, ПометкаУдаленияУстановлена);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьПометкуУдаленияПослеВопроса", ЭтотОбъект, ПараметрыВопроса);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкуУдаленияПослеВопроса(Результат, ПараметрыВопроса) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСсылка = ПараметрыВопроса.ТекущаяСсылка;
	
	УстановитьСнятьПометкуУдаления(ТекущаяСсылка, ПараметрыВопроса.ПометкаУдаленияУстановлена);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСнятьПометкуУдаления(СсылкаНаОбъект, ПометкаУдаления)
	
	Попытка
		ДокументОбъект = СсылкаНаОбъект.ПолучитьОбъект();
		ДокументОбъект.УстановитьПометкуУдаления(НЕ ПометкаУдаления);
		СтрокаДанных = ТаблицаДокументов.НайтиПоИдентификатору(Элементы.ТаблицаДокументов.ТекущаяСтрока);
		СтрокаДанных.ИдентификаторКартинки = ?(ПометкаУдаления, 2, 1);
		СтрокаДанных.ПометкаУдаления = НЕ ПометкаУдаления; 
	Исключение
		ТекстСообщения = НСтр("ru = 'Не удалось %Операция пометку удаления документа ""%1""'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", Строка(СсылкаНаОбъект));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Операция", ?(ПометкаУдаления, "снять", "установить"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры

#Область Константы

&НаКлиентеНаСервереБезКонтекста
Функция ДокументыПоДоговору()
	
	Возврат "ДокументыПоДоговору";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДокументыПоКонтрагенту()
	
	Возврат "ДокументыПоКонтрагенту";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДокументыПоНоменклатуре()
	
	Возврат "ДокументыПоНоменклатуре";
	
КонецФункции

#КонецОбласти

#Область Настройки

&НаСервере
Процедура ВосстановитьНастройки()
	
	ЗначениеНастроек = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("Обработка.ДокументыПоКритериюОтбора", КлючНастроек);
	Если ТипЗнч(ЗначениеНастроек) = Тип("Соответствие") Тогда
		
		ЗначениеИзНастройки = ЗначениеНастроек["СписокВидовДокументов"];
		Если ТипЗнч(ЗначениеИзНастройки) = Тип("СписокЗначений") Тогда
			ПрименитьНастройкиКСпискуВидовДокументов(ЗначениеИзНастройки);
		КонецЕсли;
		
		ОтборЗаказыИСчета = ЗначениеНастроек["ОтборЗаказыИСчета"];
		ОтборНакладные = ЗначениеНастроек["ОтборНакладные"];
		ОтборОплаты = ЗначениеНастроек["ОтборОплаты"];
		ОтборСобытия = ЗначениеНастроек["ОтборСобытия"];
		ОтборРозница = ЗначениеНастроек["ОтборРозница"];
		ПроизвольныйОтбор = ЗначениеНастроек["ПроизвольныйОтбор"];
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	Настройки = Новый Соответствие;
	Настройки.Вставить("СписокВидовДокументов", СписокВидовДокументов);
	Настройки.Вставить("ОтборЗаказыИСчета", ОтборЗаказыИСчета);
	Настройки.Вставить("ОтборНакладные", ОтборНакладные);
	Настройки.Вставить("ОтборОплаты", ОтборОплаты);
	Настройки.Вставить("ОтборСобытия", ОтборСобытия);
	Настройки.Вставить("ОтборРозница", ОтборРозница);
	Настройки.Вставить("ПроизвольныйОтбор", ПроизвольныйОтбор);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("Обработка.ДокументыПоКритериюОтбора", КлючНастроек, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКлючНастроек()
	
	Если Параметры.Свойство("КлючНастроек") И Не ПустаяСтрока(Параметры.КлючНастроек) Тогда
		
		КлючНастроек = Параметры.КлючНастроек;
		
	Иначе
		
		КлючНастроек = "БезОтбора";
		
	КонецЕсли;
	
	КлючНастроек = КлючНастроек + "_" + Пользователи.ТекущийПользователь().УникальныйИдентификатор();
	
	// Проектное решение: для контрагентов и договоров настройки храним в разрезе элементов справочников,
	// для номенклатуры настройки храним без разреза по всему справочнику.
	Если Параметры.Свойство("Отбор") Тогда
		Если Параметры.Отбор.Свойство("Контрагент") Тогда
			КлючНастроек = КлючНастроек + "_" + Параметры.Отбор.Контрагент.УникальныйИдентификатор();
		ИначеЕсли Параметры.Отбор.Свойство("Договор") Тогда
			КлючНастроек = КлючНастроек + "_" + Параметры.Отбор.Договор.УникальныйИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПреобразоватьВЗначениеНастройки(СписокТиповПоГруппам)
	
	Результат = СписокВидовДокументов.Скопировать();
	
	Для Каждого СтрокаВида Из Результат Цикл
		Если СписокТиповПоГруппам.НайтиПоЗначению(СтрокаВида.Значение) <> Неопределено Тогда
			СтрокаВида.Пометка = Истина;
		Иначе
			СтрокаВида.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Поиск

&НаКлиенте
Функция ИнвертироватьСтроку(Строка)
	
	// АПК:163-выкл
	// АПК:1036-выкл
	СтрокаКириллица = "йцукенгшщзхъфывапролджэячсмитьбю.ё";
	СтрокаЛатиница = "qwertyuiop[]asdfghjkl;'zxcvbnm,./`";
	// АПК:163-вкл
	// АПК:1036-вкл
	Результат = "";
	Строка = НРег(Строка);
	Для Сч = 1 По СтрДлина(Строка) Цикл
		Символ = Сред(Строка, Сч, 1);
		Позиция = Найти(СтрокаЛатиница, Символ);
		Если Позиция <> 0 Тогда
			Результат = Результат + Сред(СтрокаКириллица, Позиция, 1);
		Иначе
			Позиция = Найти(СтрокаКириллица, Символ);
			Если Позиция <> 0 Тогда
				Результат = Результат + Сред(СтрокаЛатиница, Позиция, 1);
			Иначе
				Результат = Результат + Символ;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПолучитьМассивПоиска(Знач ИскомоеЗначение)
	
	Результат = Новый Массив;
	
	Если ИскомоеЗначение <> Неопределено Тогда
		ИскомоеЗначение = СтрЗаменить(ИскомоеЗначение,"%"," ");
		ИскомоеЗначение = СтрЗаменить(ИскомоеЗначение,"["," ");
		ИскомоеЗначение = СтрЗаменить(ИскомоеЗначение,"]"," ");
		ИскомоеЗначение = СтрЗаменить(ИскомоеЗначение,"_"," ");
		ИскомоеЗначение = СтрЗаменить(ИскомоеЗначение,""""," ");
		ИскомоеЗначение = СтрЗаменить(ИскомоеЗначение,"¶"," ");
		ИскомоеЗначение = СокрЛП(ИскомоеЗначение);
		
		ИскомоеЗначение = ИскомоеЗначение + " ";
		Пока Найти(ИскомоеЗначение,"  ") <> 0 Цикл
			ИскомоеЗначение = СтрЗаменить(ИскомоеЗначение,"  "," ");
		КонецЦикла;
		
		Позиция = Найти(ИскомоеЗначение," ");
		
		Пока Позиция <> 0 Цикл
			
			ИсходноеЗначение = Лев(ИскомоеЗначение,Позиция - 1);
			ИнвертированноеЗначение = ИнвертироватьСтроку(ИсходноеЗначение);
			
			Результат.Добавить(Новый Структура("ИсходноеЗначение,ИнвертированноеЗначение", ВРег(ИсходноеЗначение), ВРег(ИнвертированноеЗначение)));
			
			ИскомоеЗначение = Сред(ИскомоеЗначение, Позиция + 1);
			Позиция = Найти(ИскомоеЗначение," ");
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	Если Не ВсеСтрокиОтображены Тогда
		ДополнитьСписок();
	КонецЕсли;
	
	ИскомоеЗначение = "" + СтрокаПоиска;
	
	Если ПустаяСтрока(ИскомоеЗначение) Тогда
		Элементы.ТаблицаДокументов.ОтборСтрок = Неопределено;
	Иначе
		МассивПоиска =  ПолучитьМассивПоиска(СтрЗаменить(ИскомоеЗначение, ".", ","));
		Если МассивПоиска.Количество() = 0 Тогда
			Элементы.Товары.ОтборСтрок = Неопределено;
		Иначе
			Для Каждого ДанныеСтроки Из ТаблицаДокументов Цикл
				ТекстПоиска = СтрШаблон("%1 %2 %3 %4 %5 %6 %7 %8",
				ДанныеСтроки.Автор, // 1
				ДанныеСтроки.Номер, // 2
				ДанныеСтроки.Дата, // 3
				ДанныеСтроки.Ответственный, // 4
				ДанныеСтроки.Комментарий, // 5
				ДанныеСтроки.ТипДокумента, // 6
				ДанныеСтроки.ВидОперации, // 7
				Формат(ДанныеСтроки.СуммаДокумента, "ЧДЦ=2; ЧРД=,; ЧГ=")); // 8
				ТекстПоиска = СтрЗаменить(ВРег(ТекстПоиска), ".", ",");
				
				ДанныеСтроки.Отбор = Истина;
				Для Каждого ПараПоиска Из МассивПоиска Цикл
					Если Найти(ТекстПоиска, ПараПоиска.ИсходноеЗначение) + Найти(ТекстПоиска, ПараПоиска.ИнвертированноеЗначение) = 0 Тогда
						ДанныеСтроки.Отбор = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			Элементы.ТаблицаДокументов.ОтборСтрок = Новый ФиксированнаяСтруктура("Отбор", Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура ДополнитьСписок()
	ЗаполнитьСтрокиИзВременногоХранилища();
	ВыполнитьОбработкуИзмененияДанных();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяОбъектаПоСсылке(Ссылка)
	Возврат Ссылка.Метаданные().Имя;
КонецФункции

&НаКлиенте
Процедура СоздатьСобытиеПоКонтрагенту(ИмяТипаСобытия)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ТипСобытия", ПредопределенноеЗначение("Перечисление.ТипыСобытий." + ИмяТипаСобытия));
	ЗначенияЗаполнения.Вставить("Контрагент", Контрагент);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.Событие.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция СписокТипов()
	
	Результат = Новый СписокЗначений;
	
	Если ВключитьВсеТипыДокументов() Тогда
		Для Каждого ТекДокумент Из ТаблицаЗапросов Цикл
			Результат.Добавить(ТекДокумент.ИмяДокумента);
		КонецЦикла;
		Возврат Результат;
	КонецЕсли;
	
	Если ОтборЗаказыИСчета Тогда
		ДополнитьСписокТиповДокументамиИзГруппы(Результат, "ОтборЗаказыИСчета");
	КонецЕсли;
	
	Если ОтборНакладные Тогда
		ДополнитьСписокТиповДокументамиИзГруппы(Результат, "ОтборНакладные");
	КонецЕсли;

	Если ОтборОплаты Тогда
		ДополнитьСписокТиповДокументамиИзГруппы(Результат, "ОтборОплаты");
	КонецЕсли;
	
	Если ОтборРозница Тогда
		ДополнитьСписокТиповДокументамиИзГруппы(Результат, "ОтборРозница");
	КонецЕсли;
	
	Если ОтборСобытия Тогда
		ДополнитьСписокТиповДокументамиИзГруппы(Результат, "ОтборСобытия");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ДополнитьСписокТиповДокументамиИзГруппы(СписокТипов, ГруппаОтбора)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ГруппаОтбора", ГруппаОтбора);
	
	Для Каждого ТекСтрока Из ТаблицаЗапросов.НайтиСтроки(ПараметрыОтбора) Цикл
		СписокТипов.Добавить(ТекСтрока.ИмяДокумента);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ВключитьВсеТипыДокументов()
	
	Возврат Не Макс(ОтборЗаказыИСчета, ОтборНакладные, ОтборОплаты, ОтборРозница, ОтборСобытия);
	
КонецФункции

&НаКлиенте
Процедура УстановитьОтборПоГруппе()
	
	ЗначениеНастройки = ПреобразоватьВЗначениеНастройки(СписокТипов());
	ПрименитьНастройкиКСпискуВидовДокументов(ЗначениеНастройки);
	
	ВыполнитьОбработкуИзмененияДанных();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуЗапросов(ОбработкаОбъект, ИмяКритерияОтбора)

	ДополнительныеДокументы = Новый Массив;
	// В массив помещаются дополнительные документы, которых нет в критерии отбора
	// ДополнительныеДокументы.Добавить(Метаданные.Документы.РасходныйОрдер);

	ПоляШапки = Новый Массив;
	ПоляШапки.Добавить("СуммаДокумента");
	ПоляШапки.Добавить("Валюта");
	ПоляШапки.Добавить("ВидОперации");
	ПоляШапки.Добавить("Автор");
	ПоляШапки.Добавить("Ответственный");
	ПоляШапки.Добавить("Организация");
	ПоляШапки.Добавить("Комментарий");
	
	ОбработкаОбъект.ЗаполнитьТаблицуЗапросов(
	ТаблицаЗапросов,
	ИмяКритерияОтбора,
	ДополнительныеДокументы,
	ПоляШапки,
	АдресТаблицыСоответствия,
	УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТекстЗапроса()
	
	ТекстыЗапросовВТ = ТекстыЗапросовВТ();
	
	ТекстЗапросаФильтр = "ВЫБРАТЬ ПЕРВЫЕ 100 Документ, Дата ПОМЕСТИТЬ ВтТаблицаОтбора ИЗ ("
	+ ТекстыЗапросовВТ.ВремТекстЗапросаВт
	+ ") КАК ВЗ УПОРЯДОЧИТЬ ПО Дата УБЫВ";

	ТекстЗапросаФильтр = СтрЗаменить(
	ТекстЗапросаФильтр,
	"ВЫБРАТЬ ПЕРВЫЕ 100",
	СтрШаблон("ВЫБРАТЬ ПЕРВЫЕ %1", Формат(ПорцияДанных(), "ЧГ=")));
	
	ТекстЗапросаФильтрПолный = ТекстыЗапросовВТ.ВремТекстЗапросаПолный;
	
	ТекстЗапросаПоДокументам = ТекстыЗапросовВТ.ВремТекстЗапросаДанные;
	
КонецПроцедуры

&НаСервере
Функция ТекстыЗапросовВТ()
	
	ВремТекстЗапросаВт = Новый Массив;
	ВремТекстЗапросаДанные = Новый Массив;
	
	Для Каждого ТекСтрокаТаблицы Из ТаблицаЗапросов.НайтиСтроки(Новый Структура("Использовать", Истина)) Цикл
		
		ВремТекстЗапросаВт.Добавить(ТекСтрокаТаблицы.ТекстЗапроса);
		ВремТекстЗапросаДанные.Добавить(ТекСтрокаТаблицы.ТекстЗапросаВыборка);
		
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ВремТекстЗапросаВт", СтрСоединить(ВремТекстЗапросаВт, " ОБЪЕДИНИТЬ ВСЕ " + Символы.ПС));
	Результат.Вставить("ВремТекстЗапросаПолный", Результат.ВремТекстЗапросаВт);
	
	ПозицияПоместить = СтрНайти(ВРег(Результат.ВремТекстЗапросаПолный), ВРег("//%ПОМЕСТИТЬ%"));
	Если ПозицияПоместить > 0 Тогда
		Результат.ВремТекстЗапросаПолный = Лев(Результат.ВремТекстЗапросаПолный, ПозицияПоместить - 1)
		+ " ПОМЕСТИТЬ ВтТаблицаОтбора "
		+ Сред(Результат.ВремТекстЗапросаПолный, ПозицияПоместить + СтрДлина("//%ПОМЕСТИТЬ%"));
	КонецЕсли;
	
	Результат.Вставить("ВремТекстЗапросаДанные", СтрСоединить(ВремТекстЗапросаДанные, " ОБЪЕДИНИТЬ ВСЕ " + Символы.ПС));
	
	ПозицияВыбрать = СтрНайти(ВРег(Результат.ВремТекстЗапросаДанные), ВРег("ВЫБРАТЬ"));
	Если ПозицияВыбрать > 0 Тогда
		Результат.ВремТекстЗапросаДанные = "ВЫБРАТЬ РАЗРЕШЕННЫЕ "
		+ Сред(Результат.ВремТекстЗапросаДанные, ПозицияВыбрать + СтрДлина("ВЫБРАТЬ"))
		+ "УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Документ
		|";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьПризнакИспользованияВидаДокумента()

	Для Каждого СтрокаТаб Из ТаблицаЗапросов Цикл

		ЭлементСписка = СписокВидовДокументов.НайтиПоЗначению(СтрокаТаб.ИмяДокумента);
		Если ЭлементСписка <> Неопределено Тогда
			СтрокаТаб.Использовать = ЭлементСписка.Пометка;
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокВидовДокументов()

	СписокВидовДокументов.Очистить();
	Для Каждого Строка Из ТаблицаЗапросов Цикл
		СписокВидовДокументов.Добавить(Строка.ИмяДокумента, Строка.СинонимДокумента, Строка.Использовать);
	КонецЦикла;

	СписокВидовДокументов.СортироватьПоЗначению(НаправлениеСортировки.Возр);

КонецПроцедуры

&НаСервере
Процедура ПрименитьНастройкиКСпискуВидовДокументов(ЗначениеНастройки)
	
	ПереформироватьЗапрос = Ложь;
	Для Каждого Элемент Из ЗначениеНастройки Цикл
		
		ЭлементСписка = СписокВидовДокументов.НайтиПоЗначению(Элемент.Значение);
		Если ЭлементСписка <> Неопределено И ЭлементСписка.Пометка <> Элемент.Пометка Тогда
			
			ЭлементСписка.Пометка = Элемент.Пометка;
			ПереформироватьЗапрос = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПереформироватьЗапрос Тогда
		
		УстановитьПризнакИспользованияВидаДокумента();
		
		ОбновитьТекстЗапроса();
		
		СохранитьНастройки();
		
		ОбновитьТаблицуДокументовНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьСоставДокументов()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СписокВидовДокументов", СписокВидовДокументов);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("РедактироватьСоставДокументовЗавершение", ЭтотОбъект);
	
	ОткрытьФорму(
	"Обработка.ДокументыПоКритериюОтбора.Форма.НастройкаСоставаВидовДокументов",
	ПараметрыФормы,
	ЭтотОбъект,,,,
	ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьСоставДокументовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("СписокЗначений") Тогда
		ПрименитьНастройкиКСпискуВидовДокументов(Результат);
	КонецЕсли;
	
	ВыполнитьОбработкуИзмененияДанных();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицуДокументовНаСервере(ВсеСтроки = Ложь)
	
	Если ПустаяСтрока(ТекстЗапросаПоДокументам) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Необходимо настроить состав документов'"));
		Возврат;
	КонецЕсли;
	
	ВсеСтрокиОтображены = Ложь;

	Запрос = Новый Запрос(ТекстЗапросаФильтр);
	Если ЗначениеЗаполнено(Договор) Тогда
		Запрос.УстановитьПараметр("Параметр", Договор);
	ИначеЕсли ЗначениеЗаполнено(Контрагент) Тогда
		Запрос.УстановитьПараметр("Параметр", Контрагент);
	ИначеЕсли ЗначениеЗаполнено(Номенклатура) Тогда
		Запрос.УстановитьПараметр("Параметр", Номенклатура);
	Иначе
		ВызватьИсключение НСтр("ru = 'Не указан параметр для отбора по документам.'");
	КонецЕсли;
	Запрос.МенеджерВременныхТаблиц  = Новый МенеджерВременныхТаблиц;
	
	УстановитьОграниченияПоВидамОпераций(Запрос);
	
	Если ВсеСтроки Тогда
		Запрос.Текст = ТекстЗапросаФильтрПолный;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);

	Запрос.Текст = ТекстЗапросаПоДокументам;
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ТаблицаДокументов.Загрузить(Результат);
	
	ПараметрыЗапроса = Запрос.Параметры;

КонецПроцедуры

&НаСервере
Процедура УстановитьОграниченияПоВидамОпераций(Запрос)
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаСоответствия = ПолучитьИзВременногоХранилища(АдресТаблицыСоответствия);
	
	ДоступныеПрофили = ДоступныеПрофилиПользователя();
	
	УточняемыеОбъекты = УточняемыеТипыДокументов();
	
	СоответствиеПараметров = Новый Соответствие;
	
	Для Каждого ДоступныйПрофиль Из ДоступныеПрофили Цикл
		
		Для Каждого УточняемыйОбъект Из УточняемыеОбъекты Цикл
			
			РезультатПоиска = ТаблицаСоответствия.НайтиСтроки(
			Новый Структура("Профиль,Объект", ДоступныйПрофиль, УточняемыйОбъект.Значение));
			
			МассивДопустимыхВидовОпераций = СоответствиеПараметров[УточняемыйОбъект.Ключ];
			
			Если МассивДопустимыхВидовОпераций = Неопределено Тогда
				МассивДопустимыхВидовОпераций = Новый Массив;
			КонецЕсли;
			
			Если РезультатПоиска.Количество() Тогда
				
				Для Каждого СтрокаВидОперации Из РезультатПоиска Цикл
					Если МассивДопустимыхВидовОпераций.Найти(СтрокаВидОперации.ВидОперации) = Неопределено Тогда
						МассивДопустимыхВидовОпераций.Добавить(СтрокаВидОперации.ВидОперации);
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
			СоответствиеПараметров.Вставить(УточняемыйОбъект.Ключ, МассивДопустимыхВидовОпераций);
			
		КонецЦикла;
	КонецЦикла;
		
	Если ДоступныеПрофили.Количество() = 0 Тогда
		ЗаполнитьВидыОперацийПриРасширеннойНастройкеПрофилей(СоответствиеПараметров);
	КонецЕсли;
	
	Для Каждого ПараметрЗапроса Из СоответствиеПараметров Цикл 
		Запрос.УстановитьПараметр("МассивДопустимыхВидовОпераций" + ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидыОперацийПриРасширеннойНастройкеПрофилей(СоответствиеПараметров)
	
	РазрешеноДобавлениеРН = ПравоДоступа("Добавление", Метаданные.Документы.РасходнаяНакладная);
	РазрешеноДобавлениеПН = ПравоДоступа("Добавление", Метаданные.Документы.ПриходнаяНакладная);
	
	ВидыОперацийРН = Перечисления.ВидыОперацийРасходнаяНакладная;
	ВидыОперацийПН = Перечисления.ВидыОперацийПриходнаяНакладная;
	
	МассивДопустимыхВидовОперацийРН = Новый Массив;
	Если РазрешеноДобавлениеРН Тогда
		Для Каждого ВидОперацииРН Из ВидыОперацийРН Цикл 	
			МассивДопустимыхВидовОперацийРН.Добавить(ВидОперацииРН);
		КонецЦикла;
	КонецЕсли;
	СоответствиеПараметров.Вставить("РасходнаяНакладная", МассивДопустимыхВидовОперацийРН);
	
	МассивДопустимыхВидовОперацийПН = Новый Массив;
	
	Если РазрешеноДобавлениеПН Тогда
		Для Каждого ВидОперацииПН Из ВидыОперацийПН Цикл 	
			МассивДопустимыхВидовОперацийПН.Добавить(ВидОперацииПН);
		КонецЦикла;
	КонецЕсли;
	
	СоответствиеПараметров.Вставить("ПриходнаяНакладная", МассивДопустимыхВидовОперацийПН);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЗадание(ПараметрыЗадания, ИмяПроцедуры)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
	ИмяПроцедуры,
	ПараметрыЗадания,
	ПараметрыВыполнения);

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьФоновоеЗадание(ПараметрыЗадания)
	
	Задание = ПолучитьЗадание(ПараметрыЗадания, "Обработки.ДокументыПоКритериюОтбора.ЗапуститьФормированиеСпискаДокументовПоКонтрагенту");
	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
	Задание,
	Новый ОписаниеОповещения("ЗаполнитьАдресВременногоХранилища", ЭтотОбъект),
	ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАдресВременногоХранилища(Результат, Параметры) Экспорт
	
	АдресВременногоХранилища = "";
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	АдресВременногоХранилища = Результат.АдресРезультата;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокиИзВременногоХранилища()
	
	Если ЗначениеЗаполнено(АдресВременногоХранилища) Тогда
		ВременноеХранилище = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
		Если ВременноеХранилище.Количество() = 0 Тогда
			ОбновитьТаблицуДокументовНаСервере(Истина);
			Возврат;
		КонецЕсли;
	Иначе
		ОбновитьТаблицуДокументовНаСервере(Истина);
		Возврат;
	КонецЕсли;

	ТаблицаДокументов.Загрузить(ВременноеХранилище);
	
	ВсеСтрокиОтображены = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДоступныеПрофилиПользователя()
	
	Пользователь = Пользователи.АвторизованныйПользователь();
	
	Если УправлениеДоступомСлужебный.УпрощенныйИнтерфейсНастройкиПравДоступа() Тогда
		
		Запрос = Новый Запрос;
		
		МассивИсключений = Новый Массив;
		// ИдентификаторПрофиляРедактированиеЦенДокументов
		МассивИсключений.Добавить(УправлениеДоступомУНФ.ПрофильРедактированиеЦенДокументов().УИД);
		// ИдентификаторПрофиляРедактированиеНоменклатуры
		МассивИсключений.Добавить(УправлениеДоступомУНФ.ПрофильРедактированиеНоменклатуры().УИД);
		// ИдентификаторПрофиляВозвратыОтПокупателей
		МассивИсключений.Добавить(УправлениеДоступомУНФ.ПрофильВозвратыОтПокупателей().УИД);
		// ИдентификаторПрофиляВозвратыПоставщикам
		МассивИсключений.Добавить(УправлениеДоступомУНФ.ПрофильВозвратыПоставщикам().УИД);
		// ИдентификаторПрофиляСинхронизацияДанных
		МассивИсключений.Добавить(УправлениеДоступомУНФ.ПрофильСинхронизацияДанных().УИД);
		// СебестоимостьВОтчетеПродажи
		МассивИсключений.Добавить(УправлениеДоступомУНФ.ПрофильСебестоимостьВОтчетеПродажи().УИД);
		
		Запрос.УстановитьПараметр("Пользователь", Пользователь);
		Запрос.УстановитьПараметр("ОтборПрофилейТолькоТекущегоПользователя", Истина);
		Запрос.УстановитьПараметр("МассивИсключений", МассивИсключений);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Профили.ИдентификаторПоставляемыхДанных
		|ИЗ
		|	Справочник.ПрофилиГруппДоступа КАК Профили
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступа КАК ГруппыДоступа
		|		ПО Профили.Ссылка = ГруппыДоступа.Профиль
		|			И (НЕ(ГруппыДоступа.Пользователь <> &Пользователь))
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступа.Пользователи КАК ГруппыДоступаПользователи
		|		ПО (ГруппыДоступа.Ссылка = ГруппыДоступаПользователи.Ссылка)
		|ГДЕ
		|	НЕ Профили.ПометкаУдаления
		|	И НЕ Профили.ИдентификаторПоставляемыхДанных В (&МассивИсключений)
		|	И НЕ(&ОтборПрофилейТолькоТекущегоПользователя = ИСТИНА
		|				И ГруппыДоступаПользователи.Ссылка ЕСТЬ NULL)
		|	И ГруппыДоступа.Пользователь = &Пользователь";
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Пользователь", Пользователь);
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ГруппыДоступаПользователи.Ссылка.Профиль.ИдентификаторПоставляемыхДанных КАК ИдентификаторПоставляемыхДанных
		|ИЗ
		|	Справочник.ГруппыДоступа.Пользователи КАК ГруппыДоступаПользователи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ГруппыПользователей
		|		ПО (ГруппыПользователей.Ссылка = ГруппыДоступаПользователи.Пользователь)
		|ГДЕ
		|	(ГруппыДоступаПользователи.Пользователь = &Пользователь
		|			ИЛИ ГруппыПользователей.Пользователь = &Пользователь)";
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	ОписанияПрофилей = Справочники.ПрофилиГруппДоступа.ОписаниеПоставляемыхПрофилей().ОписанияПрофилей;
	
	ДоступныеПрофили = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		ОписаниеПрофиля = ОписанияПрофилей.Получить(Строка(Выборка.ИдентификаторПоставляемыхДанных));
		Если ОписаниеПрофиля = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ДоступныеПрофили.Добавить(ОписаниеПрофиля.Имя);
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ДоступныеПрофили;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция УточняемыеТипыДокументов()
	
	Результат = Новый Соответствие;
	Результат.Вставить("РасходнаяНакладная", Тип("ДокументСсылка.РасходнаяНакладная"));
	Результат.Вставить("ПриходнаяНакладная", Тип("ДокументСсылка.ПриходнаяНакладная"));
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьОбработкуИзмененияДанных()
	
	Если НЕ ВсеСтрокиОтображены Тогда
		ВсеСтрокиОтображены = ТаблицаДокументов.Количество() < ПорцияДанных();
	КонецЕсли;
	
	Если НЕ ВсеСтрокиОтображены Тогда
		ПараметрыЗадания = Новый Структура;
		ПараметрыЗадания.Вставить("ТекстЗапросаФильтрПолный", ТекстЗапросаФильтрПолный);
		ПараметрыЗадания.Вставить("ТекстЗапросаПоДокументам", ТекстЗапросаПоДокументам);
		ПараметрыЗадания.Вставить("ПараметрыЗапроса", ПараметрыЗапроса);
		ПараметрыЗадания.Вставить("ИдентификаторФормы", УникальныйИдентификатор);
		ЗапуститьФоновоеЗадание(ПараметрыЗадания);
	КонецЕсли;
	
	НастроитьВидимостьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементовФормы()
	
	Если ПроизвольныйОтбор Тогда
		Элементы.НастроитьСоставДокументов.Доступность = Истина;
		Элементы.ОтборЗаказыИСчета.Доступность = Ложь;
		Элементы.ОтборНакладные.Доступность = Ложь;
		Элементы.ОтборОплаты.Доступность = Ложь;
		Элементы.ОтборРозница.Доступность = Ложь;
		Элементы.ОтборСобытия.Доступность = Ложь;
	Иначе
		Элементы.НастроитьСоставДокументов.Доступность = Ложь;
		Элементы.ОтборЗаказыИСчета.Доступность = Истина;
		Элементы.ОтборНакладные.Доступность = Истина;
		Элементы.ОтборОплаты.Доступность = Истина;
		Элементы.ОтборРозница.Доступность = Истина;
		Элементы.ОтборСобытия.Доступность = Истина;
	КонецЕсли;
	
	СоответствиеРезультат = Новый Соответствие;
	
	Для Каждого Строка Из ТаблицаЗапросов Цикл
		ИмяЭлементаФормы = ОтборыДокументов.Получить(Строка.ИмяДокумента);
		Если ИмяЭлементаФормы <> Неопределено
			И СоответствиеРезультат[ИмяЭлементаФормы] = Неопределено Тогда
			СоответствиеРезультат.Вставить(ИмяЭлементаФормы, Истина);
		КонецЕсли;
	КонецЦикла;
	
	ЭлементыФормы = Новый Массив;
	ЭлементыФормы.Добавить("ОтборЗаказыИСчета");
	ЭлементыФормы.Добавить("ОтборНакладные");
	ЭлементыФормы.Добавить("ОтборОплаты");
	
	Для Каждого ЭлементФормы Из ЭлементыФормы Цикл
		Элементы[ЭлементФормы].Видимость = СоответствиеРезультат[ЭлементФормы] <> Неопределено;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
	ЭтотОбъект.Элементы,
	"ОтборРозница",
	"Видимость",
	ЗначениеЗаполнено(Номенклатура));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
	ЭтотОбъект.Элементы,
	"ОтборСобытия",
	"Видимость",
	ЗначениеЗаполнено(Контрагент));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПорцияДанных()
	Возврат 100;
КонецФункции

&НаКлиенте
Процедура НастроитьВидимостьЭлементов()
	
	Если ВсеСтрокиОтображены Тогда
		Элементы.НадписьКоличествоДокументов.Видимость = Ложь;
	Иначе
		Элементы.НадписьКоличествоДокументов.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьИДоступностьРМК()
	
	Если УправлениеДоступомУНФ.ЕстьПрофильРабочееМестоКассира() Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПодменюСобытия", "Видимость", Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыФормыСоздаваемогоДокумента(ВидДокумента)
	
	ПараметрыФормы = Новый Структура;
	
	ТипУточняемогоДокумента = УточняемыеТипыДокументов().Получить(ВидДокумента);
	Если ТипУточняемогоДокумента = Тип("ДокументСсылка.РасходнаяНакладная") И НЕ РазрешеныПродажи() Тогда
		ПараметрыФормы.Вставить("ВидОперацииВозврат", Истина);
	ИначеЕсли ТипУточняемогоДокумента = Тип("ДокументСсылка.ПриходнаяНакладная") И НЕ РазрешеныЗакупки() Тогда
		ПараметрыФормы.Вставить("ВидОперацииВозврат", Истина);
	КонецЕсли;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Контрагент", Контрагент);
	Если ЗначениеЗаполнено(Договор) Тогда
		ЗначенияЗаполнения.Вставить("Договор", Договор);
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор, "Владелец, Организация");
		ЗначенияЗаполнения.Вставить("Контрагент", РеквизитыДоговора.Владелец);
		ЗначенияЗаполнения.Вставить("Организация", РеквизитыДоговора.Организация);
	КонецЕсли;
	Если ВидДокумента = "ЗаказНаряд" Тогда
		ЗначенияЗаполнения.Вставить("ВидОперации", Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаСервере
Функция РазрешеныПродажи()
	
	Возврат ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.СчетНаОплату)
		И ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.ЗаказПокупателя)
		И ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.РасходнаяНакладная)
		И ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.АктВыполненныхРабот);
	
КонецФункции

&НаСервере
Функция РазрешеныЗакупки()
	
	Возврат ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.ЗаказПоставщику)
		И ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.ПриходнаяНакладная);
	
КонецФункции

&НаКлиенте
Функция ПолучитьСписокДокументовДляСозданияНового()
	
	ДокументыИсключение = Новый Массив;
	ДокументыИсключение.Добавить("Событие");
	ДокументыИсключение.Добавить("ЧекККМВозврат");
	
	СписокРезультат = Новый СписокЗначений;
	Для каждого ЭлементСписка Из СписокВидовДокументов Цикл
		Если ДокументыИсключение.Найти(ЭлементСписка.Значение) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СписокРезультат.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	СписокРезультат.Добавить("ЗаказНаряд", НСтр("ru='Заказ-наряд'"));
	СписокРезультат.СортироватьПоЗначению();
	
	Возврат СписокРезультат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяМетаданныхПоВидуДокумента(ВидДокумента)
	
	Если ВидДокумента = "ЗаказНаряд" Тогда
		Возврат "ЗаказПокупателя";
	Иначе
		Возврат ВидДокумента;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьГрупповоеПроведение(РежимЗаписи)
	
	ОчиститьСообщения();
	
	ВыбранныеДокументы = Новый Массив;
	Для каждого Эл Из Элементы.ТаблицаДокументов.ВыделенныеСтроки Цикл
		ВыбранныеДокументы.Добавить(ТаблицаДокументов.НайтиПоИдентификатору(Эл).Документ);
	КонецЦикла;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ВыбранныеДокументы", ВыбранныеДокументы);
	ПараметрыЗадания.Вставить("РежимЗаписи", РежимЗаписи);
	
	Задание = ПолучитьЗадание(ПараметрыЗадания, "Обработки.ДокументыПоКритериюОтбора.ГрупповоеПроведениеДокументов");
	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Ложь;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
	Задание,
	Новый ОписаниеОповещения("ВыполнитьГрупповоеПроведениеЗавершение", ЭтотОбъект),
	ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьГрупповоеПроведениеЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.АдресРезультата) Тогда
		РезультатВыполнения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		ПоказатьОповещениеПользователя(
		СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
		НСтр("ru=';Изменен %1 документ;;Изменено %1 документа;Изменено %1 документов;Изменено %1 документа'"),
		РезультатВыполнения.ИзмененоДокументов));
		
		Для Каждого Тип Из РезультатВыполнения.ИзмененныеТипы Цикл
			ОповеститьОбИзменении(Тип);
		КонецЦикла;
	КонецЕсли;
	
	ОбновитьТаблицуДокументовНаСервере(ВсеСтрокиОтображены);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
