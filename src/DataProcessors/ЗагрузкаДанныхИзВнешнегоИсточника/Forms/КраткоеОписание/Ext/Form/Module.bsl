
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьОписание("КраткоеОписание");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОткрытьФормуСправочникаИВызватьДиалогЗагрузки(ИмяСправочника, ПараметрыОткрытия)
	
	ИмяФормыСпискаСправочника = "Справочник." + ИмяСправочника + ".ФормаСписка";
	
	ФормаСправочника = ПолучитьФорму(ИмяФормыСпискаСправочника);
	Если ФормаСправочника.Открыта() Тогда
		
		ФормаСправочника.Активизировать();
		ФормаСправочника.ПодключитьОбработчикОжидания("ПоказатьПомощникЗагрузкиДанныхИзВнешнегоИсточника", 0.1, True);
		
	Иначе
		
		ОткрытьФорму(ИмяФормыСпискаСправочника, ПараметрыОткрытия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КраткоеОписаниеПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДанныеСобытия.Element.id) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОткрытия = Новый Структура("ЭтоЗагрузкаИзВнешнегоИсточника", Истина);
		
		ИдентификаторКоманды = ДанныеСобытия.Element.id;
		Если СтрНайти(ИдентификаторКоманды, "Контрагенты") > 0 Тогда
			
			ОткрытьФормуСправочникаИВызватьДиалогЗагрузки("Контрагенты", ПараметрыОткрытия);
			
		ИначеЕсли СтрНайти(ИдентификаторКоманды, "Номенклатура") > 0 Тогда
			
			ОткрытьФормуСправочникаИВызватьДиалогЗагрузки("Номенклатура", ПараметрыОткрытия);
			
		ИначеЕсли СтрНайти(ИдентификаторКоманды, "ЦеныНоменклатуры") > 0 Тогда
			
			ОткрытьФормуСправочникаИВызватьДиалогЗагрузки("ВидыЦен", ПараметрыОткрытия);
			
		ИначеЕсли СтрНайти(ИдентификаторКоманды, "КраткоеОписание") > 0 Тогда
			
			ЗаполнитьОписание("КраткоеОписание");
			
		ИначеЕсли СтрНайти(ИдентификаторКоманды, "БыстрыйСтарт") > 0 Тогда
			
			ОткрытьБыстрыйСтарт();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьОписание(ИмяМакета)
	
	Описание = Обработки.ЗагрузкаДанныхИзВнешнегоИсточника.ПолучитьМакет(ИмяМакета).ПолучитьТекст();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьБыстрыйСтарт()
	
	НастройкиЗагрузкиДанных = Новый Структура;
	ПолучитьНастройки(НастройкиЗагрузкиДанных);
	ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.ПоказатьФормуЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных,, ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ПолучитьНастройки(НастройкиЗагрузкиДанных)

	
	// СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
	ЗагрузкаДанныхИзВнешнегоИсточника.ПриСозданииНаСервере(Метаданные.Справочники.Номенклатура, НастройкиЗагрузкиДанных, Неопределено);
	// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
	
	НастройкиЗагрузкиДанных.Вставить("ИмяМакетаСШаблоном", "ЗагрузкаИзФайла");
	НастройкиЗагрузкиДанных.Вставить("НачальнаяЗагрузка", Истина);
	НастройкиЗагрузкиДанных.Вставить("ОписаниеСтрокиВыбора", Новый Структура("ПолноеИмяОбъектаМетаданных, Тип", "Номенклатура", "ПрикладнаяЗагрузка"));
	
КонецПроцедуры

#КонецОбласти

