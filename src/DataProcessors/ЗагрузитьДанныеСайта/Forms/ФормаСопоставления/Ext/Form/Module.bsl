
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДанныеРезультат = ПолучитьИзВременногоХранилища(Параметры.АдресВременногоХранилища);
	
	Объект.СопоставлениеКатегорий.Очистить();
	Для Каждого Строка Из ДанныеРезультат.СопоставлениеКатегорий Цикл
		ЗаполнитьЗначенияСвойств(Объект.СопоставлениеКатегорий.Добавить(), Строка);
	КонецЦикла;
	
	Объект.СопоставлениеТоваров.Очистить();
	Для Каждого Строка Из ДанныеРезультат.СопоставлениеТоваров Цикл
		ЗаполнитьЗначенияСвойств(Объект.СопоставлениеТоваров.Добавить(), Строка);
	КонецЦикла;
	
	Объект.СопоставлениеСвойств.Очистить();
	Для Каждого Строка Из ДанныеРезультат.СопоставлениеСвойств Цикл
		ЗаполнитьЗначенияСвойств(Объект.СопоставлениеСвойств.Добавить(), Строка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьТехнчиескуюИнформациюПриИзменении(Элемент)
	
	Элементы.СопоставлениеКатегорийТехническаяИнформация.Видимость = ОтображатьТехническуюИнформацию;
	Элементы.СопоставлениеТоваровТехническаяИнформация.Видимость = ОтображатьТехническуюИнформацию;
	Элементы.СопоставлениеСвойствТехническаяИнформация.Видимость = ОтображатьТехническуюИнформацию;
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеКатегорийПриАктивизацииСтроки(Элемент)
	
	ОтборСтрок = Новый ФиксированнаяСтруктура("ТипТовара" , Элементы.СопоставлениеКатегорий.ТекущиеДанные.Ид);
	
	Элементы.СопоставлениеТоваров.ОтборСтрок = ОтборСтрок;
	Элементы.СопоставлениеСвойств.ОтборСтрок = ОтборСтрок;
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеТаблицаПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеТаблицаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьСопоставление(Команда)
	
	СохранитьСопоставлениеНаСервере();
	
	Закрыть(АдресВременногоХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьСопоставлениеНаСервере()
	
	СтруктураСопоставления = Новый Структура;
	СтруктураСопоставления.Вставить("СопоставлениеКатегорий" , ОбщегоНазначения.ТаблицаЗначенийВМассив(Объект.СопоставлениеКатегорий.Выгрузить()));
	СтруктураСопоставления.Вставить("СопоставлениеТоваров" , ОбщегоНазначения.ТаблицаЗначенийВМассив(Объект.СопоставлениеТоваров.Выгрузить()));
	СтруктураСопоставления.Вставить("СопоставлениеСвойств" , ОбщегоНазначения.ТаблицаЗначенийВМассив(Объект.СопоставлениеСвойств.Выгрузить()));
	
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(СтруктураСопоставления,УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти