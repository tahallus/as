
#Область ОписаниеПеременных

#Область ПеременныеФормы

&НаКлиенте
Перем ИзмененПризнакВРаботе;

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ИдентификаторГруппы", ИдентификаторГруппы);
	РазрешеноИзменятьЗадачи = Обработки.АссистентУправления.РазрешеноИзменятьЗадачи();
	
	ЭтотОбъект.ИспользоватьВидыЗаказовПокупателей = ПолучитьФункциональнуюОпцию("ИспользоватьВидыЗаказовПокупателей");
	ЭтотОбъект.ИспользоватьВидыЗаказНарядов = ПолучитьФункциональнуюОпцию("ИспользоватьВидыЗаказНарядов");
	ЭтотОбъект.ИспользоватьПодсистемуРаботы = ПолучитьФункциональнуюОпцию("ИспользоватьПодсистемуРаботы");
	НадписьЗаказПокупателя = НСтр("ru = 'Переведи заказ покупателя'");
	
	ЗаполнитьОписаниеЗадачи();
	ЗаполнитьСпособыОповещения();
	ОбновитьБлокиНастроекАссистента();
	ОбновитьЭлементыДействийАссистента();
	ЗаполнитьСписокВыбораЭлементовФормы();
	
	ИнформационнаяБазаЗарегистрирована = СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована();
	
	КлючСохраненияПоложенияОкна = Новый УникальныйИдентификатор();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИзмененПризнакВРаботе = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ РазрешеноИзменятьЗадачи Тогда
		Возврат;
	КонецЕсли;
	
	ИзмененныеЗадачи = НастройкиЗадач.НайтиСтроки(Новый Структура("Модифицированность", Истина));
	ЗадачиИзменены = ИзмененныеЗадачи.Количество() <> 0;
	
	Если НЕ ЗадачиИзменены Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтотОбъект.ВРаботе Тогда
		
		СоздатьИзменитьЗадачиАссистента();
		ЗакрытьФормуЗадач();
		Возврат;
		
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗадачиЗаполненыКорректно() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СоздатьИзменитьЗадачиАссистента();
	ЗакрытьФормуЗадач();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ЕстьОшибки = Ложь;
	
	Для каждого СтрокаНастроек Из НастройкиЗадач Цикл
		
		Если СтрокаНастроек.Удалена Тогда
			Продолжить;
		КонецЕсли;
		
		Индекс = НастройкиЗадач.Индекс(СтрокаНастроек);
		
		ЗаказПокупателяЗавершен = СтрокаНастроек.СостояниеЗаказа = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказовПокупателей.Завершен");
		ЗаказНарядЗавершен = СтрокаНастроек.СостояниеЗаказа = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказНарядов.Завершен");
		ЗаказНаПроизводствоЗавершен = СтрокаНастроек.СостояниеЗаказаНаПроизводство = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказовНаПроизводство.Завершен");
		ЗаказПоставщикуЗавершен = СтрокаНастроек.СостояниеЗаказаПоставщику = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказовПоставщикам.Завершен");

		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.ВидОперации) Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.ВидЗаказа) Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.СостояниеЗаказа) И НЕ ТолькоОповестить() Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.СостояниеЗаказаПоставщику) И ВидимостьСтрокиЗаказПоставщику() Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.СостояниеЗаказаНаПроизводство) И ВидимостьСтрокиЗаказНаПроизводство() Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.ВариантЗавершенияЗаказПокупателя) 
			И НЕ ТолькоОповестить() И (ЗаказПокупателяЗавершен ИЛИ ЗаказНарядЗавершен) Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.ВариантЗавершенияЗаказПоставщику) 
			И ВидимостьСтрокиЗаказПоставщику() И ЗаказПоставщикуЗавершен Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.ВариантЗавершенияЗаказНаПроизводство) 
			И ВидимостьСтрокиЗаказНаПроизводство() И ЗаказНаПроизводствоЗавершен Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
				
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.СпособОповещения) Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьОшибки Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнены обязательные данные'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СостояниеЗаказаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Индекс = ИндексБлока(Элемент.Имя);
	СтрокаНастроек = НастройкиЗадач.Получить(ИндексБлока(Элемент.Имя));
	СтрокаНастроек.Модифицированность = Истина;
	ПараметрыПолученияДанных.Вставить("ВидЗаказа", СтрокаНастроек.ВидЗаказа); 
	
	Если СтрокаНастроек.ВидОперации = "ЗаказПокупателя" Тогда
		ДанныеВыбора = ПолучитьДанныеВыбора(Тип("СправочникСсылка.СостоянияЗаказовПокупателей"), ПараметрыПолученияДанных);
	ИначеЕсли СтрокаНастроек.ВидОперации = "ЗаказНаряд" Тогда
		ДанныеВыбора = ПолучитьДанныеВыбора(Тип("СправочникСсылка.СостоянияЗаказНарядов"), ПараметрыПолученияДанных);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеЗаказаНаПроизводствоПриИзменении(Элемент)
	
	Индекс = ИндексБлока(Элемент.Имя);
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
	СтрокаНастроек.Модифицированность = Истина;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеЗаказаПоставщикуПриИзменении(Элемент)
	
	Индекс = ИндексБлока(Элемент.Имя);
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
	СтрокаНастроек.Модифицированность = Истина;

	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантЗавершенияЗаказПоставщикуПриИзменении(Элемент)
	
	Индекс = ИндексБлока(Элемент.Имя);
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
	СтрокаНастроек.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантЗавершенияЗаказНаПроизводствоПриИзменении(Элемент)
	
	Индекс = ИндексБлока(Элемент.Имя);
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
	СтрокаНастроек.Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура СостояниеЗаказаПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ВариантЗавершенияЗаказПокупателяПриИзменении(Элемент)
	
	Индекс = ИндексБлока(Элемент.Имя);
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
	СтрокаНастроек.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОповещенияПриИзменении(Элемент)
	
	СтрокаНастроек = НастройкиЗадач.Получить(ИндексБлока(Элемент.Имя));
	СтрокаНастроек.Модифицированность = Истина;
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ВРаботеПриИзменении(Элемент)
	
	ИзмененПризнакВРаботе = Истина;
	
	УстановитьМодифицированностьЗадач();
	
	Если НЕ ВРаботе Тогда
		Возврат;
	КонецЕсли;
	
	Если ИнформационнаяБазаЗарегистрирована Тогда
		Возврат;
	КонецЕсли;
	
	ВРаботе = Ложь;
	Элементы.ПанельОшибки.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	Индекс = ИндексБлока(Элемент.Имя);
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
	
	Если СтрокаНастроек.ВидОперации = "ЗаказПокупателя" Тогда 
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.ВидЗаказа) ИЛИ ТипЗнч(СтрокаНастроек.ВидЗаказа) = Тип("СправочникСсылка.ВидыЗаказНарядов") Тогда
			СтрокаНастроек.ВидЗаказа = ПредопределенноеЗначение("Справочник.ВидыЗаказовПокупателей.Основной");
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.СостояниеЗаказа) ИЛИ ТипЗнч(СтрокаНастроек.СостояниеЗаказа) = Тип("СправочникСсылка.СостоянияЗаказНарядов") Тогда
			СтрокаНастроек.СостояниеЗаказа = ПредопределенноеЗначение("Справочник.СостоянияЗаказовПокупателей.Завершен");
		КонецЕсли;
		
	ИначеЕсли СтрокаНастроек.ВидОперации = "ЗаказНаряд" Тогда
	
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.ВидЗаказа) ИЛИ ТипЗнч(СтрокаНастроек.ВидЗаказа) = Тип("СправочникСсылка.ВидыЗаказовПокупателей") Тогда
			СтрокаНастроек.ВидЗаказа = ПредопределенноеЗначение("Справочник.ВидыЗаказНарядов.Основной");
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.СостояниеЗаказа) ИЛИ ТипЗнч(СтрокаНастроек.СостояниеЗаказа) = Тип("СправочникСсылка.СостоянияЗаказовПокупателей") Тогда
			СтрокаНастроек.СостояниеЗаказа = ПредопределенноеЗначение("Справочник.СостоянияЗаказНарядов.Завершен");
		КонецЕсли;
		
	КонецЕсли;
	
	СтрокаНастроек.Модифицированность = Истина;
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеЗаказаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Индекс = ИндексБлока(Элемент.Имя);
	СтрокаНастроек = НастройкиЗадач.Получить(ИндексБлока(Элемент.Имя));
	СтрокаНастроек.Модифицированность = Истина;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ВидЗаказа", СтрокаНастроек.ВидЗаказа);
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗаполнитьСостояниеПослеВыбора",ЭтотОбъект,Новый Структура("Индекс", Индекс));
	
	Если СтрокаНастроек.ВидОперации = "ЗаказНаряд" Тогда
		ОткрытьФорму("Справочник.СостоянияЗаказНарядов.ФормаВыбора", ПараметрыФормы, Элемент,,,,ОповещениеОЗакрытии);
	ИначеЕсли СтрокаНастроек.ВидОперации = "ЗаказПокупателя" Тогда
		ОткрытьФорму("Справочник.СостоянияЗаказовПокупателей.ФормаВыбора", ПараметрыФормы, Элемент,,,,ОповещениеОЗакрытии);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЗаказаПриИзменении(Элемент)
	
	Индекс = ИндексБлока(Элемент.Имя);
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
	СогласоватьВидИСостояние(Индекс);
	
	СтрокаНастроек.Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияУдалитьНажатие(Элемент)
	
	Индекс = ИндексБлока(Элемент.Имя);
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
	СтрокаНастроек.Удалена = Истина;
	СтрокаНастроек.Модифицированность = Истина;
	
	ОбновитьЭлементыДействийИЗаполнитьСписокВыбора();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОповещенияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Индекс = ИндексБлока(Элемент.Имя);
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
	СтрокаНастроек.Модифицированность = Истина;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Пользователи") Тогда
		
		Элементы["СпособОповещения_" + Индекс].ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
		НастройкиЗадач[Число(Индекс)].СпособОповещения = ВыбранноеЗначение;
		НастройкиЗадач[Число(Индекс)].ПользовательДляОповещения = ВыбранноеЗначение;
		
		Возврат;
		
	КонецЕсли;
	
	ЗначениеСпособаОповещения = СпособыОповещения.НайтиСтроки(Новый Структура("Представление", ВыбранноеЗначение));
	
	Если ЗначениеСпособаОповещения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеСпособаОповещения[0].Значение <> ПредопределенноеЗначение("Перечисление.СпособОповещенияАссистентаУправления.СообщениеКонтекстногоОбсужденияПользователю") Тогда
		
		Элементы["СпособОповещения_" + Индекс].ОграничениеТипа = Новый ОписаниеТипов("Строка");
		НастройкиЗадач[Число(Индекс)].СпособОповещения = ВыбранноеЗначение;
		НастройкиЗадач[Число(Индекс)].ПользовательДляОповещения = Неопределено;
		Возврат;
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Индекс", Индекс);
	
	Оповещение = Новый ОписаниеОповещения("ВыборПользователя", ЭтотОбъект, ДополнительныеПараметры);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора",Истина);
	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора",ПараметрыФормы,,,,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОповещенияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Индекс = Прав(Элемент.Имя, СтрДлина(Элемент.Имя) - СтрНайти(Элемент.Имя, "_"));
	ЗаполнитьДанныеВыбора(ДанныеВыбора, Индекс);
КонецПроцедуры

&НаКлиенте
Процедура СпособОповещенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Индекс = ИндексБлока(Элемент.Имя);
	ЗаполнитьДанныеВыбора(ДанныеВыбора, Индекс);
КонецПроцедуры

&НаКлиенте
Процедура ТекстОшибкиНажатие(Элемент)
	НачатьПодключениеОбсуждений();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьДействие(Команда)
	
	ДобавитьДействиеСервер();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьДействиеСервер()
	
	ДобавитьПустыеНастройкиЗадачи();
	ОбновитьЭлементыДействийАссистента();
	ЗаполнитьСписокВыбораЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДействийИЗаполнитьСписокВыбора()
	ОбновитьЭлементыДействийАссистента();
	ЗаполнитьСписокВыбораЭлементовФормы();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИндексБлока(ЭлементИмя)
	
	Возврат Прав(ЭлементИмя, СтрДлина(ЭлементИмя) - СтрНайти(ЭлементИмя, "_"));
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьСостояниеПослеВыбора(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение = Неопределено ИЛИ ДополнительныеПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаНастроек = НастройкиЗадач.Получить(ДополнительныеПараметры.Индекс);
	СтрокаНастроек.СостояниеЗаказа = ВыбранноеЗначение;
	СтрокаНастроек.Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеВыбора(ДанныеВыбора,Индекс)
	
	Если ДанныеВыбора = Неопределено Тогда
		ДанныеВыбора = Новый СписокЗначений;
	КонецЕсли;
	
	СпособНикогоНеОповещать = СпособыОповещения.НайтиСтроки(Новый Структура("Значение", Перечисления.СпособОповещенияАссистентаУправления.СообщениеКонтекстногоОбсужденияБезОповещения));
	Если СпособНикогоНеОповещать.Количество() > 0 Тогда
		ДанныеВыбора.Добавить(СпособНикогоНеОповещать[0].Представление,
		,,БиблиотекаКартинок.СпособОповещенияНикогоНеОповещать);
	КонецЕсли;
	
	СпособОповестиОтветственного = СпособыОповещения.НайтиСтроки(Новый Структура("Значение", Перечисления.СпособОповещенияАссистентаУправления.СообщениеКонтекстногоОбсужденияОтветственному));
	Если СпособОповестиОтветственного.Количество() > 0 Тогда
		ДанныеВыбора.Добавить(СпособОповестиОтветственного[0].Представление,
		,,БиблиотекаКартинок.СпособОповещенияОповеститьВыбранного);
	КонецЕсли;
	
	ДанныеВыбора.Добавить(Пользователи.АвторизованныйПользователь(), ,,БиблиотекаКартинок.СпособОповещенияОповеститьВыбранного);
		
	СпособОповестиПользователя = СпособыОповещения.НайтиСтроки(Новый Структура("Значение", Перечисления.СпособОповещенияАссистентаУправления.СообщениеКонтекстногоОбсужденияПользователю));
	Если СпособОповестиПользователя.Количество() > 0 Тогда
		ДанныеВыбора.Добавить(СпособОповестиПользователя[0].Представление,
		,,БиблиотекаКартинок.СпособОповещенияОповеститьВыбранного);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ВыборПользователя(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ ДополнительныеПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Индекс = ДополнительныеПараметры.Индекс;
	Элементы["СпособОповещения_"+Индекс].ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
	НастройкиЗадач[Число(Индекс)].СпособОповещения = Результат;
	НастройкиЗадач[Число(Индекс)].ПользовательДляОповещения = Результат;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПодключениеОбсуждений()
	
	Продолжение = Новый ОписаниеОповещения("ЗавершитьПодключениеОбсуждений", ЭтотОбъект);
	ОбсужденияКлиент.ПоказатьПодключение(Продолжение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПодключениеОбсуждений(Результат, ДополнительныеПараметры) Экспорт
	
	ИнформационнаяБазаЗарегистрирована = СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована();
	Элементы.ПанельОшибки.Видимость = НЕ ИнформационнаяБазаЗарегистрирована;
	ВРаботе = ИнформационнаяБазаЗарегистрирована;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьМодифицированностьЗадач()
	
	Для Каждого СтрокаНастроек Из НастройкиЗадач Цикл
		СтрокаНастроек.Модифицированность = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ЗадачиЗаполненыКорректно()
	
	ЕстьОшибкиСостояний = Ложь;
	ЕстьОшибкиУсловий = Ложь;
	
	ОдинаковыеСостояния = Новый Массив;
	
	Для каждого СтрокаНастроек Из НастройкиЗадач Цикл
				
		Если НЕ ТолькоОповестить() Тогда
			ОтборСостояние = Новый Структура;
			ОтборСостояние.Вставить("Удалена",					 Ложь);
			ОтборСостояние.Вставить("СпособОповещения",			 СтрокаНастроек.СпособОповещения);
			ОтборСостояние.Вставить("ПользовательДляОповещения", СтрокаНастроек.ПользовательДляОповещения);
			ОтборСостояние.Вставить("СостояниеЗаказа",			 СтрокаНастроек.СостояниеЗаказа);
			ОтборСостояние.Вставить("ВидЗаказа",				 СтрокаНастроек.ВидЗаказа);
			ОтборСостояние.Вставить("ВариантЗавершенияЗаказПокупателя",СтрокаНастроек.ВариантЗавершенияЗаказПокупателя);
			
			ЗадачиСОдинаковымиСостояниями = НастройкиЗадач.НайтиСтроки(ОтборСостояние);
			
			Если ЗадачиСОдинаковымиСостояниями.Количество() > 1 Тогда
				
				ЕстьОшибкиСостояний = Истина;
				Состояние = ОдинаковыеСостояния.Найти(СтрокаНастроек.СостояниеЗаказа);
				
				Если Состояние = Неопределено Тогда
					ОбщегоНазначенияКлиент.СообщитьПользователю(
					СтрШаблон(НСтр("ru = 'Состояние %1 используется в нескольких действиях'"),СтрокаНастроек.СостояниеЗаказа));
					ОдинаковыеСостояния.Добавить(СтрокаНастроек.СостояниеЗаказа);
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		Если ЕстьОшибкиУсловий Тогда
			Продолжить;
		КонецЕсли;
		
		ОтборУсловия = Новый Структура;
		ОтборУсловия.Вставить("Удалена",					 Ложь);
		ОтборУсловия.Вставить("ВидЗаказа",					 СтрокаНастроек.ВидЗаказа);
		ОтборУсловия.Вставить("ВидОперации",				 СтрокаНастроек.ВидОперации);
		ОтборУсловия.Вставить("СпособОповещения",			 СтрокаНастроек.СпособОповещения);
		ОтборУсловия.Вставить("ПользовательДляОповещения", 	 СтрокаНастроек.ПользовательДляОповещения);
		
		Если ВидимостьСтрокиЗаказПоставщику() Тогда
			ОтборУсловия.Вставить("СостояниеЗаказаПоставщику", 		 СтрокаНастроек.СостояниеЗаказаПоставщику);
			ОтборУсловия.Вставить("ВариантЗавершенияЗаказПоставщику",СтрокаНастроек.ВариантЗавершенияЗаказПоставщику);
		КонецЕсли;	
		Если ВидимостьСтрокиЗаказНаПроизводство() Тогда
			ОтборУсловия.Вставить("СостояниеЗаказаНаПроизводство",		 СтрокаНастроек.СостояниеЗаказаНаПроизводство);
			ОтборУсловия.Вставить("ВариантЗавершенияЗаказНаПроизводство",СтрокаНастроек.ВариантЗавершенияЗаказНаПроизводство);
		КонецЕсли;

		ЗадачиСОдинаковымиУсловиями = НастройкиЗадач.НайтиСтроки(ОтборУсловия);
		
		Если ЗадачиСОдинаковымиУсловиями.Количество() > 1 Тогда
			
			ЕстьОшибкиУсловий = Истина;
			ОбщегоНазначенияКлиент.СообщитьПользователю(
					НСтр("ru = 'Настроено несколько действий с одинаковыми условиями'"));
					
		КонецЕсли;
		
	КонецЦикла;
	
	ЕстьОшибки = ЕстьОшибкиСостояний ИЛИ ЕстьОшибкиУсловий;
	Возврат НЕ ЕстьОшибки;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСпособыОповещения()
	
	СпособыОповещения.Очистить();
	
	СпособНикогоНеОповещать = СпособыОповещения.Добавить();
	СпособНикогоНеОповещать.Значение = Перечисления.СпособОповещенияАссистентаУправления.СообщениеКонтекстногоОбсужденияБезОповещения;
	СпособНикогоНеОповещать.Представление = НСтр("ru = 'Никого не оповещать'");
	
	СпособОповестиОтветственного = СпособыОповещения.Добавить();
	СпособОповестиОтветственного.Значение = Перечисления.СпособОповещенияАссистентаУправления.СообщениеКонтекстногоОбсужденияОтветственному;
	СпособОповестиОтветственного.Представление = НСтр("ru = 'Ответственного за заказ'");
	
	СпособОповестиПользователя = СпособыОповещения.Добавить();
	СпособОповестиПользователя.Значение = Перечисления.СпособОповещенияАссистентаУправления.СообщениеКонтекстногоОбсужденияПользователю;
	СпособОповестиПользователя.Представление = НСтр("ru = 'Другого пользователя'");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпособОповещенияПоЗначению(СтрокаНастроек, ПользовательДляОповещения, СпособОповещения)
	
	Если НЕ ЗначениеЗаполнено(ПользовательДляОповещения) Тогда
		
		ПредставлениеСпособаОповещения = СпособыОповещения.НайтиСтроки(Новый Структура("Значение", СпособОповещения));
		
		Если ПредставлениеСпособаОповещения.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		СтрокаНастроек.СпособОповещения = ПредставлениеСпособаОповещения[0].Представление;
		
		Возврат;
		
	КонецЕсли;
	
	СтрокаНастроек.СпособОповещения = ПользовательДляОповещения;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпособОповещенияПоПредставлению(СтрокаНастроек, ЗадачаОбъект)
		
	ЗначениеСпособаОповещения = СпособыОповещения.НайтиСтроки(Новый Структура("Представление", СтрокаНастроек.СпособОповещения));
	
	Если ЗначениеСпособаОповещения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗадачаОбъект.СпособОповещения = ЗначениеСпособаОповещения[0].Значение;
			
КонецПроцедуры

#КонецОбласти

#Область УправлениеФормой
	
&НаСервере
Процедура ЗаполнитьСписокВыбораЭлементовФормы()
	
	Для Итератор = 0 По НастройкиЗадач.Количество() - 1 Цикл
		
		Если НастройкиЗадач[Итератор].Удалена Тогда
			Продолжить;
		КонецЕсли;
		
		Элементы["ВидОперации_" + Итератор].СписокВыбора.Очистить();
		Элементы["ВидОперации_" + Итератор].СписокВыбора.Добавить("ЗаказПокупателя", ?(ТолькоОповестить(),НСтр("ru='заказа покупателя'"), НСтр("ru='Заказ покупателя'")));
		Элементы["ВидОперации_" + Итератор].СписокВыбора.Добавить("ЗаказНаряд", ?(ТолькоОповестить(),НСтр("ru='заказ-наряда'"), НСтр("ru='Заказ-наряд'")));
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДействийАссистента()
	
	Элементы.Переместить(Элементы.ДобавитьДействие, Элементы.Действие_0);
	УдалитьЭлементыЗадач(Элементы);
	
	Для Каждого СтрокаНастроек Из НастройкиЗадач Цикл
		
		ИндексНастройки = НастройкиЗадач.Индекс(СтрокаНастроек);
		Если НастройкиЗадач[ИндексНастройки].Удалена Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИндексНастройки > 0 Тогда
			ОбщаяГруппаДействия = Элементы.Добавить("ГруппаДействие_" + ИндексНастройки, Тип("ГруппаФормы"), Элементы.ДействияАссистента);
			УстановитьОформлениеГруппы(ОбщаяГруппаДействия, Элементы.ГруппаДействие_0);
			
			ГруппаДействия = Элементы.Добавить("Действие_" + ИндексНастройки, Тип("ГруппаФормы"), ОбщаяГруппаДействия);
			УстановитьОформлениеГруппы(ГруппаДействия, Элементы.Действие_0);
						
			ГруппаСтроки = Элементы.Добавить("Строки_" + ИндексНастройки, Тип("ГруппаФормы"), ГруппаДействия);
			УстановитьОформлениеГруппы(ГруппаСтроки, Элементы.Строки_0);
						
			ГруппаПервойСтрокиДействия = Элементы.Добавить("ГруппаДействие_" + ИндексНастройки + "_Строка_1", Тип("ГруппаФормы"), ГруппаСтроки);
			УстановитьОформлениеГруппы(ГруппаПервойСтрокиДействия, Элементы.ГруппаДействие_0_Строка_1);
				
			ПолеВидОперации = Элементы.Добавить("ВидОперации_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаПервойСтрокиДействия);
			ПолеВидОперации.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].ВидОперации";
			УстановитьОформлениеПоля(ПолеВидОперации, Элементы.ВидОперации_0);
			ПолеВидОперации.УстановитьДействие("ПриИзменении","ВидОперацииПриИзменении");
			
			ДекорацияНадпись = Элементы.Добавить("НадписьЗаказПокупателя_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаПервойСтрокиДействия);
			ДекорацияНадпись.ПутьКДанным = Элементы.НадписьЗаказПокупателя_0.ПутьКДанным;
			ДекорацияНадпись.Вид = Элементы.НадписьЗаказПокупателя_0.Вид;
			ДекорацияНадпись.Заголовок = Элементы.НадписьЗаказПокупателя_0.Заголовок;
			ДекорацияНадпись.ПоложениеЗаголовка = Элементы.НадписьЗаказПокупателя_0.ПоложениеЗаголовка;
			ДекорацияНадпись.Ширина = Элементы.НадписьЗаказПокупателя_0.Ширина;
			ДекорацияНадпись.Видимость = Элементы.НадписьЗаказПокупателя_0.Видимость;
			ДекорацияНадпись.РастягиватьПоГоризонтали = Элементы.НадписьЗаказПокупателя_0.РастягиватьПоГоризонтали;
			
			ПолеВидЗаказа = Элементы.Добавить("ВидЗаказа_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаПервойСтрокиДействия);
			ПолеВидЗаказа.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].ВидЗаказа";
			УстановитьОформлениеПоля(ПолеВидЗаказа, Элементы.ВидЗаказа_0);
			ПолеВидЗаказа.УстановитьДействие("ПриИзменении","ВидЗаказаПриИзменении");
			
			ПолеСостояниеЗаказа = Элементы.Добавить("СостояниеЗаказа_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаПервойСтрокиДействия);
			ПолеСостояниеЗаказа.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].СостояниеЗаказа";
			УстановитьОформлениеПоля(ПолеСостояниеЗаказа, Элементы.СостояниеЗаказа_0);
			ПолеСостояниеЗаказа.УстановитьДействие("АвтоПодбор","СостояниеЗаказаАвтоПодбор");
			ПолеСостояниеЗаказа.УстановитьДействие("НачалоВыбора","СостояниеЗаказаНачалоВыбора");
			ПолеСостояниеЗаказа.УстановитьДействие("ПриИзменении","СостояниеЗаказаПриИзменении");
			
			ПолеВариантЗавершенияЗаказПокупателя = Элементы.Добавить("ВариантЗавершенияЗаказПокупателя_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаПервойСтрокиДействия);
			ПолеВариантЗавершенияЗаказПокупателя.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].ВариантЗавершенияЗаказПокупателя";
			УстановитьОформлениеПоля(ПолеВариантЗавершенияЗаказПокупателя, Элементы.ВариантЗавершенияЗаказПокупателя_0);
			ПолеВариантЗавершенияЗаказПокупателя.УстановитьДействие("ПриИзменении","ВариантЗавершенияЗаказПокупателяПриИзменении");
						
			ДекорацияУдалить = Элементы.Добавить("ДекорацияУдалить_" + ИндексНастройки, Тип("ДекорацияФормы"), ОбщаяГруппаДействия);
			ДекорацияУдалить.Вид = Элементы.ДекорацияУдалить_0.Вид;
			ДекорацияУдалить.Заголовок = Элементы.ДекорацияУдалить_0.Заголовок;
			ДекорацияУдалить.Ширина = Элементы.ДекорацияУдалить_0.Ширина;
			ДекорацияУдалить.Высота = Элементы.ДекорацияУдалить_0.Высота;
			ДекорацияУдалить.Картинка = Элементы.ДекорацияУдалить_0.Картинка;
			ДекорацияУдалить.РазмерКартинки = Элементы.ДекорацияУдалить_0.РазмерКартинки;
			ДекорацияУдалить.Гиперссылка = Элементы.ДекорацияУдалить_0.Гиперссылка;
			ДекорацияУдалить.УстановитьДействие("Нажатие", "ДекорацияУдалитьНажатие");
			
			ГруппаВторойСтрокиДействия = Элементы.Добавить("ГруппаДействие_" + ИндексНастройки + "_Строка_2", Тип("ГруппаФормы"), ГруппаСтроки);
			УстановитьОформлениеГруппы(ГруппаВторойСтрокиДействия, Элементы.ГруппаДействие_0_Строка_2);
						
			ПолеСостояниеЗаказаПоставщику = Элементы.Добавить("СостояниеЗаказаПоставщику_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаВторойСтрокиДействия);
			ПолеСостояниеЗаказаПоставщику.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].СостояниеЗаказаПоставщику";
			УстановитьОформлениеПоля(ПолеСостояниеЗаказаПоставщику, Элементы.СостояниеЗаказаПоставщику_0);
			ПолеСостояниеЗаказаПоставщику.УстановитьДействие("ПриИзменении","СостояниеЗаказаПоставщикуПриИзменении");
						
			ПолеВариантЗавершенияЗаказПоставщику = Элементы.Добавить("ВариантЗавершенияЗаказПоставщику_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаВторойСтрокиДействия);
			ПолеВариантЗавершенияЗаказПоставщику.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].ВариантЗавершенияЗаказПоставщику";
			УстановитьОформлениеПоля(ПолеВариантЗавершенияЗаказПоставщику, Элементы.ВариантЗавершенияЗаказПоставщику_0);
			ПолеВариантЗавершенияЗаказПоставщику.УстановитьДействие("ПриИзменении","ВариантЗавершенияЗаказПоставщикуПриИзменении");
						
			ГруппаТретьейСтрокиДействия = Элементы.Добавить("ГруппаДействие_" + ИндексНастройки + "_Строка_3", Тип("ГруппаФормы"), ГруппаСтроки);
			УстановитьОформлениеГруппы(ГруппаТретьейСтрокиДействия, Элементы.ГруппаДействие_0_Строка_3);
			
			ПолеСостояниеЗаказаНаПроизводство = Элементы.Добавить("СостояниеЗаказаНаПроизводство_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаТретьейСтрокиДействия);
			ПолеСостояниеЗаказаНаПроизводство.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].СостояниеЗаказаНаПроизводство";
			УстановитьОформлениеПоля(ПолеСостояниеЗаказаНаПроизводство, Элементы.СостояниеЗаказаНаПроизводство_0);
			ПолеСостояниеЗаказаНаПроизводство.УстановитьДействие("ПриИзменении","СостояниеЗаказаНаПроизводствоПриИзменении");
					
			ПолеВариантЗавершенияЗаказНаПроизводство = Элементы.Добавить("ВариантЗавершенияЗаказНаПроизводство_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаТретьейСтрокиДействия);
			ПолеВариантЗавершенияЗаказНаПроизводство.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].ВариантЗавершенияЗаказНаПроизводство";
			УстановитьОформлениеПоля(ПолеВариантЗавершенияЗаказНаПроизводство, Элементы.ВариантЗавершенияЗаказНаПроизводство_0);
			ПолеВариантЗавершенияЗаказНаПроизводство.УстановитьДействие("ПриИзменении","ВариантЗавершенияЗаказНаПроизводствоПриИзменении");

			ГруппаЧетвертойСтрокиДействия = Элементы.Добавить("ГруппаДействие_" + ИндексНастройки + "_Строка_4", Тип("ГруппаФормы"), ГруппаСтроки);
			УстановитьОформлениеГруппы(ГруппаЧетвертойСтрокиДействия, Элементы.ГруппаДействие_0);
						
			ДекорацияТекстОбсуждение = Элементы.Добавить("ТекстОбсуждение_" + ИндексНастройки, Тип("ДекорацияФормы"), ГруппаЧетвертойСтрокиДействия);
			ДекорацияТекстОбсуждение.Вид = Элементы.ТекстОбсуждение_0.Вид;
			ДекорацияТекстОбсуждение.Заголовок = Элементы.ТекстОбсуждение_0.Заголовок;
			ДекорацияТекстОбсуждение.Ширина = Элементы.ТекстОбсуждение_0.Ширина;
			ДекорацияТекстОбсуждение.Видимость = Элементы.ТекстОбсуждение_0.Видимость;
			ДекорацияТекстОбсуждение.АвтоМаксимальнаяШирина = Элементы.ТекстОбсуждение_0.АвтоМаксимальнаяШирина;
			ДекорацияТекстОбсуждение.МаксимальнаяШирина = Элементы.ТекстОбсуждение_0.МаксимальнаяШирина;
			
			ПолеСпособОповещения = Элементы.Добавить("СпособОповещения_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаЧетвертойСтрокиДействия);
			ПолеСпособОповещения.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].СпособОповещения";
			УстановитьОформлениеПоля(ПолеСпособОповещения, Элементы.СпособОповещения_0);
			ПолеСпособОповещения.УстановитьДействие("ПриИзменении", "СпособОповещенияПриИзменении");
			ПолеСпособОповещения.УстановитьДействие("АвтоПодбор", "СпособОповещенияАвтоПодбор");
			ПолеСпособОповещения.УстановитьДействие("НачалоВыбора", "СпособОповещенияНачалоВыбора");
			ПолеСпособОповещения.УстановитьДействие("ОбработкаВыбора", "СпособОповещенияОбработкаВыбора");
			
			Элементы.Переместить(Элементы.ДобавитьДействие, Элементы["Действие_" + ИндексНастройки]);
		КонецЕсли;
	КонецЦикла;
	УправлениеФормой();
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
		
	Элементы.ДобавитьДействие.Видимость = РазрешеноИзменятьЗадачи;
	Элементы.ДействияАссистента.ТолькоПросмотр = НЕ РазрешеноИзменятьЗадачи;
	Элементы.ВРаботе.ТолькоПросмотр = НЕ РазрешеноИзменятьЗадачи;
	ЭтотОбъект.ТолькоПросмотр = НЕ РазрешеноИзменятьЗадачи;
	
	Для Каждого СтрокаНастроек Из НастройкиЗадач Цикл
		
		ИндексНастройки = НастройкиЗадач.Индекс(СтрокаНастроек);
		Если СтрокаНастроек.Удалена Тогда 
			Продолжить;
		КонецЕсли;
		ПолеВидЗаказа                            = Элементы["ВидЗаказа_"+ИндексНастройки];
		ПолеСостояниеЗаказа                      = Элементы["СостояниеЗаказа_"+ИндексНастройки];
		ПолеВидОперации                          = Элементы["ВидОперации_"+ИндексНастройки];
		ПолеСпособОповещения                     = Элементы["СпособОповещения_"+ИндексНастройки];
		ДекорацияТекстОбсуждение                 = Элементы["ТекстОбсуждение_"+ИндексНастройки];
		ДекорацияУдалить                         = Элементы["ДекорацияУдалить_"+ИндексНастройки];
		ЭлементНадписьЗаказПокупателя            = Элементы["НадписьЗаказПокупателя_"+ИндексНастройки];
		ПолеСостояниеЗаказаПоставщику            = Элементы["СостояниеЗаказаПоставщику_"+ИндексНастройки];
		ПолеСостояниеЗаказаНаПроизводство        = Элементы["СостояниеЗаказаНаПроизводство_"+ИндексНастройки];
		ПолеВариантЗавершенияЗаказПокупателя     = Элементы["ВариантЗавершенияЗаказПокупателя_"+ИндексНастройки];
		ПолеВариантЗавершенияЗаказПоставщику     = Элементы["ВариантЗавершенияЗаказПоставщику_"+ИндексНастройки];
		ПолеВариантЗавершенияЗаказНаПроизводство = Элементы["ВариантЗавершенияЗаказНаПроизводство_"+ИндексНастройки];
		
		ЗаказПокупателяЗавершен = СтрокаНастроек.СостояниеЗаказа = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказовПокупателей.Завершен");
		ЗаказНарядЗавершен = СтрокаНастроек.СостояниеЗаказа = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказНарядов.Завершен");
		ЗаказНаПроизводствоЗавершен = СтрокаНастроек.СостояниеЗаказаНаПроизводство = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказовНаПроизводство.Завершен");
		ЗаказПоставщикуЗавершен = СтрокаНастроек.СостояниеЗаказаПоставщику     = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказовПоставщикам.Завершен");
		
		Если СтрокаНастроек.ВидОперации = "ЗаказНаряд" Тогда	
			ПолеВидЗаказа.ОграничениеТипа       = Новый ОписаниеТипов("СправочникСсылка.ВидыЗаказНарядов");
			ПолеСостояниеЗаказа.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.СостоянияЗаказНарядов");
			ПолеВидЗаказа.Видимость             = ИспользоватьВидыЗаказНарядов;
		ИначеЕсли СтрокаНастроек.ВидОперации = "ЗаказПокупателя" Тогда
			ПолеВидЗаказа.ОграничениеТипа       = Новый ОписаниеТипов("СправочникСсылка.ВидыЗаказовПокупателей");
			ПолеСостояниеЗаказа.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.СостоянияЗаказовПокупателей");
			ПолеВидЗаказа.Видимость             = ИспользоватьВидыЗаказовПокупателей;
		КонецЕсли;
		
		ПолеСостояниеЗаказа.АвтоМаксимальнаяШирина = НЕ ПолеВидЗаказа.Видимость;
		ПолеВидЗаказа.АвтоМаксимальнаяШирина       = НЕ ПолеВидЗаказа.Видимость;
		ПолеВариантЗавершенияЗаказПокупателя.Видимость     = ЗаказПокупателяЗавершен ИЛИ ЗаказНарядЗавершен;
		ПолеВариантЗавершенияЗаказПоставщику.Видимость     = ЗаказПоставщикуЗавершен;
		ПолеВариантЗавершенияЗаказНаПроизводство.Видимость = ЗаказНаПроизводствоЗавершен;
		Если НЕ ЗаказПоставщикуЗавершен Тогда
			ПолеСостояниеЗаказаПоставщику.РастягиватьПоГоризонтали = Истина;
		КонецЕсли;
		Если НЕ ЗаказНаПроизводствоЗавершен Тогда
			ПолеСостояниеЗаказаНаПроизводство.РастягиватьПоГоризонтали = Истина;
		КонецЕсли;
		
		Элементы["ГруппаДействие_" + ИндексНастройки+ "_Строка_1"].Видимость = НЕ ТолькоОповестить();
		Элементы["ГруппаДействие_" + ИндексНастройки+ "_Строка_2"].Видимость = ВидимостьСтрокиЗаказПоставщику();
		Элементы["ГруппаДействие_" + ИндексНастройки+ "_Строка_3"].Видимость = ВидимостьСтрокиЗаказНаПроизводство();
			
		ОпределитьЗаголовкиПолей(ПолеСостояниеЗаказаНаПроизводство, ПолеСостояниеЗаказаПоставщику, ДекорацияТекстОбсуждение);	
		Если ПолеВидЗаказа.Видимость Тогда
			ПолеСостояниеЗаказа.МаксимальнаяШирина = 25;
			ПолеВидЗаказа.МаксимальнаяШирина       = 25;
		КонецЕсли;
		
		Если ЗаказПокупателяЗавершен ИЛИ ЗаказНарядЗавершен Тогда
			ПолеСостояниеЗаказа.МаксимальнаяШирина = 10;
		Иначе
			СтрокаНастроек.ВариантЗавершенияЗаказПокупателя = Перечисления.ВариантыЗавершенияЗаказа.ПустаяСсылка();
		КонецЕсли;
		
		Если ЗаказПоставщикуЗавершен Тогда
			ПолеСостояниеЗаказаПоставщику.МаксимальнаяШирина = 43;
		Иначе
			ПолеСостояниеЗаказаПоставщику.МаксимальнаяШирина = 55;
			СтрокаНастроек.ВариантЗавершенияЗаказПоставщику = Перечисления.ВариантыЗавершенияЗаказа.ПустаяСсылка();
		КонецЕсли;
		Если ЗаказНаПроизводствоЗавершен Тогда
			ПолеСостояниеЗаказаНаПроизводство.МаксимальнаяШирина = 43;
		Иначе
			ПолеСостояниеЗаказаНаПроизводство.МаксимальнаяШирина = 55;
			СтрокаНастроек.ВариантЗавершенияЗаказНаПроизводство = Перечисления.ВариантыЗавершенияЗаказа.ПустаяСсылка();
		КонецЕсли;
		Если ТипЗнч(СтрокаНастроек.СпособОповещения) = Тип("Строка") Тогда
			ПолеСпособОповещения.ОграничениеТипа = Новый ОписаниеТипов("Строка");
		ИначеЕсли ТипЗнч(СтрокаНастроек.СпособОповещения) = Тип("СправочникСсылка.Пользователи") Тогда
			ПолеСпособОповещения.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
		КонецЕсли;
		
		ДекорацияУдалить.Доступность = РазрешеноИзменятьЗадачи И ИндексНастройки <> 0;
		ПолеВидОперации.Видимость = ИспользоватьПодсистемуРаботы;
		ЭлементНадписьЗаказПокупателя.Видимость = НЕ ИспользоватьПодсистемуРаботы;
		Если ТолькоОповестить() Тогда
			ПолеВариантЗавершенияЗаказПокупателя.МаксимальнаяШирина     = 16;
			ПолеВариантЗавершенияЗаказПоставщику.МаксимальнаяШирина     = 16;
			ПолеВариантЗавершенияЗаказНаПроизводство.МаксимальнаяШирина = 16;
			ПолеВидОперации.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
			
			Элементы.Переместить(ЭлементНадписьЗаказПокупателя, Элементы["ГруппаДействие_" + ИндексНастройки+ "_Строка_4"], ПолеСпособОповещения);
			Элементы.Переместить(ПолеВидОперации, 				Элементы["ГруппаДействие_" + ИндексНастройки+ "_Строка_4"], ПолеСпособОповещения);
			Элементы.Переместить(ПолеВидЗаказа, 				Элементы["ГруппаДействие_" + ИндексНастройки+ "_Строка_4"], ПолеСпособОповещения);
			
			Если ВидимостьСтрокиЗаказПоставщику() Тогда
				Элементы.Переместить(ДекорацияТекстОбсуждение, Элементы["ГруппаДействие_" + ИндексНастройки+ "_Строка_2"]);
			КонецЕсли;
			
			Если ВидимостьСтрокиЗаказНаПроизводство() Тогда
				Элементы.Переместить(ДекорацияТекстОбсуждение, Элементы["ГруппаДействие_" + ИндексНастройки+ "_Строка_3"]);
			КонецЕсли;
			
		КонецЕсли;
		Если ИспользоватьПодсистемуРаботы Тогда
			Продолжить;
		КонецЕсли;
		Если СтрокаНастроек.ВидОперации = "ЗаказПокупателя" Тогда
			Продолжить;
		КонецЕсли;
		СтрокаНастроек.ВидОперации = "ЗаказПокупателя";
		СтрокаНастроек.Модифицированность = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СогласоватьВидИСостояние(Индекс)
	
	СтрокаНастройки = НастройкиЗадач.Получить(Индекс);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВидыЗаказов.Ссылка КАК Ссылка,
	|	ВидыЗаказов.ПорядокСостояний.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Состояние КАК Состояние
	|	) КАК ПорядокСостояний
	|ИЗ
	|	&ТаблицаСправочника КАК ВидыЗаказов 
	|ГДЕ
	|	ВидыЗаказов.Ссылка = &ВидЗаказа";
	
	ИмяСправочника = СтрокаНастройки.ВидЗаказа.Метаданные().Имя;
	Запрос.Текст = СтрЗаменить(Запрос.Текст , "&ТаблицаСправочника", "Справочник." + ИмяСправочника);
	
	Запрос.УстановитьПараметр("ВидЗаказа", СтрокаНастройки.ВидЗаказа);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СостоянияВида = Выборка.ПорядокСостояний.Выгрузить();
		
		Отбор = Новый Структура;
		Отбор.Вставить("Состояние",СтрокаНастройки.СостояниеЗаказа);
		НайденныеСтроки = СостоянияВида.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			СтрокаНастройки.СостояниеЗаказа = Неопределено;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеГруппы(ДобавляемаяГруппа, ГруппаФормы)
	
	ДобавляемаяГруппа.Вид = ГруппаФормы.Вид;
	ДобавляемаяГруппа.Отображение = ГруппаФормы.Отображение;
	ДобавляемаяГруппа.Группировка = ГруппаФормы.Группировка;
	ДобавляемаяГруппа.ОтображатьЗаголовок = ГруппаФормы.ОтображатьЗаголовок;
	ДобавляемаяГруппа.ЦветФона = ГруппаФормы.ЦветФона;
	ДобавляемаяГруппа.СквозноеВыравнивание = ГруппаФормы.СквозноеВыравнивание;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементыЗадач(Элементы)
	
	УдаляемыеЭлементы = Новый Массив;
	Для ИндексГруппы = 1 По Элементы.ДействияАссистента.ПодчиненныеЭлементы.Количество()-1 Цикл
		УдаляемыеЭлементы.Добавить(Элементы.ДействияАссистента.ПодчиненныеЭлементы[ИндексГруппы]);
	КонецЦикла;
	Для Каждого УдаляемыйЭлемент Из УдаляемыеЭлементы Цикл
		Элементы.Удалить(УдаляемыйЭлемент);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеПоля(ДобавляемыйЭлемент, ЭлементФормы)
	
	ДобавляемыйЭлемент.Вид = ЭлементФормы.Вид;
	ДобавляемыйЭлемент.ПоложениеЗаголовка = ЭлементФормы.ПоложениеЗаголовка;
	ДобавляемыйЭлемент.Заголовок = ЭлементФормы.Заголовок;
	ДобавляемыйЭлемент.АвтоМаксимальнаяШирина = ЭлементФормы.АвтоМаксимальнаяШирина;
	ДобавляемыйЭлемент.МаксимальнаяШирина = ЭлементФормы.МаксимальнаяШирина;
	ДобавляемыйЭлемент.РежимВыбораИзСписка = ЭлементФормы.РежимВыбораИзСписка;
	ДобавляемыйЭлемент.КнопкаВыпадающегоСписка = ЭлементФормы.КнопкаВыпадающегоСписка;
	ДобавляемыйЭлемент.КнопкаОткрытия = ЭлементФормы.КнопкаОткрытия;
	ДобавляемыйЭлемент.КнопкаВыбора = ЭлементФормы.КнопкаВыбора;
	ДобавляемыйЭлемент.АвтоОтметкаНезаполненного = ЭлементФормы.АвтоОтметкаНезаполненного;
	ДобавляемыйЭлемент.ОтображениеПодсказки = ЭлементФормы.ОтображениеПодсказки;
	ДобавляемыйЭлемент.Подсказка = ЭлементФормы.Подсказка;
	ДобавляемыйЭлемент.ИсторияВыбораПриВводе = ЭлементФормы.ИсторияВыбораПриВводе;
	ДобавляемыйЭлемент.КнопкаСоздания = ЭлементФормы.КнопкаСоздания;
	ДобавляемыйЭлемент.КнопкаОткрытия = ЭлементФормы.КнопкаОткрытия;
	ДобавляемыйЭлемент.ПодсказкаВвода = ЭлементФормы.ПодсказкаВвода;

КонецПроцедуры

&НаСервере
Функция ВидимостьСтрокиЗаказПоставщику()
	
	Возврат ЭтоЗадачаИзменениеПоЗаказамПоставщикам() 
		ИЛИ ЭтоЗадачаИзменениеПоЗаказамНаПроизводствоИПоставщикам()
		ИЛИ ЭтоЗадачаСообщенияПоЗаказамПоставщикам()
		ИЛИ ЭтоЗадачаСообщенияПоЗаказамНаПроизводствоИПоставщикам();
		
КонецФункции
	
&НаСервере
Функция ВидимостьСтрокиЗаказНаПроизводство()
	
	Возврат ЭтоЗадачаИзменениеПоЗаказамНаПроизводство() 
		ИЛИ ЭтоЗадачаИзменениеПоЗаказамНаПроизводствоИПоставщикам()
		ИЛИ ЭтоЗадачаСообщенияПоЗаказамНаПроизводство()
		ИЛИ ЭтоЗадачаСообщенияПоЗаказамНаПроизводствоИПоставщикам();
		
КонецФункции

&НаСервере
Функция ТолькоОповестить()
	
	Возврат ЭтоЗадачаСообщенияПоЗаказамПоставщикам() ИЛИ ЭтоЗадачаСообщенияПоЗаказамНаПроизводствоИПоставщикам()
		ИЛИ ЭтоЗадачаСообщенияПоЗаказамНаПроизводство();
		
КонецФункции

&НаСервере
Процедура ОпределитьЗаголовкиПолей(ПолеСостояниеЗаказаНаПроизводство, ПолеСостояниеЗаказаПоставщику, ДекорацияТекстОбсуждение)
	
	Если ЭтоЗадачаИзменениеПоЗаказамНаПроизводство() Тогда
		ПолеСостояниеЗаказаНаПроизводство.Заголовок = НСтр("ru = 'когда заказы на производство будут в состоянии'");
	КонецЕсли;
	
	Если ЭтоЗадачаСообщенияПоЗаказамПоставщикам() ИЛИ ЭтоЗадачаСообщенияПоЗаказамНаПроизводствоИПоставщикам() Тогда
		ПолеСостояниеЗаказаПоставщику.Заголовок = НСтр("ru = 'Когда заказы поставщикам будут в состоянии'");
	КонецЕсли;
	
	Если ЭтоЗадачаСообщенияПоЗаказамНаПроизводство() Тогда
		ПолеСостояниеЗаказаНаПроизводство.Заголовок = НСтр("ru = 'Когда заказы на производство будут в состоянии'");
	КонецЕсли;
	
	Если ТолькоОповестить() Тогда
		ДекорацияТекстОбсуждение.Заголовок = НСтр("ru = 'напиши в обсуждение связанного '");
		НадписьЗаказПокупателя = НСтр("ru = 'заказа покупателя'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗадачиАссистента

&НаСервере
Процедура УдалитьОдинаковыеДействияИзНастроек()
	
	Для Каждого СтрокаНастроек Из НастройкиЗадач Цикл
		
		ЗаказПокупателяЗавершен     = СтрокаНастроек.СостояниеЗаказа = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказовПокупателей.Завершен");
		ЗаказНарядЗавершен          = СтрокаНастроек.СостояниеЗаказа = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказНарядов.Завершен");
		ЗаказНаПроизводствоЗавершен = СтрокаНастроек.СостояниеЗаказаНаПроизводство = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказовНаПроизводство.Завершен");
		ЗаказПоставщикуЗавершен     = СтрокаНастроек.СостояниеЗаказаПоставщику     = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказовПоставщикам.Завершен");

		ОтборПоУсловиям = Новый Структура;
		ОтборПоУсловиям.Вставить("Удалена", Ложь);
		ОтборПоУсловиям.Вставить("ВидОперации", СтрокаНастроек.ВидОперации);
		ОтборПоУсловиям.Вставить("ВидЗаказа", СтрокаНастроек.ВидЗаказа);
		ОтборПоУсловиям.Вставить("СпособОповещения", СтрокаНастроек.СпособОповещения);
		ОтборПоУсловиям.Вставить("ПользовательДляОповещения", СтрокаНастроек.ПользовательДляОповещения);
		
		Если НЕ ТолькоОповестить() Тогда
			ОтборПоУсловиям.Вставить("СостояниеЗаказа", СтрокаНастроек.СостояниеЗаказа);
		КонецЕсли;
		Если НЕ ТолькоОповестить() И (ЗаказПокупателяЗавершен ИЛИ ЗаказНарядЗавершен) Тогда
			ОтборПоУсловиям.Вставить("ВариантЗавершенияЗаказПокупателя", СтрокаНастроек.ВариантЗавершенияЗаказПокупателя);
		КонецЕсли;
		Если ВидимостьСтрокиЗаказПоставщику() Тогда
			ОтборПоУсловиям.Вставить("СостояниеЗаказаПоставщику", СтрокаНастроек.СостояниеЗаказаПоставщику);
		КонецЕсли;
		Если ВидимостьСтрокиЗаказПоставщику() И ЗаказПоставщикуЗавершен Тогда
			ОтборПоУсловиям.Вставить("ВариантЗавершенияЗаказПоставщику", СтрокаНастроек.ВариантЗавершенияЗаказПоставщику);
		КонецЕсли;
		Если ВидимостьСтрокиЗаказНаПроизводство() Тогда
			ОтборПоУсловиям.Вставить("СостояниеЗаказаНаПроизводство", СтрокаНастроек.СостояниеЗаказаНаПроизводство);
		КонецЕсли;
		Если ВидимостьСтрокиЗаказНаПроизводство() И ЗаказНаПроизводствоЗавершен Тогда
			ОтборПоУсловиям.Вставить("ВариантЗавершенияЗаказНаПроизводство", СтрокаНастроек.ВариантЗавершенияЗаказНаПроизводство);
		КонецЕсли;
		
		ЕстьЗадачиСОдинаковымиУсловиями = НастройкиЗадач.НайтиСтроки(ОтборПоУсловиям).Количество() > 1;
		
		Если ЕстьЗадачиСОдинаковымиУсловиями Тогда
			СтрокаНастроек.Удалена = Истина;
			СтрокаНастроек.Модифицированность = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьБлокиНастроекАссистента()
	
	ГруппаЗадач = Справочники.ЗадачиАссистентаУправления.СсылкаНаГруппуЗадач(ИдентификаторГруппы);
	
	Если НЕ ЗначениеЗаполнено(ГруппаЗадач) Тогда
		ДобавитьПустыеНастройкиЗадачи();
		Возврат;
	КонецЕсли;

	ОтобранныеЗадачи = Справочники.ЗадачиАссистентаУправления.ПолучитьЗадачиПоГруппе(ГруппаЗадач);
	
	НастройкиЗадач.Очистить();
	
	Для каждого Задача Из ОтобранныеЗадачи Цикл
				
		ДанныеЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Задача, "СпособОповещения,Используется,ПользовательДляОповещения");
		ЗначенияЗаполнения = Справочники.ЗадачиАссистентаУправления.ЗначенияЗаполнения(Задача);
		ПараметрыУсловия = Справочники.ЗадачиАссистентаУправления.ПредставленияПараметровУсловия(Задача);
		
		СтрокаНастроек = НастройкиЗадач.Добавить();
		СтрокаНастроек.Задача = Задача;
		
		Если ТипЗнч(ЗначенияЗаполнения.ВидЗаказаПокупателя) = Тип("СправочникСсылка.ВидыЗаказовПокупателей") Тогда
			СтрокаНастроек.ВидОперации = "ЗаказПокупателя";
		Иначе
			СтрокаНастроек.ВидОперации = "ЗаказНаряд";
		КонецЕсли;
		
		СтрокаНастроек.ВидЗаказа = ЗначенияЗаполнения.ВидЗаказаПокупателя;
		
		Если НЕ ТолькоОповестить() Тогда
			СтрокаНастроек.СостояниеЗаказа = ЗначенияЗаполнения.СостояниеЗаказаПокупателя;
			СтрокаНастроек.ВариантЗавершенияЗаказПокупателя = ЗначенияЗаполнения.ВариантЗавершенияЗаказаПокупателя;
		КонецЕсли;
		
		СтрокаНастроек.Удалена = Ложь;
		СтрокаНастроек.Модифицированность = Ложь;
		СтрокаНастроек.ПользовательДляОповещения = ДанныеЗадачи.ПользовательДляОповещения;
		
		Если ВидимостьСтрокиЗаказПоставщику() Тогда
			СтрокаНастроек.СостояниеЗаказаПоставщику        = ЗначенияЗаполнения.СостояниеЗаказаПоставщику;
			СтрокаНастроек.ВариантЗавершенияЗаказПоставщику = ЗначенияЗаполнения.ВариантЗавершенияЗаказаПоставщику;
		КонецЕсли;
		
		Если ВидимостьСтрокиЗаказНаПроизводство() Тогда
			СтрокаНастроек.СостояниеЗаказаНаПроизводство        = ЗначенияЗаполнения.СостояниеЗаказаНаПроизводство;
			СтрокаНастроек.ВариантЗавершенияЗаказНаПроизводство = ЗначенияЗаполнения.ВариантЗавершенияЗаказаНаПроизводство;
		КонецЕсли;
	
		ЗаполнитьСпособОповещенияПоЗначению(СтрокаНастроек, ДанныеЗадачи.ПользовательДляОповещения, ДанныеЗадачи.СпособОповещения);
		ЭтотОбъект.ВРаботе = ДанныеЗадачи.Используется;
		
	КонецЦикла;
	
	Если НастройкиЗадач.Количество() = 0 Тогда
		ДобавитьПустыеНастройкиЗадачи();
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура СоздатьИзменитьЗадачиАссистента()
	
	ДействиеИдентификатор = "СинхронизироватьСтатусЗаказПокупателя";
	УдалитьОдинаковыеДействияИзНастроек();
	
	НачатьТранзакцию();
	
	Попытка
		
		ГруппаЗадач = Справочники.ЗадачиАссистентаУправления.СсылкаНаГруппуЗадач(ИдентификаторГруппы);
		
		Если НЕ ЗначениеЗаполнено(ГруппаЗадач) Тогда
			ГруппаЗадач = Справочники.ЗадачиАссистентаУправления.СоздатьГруппу();
			ГруппаЗадач.Наименование = ЭтотОбъект.Заголовок;
			ГруппаЗадач.ИдентификаторГруппы = ИдентификаторГруппы;
			ГруппаЗадач.Записать();
		КонецЕсли;
		
		Для каждого СтрокаНастроек Из НастройкиЗадач Цикл
			
			Если ЗначениеЗаполнено(СтрокаНастроек.Задача) Тогда
				ЗадачаОбъект = СтрокаНастроек.Задача.ПолучитьОбъект();
			Иначе
				ЗадачаОбъект = Справочники.ЗадачиАссистентаУправления.СоздатьЭлемент();
				АвторИзменений = Пользователи.АвторизованныйПользователь();
				ЕстьЗадачиПользователя = Справочники.ЗадачиАссистентаУправления.ЕстьЗадачиПользователя(АвторИзменений);
			КонецЕсли;
			
			Если СтрокаНастроек.Удалена И ЗначениеЗаполнено(ЗадачаОбъект.Ссылка) Тогда
				УдалитьОсновнуюЗадачу(ЗадачаОбъект);
			КонецЕсли;
			
			Если СтрокаНастроек.Удалена Тогда
				СтрокаНастроек.Модифицированность = Ложь;
				Продолжить;
			КонецЕсли;
			
			ЗадачаОбъект.ДействиеИдентификатор = ДействиеИдентификатор;
			
			ЗадачаОбъект.События.Очистить();
			НовоеСобытие = ЗадачаОбъект.События.Добавить();
			НовоеСобытие.СобытиеИдентификатор = "ИзменениеСостоянияЗаказа";
			
			ЗадачаОбъект.ТипПредмета               = "ЗаказПоставщикуЗаказНаПроизводство";
			ЗадачаОбъект.ПользовательДляОповещения = СтрокаНастроек.ПользовательДляОповещения;
			ЗадачаОбъект.Используется              = ЭтотОбъект.ВРаботе;
			ЗадачаОбъект.Родитель                  = ГруппаЗадач.Ссылка;
						
			Если ЗначениеЗаполнено(СтрокаНастроек.ПользовательДляОповещения) Тогда
				ЗадачаОбъект.СпособОповещения = Перечисления.СпособОповещенияАссистентаУправления.СообщениеКонтекстногоОбсужденияПользователю;
			Иначе
				ЗаполнитьСпособОповещенияПоПредставлению(СтрокаНастроек, ЗадачаОбъект);
			КонецЕсли;
			
			ЗадачаОбъект.ЗначенияЗаполнения.Очистить();
			ЗадачаОбъект.ПараметрыУсловия.Очистить();
			
			ВидОперации = "";
			
			Если СтрокаНастроек.ВидОперации = "ЗаказНаряд" Тогда
				ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд;
			ИначеЕсли СтрокаНастроек.ВидОперации = "ЗаказПокупателя" Тогда
				ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаПродажу;
			КонецЕсли;
			
			Если НЕ ТолькоОповестить() Тогда
				ДобавитьПараметрЗадачи(ЗадачаОбъект.ЗначенияЗаполнения, "СостояниеЗаказаПокупателя", СтрокаНастроек.СостояниеЗаказа);
				ДобавитьПараметрЗадачи(ЗадачаОбъект.ЗначенияЗаполнения, "ВариантЗавершенияЗаказаПокупателя", СтрокаНастроек.ВариантЗавершенияЗаказПокупателя);
			КонецЕсли;
			
			Если ВидимостьСтрокиЗаказНаПроизводство() Тогда
				ДобавитьПараметрЗадачи(ЗадачаОбъект.ЗначенияЗаполнения, "СостояниеЗаказаНаПроизводство", СтрокаНастроек.СостояниеЗаказаНаПроизводство);
				ДобавитьПараметрЗадачи(ЗадачаОбъект.ЗначенияЗаполнения, "ВариантЗавершенияЗаказаНаПроизводство", СтрокаНастроек.ВариантЗавершенияЗаказНаПроизводство);
			КонецЕсли;
			
			Если ВидимостьСтрокиЗаказПоставщику() Тогда
				ДобавитьПараметрЗадачи(ЗадачаОбъект.ЗначенияЗаполнения, "СостояниеЗаказаПоставщику", СтрокаНастроек.СостояниеЗаказаПоставщику);
				ДобавитьПараметрЗадачи(ЗадачаОбъект.ЗначенияЗаполнения, "ВариантЗавершенияЗаказаПоставщику", СтрокаНастроек.ВариантЗавершенияЗаказПоставщику);
			КонецЕсли;
			
			ДобавитьПараметрЗадачи(ЗадачаОбъект.ЗначенияЗаполнения, "ТолькоОповестить", ТолькоОповестить());
			ДобавитьПараметрЗадачи(ЗадачаОбъект.ЗначенияЗаполнения, "ВидЗаказаПокупателя", СтрокаНастроек.ВидЗаказа);
			
			ЗадачаОбъект.Наименование = ЭтотОбъект.Заголовок;
			ЗадачаОбъект.Записать();
		
			СтрокаНастроек.Модифицированность = Ложь;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПараметрЗадачи(Таблица, Параметр, Значение)
		
	НоваяСтрока = Таблица.Добавить();
	НоваяСтрока.Параметр = Параметр;
	НоваяСтрока.Значение = Новый ХранилищеЗначения(Значение);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПустыеНастройкиЗадачи()
	
	НовыеНастройки = НастройкиЗадач.Добавить();
	НовыеНастройки.ВидОперации = "ЗаказПокупателя";
	НовыеНастройки.ВидЗаказа = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыЗаказовПокупателей.Основной");
	НовыеНастройки.СостояниеЗаказа = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказовПокупателей.Завершен");
	НовыеНастройки.СостояниеЗаказаПоставщику = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказовПоставщикам.Завершен");
	НовыеНастройки.СостояниеЗаказаНаПроизводство = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СостоянияЗаказовНаПроизводство.Завершен");
	НовыеНастройки.ВариантЗавершенияЗаказПоставщику = ОбщегоНазначения.ПредопределенныйЭлемент("Перечисление.ВариантыЗавершенияЗаказа.Успешно");
	НовыеНастройки.ВариантЗавершенияЗаказНаПроизводство = ОбщегоНазначения.ПредопределенныйЭлемент("Перечисление.ВариантыЗавершенияЗаказа.Успешно");
	НовыеНастройки.ВариантЗавершенияЗаказПокупателя = ОбщегоНазначения.ПредопределенныйЭлемент("Перечисление.ВариантыЗавершенияЗаказа.Успешно");
	
	НовыеНастройки.Модифицированность = Истина;
	
	ЗаполнитьСпособОповещенияПоЗначению(
		НовыеНастройки,
		НовыеНастройки.ПользовательДляОповещения,
		Перечисления.СпособОповещенияАссистентаУправления.СообщениеКонтекстногоОбсужденияБезОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуЗадач()
	
	Результат = Новый Структура();
	
	Результат.Вставить("ИзмененПризнакВРаботе", ИзмененПризнакВРаботе);
	Результат.Вставить("АвторИзменений", АвторИзменений);
	Результат.Вставить("ГруппаЗадач", ИдентификаторГруппы);
	Результат.Вставить("НужноДобавитьВОбсуждение", НЕ ЕстьЗадачиПользователя);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьОсновнуюЗадачу(ЗадачаОбъект)
	
	Отбор = Справочники.ЗадачиАссистентаУправления.НовыйОтборЗадач();
	Отбор.ОсновнаяЗадача = ЗадачаОбъект.Ссылка;
	ОтобранныеЗадачи = Справочники.ЗадачиАссистентаУправления.ПолучитьЗадачи(Отбор);
	ЗадачаОбъект.Удалить();

КонецПроцедуры

&НаСервере
Функция ЭтоЗадачаИзменениеПоЗаказамПоставщикам()
	
	Возврат ИдентификаторГруппы = "ИзменениеСостоянияЗаказаПокупателяПоСостояниюЗаказовПоставщикам";
	
КонецФункции

&НаСервере
Функция ЭтоЗадачаИзменениеПоЗаказамНаПроизводство()
	
	Возврат ИдентификаторГруппы = "ИзменениеСостоянияЗаказаПокупателяПоСостояниюЗаказовНаПроизводство";
	
КонецФункции

&НаСервере
Функция ЭтоЗадачаИзменениеПоЗаказамНаПроизводствоИПоставщикам()
	
	Возврат ИдентификаторГруппы = "ИзменениеСостоянияЗаказаПокупателяПоСостояниюЗаказовПоставщикамИНаПроизводство";
	
КонецФункции

&НаСервере
Функция ЭтоЗадачаСообщенияПоЗаказамПоставщикам()
	
	Возврат ИдентификаторГруппы = "ОповещениеЗаказуПокупателяПоСостояниюЗаказовПоставщикам";
	
КонецФункции

&НаСервере
Функция ЭтоЗадачаСообщенияПоЗаказамНаПроизводство()
	
	Возврат ИдентификаторГруппы = "ОповещениеЗаказуПокупателяПоСостояниюЗаказовНаПроизводство";
	
КонецФункции

&НаСервере
Функция ЭтоЗадачаСообщенияПоЗаказамНаПроизводствоИПоставщикам()
	
	Возврат ИдентификаторГруппы = "ОповещениеЗаказуПокупателяПоСостояниюЗаказовПоставщикамИНаПроизводство";
	
КонецФункции

&НаСервере
Процедура ЗаполнитьОписаниеЗадачиИзмененияПоЗаказамПоставщикам()
	
	Элементы.ОсновноеОписание.Заголовок = НСтр("ru = 'Когда изменится состояние заказов поставщикам, Даша изменит состояние связанного с ними заказа покупателя, а также сообщит, если: '");
	Элементы.ДопФункция_0.Заголовок = НСтр("ru = 'Заказы поставщикам вернулись в более раннее состояние'");
	Элементы.ДопФункция_1.Заголовок = НСтр("ru = 'Заказ покупателя перешел в указанное для него состояние раньше, чем заказы поставщикам прошли состояние, указанное для них'");
	
	Элементы.КартинкаЗадачи.Картинка = БиблиотекаКартинок.АссистентУправленияИзменитьСостояниеЗакупки;
	ЭтотОбъект.Заголовок = НСтр("ru = 'Изменение состояния заказа покупателя по состоянию заказов поставщикам'");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписаниеЗадачиИзмененияПоЗаказамНаПроизводство()
	
	Элементы.ОсновноеОписание.Заголовок = НСтр("ru = 'Когда изменится состояние заказов на производство, Даша изменит состояние связанного с ними заказа покупателя, а также сообщит, если: '");
	Элементы.ДопФункция_0.Заголовок = НСтр("ru = 'Заказы на производство вернулись в более раннее состояние'");
	Элементы.ДопФункция_1.Заголовок = НСтр("ru = 'Заказ покупателя перешел в указанное для него состояние раньше, чем заказы на производство прошли состояние, указанное для них'");
	
	Элементы.КартинкаЗадачи.Картинка = БиблиотекаКартинок.АссистентУправленияИзменитьСостояниеПроизводство;
	ЭтотОбъект.Заголовок = НСтр("ru = 'Изменение состояния заказа покупателя по состоянию заказов на производство'");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписаниеЗадачиИзмененияПоЗаказамНаПроизводствоИПоставщикам()
	
	Элементы.ОсновноеОписание.Заголовок = НСтр("ru = 'Когда изменится состояние заказов на производство и поставщикам, Даша изменит состояние связанного с ним заказа покупателя, а также сообщит, если:'");
	Элементы.ДопФункция_0.Заголовок = НСтр("ru = 'Заказы на производство и/или поставщикам вернулись в более раннее состояние '");
	ПерваяСтрока = НСтр("ru = 'Заказ покупателя перешел в указанное для него состояние раньше, чем заказы на производство и/или поставщикам прошли'");
	ВтораяСтрока = НСтр("ru = 'состояние, указанное для них'");
	Элементы.ДопФункция_1.Заголовок = ПерваяСтрока + Символы.ПС + ВтораяСтрока;
	
	Элементы.КартинкаЗадачи.Картинка = БиблиотекаКартинок.АссистентУправленияИзменитьСостояниеПроизводствоИЗакупки;
	ЭтотОбъект.Заголовок = НСтр("ru = 'Изменение состояния заказа покупателя по состоянию заказов на производство и поставщикам'");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписаниеЗадачиСообщенияПоЗаказамПоставщикам()
	
	Элементы.ОсновноеОписание.Заголовок = НСтр("ru = 'Когда изменится состояние заказов поставщикам, Даша сообщит связанному с ними заказу покупателя, а также сообщит, если: '");
	Элементы.ДопФункция_0.Заголовок = НСтр("ru = 'Заказы поставщикам вернулись в более раннее состояние '");
		
	Элементы.КартинкаЗадачи.Картинка = БиблиотекаКартинок.АссистентУправленияСообщениеЗакупки;
	ЭтотОбъект.Заголовок = НСтр("ru = 'Сообщение заказу покупателя по состоянию заказов поставщикам'");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписаниеЗадачиСообщенияПоЗаказамНаПроизводство()
	
	Элементы.ОсновноеОписание.Заголовок = НСтр("ru = 'Когда изменится состояние заказов на производство, Даша сообщит связанному с ними заказу покупателя, а также сообщит, если: '");
	Элементы.ДопФункция_0.Заголовок = НСтр("ru = 'Заказы на производство вернулись в более раннее состояние '");
		
	Элементы.КартинкаЗадачи.Картинка = БиблиотекаКартинок.АссистентУправленияСообщениеПроизводство;
	ЭтотОбъект.Заголовок = НСтр("ru = 'Сообщение заказу покупателя по состоянию заказов на производство'");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписаниеЗадачиСообщенияПоЗаказамНаПроизводствоИПоставщикам()
	
	Элементы.ОсновноеОписание.Заголовок = НСтр("ru = 'Когда изменится состояние заказов на производство и поставщикам, Даша сообщит связанному с ними заказу покупателя, а также сообщит, если:'");
	Элементы.ДопФункция_0.Заголовок = НСтр("ru = 'Заказы поставщикам и/или на производство вернулись в более раннее состояние '");
	
	Элементы.КартинкаЗадачи.Картинка = БиблиотекаКартинок.АссистентУправленияСообщениеЗакупкиИПроизводство;
	ЭтотОбъект.Заголовок = НСтр("ru = 'Сообщение заказу покупателя по состоянию заказов на производство и поставщикам'");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписаниеЗадачи()
	
	Если ЭтоЗадачаИзменениеПоЗаказамПоставщикам() Тогда
		ЗаполнитьОписаниеЗадачиИзмененияПоЗаказамПоставщикам();
		Возврат;
	КонецЕсли;
	
	Если ЭтоЗадачаИзменениеПоЗаказамНаПроизводство() Тогда
		ЗаполнитьОписаниеЗадачиИзмененияПоЗаказамНаПроизводство();
		Возврат;
	КонецЕсли;
	
	Если ЭтоЗадачаИзменениеПоЗаказамНаПроизводствоИПоставщикам() Тогда
		ЗаполнитьОписаниеЗадачиИзмененияПоЗаказамНаПроизводствоИПоставщикам();
		Возврат;
	КонецЕсли;
	
	Если ЭтоЗадачаСообщенияПоЗаказамПоставщикам() Тогда
		ЗаполнитьОписаниеЗадачиСообщенияПоЗаказамПоставщикам();
		Возврат;
	КонецЕсли;
	
	Если ЭтоЗадачаСообщенияПоЗаказамНаПроизводство() Тогда
		ЗаполнитьОписаниеЗадачиСообщенияПоЗаказамНаПроизводство();
		Возврат;
	КонецЕсли;
	
	Если ЭтоЗадачаСообщенияПоЗаказамНаПроизводствоИПоставщикам() Тогда
		ЗаполнитьОписаниеЗадачиСообщенияПоЗаказамНаПроизводствоИПоставщикам();
		Возврат;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
