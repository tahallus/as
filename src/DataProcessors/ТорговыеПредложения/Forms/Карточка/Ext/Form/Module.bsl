
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ИдентификаторЗаказа",      ИдентификаторЗаказа);
	Параметры.Свойство("ИдентификаторКонтрагента", ИдентификаторКонтрагента);
	Параметры.Свойство("Идентификатор",            Идентификатор);
	Параметры.Свойство("Номенклатура",             Номенклатура);
	Параметры.Свойство("Характеристика",           Характеристика);
	Параметры.Свойство("Валюта",                   Валюта);
	Параметры.Свойство("Наименование",             Заголовок);
	Параметры.Свойство("Организация",              Организация);
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗаказа) Тогда
		Элементы.ДобавитьКЗаказу.Видимость = Ложь;
	КонецЕсли;
	
	// Запуск фонового задания для поиска.
	ФоновоеЗаданиеОбновления = ОбновитьКарточкуТорговогоПредложения(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Обработка завершения задания загрузки данных.
	Если ЗначениеЗаполнено(ФоновоеЗаданиеОбновления) Тогда
		ОжидатьЗавершенияФоновогоЗаданияОбновления();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КарточкаВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Если Область.Имя = "СледующееИзображение" Тогда	
		
		Элементы. ТабличныйДокумент.ТекущаяОбласть = ТабличныйДокумент.Области.ЗаголовокИзображения;
		
		ПереключитьИзображение(Истина);
		
	ИначеЕсли Область.Имя = "ПредыдущееИзображение" Тогда	
		
		Элементы.ТабличныйДокумент.ТекущаяОбласть = ТабличныйДокумент.Области.ЗаголовокИзображения;
		
		ПереключитьИзображение(Ложь);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КарточкаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Если Элемент.ТекущаяОбласть.Имя = "Контрагент_НаименованиеКонтрагента" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьПрофильКонтрагента();
	ИначеЕсли Элемент.ТекущаяОбласть.Имя = "КатегорияСервиса_ПредставлениеКатегории" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуПоиска();
	ИначеЕсли Элемент.ТекущаяОбласть.Имя = "Контрагент_СайтКонтрагента" Тогда
		Если ЗначениеЗаполнено(СайтКонтрагента) Тогда
			ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(СайтКонтрагента);
		КонецЕсли;
		СтандартнаяОбработка = Ложь;
	ИначеЕсли Элемент.ТекущаяОбласть.Имя = "НоменклатураСервиса_ОписаниеНоменклатурыСервиса" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуНоменклатуруСервиса();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьКЗаказу(Команда)
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ИдентификаторЗаказа",      ИдентификаторЗаказа);
	ПараметрыОповещения.Вставить("ИдентификаторКонтрагента", ИдентификаторКонтрагента);
	ПараметрыОповещения.Вставить("Идентификатор",            Идентификатор);
	ПараметрыОповещения.Вставить("Номенклатура",             Номенклатура);
	ПараметрыОповещения.Вставить("Характеристика",           Характеристика);
	ПараметрыОповещения.Вставить("Валюта",                   Валюта);
	Оповестить("ТорговыеПредложения_ИзменитьКоличествоВЗаказе", ПараметрыОповещения);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьПрофильКонтрагента()
	
	ПараметрыОткрытия = БизнесСетьКлиентСервер.ОписаниеИдентификацииОрганизацииКонтрагентов();
	ПараметрыОткрытия.ИНН = ИННКонтрагента;
	ПараметрыОткрытия.КПП = КППКонтрагента;
	БизнесСетьСлужебныйКлиент.ОткрытьПрофильУчастника(ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПоиска()
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ИдентификаторКатегории",          ИдентификаторКатегории);
	ПараметрыОткрытия.Вставить("ОтборНаименование",               Наименование);
	ПараметрыОткрытия.Вставить("Валюта",                          Валюта);
	ПараметрыОткрытия.Вставить("РежимВыбораТорговогоПредложения", Истина);
	
	ОчиститьСообщения();
	ТорговыеПредложенияКлиент.ОткрытьФормуПоискаПоОтборам(ПараметрыОткрытия);
	
	// Дополнительно оповестим формы, если форма поиска ранее была открыта.
	Оповестить("ТорговыеПредложения_ПоискПоОтборам_Обновить", ПараметрыОткрытия, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНоменклатуруСервиса()
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ИдентификаторНоменклатуры", ИдентификаторНоменклатурыСервиса);
	ПараметрыОткрытия.Вставить("ИдентификаторХарактеристики", ИдентификаторХарактеристикиСервиса);
	ОчиститьСообщения();

	// Открытие формы если существует подсистема РаботаСНоменклатурой
	Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСНоменклатуройКлиент");
	Модуль.ОткрытьФормуКарточкиНоменклатуры(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыОткрытия), ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершенияФоновогоЗаданияОбновления()
	
	Если ФоновоеЗаданиеОбновления.Статус = "Выполнено" Тогда
		ОбновитьКарточкуТорговогоПредложенияЗавершение(ФоновоеЗаданиеОбновления, Неопределено);
	Иначе
		// Инициализация обработчик ожидания завершения.
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Чтение торгового предложения.'");
		ПараметрыОжидания.ВыводитьПрогрессВыполнения = Ложь;
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ПараметрыОжидания.ОповещениеПользователя.Показать = Ложь;
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		ПараметрыОжидания.Вставить("ИдентификаторЗадания", ФоновоеЗаданиеОбновления.ИдентификаторЗадания);
		ОбработкаЗавершенияПоиска = Новый ОписаниеОповещения("ОбновитьКарточкуТорговогоПредложенияЗавершение",
			ЭтотОбъект, ФоновоеЗаданиеОбновления);
			
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗаданиеОбновления, ОбработкаЗавершенияПоиска, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры
	
&НаКлиенте
Процедура ОбновитьКарточкуТорговогоПредложенияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат.Сообщения <> Неопределено Тогда
		Для Каждого Сообщение Из Результат.Сообщения Цикл
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	ЕстьОшибки = Ложь;
	ОтображениеСостояния = Элементы.ТабличныйДокумент.ОтображениеСостояния;
	
	Если Результат = Неопределено Тогда
		ЕстьОшибки = Истина;
	Иначе
		КэшКарточки = ЗакэшироватьКарточкуТорговогоПредложенияНаСервере(Результат.АдресРезультата, УникальныйИдентификатор, ЕстьОшибки);
	КонецЕсли;
	
	Если ЕстьОшибки = Ложь Тогда
		ТабличныйДокумент = ПолучитьИзВременногоХранилища(КэшКарточки.АдресТабличногоДокумента);
		КэшИзображений    = КэшКарточки.КэшИзображений;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, КэшКарточки.Реквизиты);
		
		Элементы.ДобавитьКЗаказу.Доступность = Истина;
		ОтображениеСостояния.Видимость       = Ложь;
		ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.НеИспользовать;
	Иначе
		ОтображениеСостояния.Видимость = Истина;
		ОтображениеСостояния.Картинка  = Неопределено;
		ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
		ОтображениеСостояния.Текст = НСтр("ru = 'Ошибка загрузки торгового предложения.'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗакэшироватьКарточкуТорговогоПредложенияНаСервере(Знач АдресРезультата, Знач УникальныйИдентификатор, ЕстьОшибки)
	
	КэшКарточки = Новый Структура;
	КэшКарточки.Вставить("АдресТабличногоДокумента", "");
	КэшКарточки.Вставить("КэшИзображений", Новый СписокЗначений);
	КэшКарточки.Вставить("Реквизиты", Новый Структура);
	
	РезультатЗадания = БизнесСеть.ПолучитьУдалитьИзВременногоХранилища(АдресРезультата);
	Если ЗначениеЗаполнено(РезультатЗадания) Тогда
		КэшКарточки.Реквизиты = РезультатЗадания.Реквизиты;
		КэшКарточки.АдресТабличногоДокумента = ПоместитьВоВременноеХранилище(РезультатЗадания.ТабличныйДокумент, УникальныйИдентификатор);
		Если РезультатЗадания.Реквизиты.ДанныеИзображений.Количество() Тогда  
			Для Каждого ЭлементКоллекции Из РезультатЗадания.Реквизиты.ДанныеИзображений Цикл 
				КэшКарточки.КэшИзображений.Добавить(ПоместитьВоВременноеХранилище(ЭлементКоллекции.Значение, УникальныйИдентификатор))
			КонецЦикла;
		КонецЕсли;
	Иначе
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Возврат КэшКарточки;
	
КонецФункции

&НаСервере
Функция ОбновитьКарточкуТорговогоПредложения(Отказ)
	
	Если ФоновоеЗаданиеОбновления <> Неопределено Тогда
		ОтменитьВыполнениеЗадания(ФоновоеЗаданиеОбновления.ИдентификаторЗадания);
		Отказ = Истина;
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	
	ПараметрыЗапроса.Вставить("Идентификатор", Идентификатор);
	ПараметрыЗапроса.Вставить("Валюта",        Валюта);
	ПараметрыЗапроса.Вставить("Организация",   Организация);
	
	Задание = Новый Структура("ИмяПроцедуры, Наименование, ПараметрыПроцедуры");
	Задание.Наименование = НСтр("ru = '1С:Бизнес-сеть. Чтение торгового предложения.'");
	Задание.ИмяПроцедуры = "ТорговыеПредложенияСлужебный.СформироватьПредставлениеТорговогоПредложения";
	Задание.ПараметрыПроцедуры = ПараметрыЗапроса;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = Задание.Наименование;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(Задание.ИмяПроцедуры,
		Задание.ПараметрыПроцедуры, ПараметрыВыполнения);
	
	Если ДлительнаяОперация <> Неопределено
		И ДлительнаяОперация.Статус = "Выполняется" Тогда
		ОтображениеСостояния = Элементы.ТабличныйДокумент.ОтображениеСостояния;
		ОтображениеСостояния.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
		ОтображениеСостояния.Текст = НСтр("ru = 'Торговое предложение загружается...'");
		ОтображениеСостояния.Видимость = Истина;
	КонецЕсли;
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОтменитьВыполнениеЗадания(ИдентификаторЗадания)
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
		ИдентификаторЗадания = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПереключитьИзображение(СледующееИзображение)
		
	Если СледующееИзображение И ИндексТекущегоИзображения + 1 = КэшИзображений.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не СледующееИзображение И ИндексТекущегоИзображения = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Если СледующееИзображение Тогда
		ИндексТекущегоИзображения = ИндексТекущегоИзображения + 1;
	Иначе
		ИндексТекущегоИзображения = ИндексТекущегоИзображения - 1;	
	КонецЕсли;
	
	ДанныеКартинки = ПолучитьИзВременногоХранилища(КэшИзображений.Получить(ИндексТекущегоИзображения).Значение);
	
	ТабличныйДокумент.Области.ЗаголовокИзображения.Текст 
		= СтрШаблон("Изображение (%1/%2)", 
			ИндексТекущегоИзображения + 1, КэшИзображений.Количество());
						
	ТабличныйДокумент.Рисунки.ИзображениеПредложения.Картинка = Новый Картинка(ДанныеКартинки);
	
КонецПроцедуры

#КонецОбласти