
#Область ОписаниеПеременных

&НаКлиенте
Перем МассивИзмененныхРеквизитов;

#КонецОбласти

#Область Служебные

&НаКлиенте
Процедура ПроверитьДублированиеМОЛ(Отказ = Ложь)
	
	Если ЗначениеЗаполнено(КонтекстПечати.ПодписьМОЛ1)
		И ЗначениеЗаполнено(КонтекстПечати.ПодписьМОЛ2)
		И КонтекстПечати.ПодписьМОЛ1 = КонтекстПечати.ПодписьМОЛ2 Тогда
		
		ТекстСообщения = НСтр("ru ='Совпадает подпись материально-ответственных лиц. Просьба исправить дублирование.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "КонтекстПечати.ПодписьМОЛ2", , Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДеталиПоКомиссии(Комиссия)
	
	ЗаполнитьДеталиПоКомиссииНаСервере(Комиссия);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗафиксироватьИзменениеЗначенияРеквизита(ИмяРеквизита)
	
	Если МассивИзмененныхРеквизитов.Найти(ИмяРеквизита) = Неопределено Тогда
		
		МассивИзмененныхРеквизитов.Добавить(ИмяРеквизита);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИзмененияИЗакрытьФорму(Команда = Неопределено)
	
	ПараметрыЗакрытия = Новый Структура;
	Если Команда <> Неопределено Тогда
		
		ПараметрыЗакрытия.Вставить("Команда", Команда);
		
	КонецЕсли;
	
	ИзмененныеРеквизиты = Новый Структура;
	Для каждого ЭлементМассива Из МассивИзмененныхРеквизитов Цикл
		
		ИзмененныеРеквизиты.Вставить(ЭлементМассива, КонтекстПечати[ЭлементМассива]);
		
	КонецЦикла;
	ПараметрыЗакрытия.Вставить("ИзмененныеРеквизиты", ИзмененныеРеквизиты);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаСервере
Процедура ЗаголовокФормы()
	
	ЭтаФорма.Заголовок = Обработки.РеквизитыПечати.ЗаголовокФормы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеталиПоКомиссииНаСервере(Комиссия)
	Перем Итератор;
	
	Если НЕ ЗначениеЗаполнено(Комиссия) Тогда
		
		КомиссияПредседатель = Неопределено;
		КомиссияЧлен1 = Неопределено;
		КомиссияЧлен2 = Неопределено;
		КомиссияЧлен3 = Неопределено;
		Возврат;
		
	КонецЕсли;
	
	ДанныеКомиссии = Справочники.Комиссии.ПодписиКомиссииМассивом(Комиссия);
	Если ДанныеКомиссии.РазмерКомиссии > 0 Тогда
		
		Если ЗначениеЗаполнено(ДанныеКомиссии.ПодписьПредседателя) Тогда
			
			КомиссияПредседатель = ДанныеКомиссии.ПодписьПредседателя;
			
		КонецЕсли;
		
		БлижайшийСвободныйРеквизитФормы = 1;
		Для каждого ЭлементМассива Из ДанныеКомиссии.ПодписиКомиссии Цикл
			
			Если ДанныеКомиссии.ПодписьПредседателя = ЭлементМассива Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			ЭтотОбъект["КомиссияЧлен" + БлижайшийСвободныйРеквизитФормы] = ЭлементМассива;
			
			БлижайшийСвободныйРеквизитФормы = БлижайшийСвободныйРеквизитФормы + 1;
			Если БлижайшийСвободныйРеквизитФормы > 3 Тогда
				
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьПодписиПоУмолчаниюНаСервере()
	
	ДокументОбъект = ДанныеФормыВЗначение(КонтекстПечати, Тип("ДокументОбъект.ИнвентаризацияЗапасов"));
	Обработки.РеквизитыПечати.ПодписиПоУмолчанию(ДокументОбъект);
	ЗначениеВДанныеФормы(ДокументОбъект, КонтекстПечати);
	
КонецПроцедуры

&НаСервере
Процедура ДоступностьКомандФормы()
	
	Если НЕ ПравоДоступа("Изменение", КонтекстПечати.Ссылка.Метаданные()) Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаВосстановитьПодписиПоУмолчанию", "Доступность", Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Форма

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивОбъектовМетаданных = Новый Массив(1);
	МассивОбъектовМетаданных[0] = Параметры.КонтекстПечати.Ссылка.Метаданные();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.Источники = МассивОбъектовМетаданных;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПечатьДокументовУНФ.УстановитьОтображениеПодменюПечати(Элементы.ПодменюПечать);
	
	ЗаполнитьЗначенияСвойств(КонтекстПечати, Параметры.КонтекстПечати, "Организация, СтруктурнаяЕдиница, Дата, ДатаОкончания, Комиссия, ПодписьМОЛ1, ПодписьМОЛ2, ПодписьПроверяющего, ДокументОснованиеНомер, ДокументОснованиеДата, ВидЦенностей, ФормаСобственностиЦенностей");
	
	ЗаголовокФормы();
	ЗаполнитьДеталиПоКомиссииНаСервере(КонтекстПечати.Комиссия);
	ДоступностьКомандФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МассивИзмененныхРеквизитов = Новый Массив;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ПроверитьДублированиеМОЛ(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область Библиотеки

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ЗаписатьИзмененияИЗакрытьФорму(Команда);
	
	Возврат; // работа типового метода не предусмотрена
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, КонтекстПечати);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	
	Возврат; // работа типового метода не предусмотрена
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, КонтекстПечати, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	Возврат; // работа типового метода не предусмотрена
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, КонтекстПечати);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура ОК(Команда)
	
	ЗаписатьИзмененияИЗакрытьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьПодписиПоУмолчанию(Команда)
	
	ПредыдущиеЗначения = Новый Структура("Комиссия, ПодписьМОЛ1, ПодписьМОЛ2, ПодписьПроверяющего");
	ЗаполнитьЗначенияСвойств(ПредыдущиеЗначения, КонтекстПечати);
	
	ПолучитьПодписиПоУмолчаниюНаСервере();
	
	Для каждого ЭлементСтруктуры Из ПредыдущиеЗначения Цикл
		
		Если ЭлементСтруктуры.Значение <> КонтекстПечати[ЭлементСтруктуры.Ключ] Тогда
			
			ЗафиксироватьИзменениеЗначенияРеквизита(ЭлементСтруктуры.Ключ);
			
			Если ЭлементСтруктуры.Ключ = "Комиссия" Тогда
				
				ЗаполнитьДеталиПоКомиссии(КонтекстПечати.Комиссия);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Реквизиты

&НаКлиенте
Процедура МОЛ1ПриИзменении(Элемент)
	
	ПроверитьДублированиеМОЛ();
	ЗафиксироватьИзменениеЗначенияРеквизита(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура МОЛ2ПриИзменении(Элемент)
	
	ПроверитьДублированиеМОЛ();
	ЗафиксироватьИзменениеЗначенияРеквизита(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура КомиссияПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита(Элемент.Имя);
	
	ЗаполнитьДеталиПоКомиссии(КонтекстПечати.Комиссия);
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеПроверилПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеНомерПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеДатаПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЦенностейПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаСобственностиЦенностейПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита(Элемент.Имя);
	
КонецПроцедуры

#КонецОбласти
