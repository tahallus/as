#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ЗаголовокФормы() Экспорт
	
	Возврат НСтр("ru ='Реквизиты печати'");
	
КонецФункции

Функция ОписанияФормНастройкиПечати()
	
	ОписанияФормНастройкиПечати = Новый Структура;
	ОписанияФормНастройкиПечати.Вставить("ИнвентаризацияЗапасов", "Обработка.РеквизитыПечати.Форма.РеквизитыПечатиИнвентаризацияЗапасов");
	
	Возврат ОписанияФормНастройкиПечати;
	
КонецФункции

Процедура ЗаполнитьНастройкиПечати(ИсточникПечати, НастройкиПечати) Экспорт
	
	НастройкиПечати = Новый Структура;
	НастройкиПечати.Вставить("ПараметрыПечати", Новый Структура("КонтекстПечати", ИсточникПечати));
	НастройкиПечати.Вставить("ИмяФормы", ОписанияФормНастройкиПечати()[ИсточникПечати.Метаданные().Имя]);
	
КонецПроцедуры

Процедура ПодписиПоУмолчанию(ОбъектЗаполнения, Организация = Неопределено, СтруктурнаяЕдиница = Неопределено, Касса = Неопределено) Экспорт
	
	ПодписьДокументовУНФ.ОбработкаЗаполненияПодписей(ОбъектЗаполнения, Организация, СтруктурнаяЕдиница, Касса);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыВыбораБанковскогоСчетаОрганизации(ЭлементБанковскийСчет, КонтекстПечати) Экспорт
	
	Если ЗначениеЗаполнено(КонтекстПечати.Договор) Тогда
		
		Если КонтекстПечати.Договор.РасчетыВУсловныхЕдиницах Тогда
			
			СвязиПараметровВыбора = Новый Массив(ЭлементБанковскийСчет.СвязиПараметровВыбора);
			Для ИндексМассива = 0 По СвязиПараметровВыбора.Количество() - 1 Цикл
				
				СвязьВыбора = СвязиПараметровВыбора.Получить(ИндексМассива);
				Если СвязьВыбора.Имя = "Отбор.ВалютаДенежныхСредств" Тогда
					
					СвязиПараметровВыбора.Удалить(ИндексМассива);
					ЭлементБанковскийСчет.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиПараметровВыбора);
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
			ЗначениеОтбора = Новый Массив;
			ЗначениеОтбора.Добавить(КонтекстПечати.Договор.ВалютаРасчетов);
			ЗначениеОтбора.Добавить(Константы.НациональнаяВалюта.Получить());
			
			НовыйПараметр = Новый ПараметрВыбора("Отбор.ВалютаДенежныхСредств", Новый ФиксированныйМассив(ЗначениеОтбора));
			
			МассивПараметровВыбора = Новый Массив;
			МассивПараметровВыбора.Добавить(НовыйПараметр);
			
			ЭлементБанковскийСчет.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли