&НаКлиенте
Перем КонтекстЭДОКлиент Экспорт;

&НаКлиенте
Перем СсылкаНаДокумент;

&НаКлиенте
Перем МенеджерКриптографии;

&НаКлиенте
Перем СертификатКриптографии;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Из параметров
	ДокументЗаявление = Параметры.ДокументЗаявление;
	
	НапоминитьПозжеПроЗаявление(ДокументЗаявление);
	
	Если ДокументЗаявление.ПереиздатьСертификатЭДО Тогда
		Элементы.ЗавершитьПодключениеКЭДО.Заголовок = НСтр("ru = 'Завершить обновление сертификата 1С-ЭДО'");
		Элементы.ЗавершениеНастройки1СЭДО.Заголовок = НСтр("ru = 'Далее будет выполнено завершение обновления сертификата 1С-ЭДО.'");
	КонецЕсли;
	
	КонтекстЭДОСервер  =  ДокументооборотСКО.ПолучитьОбработкуЭДО();
	КонтекстЭДОСервер.УстановитьПредставлениеЗаявленияВВидеФорматированнойСтроки(ДокументЗаявление, Элементы.ТекстОповещения, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ПрограммноеЗакрытие Тогда
		Возврат
	КонецЕсли;
	
	Отказ = Истина;
	ПодключитьОбработчикОжидания("Подключаемый_СпроситьПроЭДОПередЗакрытием", 0.1, Истина);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТребуетсяПомощь2Нажатие(Элемент)
	ОткрытьФормуПомощи();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаБольшеНеНапоминать(Команда)
	
	ПрограммноеЗакрытие = Истина;
	НапоминитьПозжеПроЗаявление(ДокументЗаявление, Истина);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"КомандаБольшеНеНапоминать_Завершение", 
		ЭтотОбъект);
		
	Текст = НСтр("ru = 'Вы сможете завершить настройку 1С-ЭДО по данному заявлению позже, нажав в форме заявления кнопку ""Обновить"" сверху.
                  |Все заявления доступны по ссылке ""Список заявлений"" в разделе ""Настройки"" формы ""1С-Отчетность""'");
	ПоказатьПредупреждение(ОписаниеОповещения, Текст);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПодключениеКЭДО(Команда)
	
	ПрограммноеЗакрытие = Истина;
	
	Закрыть(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_СпроситьПроЭДОПередЗакрытием() Экспорт
	
	КонтекстЭДОКлиент.Подключаемый_СпроситьПроЭДОПередЗакрытием(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаБольшеНеНапоминать_Завершение(ВходящийКонтекст) Экспорт
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(РезультатПолученияКонтекста, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = РезультатПолученияКонтекста.КонтекстЭДО;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыОткрытияПомощи()
	
	ФИО = СокрЛП(ДокументЗаявление.ВладелецЭЦПИмя + " " + ДокументЗаявление.ВладелецЭЦПОтчество);
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Фио", ФИО);
	ДополнительныеПараметры.Вставить("НомерТелефона", ДокументЗаявление.ТелефонМобильныйДляАвторизации);

	Возврат ДополнительныеПараметры;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуПомощи()
	
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.Мастер_Помощь", ПараметрыОткрытияПомощи());
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НапоминитьПозжеПроЗаявление(ЗаявлениеАбонента, БольшеНеНапоминать = Ложь)
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	КонтекстЭДОСервер.НапоминитьПозжеПроЗаявление(ЗаявлениеАбонента, БольшеНеНапоминать);

КонецПроцедуры

#КонецОбласти


