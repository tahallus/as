#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ДекорацияОписание.Заголовок = Параметры.Описание;
	ИзменилиСостав = Ложь;
	СписокВыбора = Параметры.СписокВыбора;
	
	Если СписокВыбора = Неопределено Тогда
		КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
		Если КонтекстЭДОСервер <> Неопределено Тогда
			СписокВыбора = КонтекстЭДОСервер.ОрганизацииСОграничениемПрав();
		Иначе
			СписокВыбора = Новый Массив;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого СтрокаМассива Из СписокВыбора Цикл
		НоваяСтрока = ТаблицаВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаМассива);
		НоваяСтрока.ИсходноеЗначение = НоваяСтрока.НетОграничения;
	КонецЦикла;	
	
	ЕстьДоступ = Пользователи.ЭтоПолноправныйПользователь();
	
	Если Параметры.АвтоматическийВызов Тогда
		Режим = ?(ЕстьДоступ, 1, 3);
	Иначе
		Режим = ?(ЕстьДоступ, 2, 4);
	КонецЕсли;	
	
	Если Режим = 3 Тогда
		КлючСохраненияПоложенияОкна = "Предупреждение";
	КонецЕсли;
	
	НачальныеНастройкиФормы();
	ПодготовитьОформлениеФормы();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	ПодключитьОбработчикОжидания("ИзменитьРазмерыФормы", 0.2, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Изменить(Команда)
	
	Если ИзменитьСоставОграничений() Тогда
		Оповестить("Изменены пользователи учетной записи");
		ЗакрытьФорму(Истина);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Пропустить(Команда)
	
	ЗакрытьФорму(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	УстановитьПометки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВсе(Команда)
	
	УстановитьПометки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Если Режим = 3 Тогда
		ЗакрытьФорму(Истина);
	Иначе	
		ЗакрытьФорму(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаВыбора

&НаКлиенте
Процедура ТаблицаВыбораПометкаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТаблицаВыбора.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;	
		
	Если НЕ ЕстьДоступ Тогда
		ТекущаяСтрока.НетОграничения = ТекущаяСтрока.ИсходноеЗначение;
	Иначе
		ИзменилиСостав = Истина;
		УправлениеФормой();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция УправлениеФормой()
	
	НашлиСтроки	= ТаблицаВыбора.НайтиСтроки(Новый Структура("НетОграничения", Истина));
	Если НашлиСтроки.Количество() = 1 Тогда
		ТекстИтогов = НСтр("ru = 'Выбрана %1 из %2 организаций'");
	Иначе
		ТекстИтогов = НСтр("ru = 'Выбрано %1 из %2 организаций'");
	КонецЕсли;
	
	ТекстИтогов = СтрШаблон(ТекстИтогов, НашлиСтроки.Количество(), ТаблицаВыбора.Количество());
	
	Элементы.Изменить.Доступность = ИзменилиСостав;
	Элементы.ДекорацияВсего.Заголовок = ТекстИтогов;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьРазмерыФормы()
	
	ИмяЭлемента = "ДекорацияПредупреждение";
	Элементы[ИмяЭлемента].Видимость = НЕ Элементы[ИмяЭлемента].Видимость;
	Элементы[ИмяЭлемента].Видимость = НЕ Элементы[ИмяЭлемента].Видимость;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(РезультатРаборы = Неопределено)
	
	Закрыть(РезультатРаборы);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометки(ТекущееЗначение)
	
	Для Каждого СтрокаТаблицы Из ТаблицаВыбора Цикл
		СтрокаТаблицы.НетОграничения = ТекущееЗначение;
	КонецЦикла;
	
	ИзменилиСостав = Истина;
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Функция ИзменитьСоставОграничений()
	
	Результат = Ложь;
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	ПользовательОрганизаций = Пользователи.ТекущийПользователь();
		
	НаборЗаписей = РегистрыСведений.ПользователиУчетныхЗаписейДокументооборота.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(ПользовательОрганизаций);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	Для Каждого СтрокаТаблицы Из ТаблицаВыбора Цикл
		Если НЕ СтрокаТаблицы.НетОграничения Тогда
			Продолжить;
		КонецЕсли;
		
		УчетнаяЗапись = КонтекстЭДОСервер.УчетнаяЗаписьОрганизации(СтрокаТаблицы.Ссылка);
		
		НоваяСтрока = НаборЗаписей.Добавить();
		НоваяСтрока.УчетнаяЗапись = УчетнаяЗапись;
		НоваяСтрока.Пользователь = ПользовательОрганизаций;
	КонецЦикла;
	
	Попытка
		НаборЗаписей.Записать();
		Результат = Истина;
		ДокументооборотСКОВызовСервера.УстановитьПараметрСеансаТекущиеУчетныеЗаписиНалогоплательщика();
	Исключение
		РегламентированнаяОтчетностьКлиентСервер.СообщитьОбОшибке(ОписаниеОшибки(), Ложь,
			"Не удалось обновить список учетных записей налогоплательщика пользователя""" + СокрЛП(ПользовательОрганизаций) + """.");
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПодготовитьОформлениеФормы()
	
	ОднаОрганизация = ТаблицаВыбора.Количество() = 1;
	
	Если Режим = 1 Тогда
		Если ОднаОрганизация Тогда
			ТекстЗаголовока = НСтр("ru = 'Выберите организацию, по которой производится обновление'");
		Иначе
			ТекстЗаголовока = НСтр("ru = 'Выберите организации, по которым производится обновление'");
		КонецЕсли;	
		Описание = НСтр("ru = 'Обновление 1С-Отчетности по ФНС, ПФР и Росстат производится по той организации, пользователем которой Вы являетесь.'")
					+ Символы.ПС
					+ НСтр("ru = 'Проверьте и скорректируйте при необходимости состав организаций.'");
		
	ИначеЕсли Режим = 2 Тогда				
		Если ОднаОрганизация Тогда
			ТекстЗаголовока = НСтр("ru = 'Организация, по которой производится обновление'");
		Иначе
			ТекстЗаголовока = НСтр("ru = 'Организации, по которым производится обновление'");
		КонецЕсли;	
		Описание = НСтр("ru = 'Обновление 1С-Отчетности по ФНС, ПФР и Росстат производится по тем организациям, пользователем которых Вы являетесь.'");
		
	ИначеЕсли Режим = 3 Тогда
		ТекстЗаголовока = НСтр("ru = 'Нет организаций для обновления'");
		Описание = НСтр("ru = 'Вы не являетесь пользователем 1С-Отчетности ни по одной организации для обновления по ФНС, ПФР или Росстат.'")
					+ Символы.ПС
					+ НСтр("ru = 'Чтобы назначить Вас пользователем обратитесь к администратору.'");
		Элементы.ГруппаПодвал.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
		Элементы.Отмена.Заголовок = НСтр("ru = 'Закрыть'");
		Элементы.Отмена.КнопкаПоУмолчанию = Истина;
					
	ИначеЕсли Режим = 4 Тогда
		Если ОднаОрганизация Тогда
			ТекстЗаголовока = НСтр("ru = 'Организация, по которой производится обновление'");
		Иначе
			ТекстЗаголовока = НСтр("ru = 'Организации, по которым производится обновление'");
		КонецЕсли;	
		Описание = НСтр("ru = 'Обновление 1С-Отчетности по ФНС, ПФР и Росстат производится по той организации, пользователем которой Вы являетесь.'")
					+ Символы.ПС
					+ НСтр("ru = 'Чтобы изменить состав организаций обратитесь к администратору.'");
		Элементы.Отмена.Заголовок = НСтр("ru = 'Закрыть'");
		Элементы.Отмена.КнопкаПоУмолчанию = Истина;
					
	КонецЕсли;
	
	Элементы.ГруппаОсновное.Видимость = Режим <> 3;
	Элементы.ДекорацияПредупреждение.Видимость = Режим = 3;
	Элементы.Изменить.Видимость = ЕстьДоступ;
	Элементы.Отмена.Видимость = Режим <> 1;
	Элементы.Продолжить.Видимость = Режим = 1;
	Элементы.ГруппаИтоги.Доступность = ЕстьДоступ;
	Элементы.ДекорацияОписание.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(Описание);
	Заголовок = ТекстЗаголовока;
	
КонецПроцедуры

&НаСервере
Процедура НачальныеНастройкиФормы()
	
	ПолныйПуть = ЭтотОбъект.ИмяФормы;
	
	ВсеПути = Новый Массив;
	ВсеПути.Добавить("НастройкиФормы");
	ВсеПути.Добавить("НастройкиОкна");
	ВсеПути.Добавить("Такси/НастройкиОкна");
	ВсеПути.Добавить("НастройкиОкнаВебКлиента");
	ВсеПути.Добавить("Предупреждение/НастройкиОкна");
	ВсеПути.Добавить("Предупреждение/Такси/НастройкиОкна");
	ВсеПути.Добавить("Предупреждение/НастройкиОкнаВебКлиента");
	
	Для Каждого СтрокаМассива Из ВсеПути Цикл
		ОбщегоНазначения.ХранилищеСистемныхНастроекУдалить(ПолныйПуть + "/" + СтрокаМассива, Неопределено, ИмяПользователя());
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти