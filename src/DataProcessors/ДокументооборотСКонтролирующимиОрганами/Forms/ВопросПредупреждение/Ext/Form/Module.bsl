
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеЭУ();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ОсновнойТекст) Тогда
		Элементы.ОсновнойТекст.Заголовок = Параметры.ОсновнойТекст;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ТекстПереключатель1) Тогда
		Элементы.ПереключательСогласен.СписокВыбора.Добавить(0, Параметры.ТекстПереключатель1, Истина);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ТекстПереключатель2) Тогда
		Элементы.ПереключательСогласен.СписокВыбора.Добавить(1, Параметры.ТекстПереключатель2);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Параметры.ТекстПереключатель1) И НЕ ЗначениеЗаполнено(Параметры.ТекстПереключатель2) Тогда
		Элементы.ГруппаПереключателя.Видимость = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.КартинкаДиалога) Тогда
		Элементы.ДекорацияКартинка.Картинка = Параметры.КартинкаДиалога;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.КнопкиДиалога) Тогда
		КнопкиДиалога = Параметры.КнопкиДиалога;
		Если КнопкиДиалога.Количество() >= 1 Тогда
			Элементы.Продолжить.Заголовок = КнопкиДиалога[0].Представление;
			Элементы.Продолжить.Шрифт = Новый Шрифт;
		КонецЕсли;
		Если КнопкиДиалога.Количество() >= 2 Тогда
			Элементы.Отмена.Заголовок = КнопкиДиалога[1].Представление;
		Иначе
			Элементы.Отмена.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(КнопкиДиалога) И КнопкиДиалога.Количество() = 1 Тогда
		Отказ = Истина;
		
		Если ЗначениеЗаполнено(КнопкиДиалога[0].Значение) Тогда
			Закрыть(КнопкиДиалога[0].Значение);
			
		Иначе
			Закрыть(КодВозвратаДиалога.ОК);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПереключательСогласенПриИзменении(Элемент)
	
	УправлениеЭУ();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДействиеПродолжить(Команда)
	
	Если ЗначениеЗаполнено(КнопкиДиалога) Тогда
		Если ЗначениеЗаполнено(КнопкиДиалога[0].Значение) Тогда
			Закрыть(КнопкиДиалога[0].Значение);
			
		Иначе
			Закрыть(КодВозвратаДиалога.Повторить);
		КонецЕсли;
		
	Иначе
		Закрыть(КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УправлениеЭУ()
	
	Если Элементы.ГруппаПереключателя.Видимость Тогда
		Элементы.Продолжить.Доступность = (ЗначениеВыбора = 1);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

