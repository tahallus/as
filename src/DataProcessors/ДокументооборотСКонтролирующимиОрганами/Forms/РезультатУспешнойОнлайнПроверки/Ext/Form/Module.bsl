&НаКлиенте
Перем КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отчет = Параметры.Отчет;
	ИмяФайлаВыгрузки = Параметры.ИмяФайлаВыгрузки;
	ТекстВыгрузки = Параметры.ТекстВыгрузки;
	Протокол = Параметры.Протокол;
	ИмяФайлаПротокола = Параметры.ИмяФайлаПротокола;
	ПрограммаПроверки = Параметры.ПрограммаПроверки;
	
	Если ТипЗнч(Отчет) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		Заголовок = Заголовок + ": " + РегламентированнаяОтчетностьКлиентСервер.ПредставлениеДокументаРеглОтч(Отчет);
	Иначе
		Заголовок = Заголовок + ": " + Строка(Отчет);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Протокол) Тогда
		КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
		
		Если ЭтоАдресВременногоХранилища(Протокол) Тогда
			Содержимое = ПолучитьИзВременногоХранилища(Протокол);
			ТипСодержимого = ТипЗнч(Содержимое);
			Если ТипСодержимого = Тип("ДвоичныеДанные") Тогда
				ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
				Содержимое.Записать(ИмяВременногоФайла);
				Текст = КонтекстЭДОСервер.НовыйЧтениеТекстаНаСервере(ИмяВременногоФайла);
				ТекстСодержимого = Текст.Прочитать();
				ТекстПротокола = ТекстСодержимого;
			ИначеЕсли ТипСодержимого = Тип("ХранилищеЗначения") Тогда
				ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
				Содержимое.Получить().Записать(ИмяВременногоФайла);
				Текст = КонтекстЭДОСервер.НовыйЧтениеТекстаНаСервере(ИмяВременногоФайла);
				ТекстСодержимого = Текст.Прочитать();
				ТекстПротокола = ТекстСодержимого;
			ИначеЕсли ТипСодержимого = Тип("Строка") Тогда
				// В качестве значения реквизита формы может выступать навигационная ссылка или текст HTML-документа
				ТекстПротокола = Содержимое;
			КонецЕсли;
		КонецЕсли;
		
		// Поиск информации о предупреждениях.
		Если СтрНайти(ТекстПротокола, "предупрежден") > 0 Тогда
			КлючСохраненияПоложенияОкна = "Предупреждения";
			Ширина = 50;
			Элементы.ПротоколПроверки.Заголовок = НСтр("ru = 'Протокол проверки (есть предупреждения)'");
			Элементы.ПротоколПроверки.ЦветТекста = Новый Цвет(255, 128, 0);
		КонецЕсли;
		
	Иначе
		Элементы.ПротоколПроверки.Видимость = Ложь;
		
		Если ЗначениеЗаполнено(ПрограммаПроверки) Тогда
			Элементы.ПрограммаПроверки.Заголовок = НСтр("ru = 'Проверено'") + ": " + ПрограммаПроверки;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПротоколПроверкиНажатие(Элемент)
	
	КонтекстЭДОКлиент.ПоказатьПротоколОнлайнПроверки(Отчет, ИмяФайлаВыгрузки, ТекстВыгрузки, Протокол, ИмяФайлаПротокола, 0, ПрограммаПроверки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
КонецПроцедуры

#КонецОбласти