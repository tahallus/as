
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры,
		"ДисконтнаяКарта, ИнформацияКонтрагентТелефон, ИнформацияКонтрагентЭП, "
		+ "ИнформацияПроцентСкидкиПоДисконтнойКарте, ИнформацияСуммаПродажПоДисконтнойКарте, "
		+ "ИнформацияУпрВалюта, ЭлементыСписокТекущиеДанныеВладелецКарты");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик нажатия на кнопку ОтправитьEmailКонтрагенту.
//
&НаКлиенте
Процедура ОтправитьEmailКонтрагенту(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Получатели = Новый Массив;
	Если ЗначениеЗаполнено(ИнформацияКонтрагентЭП) Тогда
		СтруктураПолучателя = Новый Структура;
		СтруктураПолучателя.Вставить("Представление", ЭлементыСписокТекущиеДанныеВладелецКарты);
		СтруктураПолучателя.Вставить("Адрес", ИнформацияКонтрагентЭП);
		Получатели.Добавить(СтруктураПолучателя);
	КонецЕсли;
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Получатель", Получатели);
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыОтправки);
	
КонецПроцедуры // ОтправитьEmailКонтрагенту()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПродажиПоДисконтнойКарте(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючВарианта", "ПродажиПоДисконтнойКарте");
	ПараметрыФормы.Вставить("Отбор", Новый Структура("СтПериод,ДисконтнаяКарта", Новый СтандартныйПериод, ДисконтнаяКарта));
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.ПродажиПоДисконтнымКартам.Форма", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти