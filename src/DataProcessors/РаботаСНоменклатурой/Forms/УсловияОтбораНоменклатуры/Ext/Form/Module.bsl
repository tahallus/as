
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// инициализация компоновщика настроек
	ТекстЗапроса = РаботаСНоменклатуройСлужебный.ТекстЗапросаОтборНоменклатуры();
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	ВыражениеNULL = Новый ВыражениеСхемыЗапроса("NULL");
	КоличествоПолей = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля.Количество();
	Для ОбратныйИндекс = 1 По КоличествоПолей Цикл
		ПроверяемоеПоле = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля[КоличествоПолей - ОбратныйИндекс];
		Если ПроверяемоеПоле = ВыражениеNULL Тогда
			СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля.Удалить(КоличествоПолей - ОбратныйИндекс);
		КонецЕсли;
	КонецЦикла;
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	СКД = РаботаСНоменклатуройСлужебный.СхемаКомпоновкиДанных(ТекстЗапроса);
	АдресСхемыКомпановки = ПоместитьВоВременноеХранилище(СКД, УникальныйИдентификатор);
	КомпоновщикНастроекВыгрузки.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпановки));
	
	Если Параметры.Свойство("НастройкиОтбора") И ТипЗнч(Параметры.НастройкиОтбора) = Тип("НастройкиКомпоновкиДанных") Тогда
		КомпоновщикНастроекВыгрузки.ЗагрузитьНастройки(Параметры.НастройкиОтбора);
	Иначе 
		КомпоновщикНастроекВыгрузки.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	Закрыть(КомпоновщикНастроекВыгрузки.Настройки);
КонецПроцедуры

#КонецОбласти