
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкиВыгрузки = Новый Структура("ИспользоватьВыгрузкуВКаталог1С, ИспользоватьВыгрузкуВНациональныйКаталог");
	ЗаполнитьЗначенияСвойств(НастройкиВыгрузки, РаботаСНоменклатурой.НастройкиПодсистемы());
	Если НЕ РаботаСНоменклатурой.ВыгрузкаНоменклатурыИспользуется(НастройкиВыгрузки) 
		ИЛИ НЕ РаботаСНоменклатурой.ДоступнаФункциональностьПодсистемы() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЦветОсновной       = ЦветаСтиля.ГиперссылкаЦвет;
	ЦветОшибки         = ЦветаСтиля.ПоясняющийОшибкуТекст;
	ЛимитЗаписей       = РаботаСНоменклатуройСлужебныйКлиентСервер.РазмерПорции();
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	
	// размещение гиперссылок в заголовках элементов
	НадписиНоменклатура = Новый Массив;
	НадписиНоменклатура.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = '1С:Номенклатура'"), Новый Шрифт(,, Истина)));
	НадписиНоменклатура.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ' - Единый каталог описаний товаров в 1С:Предприятии 8'"),, ЦветаСтиля.ПоясняющийТекст));
	НадписиНоменклатура.Добавить(Символы.ПС);
	НадписиНоменклатура.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'подробнее'"),,,, РаботаСНоменклатурой.ГиперссылкаНаПромоСайтНоменклатура()));
	Элементы.НадписьНоменклатура.Заголовок = Новый ФорматированнаяСтрока(НадписиНоменклатура);
	
	НадписиНациональныйКаталог = Новый Массив;
	НадписиНациональныйКаталог.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Национальный каталог'"), Новый Шрифт(,, Истина)));
	НадписиНациональныйКаталог.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ' - Важная составляющая национальной системы цифровой маркировки Честный ЗНАК'"),, ЦветаСтиля.ПоясняющийТекст));
	НадписиНациональныйКаталог.Добавить(Символы.ПС);
	НадписиНациональныйКаталог.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'подробнее'"),,,, РаботаСНоменклатурой.ГиперссылкаНаСайтНациональногоКаталога()));
	Элементы.НадписьНациональныйКаталог.Заголовок = Новый ФорматированнаяСтрока(НадписиНациональныйКаталог);
	
	Оферта = Новый Массив;
	Оферта.Добавить(НСтр("ru = 'Нажатие ""Выгрузить номенклатуру"" означает согласие с '"));
	Оферта.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Условиями использования сервиса 1С:Номенклатура'"),,,, РаботаСНоменклатурой.ГиперссылкаНаОфертуСервисов()));
	Элементы.ГруппаВыгрузитьНоменклатуру.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(Оферта);
	
	Если Не РазделениеВключено Тогда
		// параметры регламентного задания
		УстановитьПривилегированныйРежим(Истина);
		СписокЗаданий = РегламентныеЗаданияСервер.НайтиЗадания(Новый Структура("Метаданные", Метаданные.РегламентныеЗадания.ВыгрузкаНоменклатурыРаботаСНоменклатурой));
		Если НЕ СписокЗаданий.Количество() = 0 Тогда
			Расписание        = СписокЗаданий[0].Расписание;
			РасписаниеАктивно = СписокЗаданий[0].Использование;
		Иначе 
			Расписание                   = Новый РасписаниеРегламентногоЗадания;
			Расписание.ДеньВМесяце       = 1;
			Расписание.ПериодПовтораДней = 1;
			РасписаниеАктивно            = Истина;
			СоздатьЗадание               = Истина;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		Элементы.НастроитьРасписание.Заголовок = ПредставлениеРасписания(Расписание);
		Элементы.НастроитьРасписание.Доступность = РасписаниеАктивно;
	КонецЕсли;
	
	Параметры.Свойство("Организация", Организация);
	Если Организация.Пустая() Тогда
		Организация = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("РаботаСНоменклатурой", "ПомощникВыгрузки\Организация");
	КонецЕсли;
	
	Обработки.РаботаСНоменклатурой.СоздатьКомандыСостояния(ЭтаФорма, Элементы.ГруппаРезультатыНациональныйКаталог.Имя, ВидКнопкиФормы.Гиперссылка);
	
	ВыбраноКВыгрузке = НоменклатураКВыгрузке(Организация, ЛимитЗаписей);
	
	Элементы.ГруппаРасписание.Видимость = НЕ РазделениеВключено И (ВыбраноКВыгрузке > 0);
	
	Если НастройкиВыгрузки.ИспользоватьВыгрузкуВНациональныйКаталог Тогда
		НациональныйКаталогВидимость = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("РаботаСНоменклатурой", "ПомощникВыгрузки\НациональныйКаталогВидимость");
		НациональныйКаталогВидимость = (НациональныйКаталогВидимость = Истина);
		Элементы.ГруппаНациональныйКаталог.Видимость = НациональныйКаталогВидимость;
		
		Если НациональныйКаталогВидимость = Ложь Тогда
			ПроверкаАктивностиПартнера = ЗапуститьПроверкуАктивностиПартнера(УникальныйИдентификатор, "crpt");
		КонецЕсли;
	Иначе 
		Элементы.ГруппаНациональныйКаталог.Видимость = Ложь;
	КонецЕсли;
	Элементы.ГруппаНоменклатура.Видимость = (НастройкиВыгрузки.ИспользоватьВыгрузкуВКаталог1С = Истина);
	
	ПодсчетСтатистикиРезультатов   = РаботаСНоменклатуройСлужебный.ПолучитьСтатистикуРезультатов(УникальныйИдентификатор, Организация);
	ЕстьПроблемыДлительнаяОперация = ДлительнаяОперацияЕстьПроблемыБыстро(УникальныйИдентификатор, Организация);
	КлючиВыгрузки                  = КлючиВыгрузки();
	ОбновитьКлючи                  = Ложь;
	НастройкиВыгрузкиНоменклатуры  = Константы.НастройкиВыгрузкиНоменклатуры.Получить().Получить();
	Если ТипЗнч(НастройкиВыгрузкиНоменклатуры) = Тип("Соответствие") Тогда
		ТипДанныхОрганизация  = Метаданные.ОпределяемыеТипы.Организация.Тип;
		ИмяТаблицыОрганизация = РаботаСНоменклатурой.ИмяТаблицыПоТипу(ТипДанныхОрганизация);
		ТекстЗапроса          = "ВЫБРАТЬ
		|	ДанныеПоОрганизациям.Организация КАК Организация,
		|	ДанныеПоОрганизациям.ДатаПредставление КАК ДатаПредставление,
		|	ДанныеПоОрганизациям.ДатаВыгрузки КАК ДатаВыгрузки,
		|	ДанныеПоОрганизациям.ВыгруженоПредставление КАК ВыгруженоПредставление,
		|	ДанныеПоОрганизациям.ИдентификаторЗадания КАК ИдентификаторЗадания
		|ПОМЕСТИТЬ втДанныеПоОрганизациям
		|ИЗ
		|	&ДанныеПоОрганизациям КАК ДанныеПоОрганизациям
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Организация,
		|	ПРЕДСТАВЛЕНИЕ(Организации.Ссылка) КАК ОрганизацияПредставление,
		|	втДанныеПоОрганизациям.ДатаПредставление КАК ДатаПредставление,
		|	втДанныеПоОрганизациям.ДатаВыгрузки КАК ДатаВыгрузки,
		|	втДанныеПоОрганизациям.ВыгруженоПредставление КАК ВыгруженоПредставление,
		|	втДанныеПоОрганизациям.ИдентификаторЗадания КАК ИдентификаторЗадания
		|ИЗ
		|	Справочник.Организации КАК Организации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеПоОрганизациям КАК втДанныеПоОрганизациям
		|		ПО Организации.Ссылка = втДанныеПоОрганизациям.Организация";
		ТекстЗапроса         = СтрЗаменить(ТекстЗапроса, "Справочник.Организации", ИмяТаблицыОрганизация);
		Запрос               = Новый Запрос(ТекстЗапроса);
		ДанныеПоОрганизациям = Новый ТаблицаЗначений;
		ДанныеПоОрганизациям.Колонки.Добавить("Организация",            ТипДанныхОрганизация);
		ДанныеПоОрганизациям.Колонки.Добавить("ДатаПредставление",      ОбщегоНазначения.ОписаниеТипаСтрока(200));
		ДанныеПоОрганизациям.Колонки.Добавить("ДатаВыгрузки",           ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
		ДанныеПоОрганизациям.Колонки.Добавить("ВыгруженоПредставление", ОбщегоНазначения.ОписаниеТипаСтрока(200));
		ДанныеПоОрганизациям.Колонки.Добавить("ИдентификаторЗадания",   ОбщегоНазначения.ОписаниеТипаСтрока(50));
		Для каждого ПараметрыНастройки Из НастройкиВыгрузкиНоменклатуры Цикл
			ЗаполнитьЗначенияСвойств(ДанныеПоОрганизациям.Добавить(), ПараметрыНастройки.Значение);
			Если ПараметрыНастройки.Ключ = Организация Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыНастройки.Значение,, "Организация");
			КонецЕсли;
		КонецЦикла;
		Запрос.УстановитьПараметр("ДанныеПоОрганизациям", ДанныеПоОрганизациям);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекущаяОрганизация       = Выборка.Организация;
			ВыводитьНадпись          = ЗначениеЗаполнено(Выборка.ДатаПредставление) ИЛИ ЗначениеЗаполнено(Выборка.ДатаВыгрузки);
			ПредставлениеДаты        = ПредставлениеДатыВыгрузки(Выборка.ДатаВыгрузки, Выборка.ДатаПредставление);
			ПредставлениеОрганизации = СтрШаблон("%1 (%2)", ТекущаяОрганизация, ПредставлениеДаты);
			Элементы.Организация.СписокВыбора.Добавить(ТекущаяОрганизация, ПредставлениеОрганизации, ВыводитьНадпись);
			Если ЗначениеЗаполнено(Выборка.ИдентификаторЗадания) 
				И Выборка.ИдентификаторЗадания <> "РегламентноеЗадание"
				И (КлючиВыгрузки.Найти(ТекущаяОрганизация) = Неопределено) Тогда
				КлючиВыгрузки.Добавить(ТекущаяОрганизация);
				ОбновитьКлючи = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если ОбновитьКлючи = Истина Тогда
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("РаботаСНоменклатурой", "ПомощникВыгрузки\КлючиВыгрузки", КлючиВыгрузки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВыгружатьВНациональныйКаталог = Ложь И ТипЗнч(ПроверкаАктивностиПартнера) = Тип("Структура") Тогда
		Если ПроверкаАктивностиПартнера.Статус = "Выполнено" Тогда
			ПроверкаАктивностиПартнераЗавершение(ПроверкаАктивностиПартнера, Неопределено);
		Иначе 
			ОповещениеОЗавершении = Новый ОписаниеОповещения("ПроверкаАктивностиПартнераЗавершение", ЭтотОбъект);
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
			ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
			ПараметрыОжидания.ВыводитьПрогрессВыполнения = Ложь;
			ПараметрыОжидания.ВыводитьСообщения = Ложь;
			ДлительныеОперацииКлиент.ОжидатьЗавершение(ПроверкаАктивностиПартнера, ОповещениеОЗавершении, ПараметрыОжидания);
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьПараметрыФормыПриИзмененииОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ВыгрузкаНоменклатурыЗавершена" И Организация <> Параметр Тогда
		Организация = Параметр;
		ОрганизацияПриИзменении(Неопределено);
	ИначеЕсли Источник = "ИспользоватьСервисРаботаСНоменклатурой" Тогда
		// Событие может возникнуть только в случае, если при открытой форме сбросили значение ФО в Ложь
		// В этом случае форму нужно закрыть, поскольку функциональность подсистемы отключена
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	РаботаСНоменклатуройСлужебныйКлиент.ЗапуститьПроверкуВыгрузкиНоменклатуры();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВыгружатьНоменклатуруПриИзменении(Элемент)
	Если ВыгружатьВНациональныйКаталог Тогда 
		ОбновляемыеПараметры = Новый Структура(Элемент.Имя, ЭтотОбъект[Элемент.Имя]);
		РаботаСНоменклатуройСлужебныйВызовСервера.ОбновитьПараметрыНастройкиВыгрузки(Организация, ОбновляемыеПараметры);
	Иначе 
		ОбновляемыеПараметры = ОбновляемыеПараметрыПриИзмененииФлагаКаталога(Элемент.Имя);
		ОбновитьПараметрНастройкиНаКлиенте(ОбновляемыеПараметры);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КонтактнаяИнформацияПриИзменении()
	
	КонтактнаяИнформация = КонтактнаяИнформация();
	Если ПараметрыЗаполнены(КонтактнаяИнформация) Тогда
		ВводКонтактнойИнформации = СтрШаблон("%1, %2, %3", КонтактнаяИнформация.КонтактноеЛицо, КонтактнаяИнформация.Должность, КонтактнаяИнформация.Email);
		Элементы.ВводКонтактнойИнформации.ЦветТекста = ЦветОсновной;
	Иначе 
		ВводКонтактнойИнформации = НСтр("ru = 'Контактная информация (не заполнена)'");
		Элементы.ВводКонтактнойИнформации.ЦветТекста = ЦветОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлементВыгружатьВНациональныйКаталогПриИзменении(Элемент)
	
	ОбновляемыеПараметры = ОбновляемыеПараметрыПриИзмененииФлагаКаталога(Элемент.Имя);
	Если ВыгружатьВНациональныйКаталог Тогда
		ОбновитьПараметрНастройкиНаКлиенте(ОбновляемыеПараметры);
	Иначе
		СостояниеВыгрузки = СостояниеВыгрузкиВНациональныйКаталог(Организация);
		Если СостояниеВыгрузки.НаМодерации = Истина Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Нельзя отключить обмен с ""Национальным каталогом"" пока есть данные на модерации.'"));
			ВыгружатьВНациональныйКаталог = Истина;
			Возврат;
		ИначеЕсли СостояниеВыгрузки.Отклонена = Истина Тогда
			Оповещение = Новый ОписаниеОповещения("ОбработатьОтветПоВыгрузкеВНациональныйКаталог", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'Есть товарные позиции в состоянии ""Отклонена"".
			|Если отключить обмен с ""Национальным каталогом"", то эти позиции не будут приняты в каталог.
			|Отключить обмен?'"), РежимДиалогаВопрос.ДаНет);
			Возврат;
		Иначе
			ОбновитьПараметрНастройкиНаКлиенте(ОбновляемыеПараметры);
		КонецЕсли;
	КонецЕсли;
	ВыгружатьВНациональныйКаталогПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьВНациональныйКаталогПриИзменении()
	
	Если Элементы.Шаг5Результаты.Видимость И НЕ ВыгружатьВНациональныйКаталог Тогда
		Элементы.Шаг5Результаты.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.НадписьПараметрыДоступаНациональныйКаталог.Доступность = ВыгружатьВНациональныйКаталог;
	Если ВыгружатьВНациональныйКаталог = Истина Тогда
		ПараметрыНациональныйКаталогПриИзменении();
	Иначе
		Элементы.НадписьПараметрыДоступаНациональныйКаталог.ЦветТекста = Элементы.ГруппаНациональныйКаталогДетали.ЦветТекстаЗаголовка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыНациональныйКаталогПриИзменении()
	НадписьПараметрыДоступаНациональныйКаталог = НСтр("ru = 'Параметры доступа'");
	Если НЕ ПараметрыДоступаНациональныйКаталогЗаполнены() Тогда
		НадписьПараметрыДоступаНациональныйКаталог = НадписьПараметрыДоступаНациональныйКаталог + НСтр("ru = ' (не заполнены)'");
		Элементы.НадписьПараметрыДоступаНациональныйКаталог.ЦветТекста = ЦветОшибки;
	ИначеЕсли ТестовыйРежим Тогда
		НадписьПараметрыДоступаНациональныйКаталог = НадписьПараметрыДоступаНациональныйКаталог + НСтр("ru = ' (тестовый режим)'");
		Элементы.НадписьПараметрыДоступаНациональныйКаталог.ЦветТекста = ЦветОшибки;
	Иначе 
		Элементы.НадписьПараметрыДоступаНациональныйКаталог.ЦветТекста = ЦветОсновной;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеАктивноПриИзменении(Элемент)
	Элементы.НастроитьРасписание.Доступность = РасписаниеАктивно;
	УстановитьПараметрыРегламентногоЗаданияВыгрузки(Новый Структура("Использование", РасписаниеАктивно));
КонецПроцедуры

&НаКлиенте
Процедура ТипОрганизацииПриИзменении(Элемент)
	Элементы.ТипОрганизацииДругое.Видимость = (ТипОрганизации = "other");
	ОбновляемыеПараметры = Новый Структура(Элемент.Имя, ЭтотОбъект[Элемент.Имя]);
	РаботаСНоменклатуройСлужебныйВызовСервера.ОбновитьПараметрыНастройкиВыгрузки(Организация, ОбновляемыеПараметры);
КонецПроцедуры

&НаКлиенте
Процедура ТипОрганизацииДругоеПриИзменении(Элемент)
	ОбновляемыеПараметры = Новый Структура(Элемент.Имя, ЭтотОбъект[Элемент.Имя]);
	РаботаСНоменклатуройСлужебныйВызовСервера.ОбновитьПараметрыНастройкиВыгрузки(Организация, ОбновляемыеПараметры);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыФормы(Организация, ЛимитЗаписей, УникальныйИдентификатор),, "Организация");
	ОбновитьПараметрыФормыПриИзмененииОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВводКонтактнойИнформацииНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Оповещение           = Новый ОписаниеОповещения("ВводКонтактнойИнформацииЗавершение", ЭтотОбъект);
	ПараметрыОткрытия    = Новый Структура("КонтактнаяИнформация", КонтактнаяИнформация());
	ОткрытьФорму("Обработка.РаботаСНоменклатурой.Форма.ВводКонтактнойИнформации", ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор,,, Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ВводКонтактнойИнформацииЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт 
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") 
		И НЕ РаботаСНоменклатуройСлужебныйКлиентСервер.ДанныеСовпадают(РезультатЗакрытия, КонтактнаяИнформация()) Тогда
		РаботаСНоменклатуройСлужебныйВызовСервера.ОбновитьПараметрыНастройкиВыгрузки(Организация, РезультатЗакрытия);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатЗакрытия);
		КонтактнаяИнформацияПриИзменении();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыДоступаНациональныйКаталогНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Оповещение           = Новый ОписаниеОповещения("ВводПараметровДоступаНациональныйКаталогЗавершение", ЭтотОбъект);
	ПараметрыОткрытия    = Новый Структура("ПараметрыДоступаНациональныйКаталог, Организация", ПараметрыДоступаНациональныйКаталог(), Организация);
	ОткрытьФорму("Обработка.РаботаСНоменклатурой.Форма.ВводПараметровДоступаНациональныйКаталог", ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор,,, Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ВводПараметровДоступаНациональныйКаталогЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт 
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура")
		И НЕ РаботаСНоменклатуройСлужебныйКлиентСервер.ДанныеСовпадают(РезультатЗакрытия, ПараметрыДоступаНациональныйКаталог()) Тогда
		ТестовыйРежимДо = ТестовыйРежим;
		ОбновитьПараметрНастройкиНаКлиенте(РезультатЗакрытия);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатЗакрытия);
		ПараметрыНациональныйКаталогПриИзменении();
		Если ТестовыйРежимДо И НЕ ТестовыйРежим И ТипЗнч(СтатистикаРезультатов) = Тип("Структура") Тогда
			Для каждого ДанныеСостояния Из СтатистикаРезультатов Цикл
				СтатистикаРезультатов.Вставить(ДанныеСостояния.Ключ, 0);
			КонецЦикла;
			РаботаСНоменклатуройСлужебныйКлиент.ОбновитьЗаголовкиКнопокСостояние(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыгрузитьНоменклатуру(Команда)
	
	ОчиститьСообщения();
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выгрузка уже запущена, дождитесь завершения'"));
		Возврат;
	КонецЕсли;
	
	ЕстьОшибки = Ложь;
	
	Если ОрганизацияЗаполнена(Ложь) = Ложь Тогда
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	КонтактнаяИнформация = КонтактнаяИнформация();
	Если НЕ ПараметрыЗаполнены(КонтактнаяИнформация) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнена контактная информация'"),, "ВводКонтактнойИнформации",, ЕстьОшибки);
	КонецЕсли;
	
	ТекстСообщения = "";
	Если ПустаяСтрока(Email) Тогда
		ТекстСообщения = НСтр("ru = 'Введите адрес электронной почты'");
	ИначеЕсли Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(Email) Тогда
		ТекстСообщения = НСтр("ru = 'Адрес электронной почты введен неверно'");
	КонецЕсли;
	Если НЕ ТекстСообщения = "" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "ВводКонтактнойИнформации",, ЕстьОшибки);
	КонецЕсли;
	
	Если НЕ (ВыгружатьНоменклатуру ИЛИ ВыгружатьВНациональныйКаталог) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбран ни один каталог для выгрузки'"),, "ВыгружатьНоменклатуру",, ЕстьОшибки);
	КонецЕсли;
	
	Если ВыгружатьВНациональныйКаталог И НЕ ПараметрыДоступаНациональныйКаталогЗаполнены() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнены параметры доступа в Национальный каталог'"),, "ПараметрыДоступаНациональныйКаталог",, ЕстьОшибки);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТипОрганизации) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указан тип организации'"),, "ТипОрганизации",, ЕстьОшибки);
	КонецЕсли;
	
	Если ЕстьОшибки = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕстьПроблемы Тогда
		Оповещение   = Новый ОписаниеОповещения("ОтветНаВопросВыгрузкаНоменклатуры", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Выберите действие'");
		Кнопки       = Новый СписокЗначений;
		Кнопки.Добавить("ПодготовитьНоменклатуру", НСтр("ru = 'Подготовить номенклатуру'"));
		Кнопки.Добавить("ПродолжитьВыгрузку", НСтр("ru = 'Продолжить выгрузку'"));
		Кнопки.Добавить("Отмена", НСтр("ru = 'Отмена'"));
		ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		ПараметрыВопроса.КнопкаПоУмолчанию = "ПодготовитьНоменклатуру";
		ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
		ПараметрыВопроса.Заголовок = НСтр("ru = 'Не вся номенклатура готова к выгрузке'");
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Оповещение, ТекстВопроса, Кнопки, ПараметрыВопроса);
		Возврат;
	КонецЕсли;
	
	ВыгрузкаЗапущена = ЗапуститьВыгрузкуНоменклатурыВФоне(РазделениеВключено, Организация, Истина);
	Если ТипЗнч(ВыгрузкаЗапущена) = Тип("Структура") Тогда
		Если ВыгрузкаЗапущена.Свойство("ТекстСообщения") И ЗначениеЗаполнено(ВыгрузкаЗапущена.ТекстСообщения) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ВыгрузкаЗапущена.ТекстСообщения);
		КонецЕсли;
		Если ВыгрузкаЗапущена.Свойство("ИдентификаторЗадания") И ЗначениеЗаполнено(ВыгрузкаЗапущена.ИдентификаторЗадания) Тогда
			ИдентификаторЗадания = ВыгрузкаЗапущена.ИдентификаторЗадания;
			ПослеЗапускаВыгрузкиНоменклатурыВФоне();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыгружаемыеРеквизиты(Команда)
	Если ОрганизацияЗаполнена() = Ложь Тогда
		Возврат;
	КонецЕсли;
	ПараметрыОткрытия  = Новый Структура("Организация", Организация);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗакрытииФормыВыгружаемыехРеквизитов", ЭтотОбъект);
	ОценкаПроизводительностиКлиент.ЗамерВремени("Обработка.РаботаСНоменклатурой.Форма.ВыгружаемыеРеквизиты.ОткрытиеФормы");
	ОткрытьФорму("Обработка.РаботаСНоменклатурой.Форма.ВыгружаемыеРеквизиты", ПараметрыОткрытия, ЭтаФорма, УникальныйИдентификатор,,, ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытииФормыВыгружаемыехРеквизитов(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		ДоИзменения = Новый Структура("ВыгружаемыеРеквизиты", ВыгружаемыеРеквизиты);
		Если НЕ РаботаСНоменклатуройСлужебныйКлиентСервер.ДанныеСовпадают(РезультатЗакрытия, ДоИзменения) Тогда
			ВыгружаемыеРеквизиты = РезультатЗакрытия.ВыгружаемыеРеквизиты;
			УстановитьЗаголовокВыгружаемыеРеквизиты();
			ОбновитьПараметрНастройкиНаКлиенте(РезультатЗакрытия);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьНоменклатуру(Команда)
	ОткрытьФормыСопоставление();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДействиеКомандыСостояние(Команда)
	
	Если ОрганизацияЗаполнена() = Ложь Тогда
		Возврат;
	КонецЕсли;
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Организация", Организация);
	ПараметрыОткрытия.Вставить("ИмяКоманды", СтрЗаменить(Команда.Имя, "ВсеРезультаты", ""));
	ПараметрыОткрытия.Вставить("СтатистикаРезультатов", СтатистикаРезультатов);
	ОписаниеОповещения = Новый ОписаниеОповещения("ИсторияВыгрузкиЗакрытие", ЭтотОбъект, Новый Структура("СтатистикаРезультатов", СтатистикаРезультатов));
	ОценкаПроизводительностиКлиент.ЗамерВремени("Обработка.РаботаСНоменклатурой.Форма.РезультатыВыгрузкиНоменклатуры.ОткрытиеФормы");
	ОткрытьФорму("Обработка.РаботаСНоменклатурой.Форма.РезультатыВыгрузкиНоменклатуры", ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияВыгрузкиЗакрытие(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	СтатистикаРезультатов = ДополнительныеПараметры.СтатистикаРезультатов;
	РаботаСНоменклатуройСлужебныйКлиент.ОбновитьЗаголовкиКнопокСостояние(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРасписание(Команда)
	
	Если Расписание = Неопределено Тогда
		РедактируемоеРасписание = Новый РасписаниеРегламентногоЗадания;
	Иначе
		РедактируемоеРасписание = Расписание;
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РедактируемоеРасписание);
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ИзменитьРасписание", ЭтотОбъект);
	Диалог.Показать(ОписаниеОповещенияОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРасписание(НовоеРасписание, ДополнительныеПараметры) Экспорт
	
	Если НовоеРасписание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Расписание = НовоеРасписание;
	
	Элементы.НастроитьРасписание.Заголовок = ПредставлениеРасписания(Расписание);
	
	УстановитьПараметрыРегламентногоЗаданияВыгрузки(Новый Структура("Расписание", Расписание));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНоменклатуру(Команда)
	Если ОрганизацияЗаполнена() = Ложь Тогда
		Возврат;
	КонецЕсли;
	ОценкаПроизводительностиКлиент.ЗамерВремени("Обработка.РаботаСНоменклатурой.Форма.НоменклатураКВыгрузке.ОткрытиеФормы");
	ПараметрыФормы = Новый Структура("Организация", Организация);
	Оповещение     = Новый ОписаниеОповещения("НоменклатураКВыгрузкеЗакрытие", ЭтотОбъект);
	ОткрытьФорму("Обработка.РаботаСНоменклатурой.Форма.НоменклатураКВыгрузке", ПараметрыФормы, ЭтотОбъект,
		УникальныйИдентификатор,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураКВыгрузкеЗакрытие(РезультатЗакрытия, ДополнительныеПараметры) Экспорт 
	ОбновитьКоличествоКВыгрузке = Истина;
	Если ТипЗнч(РезультатЗакрытия) = Тип("Число") Тогда
		ВыбраноКВыгрузке = РезультатЗакрытия;
		ОбновитьЗаголовокЭлементаВыбратьНоменклатуру();
		ОбновитьКоличествоКВыгрузке = Ложь;
	КонецЕсли;
	ОбновитьСтатистикуИПроблемыБыстро(ОбновитьКоличествоКВыгрузке);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеРасписания(Знач Расписание)
	
	Возврат ?(Расписание = Неопределено, НСтр("ru = 'Не задано'"), Строка(Расписание));
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьПараметрыРегламентногоЗаданияВыгрузки(ПараметрыЗадания)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МетаданныеЗадания	= Метаданные.РегламентныеЗадания.ВыгрузкаНоменклатурыРаботаСНоменклатурой;
	СписокЗаданий		= РегламентныеЗаданияСервер.НайтиЗадания(Новый Структура("Метаданные", МетаданныеЗадания));
	
	Если СписокЗаданий.Количество() = 0 Тогда
		ПараметрыЗадания.Вставить("Метаданные", МетаданныеЗадания);
		РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
	Иначе
		РегламентныеЗаданияСервер.ИзменитьЗадание(СписокЗаданий[0].УникальныйИдентификатор, ПараметрыЗадания);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокВыгружаемыеРеквизиты()
	
	Если ТипЗнч(ВыгружаемыеРеквизиты) = Тип("Структура") И (
		ВыгружаемыеРеквизиты.Свойство("Реквизиты") И ВыгружаемыеРеквизиты.Реквизиты.Количество() ИЛИ
		ВыгружаемыеРеквизиты.Свойство("ДополнительныеРеквизиты") И ВыгружаемыеРеквизиты.ДополнительныеРеквизиты.Количество() ИЛИ 
		ВыгружаемыеРеквизиты.Свойство("ВыгружатьИндивидуальныеХарактеристики") И ВыгружаемыеРеквизиты.ВыгружатьИндивидуальныеХарактеристики <> ИСТИНА ИЛИ
		ВыгружаемыеРеквизиты.Свойство("ВидыНоменклатуры") И ВыгружаемыеРеквизиты.ВидыНоменклатуры.Количество())
		Тогда 
		ТекстЗаголовка = НСтр("ru = 'выбранные'");
	Иначе 
		ТекстЗаголовка = НСтр("ru = 'все'");
	КонецЕсли;
	
	ТекстЗаголовка = СтрШаблон(НСтр("ru = 'Выбрать выгружаемые реквизиты (%1)'"), ТекстЗаголовка);
	Элементы.ВыбратьВыгружаемыеРеквизиты.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

&НаКлиенте
Функция КонтактнаяИнформация()

	КонтактнаяИнформация = Новый Структура;
	КонтактнаяИнформация.Вставить("КонтактноеЛицо");
	КонтактнаяИнформация.Вставить("Должность");
	КонтактнаяИнформация.Вставить("Email");
	ЗаполнитьЗначенияСвойств(КонтактнаяИнформация, ЭтотОбъект);
	
	Возврат КонтактнаяИнформация;

КонецФункции

&НаКлиенте
Функция ПараметрыДоступаНациональныйКаталог()

	ПараметрыДоступаНациональныйКаталог = Новый Структура;
	ПараметрыДоступаНациональныйКаталог.Вставить("apikey");
	ПараметрыДоступаНациональныйКаталог.Вставить("party_id");
	ПараметрыДоступаНациональныйКаталог.Вставить("ПолучитьШтрихкоды");
	ПараметрыДоступаНациональныйКаталог.Вставить("ТестовыйРежим");
	ЗаполнитьЗначенияСвойств(ПараметрыДоступаНациональныйКаталог, ЭтотОбъект);
	
	Возврат ПараметрыДоступаНациональныйКаталог;

КонецФункции

&НаКлиенте
Функция ПараметрыЗаполнены(Параметры)
	
	ДанныеЗаполнены = Истина;
	Для каждого Параметр Из Параметры Цикл
		Если НЕ ЗначениеЗаполнено(Параметр.Значение) Тогда
			ДанныеЗаполнены = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДанныеЗаполнены;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормыСопоставление(ТолькоНеподготовленные = Ложь)
	
	Если ОрганизацияЗаполнена() = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия  = РаботаСНоменклатуройКлиент.ПараметрыФормыСопоставленияНоменклатурыСРубрикатором();
	ПараметрыОткрытия.Заголовок             = НСтр("ru = 'Подготовка номенклатуры к выгрузке'");
	ПараметрыОткрытия.ЗаголовокКатегории    = НСтр("ru = 'Категория 1С:Номенклатура'");
	ПараметрыОткрытия.ЗаголовокРеквизита    = НСтр("ru = 'Реквизит 1С:Номенклатура'");
	ПараметрыОткрытия.ЗаголовокТипа         = НСтр("ru = 'Тип 1С:Номенклатура'");
	ПараметрыОткрытия.ИнформацияСписок      = НСтр("ru = 'Необходимо сопоставить категории рубрикатора 1С:Номенклатура.'");
	ПараметрыОткрытия.ИнформацияРеквизиты   = НСтр("ru = 'Необходимо сопоставить реквизиты номенклатуры с реквизитами сервиса 1С:Номенклатура.'");
	ПараметрыОткрытия.СценарийИспользования = "ВыгрузкаНоменклатуры";
	ПараметрыОткрытия.Вставить("Организация", Организация);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СопоставлениеНоменклатурыСРубрикаторомЗакрытие", ЭтотОбъект);
	
	РаботаСНоменклатуройКлиент.ОткрытьФормуСопоставленияНоменклатурыСРубрикатором(ПараметрыОткрытия, ЭтотОбъект, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеНоменклатурыСРубрикаторомЗакрытие(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ОбновитьСтатусПроблем = Истина;
	Если ТипЗнч(РезультатЗакрытия) = Тип("Булево") Тогда
		ЕстьПроблемы = РезультатЗакрытия;
		ОбновитьЗаголовокЭлементаСопоставитьНоменклатуру();
		ОбновитьСтатусПроблем = Ложь;
	КонецЕсли;
	
	ОбновитьСтатистикуИПроблемыБыстро(, ОбновитьСтатусПроблем);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеНоменклатурыГотовоСтатистикаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФормыСопоставление(Истина);
КонецПроцедуры

&НаКлиенте
Функция ОрганизацияЗаполнена(ОчищатьСообщения = Истина)
	Если Организация.Пустая() Тогда
		Если ОчищатьСообщения = Истина Тогда
			ОчиститьСообщения();
		КонецЕсли;
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите организацию'"),, "Организация");
		Возврат Ложь;
	Иначе 
		Возврат Истина;
	КонецЕсли;
КонецФункции

&НаСервереБезКонтекста
Функция ЗапуститьПроверкуАктивностиПартнера(Знач УникальныйИдентификатор, Знач КодПартренра)
	
	ИмяМетода           = "РаботаСНоменклатуройСлужебный.ПартнерАктивен";
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Работа с номенклатурой. Проверка активности партнера сервиса 1С:Номенклатура'");
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, КодПартренра);
	
КонецФункции

&НаКлиенте
Процедура ПроверкаАктивностиПартнераЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Если НЕ (ТипЗнч(Результат) = Тип("Структура") 
		И Результат.Свойство("Статус")
		И Результат.Статус = "Выполнено" 
		И Результат.Свойство("АдресРезультата")
		И ТипЗнч(Результат.АдресРезультата) = Тип("Строка") 
		И ЭтоАдресВременногоХранилища(Результат.АдресРезультата)) Тогда 
		Возврат;
	КонецЕсли;
	
	НациональныйКаталогВидимость = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	НациональныйКаталогВидимость = (НациональныйКаталогВидимость = Истина);
	Элементы.ГруппаНациональныйКаталог.Видимость = НациональныйКаталогВидимость;
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить("РаботаСНоменклатурой",
	"ПомощникВыгрузки\НациональныйКаталогВидимость", НациональныйКаталогВидимость);
	
	Если НациональныйКаталогВидимость = Ложь И ВыгружатьВНациональныйКаталог = Истина Тогда
		ВыгружатьВНациональныйКаталог = Ложь;
		ОбновляемыеПараметры = Новый Структура("ВыгружатьВНациональныйКаталог", Ложь);
		РаботаСНоменклатуройСлужебныйВызовСервера.ОбновитьПараметрыНастройкиВыгрузки(Организация, ОбновляемыеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НоменклатураКВыгрузке(Знач Организация, Знач ЛимитЗаписей)
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат 0;
	КонецЕсли;
	
	Запрос = Новый Запрос(РаботаСНоменклатуройСлужебный.ТекстЗапросаВыгружаемаяНоменклатура(, ЛимитЗаписей));
	Запрос.УстановитьПараметр("СписокСостояний", РаботаСНоменклатуройСлужебный.СостоянияВыгружаемаяНоменклатура());
	Запрос.УстановитьПараметр("Организация", Организация);
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Выборка.Количество();
	
КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыФормы(Знач Организация, Знач ЛимитЗаписей, Знач УникальныйИдентификатор)
	
	Настройка = РаботаСНоменклатуройСлужебный.НастройкаВыгрузкиНоменклатуры(Организация);
	Настройка.Вставить("ВыбраноКВыгрузке");
	Настройка.Вставить("ПодсчетСтатистикиРезультатов");
	Настройка.Вставить("ЕстьПроблемыДлительнаяОперация");
	
	ЗаполнитьПараметрыНаСервере(Настройка, УникальныйИдентификатор, Организация, ЛимитЗаписей);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("РаботаСНоменклатурой", "ПомощникВыгрузки\Организация", Организация);
	
	Возврат Настройка;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьЗаголовокЭлементаВыбратьНоменклатуру()

	Если ВыбраноКВыгрузке >= ЛимитЗаписей Тогда
		ВыбраноКВыгрузкеСтрокой = СтрШаблон(НСтр("ru = 'выбрано более %1'"), Формат(ЛимитЗаписей, "ЧГ="));
	ИначеЕсли ВыбраноКВыгрузке = 0 Тогда
		ВыбраноКВыгрузкеСтрокой = НСтр("ru = 'не выбрана'");
	Иначе 
		ВыбраноКВыгрузкеСтрокой = СтрШаблон(НСтр("ru = 'выбрано %1'"), Формат(ВыбраноКВыгрузке, "ЧГ="));
	КонецЕсли;
	Элементы.ВыбратьНоменклатуру.Заголовок = СтрШаблон(НСтр("ru = 'Выбрать номенклатуру для выгрузки (%1)'"), ВыбраноКВыгрузкеСтрокой);

КонецПроцедуры

&НаКлиенте
Процедура ОтветНаВопросВыгрузкаНоменклатуры(Результат, ДополнительныеПараметры) Экспорт 
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ОтветНаВопрос = Результат.Значение;
	ИначеЕсли ТипЗнч(Результат) = Тип("Строка") Тогда 
		ОтветНаВопрос = Результат;
	Иначе 
		Возврат;
	КонецЕсли;
	
	Если ОтветНаВопрос = "ПодготовитьНоменклатуру" Тогда
		ОткрытьФормыСопоставление();
	ИначеЕсли ОтветНаВопрос = "ПродолжитьВыгрузку" Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗапуститьВыгрузкуНоменклатурыВФоне(РазделениеВключено, Организация));
		ПослеЗапускаВыгрузкиНоменклатурыВФоне();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПараметрыФормыПриИзмененииОрганизации()
	
	ОбновитьИнформациюОВыгрузке();

	Элементы.ГруппаРасписание.Видимость = НЕ РазделениеВключено И (ВыбраноКВыгрузке > 0);
	
	ОбновитьЗаголовокЭлементаВыбратьНоменклатуру();
	
	РаботаСНоменклатуройСлужебныйКлиент.ОбновитьСтатистикуРезультатов(ЭтотОбъект);
	
	Элементы.ТипОрганизацииДругое.Видимость = (ТипОрганизации = "other");
	ВыгружатьВНациональныйКаталогПриИзменении();
	КонтактнаяИнформацияПриИзменении();
	ПараметрыНациональныйКаталогПриИзменении();
	УстановитьЗаголовокВыгружаемыеРеквизиты();
	ПроверкаПроблемБыстро();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапуститьВыгрузкуНоменклатурыВФоне(Знач РазделениеВключено, Знач Организация, Знач ПроверитьГотовность = Ложь)
	
	РезультатЗапуска = Новый Структура("ТекстСообщения, ИдентификаторЗадания", "", "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	Истина
	|ИЗ
	|	РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|ГДЕ
	|	СостоянияВыгрузкиНоменклатуры.Организация = &Организация
	|	И СостоянияВыгрузкиНоменклатуры.Состояние В (&Состояния)";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Состояния", РаботаСНоменклатуройСлужебный.СостоянияВыгружаемаяНоменклатура());
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		РезультатЗапуска.ТекстСообщения = НСтр("ru = 'Нет данных для выгрузки'");
		Возврат РезультатЗапуска;
	КонецЕсли;
	
	КлючЗадания = ?(РазделениеВключено, Строка(ПараметрыСеанса.ОбластьДанныхЗначение), "");
	
	УстановитьПривилегированныйРежим(Истина);
	
	// проверка запуска выгрузки по расписанию
	РегламентноеЗадание = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.ВыгрузкаНоменклатурыРаботаСНоменклатурой);
	Если РегламентноеЗадание <> Неопределено Тогда
		ОтборЗаданий        = Новый Структура("РегламентноеЗадание, Состояние", РегламентноеЗадание, СостояниеФоновогоЗадания.Активно);
		Если ЗначениеЗаполнено(КлючЗадания) Тогда
			ОтборЗаданий.Вставить("Ключ", КлючЗадания);
		КонецЕсли;
		Задания = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборЗаданий);
		Если Задания.Количество() Тогда
			// регламентное задание работает
			РезультатЗапуска.ИдентификаторЗадания = Строка(Задания[0].УникальныйИдентификатор);
			Возврат РезультатЗапуска;
		КонецЕсли;
	КонецЕсли;

	КлючЗадания     = "ВыгрузкаНоменклатуры" + КлючЗадания;
	КлючОрганизации = XMLСтрока(Организация);
	КлючЗадания     = КлючЗадания + КлючОрганизации;
	КлючиВыгрузки   = КлючиВыгрузки();
	Если КлючиВыгрузки.Найти(КлючОрганизации) = Неопределено Тогда
		КлючиВыгрузки.Добавить(Организация);
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("РаботаСНоменклатурой", "ПомощникВыгрузки\КлючиВыгрузки", КлючиВыгрузки);
	КонецЕсли;
	
	ИмяМетода = "РаботаСНоменклатуройСлужебный.ВыгрузитьДанныеНоменклатуры";
	Задания   = ФоновыеЗадания.ПолучитьФоновыеЗадания(Новый Структура("ИмяМетода, Ключ, Состояние", ИмяМетода, КлючЗадания, СостояниеФоновогоЗадания.Активно)); 
	
	Если Задания.Количество() Тогда
		// уже запущено
		РезультатЗапуска.ИдентификаторЗадания = Строка(Задания[0].УникальныйИдентификатор);
		Возврат РезультатЗапуска;
	КонецЕсли;
	
	НастройкаВыгрузки = РаботаСНоменклатуройСлужебный.НастройкаВыгрузкиНоменклатуры(Организация);
	НастройкаВыгрузки.Вставить("ПроверитьПередВыгрузкой", Истина);
	
	ФоновоеЗадание = ФоновыеЗадания.Выполнить(ИмяМетода, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НастройкаВыгрузки),
		КлючЗадания, НСтр("ru = 'Выгрузка номенклатуры'"));
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ИдентификаторЗадания = Строка(ФоновоеЗадание.УникальныйИдентификатор);
	РаботаСНоменклатуройСлужебный.ОбновитьПараметрыНастройкиВыгрузки(Организация, Новый Структура("ИдентификаторЗадания", ИдентификаторЗадания));
	РезультатЗапуска.ИдентификаторЗадания = ИдентификаторЗадания;
	Возврат РезультатЗапуска;
	
КонецФункции

&НаКлиенте
Процедура ПослеЗапускаВыгрузкиНоменклатурыВФоне()
	
	Элементы.ВыгрузкаДлительнаяОперация.Видимость = Истина;
	Элементы.ДатаПредставление.Видимость = Истина;
	ДатаПредставление      = НСтр("ru = 'выгружается'");
	ВыгруженоПредставление = НСтр("ru = 'Выгружается...'");
	ЭлементСписка = Элементы.Организация.СписокВыбора.НайтиПоЗначению(Организация);
	Если ЭлементСписка = Неопределено Тогда
		ЭлементСписка = Элементы.Организация.СписокВыбора.Добавить(Организация);
	КонецЕсли;
	ЭлементСписка.Представление = СтрШаблон("%1 (%2)", Организация, ДатаПредставление);
	ЭлементСписка.Пометка = Истина;
	
	ПодключитьОбработчикОжидания("ПроверитьВыгрузкуНоменклатурыВФорме", 1);
	РаботаСНоменклатуройСлужебныйКлиент.ЗапуститьПроверкуВыгрузкиНоменклатуры();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуНоменклатурыВФорме() Экспорт 
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ПрогрессВыгрузки = ПрогрессВыгрузки(Организация, ИдентификаторЗадания, РазделениеВключено, СоздатьЗадание, УникальныйИдентификатор);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПрогрессВыгрузки);
		Если НЕ ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
			Элементы.ВыгрузкаДлительнаяОперация.Видимость = Ложь;
			ОтключитьОбработчикОжидания("ПроверитьВыгрузкуНоменклатурыВФорме");
			Элементы.Шаг5Результаты.Видимость   = ВыгружатьВНациональныйКаталог И НЕ Организация.Пустая() И ВыбраноКВыгрузке > 0;
			Элементы.ГруппаРасписание.Видимость = НЕ РазделениеВключено И (ВыбраноКВыгрузке > 0);
			РаботаСНоменклатуройСлужебныйКлиент.ОбновитьСтатистикуРезультатов(ЭтотОбъект);
			
			Если ПрогрессВыгрузки.Свойство("ДатаПредставление") ИЛИ ПрогрессВыгрузки.Свойство("ДатаВыгрузки") Тогда
				ДатаПредставление        = ПредставлениеДатыВыгрузки(ДатаВыгрузки, ДатаПредставление);
				ОрганизацияПредставление = СтрШаблон("%1 (%2)", Организация, ДатаПредставление);
				ЭлементСписка = Элементы.Организация.СписокВыбора.НайтиПоЗначению(Организация);
				Если ЭлементСписка <> Неопределено Тогда
					ЭлементСписка.Представление = ОрганизацияПредставление;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Иначе 
		ОтключитьОбработчикОжидания("ПроверитьВыгрузкуНоменклатурыВФорме");
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПрогрессВыгрузки(Знач Организация, Знач ИдентификаторЗадания, Знач РазделениеВключено, Знач СоздатьЗадание, Знач ИдентификаторФормы) Экспорт 
	
	НастройкаВыгрузки = РаботаСНоменклатуройСлужебный.НастройкаВыгрузкиНоменклатуры(Организация);
	КлючЗадания       = ?(РазделениеВключено, Строка(ПараметрыСеанса.ОбластьДанныхЗначение), "");
	ПрогрессВыгрузки  = Новый Структура;
	Задания           = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	РегламентноеЗадание = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.ВыгрузкаНоменклатурыРаботаСНоменклатурой);
	Если ИдентификаторЗадания = "РегламентноеЗадание" 
		И РегламентноеЗадание <> Неопределено Тогда
		Задания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Новый Структура("РегламентноеЗадание, Состояние", РегламентноеЗадание, СостояниеФоновогоЗадания.Активно)); 
	Иначе 
		Попытка
			УникальныйИдентификатор = Новый УникальныйИдентификатор(ИдентификаторЗадания);
			Задания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Новый Структура("УникальныйИдентификатор, Состояние", УникальныйИдентификатор, СостояниеФоновогоЗадания.Активно)); 
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Если ТипЗнч(Задания) = Тип("Массив") И Задания.Количество() Тогда
		Задание   = Задания[0];
		Сообщения = Задание.ПолучитьСообщенияПользователю(Истина);
		Индекс    = Сообщения.ВГраница();
		Если Индекс >= 0 Тогда
			Сообщение = РаботаСНоменклатуройСлужебный.ЗначениеИзСтрокиJSON(Сообщения[Индекс].Текст);
			Если Задание.РегламентноеЗадание <> РегламентноеЗадание 
				ИЛИ ТипЗнч(Сообщение) = Тип("Структура")
				И Сообщение.Свойство("Организация")
				И Сообщение.Организация = XMLСтрока(Организация) Тогда
				ПрогрессВыгрузки.Вставить("ВыгруженоПредставление", Сообщение.ТекстСообщения);
			КонецЕсли;
		КонецЕсли;
	Иначе 
		// фоновое задание по идентификатору не найдено
		// это означает, что надо проверить состояние константы: 
		// в случае, если фоновое задание завершилось успешно, то идентификатор задания в константе пустой,
		// в противном случае нужно его очистить, а пользователю сообщить об ошибке
		ПрогрессВыгрузки.Вставить("ВыгруженоПредставление");
		ПрогрессВыгрузки.Вставить("ИдентификаторЗадания");
		ПрогрессВыгрузки.Вставить("ДатаПредставление");
		Если ЗначениеЗаполнено(НастройкаВыгрузки.ИдентификаторЗадания) Тогда
			ПрогрессВыгрузки.ВыгруженоПредставление = НСтр("ru = 'Последняя выгрузка завершена ошибкой'");
			ПрогрессВыгрузки.ИдентификаторЗадания   = "";
			ПрогрессВыгрузки.ДатаПредставление      = НСтр("ru = 'выгрузка завершена ошибкой'");
			РаботаСНоменклатуройСлужебный.ОбновитьПараметрыНастройкиВыгрузки(Организация, ПрогрессВыгрузки);
		Иначе
			ПрогрессВыгрузки.Вставить("ДатаВыгрузки");
			ЗаполнитьЗначенияСвойств(ПрогрессВыгрузки, НастройкаВыгрузки);
			// обновить статистику результатов
			ПрогрессВыгрузки.Вставить("ПодсчетСтатистикиРезультатов");
			ЗаполнитьПараметрыНаСервере(ПрогрессВыгрузки, ИдентификаторФормы, Организация);
		КонецЕсли;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Если СоздатьЗадание = Истина
		И ПрогрессВыгрузки.Свойство("ИдентификаторЗадания") 
		И НЕ ЗначениеЗаполнено(ПрогрессВыгрузки.ИдентификаторЗадания) Тогда
		// такой набор условий означает, что выгрузка запущена впервые
		Расписание                   = Новый РасписаниеРегламентногоЗадания;
		Расписание.ДеньВМесяце       = 1;
		Расписание.ПериодПовтораДней = 1;
		УстановитьПараметрыРегламентногоЗаданияВыгрузки(Новый Структура("Расписание, Использование", Расписание, Истина));
	КонецЕсли;
	
	Возврат ПрогрессВыгрузки;
	
КонецФункции

&НаСервереБезКонтекста
Функция КлючиВыгрузки() Экспорт 
	
	КлючиВыгрузки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("РаботаСНоменклатурой", "ПомощникВыгрузки\КлючиВыгрузки");
	Если ТипЗнч(КлючиВыгрузки) <> Тип("Массив") Тогда
		КлючиВыгрузки = Новый Массив;
	КонецЕсли;
	Возврат КлючиВыгрузки;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДлительнаяОперацияЕстьПроблемыБыстро(Знач УникальныйИдентификатор, Знач Организация) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяМетода           = "РаботаСНоменклатуройСлужебный.ЕстьПроблемыБыстро";
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Работа с номенклатурой. Поиск проблем заполнения номенклатуры в режиме ""Быстрая проверка"".'");
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, Организация);
	
КонецФункции

&НаКлиенте
Процедура ПроверкаПроблемБыстро()
	
	Если НЕ ВыгружатьНоменклатуру И НЕ ВыгружатьВНациональныйКаталог Тогда
		Элементы.СопоставитьНоменклатуру.Заголовок = НСтр("ru = 'Подготовить номенклатуру для выгрузки (не выбран каталог для выгрузки)'");
	КонецЕсли;
	
	Если ТипЗнч(ЕстьПроблемыДлительнаяОперация) <> Тип("Структура") Тогда
		Возврат
	КонецЕсли;
	
	Если ЕстьПроблемыДлительнаяОперация.Статус = "Выполнено" Тогда
		ПроверкаПроблемБыстроЗавершение(ЕстьПроблемыДлительнаяОперация, Неопределено);
	Иначе
		Элементы.СопоставитьНоменклатуру.Заголовок = НСтр("ru = 'Подготовить номенклатуру для выгрузки (выполняется проверка…)'");
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПроверкаПроблемБыстроЗавершение", ЭтотОбъект);
		ПараметрыОжидания     = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ПараметрыОжидания.ВыводитьПрогрессВыполнения = Ложь;
		ПараметрыОжидания.ВыводитьСообщения = Ложь;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ЕстьПроблемыДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаПроблемБыстроЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат
	КонецЕсли;
	
	Если НЕ (Результат.Статус = "Выполнено" 
		И Результат.Свойство("АдресРезультата")
		И ТипЗнч(Результат.АдресРезультата) = Тип("Строка") 
		И ЭтоАдресВременногоХранилища(Результат.АдресРезультата)) Тогда 
		Возврат
	КонецЕсли;
	
	ПроблемыСтрокой = "";
	ПолученныйОтвет = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	Если ПолученныйОтвет = Неопределено Тогда
		ЕстьПроблемы = Истина;
		ПроблемыСтрокой = НСтр("ru = 'не удалось проверить готовность к выгрузке'");
	ИначеЕсли ВыбраноКВыгрузке = 0 Тогда
		ПроблемыСтрокой = НСтр("ru = 'не выбрана номенклатура'");
	Иначе
		ЕстьПроблемы = (ПолученныйОтвет = Истина);
	КонецЕсли;
	
	ОбновитьЗаголовокЭлементаСопоставитьНоменклатуру(ПроблемыСтрокой);
	
	ЕстьПроблемыДлительнаяОперация = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Функция ОбновитьПараметрНастройкиНаКлиенте(ОбновляемыеПараметры, ОбработатьОтклоненные = Ложь)
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ОбновитьПараметрыНастройкиВыгрузки(УникальныйИдентификатор, Организация, ОбновляемыеПараметры, ОбработатьОтклоненные, ИдентификаторЗадания));
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ОбновляемыеПараметры);
	ПроверкаПроблемБыстро();
КонецФункции

&НаСервереБезКонтекста
Функция ОбновитьПараметрыНастройкиВыгрузки(Знач УникальныйИдентификатор, Знач Организация, Знач ОбновляемыеПараметры, Знач ОбработатьОтклоненные, Знач ИдентификаторЗадания)
	
	РаботаСНоменклатуройСлужебный.ОбновитьПараметрыНастройкиВыгрузки(Организация, ОбновляемыеПараметры);
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		УстановитьПривилегированныйРежим(Истина);
		Задания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Новый Структура("УникальныйИдентификатор", ИдентификаторЗадания));
		Для каждого Задание Из Задания Цикл
			Задание.Отменить();
		КонецЦикла;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ЕстьПроблемыДлительнаяОперация", ДлительнаяОперацияЕстьПроблемыБыстро(УникальныйИдентификатор, Организация));
	Если ОбработатьОтклоненные Тогда
		ДлительныеОперации.ВыполнитьПроцедуру(, "Обработки.РаботаСНоменклатурой.СброситьСостоянияВыгрузкиНоменклатуры", Организация, Перечисления.СостоянияВыгрузкиНоменклатуры.Отклонена);
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьПараметрыНаСервере(Параметры, Знач УникальныйИдентификатор, Знач Организация, Знач ЛимитЗаписей = 0)
	
	Если Параметры.Свойство("ПодсчетСтатистикиРезультатов") Тогда
		Параметры.ПодсчетСтатистикиРезультатов = РаботаСНоменклатуройСлужебный.ПолучитьСтатистикуРезультатов(УникальныйИдентификатор, Организация);
	КонецЕсли;
	
	Если Параметры.Свойство("ЕстьПроблемыДлительнаяОперация") Тогда
		Параметры.ЕстьПроблемыДлительнаяОперация = ДлительнаяОперацияЕстьПроблемыБыстро(УникальныйИдентификатор, Организация);
	КонецЕсли;
	
	Если Параметры.Свойство("ВыбраноКВыгрузке") Тогда
		Параметры.ВыбраноКВыгрузке = НоменклатураКВыгрузке(Организация, ЛимитЗаписей);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатистикуИПроблемыБыстро(ОбновитьКоличествоКВыгрузке = Истина, ОбновитьСтатусПроблем = Истина)
	
	ЗапроситьНаСервере = Новый Структура;
	Если ВыгружатьВНациональныйКаталог Тогда
		ЗапроситьНаСервере.Вставить("ПодсчетСтатистикиРезультатов");
	КонецЕсли;
	Если ОбновитьКоличествоКВыгрузке Тогда
		ЗапроситьНаСервере.Вставить("ВыбраноКВыгрузке");
	КонецЕсли;
	Если ОбновитьСтатусПроблем Тогда
		ЗапроситьНаСервере.Вставить("ЕстьПроблемыДлительнаяОперация");
	КонецЕсли;
	
	Если НЕ ЗапроситьНаСервере.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПараметрыНаСервере(ЗапроситьНаСервере, УникальныйИдентификатор, Организация, ЛимитЗаписей);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗапроситьНаСервере);
	
	Если ВыгружатьВНациональныйКаталог Тогда
		РаботаСНоменклатуройСлужебныйКлиент.ОбновитьСтатистикуРезультатов(ЭтотОбъект);
	КонецЕсли;
	Если ОбновитьКоличествоКВыгрузке Тогда
		ОбновитьЗаголовокЭлементаВыбратьНоменклатуру();
	КонецЕсли;
	Если ОбновитьСтатусПроблем Тогда
		ПроверкаПроблемБыстро();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеДатыВыгрузки(Знач ДатаВыгрузки, Знач ДатаПредставление)

	Если ПустаяСтрока(ДатаПредставление) Тогда
		Если ЗначениеЗаполнено(ДатаВыгрузки) Тогда
			ДатаПредставление = СтрШаблон(НСтр("ru = 'выгружена %1'"), ДатаВыгрузки);
		Иначе
			ДатаПредставление = НСтр("ru = 'не выгружалась'");
		КонецЕсли;
	КонецЕсли;

	Возврат ДатаПредставление;
	
КонецФункции

&НаСервереБезКонтекста
Функция СостояниеВыгрузкиВНациональныйКаталог(Знач Организация)
	
	СостоянияВыгрузки = Новый Структура("НаМодерации, Отклонена");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК НаМодерации,
	|	NULL КАК Отклонена
	|ИЗ
	|	РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|ГДЕ
	|	СостоянияВыгрузкиНоменклатуры.Организация = &Организация
	|	И СостоянияВыгрузкиНоменклатуры.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВыгрузкиНоменклатуры.ПроверяетсяМодератором)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|ГДЕ
	|	СостоянияВыгрузкиНоменклатуры.Организация = &Организация
	|	И СостоянияВыгрузкиНоменклатуры.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВыгрузкиНоменклатуры.Отклонена)";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл 
		Если Выборка.НаМодерации = Истина Тогда
			СостоянияВыгрузки.Вставить("НаМодерации", Истина);
		КонецЕсли;
		Если Выборка.Отклонена = Истина Тогда
			СостоянияВыгрузки.Вставить("Отклонена", Истина);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СостоянияВыгрузки;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьОтветПоВыгрузкеВНациональныйКаталог(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		ВыгружатьВНациональныйКаталог = Истина;
	Иначе 
		ОбновляемыеПараметры = ОбновляемыеПараметрыПриИзмененииФлагаКаталога("ВыгружатьВНациональныйКаталог");
		ОбновитьПараметрНастройкиНаКлиенте(ОбновляемыеПараметры, Истина);
		ВыгружатьВНациональныйКаталогПриИзменении();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ОбновляемыеПараметрыПриИзмененииФлагаКаталога(ИмяКаталога)
	
	ОбновляемыеПараметры = Новый Структура(ИмяКаталога, ЭтотОбъект[ИмяКаталога]);
	ОбновляемыеПараметры.Вставить("ДатаПредставление",      "");
	ОбновляемыеПараметры.Вставить("ВыгруженоПредставление", "");
	ОбновляемыеПараметры.Вставить("ДатаВыгрузки",           Дата(1, 1, 1));
	ОбновляемыеПараметры.Вставить("ИдентификаторЗадания",   "");
	
	Возврат ОбновляемыеПараметры;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьИнформациюОВыгрузке()
	
	Если НЕ Организация.Пустая() Тогда
		ДатаПредставление        = ПредставлениеДатыВыгрузки(ДатаВыгрузки, ДатаПредставление);
		ОрганизацияПредставление = СтрШаблон("%1 (%2)", Организация, ДатаПредставление);
		ЭлементСписка = Элементы.Организация.СписокВыбора.НайтиПоЗначению(Организация);
		Если ЭлементСписка = Неопределено Тогда
			ЭлементСписка = Элементы.Организация.СписокВыбора.Добавить(Организация, ОрганизацияПредставление);
		Иначе 
			ЭлементСписка.Представление = ОрганизацияПредставление;
		КонецЕсли;
		Элементы.ДатаПредставление.Видимость = ЭлементСписка.Пометка;
	Иначе 
		Элементы.ДатаПредставление.Видимость = Ложь;
	КонецЕсли;
	НоменклатураВыгружается = ЗначениеЗаполнено(ИдентификаторЗадания);
	Элементы.ВыгрузкаДлительнаяОперация.Видимость = НоменклатураВыгружается;
	Если НоменклатураВыгружается Тогда
		ПодключитьОбработчикОжидания("ПроверитьВыгрузкуНоменклатурыВФорме", 1);
	Иначе 
		Элементы.Шаг5Результаты.Видимость = ВыгружатьВНациональныйКаталог И НЕ Организация.Пустая() И ЗначениеЗаполнено(ВыгруженоПредставление);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовокЭлементаСопоставитьНоменклатуру(ПроблемыСтрокой = "")
	
	Если ПроблемыСтрокой = "" Тогда
		ПроблемыСтрокой = ?(ЕстьПроблемы, НСтр("ru = 'есть проблемы'"), НСтр("ru = 'готова к выгрузке'"));
	КонецЕсли;
	
	Элементы.СопоставитьНоменклатуру.Заголовок = СтрШаблон(НСтр("ru = 'Подготовить номенклатуру для выгрузки (%1)'"), ПроблемыСтрокой);
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыДоступаНациональныйКаталогЗаполнены()
	Возврат ЗначениеЗаполнено(apikey);
КонецФункции

#КонецОбласти