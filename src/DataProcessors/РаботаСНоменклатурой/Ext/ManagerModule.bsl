#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область ТекущиеДела

// См. ТекущиеДелаПереопределяемый.ПриОпределенииОбработчиковТекущихДел.
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	// Определим доступны ли текущему пользователю состояния выгрузки
	Доступность = ПравоДоступа("Чтение", Метаданные.РегистрыСведений.СостоянияВыгрузкиНоменклатуры);
	
	Если НЕ Доступность Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыДел = Новый Структура("ВыгрузкаНовойНоменклатуры, ПодготовкаНоменклатурыКВыгрузке, ОбработкаОшибокВыгрузки", 0, 0, 0);
	ЛимитЗаписей = РаботаСНоменклатуройСлужебныйКлиентСервер.РазмерПорции();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(НоменклатураДляТекущихДел.Номенклатура) КАК КоличествоДанных,
	|	НоменклатураДляТекущихДел.ИндексВыборки КАК ИндексВыборки
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1000
	|		СостоянияВыгрузкиНоменклатуры.Номенклатура КАК Номенклатура,
	|		""ВыгрузкаНовойНоменклатуры"" КАК ИндексВыборки
	|	ИЗ
	|		РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|	ГДЕ
	|		СостоянияВыгрузкиНоменклатуры.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВыгрузкиНоменклатуры.ОжидаетПодтверждения)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1000
	|		СостоянияВыгрузкиНоменклатуры.Номенклатура,
	|		""ВыгрузкаНовойНоменклатуры""
	|	ИЗ
	|		РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|	ГДЕ
	|		СостоянияВыгрузкиНоменклатуры.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВыгрузкиНоменклатуры.ОжидаетИсправления)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1000
	|		СостоянияВыгрузкиНоменклатуры.Номенклатура,
	|		""ПодготовкаНоменклатурыКВыгрузке""
	|	ИЗ
	|		РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|	ГДЕ
	|		СостоянияВыгрузкиНоменклатуры.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВыгрузкиНоменклатуры.СодержитПроблемы)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1000
	|		СостоянияВыгрузкиНоменклатуры.Номенклатура,
	|		""ОбработкаОшибокВыгрузки""
	|	ИЗ
	|		РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|	ГДЕ
	|		СостоянияВыгрузкиНоменклатуры.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВыгрузкиНоменклатуры.Отклонена)) КАК НоменклатураДляТекущихДел
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураДляТекущихДел.ИндексВыборки";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "1000", Формат(ЛимитЗаписей, "ЧГ=0"));
	Результат    = Запрос.Выполнить();
	Выборка      = Результат.Выбрать();
	
	ЕстьДела = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ПараметрыДел.Свойство(Выборка.ИндексВыборки) Тогда
			ПараметрыДел.Вставить(Выборка.ИндексВыборки, Выборка.КоличествоДанных);
			ЕстьДела = Истина;
		КонецЕсли;
	КонецЦикла;
	
	ДелоРодитель = ТекущиеДела.Добавить();
	ДелоРодитель.Идентификатор  = "ВыгрузкаНоменклатуры";
	ДелоРодитель.Представление  = НСтр("ru = 'Выгрузка номенклатуры '");
	ДелоРодитель.Владелец       = Метаданные.Подсистемы.Администрирование;
	ДелоРодитель.ЕстьДела       = ЕстьДела;
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ВыгрузкаНовойНоменклатуры";
	Дело.ЕстьДела       = ПараметрыДел[Дело.Идентификатор] > 0;
	Дело.Представление  = НСтр("ru = 'Выгрузить новую номенклатуру'");
	Дело.Количество     = ПараметрыДел[Дело.Идентификатор];
	Дело.Важное         = Ложь;
	Дело.Форма          = "Обработка.РаботаСНоменклатурой.Форма.НоваяНоменклатураКВыгрузке";
	Дело.Владелец       = "ВыгрузкаНоменклатуры";
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ПодготовкаНоменклатурыКВыгрузке";
	Дело.ЕстьДела       = ПараметрыДел[Дело.Идентификатор] > 0;
	Дело.Представление  = НСтр("ru = 'Подготовить номенклатуру к выгрузке'");
	Дело.Количество     = ПараметрыДел[Дело.Идентификатор];
	Дело.Важное         = Истина;
	Дело.Форма          = "Обработка.РаботаСНоменклатурой.Форма.СопоставлениеНоменклатурыСРубрикатором";
	Дело.ПараметрыФормы = Новый Структура("СценарийИспользования", "ВыгрузкаНоменклатуры");
	Дело.Владелец       = "ВыгрузкаНоменклатуры";
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ОбработкаОшибокВыгрузки";
	Дело.ЕстьДела       = ПараметрыДел[Дело.Идентификатор] > 0;
	Дело.Представление  = НСтр("ru = 'Обработать ошибки выгрузки'");
	Дело.Количество     = ПараметрыДел[Дело.Идентификатор];
	Дело.Важное         = Истина;
	Дело.Форма          = "Обработка.РаботаСНоменклатурой.Форма.РезультатыВыгрузкиНоменклатуры";
	Дело.ПараметрыФормы = Новый Структура("ИмяКоманды", "КомандаСостояниеОтклонена");
	Дело.Владелец       = "ВыгрузкаНоменклатуры";

КонецПроцедуры

#КонецОбласти

// Получает массивы родителей и видов номенклатуры по настройкам отбора
//
// Параметры:
//  Настройки                - НастройкиКомпоновкиДанных - содержит пользовательские настройки отбора номенклатуры
//  ПолучитьРодителей        - Булево - признак необходимости формирования массива родителей
//  ПолучитьВидыНоменклатуры - Булево - признак необходимости формирования массива видов номенклатуры
//
// Возвращаемое значение:
//  СтруктураРезультата - Структура с ключами Родители и ВидыНоменклатуры
//
Функция РодителиИВидыНоменклатурыПоОтбору(Настройки, ПолучитьРодителей = Ложь, ПолучитьВидыНоменклатуры = Ложь) Экспорт
	
	СтруктураРезультата   = Новый Структура("Родители, ВидыНоменклатуры");
	Запрос                = РаботаСНоменклатуройСлужебный.ЗапросОтборНоменклатуры(Настройки, Ложь);
	ТекстЗапроса          = "";
	ТекстЗапросаИсточник  = Запрос.Текст;
	ИндексВидНоменклатуры = 0;
	СхемаЗапроса          = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапросаИсточник);
	СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбиратьРазличные = Истина;
	ТекстЗапросаИсточник  = СхемаЗапроса.ПолучитьТекстЗапроса();
	КоличествоПолей       = СхемаЗапроса.ПакетЗапросов[0].Колонки.Количество();
	
	Если ПолучитьРодителей Тогда
		ИндексВидНоменклатуры = 1;
		Для ОбратныйИндекс = 1 По КоличествоПолей Цикл
			ПроверяемоеПоле = СхемаЗапроса.ПакетЗапросов[0].Колонки[КоличествоПолей - ОбратныйИндекс];
			Если ПроверяемоеПоле.Псевдоним <> "Родитель" Тогда
				СхемаЗапроса.ПакетЗапросов[0].Колонки.Удалить(КоличествоПолей - ОбратныйИндекс);
			КонецЕсли;
		КонецЦикла;
		ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
		СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапросаИсточник);
	КонецЕсли;
	
	Если ПолучитьВидыНоменклатуры Тогда
		Для ОбратныйИндекс = 1 По КоличествоПолей Цикл
			ПроверяемоеПоле = СхемаЗапроса.ПакетЗапросов[0].Колонки[КоличествоПолей - ОбратныйИндекс];
			Если ПроверяемоеПоле.Псевдоним <> "ВидНоменклатуры" Тогда
				СхемаЗапроса.ПакетЗапросов[0].Колонки.Удалить(КоличествоПолей - ОбратныйИндекс);
			КонецЕсли;
		КонецЦикла;
		ТекстЗапроса = ТекстЗапроса + ?(ПустаяСтрока(ТекстЗапроса), "", ";");
		ТекстЗапроса = ТекстЗапроса + СхемаЗапроса.ПолучитьТекстЗапроса();
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Результаты   = Запрос.ВыполнитьПакет();
	
	Если ПолучитьРодителей Тогда
		СтруктураРезультата.Вставить("Родители", Результаты[0].Выгрузить().ВыгрузитьКолонку(0));
	КонецЕсли;
	Если ПолучитьВидыНоменклатуры Тогда
		СтруктураРезультата.Вставить("ВидыНоменклатуры", Результаты[ИндексВидНоменклатуры].Выгрузить().ВыгрузитьКолонку(0));
	КонецЕсли;
	
	Возврат СтруктураРезультата;
	
КонецФункции

Функция ВидыНоменклатурыКВыгрузке(Организация, СписокСостояний = Неопределено) Экспорт
	
	ВидыНоменклатуры = Новый СписокЗначений;
	
	Если СписокСостояний = Неопределено Тогда
		СписокСостояний = РаботаСНоменклатуройСлужебный.СостоянияВыгружаемаяНоменклатура();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(РаботаСНоменклатуройСлужебный.ТекстЗапросаВыгружаемаяНоменклатура());
		ОсновнойЗапрос  = СхемаЗапроса.ПакетЗапросов[0];
		КоличествоПолей = ОсновнойЗапрос.Колонки.Количество();
		Для ОбратныйИндексПоля = 1 По КоличествоПолей Цикл
			ПроверяемоеПоле = ОсновнойЗапрос.Колонки[КоличествоПолей - ОбратныйИндексПоля];
			Если ПроверяемоеПоле.Псевдоним <> "ВидНоменклатуры" Тогда
				ОсновнойЗапрос.Колонки.Удалить(КоличествоПолей - ОбратныйИндексПоля);
			КонецЕсли;
		КонецЦикла;
		ОсновнойЗапрос.Операторы[0].ВыбиратьРазличные = Истина;
		Запрос = Новый Запрос(ОсновнойЗапрос.ПолучитьТекстЗапроса());
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("СписокСостояний", СписокСостояний);
		
		ВидыНоменклатуры.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидНоменклатуры"));
	КонецЕсли;
	
	Возврат ВидыНоменклатуры;
	
КонецФункции

Функция ДобавитьКВыгрузкеНоменклатуруПоСписку(Организация, МассивНоменклатуры) Экспорт 
	
	ЛимитЗаписей           = РаботаСНоменклатуройСлужебныйКлиентСервер.РазмерПорции();
	ЛимитСтрока            = Формат(ЛимитЗаписей, "ЧГ=");
	Состояние              = Перечисления.СостоянияВыгрузкиНоменклатуры.Новая;
	ТипНоменклатура        = Метаданные.ОпределяемыеТипы.НоменклатураРаботаСНоменклатурой.Тип;
	ПорядокНоменклатура    = ТипНоменклатура.ПривестиЗначение();
	ИмяТаблицыНоменклатура = РаботаСНоменклатурой.ИмяТаблицыПоТипу(ТипНоменклатура);
	Исключено              = 0;
	Добавлено              = 0;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	СправочникНоменклатура.Ссылка КАК Номенклатура
	               |ИЗ
	               |	Справочник.Номенклатура КАК СправочникНоменклатура";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Справочник.Номенклатура", ИмяТаблицыНоменклатура);
	ТекстЗапроса = ДобавитьВТекстЗапросаУсловиеНоваяНоменклатура(ТекстЗапроса,, МассивНоменклатуры);
	Запрос       = Новый Запрос(ТекстЗапроса);
	
	ПустаяХарактеристика = РаботаСНоменклатурой.ПустаяСсылкаНаХарактеристику();
	Запрос.УстановитьПараметр("Параметр_ПустаяХарактеристика", ПустаяХарактеристика);
	Запрос.УстановитьПараметр("Параметр_Организация", Организация);
	
	НаборЗаписей = РегистрыСведений.СостоянияВыгрузкиНоменклатуры.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	Пока Истина Цикл
		Запрос.УстановитьПараметр("Параметр_Порядок_Номенклатура", ПорядокНоменклатура);
		Запрос.УстановитьПараметр("МассивНоменклатуры", МассивНоменклатуры);
		НовыеЗаписи         = Запрос.Выполнить().Выгрузить();
		ЗаписейНаДобавление = НовыеЗаписи.Количество();
		Если ЗаписейНаДобавление = 0 Тогда
			Прервать;
		КонецЕсли;
		
		НовыеЗаписи.Индексы.Добавить("Номенклатура");
		
		Для каждого ПараметрыЗаписи Из НовыеЗаписи Цикл
			Запись = НаборЗаписей.Добавить();
			Запись.Номенклатура  = ПараметрыЗаписи.Номенклатура;
			Запись.Организация   = Организация;
			Запись.Состояние     = Состояние;
			Запись.ДатаСостояния = ТекущаяДатаСеанса();
		КонецЦикла;
		
		ПорядокНоменклатура = НовыеЗаписи[ЗаписейНаДобавление - 1].Номенклатура;
		Добавлено           = Добавлено + ЗаписейНаДобавление;
		
		ВсегоНоменклатуры  = МассивНоменклатуры.Количество();
		Для ОбратныйИндекс = 1 По ВсегоНоменклатуры Цикл
			ОбработанныеСтроки = НовыеЗаписи.НайтиСтроки(Новый Структура("Номенклатура", МассивНоменклатуры[ВсегоНоменклатуры - ОбратныйИндекс]));
			Если ОбработанныеСтроки.Количество() Тогда
				МассивНоменклатуры.Удалить(ВсегоНоменклатуры - ОбратныйИндекс);
			КонецЕсли;
		КонецЦикла;
		
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать(Ложь);
		НаборЗаписей.Очистить();
	КонецЦикла;
	
	Если МассивНоменклатуры.Количество() Тогда
		// Остались строки для обработки, значит выбраны строки, присутствующие в регистре.
		// С этими данными нужно поступить следующим образом:
		// 1. Если состояние ВыгрузкаЗапрещена, то подсчитать количество таких записей и вернуть в сообщении пользователю
		// 2. Если состояние ОжидаетПодтверждения или ОжидаетИсправления, то есть номенклатура добавлена регламентным заданием
		//    и ожидает интерактивного подтверждения пользователем, то обновить состояние на Новая
		// 3. Не менять состояние во всех прочих случаях.
		Запрос.Текст = "ВЫБРАТЬ
		|	СостоянияВыгрузкиНоменклатуры.Номенклатура КАК Номенклатура,
		|	СостоянияВыгрузкиНоменклатуры.Состояние КАК Состояние
		|ИЗ
		|	РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
		|ГДЕ
		|	СостоянияВыгрузкиНоменклатуры.Организация = &Параметр_Организация
		|	И СостоянияВыгрузкиНоменклатуры.Номенклатура В(&МассивНоменклатуры)
		|	И СостоянияВыгрузкиНоменклатуры.ХарактеристикаНоменклатуры = &Параметр_ПустаяХарактеристика
		|	И СостоянияВыгрузкиНоменклатуры.Состояние В(&СписокСостояний)";
		
		СписокСостояний = Новый СписокЗначений;
		СписокСостояний.Добавить(Перечисления.СостоянияВыгрузкиНоменклатуры.ВыгрузкаЗапрещена);
		СписокСостояний.Добавить(Перечисления.СостоянияВыгрузкиНоменклатуры.ОжидаетИсправления);
		СписокСостояний.Добавить(Перечисления.СостоянияВыгрузкиНоменклатуры.ОжидаетПодтверждения);
		Запрос.УстановитьПараметр("СписокСостояний", СписокСостояний);
		Запрос.УстановитьПараметр("МассивНоменклатуры", МассивНоменклатуры);
		
		НоменклатураВРегистре = Запрос.Выполнить().Выгрузить();
		Для каждого СостояниеНоменклатуры Из НоменклатураВРегистре Цикл
			Номенклатура = СостояниеНоменклатуры.Номенклатура;
			Если СостояниеНоменклатуры.Состояние = Перечисления.СостоянияВыгрузкиНоменклатуры.ВыгрузкаЗапрещена Тогда
				Исключено = Исключено + 1;
			Иначе 
				НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура);
				Запись = НаборЗаписей.Добавить();
				Запись.Номенклатура  = Номенклатура;
				Запись.Организация   = Организация;
				Запись.Состояние     = Состояние;
				НаборЗаписей.Записать();
				НаборЗаписей.Очистить();
				Добавлено = Добавлено + 1;
			КонецЕсли;
			
			МассивНоменклатуры.Удалить(МассивНоменклатуры.Найти(Номенклатура));
		КонецЦикла;
	КонецЕсли;
	
	ТекстЛога = "";
	Если Исключено > 0 Тогда
		ТекстЛога = СтрШаблон(НСтр("ru = 'Добавлено к выгрузке %1, найдены в исключениях %2'"), Добавлено + МассивНоменклатуры.Количество(), Исключено);
	КонецЕсли;
	
	Возврат Новый Структура("Добавлено, ТекстЛога", Добавлено, ТекстЛога);;

КонецФункции

Функция ДобавитьКВыгрузкеНоменклатуруПоОтбору(Организация, НастройкиОтбора) Экспорт 
	
	// Добавление номенклатуры по отбору подразумевает полное перезаполнение всех выгружаемых позиций. 
	// При этом все исключения должны остаться.
	// Поэтому обработка делится на 2 этапа: 
	// 1. запись набора записей только с исключениями – при этом в регистре останутся только исключения.
	// 2. запись в режиме без замещения выгружаемых позиций по отбору.
	// Исключений много быть не должно, но на всякий случай запись тоже реализована порциями.
	// По отбору могут быть выбраны миллионы записей, поэтому запись ведется порциями.
	
	КоличествоВыгружается = 0;
	КоличествоИсключения  = 0;
	ЛимитЗаписей          = РаботаСНоменклатуройСлужебныйКлиентСервер.РазмерПорции();
	ЛимитСтрока           = Формат(ЛимитЗаписей, "ЧГ=");
	ПустаяХарактеристика  = РаботаСНоменклатурой.ПустаяСсылкаНаХарактеристику();
	ПустаяНоменклатура    = Метаданные.ОпределяемыеТипы.НоменклатураРаботаСНоменклатурой.Тип.ПривестиЗначение();
	ПорядокНоменклатура   = ПустаяНоменклатура;
	ВыгрузкаЗапрещена     = Перечисления.СостоянияВыгрузкиНоменклатуры.ВыгрузкаЗапрещена;
	СостояниеНовая        = Перечисления.СостоянияВыгрузкиНоменклатуры.Новая;
	НаборЗаписей          = РегистрыСведений.СостоянияВыгрузкиНоменклатуры.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	
	ЗапросИсключения = Новый Запрос;
	ЗапросИсключения.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1000
	|	СостоянияВыгрузкиНоменклатуры.Номенклатура КАК Номенклатура,
	|	СостоянияВыгрузкиНоменклатуры.ДатаСостояния КАК ДатаСостояния
	|ИЗ
	|	РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|ГДЕ
	|	СостоянияВыгрузкиНоменклатуры.Организация = &Параметр_Организация
	|	И СостоянияВыгрузкиНоменклатуры.ХарактеристикаНоменклатуры = &Параметр_ПустаяХарактеристика
	|	И СостоянияВыгрузкиНоменклатуры.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВыгрузкиНоменклатуры.ВыгрузкаЗапрещена)
	|	И СостоянияВыгрузкиНоменклатуры.Номенклатура > &Параметр_Порядок_Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура";
	ЗапросИсключения.Текст = СтрЗаменить(ЗапросИсключения.Текст, "1000", ЛимитСтрока);

	ЗапросИсключения.УстановитьПараметр("Параметр_ПустаяХарактеристика", ПустаяХарактеристика);
	ЗапросИсключения.УстановитьПараметр("Параметр_Организация", Организация);

	Запрос = РаботаСНоменклатуройСлужебный.ЗапросОтборНоменклатуры(НастройкиОтбора, Ложь, Новый Структура("Номенклатура"));
	Запрос.Текст = ДобавитьВТекстЗапросаУсловиеНоваяНоменклатура(Запрос.Текст);
	Запрос.УстановитьПараметр("Параметр_Организация", Организация);
	
	БлокировкаДанных  = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.СостоянияВыгрузкиНоменклатуры");
	ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	Попытка
		БлокировкаДанных.Заблокировать(); 
		
		Пока Истина Цикл
			ЗапросИсключения.УстановитьПараметр("Параметр_Порядок_Номенклатура", ПорядокНоменклатура);
			
			ТаблицаИсключения = ЗапросИсключения.Выполнить().Выгрузить();
			ВсегоИсключений   = ТаблицаИсключения.Количество();
			Если ВсегоИсключений = 0 Тогда
				Прервать;
			КонецЕсли;
			
			Для каждого НоменклатураИсключение Из ТаблицаИсключения Цикл
				Запись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, НоменклатураИсключение);
				Запись.Организация = Организация;
				Запись.Состояние   = ВыгрузкаЗапрещена;
			КонецЦикла;
			ПорядокНоменклатура = ТаблицаИсключения[ВсегоИсключений - 1].Номенклатура;
			
			КоличествоИсключения = КоличествоИсключения + ВсегоИсключений;
		КонецЦикла;
		
		НаборЗаписей.Записать();
		НаборЗаписей.Очистить();
		
		ПорядокНоменклатура = ПустаяНоменклатура;
		Пока Истина Цикл
			Запрос.УстановитьПараметр("Параметр_Порядок_Номенклатура", ПорядокНоменклатура);
			
			НовыеЗаписи         = Запрос.Выполнить().Выгрузить();
			НовыеЗаписи         = НовыеЗаписи.ВыгрузитьКолонку("Номенклатура");
			ЗаписейНаДобавление = НовыеЗаписи.Количество();
			Если ЗаписейНаДобавление = 0 Тогда
				Прервать;
			КонецЕсли;
			
			Для каждого Номенклатура Из НовыеЗаписи Цикл
				Запись = НаборЗаписей.Добавить();
				Запись.Номенклатура  = Номенклатура;
				Запись.Организация   = Организация;
				Запись.Состояние     = СостояниеНовая;
				Запись.ДатаСостояния = ТекущаяДатаСеанса();
			КонецЦикла;
			
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Записать(Ложь);
			НаборЗаписей.Очистить();
			
			ПорядокНоменклатура   = НовыеЗаписи[ЗаписейНаДобавление - 1];
			КоличествоВыгружается = КоличествоВыгружается + ЗаписейНаДобавление;
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
	Возврат Новый Структура("КоличествоВыгружается, КоличествоИсключения", КоличествоВыгружается, КоличествоИсключения);

КонецФункции

Функция ОбновитьСостоянияНовойНоменклатуры(Организация, ДатаОткрытияФормы, Добавить) Экспорт 
	
	ПустаяХарактеристика = РаботаСНоменклатурой.ПустаяСсылкаНаХарактеристику();
	ПустаяНоменклатура   = Метаданные.ОпределяемыеТипы.НоменклатураРаботаСНоменклатурой.Тип.ПривестиЗначение();
	ПорядокНоменклатура  = ПустаяНоменклатура;
	ВсегоЗаписей         = 0;
	ЛимитСтрока          = Формат(РаботаСНоменклатуройСлужебныйКлиентСервер.РазмерПорции(), "ЧГ=");
	Запрос               = Новый Запрос;
	Запрос.Текст         = "ВЫБРАТЬ ПЕРВЫЕ 1000
	|	СостоянияВыгрузкиНоменклатуры.Номенклатура КАК Номенклатура
	|ИЗ
	|	РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|ГДЕ
	|	СостоянияВыгрузкиНоменклатуры.Организация = &Параметр_Организация
	|	И СостоянияВыгрузкиНоменклатуры.ХарактеристикаНоменклатуры = &Параметр_ПустаяХарактеристика
	|	И СостоянияВыгрузкиНоменклатуры.Состояние = &СостояниеИсходное
	|	И (&НеПроверятьДату
	|			ИЛИ СостоянияВыгрузкиНоменклатуры.ДатаСостояния > &ДатаОткрытияФормы)
	|	И СостоянияВыгрузкиНоменклатуры.Номенклатура > &Параметр_Порядок_Номенклатура
	|
	|ДЛЯ ИЗМЕНЕНИЯ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "1000", ЛимитСтрока);

	Если Добавить = Истина Тогда
		СостояниеИсходное = Перечисления.СостоянияВыгрузкиНоменклатуры.ОжидаетПодтверждения;
		Состояние         = Перечисления.СостоянияВыгрузкиНоменклатуры.ОжидаетВыгрузки;
	Иначе
		СостояниеИсходное = Перечисления.СостоянияВыгрузкиНоменклатуры.ОжидаетВыгрузки;
		Состояние         = Перечисления.СостоянияВыгрузкиНоменклатуры.ОжидаетПодтверждения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Параметр_ПустаяХарактеристика", ПустаяХарактеристика);
	Запрос.УстановитьПараметр("Параметр_Организация",          Организация);
	Запрос.УстановитьПараметр("СостояниеИсходное",             СостояниеИсходное);
	Запрос.УстановитьПараметр("ДатаОткрытияФормы",             ДатаОткрытияФормы);
	Запрос.УстановитьПараметр("НеПроверятьДату",               Добавить = Истина);
	
	Пока Истина Цикл
		Запрос.УстановитьПараметр("Параметр_Порядок_Номенклатура", ПорядокНоменклатура);
		ТаблицаНоменклатуры = Запрос.Выполнить().Выгрузить();
		ТекущееКоличество   = ТаблицаНоменклатуры.Количество();
		Если ТекущееКоличество = 0 Тогда
			Прервать;
		КонецЕсли;
		ЗаписатьДанныеВРегистрНоменклатураКВыгрузке(Организация, ТаблицаНоменклатуры, Состояние);
		
		ВсегоЗаписей        = ВсегоЗаписей + ТекущееКоличество;
		ПорядокНоменклатура = ТаблицаНоменклатуры[ТекущееКоличество - 1].Номенклатура;
	КонецЦикла;
	
	Возврат ВсегоЗаписей;

КонецФункции

Функция НоваяНоменклатураПоОрганизациям() Экспорт 
	
	ПустаяХарактеристика = РаботаСНоменклатурой.ПустаяСсылкаНаХарактеристику();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПодЗапрос.Организация КАК Организация,
	|	КОЛИЧЕСТВО(ПодЗапрос.Номенклатура) КАК Количество
	|ИЗ
	|	(ВЫБРАТЬ
	|		СостоянияВыгрузкиНоменклатуры.Организация КАК Организация,
	|		СостоянияВыгрузкиНоменклатуры.Номенклатура КАК Номенклатура
	|	ИЗ
	|		РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|	ГДЕ
	|		СостоянияВыгрузкиНоменклатуры.ХарактеристикаНоменклатуры = &ПустаяХарактеристика
	|		И СостоянияВыгрузкиНоменклатуры.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВыгрузкиНоменклатуры.ОжидаетПодтверждения)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СостоянияВыгрузкиНоменклатуры.Организация,
	|		СостоянияВыгрузкиНоменклатуры.Номенклатура
	|	ИЗ
	|		РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|	ГДЕ
	|		СостоянияВыгрузкиНоменклатуры.ХарактеристикаНоменклатуры = &ПустаяХарактеристика
	|		И СостоянияВыгрузкиНоменклатуры.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВыгрузкиНоменклатуры.ОжидаетИсправления)) КАК ПодЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ПодЗапрос.Организация";
	
	Запрос.УстановитьПараметр("ПустаяХарактеристика", ПустаяХарактеристика);
	
	Результат = Новый Соответствие;
	Для каждого СтрокаРезультата Из Запрос.Выполнить().Выгрузить() Цикл
		Результат.Вставить(СтрокаРезультата.Организация, СтрокаРезультата.Количество);
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

Процедура ЗаписатьДанныеВРегистрНоменклатураКВыгрузке(Знач Организация, Знач МассивКлючей, Знач Состояние) Экспорт 
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ТекущийКлюч Из МассивКлючей Цикл
		
		ДанныеЗаписи = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры");
		ЗаполнитьЗначенияСвойств(ДанныеЗаписи, ТекущийКлюч);
		Если Не ЗначениеЗаполнено(ДанныеЗаписи.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		ДанныеЗаписи.Вставить("Организация", Организация);
		
		НаборЗаписей = РегистрыСведений.СостоянияВыгрузкиНоменклатуры.СоздатьНаборЗаписей();
		Для Каждого ЭлементКлюча Из ДанныеЗаписи Цикл
			Если ЗначениеЗаполнено(ЭлементКлюча.Значение) Тогда
				НаборЗаписей.Отбор[ЭлементКлюча.Ключ].Установить(ЭлементКлюча.Значение);
			КонецЕсли;
		КонецЦикла;
		ДанныеЗаписи.Вставить("Состояние", Состояние);
		ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), ДанныеЗаписи);
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьКомандыСостояния(Форма, ИмяГруппы, ВидКнопки = Неопределено) Экспорт
	
	ПрефиксКоманды               = "КомандаСостояние";
	Форма.ПрефиксКомандСостояние = ПрефиксКоманды;
	Форма.СтатистикаРезультатов  = Новый Структура;
	ЗаполнитьСписокСостояний     = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "СписокСостояний");
	
	КартинкиСостояняий = Новый Структура;
	КартинкиСостояняий.Вставить("Отклонена", БиблиотекаКартинок.КрасныйШарБЭД);
	КартинкиСостояняий.Вставить("Принята", БиблиотекаКартинок.ЗеленыйШарБЭД);
	КартинкиСостояняий.Вставить("ПроверяетсяМодератором", БиблиотекаКартинок.ЖелтыйШарБЭД);
	КартинкиСостояняий.Вставить("ОжидаетВыгрузки", БиблиотекаКартинок.СерыйШарБЭД);
	
	Для каждого ПараметрыСостояния Из КартинкиСостояняий Цикл
		СостояниеВыгрузки             = Перечисления.СостоянияВыгрузкиНоменклатуры[ПараметрыСостояния.Ключ];
		ИмяКоманды                    = ПрефиксКоманды + ПараметрыСостояния.Ключ;
		КомандаСостояния              = Форма.Команды.Добавить(ИмяКоманды);
		КомандаСостояния.Действие     = "Подключаемый_ДействиеКомандыСостояние";
		КомандаСостояния.Отображение  = ОтображениеКнопки.КартинкаИТекст;
		КомандаСостояния.Заголовок    = Строка(СостояниеВыгрузки);
		КомандаСостояния.Картинка     = ПараметрыСостояния.Значение;
		КнопкаСостояния               = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Форма.Элементы[ИмяГруппы]);
		КнопкаСостояния.ИмяКоманды    = ИмяКоманды;
		КнопкаСостояния.Заголовок     = Строка(СостояниеВыгрузки);
		Если ВидКнопки <> Неопределено Тогда
			КнопкаСостояния.Вид    = ВидКнопки;
		КонецЕсли;
		
		Форма.СтатистикаРезультатов.Вставить(ПараметрыСостояния.Ключ, 0);
		
		Если ЗаполнитьСписокСостояний Тогда
			Форма.СписокСостояний.Добавить(СостояниеВыгрузки, ИмяКоманды);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура СброситьСостоянияВыгрузкиНоменклатуры(Организация, Состояние) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1000
	|	СостоянияВыгрузкиНоменклатуры.Номенклатура КАК Номенклатура
	|ИЗ
	|	РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|ГДЕ
	|	СостоянияВыгрузкиНоменклатуры.Организация = &Организация";
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Если (ТипЗнч(Состояние) = Тип("Массив") ИЛИ ТипЗнч(Состояние) = Тип("СписокЗначений")) 
		И Состояние.Количество() > 0 Тогда
		Запрос.Текст = Запрос.Текст + Символы.ПС + "И СостоянияВыгрузкиНоменклатуры.Состояние В (&Состояние)";
	ИначеЕсли ТипЗнч(Состояние) = Тип("ПеречислениеСсылка.СостоянияВыгрузкиНоменклатуры") Тогда 
		Запрос.Текст = Запрос.Текст + Символы.ПС + "И СостоянияВыгрузкиНоменклатуры.Состояние = &Состояние";
	Иначе 
		Запрос.Текст = Запрос.Текст + Символы.ПС + "И СостоянияВыгрузкиНоменклатуры.Состояние <> &Состояние";
		Состояние = Перечисления.СостоянияВыгрузкиНоменклатуры.Новая;
	КонецЕсли;
	Запрос.УстановитьПараметр("Состояние", Состояние);
	
	Пока Истина Цикл
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			Прервать;
		КонецЕсли;
		
		ЗаписатьДанныеВРегистрНоменклатураКВыгрузке(Организация, Результат.Выгрузить(), Перечисления.СостоянияВыгрузкиНоменклатуры.Новая);
	КонецЦикла;
	
КонецПроцедуры

Функция СостояниеВыгрузкиВНациональныйКаталог(Знач Организация) Экспорт
	
	СостоянияВыгрузки = Новый Структура("НаМодерации, Отклонена", Ложь, Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК НаМодерации,
	|	NULL КАК Отклонена
	|ИЗ
	|	РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|ГДЕ
	|	СостоянияВыгрузкиНоменклатуры.Организация = &Организация
	|	И СостоянияВыгрузкиНоменклатуры.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВыгрузкиНоменклатуры.ПроверяетсяМодератором)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|ГДЕ
	|	СостоянияВыгрузкиНоменклатуры.Организация = &Организация
	|	И СостоянияВыгрузкиНоменклатуры.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВыгрузкиНоменклатуры.Отклонена)";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл 
		Если Выборка.НаМодерации = Истина Тогда
			СостоянияВыгрузки.Вставить("НаМодерации", Истина);
		КонецЕсли;
		Если Выборка.Отклонена = Истина Тогда
			СостоянияВыгрузки.Вставить("Отклонена", Истина);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СостоянияВыгрузки;
	
КонецФункции
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДобавитьВТекстЗапросаУсловиеНоваяНоменклатура(ТекстЗапроса, ЛимитЗаписей = 1000, МассивНоменклатуры = Неопределено)

	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	ОсновнойЗапрос      = СхемаЗапроса.ПакетЗапросов[0];
	КолонкаНоменклатура = ОсновнойЗапрос.Колонки.Найти("Номенклатура");
	ПолеНоменклатура    = ОсновнойЗапрос.Операторы[0].ВыбираемыеПоля[ОсновнойЗапрос.Колонки.Индекс(КолонкаНоменклатура)];
	ПолеНоменклатура    = Строка(ПолеНоменклатура);
	
	УсловиеНовая        = "
	|	ИСТИНА НЕ В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			РегистрСведений.СостоянияВыгрузкиНоменклатуры КАК СостоянияВыгрузкиНоменклатуры
	|		ГДЕ
	|			СостоянияВыгрузкиНоменклатуры.Номенклатура = %1
	|			И СостоянияВыгрузкиНоменклатуры.Организация = &Параметр_Организация)";
	
	Если МассивНоменклатуры <> Неопределено Тогда
		ОсновнойЗапрос.Операторы[0].Отбор.Добавить(СтрШаблон("%1 В(&МассивНоменклатуры)", ПолеНоменклатура));
	КонецЕсли;
	ОсновнойЗапрос.Операторы[0].Отбор.Добавить(СтрШаблон(УсловиеНовая, ПолеНоменклатура));
	ОсновнойЗапрос.Операторы[0].Отбор.Добавить(СтрШаблон("%1 > &Параметр_Порядок_Номенклатура", Строка(ПолеНоменклатура)));
	ОсновнойЗапрос.Операторы[0].КоличествоПолучаемыхЗаписей = ЛимитЗаписей;
	ОсновнойЗапрос.Порядок.Добавить(ПолеНоменклатура);
	
	Возврат СхемаЗапроса.ПолучитьТекстЗапроса();

КонецФункции

#КонецОбласти

#КонецЕсли