#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Возвращает текст запроса для расчета статусов указания серий
// Параметры:
// 	ПараметрыУказанияСерий - Структура - состав полей задается в функции ПроверкаИПодборПродукцииМОТП.ПараметрыУказанияСерий
// Возвращаемое значение:
// 	Строка - текст запроса расчета статуса указания серий.
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	Возврат ИнтеграцияИС.ТекстЗапросаЗаполненияСтатусовУказанияСерий(
		Метаданные.Обработки.ПроверкаИПодборТабачнойПродукцииМОТП, ПараметрыУказанияСерий);
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Вызывается из длительной операции по подготовке данных для формы проверки и подбора табачной продукции.
// 
// Параметры:
//	Параметры - Структура - содержит следующие значения:
// 	* ПроверкаНеПоДокументу            - Булево - признак получения данных не по ссылке на документ
// 	* ПроверяемыйДокумент              - ДокументСсылка - ссылка на документ, из формы которого открыта форма проверки и подбора
// 	* НачальныйСтатусПроверки          - ПеречислениеСсылка.СтатусыПроверкиНаличияПродукцииИС - статус наличия продукции, используемый при подготовке данных
// 	* ДетализацияСтруктурыХранения     - ПеречислениеСсылка.ДетализацияСтруктурыХраненияИС - значение детализации из формы проверки
// 	* РедактированиеФормыНедоступно    - Булево - признак запрета редактирования формы подбора
// 	* РежимПодбораСуществующихУпаковок - Булево - признак работы со штрихкодами упаковок, имеющимися в информационной базе
// 	* ПараметрыСканирования            - Структура - параметры обработки кодов маркировки, сформированные в форме проверки и подбора
// 	* ПараметрыПроверкиКодовМаркировки - Структура - параметры проверки кодов маркировки по статусу и владельцу, сформированные в форме проверки и подбора
// 	* КонтролироватьСканируемуюПродукциюПоДокументуОснованию - Булево - признак необходимости контроля наличия табачной продукции по основанию проверяемого документа
//	АдресРезультата - Строка - адрес временного хранилища, в которое будут помещены результаты выполнения.
Процедура ЗагрузитьДанныеДокументаДлительнаяОперация(Параметры, АдресРезультата) Экспорт
	
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"Обработка.ПроверкаИПодборТабачнойПродукцииМОТП.МодульМенеджера.ЗагрузитьДанныеДокументаДлительнаяОперация");
	
	ДанныеДокумента = Новый Структура;
	ДанныеДокумента.Вставить("ДеревоМаркированнойПродукции",          ДеревоМаркированнойПродукции());
	ДанныеДокумента.Вставить("ПодобраннаяМаркируемаяПродукция",       ПодобраннаяМаркируемаяПродукция());
	ДанныеДокумента.Вставить("ДоступныеДляПроверкиУпаковки",          Новый СписокЗначений());
	ДанныеДокумента.Вставить("УпаковкиДокумента",                     Новый СписокЗначений());
	ДанныеДокумента.Вставить("НачальныйСтатусПроверки",               Параметры.НачальныйСтатусПроверки);
	ДанныеДокумента.Вставить("ДетализацияСтруктурыХранения",          Параметры.ДетализацияСтруктурыХранения);
	ДанныеДокумента.Вставить("РежимПодбораСуществующихУпаковок",      Параметры.РежимПодбораСуществующихУпаковок);
	ДанныеДокумента.Вставить("ПроверкаЭлектронногоДокумента",         Параметры.ПроверкаЭлектронногоДокумента);
	ДанныеДокумента.Вставить("ПараметрыПроверкиКодовМаркировки",      Параметры.ПараметрыПроверкиКодовМаркировки);
	ДанныеДокумента.Вставить("КоличествоНедопустимыхКодовМаркировки", 0);
	ДанныеДокумента.Вставить(
		"КонтролироватьСканируемуюПродукциюПоДокументуОснованию",
		Параметры.КонтролироватьСканируемуюПродукциюПоДокументуОснованию);
	ДанныеДокумента.Вставить(
		"ВозможностьЗагрузкиДанныхБезПодключенияМОТП",
		Параметры.ВозможностьЗагрузкиДанныхБезПодключенияМОТП);
	ДанныеДокумента.Вставить("УточнениеКоэффициентовУпаковок",        Неопределено);
	ДанныеДокумента.Вставить("ДополнительноеДействиеЗагрузки",        Неопределено);
	
	ДопустимыеДетализации = ПроверкаИПодборПродукцииМОТП.ДопустимыеДетализацииСтруктурыХраненияПоДокументу(Параметры.ПроверяемыйДокумент);
	
	СохраненнаяДетализацияСтруктурыХранения = СохраненнаяДетализацияСтруктурыХранения(Параметры.РежимПодбораСуществующихУпаковок);
	Если ЗначениеЗаполнено(СохраненнаяДетализацияСтруктурыХранения)
		И ДопустимыеДетализации.Найти(СохраненнаяДетализацияСтруктурыХранения) = Неопределено Тогда
		СохраненнаяДетализацияСтруктурыХранения = Неопределено;
	КонецЕсли;
	
	ДанныеДокумента.Вставить("СохраненнаяДетализацияСтруктурыХранения", СохраненнаяДетализацияСтруктурыХранения);
	ДанныеДокумента.Вставить("СодержимоеУпаковокНедоступно", Ложь);
	ДанныеДокумента.Вставить("СтрокаПачкиБезБлока",          Неопределено);
	ДанныеДокумента.Вставить("СтрокаБлокиБезКоробки",        Неопределено);
	ДанныеДокумента.Вставить("Организация",                  Параметры.ПараметрыСканирования.Организация);
	ДанныеДокумента.Вставить("ЭтоВосстановлениеДетализации", Параметры.ЭтоВосстановлениеДетализации);
	ДанныеДокумента.Вставить("ВидМаркируемойПродукции",      Параметры.ВидМаркируемойПродукции);
	ДанныеДокумента.Вставить("ПроверяемыйДокумент",          Параметры.ПроверяемыйДокумент);
	ДанныеДокумента.Вставить(
		"ОшибкиПроверкиСредствамиККТ",
		РегистрыСведений.РезультатыПроверкиСредствамиККТИСМП.ДанныеРезультатовПроверкиДляПроверкиИПодбора(
			Параметры.ПроверяемыйДокумент,
			Параметры.ВидМаркируемойПродукции));
	
	ПринудительноУстановитьДетализацию = Неопределено;
	Если Параметры.ЭтоВосстановлениеДетализации Тогда
		ПринудительноУстановитьДетализацию = Параметры.ДетализацияСтруктурыХранения;
	КонецЕсли;
	
	Если Параметры.ПроверкаНеПоДокументу Тогда
		
		ПараметрыСканирования = Параметры.ПараметрыСканирования;
		
		ПроверяемыеДанные = Параметры.ПроверяемыеДанные;
		
		ПараметрыСканирования.ИспользуетсяСоответствиеШтрихкодовСтрокДерева = Ложь; // Для нормализации вложенных штрихкодов
		РезультатПолученияДанных = ШтрихкодированиеМОТП.ВложенныеШтрихкодыУпаковок(
			ПроверяемыеДанные.МаркированныеТовары,
			ПараметрыСканирования.ДетализацияСтруктурыХранения,
			ПараметрыСканирования);
		
		Если РезультатПолученияДанных.ЕстьОшибки Тогда
			ВызватьИсключение РезультатПолученияДанных.ТекстОшибки;
		КонецЕсли;
		
		ЗаполнитьТаблицуПодобраннойТабачнойПродукции(ПроверяемыеДанные.ТаблицаМаркируемойПродукции, ДанныеДокумента);
		
		ДанныеПроверяемогоОбъекта = РезультатПолученияДанных.ВложенныеШтрихкоды;
		
		ДополнитьТаблицуПодобраннойТабачнойПродукции(
			ДанныеПроверяемогоОбъекта, ДанныеДокумента, ПараметрыСканирования, Параметры.ВидМаркируемойПродукции);
		
		ЗаполнитьДеревоМаркированнойПродукции(
			ДанныеПроверяемогоОбъекта.ДеревоУпаковок, ДанныеДокумента, ПринудительноУстановитьДетализацию);
		
	Иначе
		
		ТаблицаМаркируемойПродукции = ПроверкаИПодборПродукцииИСМП.ТаблицаМаркируемойПродукцииДокумента(
			Параметры.ПроверяемыйДокумент, Параметры.ВидМаркируемойПродукции);
		
		ЗаполнитьТаблицуПодобраннойТабачнойПродукции(ТаблицаМаркируемойПродукции, ДанныеДокумента);
		
		Если Параметры.Свойство("ДанныеКлючаСессииИСМП") Тогда
			ПараметрыСеанса.ДанныеКлючаСессииИСМП = Параметры.ДанныеКлючаСессииИСМП;
		КонецЕсли;
		
		Если Параметры.Свойство("ПараметрыЛогированияЗапросовИСМП") Тогда
			ЛогированиеЗапросовИСМП.УстановитьПараметрыЛогированияЗапросов(Параметры.ПараметрыЛогированияЗапросовИСМП);
			ЛогированиеЗапросовИСМП.НастроитьПараметрыЛогированияВФоновомЗадании();
		КонецЕсли;
		
		ПараметрыСканирования = Параметры.ПараметрыСканирования;
		Если Параметры.РежимПодбораСуществующихУпаковок Тогда
			
			Если Не Параметры.ЭтоВосстановлениеДетализации
				И Не ЗначениеЗаполнено(ПараметрыСканирования.ДетализацияСтруктурыХранения) Тогда
				
				ПараметрыСканирования.ДетализацияСтруктурыХранения = СохраненнаяДетализацияСтруктурыХранения;
				
				ШтрихкодыУпаковокПоДокументу = ШтрихкодированиеМОТП.ШтрихкодыУпаковокИзДокумента(
					Параметры.ПроверяемыйДокумент, ПараметрыСканирования);
				ПринудительноУстановитьДетализацию = ПроверкаИПодборПродукцииМОТП.ДетализацияНаОснованииСтатистикиПоШтрихкодам(
					ШтрихкодыУпаковокПоДокументу, ПараметрыСканирования, Параметры.ПроверяемыйДокумент, Параметры.ВидМаркируемойПродукции);
				
				ПараметрыСканирования.ДетализацияСтруктурыХранения = ПринудительноУстановитьДетализацию;
				ДанныеДокумента.ДетализацияСтруктурыХранения = ПринудительноУстановитьДетализацию;
				
			КонецЕсли;
			
			ПараметрыСканирования.ИспользуетсяСоответствиеШтрихкодовСтрокДерева = Ложь;  // Для нормализации вложенных штрихкодов
			РезультатПолученияДанных = ШтрихкодированиеМОТП.ВложенныеШтрихкодыУпаковокПоДокументу(
				Параметры.ПроверяемыйДокумент,
				ПараметрыСканирования.ДетализацияСтруктурыХранения,
				ПараметрыСканирования);
			
		Иначе
			
			ВосстановлениеДетализацииТребуетОбращенияКСервису = Ложь;
			
			ПараметрыСканирования.ДетализацияСтруктурыХранения = СохраненнаяДетализацияСтруктурыХранения;
			ДанныеДокумента.ДетализацияСтруктурыХранения = СохраненнаяДетализацияСтруктурыХранения;
			
			ДанныеШтрихкодовСписок = Новый Массив;
			
			Если Параметры.ПроверкаЭлектронногоДокумента Тогда
				
				ДанныеНоменклатурыПоДаннымУПД = ПроверкаИПодборПродукцииИСМП.ДанныеНоменклатурыПоДаннымУПД(
					ДанныеДокумента.ПроверяемыйДокумент, ДанныеДокумента.ВидМаркируемойПродукции);
				
				Для Каждого Строка Из ДанныеНоменклатурыПоДаннымУПД.СтрокиТекущейТоварнойГруппы Цикл
					ДанныеШтрихкодовСписок.Добавить(
						Новый Структура("Штрихкод, Количество", Строка.Штрихкод, 1));
				КонецЦикла;
				
				Если Не Параметры.ЭтоВосстановлениеДетализации Тогда
					ПринудительноУстановитьДетализацию = ПроверкаИПодборПродукцииМОТП.ДетализацияНаОснованииСтатистикиПоШтрихкодам(
						ДанныеНоменклатурыПоДаннымУПД.СтрокиТекущейТоварнойГруппы, ПараметрыСканирования, Параметры.ПроверяемыйДокумент, Параметры.ВидМаркируемойПродукции);
				Иначе
					ФактическаяДетализация = ПроверкаИПодборПродукцииМОТП.ДетализацияНаОснованииСтатистикиПоШтрихкодам(
						ДанныеНоменклатурыПоДаннымУПД.СтрокиТекущейТоварнойГруппы, ПараметрыСканирования, Параметры.ПроверяемыйДокумент, Параметры.ВидМаркируемойПродукции);
					ДетализацияТребующиеЗапросаКСервису = ПроверкаИПодборПродукцииМОТП.ДетализацияТребуетЗапросаКСервису(ФактическаяДетализация);
					Если ДетализацияТребующиеЗапросаКСервису.Найти(ПринудительноУстановитьДетализацию) <> Неопределено Тогда
						ВосстановлениеДетализацииТребуетОбращенияКСервису = Истина;
					КонецЕсли;
				КонецЕсли;
				
				ПараметрыСканирования.ДетализацияСтруктурыХранения = ПринудительноУстановитьДетализацию;
				ДанныеДокумента.ДетализацияСтруктурыХранения = ПринудительноУстановитьДетализацию;
				
				ПроверкаИПодборПродукцииИСМП.ПроверитьДанныеУПДНаНаличиеОшибок(ДанныеДокумента, ДанныеНоменклатурыПоДаннымУПД);
				
				ПараметрыСканирования.ИспользуетсяСоответствиеШтрихкодовСтрокДерева = Ложь; // Для вызова ШтрихкодированиеИС.ДобавитьВДанныеШтрихкодовВложенныеШтрихкоды
				
				ДляУпаковокТребоватьПодключениеМОТП = Истина;
				Если ДанныеДокумента.ВозможностьЗагрузкиДанныхБезПодключенияМОТП = Истина Тогда
					ДляУпаковокТребоватьПодключениеМОТП = Ложь;
				КонецЕсли;
				РезультатПолученияДанных = ШтрихкодированиеМОТП.ВложенныеШтрихкоды(
					ДанныеШтрихкодовСписок, ПараметрыСканирования, ДляУпаковокТребоватьПодключениеМОТП);
				
			Иначе
				
				ШтрихкодыУпаковокПоДокументу = ШтрихкодированиеМОТП.ШтрихкодыУпаковокИзДокумента(
					Параметры.ПроверяемыйДокумент, ПараметрыСканирования);
				
				Если Не Параметры.ЭтоВосстановлениеДетализации Тогда
					ПринудительноУстановитьДетализацию = ПроверкаИПодборПродукцииМОТП.ДетализацияНаОснованииСтатистикиПоШтрихкодам(
						ШтрихкодыУпаковокПоДокументу, ПараметрыСканирования, Параметры.ПроверяемыйДокумент, Параметры.ВидМаркируемойПродукции);
				Иначе
					ФактическаяДетализация = ПроверкаИПодборПродукцииМОТП.ДетализацияНаОснованииСтатистикиПоШтрихкодам(
						ШтрихкодыУпаковокПоДокументу, ПараметрыСканирования, Параметры.ПроверяемыйДокумент, Параметры.ВидМаркируемойПродукции);
					ДетализацияТребующиеЗапросаКСервису = ПроверкаИПодборПродукцииМОТП.ДетализацияТребуетЗапросаКСервису(ФактическаяДетализация);
					Если ДетализацияТребующиеЗапросаКСервису.Найти(ПринудительноУстановитьДетализацию) <> Неопределено Тогда
						ВосстановлениеДетализацииТребуетОбращенияКСервису = Истина;
					КонецЕсли;
				КонецЕсли;
				
				ПараметрыСканирования.ДетализацияСтруктурыХранения = ПринудительноУстановитьДетализацию;
				ДанныеДокумента.ДетализацияСтруктурыХранения = ПринудительноУстановитьДетализацию;
				
				ПараметрыСканирования.ОпределениеВидаПродукцииИСМП                  = Истина;
				ПараметрыСканирования.ИспользуетсяСоответствиеШтрихкодовСтрокДерева = Ложь; // Для нормализации вложенных штрихкодов
				РезультатПолученияДанных = ШтрихкодированиеМОТП.ВложенныеШтрихкодыУпаковокПоДокументу(
					Параметры.ПроверяемыйДокумент,
					ПараметрыСканирования.ДетализацияСтруктурыХранения,
					ПараметрыСканирования);
				
			КонецЕсли;
			
			Если Не РезультатПолученияДанных.ЕстьОшибки
				И Параметры.ПроверкаЭлектронногоДокумента
				И ПроверкаИПодборПродукцииИСМПКлиентСервер.ЭтоДокументПриобретения(Параметры.ПроверяемыйДокумент) Тогда
				
				Если ПрерватьОбработкуЗагрузкиДанных(
					ДанныеДокумента,
					ПараметрыСканирования,
					ДанныеНоменклатурыПоДаннымУПД,
					РезультатПолученияДанных.ВложенныеШтрихкоды.ДеревоУпаковок,
					ВосстановлениеДетализацииТребуетОбращенияКСервису) Тогда
				
					ЛогированиеЗапросовИСМП.ЗаполнитьВозвращаемыеДанныеФоновогоЗадания(ДанныеДокумента);
					ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, 0);
					ПоместитьВоВременноеХранилище(ДанныеДокумента, АдресРезультата);
					
					Возврат;
				
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если РезультатПолученияДанных.ЕстьОшибки Тогда
			ВызватьИсключение РезультатПолученияДанных.ТекстОшибки;
		КонецЕсли;
		
		ЗаполнитьТаблицуПродукцияПоДокументу(ДанныеДокумента, Параметры.ВидМаркируемойПродукции);
		
		ДанныеПроверяемогоДокумента = РезультатПолученияДанных.ВложенныеШтрихкоды;
		
		ДополнитьТаблицуПодобраннойТабачнойПродукции(
			ДанныеПроверяемогоДокумента, ДанныеДокумента, Параметры.ПараметрыСканирования, Параметры.ВидМаркируемойПродукции);
		ЗаполнитьДеревоМаркированнойПродукции(
			ДанныеПроверяемогоДокумента.ДеревоУпаковок, ДанныеДокумента, ПринудительноУстановитьДетализацию);
		
	КонецЕсли;
	
	КоличествоДанных = ИнтеграцияИС.КоличествоСтрокДереваЗначений(ДанныеДокумента.ДеревоМаркированнойПродукции)
		+ ДанныеДокумента.ПодобраннаяМаркируемаяПродукция.Количество();
	
	ЛогированиеЗапросовИСМП.ЗаполнитьВозвращаемыеДанныеФоновогоЗадания(ДанныеДокумента);
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоДанных);
	
	ПоместитьВоВременноеХранилище(ДанныеДокумента, АдресРезультата);
	
КонецПроцедуры

// Фоновая операция по фиксации результатов проверки и подбора в документе, 
// 
// Параметры:
// 	Параметры - Структура - содержит следующие значения:
// 	   * СоздаватьАктОРасхождениях    - Булево - признак необходимости создания акта о расхождениях.
// 	   * ПроверяемыйДокумент          - ДокументСсылка - документ, для которого выполнялась проверка и подбор.
// 	   * ДеревоМаркированнойПродукции - ДеревоЗначений - содержит результаты проверки и подбора с иерархией упаковок.
// 	   * ДанныеПроверкиИПодбора       - ХранилищеЗначения - содержит слепок состояния проверки и подбора, если предусмотрено его хранение по окончании результатов проверки.
// 	   * ПроверкаНеПоДокументу        - Булево - признак того, что проверка и подбор была вызвана из произвольной формы.
// 	   * СохранятьОписаниеGTIN        - Булево - Сохранять описание GTIN, EAN, коэффициенты групповых упаковок.
// 	   * ПодобраннаяМаркируемаяПродукция - ТаблицаЗначений - содержит информацию о подобранной продукции сгруппированную до 
// 	       номенклатуры, характеристики, серии.
// 	АдресРезультата - Строка - адрес временного хранилища, в которое будут помещены результаты выполнения
Процедура ЗафиксироватьРезультатПроверкиИПодбора(Параметры, АдресРезультата) Экспорт
	
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"Обработка.ПроверкаИПодборТабачнойПродукцииМОТП.МодульМенеджера.ЗафиксироватьРезультатПроверкиИПодбора");

	НачатьТранзакцию();
	
	Попытка
		
		ПараметрыОкончанияПроверки = ПроверкаИПодборПродукцииИСМП.ПараметрыОкончанияПроверки();
		ПараметрыОкончанияПроверки.ВидПродукцииИС            = Параметры.ВидМаркируемойПродукции;
		ПараметрыОкончанияПроверки.СоздаватьАктОРасхождениях = Параметры.СоздаватьАктОРасхождениях;
		ПараметрыОкончанияПроверки.ПроверяемыйДокумент       = Параметры.ПроверяемыйДокумент;
		
		ДеревоМаркированнойПродукции = Параметры.ДеревоМаркированнойПродукции;
		ДеревоМаркированнойПродукции.Колонки.Добавить("ШтрихкодУпаковки", Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
		ДеревоМаркированнойПродукции.Колонки["КоличествоПодчиненныхПачек"].Имя = "КоличествоПодчиненнойПродукции";
		Если Не Параметры.ПроверкаНеПоДокументу и Параметры.СоздаватьАктОРасхождениях Тогда
			ШтрихкодыБрак = ШтрихкодированиеИС.ЗначенияНедопустимыхШтрихкодовИзДереваМаркируемойПродукции(
					ДеревоМаркированнойПродукции);
		КонецЕсли;
		ПроверкаИПодборПродукцииИСМП.УдалитьПустыеУпаковкиДерева(ДеревоМаркированнойПродукции);
		
		ТаблицаШтрихкодовВерхнегоУровня        = ПроверкаИПодборПродукцииИСМП.ПустаяТаблицаШтрихкодовВерхнегоУровня();
		ТаблицаПодобраннойПровереннойПродукции = ПроверкаИПодборПродукцииИСМП.ПустаяТаблицаПодобраннойПровереннойПродукции();
		
		ТаблицаПодобраннойПровереннойПродукции.Колонки.Добавить("МРЦ", ОбщегоНазначения.ОписаниеТипаЧисло(10, 2));
		
		Справочники.ШтрихкодыУпаковокТоваров.РезультатСозданияШтрихкодовУпаковокПоДеревуМаркируемойПродукции(
			ДеревоМаркированнойПродукции, ТаблицаШтрихкодовВерхнегоУровня,
			Неопределено, Неопределено,
			Параметры.ВидМаркируемойПродукции);
		ПараметрыОкончанияПроверки.ТаблицаШтрихкодовВерхнегоУровня = ТаблицаШтрихкодовВерхнегоУровня;
		
		ЕстьРасхождения = Ложь;
		
		Для Каждого СтрокаДерева Из Параметры.ПодобраннаяМаркируемаяПродукция Цикл
		
			НоваяСтрока = ТаблицаПодобраннойПровереннойПродукции.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДерева);
			Если Не ЗначениеЗаполнено(НоваяСтрока.Характеристика) Тогда
				НоваяСтрока.Характеристика = ИнтеграцияИС.ПустоеЗначениеОпределяемогоТипа("ХарактеристикаНоменклатуры"); 
			КонецЕсли;
				
			Если Не ЗначениеЗаполнено(НоваяСтрока.Серия) Тогда
				НоваяСтрока.Серия = ИнтеграцияИС.ПустоеЗначениеОпределяемогоТипа("СерияНоменклатуры"); 
			КонецЕсли;
			
			Если НоваяСтрока.Количество <> НоваяСтрока.КоличествоПодобрано Тогда
				
				ЕстьРасхождения = Истина;
				
			КонецЕсли;
		
		КонецЦикла;
		
		ПараметрыОкончанияПроверки.ТаблицаПодобраннойПровереннойПродукции = ТаблицаПодобраннойПровереннойПродукции;
		
		Если Не Параметры.ПроверкаНеПоДокументу Тогда
			
			Если Параметры.СоздаватьАктОРасхождениях Тогда
				
				ПараметрыОкончанияПроверки.ТаблицаРасхожденийШтриховыхКодов = ТаблицаРасхожденийУпаковокПоРезультатамПроверкиИПодбора(Параметры);
				
				Если Параметры.Свойство("ДанныеКлючаСессииИСМП") Тогда
					ПараметрыСеанса.ДанныеКлючаСессииИСМП = Параметры.ДанныеКлючаСессииИСМП;
				КонецЕсли;
				
				ПараметрыСканирования = ШтрихкодированиеИС.ПараметрыСканирования(Параметры.ПроверяемыйДокумент, Неопределено, ПараметрыОкончанияПроверки.ВидПродукцииИС);
				ПараметрыСканирования.ЗапрашиватьДанныеСервисаИСМП = Ложь;
				ПараметрыСканирования.СопоставлятьНоменклатуру     = Ложь;
				ПараметрыСканирования.СсылкаНаОбъект               = Параметры.ПроверяемыйДокумент;
				ПараметрыСканирования.ДетализацияСтруктурыХранения = Параметры.ДетализацияСтруктурыХранения;
				РезультатПоискаПоДаннымДокумента = ШтрихкодированиеИС.ВложенныеШтрихкодыУпаковок(
					ТаблицаШтрихкодовВерхнегоУровня.ВыгрузитьКолонку("ШтрихкодУпаковки"),
					ПараметрыСканирования);
				ДеревоУпаковокПоДаннымДокумента = РезультатПоискаПоДаннымДокумента.ДеревоУпаковок;
				
				// Заполнение по данным ИСМП
				ДанныеНоменклатурыПоДаннымУПД = ШтрихкодированиеИСМПСлужебный.ДанныеМаркируемойНоменклатурыПоДаннымУПД(Параметры.ПроверяемыйДокумент);
				
				ДанныеШтрихкодовСписок = Новый Массив;
				Для Каждого КлючИЗначение Из ДанныеНоменклатурыПоДаннымУПД Цикл
					Штрихкод = КлючИЗначение.Ключ;
					Если ЗначениеЗаполнено(Штрихкод) Тогда
						ДанныеШтрихкодовСписок.Добавить(
							Новый Структура("Штрихкод, Количество", Штрихкод, 1));
					КонецЕсли;
				КонецЦикла;
				
				ПараметрыСканирования.ДопустимыеВидыПродукции = ИнтеграцияИСКлиентСервер.ВидыПродукцииИСМП(Истина);
				ПараметрыСканирования.ЗапрашиватьДанныеНеизвестныхУпаковокИСМП = Истина;
				РезультатПолученияДереваИСМП = ШтрихкодированиеМОТП.ВложенныеШтрихкоды(ДанныеШтрихкодовСписок, ПараметрыСканирования);
				Если РезультатПолученияДереваИСМП.ЕстьОшибки Тогда
					ВызватьИсключение РезультатПолученияДереваИСМП.ТекстОшибки;
				КонецЕсли;
				ДеревоУпаковокПоДаннымИСМП = РезультатПолученияДереваИСМП.ВложенныеШтрихкоды.ДеревоУпаковок;
				
				ПараметрыСверки = СверкаКодовМаркировкиИСМП.ИнициализироватьПараметрыСверки();
				ПараметрыСверки.ЭтоПродукцияМОТП = Истина;
				ПараметрыСверки.СохраненнаяДетализацияСтруктурыХранения = Параметры.ДетализацияСтруктурыХранения;
				
				ТаблицыТОРГ2 = СверкаКодовМаркировкиИСМП.ТаблицыРасхожденийПоДаннымЭДО(
					ДеревоУпаковокПоДаннымДокумента,
					ДеревоУпаковокПоДаннымИСМП,
					ШтрихкодыБрак,
					ПараметрыСверки);
				
				ДанныеПроверкиИПодбора = Параметры.ДанныеПроверкиИПодбора.Получить();
				ДанныеПроверкиИПодбора.Вставить("ТаблицыТОРГ2", ТаблицыТОРГ2);
				Параметры.ДанныеПроверкиИПодбора = Новый ХранилищеЗначения(ДанныеПроверкиИПодбора);
				
				ПараметрыОкончанияПроверки.ШтрихкодыУпаковокПланЭДО = ТаблицыТОРГ2.ШтрихкодыУпаковокПланЭДО;
				ПараметрыОкончанияПроверки.ШтрихкодыУпаковокФактЭДО = ТаблицыТОРГ2.ШтрихкодыУпаковокФактЭДО;

			КонецЕсли;
			
			Если Параметры.СохранятьОписаниеGTIN Тогда
				
				ТаблицаОписанияGTIN                            = ПроверкаИПодборПродукцииИСМП.ПустаяТаблицаОписанияGTIN();
				ПараметрыОкончанияПроверки.ТаблицаОписанияGTIN = ТаблицаОписанияGTIN;
				
				ПроверкаИПодборПродукцииИСМП.ЗаполнитьТаблицуОписанияGTINПоДеревуУпаковок(
					ТаблицаОписанияGTIN,
					Параметры.ДеревоМаркированнойПродукции);
				
			КонецЕсли;
			
			ПараметрыОкончанияПроверки.ДеревоМаркированнойПродукции = ДеревоМаркированнойПродукции;
			ПроверкаИПодборПродукцииИСМП.ЗафиксироватьРезультатПроверкиИПодбора(ПараметрыОкончанияПроверки);
			
			Если Параметры.СоздаватьАктОРасхождениях
				И ЗначениеЗаполнено(ПараметрыОкончанияПроверки.СозданныйАктОРасхождениях) Тогда
				
				РегистрыСведений.СтатусыПроверкиИПодбораДокументовИСМП.ОтразитьЗавершениеПроверкиДокумента(
					ПараметрыОкончанияПроверки.СозданныйАктОРасхождениях,
					Параметры.ВидМаркируемойПродукции,
					Параметры.ДанныеПроверкиИПодбора);
					
				РегистрыСведений.СтатусыПроверкиИПодбораДокументовИСМП.ОтразитьЗавершениеПроверкиДокумента(
					Параметры.ПроверяемыйДокумент,
					Параметры.ВидМаркируемойПродукции,
					Неопределено,
					Перечисления.ТребуемоеДействиеДокументЭДО.Подтвердить);
				
			Иначе
				
				Если ЕстьРасхождения Тогда
					ТребуемоеДействие = Перечисления.ТребуемоеДействиеДокументЭДО.Отклонить;
				Иначе
					ТребуемоеДействие = Перечисления.ТребуемоеДействиеДокументЭДО.Подтвердить;
				КонецЕсли;
				
				РегистрыСведений.СтатусыПроверкиИПодбораДокументовИСМП.ОтразитьЗавершениеПроверкиДокумента(
					Параметры.ПроверяемыйДокумент,
					Параметры.ВидМаркируемойПродукции,
					Параметры.ДанныеПроверкиИПодбора,
					ТребуемоеДействие);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Информация = ИнформацияОбОшибке();
		
		ТекстСообщения = НСтр("ru = 'Произошла ошибка при сохранении результатов проверки.'");
		
		ИнтеграцияИСМПСлужебный.ЗаписатьОшибкуВЖурналРегистрации(
			ТекстСообщения + Символы.ПС + ПодробноеПредставлениеОшибки(Информация),
			Метаданные.Обработки.ПроверкаИПодборТабачнойПродукцииМОТП);
			
		ТекстОшибки = ТекстСообщения + Символы.ПС
			+ КраткоеПредставлениеОшибки(Информация) + Символы.ПС
			+ НСтр("ru = 'Подробности см. в журнале регистрации.'");
		
		ВызватьИсключение ТекстОшибки;
		
	КонецПопытки;
	
	КоличествоДанных = ИнтеграцияИС.КоличествоСтрокДереваЗначений(ДеревоМаркированнойПродукции);
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоДанных);
	
	РезультатПроверки = Новый Структура();
	
	Если Параметры.ПроверкаНеПоДокументу Тогда
		РезультатПроверки.Вставить("ТаблицаШтрихкодовВерхнегоУровня", ТаблицаШтрихкодовВерхнегоУровня);
		РезультатПроверки.Вставить("ТаблицаПодобраннойПровереннойПродукции", ТаблицаПодобраннойПровереннойПродукции);
	Иначе
		РезультатПроверки.Вставить("СозданныйАктОРасхождениях", ПараметрыОкончанияПроверки.СозданныйАктОРасхождениях);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(РезультатПроверки, АдресРезультата);
	
КонецПроцедуры

// Возвращает сохраненную в хранилище настроек данных форм детализацию отображения структуры упаковок в форме проверки и подбора табачной продукции.
// 
// Параметры:
// 	РежимПодбораСуществующихУпаковок - Булево - признак работы со штрихкодами упаковок, имеющимися в информационной базе.
// Возвращаемое значение:
// 	ПеречислениеСсылка.ДетализацияСтруктурыХраненияИС - сохраненная детализация отображения структуры упаковок
Функция СохраненнаяДетализацияСтруктурыХранения(РежимПодбораСуществующихУпаковок) Экспорт
	
	СохраненнаяДетализация = Неопределено;
	СохраненныеНастройки   = ОбщегоНазначения.ХранилищеНастроекДанныхФормЗагрузить("Обработка.ПроверкаИПодборТабачнойПродукцииМОТП.Форма.ПроверкаИПодбор", "");
	
	Если ТипЗнч(СохраненныеНастройки) = Тип("Структура") Тогда
		Если РежимПодбораСуществующихУпаковок
			И СохраненныеНастройки.Свойство("ДетализацияСтруктурыХранения") Тогда
			СохраненнаяДетализация = СохраненныеНастройки.ДетализацияСтруктурыХранения;
		ИначеЕсли Не РежимПодбораСуществующихУпаковок
			И СохраненныеНастройки.Свойство("ДетализацияСтруктурыХраненияПриобретение") Тогда
			СохраненнаяДетализация = СохраненныеНастройки.ДетализацияСтруктурыХраненияПриобретение;
		ИначеЕсли СохраненныеНастройки.Свойство("ДетализацияСтруктурыХранения") Тогда
			СохраненнаяДетализация = СохраненныеНастройки.ДетализацияСтруктурыХранения;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДетализацияСтруктурыХраненияИС(СохраненнаяДетализация);
	
КонецФункции

// Выполняет конвертацию детализации структуры хранения
// 
// Параметры:
// 	ДетализацияСтруктурыХранения - ПеречислениеСсылка.ДетализацияСтруктурыХраненияТабачнойПродукцииМОТП,
// 	                               ПеречислениеСсылка.ДетализацияСтруктурыХраненияИС - Старая детализация
// Возвращаемое значение:
// 	ПеречислениеСсылка.ДетализацияСтруктурыХраненияИС - Новая детализация
Функция ДетализацияСтруктурыХраненияИС(ДетализацияСтруктурыХранения) Экспорт
	
	Если ТипЗнч(ДетализацияСтруктурыХранения) = Тип("ПеречислениеСсылка.ДетализацияСтруктурыХраненияИС") Тогда
		Возврат ДетализацияСтруктурыХранения;
	КонецЕсли;
	
	НоваяДетализация = Неопределено;
	
	Если ДетализацияСтруктурыХранения = Перечисления.ДетализацияСтруктурыХраненияТабачнойПродукцииМОТП.Полная Тогда
		НоваяДетализация = Перечисления.ДетализацияСтруктурыХраненияИС.Полная;
	ИначеЕсли ДетализацияСтруктурыХранения = Перечисления.ДетализацияСтруктурыХраненияТабачнойПродукцииМОТП.БлокиСПачками Тогда
		НоваяДетализация =  Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими;
	ИначеЕсли ДетализацияСтруктурыХранения = Перечисления.ДетализацияСтруктурыХраненияТабачнойПродукцииМОТП.Коробки Тогда
		НоваяДетализация =  Перечисления.ДетализацияСтруктурыХраненияИС.ПалетыСМонотоварнымиКоробами;
	ИначеЕсли ДетализацияСтруктурыХранения = Перечисления.ДетализацияСтруктурыХраненияТабачнойПродукцииМОТП.КоробкиСБлоками Тогда
		НоваяДетализация =  Перечисления.ДетализацияСтруктурыХраненияИС.КоробаСГрупповымиУпаковками;
	ИначеЕсли ДетализацияСтруктурыХранения = Перечисления.ДетализацияСтруктурыХраненияТабачнойПродукцииМОТП.Пачки Тогда
		НоваяДетализация =  Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки;
	КонецЕсли;
	
	Возврат НоваяДетализация;
	
КонецФункции

// Возвращает детализацию переданной иерархической структуры упаковок табачной продукции.
// 
// Параметры:
// 	ДеревоУпаковок - ДеревоЗначений, ДанныеФормыКоллекция - иерархическая структура упаковок табачной продукции.
// Возвращаемое значение:
//	ПеречислениеСсылка.ДетализацияСтруктурыХраненияИС - расчитаная детализация структуры упаковок.
Функция ДетализацияСтруктурыХраненияДерева(ДеревоУпаковок) Экспорт
	
	УпаковкиДерева = Новый Структура;
	УпаковкиДерева.Вставить("ЕстьПачки",           Ложь);
	УпаковкиДерева.Вставить("ЕстьБлоки",           Ложь);
	УпаковкиДерева.Вставить("ЕстьКоробки",         Ложь);
	УпаковкиДерева.Вставить("ЕстьПалеты",          Ложь);
	УпаковкиДерева.Вставить("ЕстьПачкиБезБлока",   Ложь);
	УпаковкиДерева.Вставить("ЕстьБлокиБезКоробки", Ложь);
	
	РазрешеныПачкиВКоробках = Истина;
	
	Если ТипЗнч(ДеревоУпаковок) = Тип("ДеревоЗначений") Тогда
		СтрокиДерева = ДеревоУпаковок.Строки;
	Иначе
		СтрокиДерева = Новый Массив;
		СтрокиДерева.Добавить(ДеревоУпаковок);
	КонецЕсли;
	
	ОпределитьНаличиеУпаковокВСтрокахДерева(СтрокиДерева, УпаковкиДерева, РазрешеныПачкиВКоробках);
	
	Если УпаковкиДерева.ЕстьКоробки Тогда
		Если УпаковкиДерева.ЕстьБлоки И УпаковкиДерева.ЕстьПачки Тогда
			ДетализацияСтруктурыХраненияДерева = Перечисления.ДетализацияСтруктурыХраненияИС.Полная
		ИначеЕсли УпаковкиДерева.ЕстьБлоки Тогда
			ДетализацияСтруктурыХраненияДерева = Перечисления.ДетализацияСтруктурыХраненияИС.КоробаСГрупповымиУпаковками;
		Иначе
			ДетализацияСтруктурыХраненияДерева = Перечисления.ДетализацияСтруктурыХраненияИС.Полная;
		КонецЕсли;
	ИначеЕсли УпаковкиДерева.ЕстьБлоки Тогда
		ДетализацияСтруктурыХраненияДерева = Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими;
	ИначеЕсли УпаковкиДерева.ЕстьПачки Тогда
		ДетализацияСтруктурыХраненияДерева = Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки;
	ИначеЕсли УпаковкиДерева.ЕстьБлокиБезКоробки Тогда
		ДетализацияСтруктурыХраненияДерева = Перечисления.ДетализацияСтруктурыХраненияИС.КоробаСГрупповымиУпаковками;
	ИначеЕсли УпаковкиДерева.ЕстьПачкиБезБлока Тогда
		ДетализацияСтруктурыХраненияДерева = Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими;
	Иначе
		ДетализацияСтруктурыХраненияДерева = Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки;
	КонецЕсли;
	
	Возврат ДетализацияСтруктурыХраненияДерева;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТаблицаРасхожденийУпаковокПоРезультатамПроверкиИПодбора(Параметры)
	
	ТаблицаРасхождений = Новый ТаблицаЗначений;
	ТаблицаРасхождений.Колонки.Добавить("ШтрихкодУпаковки", Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	ТаблицаРасхождений.Колонки.Добавить("Штрихкод",         ОбщегоНазначения.ОписаниеТипаСтрока(200));
	ТаблицаРасхождений.Колонки.Добавить("Номенклатура",     Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
	ТаблицаРасхождений.Колонки.Добавить("Характеристика",   Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
	ТаблицаРасхождений.Колонки.Добавить("Серия",            Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип);
	ТаблицаРасхождений.Колонки.Добавить("ЭтоИзлишек",       Новый ОписаниеТипов("Булево"));
	
	Для Каждого СтрокаДерева Из Параметры.ДеревоМаркированнойПродукции.Строки Цикл
		
		ЗаполнитьТаблицуРасхожденийПоСтрокеДерева(СтрокаДерева, ТаблицаРасхождений);
		
	КонецЦикла;
	
	Возврат ТаблицаРасхождений;
	
КонецФункции

Процедура ЗаполнитьТаблицуРасхожденийПоСтрокеДерева(СтрокаДерева, ТаблицаРасхождений)
	
	ЭтоИзлишек = (СтрокаДерева.СтатусПроверки = Перечисления.СтатусыПроверкиНаличияПродукцииИС.НеЧислилась
	             Или СтрокаДерева.СтатусПроверки = СтрокаДерева.НеСодержитсяВДанныхДокумента);
	
	ЭтоНедостача = СтрокаДерева.СтатусПроверки = Перечисления.СтатусыПроверкиНаличияПродукцииИС.Отсутствует;
	
	ЭтоТипУпаковкиПоКоторомуФиксируютсяРасхождения = (СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МонотоварнаяУпаковка
	                                                  Или СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар);
	
	ВозможнаПроверкаПодчиненныхСтрок = (СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МонотоварнаяУпаковка
	                                   Или СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МультитоварнаяУпаковка
	                                   Или СтрокаДерева.ТипУпаковки = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.БлокиБезКоробки
	                                   Или СтрокаДерева.ТипУпаковки = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПачкиБезБлока); 
	
	Если ЭтоТипУпаковкиПоКоторомуФиксируютсяРасхождения
		И (ЭтоИзлишек Или ЭтоНедостача) Тогда
		
		НоваяСтрокаТаблицы = ТаблицаРасхождений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицы, СтрокаДерева); 	
		НоваяСтрокаТаблицы.ЭтоИзлишек = ЭтоИзлишек;
		
		Возврат;
			
	КонецЕсли; 
	
	Если ВозможнаПроверкаПодчиненныхСтрок Тогда
		
		Для Каждого ПодчиненнаяСтрока Из СтрокаДерева.Строки Цикл
		
			ЗаполнитьТаблицуРасхожденийПоСтрокеДерева(ПодчиненнаяСтрока, ТаблицаРасхождений);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьНаличиеУпаковокВСтрокахДерева(СтрокиДерева, УпаковкиДерева, РазрешеныПачкиВКоробках)
	
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Если УпаковкиДерева.ЕстьПачки И Не РазрешеныПачкиВКоробках Тогда
			Прервать;
		ИначеЕсли СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
			Если СтрокаДерева.Родитель <> Неопределено
				И СтрокаДерева.Родитель.ТипУпаковки = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПачкиБезБлока Тогда
				УпаковкиДерева.ЕстьПачкиБезБлока = Истина;
			Иначе
				УпаковкиДерева.ЕстьПачки = Истина;
				Если Не РазрешеныПачкиВКоробках Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая Тогда
			Если СтрокаДерева.Родитель <> Неопределено
				И СтрокаДерева.Родитель.ТипУпаковки = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.БлокиБезКоробки Тогда
				УпаковкиДерева.ЕстьБлокиБезКоробки = Истина;
			Иначе
				УпаковкиДерева.ЕстьБлоки = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если Не УпаковкиДерева.ЕстьКоробки Тогда
			УпаковкиДерева.ЕстьКоробки = (СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая);
		КонецЕсли;
		
		Если СтрокаДерева.Строки.Количество() > 0 Тогда
			ОпределитьНаличиеУпаковокВСтрокахДерева(СтрокаДерева.Строки, УпаковкиДерева, РазрешеныПачкиВКоробках);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуПодобраннойТабачнойПродукции(ТаблицаМаркируемойПродукции, ДанныеДокумента)
	
	Для Каждого СтрокаИсточника Из ТаблицаМаркируемойПродукции Цикл
		СтрокаПодобраннойПродукции = ДанныеДокумента.ПодобраннаяМаркируемаяПродукция.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПодобраннойПродукции, СтрокаИсточника);
		СтрокаПодобраннойПродукции.НоменклатураСопоставлена = Истина;
		
		ПроверкаИПодборПродукцииИСМПКлиентСервер.УстановитьИндексКартинкиСостояниеПодбораКодаМаркировки(СтрокаПодобраннойПродукции);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьТаблицуПодобраннойТабачнойПродукции(ДанныеПроверяемогоДокумента, ДанныеДокумента, ПараметрыСканирования, ВидПродукции)
	
	ТаблицаМаркируемойПродукции = ДанныеПроверяемогоДокумента.МаркированныеТовары.СкопироватьКолонки();
	
	ДополнитьТаблицуМаркируемойПродукции(ДанныеПроверяемогоДокумента.ДеревоУпаковок, ТаблицаМаркируемойПродукции);
	
	ПроверкаИПодборПродукцииИСМП.ДополнитьТаблицуПодобраннойМаркируемойПродукцииМОТП(
		ТаблицаМаркируемойПродукции, ДанныеДокумента, ВидПродукции, ПараметрыСканирования);
	
КонецПроцедуры

Процедура ЗаполнитьДеревоМаркированнойПродукции(ДеревоУпаковокДокумента, ДанныеДокумента, ПринудительноУстановитьДетализацию)
	
	Если ПринудительноУстановитьДетализацию = Неопределено Тогда
		
		ДетализацияСтруктурыХраненияДанныхДокумента = ДетализацияСтруктурыХраненияДерева(ДеревоУпаковокДокумента);
		Если ДанныеДокумента.СохраненнаяДетализацияСтруктурыХранения <> Неопределено Тогда
			
			Если ДеревоУпаковокДокумента.Строки.Количество() = 0 Тогда
				ДанныеДокумента.ДетализацияСтруктурыХранения = ДанныеДокумента.СохраненнаяДетализацияСтруктурыХранения;
			ИначеЕсли ДетализацияСтруктурыХраненияДанныхДокумента = Перечисления.ДетализацияСтруктурыХраненияИС.Полная
				И ДанныеДокумента.СохраненнаяДетализацияСтруктурыХранения = Перечисления.ДетализацияСтруктурыХраненияИС.КоробаСГрупповымиУпаковками Тогда
				ДанныеДокумента.ДетализацияСтруктурыХранения = ДанныеДокумента.СохраненнаяДетализацияСтруктурыХранения;
			ИначеЕсли ДетализацияСтруктурыХраненияДанныхДокумента <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки Тогда
				ДанныеДокумента.ДетализацияСтруктурыХранения = ДетализацияСтруктурыХраненияДанныхДокумента;
			Иначе
				ДанныеДокумента.ДетализацияСтруктурыХранения = ДанныеДокумента.СохраненнаяДетализацияСтруктурыХранения;
			КонецЕсли;
			
		Иначе
			
			ДанныеДокумента.ДетализацияСтруктурыХранения = ДетализацияСтруктурыХраненияДерева(ДеревоУпаковокДокумента);
			
		КонецЕсли;
		
	Иначе
		
		ДанныеДокумента.ДетализацияСтруктурыХранения = ПринудительноУстановитьДетализацию;
		
	КонецЕсли;
	
	КоллекцияСтрокПриемника = ДанныеДокумента.ДеревоМаркированнойПродукции.Строки;
	
	Для Каждого СтрокаДереваУпаковок Из ДеревоУпаковокДокумента.Строки Цикл
		ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузке(СтрокаДереваУпаковок, КоллекцияСтрокПриемника, ДанныеДокумента);
	КонецЦикла;
	
	Если ДанныеДокумента.СтрокаПачкиБезБлока = Неопределено
		И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки Тогда
		ДобавленнаяСтрокаПачкиБезБлока(ДанныеДокумента);
	КонецЕсли;
	
	Если ДанныеДокумента.СтрокаБлокиБезКоробки = Неопределено
		И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки
		И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими Тогда
		ДобавленнаяСтрокаБлокиБезКоробки(ДанныеДокумента);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузке(СтрокаИсточника, КоллекцияСтрокПриемника, ДанныеДокумента)
	
	Если СтрокаИсточника.ТипУпаковки = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПачкиБезБлока Тогда
		
		Если ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки Тогда
			
			НоваяСтрока = ДанныеДокумента.СтрокаПачкиБезБлока;
			
			Если НоваяСтрока = Неопределено Тогда
				НоваяСтрока = ДобавленнаяСтрокаПачкиБезБлока(ДанныеДокумента);
			КонецЕсли;
			
		Иначе
			
			Для Каждого СтрокаИсточникаПачка Из СтрокаИсточника.Строки Цикл
				ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузке(СтрокаИсточникаПачка, КоллекцияСтрокПриемника, ДанныеДокумента);
			КонецЦикла;
			
			Возврат;
			
		КонецЕсли;
	
	ИначеЕсли СтрокаИсточника.ТипУпаковки = Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.БлокиБезКоробки Тогда
		
		Если ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки
			И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими Тогда
			
			НоваяСтрока = ДанныеДокумента.СтрокаБлокиБезКоробки;
			
			Если НоваяСтрока = Неопределено Тогда
				НоваяСтрока = ДобавленнаяСтрокаБлокиБезКоробки(ДанныеДокумента);
			КонецЕсли;
			
		Иначе

			Для Каждого СтрокаИсточникаБлок Из СтрокаИсточника.Строки Цикл
				Если ДанныеДокумента.ДетализацияСтруктурыХранения = Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими Тогда
					ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузке(СтрокаИсточникаБлок, КоллекцияСтрокПриемника, ДанныеДокумента);
				Иначе
					Для Каждого СтрокаИсточникаПачка Из СтрокаИсточникаБлок.Строки Цикл
						ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузке(СтрокаИсточникаПачка, КоллекцияСтрокПриемника, ДанныеДокумента);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			
			Возврат;

		КонецЕсли;
		
	ИначеЕсли СтрокаИсточника.Родитель = Неопределено
		И СтрокаИсточника.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар
		И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки Тогда
			
		СтрокаПачкиБезБлока = ДанныеДокумента.СтрокаПачкиБезБлока;
		
		Если СтрокаПачкиБезБлока = Неопределено Тогда
			СтрокаПачкиБезБлока = ДобавленнаяСтрокаПачкиБезБлока(ДанныеДокумента);
		КонецЕсли;
		
		НоваяСтрока = СтрокаПачкиБезБлока.Строки.Добавить();
		НоваяСтрока.СтатусПроверки = ДанныеДокумента.НачальныйСтатусПроверки;
		
	ИначеЕсли СтрокаИсточника.Родитель = Неопределено
		И ПроверкаИПодборПродукцииМОТПКлиентСервер.ЭтоУпаковкаБлока(СтрокаИсточника)
		И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки
		И ДанныеДокумента.ДетализацияСтруктурыХранения <> Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими Тогда
			
		СтрокаБлокиБезКоробки = ДанныеДокумента.СтрокаБлокиБезКоробки;
		
		Если СтрокаБлокиБезКоробки = Неопределено Тогда
			СтрокаБлокиБезКоробки = ДобавленнаяСтрокаБлокиБезКоробки(ДанныеДокумента);
		КонецЕсли;
		
		НоваяСтрока = СтрокаБлокиБезКоробки.Строки.Добавить();
		НоваяСтрока.СтатусПроверки = ДанныеДокумента.НачальныйСтатусПроверки;
		
	ИначеЕсли СтрокаИсточника.Родитель <> Неопределено
		И ПроверкаИПодборПродукцииМОТПКлиентСервер.ЭтоУпаковкаБлока(СтрокаИсточника.Родитель)
		И СтрокаИсточника.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар
		И ДанныеДокумента.ДетализацияСтруктурыХранения = Перечисления.ДетализацияСтруктурыХраненияИС.КоробаСГрупповымиУпаковками Тогда
		
		Возврат;
		
	Иначе
		
		НоваяСтрока = КоллекцияСтрокПриемника.Добавить();
		НоваяСтрока.СтатусПроверки = ДанныеДокумента.НачальныйСтатусПроверки;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаИсточника);
	
	НоваяСтрока.СтатусКодаМаркировки = СтрокаИсточника.Статус;
	
	ОшибкаПроверкиККТ = ДанныеДокумента.ОшибкиПроверкиСредствамиККТ.Получить(НоваяСтрока.Штрихкод);
	Если ОшибкаПроверкиККТ <> Неопределено Тогда
		НоваяСтрока.ТекстОшибкиПроверкиСредствамиККТ = ОшибкаПроверкиККТ.ОписаниеОшибок;
		Если ОшибкаПроверкиККТ.ТребуетсяПолныйКодМаркировки Тогда
			НоваяСтрока.СостояниеТребованияПолногоКодаККТ = 1;
		КонецЕсли;
	КонецЕсли;
	
	ПроверкаИПодборПродукцииИСМПКлиентСервер.ЗаполнитьСвойствоВключаетМРЦ(
		ДанныеДокумента.ВидМаркируемойПродукции, НоваяСтрока, СтрокаИсточника);
	
	Если НоваяСтрока.ТипУпаковки = Перечисления.ТипыУпаковок.ПустаяСсылка() Тогда
		НоваяСтрока.ТипУпаковки = Перечисления.ТипыУпаковок.МонотоварнаяУпаковка;
	ИначеЕсли НоваяСтрока.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар
		И НЕ ЗначениеЗаполнено(НоваяСтрока.Номенклатура) Тогда
		НоваяСтрока.ПредставлениеСодержимоеУпаковки = СтрокаИсточника.ПредставлениеНоменклатуры;
		НоваяСтрока.ПредставлениеСодержимогоДоСопоставления = СтрокаИсточника.ПредставлениеНоменклатуры;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеДокумента.ПроверяемыйДокумент) = Тип("ДокументСсылка.МаркировкаТоваровИСМП") Тогда
		
		ОтрезатьСодержимоеБлока                 = Ложь;
		ОтрезатьСодержимоеЛогистическойУпаковки = Ложь;
		
	Иначе
		
		ОтрезатьСодержимоеБлока =
			СтрокаИсточника.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
			И (ДанныеДокумента.ДетализацияСтруктурыХранения = Перечисления.ДетализацияСтруктурыХраненияИС.КоробаСГрупповымиУпаковками
				Или ДанныеДокумента.ДетализацияСтруктурыХранения = Перечисления.ДетализацияСтруктурыХраненияИС.ПалетыСМонотоварнымиКоробами
				Или (СтрокаИсточника.КоличествоПачек > 0 И СтрокаИсточника.Строки.Количество() = 0));
		
		ОтрезатьСодержимоеЛогистическойУпаковки =
			СтрокаИсточника.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая
			И (ЗначениеЗаполнено(СтрокаИсточника.Номенклатура) И СтрокаИсточника.КоличествоПачек > 0 И ЗначениеЗаполнено(СтрокаИсточника.GTIN))
			И (ДанныеДокумента.ДетализацияСтруктурыХранения = Перечисления.ДетализацияСтруктурыХраненияИС.ПалетыСМонотоварнымиКоробами
				Или СтрокаИсточника.Строки.Количество() = 0);
			
	КонецЕсли;
	
	НоваяСтрока.КоличествоПодчиненныхПачек = СтрокаИсточника.КоличествоПачек;
	
	Если НоваяСтрока.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
		И ОтрезатьСодержимоеБлока Тогда
		НоваяСтрока.НеПересчитыватьКоличествоПачек = Истина;
	КонецЕсли;
	
	Если НоваяСтрока.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая
		И ОтрезатьСодержимоеЛогистическойУпаковки Тогда
		НоваяСтрока.НеПересчитыватьКоличествоПачек = Истина;
	КонецЕсли;
	
	КоллекцияСтрокНовойСтроки = НоваяСтрока.Строки;
	
	ПроверкаИПодборПродукцииИСМПКлиентСервер.УстановитьДоступностьУпаковкиДляПроверки(НоваяСтрока, ДанныеДокумента.ДоступныеДляПроверкиУпаковки);
	ПроверкаИПодборПродукцииИСМПКлиентСервер.СформироватьПредставлениеДляСтрокиДереваМаркированнойПродукции(НоваяСтрока);
	ПроверкаИПодборПродукцииМОТПКлиентСервер.ПроверитьДопустимостьКодаМаркировки(НоваяСтрока, ДанныеДокумента.ПараметрыПроверкиКодовМаркировки, ДанныеДокумента.РежимПодбораСуществующихУпаковок);
	
	Если НоваяСтрока.НедопустимыйКодМаркировки Тогда
		ДанныеДокумента.КоличествоНедопустимыхКодовМаркировки = ДанныеДокумента.КоличествоНедопустимыхКодовМаркировки + 1;
	КонецЕсли;
	
	Если НоваяСтрока.СодержимоеНедоступно Тогда
		ДанныеДокумента.СодержимоеУпаковокНедоступно = Истина;
	КонецЕсли;
	
	Если ИнтеграцияИСКлиентСервер.ЭтоУпаковка(НоваяСтрока.ТипУпаковки) Тогда
		ДанныеДокумента.УпаковкиДокумента.Добавить(НоваяСтрока.Штрихкод);
	КонецЕсли;
	
	Для Каждого ПодчиненнаяСтрокаИсточника Из СтрокаИсточника.Строки Цикл
		ДобавитьСтрокуДереваМаркированнойПродукцииПриЗагрузке(ПодчиненнаяСтрокаИсточника, КоллекцияСтрокНовойСтроки, ДанныеДокумента);
	КонецЦикла;
	
КонецПроцедуры

Функция ДобавленнаяСтрокаПачкиБезБлока(ДанныеДокумента)
	
	НоваяСтрока = ДанныеДокумента.ДеревоМаркированнойПродукции.Строки.Вставить(0);
	
	ПроверкаИПодборПродукцииМОТПКлиентСервер.ЗаполнитьСтрокуПачкиБезБлока(НоваяСтрока);
	
	ДанныеДокумента.СтрокаПачкиБезБлока = НоваяСтрока;
	
	Возврат НоваяСтрока;

КонецФункции

Функция ДобавленнаяСтрокаБлокиБезКоробки(ДанныеДокумента)
	
	СтрокиДерева = ДанныеДокумента.ДеревоМаркированнойПродукции.Строки;
	
	Если СтрокиДерева.Количество() = 0 Тогда
		НоваяСтрока = СтрокиДерева.Вставить(0);
	ИначеЕсли СтрокиДерева[0].ТипУпаковки <> Перечисления.ПрочиеЗоныПересчетаПродукцииИСМП.ПачкиБезБлока Тогда
		НоваяСтрока = СтрокиДерева.Вставить(0);
	Иначе
		НоваяСтрока = СтрокиДерева.Вставить(1);
	КонецЕсли;
	
	ПроверкаИПодборПродукцииМОТПКлиентСервер.ЗаполнитьСтрокуБлокиБезКоробки(НоваяСтрока);
	
	ДанныеДокумента.СтрокаБлокиБезКоробки = НоваяСтрока;
	
	Возврат НоваяСтрока;

КонецФункции

Функция ДеревоМаркированнойПродукции()
	
	ДеревоМаркированнойПродукции = Новый ДеревоЗначений();
	ДеревоМаркированнойПродукции.Колонки.Добавить("СтатусПроверки",                      Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыПроверкиНаличияПродукцииИС"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ПредставлениеПроверкиПодчиненных",    Новый ОписаниеТипов("Строка"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ТипУпаковки", Новый ОписаниеТипов("ПеречислениеСсылка.ПрочиеЗоныПересчетаПродукцииИСМП, ПеречислениеСсылка.ТипыУпаковок"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("КоличествоПодчиненныхУпаковок",       Новый ОписаниеТипов("Число"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("КоличествоПодчиненныхВНаличии",       Новый ОписаниеТипов("Число"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("КоличествоПодчиненныхОтсутствует",    Новый ОписаниеТипов("Число"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("Штрихкод",                            Новый ОписаниеТипов("Строка"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ВсяУпаковкаПроверена",                Новый ОписаниеТипов("Булево"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ИндексКартинкиТипУпаковки",           Новый ОписаниеТипов("Число"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ИндексКартинкиСтатусПроверки",        Новый ОписаниеТипов("Число"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ТребуетсяПеремаркировка",             Новый ОписаниеТипов("Булево"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("Представление",                       Новый ОписаниеТипов("Строка"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ПредставлениеСодержимоеУпаковки",     Новый ОписаниеТипов("Строка"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ПредставлениеСодержимогоДоСопоставления", Новый ОписаниеТипов("Строка"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("КоличествоПодчиненныхОтложено",       Новый ОписаниеТипов("Число"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("КоличествоПодчиненныхПачек",          Новый ОписаниеТипов("Число"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("НомерСтикераОтложено",                Новый ОписаниеТипов("Строка"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("НеСодержитсяВДанныхДокумента",        Новый ОписаниеТипов("Булево"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("НеСоответствуетОтбору",               Новый ОписаниеТипов("Булево"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ИдетПроверкаДаннойУпаковки",          Новый ОписаниеТипов("Булево"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("КоличествоПодчиненныхНеЧислилось",    Новый ОписаниеТипов("Число"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("КоличествоПодчиненныхНеПроверялось",  Новый ОписаниеТипов("Число"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("КоличествоПодчиненныхВсего",          Новый ОписаниеТипов("Число"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ХешСумма",                            Новый ОписаниеТипов("Строка"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("Номенклатура",                        Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
	ДеревоМаркированнойПродукции.Колонки.Добавить("Характеристика",                      Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
	ДеревоМаркированнойПродукции.Колонки.Добавить("Серия",                               Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип);
	ДеревоМаркированнойПродукции.Колонки.Добавить("ИдентификаторСтроки",                 Новый ОписаниеТипов("Строка"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ВУпаковкеРазныеСерии",                Новый ОписаниеТипов("Булево"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ВУпаковкеРазнаяНоменклатура",         Новый ОписаниеТипов("Булево"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ВУпаковкеРазныеХарактеристики",       Новый ОписаниеТипов("Булево"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ИННВладельца",                        Новый ОписаниеТипов("Строка"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("МРЦ",                                 Новый ОписаниеТипов("Число"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ВключаетМРЦ",                         Новый ОписаниеТипов("Булево"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("СтатусКодаМаркировки",                Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыКодовМаркировкиМОТП"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("КоличествоПодчиненныхБлоков",         Новый ОписаниеТипов("Число"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("GTIN",                                Метаданные.ОпределяемыеТипы.GTIN.Тип);
	ДеревоМаркированнойПродукции.Колонки.Добавить("ВУпаковкеРазныйGTIN",                 Новый ОписаниеТипов("Булево"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ПредставлениеПроверкиКодаМаркировки", Новый ОписаниеТипов("Строка"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("НедопустимыйКодМаркировки",           Новый ОписаниеТипов("Булево"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("НеПересчитыватьКоличествоПачек",      Новый ОписаниеТипов("Булево"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("СодержимоеНедоступно",                Новый ОписаниеТипов("Булево"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ВидУпаковки",                         Новый ОписаниеТипов("ПеречислениеСсылка.ВидыУпаковокИС"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ТипШтрихкода",                        Новый ОписаниеТипов("ПеречислениеСсылка.ТипыШтрихкодов"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ТекстОшибкиПроверкиСредствамиККТ",    Новый ОписаниеТипов("Строка"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("СостояниеТребованияПолногоКодаККТ"  , Новый ОписаниеТипов("Число"));
	
	ДеревоМаркированнойПродукции.Колонки.Добавить("ХэшСуммаНормализации",                Новый ОписаниеТипов("Строка"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("НормализованныйШтрихкод",             Новый ОписаниеТипов("Строка"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ДатаПроизводства",                    Новый ОписаниеТипов("Дата"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("ВСеройЗоне",                          Новый ОписаниеТипов("Булево"));
	ДеревоМаркированнойПродукции.Колонки.Добавить("Коэффициент",                         Новый ОписаниеТипов("Число"));
	
	Возврат ДеревоМаркированнойПродукции;
	
КонецФункции

Функция ПодобраннаяМаркируемаяПродукция()
	
	ПодобраннаяМаркируемаяПродукция = Новый ТаблицаЗначений();
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("Номенклатура",               Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("Характеристика",             Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("Серия",                      Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип);
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("Количество",                 Новый ОписаниеТипов("Число"));
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("КоличествоПодобрано",        Новый ОписаниеТипов("Число"));
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("ИндексКодаМаркировки",       Новый ОписаниеТипов("Число"));
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("МРЦ",                        Новый ОписаниеТипов("Число"));
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("ВключаетМРЦ",                Новый ОписаниеТипов("Булево"));
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("GTIN",                       Метаданные.ОпределяемыеТипы.GTIN.Тип);
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("ПредставлениеНоменклатуры",  Новый ОписаниеТипов("Строка"));
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("СтатусУказанияСерий",        Новый ОписаниеТипов("Число"));
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("ХарактеристикиИспользуются", Новый ОписаниеТипов("Булево"));
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("ТипНоменклатуры",            Метаданные.ОпределяемыеТипы.ТипНоменклатуры.Тип);
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("НомерСтроки",                Новый ОписаниеТипов("Число"));
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("КоличествоИС",               Новый ОписаниеТипов("Число"));
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("НоменклатураСопоставлена",   Новый ОписаниеТипов("Булево"));
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("КодыGTIN",                   Новый ОписаниеТипов("СписокЗначений"));
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("ПредставлениеGTIN",          Новый ОписаниеТипов("Строка"));
	
	ПодобраннаяМаркируемаяПродукция.Колонки.Добавить("НоменклатураСопоставленаПоУПД", Новый ОписаниеТипов("Булево"));
	
	Возврат ПодобраннаяМаркируемаяПродукция;
	
КонецФункции

Процедура ЗаполнитьТаблицуПродукцияПоДокументу(ДанныеДокумента, ВидПродукции)
	
	ПодобраннаяМаркируемаяПродукция = ДанныеДокумента.ПодобраннаяМаркируемаяПродукция;
	
	Если ПодобраннаяМаркируемаяПродукция.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПродукцияПоДокументу = ПодобраннаяМаркируемаяПродукция.СкопироватьКолонки();
	
	Для Каждого СтрокаПодобраннойПродукции Из ПодобраннаяМаркируемаяПродукция Цикл
		
		Если СтрокаПодобраннойПродукции.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаПодобраннойПродукции.GTIN) Тогда
			
			// Только остаточные GTIN
			Если ЗначениеЗаполнено(СтрокаПодобраннойПродукции.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;
			
		Иначе
			
			Если Не ЗначениеЗаполнено(СтрокаПодобраннойПродукции.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(
			ПродукцияПоДокументу.Добавить(), СтрокаПодобраннойПродукции);
		
	КонецЦикла;
	
	Если ПродукцияПоДокументу.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДокумента.Вставить("ПродукцияПоДокументу", ПродукцияПоДокументу);
	
КонецПроцедуры

Процедура ДополнитьТаблицуМаркируемойПродукции(ДеревоУпаковок, ТаблицаМаркируемойПродукции)
	
	Для Каждого СтрокаДерева Из ДеревоУпаковок.Строки Цикл
		
		ДобавитьСтроку = Ложь;
		
		Если СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская Тогда
			
			ДобавитьСтроку = Истина;
			
		ИначеЕсли СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
			Или СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			
			Если СтрокаДерева.Строки.Количество() = 0 Тогда
				ДобавитьСтроку = Истина;
			Иначе
				ДополнитьТаблицуМаркируемойПродукции(СтрокаДерева, ТаблицаМаркируемойПродукции);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ДобавитьСтроку Тогда
			
			СтрокаТЧ = ТаблицаМаркируемойПродукции.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаДерева);
			СтрокаТЧ.СтрокаДерева = СтрокаДерева;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПрерватьОбработкуЗагрузкиДанных(ДанныеДокумента, ПараметрыСканирования, ДанныеНоменклатурыПоДаннымУПД, ДеревоУпаковок, ВосстановлениеДетализацииТребуетОбращенияКСервису)
	
	ТребуетсяПрерватьОбработкуЗагрузкиДанных = Ложь;
	
	Для Каждого СтрокаДерева Из ДеревоУпаковок.Строки Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаДерева.GTIN)
			И ДанныеДокумента.ВозможностьЗагрузкиДанныхБезПодключенияМОТП = Истина Тогда
			
			ТребуетсяПрерватьОбработкуЗагрузкиДанных = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТребуетсяПрерватьОбработкуЗагрузкиДанных Тогда
		
		ДанныеДокумента.ДополнительноеДействиеЗагрузки = "ЗапроситьПодключениеМОТП";
		
		Возврат Истина;
		
	КонецЕсли;
	
	ДанныеGTIN = Новый Соответствие;
	УточнениеКоэффициентовУпаковок = Новый Массив;
	ПроверитьКоэффициентыУпаковок(
		ДеревоУпаковок, УточнениеКоэффициентовУпаковок, ДанныеGTIN, ДанныеДокумента);
	
	Если УточнениеКоэффициентовУпаковок.Количество() Тогда
		
		ШтрихкодированиеМОТП.РасчетКоэффициентовПоСтатистике(
			ДеревоУпаковок,
			УточнениеКоэффициентовУпаковок,
			ПараметрыСканирования);
		
		ДанныеДокумента.УточнениеКоэффициентовУпаковок = УточнениеКоэффициентовУпаковок;
		ДанныеДокумента.ДополнительноеДействиеЗагрузки = "УточнитьКоэффициентыУпаковок";
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура ПроверитьКоэффициентыУпаковок(СтрокаДерева, УточнениеКоэффициентовУпаковок, ДанныеGTIN, ДанныеДокумента)
	
	Для Каждого ВложеннаяСтрокаДерева Из СтрокаДерева.Строки Цикл
		
		Если (ВложеннаяСтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая
			Или ВложеннаяСтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая)
			И ЗначениеЗаполнено(ВложеннаяСтрокаДерева.GTIN)
			И ДанныеДокумента.ВидМаркируемойПродукции = ВложеннаяСтрокаДерева.ВидПродукции Тогда
			
			Если ВложеннаяСтрокаДерева.Коэффициент < 2 И ДанныеGTIN[ВложеннаяСтрокаДерева.GTIN] = Неопределено Тогда
				
				СтрокаДанныхПоGTIN = Новый Структура;
				СтрокаДанныхПоGTIN.Вставить("GTIN",        ВложеннаяСтрокаДерева.GTIN);
				СтрокаДанныхПоGTIN.Вставить("ВидУпаковки", ВложеннаяСтрокаДерева.ВидУпаковки);
				СтрокаДанныхПоGTIN.Вставить("Коэффициент", 0);
				
				СтрокаДанныхПоGTIN.Вставить(
					"ПредставлениеСодержимоеУпаковки",
					ИнтеграцияИС.ПредставлениеНоменклатуры(
						ВложеннаяСтрокаДерева.Номенклатура,
						ВложеннаяСтрокаДерева.Характеристика));
				
				ДанныеGTIN.Вставить(ВложеннаяСтрокаДерева.GTIN, СтрокаДанныхПоGTIN);
				УточнениеКоэффициентовУпаковок.Добавить(СтрокаДанныхПоGTIN);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ВложеннаяСтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			ПроверитьКоэффициентыУпаковок(
				ВложеннаяСтрокаДерева, УточнениеКоэффициентовУпаковок, ДанныеGTIN, ДанныеДокумента);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли