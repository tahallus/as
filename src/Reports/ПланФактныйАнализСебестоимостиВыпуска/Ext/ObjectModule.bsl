#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// Задать настройки формы отчета.
//
// Параметры:
//  Форма		 - ФормаКлиентскогоПриложения	 - Форма отчета
//  КлючВарианта - Строка						 - Ключ загружаемого варианта
//  Настройки	 - Структура					 - см. ОтчетыКлиентСервер.НастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт

	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПриЗагрузкеВариантаНаСервере = Истина;
	Настройки.События.ПриЗагрузкеПользовательскихНастроекНаСервере = Истина;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

// Процедура - Обработчик заполнения настроек отчета и варианта
//
// Параметры:
//  НастройкиОтчета		 - Структура - Настройки отчета, подробнее см. процедуру ОтчетыУНФ.ИнициализироватьНастройкиОтчета 
//  НастройкиВариантов	 - Структура - Настройки варианта отчета, подробнее см. процедуру ОтчетыУНФ.ИнициализироватьНастройкиВарианта
//
Процедура ПриОпределенииНастроекОтчета(НастройкиОтчета, НастройкиВариантов) Экспорт
	
	УстановитьТегиВариантов(НастройкиВариантов);
	ДобавитьОписанияСвязанныхПолей(НастройкиВариантов);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	ОтчетыУНФ.ФормаОтчетаПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

// Обработчик события ПриЗагрузкеВариантаНаСервере
//
// Параметры:
//  Форма			 - ФормаКлиентскогоПриложения	 - Форма отчета
//  НовыеНастройкиКД - НастройкиКомпоновкиДанных	 - Загружаемые настройки КД
//
Процедура ПриЗагрузкеВариантаНаСервере(Форма, НовыеНастройкиКД) Экспорт
	
	ОтчетыУНФ.ПреобразоватьСтарыеНастройки(Форма, НовыеНастройкиКД);	
	ОтчетыУНФ.ОбновитьВидимостьОтбораОрганизация(Форма.Отчет.КомпоновщикНастроек);	
	ОтчетыУНФ.ФормаОтчетаПриЗагрузкеВариантаНаСервере(Форма, НовыеНастройкиКД);
	
КонецПроцедуры

// Обработчик события ПриЗагрузкеПользовательскихНастроекНаСервере
//
// Параметры:
//  Форма							 - ФормаКлиентскогоПриложения				 - Форма отчета
//  НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных - Загружаемые пользовательские настройки КД
//
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Форма, НовыеПользовательскиеНастройкиКД) Экспорт
	
	ОтчетыУНФ.ПеренестиПараметрыЗаголовкаВНастройки(КомпоновщикНастроек.Настройки, НовыеПользовательскиеНастройкиКД);	
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	Перем ТаблицаВыпуска;
	Перем ТаблицаПлановыхЗатратНаВыпуск;
	
	СтандартнаяОбработка = Ложь;
	ОтчетыУНФ.ОбъединитьСПользовательскимиНастройками(КомпоновщикНастроек);
	
	НастройкиОтчета = КомпоновщикНастроек.Настройки;
	ПараметрыОтчета = ОтчетыУНФ.ПараметрыФормированияОтчета(НастройкиОтчета);
	
	УправлениеНебольшойФирмойОтчеты.УстановитьМакетОформленияОтчета(НастройкиОтчета);
	УправлениеНебольшойФирмойОтчеты.ВывестиЗаголовокОтчета(ПараметрыОтчета, ДокументРезультат);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ЗначенияПараметровМКД = МакетКомпоновки.ЗначенияПараметров;
	ПараметрКомпоновкиДанных = ЗначенияПараметровМКД.Найти("КонецПериода");
	Если НЕ ПараметрКомпоновкиДанных = Неопределено Тогда
		
		Если ТипЗнч(ПараметрКомпоновкиДанных.Значение) = Тип("Дата")
			И ПараметрКомпоновкиДанных.Значение = Дата(1,1,1) Тогда
		
			ПараметрКомпоновкиДанных.Значение = Дата(3999,12,31);
			
		КонецЕсли;
	
	КонецЕсли;
	
	СформироватьТаблицыВыпускаИПлановыхЗатрат(ТаблицаВыпуска, ТаблицаПлановыхЗатратНаВыпуск, ЗначенияПараметровМКД);
	
	РассчитатьПлановуюСебестоимостьЗатратНаВыпуск(ТаблицаПлановыхЗатратНаВыпуск, ЗначенияПараметровМКД);
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТаблицаВыпуска", ТаблицаВыпуска);
	ВнешниеНаборыДанных.Вставить("ТаблицаПлановыхЗатратНаВыпуск", ТаблицаПлановыхЗатратНаВыпуск);
	
	// Создадим и инициализируем процессор компоновки
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	// Создадим и инициализируем процессор вывода результата
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	// Обозначим начало вывода
	ПроцессорВывода.НачатьВывод();
	ТаблицаЗафиксирована = Ложь;
	
	ДокументРезультат.ФиксацияСверху = 0;
	// Основной цикл вывода отчета
	Пока Истина Цикл
		// Получим следующий элемент результата компоновки
		ЭлементРезультата = ПроцессорКомпоновки.Следующий();
		
		Если ЭлементРезультата = Неопределено Тогда
			// Следующий элемент не получен - заканчиваем цикл вывода
			Прервать;
		Иначе
			// Зафиксируем шапку
			Если  Не ТаблицаЗафиксирована 
				  И ЭлементРезультата.ЗначенияПараметров.Количество() > 0 
				  И ТипЗнч(КомпоновщикНастроек.Настройки.Структура[0]) <> Тип("ДиаграммаКомпоновкиДанных") Тогда
				
				ТаблицаЗафиксирована = Истина;
				ДокументРезультат.ФиксацияСверху = ДокументРезультат.ВысотаТаблицы;
			
			КонецЕсли;
			// Элемент получен - выведем его при помощи процессора вывода
			ПроцессорВывода.ВывестиЭлемент(ЭлементРезультата);
		КонецЕсли;
	КонецЦикла;
	
	ПроцессорВывода.ЗакончитьВывод();
	
	ОтчетыУНФ.ОбработатьДиаграммыТабличногоДокумента(ДокументРезультат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьТегиВариантов(НастройкиВариантов)
	
	Для Каждого НастройкиТекВарианта Из НастройкиВариантов Цикл
		НастройкиТекВарианта.Значение.Теги = НСтр("ru = 'Производство,Себестоимость,Продукция'");
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьОписанияСвязанныхПолей(НастройкиВариантов)
	
	Для Каждого НастройкиТекВарианта Из НастройкиВариантов Цикл
		ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиТекВарианта.Значение.СвязанныеПоля, "ЗаказПокупателя", "Документ.ЗаказПокупателя");
		ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиТекВарианта.Значение.СвязанныеПоля, "Продукция", "Справочник.Номенклатура");
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьТаблицыВыпускаИПлановыхЗатрат(ТаблицаВыпуска, ТаблицаПлановыхЗатратНаВыпуск, ЗначенияПараметровМКД)
	
	Запрос = Новый Запрос;
	УстановитьПериодВЗапросе(Запрос, ЗначенияПараметровМКД);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыпускПродукцииОбороты.Организация КАК Организация,
	|	ВыпускПродукцииОбороты.СтруктурнаяЕдиница КАК Подразделение,
	|	ВыпускПродукцииОбороты.Номенклатура КАК Продукция,
	|	ВыпускПродукцииОбороты.Характеристика КАК ХарактеристикаПродукции,
	|	ВыпускПродукцииОбороты.Партия КАК ПартияПродукции,
	|	ВыпускПродукцииОбороты.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВыпускПродукцииОбороты.Спецификация КАК СпецификацияПродукции,
	|	ЕСТЬNULL(СпецификацииПоУмолчанию.Спецификация, ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)) КАК СпецификацияПоУмолчанию,
	|	ВыпускПродукцииОбороты.КоличествоОборот КАК КоличествоПродукции,
	|	NULL КАК Номенклатура
	|ПОМЕСТИТЬ ВременнаяТаблицаВыпуск
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции.Обороты(&НачалоПериода, &КонецПериода, , Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)) КАК ВыпускПродукцииОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпецификацииПоУмолчанию КАК СпецификацииПоУмолчанию
	|		ПО ВыпускПродукцииОбороты.Номенклатура = СпецификацииПоУмолчанию.Номенклатура
	|			И ВыпускПродукцииОбороты.Характеристика = СпецификацииПоУмолчанию.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Продукция,
	|	ХарактеристикаПродукции,
	|	СпецификацияПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыпускПродукции.Организация КАК Организация,
	|	ВыпускПродукции.Подразделение КАК Подразделение,
	|	ВыпускПродукции.Продукция КАК Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	ВыпускПродукции.ПартияПродукции КАК ПартияПродукции,
	|	ВыпускПродукции.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВыпускПродукции.СпецификацияПродукции КАК СпецификацияПродукции,
	|	ВыпускПродукции.КоличествоПродукции КАК КоличествоПродукции
	|ИЗ
	|	ВременнаяТаблицаВыпуск КАК ВыпускПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыпускПродукции.Организация КАК Организация,
	|	ВыпускПродукции.Подразделение КАК Подразделение,
	|	ВыпускПродукции.Продукция КАК Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	ВыпускПродукции.ПартияПродукции КАК ПартияПродукции,
	|	ВыпускПродукции.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВыпускПродукции.СпецификацияПродукции КАК СпецификацияВыпускаПродукции,
	|	ВЫБОР
	|		КОГДА ВыпускПродукции.СпецификацияПродукции = ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|			ТОГДА ВыпускПродукции.СпецификацияПоУмолчанию
	|		ИНАЧЕ ВыпускПродукции.СпецификацияПродукции
	|	КОНЕЦ КАК СпецификацияПродукции,
	|	ВыпускПродукции.КоличествоПродукции КАК КоличествоВыпускаПродукции,
	|	ЕСТЬNULL(ВыпускПродукции.Продукция, ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка)) КАК Номенклатура,
	|	ВыпускПродукции.ХарактеристикаПродукции КАК Характеристика,
	|	ВыпускПродукции.КоличествоПродукции КАК Количество,
	|	ВЫБОР
	|		КОГДА ВыпускПродукции.СпецификацияПродукции = ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|			ТОГДА ВыпускПродукции.СпецификацияПоУмолчанию
	|		ИНАЧЕ ВыпускПродукции.СпецификацияПродукции
	|	КОНЕЦ КАК Спецификация,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА ВыпускПродукции.СпецификацияПродукции = ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|					ТОГДА ВыпускПродукции.СпецификацияПоУмолчанию
	|				ИНАЧЕ ВыпускПродукции.СпецификацияПродукции
	|			КОНЕЦ = ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Материал)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|	КОНЕЦ КАК ТипСтрокиСостава
	|ПОМЕСТИТЬ ВременнаяТаблицаСоставВыпуска
	|ИЗ
	|	ВременнаяТаблицаВыпуск КАК ВыпускПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставВыпуска.Организация КАК Организация,
	|	СоставВыпуска.Подразделение КАК Подразделение,
	|	СоставВыпуска.Продукция КАК Продукция,
	|	СоставВыпуска.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	СоставВыпуска.ПартияПродукции КАК ПартияПродукции,
	|	СоставВыпуска.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СоставВыпуска.СпецификацияВыпускаПродукции КАК СпецификацияВыпускаПродукции,
	|	СоставВыпуска.СпецификацияПродукции КАК СпецификацияПродукции,
	|	СоставВыпуска.КоличествоВыпускаПродукции КАК КоличествоВыпускаПродукции,
	|	СоставВыпуска.Номенклатура КАК Номенклатура,
	|	СоставВыпуска.Характеристика КАК Характеристика,
	|	СоставВыпуска.Количество КАК Количество,
	|	СоставВыпуска.Спецификация КАК Спецификация,
	|	СоставВыпуска.ТипСтрокиСостава КАК ТипСтрокиСостава,
	|	ВЫБОР
	|		КОГДА СоставВыпуска.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.ПустаяСсылка)
	|		ИНАЧЕ СоставВыпуска.ТипСтрокиСостава
	|	КОНЕЦ КАК ТипСтрокиСоставаОтчет,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА СоставВыпуска.ТипСтрокиСостава <> ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|				ТОГДА СоставВыпуска.Номенклатура
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		КОНЕЦ, ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка)) КАК НоменклатураОтчет,
	|	ВЫБОР
	|		КОГДА СоставВыпуска.ТипСтрокиСостава <> ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|			ТОГДА СоставВыпуска.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК ХарактеристикаОтчет,
	|	ВЫБОР
	|		КОГДА СоставВыпуска.ТипСтрокиСостава <> ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|			ТОГДА СоставВыпуска.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоОтчет,
	|	ВЫБОР
	|		КОГДА СоставВыпуска.ТипСтрокиСостава <> ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|			ТОГДА СоставВыпуска.Спецификация
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|	КОНЕЦ КАК СпецификацияОтчет,
	|	ВЫБОР
	|		КОГДА СоставВыпуска.ТипСтрокиСостава <> ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|			ТОГДА СоставВыпуска.Номенклатура.СчетУчетаЗатрат
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка)
	|	КОНЕЦ КАК СчетУчетаЗатратОтчет
	|ИЗ
	|	ВременнаяТаблицаСоставВыпуска КАК СоставВыпуска
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоставВыпуска.Номенклатура КАК НоменклатураУзел,
	|	СоставВыпуска.Характеристика КАК ХарактеристикаУзла,
	|	СоставВыпуска.Спецификация КАК СпецификацияУзла,
	|	СоставВыпуска.ТипСтрокиСостава КАК ТипСтрокиСостава
	|ИЗ
	|	ВременнаяТаблицаСоставВыпуска КАК СоставВыпуска
	|ГДЕ
	|	СоставВыпуска.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)";
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаВыпуска = Результат[1].Выгрузить();
	ТаблицаПлановыхЗатратНаВыпуск = Результат[3].Выгрузить();
	ТаблицаУзловСпецификаций = Результат[4].Выгрузить();
	
	Итерация = 1;
	Пока ТаблицаУзловСпецификаций.Количество() > 0 Цикл
		
		РазузловатьУзлыСпецификаций(ТаблицаУзловСпецификаций, ТаблицаПлановыхЗатратНаВыпуск);
		Итерация = Итерация + 1;
		Если Итерация>50 Тогда
			ВызватьИсключение НСтр("ru = 'Слишком много итераций разузлования.'");
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

Процедура РазузловатьУзлыСпецификаций(ТаблицаУзловСпецификаций, ТаблицаПлановыхЗатратНаВыпуск)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаУзловСпецификаций", ТаблицаУзловСпецификаций);
	Запрос.УстановитьПараметр("ТаблицаПлановыхЗатратНаВыпуск", ТаблицаПлановыхЗатратНаВыпуск);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаУзловСпецификаций.СпецификацияУзла КАК СпецификацияУзла,
	|	ТаблицаУзловСпецификаций.ТипСтрокиСостава КАК ТипСтрокиСостава
	|ПОМЕСТИТЬ Вт_ТаблицаУзловСпецификаций
	|ИЗ
	|	&ТаблицаУзловСпецификаций КАК ТаблицаУзловСпецификаций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Спецификаций.СпецификацияУзла КАК СпецификацияУзла,
	|	СпецификацииСостав.Номенклатура.СчетУчетаЗатрат КАК СчетУчетаЗатрат,
	|	СпецификацииСостав.Номенклатура КАК Номенклатура,
	|	СпецификацииСостав.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА СпецификацииСостав.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|			ТОГДА СпецификацииСостав.Количество * ВЫРАЗИТЬ(СпецификацииСостав.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
	|		ИНАЧЕ СпецификацииСостав.Количество
	|	КОНЕЦ / ЕСТЬNULL(СпецификацииСостав.КоличествоПродукции, 1) КАК Количество,
	|	СпецификацииСостав.Спецификация КАК Спецификация,
	|	СпецификацииСостав.ТипСтрокиСостава КАК ТипСтрокиСостава
	|ПОМЕСТИТЬ ВременнаяТаблицаСоставУзлов
	|ИЗ
	|	Вт_ТаблицаУзловСпецификаций КАК Спецификаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Спецификации.Состав КАК СпецификацииСостав
	|		ПО Спецификаций.СпецификацияУзла = СпецификацииСостав.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Спецификаций.СпецификацияУзла,
	|	СпецификацииОперации.Операция.СчетУчетаЗатрат,
	|	СпецификацииОперации.Операция,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	ВЫБОР
	|		КОГДА СпецификацииОперации.Операция.ФиксированнаяСтоимость
	|			ТОГДА ВЫБОР
	|					КОГДА СпецификацииОперации.Количество = 0
	|						ТОГДА 1
	|					ИНАЧЕ СпецификацииОперации.Количество
	|				КОНЕЦ
	|		ИНАЧЕ СпецификацииОперации.НормаВремени
	|	КОНЕЦ / ЕСТЬNULL(СпецификацииОперации.КоличествоПродукции, 1),
	|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.ПустаяСсылка)
	|ИЗ
	|	Вт_ТаблицаУзловСпецификаций КАК Спецификаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Спецификации.Операции КАК СпецификацииОперации
	|		ПО Спецификаций.СпецификацияУзла = СпецификацииОперации.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СпецификацияУзла
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлановыеЗатратыНаВыпуск.Организация КАК Организация,
	|	ПлановыеЗатратыНаВыпуск.Подразделение КАК Подразделение,
	|	ПлановыеЗатратыНаВыпуск.Продукция КАК Продукция,
	|	ПлановыеЗатратыНаВыпуск.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	ПлановыеЗатратыНаВыпуск.ПартияПродукции КАК ПартияПродукции,
	|	ПлановыеЗатратыНаВыпуск.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ПлановыеЗатратыНаВыпуск.СпецификацияВыпускаПродукции КАК СпецификацияВыпускаПродукции,
	|	ПлановыеЗатратыНаВыпуск.СпецификацияПродукции КАК СпецификацияПродукции,
	|	ПлановыеЗатратыНаВыпуск.КоличествоВыпускаПродукции КАК КоличествоВыпускаПродукции,
	|	ПлановыеЗатратыНаВыпуск.Номенклатура КАК Номенклатура,
	|	ПлановыеЗатратыНаВыпуск.Характеристика КАК Характеристика,
	|	ПлановыеЗатратыНаВыпуск.Количество КАК Количество,
	|	ПлановыеЗатратыНаВыпуск.Спецификация КАК Спецификация,
	|	ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава КАК ТипСтрокиСостава,
	|	ПлановыеЗатратыНаВыпуск.НоменклатураОтчет КАК НоменклатураОтчет,
	|	ПлановыеЗатратыНаВыпуск.ХарактеристикаОтчет КАК ХарактеристикаОтчет,
	|	ПлановыеЗатратыНаВыпуск.КоличествоОтчет КАК КоличествоОтчет,
	|	ПлановыеЗатратыНаВыпуск.СпецификацияОтчет КАК СпецификацияОтчет,
	|	ПлановыеЗатратыНаВыпуск.СчетУчетаЗатратОтчет КАК СчетУчетаЗатратОтчет,
	|	ПлановыеЗатратыНаВыпуск.ТипСтрокиСоставаОтчет КАК ТипСтрокиСоставаОтчет
	|ПОМЕСТИТЬ ВременнаяТаблицаПлановыеЗатраты
	|ИЗ
	|	&ТаблицаПлановыхЗатратНаВыпуск КАК ПлановыеЗатратыНаВыпуск
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Спецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлановыеЗатратыНаВыпуск.Организация КАК Организация,
	|	ПлановыеЗатратыНаВыпуск.Подразделение КАК Подразделение,
	|	ПлановыеЗатратыНаВыпуск.Продукция КАК Продукция,
	|	ПлановыеЗатратыНаВыпуск.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	ПлановыеЗатратыНаВыпуск.ПартияПродукции КАК ПартияПродукции,
	|	ПлановыеЗатратыНаВыпуск.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ПлановыеЗатратыНаВыпуск.СпецификацияВыпускаПродукции КАК СпецификацияВыпускаПродукции,
	|	ПлановыеЗатратыНаВыпуск.СпецификацияПродукции КАК СпецификацияПродукции,
	|	ПлановыеЗатратыНаВыпуск.КоличествоВыпускаПродукции КАК КоличествоВыпускаПродукции,
	|	ЕСТЬNULL(ЕСТЬNULL(СоставУзлов.Номенклатура, ПлановыеЗатратыНаВыпуск.Номенклатура), ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка)) КАК Номенклатура,
	|	ЕСТЬNULL(СоставУзлов.Характеристика, ПлановыеЗатратыНаВыпуск.Характеристика) КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|				ИЛИ ПлановыеЗатратыНаВыпуск.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ПлановыеЗатратыНаВыпуск.Количество, 0) = 0
	|						ТОГДА 1
	|					ИНАЧЕ ПлановыеЗатратыНаВыпуск.Количество
	|				КОНЕЦ * СоставУзлов.Количество
	|		ИНАЧЕ ПлановыеЗатратыНаВыпуск.Количество
	|	КОНЕЦ КАК Количество,
	|	ЕСТЬNULL(СоставУзлов.Спецификация, ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)) КАК Спецификация,
	|	ЕСТЬNULL(СоставУзлов.ТипСтрокиСостава, ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава) КАК ТипСтрокиСостава,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ПлановыеЗатратыНаВыпуск.НоменклатураОтчет <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				ТОГДА ПлановыеЗатратыНаВыпуск.НоменклатураОтчет
	|			КОГДА СоставУзлов.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ИНАЧЕ ЕСТЬNULL(СоставУзлов.Номенклатура, ПлановыеЗатратыНаВыпуск.Номенклатура)
	|		КОНЕЦ, ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка)) КАК НоменклатураОтчет,
	|	ВЫБОР
	|		КОГДА ПлановыеЗатратыНаВыпуск.НоменклатураОтчет <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ПлановыеЗатратыНаВыпуск.ХарактеристикаОтчет
	|		КОГДА СоставУзлов.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ЕСТЬNULL(СоставУзлов.Характеристика, ПлановыеЗатратыНаВыпуск.Характеристика)
	|	КОНЕЦ КАК ХарактеристикаОтчет,
	|	ВЫБОР
	|		КОГДА ПлановыеЗатратыНаВыпуск.НоменклатураОтчет <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ПлановыеЗатратыНаВыпуск.КоличествоОтчет
	|		КОГДА СоставУзлов.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ВЫБОР
	|					КОГДА ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|							ИЛИ ПлановыеЗатратыНаВыпуск.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|						ТОГДА ВЫБОР
	|								КОГДА ЕСТЬNULL(ПлановыеЗатратыНаВыпуск.Количество, 0) = 0
	|									ТОГДА 1
	|								ИНАЧЕ ПлановыеЗатратыНаВыпуск.Количество
	|							КОНЕЦ * СоставУзлов.Количество
	|					ИНАЧЕ ПлановыеЗатратыНаВыпуск.Количество
	|				КОНЕЦ, ПлановыеЗатратыНаВыпуск.Количество)
	|	КОНЕЦ КАК КоличествоОтчет,
	|	ВЫБОР
	|		КОГДА ПлановыеЗатратыНаВыпуск.НоменклатураОтчет <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ПлановыеЗатратыНаВыпуск.СпецификацияОтчет
	|		КОГДА СоставУзлов.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|		ИНАЧЕ ЕСТЬNULL(СоставУзлов.Спецификация, ПлановыеЗатратыНаВыпуск.Спецификация)
	|	КОНЕЦ КАК СпецификацияОтчет,
	|	ВЫБОР
	|		КОГДА ПлановыеЗатратыНаВыпуск.НоменклатураОтчет <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ПлановыеЗатратыНаВыпуск.СчетУчетаЗатратОтчет
	|		КОГДА СоставУзлов.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка)
	|		ИНАЧЕ ЕСТЬNULL(СоставУзлов.СчетУчетаЗатрат, ПлановыеЗатратыНаВыпуск.СчетУчетаЗатратОтчет)
	|	КОНЕЦ КАК СчетУчетаЗатратОтчет,
	|	ВЫБОР
	|		КОГДА ПлановыеЗатратыНаВыпуск.НоменклатураОтчет <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ПлановыеЗатратыНаВыпуск.ТипСтрокиСоставаОтчет
	|		КОГДА СоставУзлов.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.ПустаяСсылка)
	|		ИНАЧЕ ЕСТЬNULL(СоставУзлов.ТипСтрокиСостава, ПлановыеЗатратыНаВыпуск.ТипСтрокиСоставаОтчет)
	|	КОНЕЦ КАК ТипСтрокиСоставаОтчет
	|ПОМЕСТИТЬ ВременнаяТаблицаСоставВыпуска
	|ИЗ
	|	ВременнаяТаблицаПлановыеЗатраты КАК ПлановыеЗатратыНаВыпуск
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСоставУзлов КАК СоставУзлов
	|		ПО ПлановыеЗатратыНаВыпуск.Спецификация = СоставУзлов.СпецификацияУзла
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставВыпуска.Организация КАК Организация,
	|	СоставВыпуска.Подразделение КАК Подразделение,
	|	СоставВыпуска.Продукция КАК Продукция,
	|	СоставВыпуска.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	СоставВыпуска.ПартияПродукции КАК ПартияПродукции,
	|	СоставВыпуска.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СоставВыпуска.СпецификацияВыпускаПродукции КАК СпецификацияВыпускаПродукции,
	|	СоставВыпуска.СпецификацияПродукции КАК СпецификацияПродукции,
	|	СоставВыпуска.КоличествоВыпускаПродукции КАК КоличествоВыпускаПродукции,
	|	СоставВыпуска.Номенклатура КАК Номенклатура,
	|	СоставВыпуска.Характеристика КАК Характеристика,
	|	СоставВыпуска.Количество КАК Количество,
	|	СоставВыпуска.Спецификация КАК Спецификация,
	|	СоставВыпуска.ТипСтрокиСостава КАК ТипСтрокиСостава,
	|	СоставВыпуска.ТипСтрокиСоставаОтчет КАК ТипСтрокиСоставаОтчет,
	|	СоставВыпуска.НоменклатураОтчет КАК НоменклатураОтчет,
	|	СоставВыпуска.ХарактеристикаОтчет КАК ХарактеристикаОтчет,
	|	СоставВыпуска.КоличествоОтчет КАК КоличествоОтчет,
	|	СоставВыпуска.СпецификацияОтчет КАК СпецификацияОтчет,
	|	СоставВыпуска.СчетУчетаЗатратОтчет КАК СчетУчетаЗатратОтчет
	|ИЗ
	|	ВременнаяТаблицаСоставВыпуска КАК СоставВыпуска
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоставВыпуска.Номенклатура КАК НоменклатураУзел,
	|	СоставВыпуска.Характеристика КАК ХарактеристикаУзла,
	|	СоставВыпуска.Спецификация КАК СпецификацияУзла,
	|	СоставВыпуска.ТипСтрокиСостава КАК ТипСтрокиСостава
	|ИЗ
	|	ВременнаяТаблицаСоставВыпуска КАК СоставВыпуска
	|ГДЕ
	|	СоставВыпуска.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)";
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаПлановыхЗатратНаВыпуск = Результат[4].Выгрузить();
	ТаблицаУзловСпецификаций = Результат[5].Выгрузить();
	
КонецПроцедуры

Процедура РассчитатьПлановуюСебестоимостьЗатратНаВыпуск(ТаблицаПлановыхЗатратНаВыпуск, ЗначенияПараметровСКД)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	УстановитьПериодВЗапросе(Запрос, ЗначенияПараметровСКД);
	Запрос.УстановитьПараметр("ТаблицаПлановыхЗатратНаВыпуск", ТаблицаПлановыхЗатратНаВыпуск);
	Запрос.УстановитьПараметр("ТаблицаПорядковОкругления", ЦенообразованиеСервер.ТаблицаПорядковОкругления());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаПорядковОкругления.Порядок КАК Порядок,
	|	ТаблицаПорядковОкругления.Значение КАК Значение
	|ПОМЕСТИТЬ ТаблицаПорядковОкругления
	|ИЗ
	|	&ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления";
	Запрос.Выполнить();
	
	ПараметрКомпоновкиДанных = ЗначенияПараметровСКД.Найти("ВариантРасчетаПлановойСебестоимости");
	Если ПараметрКомпоновкиДанных = Неопределено 
		ИЛИ ТипЗнч(ПараметрКомпоновкиДанных.Значение) <> Тип("Число") 
		ИЛИ НЕ ЗначениеЗаполнено(ПараметрКомпоновкиДанных.Значение) Тогда
		ВариантРасчетаПлановойСебестоимости = 0;
	Иначе
		ВариантРасчетаПлановойСебестоимости = ПараметрКомпоновкиДанных.Значение;
	КонецЕсли;
	// Вариант расчета плановой себестоимости: 
	// 0 - По фактической стоимости
	// 1 - По последней закупке
	// 2 - По виду цен
	Если ВариантРасчетаПлановойСебестоимости = 0 Тогда
		
		// По фактической стоимости
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПлановыеЗатратыНаВыпуск.Организация КАК Организация,
		|	ПлановыеЗатратыНаВыпуск.Подразделение КАК Подразделение,
		|	ПлановыеЗатратыНаВыпуск.Продукция КАК Продукция,
		|	ПлановыеЗатратыНаВыпуск.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ПлановыеЗатратыНаВыпуск.ПартияПродукции КАК ПартияПродукции,
		|	ПлановыеЗатратыНаВыпуск.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ПлановыеЗатратыНаВыпуск.СпецификацияВыпускаПродукции КАК СпецификацияВыпускаПродукции,
		|	ПлановыеЗатратыНаВыпуск.СпецификацияПродукции КАК СпецификацияПродукции,
		|	ПлановыеЗатратыНаВыпуск.КоличествоВыпускаПродукции КАК КоличествоВыпускаПродукции,
		|	ПлановыеЗатратыНаВыпуск.НоменклатураОтчет КАК Номенклатура,
		|	ПлановыеЗатратыНаВыпуск.ХарактеристикаОтчет КАК Характеристика,
		|	ПлановыеЗатратыНаВыпуск.СпецификацияОтчет КАК Спецификация,
		|	ПлановыеЗатратыНаВыпуск.КоличествоОтчет КАК Количество,
		|	ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава КАК ТипСтрокиСостава,
		|	ПлановыеЗатратыНаВыпуск.ТипСтрокиСоставаОтчет КАК ТипСтрокиСоставаОтчет,
		|	ПлановыеЗатратыНаВыпуск.СчетУчетаЗатратОтчет КАК СчетУчетаЗатрат
		|ПОМЕСТИТЬ ВременнаяТаблицаПлановыеЗатратыНаВыпуск
		|ИЗ
		|	&ТаблицаПлановыхЗатратНаВыпуск КАК ПлановыеЗатратыНаВыпуск
		|ГДЕ
		|	ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава <> ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Запасы.Организация КАК Организация,
		|	Запасы.КоррСтруктурнаяЕдиница КАК Подразделение,
		|	Запасы.КоррНоменклатура КАК Продукция,
		|	Запасы.КоррХарактеристика КАК ХарактеристикаПродукции,
		|	Запасы.КоррПартия КАК ПартияПродукции,
		|	Запасы.КоррЗаказПокупателя КАК ЗаказПокупателя,
		|	Запасы.КоррСпецификация КАК СпецификацияПродукции,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Спецификация КАК Спецификация,
		|	СУММА(Запасы.Сумма) КАК Сумма,
		|	СУММА(Запасы.Количество) КАК Количество
		|ПОМЕСТИТЬ ВременнаяТаблицаЗатратыНаВыпуск
		|ИЗ
		|	РегистрНакопления.Запасы КАК Запасы
		|ГДЕ
		|	Запасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И Запасы.ЗатратыНаВыпуск
		|	И Запасы.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ Запасы.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	Запасы.КоррСпецификация,
		|	Запасы.Характеристика,
		|	Запасы.КоррСтруктурнаяЕдиница,
		|	Запасы.КоррНоменклатура,
		|	Запасы.КоррПартия,
		|	Запасы.Номенклатура,
		|	Запасы.КоррЗаказПокупателя,
		|	Запасы.КоррХарактеристика,
		|	Запасы.Организация,
		|	Запасы.Спецификация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеЗатратыНаВыпуск.Организация КАК Организация,
		|	ПлановыеЗатратыНаВыпуск.Подразделение КАК Подразделение,
		|	ПлановыеЗатратыНаВыпуск.Продукция КАК Продукция,
		|	ПлановыеЗатратыНаВыпуск.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ПлановыеЗатратыНаВыпуск.ПартияПродукции КАК ПартияПродукции,
		|	ПлановыеЗатратыНаВыпуск.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ПлановыеЗатратыНаВыпуск.СпецификацияВыпускаПродукции КАК СпецификацияПродукции,
		|	ПлановыеЗатратыНаВыпуск.Номенклатура КАК Номенклатура,
		|	ПлановыеЗатратыНаВыпуск.Характеристика КАК Характеристика,
		|	ПлановыеЗатратыНаВыпуск.Спецификация КАК Спецификация,
		|	ПлановыеЗатратыНаВыпуск.Количество КАК КоличествоЗатратПлан,
		|	ПлановыеЗатратыНаВыпуск.ТипСтрокиСоставаОтчет КАК ТипСтрокиСостава,
		|	ПлановыеЗатратыНаВыпуск.СчетУчетаЗатрат КАК СчетУчетаЗатрат,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ФактическиеЗатратыНаВыпуск.Сумма, 0) = 0
		|				ИЛИ ЕСТЬNULL(ФактическиеЗатратыНаВыпуск.Количество, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(ПлановыеЗатратыНаВыпуск.Количество * (ФактическиеЗатратыНаВыпуск.Сумма / ФактическиеЗатратыНаВыпуск.Количество) КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК СтоимостьЗатратПлан
		|ИЗ
		|	ВременнаяТаблицаПлановыеЗатратыНаВыпуск КАК ПлановыеЗатратыНаВыпуск
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаЗатратыНаВыпуск КАК ФактическиеЗатратыНаВыпуск
		|		ПО ПлановыеЗатратыНаВыпуск.Организация = ФактическиеЗатратыНаВыпуск.Организация
		|			И ПлановыеЗатратыНаВыпуск.Подразделение = ФактическиеЗатратыНаВыпуск.Подразделение
		|			И ПлановыеЗатратыНаВыпуск.Продукция = ФактическиеЗатратыНаВыпуск.Продукция
		|			И ПлановыеЗатратыНаВыпуск.ХарактеристикаПродукции = ФактическиеЗатратыНаВыпуск.ХарактеристикаПродукции
		|			И ПлановыеЗатратыНаВыпуск.ПартияПродукции = ФактическиеЗатратыНаВыпуск.ПартияПродукции
		|			И ПлановыеЗатратыНаВыпуск.ЗаказПокупателя = ФактическиеЗатратыНаВыпуск.ЗаказПокупателя
		|			И ПлановыеЗатратыНаВыпуск.СпецификацияВыпускаПродукции = ФактическиеЗатратыНаВыпуск.СпецификацияПродукции
		|			И ПлановыеЗатратыНаВыпуск.Номенклатура = ФактическиеЗатратыНаВыпуск.Номенклатура
		|			И ПлановыеЗатратыНаВыпуск.Характеристика = ФактическиеЗатратыНаВыпуск.Характеристика
		|			И ПлановыеЗатратыНаВыпуск.Спецификация = ФактическиеЗатратыНаВыпуск.Спецификация";
		
	Иначе
		
		// По последней закупке или по виду цен
		ПараметрВидЦен = ЗначенияПараметровСКД.Найти("ВидЦен");
		Если ПараметрВидЦен=Неопределено ИЛИ ТипЗнч(ПараметрВидЦен.Значение)<>Тип("СправочникСсылка.ВидыЦен") ИЛИ ПараметрКомпоновкиДанных.Значение=1 Тогда
			Запрос.УстановитьПараметр("ВидЦен", ПредопределенноеЗначение("Справочник.ВидыЦен.Учетная"));
		Иначе
			Запрос.УстановитьПараметр("ВидЦен", ПараметрВидЦен.Значение);
		КонецЕсли; 
		Запрос.УстановитьПараметр("ВариантРасчета", ПараметрКомпоновкиДанных.Значение);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПлановыеЗатратыНаВыпуск.Организация КАК Организация,
		|	ПлановыеЗатратыНаВыпуск.Подразделение КАК Подразделение,
		|	ПлановыеЗатратыНаВыпуск.Продукция КАК Продукция,
		|	ПлановыеЗатратыНаВыпуск.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ПлановыеЗатратыНаВыпуск.ПартияПродукции КАК ПартияПродукции,
		|	ПлановыеЗатратыНаВыпуск.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ПлановыеЗатратыНаВыпуск.СпецификацияВыпускаПродукции КАК СпецификацияВыпускаПродукции,
		|	ПлановыеЗатратыНаВыпуск.СпецификацияПродукции КАК СпецификацияПродукции,
		|	ПлановыеЗатратыНаВыпуск.КоличествоВыпускаПродукции КАК КоличествоВыпускаПродукции,
		|	ПлановыеЗатратыНаВыпуск.Номенклатура КАК Номенклатура,
		|	ПлановыеЗатратыНаВыпуск.Характеристика КАК Характеристика,
		|	ПлановыеЗатратыНаВыпуск.Спецификация КАК Спецификация,
		|	ПлановыеЗатратыНаВыпуск.Количество КАК Количество,
		|	ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава КАК ТипСтрокиСостава,
		|	ПлановыеЗатратыНаВыпуск.ТипСтрокиСоставаОтчет КАК ТипСтрокиСоставаОтчет,
		|	ПлановыеЗатратыНаВыпуск.СчетУчетаЗатратОтчет КАК СчетУчетаЗатрат,
		|	ПлановыеЗатратыНаВыпуск.НоменклатураОтчет КАК НоменклатураОтчет,
		|	ПлановыеЗатратыНаВыпуск.ХарактеристикаОтчет КАК ХарактеристикаОтчет,
		|	ПлановыеЗатратыНаВыпуск.КоличествоОтчет КАК КоличествоОтчет,
		|	ПлановыеЗатратыНаВыпуск.СпецификацияОтчет КАК СпецификацияОтчет
		|ПОМЕСТИТЬ ВременнаяТаблицаПлановыеЗатратыНаВыпуск
		|ИЗ
		|	&ТаблицаПлановыхЗатратНаВыпуск КАК ПлановыеЗатратыНаВыпуск
		|ГДЕ
		|	ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава <> ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеЗатратыНаВыпуск.Организация КАК Организация,
		|	ПлановыеЗатратыНаВыпуск.Подразделение КАК Подразделение,
		|	ПлановыеЗатратыНаВыпуск.Продукция КАК Продукция,
		|	ПлановыеЗатратыНаВыпуск.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ПлановыеЗатратыНаВыпуск.ПартияПродукции КАК ПартияПродукции,
		|	ПлановыеЗатратыНаВыпуск.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ПлановыеЗатратыНаВыпуск.СпецификацияВыпускаПродукции КАК СпецификацияПродукции,
		|	ПлановыеЗатратыНаВыпуск.Номенклатура КАК Номенклатура,
		|	ПлановыеЗатратыНаВыпуск.Характеристика КАК Характеристика,
		|	ПлановыеЗатратыНаВыпуск.Спецификация КАК Спецификация,
		|	ПлановыеЗатратыНаВыпуск.Количество КАК КоличествоЗатратПлан,
		|	ПлановыеЗатратыНаВыпуск.ТипСтрокиСоставаОтчет КАК ТипСтрокиСостава,
		|	ПлановыеЗатратыНаВыпуск.СчетУчетаЗатрат КАК СчетУчетаЗатрат,
		|	ВЫБОР
		|		КОГДА &ВариантРасчета = 1
		|				И ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава <> ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.ПустаяСсылка)
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ЗакупочныеЦены.Цена, 0) КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ (ВЫРАЗИТЬ(ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) / ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01) КАК ЧИСЛО(15, 0))) * ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01)
		|	КОНЕЦ * ПлановыеЗатратыНаВыпуск.Количество КАК СтоимостьЗатратПлан,
		|	ПлановыеЗатратыНаВыпуск.НоменклатураОтчет КАК НоменклатураОтчет,
		|	ПлановыеЗатратыНаВыпуск.ХарактеристикаОтчет КАК ХарактеристикаОтчет,
		|	ПлановыеЗатратыНаВыпуск.КоличествоОтчет КАК КоличествоОтчет,
		|	ПлановыеЗатратыНаВыпуск.СпецификацияОтчет КАК СпецификацияОтчет
		|ИЗ
		|	ВременнаяТаблицаПлановыеЗатратыНаВыпуск КАК ПлановыеЗатратыНаВыпуск
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|				&КонецПериода,
		|				ВидЦен = &ВидЦен
		|					И (Номенклатура, Характеристика) В
		|						(ВЫБРАТЬ
		|							Состав.Номенклатура,
		|							Состав.Характеристика
		|						ИЗ
		|							ВременнаяТаблицаПлановыеЗатратыНаВыпуск КАК Состав
		|						ГДЕ
		|							(&ВариантРасчета = 2
		|								ИЛИ Состав.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.ПустаяСсылка)))) КАК ЦеныНоменклатурыСрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления
		|			ПО ЦеныНоменклатурыСрезПоследних.ВидЦен.ПорядокОкругления = ТаблицаПорядковОкругления.Порядок
		|		ПО ПлановыеЗатратыНаВыпуск.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
		|			И ПлановыеЗатратыНаВыпуск.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
		|			И (&ВариантРасчета = 2
		|				ИЛИ ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.ПустаяСсылка))
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			Закупки.Номенклатура КАК Номенклатура,
		|			Закупки.Характеристика КАК Характеристика,
		|			МИНИМУМ(ВЫРАЗИТЬ(ВЫБОР
		|						КОГДА Закупки.Количество = 0
		|							ТОГДА 0
		|						ИНАЧЕ Закупки.Сумма * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность / Закупки.Количество
		|					КОНЕЦ КАК ЧИСЛО(15, 2))) КАК Цена
		|		ИЗ
		|			(ВЫБРАТЬ
		|				Закупки.Номенклатура КАК Номенклатура,
		|				Закупки.Характеристика КАК Характеристика,
		|				МАКСИМУМ(Закупки.Период) КАК Период
		|			ИЗ
		|				РегистрНакопления.Закупки КАК Закупки
		|			ГДЕ
		|				Закупки.Период <= &КонецПериода
		|				И (Закупки.Номенклатура, Закупки.Характеристика) В
		|						(ВЫБРАТЬ
		|							Состав.Номенклатура,
		|							Состав.Характеристика
		|						ИЗ
		|							ВременнаяТаблицаПлановыеЗатратыНаВыпуск КАК Состав
		|						ГДЕ
		|							&ВариантРасчета = 1
		|							И Состав.ТипСтрокиСостава <> ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.ПустаяСсылка))
		|			
		|			СГРУППИРОВАТЬ ПО
		|				Закупки.Номенклатура,
		|				Закупки.Характеристика) КАК ПоследниеЗакупки
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Закупки КАК Закупки
		|				ПО (Закупки.Номенклатура = ПоследниеЗакупки.Номенклатура)
		|					И (Закупки.Характеристика = ПоследниеЗакупки.Характеристика)
		|					И (Закупки.Период = ПоследниеЗакупки.Период),
		|			РегистрСведений.КурсыВалют.СрезПоследних(
		|					&КонецПериода,
		|					Валюта В
		|						(ВЫБРАТЬ
		|							ВалютаУчета.Значение
		|						ИЗ
		|							Константа.ВалютаУчета КАК ВалютаУчета)) КАК КурсыВалютСрезПоследних
		|		
		|		СГРУППИРОВАТЬ ПО
		|			Закупки.Номенклатура,
		|			Закупки.Характеристика) КАК ЗакупочныеЦены
		|		ПО ПлановыеЗатратыНаВыпуск.Номенклатура = ЗакупочныеЦены.Номенклатура
		|			И ПлановыеЗатратыНаВыпуск.Характеристика = ЗакупочныеЦены.Характеристика
		|			И (&ВариантРасчета = 1)
		|			И (ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава <> ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.ПустаяСсылка))"
		
	КонецЕсли; 
	
	ТаблицаПлановыхЗатратНаВыпуск = Запрос.Выполнить().Выгрузить();
	
	// Требуется свернуть развернутый состав спецификаций
	Если ТаблицаПлановыхЗатратНаВыпуск.Колонки.Найти("НоменклатураОтчет") <> Неопределено Тогда
		Для каждого СтрокаТабличнойЧасти Из ТаблицаПлановыхЗатратНаВыпуск Цикл
			СтрокаТабличнойЧасти.Номенклатура = СтрокаТабличнойЧасти.НоменклатураОтчет;
			СтрокаТабличнойЧасти.Характеристика = СтрокаТабличнойЧасти.ХарактеристикаОтчет;
			СтрокаТабличнойЧасти.КоличествоЗатратПлан = СтрокаТабличнойЧасти.КоличествоОтчет;
			СтрокаТабличнойЧасти.Спецификация = СтрокаТабличнойЧасти.СпецификацияОтчет;
		КонецЦикла;
	КонецЕсли; 
	Для каждого СтрокаТабличнойЧасти Из ТаблицаПлановыхЗатратНаВыпуск Цикл
		Если СтрокаТабличнойЧасти.ТипСтрокиСостава = Перечисления.ТипыСтрокСоставаСпецификации.Расход
			ИЛИ НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ТипСтрокиСостава) Тогда
			СтрокаТабличнойЧасти.Номенклатура = СтрокаТабличнойЧасти.СчетУчетаЗатрат;
			СтрокаТабличнойЧасти.КоличествоЗатратПлан = 0;
		КонецЕсли; 
	КонецЦикла;
	Если ВариантРасчетаПлановойСебестоимости = 0 Тогда
		ТаблицаПлановыхЗатратНаВыпуск.Свернуть("Организация, Подразделение, Продукция, ХарактеристикаПродукции, ПартияПродукции, ЗаказПокупателя, СпецификацияПродукции, Номенклатура, Характеристика, Спецификация, КоличествоЗатратПлан, СтоимостьЗатратПлан");
	Иначе
		ТаблицаПлановыхЗатратНаВыпуск.Свернуть("Организация, Подразделение, Продукция, ХарактеристикаПродукции, ПартияПродукции, ЗаказПокупателя, СпецификацияПродукции, Номенклатура, Характеристика, Спецификация, КоличествоЗатратПлан", "СтоимостьЗатратПлан");
	КонецЕсли; 
	 
КонецПроцедуры

Процедура УстановитьПериодВЗапросе(Запрос, Знач ЗначенияПараметровМКД)
	
	ДатаНачалаПериода = Дата(1,1,1);
	ДатаКонцаПериода = Дата(3999,12,31);
	
	ПараметрКомпоновкиДанных = ЗначенияПараметровМКД.Найти("НачалоПериода");
	Если НЕ ПараметрКомпоновкиДанных = Неопределено 
		И ТипЗнч(ПараметрКомпоновкиДанных.Значение) = Тип("Дата")
		И ПараметрКомпоновкиДанных.Значение <> Дата(1,1,1) Тогда
		
		ДатаНачалаПериода = ПараметрКомпоновкиДанных.Значение;
	КонецЕсли;
	
	ПараметрКомпоновкиДанных = ЗначенияПараметровМКД.Найти("КонецПериода");
	Если НЕ ПараметрКомпоновкиДанных = Неопределено 
		И ТипЗнч(ПараметрКомпоновкиДанных.Значение) = Тип("Дата")
		И ПараметрКомпоновкиДанных.Значение <> Дата(1,1,1) Тогда
		
		ДатаКонцаПериода = ПараметрКомпоновкиДанных.Значение;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачалаПериода);
	Запрос.УстановитьПараметр("КонецПериода",  ДатаКонцаПериода);
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

ЭтоОтчетУНФ = Истина;

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли