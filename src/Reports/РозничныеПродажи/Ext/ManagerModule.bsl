
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// Вызывается при работе в модели сервиса для получения сведений о предопределенных вариантах отчета.
//
// Возвращаемое значение:
//  Массив из Структура:
//    * Имя           - Строка - имя варианта отчета; например, "Основной";
//    * Представление - Строка - имя варианта отчета; например, НСтр("ru = 'Динамика изменений файлов'").
//
Функция ВариантыНастроек() Экспорт 
	
	Результат = Новый Массив;
	Результат.Добавить(Новый Структура("Имя, Представление", "РозничныеПродажиКонтекст", 
		НСтр("ru = 'Розничные продажи'")));
	Результат.Добавить(Новый Структура("Имя, Представление", "РозничныеПродажиПоТочкамКонтекст", 
		НСтр("ru = 'Розничные продажи по кассам ККМ'")));
	Результат.Добавить(Новый Структура("Имя, Представление", "РозничнаяВыручка", 
		НСтр("ru = 'Розничная выручка'")));
	Возврат Результат;
	
КонецФункции

// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   НастройкиОтчета - см. ВариантыОтчетов.ОписаниеОтчета.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Ложь);
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Метаданные.Отчеты.РозничныеПродажи, "РозничныеПродажиКонтекст");
	Вариант.Описание = НСтр("ru = 'История розничных продаж с детализацией по товару или услуге за указанный период.'");
	Вариант.Включен = Ложь;
	Вариант.Размещение.Очистить();
	
	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Метаданные.Отчеты.РозничныеПродажи, "РозничныеПродажиПоТочкамКонтекст");
	Вариант.Описание = НСтр("ru = 'История розничных продаж в разрезе касс ККМ за указанный период.'");
	Вариант.Включен = Ложь;
	Вариант.Размещение.Очистить();
	
	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Метаданные.Отчеты.РозничныеПродажи, "РозничнаяВыручка");
	Вариант.Описание = НСтр("ru = 'Розничная выручка в разрезе касс ККМ за указанный период.'");
	Вариант.Размещение.Вставить(ВариантыОтчетовКлиентСервер.ИдентификаторНачальнойСтраницы());
	Вариант.Размещение.Вставить(Метаданные.Подсистемы.Продажи.Подсистемы.РозничныеПродажи, "Важный");
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

#КонецЕсли