
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("АдресСхемы") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ДанныеРасшифровки") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("Расшифровка") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ДанныеРасшифровки = ПолучитьИзВременногоХранилища(Параметры.ДанныеРасшифровки);
	
	ДанныеРасшифровки.Настройки.ДополнительныеСвойства.Вставить("АдресСхемы", Параметры.АдресСхемы);
	
	ПараметрыРасшифровки = Новый Структура;
	
	Для Каждого ТекЭлемент Из ДанныеРасшифровки.Элементы.Получить(Параметры.Расшифровка).ПолучитьПоля() Цикл
		ПараметрыРасшифровки.Вставить(ТекЭлемент.Поле, ТекЭлемент.Значение);
	КонецЦикла;
	
	Если ПараметрыРасшифровки.Свойство("ЭтоПотери") Тогда
		ДокументРезультат = РезультатОбработкиРасшифровкиНаСервере(ДанныеРасшифровки.Настройки, ПараметрыРасшифровки);
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Заголовок = ПредставлениеРасшифровки(ПараметрыРасшифровки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция РезультатОбработкиРасшифровкиНаСервере(НастройкиКД, ПараметрыРасшифровки)
	
	ЗапросРасшифровка = Новый Запрос;
	ЗапросРасшифровка.МенеджерВременныхТаблиц = Отчеты.ВоронкаПоЛидам.ВременныеТаблицыВоронкиПродаж(НастройкиКД);
	
	Если ПараметрыРасшифровки.ЭтоПотери Тогда
		ЗапросРасшифровка.Текст = Отчеты.ВоронкаПоЛидам.ТекстЗапросаСУстановленнымПолемГруппировки(
		"ВЫБРАТЬ
		|	ИсторияСостоянийЛидовСрезПоследних.Лид КАК Лид,
		|	ИсторияСостоянийЛидовСрезПоследних.Лид.Ответственный КАК Ответственный,
		|	ИсторияСостоянийЛидовСрезПоследних.Лид.ПричинаНеуспешногоЗавершенияРаботы КАК ПричинаОтмены,
		|	1 КАК Потеря
		|ИЗ
		|	РегистрСведений.ИсторияСостоянийЛидов.СрезПоследних(
		|			,
		|			Лид.ВариантЗавершения = ЗНАЧЕНИЕ(Перечисление.ВариантЗавершенияРаботыСЛидом.НекачественныйЛид)
		|				И Состояние <> ЗНАЧЕНИЕ(Справочник.СостоянияЛидов.Завершен)
		|				И Лид В
		|					(ВЫБРАТЬ
		|						ВТ_Лиды.Лид
		|					ИЗ
		|						ВТ_Лиды)) КАК ИсторияСостоянийЛидовСрезПоследних
		|ГДЕ
		|	&ПолеГруппировки = &ЗначениеПоляГруппировки
		|	И ИсторияСостоянийЛидовСрезПоследних.Состояние = &Состояние
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПричинаОтмены,
		|	ИсторияСостоянийЛидовСрезПоследних.Период",
		"ИсторияСостоянийЛидовСрезПоследних",
		НастройкиКД);
	Иначе
		ЗапросРасшифровка.Текст =
		"ВЫБРАТЬ
		|	ВТ_ДанныеВоронкиПродаж.Лид КАК Лид,
		|	ВТ_ДанныеВоронкиПродаж.Лид.СостояниеЛида КАК ТекущееСостояние,
		|	ВТ_ДанныеВоронкиПродаж.Лид.ВариантЗавершения КАК ВариантЗавершения,
		|	ВТ_ДанныеВоронкиПродаж.Лид.Ответственный КАК Ответственный
		|ИЗ
		|	ВТ_ДанныеВоронкиПродаж КАК ВТ_ДанныеВоронкиПродаж
		|ГДЕ
		|	ВТ_ДанныеВоронкиПродаж.ПолеГруппировки = &ЗначениеПоляГруппировки
		|	И ВТ_ДанныеВоронкиПродаж.Состояние = &Состояние
		|	И ВТ_ДанныеВоронкиПродаж.УчитыватьВВоронке = 1
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТ_ДанныеВоронкиПродаж.Лид.ДатаСоздания";
	КонецЕсли;
	
	ЗапросРасшифровка.УстановитьПараметр("ЗначениеПоляГруппировки", ПараметрыРасшифровки.ПолеГруппировки);
	ЗапросРасшифровка.УстановитьПараметр("Состояние", ПараметрыРасшифровки.Состояние);
	
	ТабРезультатРасшифровки = Новый ТабличныйДокумент;
	ТабРезультатРасшифровки.ТолькоПросмотр = Истина;
	ТабРезультатРасшифровки.ОтображатьСетку = Ложь;
	ТабРезультатРасшифровки.ОтображатьЗаголовки = Ложь;
	
	Если ПараметрыРасшифровки.ЭтоПотери Тогда
		МакетРасшифровки = Отчеты.ВоронкаПоЛидам.ПолучитьМакет("ТД_РасшифровкаПотери");
	Иначе
		МакетРасшифровки = Отчеты.ВоронкаПоЛидам.ПолучитьМакет("ТД_Расшифровка");
	КонецЕсли;
	ОбластьШапка = МакетРасшифровки.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.ПредставлениеРасшифровки = ПредставлениеРасшифровки(ПараметрыРасшифровки);
	ТабРезультатРасшифровки.Вывести(ОбластьШапка);
	
	РезультатЗапроса = ЗапросРасшифровка.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ТабРезультатРасшифровки.Вывести(МакетРасшифровки.ПолучитьОбласть("СтрокаОтсутствуютДанные"));
		Возврат ТабРезультатРасшифровки;
	КонецЕсли;
	
	ЦветаГрадиента = НастройкиКД.ДополнительныеСвойства.ЦветаГрадиента;
	
	НомерСтроки = 0;
	СуммаИтог = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НомерСтроки = НомерСтроки + 1;
		ОбластьСтрока = ПолучитьОбластьРасшифровки(МакетРасшифровки, Выборка, ПараметрыРасшифровки.ЭтоПотери);
		ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
		ОбластьСтрока.Параметры.Лид = Выборка.Лид;
		ОбластьСтрока.Параметры.Ответственный = Выборка.Ответственный;
		
		Если ПараметрыРасшифровки.ЭтоПотери Тогда
			ОбластьСтрока.Параметры.ПричинаОтмены = Выборка.ПричинаОтмены;
			Если ТипЗнч(ЦветаГрадиента[Выборка.ПричинаОтмены]) = Тип("Цвет") Тогда
				ОбластьСтрока.Область("R1C3:R1C3").ЦветФона = ЦветаГрадиента[Выборка.ПричинаОтмены];
			КонецЕсли;
		Иначе
			ОбластьСтрока.Параметры.ТекущееСостояние = Выборка.ТекущееСостояние;
			ОбластьСтрока.Параметры.ВариантЗавершения = Выборка.ВариантЗавершения;
		КонецЕсли;
		ТабРезультатРасшифровки.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	ОбластьПодвал = МакетРасшифровки.ПолучитьОбласть("Подвал");
	
	Возврат ТабРезультатРасшифровки;
	
КонецФункции

&НаСервере
Функция ПолучитьОбластьРасшифровки(МакетРасшифровки, Выборка, Потери)
	
	Если Потери Тогда
		Возврат МакетРасшифровки.ПолучитьОбласть("Строка");
	КонецЕсли;
	
	Если Выборка.ВариантЗавершения = Перечисления.ВариантЗавершенияРаботыСЛидом.НекачественныйЛид Тогда
		Возврат МакетРасшифровки.ПолучитьОбласть("СтрокаОтмена");
	КонецЕсли;
	
	Возврат МакетРасшифровки.ПолучитьОбласть("Строка");
	
КонецФункции

&НаСервере
Функция ПредставлениеРасшифровки(ПараметрыРасшифровки)
	
	Возврат СтрШаблон(НСтр("ru = '%1: %2, уровень воронки: %3'"),
	?(ПараметрыРасшифровки.ЭтоПотери, НСтр("ru = 'Потери'"), НСтр("ru = 'Лиды'")),
	?(ТипЗнч(ПараметрыРасшифровки.ПолеГруппировки) = Тип("Булево"), НСтр("ru = 'Все'"),ПараметрыРасшифровки.ПолеГруппировки),
	ПараметрыРасшифровки.Состояние);
	
КонецФункции

#КонецОбласти