
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	ИспользоватьРазделениеПоОбластямДанных = РазделениеВключено
		И РаботаВМоделиСервиса.ДоступноИспользованиеРазделенныхДанных();
		
	РазрешенаРаботаСНовостями = ОбработкаНовостейПовтИсп.РазрешенаРаботаСНовостямиТекущемуПользователю();
	
	// если не разрешена работа с новостями в коробке форма не отображается
	Если НЕ РазделениеВключено И НЕ РазрешенаРаботаСНовостями Тогда
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	// В конфигурации есть общие реквизиты с разделением и включена ФО РаботаВМоделиСервиса.
	Если РазделениеВключено Тогда
		// Если включено разделение данных, и мы зашли в неразделенном сеансе,
		//  то нельзя устанавливать пользовательские свойства новости.
		// Зашли в конфигурацию под пользователем без разделения (и не вошли в область данных).
		Если ИнтернетПоддержкаПользователей.СеансЗапущенБезРазделителей() Тогда
			ПолучитьТекущегоПользователя = Ложь;
		Иначе
			ПолучитьТекущегоПользователя = Истина;
		КонецЕсли;
	Иначе
		ПолучитьТекущегоПользователя = Истина;
	КонецЕсли;

	Если ПолучитьТекущегоПользователя = Истина Тогда
		ЭтотОбъект.ПараметрыСеанса_ТекущийПользователь = Пользователи.ТекущийПользователь();
	Иначе
		ЭтотОбъект.ПараметрыСеанса_ТекущийПользователь = Справочники.Пользователи.ПустаяСсылка();
	КонецЕсли;
	
	ЭтотОбъект.ЧастотаАвтолистания = 50; // Секунд
	ЭтотОбъект.КоличествоНовостейДляЛистанияМаксимум = 6;
	ЭтотОбъект.КоличествоНовостейДляЛистания = 6;
	ПятьЧасовВМинутах = 300;
	ЭтотОбъект.СписокНовостей_ИнтервалАвтообновления = ПятьЧасовВМинутах;
	
	ОбновитьСписокНовостейСервер();
	
	Если Новости.Количество() = 0 Тогда
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
		
	Если ИспользоватьРазделениеПоОбластямДанных Тогда
		СформироватьСписокНовостейСервиса();
	КонецЕсли;
	
	Элементы.ГруппаНовостиСервиса.Видимость = ТаблицаСообщенийСервиса.Количество() > 0;
	
	Если (ОбработкаНовостейПовтИсп.ЕстьРолиЧтенияНовостей()) Тогда
		Элементы.ГруппаНавигация.Видимость = Истина;
	Иначе
		Элементы.ГруппаНавигация.Видимость = Ложь;
	КонецЕсли;
	
	НастроитьФормуМобильныйКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЭтотОбъект.КоличествоНовостейДляЛистания > 1 Тогда
		ЭтотОбъект.ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьАвтолистание", ЭтотОбъект.ЧастотаАвтолистания, Ложь);
	КонецЕсли;
	
	Если РазрешенаРаботаСНовостями Тогда
		ЭтотОбъект.ПодключитьОбработчикОжидания("Подключаемый_АвтообновлениеСпискаНовостей", 
			ЭтотОбъект.СписокНовостей_ИнтервалАвтообновления * 60,
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "Новости. Новость прочтена" Тогда
		// Не обновлять список новостей.

	ИначеЕсли ИмяСобытия = "Новости. Загружены новости" Тогда
		Если Источник <> ЭтотОбъект.УникальныйИдентификатор Тогда
			ОбновитьСписокНовостейСервер();
		КонецЕсли;

	ИначеЕсли ИмяСобытия = "Новости. Обновлены настройки чтения новостей" Тогда
		ПриСозданииНаСервере(Ложь, Истина);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ИндексТекущейНовостиПриИзменении(Элемент)

	// Если пользователь явно нажал на кнопку выбора новости, то остановить автолистание.
	// Оно автоматически включится после обновления списка новостей.
	// Отключить автолистание.
	ЭтотОбъект.ОтключитьОбработчикОжидания("Подключаемый_ВыполнитьАвтолистание");
	// Вывести текст текущей новости.
	ЭтотОбъект.ТекстТекущейНовостиХТМЛ = ЭтотОбъект.Новости.Получить(ЭтотОбъект.ИндексТекущейНовости).ТекстНовостиХТМЛ;

КонецПроцедуры

&НаКлиенте
Процедура ТекстТекущейНовостиХТМЛПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если СтрНайти(ДанныеСобытия.Href, "UNF_BannerNewsInformCenter") <> 0 Тогда
		ОткрытьФорму("Обработка.ИнформационныйЦентр.Форма.ИнформационныйЦентр");
		Возврат;
	КонецЕсли;
	
	ТекущаяНовость = Новости[ИндексТекущейНовости].Ссылка;
	
	Если НЕ ЗначениеЗаполнено(ТекущаяНовость) Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Справочник.Новости.Форма.ФормаНовости",Новый Структура("Ключ", ТекущаяНовость));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьСписокНовостейСервиса()
	
	ИнформационныйЦентрСервер.СформироватьСписокНовостейНаРабочийСтол(ТаблицаСообщенийСервиса, 1);
	
	Если ТаблицаСообщенийСервиса.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Сообщение = ТаблицаСообщенийСервиса[0];
	СвойстваСообщения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Сообщение.СсылкаНаДанные, "Наименование, Критичность");
	НаименованиеБезПробелов = СтрокаБезПереносов(СвойстваСообщения.Наименование);	
	
	Элементы.ИмяНовости_0.Заголовок = НаименованиеБезПробелов;
			
	Сообщение.ИмяЭлементаФормы = Элементы.ИмяНовости_0.Имя;
	Сообщение.ТипИнформации    = Сообщение.СсылкаНаДанные.ТипИнформации.Наименование;
	Сообщение.Идентификатор    = Сообщение.СсылкаНаДанные.Идентификатор;
	Сообщение.ВнешняяСсылка    = Сообщение.СсылкаНаДанные.ВнешняяСсылка;
		
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	Элементы.ГруппаНовостиСервиса.Видимость = РазделениеВключено;
	Если РазделениеВключено Тогда
		Элементы.ГруппаБаннеры.Высота = 17;
	Иначе
		Элементы.ГруппаБаннеры.Высота = 14;
	КонецЕсли;
	
	ЭтотОбъект.КоличествоНовостейДляЛистания = Мин(ЭтотОбъект.Новости.Количество(), ЭтотОбъект.КоличествоНовостейДляЛистанияМаксимум);
	
	// Перерисовать поле переключателей.
	Элементы.ИндексТекущейНовости.СписокВыбора.Очистить();
	Для С=0 По ЭтотОбъект.КоличествоНовостейДляЛистания-1 Цикл
		Элементы.ИндексТекущейНовости.СписокВыбора.Добавить(С, " ");
	КонецЦикла;
	// Если количество новостей для листания = 1, то скрыть поле переключателей.
	Если ЭтотОбъект.КоличествоНовостейДляЛистания <= 1 Тогда
		Элементы.ИндексТекущейНовости.Видимость = Ложь;
	Иначе
		Элементы.ИндексТекущейНовости.Видимость = Истина;
	КонецЕсли;
	// Сбросить счетчик текущей новости.
	Если РазделениеВключено Тогда
		ЭтотОбъект.ИндексТекущейНовости = 1;
	Иначе
		ЭтотОбъект.ИндексТекущейНовости = 0;
	КонецЕсли;
	
	Если Новости.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Вывести текст текущей новости.
	ЭтотОбъект.ТекстТекущейНовостиХТМЛ = ЭтотОбъект.Новости.Получить(ЭтотОбъект.ИндексТекущейНовости).ТекстНовостиХТМЛ;

КонецПроцедуры

&НаСервере
Функция ВыбранныеЛентыНовостей()
	
	ЛентыНовостейДляПоказа = Новый Массив;
			
	ЛентыНовостейДляПоказа.Добавить("WhatIsNew");
	ЛентыНовостейДляПоказа.Добавить("HelpfulHints");
	ЛентыНовостейДляПоказа.Добавить("userevents");
	ЛентыНовостейДляПоказа.Добавить("usecases");
		
	Возврат ЛентыНовостейДляПоказа;
	
КонецФункции

&НаСервере
Процедура ОбновитьСписокНовостейСервер()
	
	СтруктураНастроекПоказаНовостей = ХранилищаНастроек.НастройкиНовостей.Загрузить(
		"НастройкиПоказаНовостей", // КлючОбъекта
		""); // КлючНастроек. Настройки показа новостей - единые для пользователя.
	
	УстановленыНастройкиПользователя = Ложь;
	Если ТипЗнч(СтруктураНастроекПоказаНовостей) = Тип("Структура") 
		И СтруктураНастроекПоказаНовостей.Свойство("ОтображаемыеНовости_Вариант")
		И СтруктураНастроекПоказаНовостей.ОтображаемыеНовости_Вариант = "Все" Тогда
		
		УстановленыНастройкиПользователя = Истина;
		СтруктураНастроекПоказаНовостей.ОтображаемыеНовости_Вариант = "Количество";
		ХранилищаНастроек.НастройкиНовостей.Сохранить(
			"НастройкиПоказаНовостей", // КлючОбъекта
			"",
			СтруктураНастроекПоказаНовостей);
	Иначе
		// Настройки по-умолчанию.
	КонецЕсли;
	
	Новости.Очистить();
	ЛентыНовостей = ВыбранныеЛентыНовостей();
	
	МассивОтключенныхЛентНовостейИзНастроек = ХранилищаНастроек.НастройкиНовостей.Загрузить(
		"ОтключенныеЛентыНовостей");
	
	Для Каждого ЛентаНовостей Из ЛентыНовостей Цикл
		
		ТаблицаНовостейЛенты = Новый ТаблицаЗначений;
		ПараметрыПолученияНовостей = Новый Структура;
		ИнтерактивныеДействия = Новый Массив;
		ЛентаНовостейПоКоду = ОбработкаНовостейПовтИсп.ПолучитьЛентуНовостейПоКоду(ЛентаНовостей);
		
		Если ТипЗнч(МассивОтключенныхЛентНовостейИзНастроек) = Тип("Массив") 
			И МассивОтключенныхЛентНовостейИзНастроек.Найти(ЛентаНовостейПоКоду) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыПолученияНовостей = Новый Структура;
		ПараметрыПолученияНовостей.Вставить("ВариантОтбора",      1);
		ПараметрыПолученияНовостей.Вставить("ЛентаНовостей",      ЛентаНовостейПоКоду);
		ПараметрыПолученияНовостей.Вставить("КоличествоНовостей", МаксимальноеКоличествоНовостей(ЛентаНовостей));
		ПараметрыПолученияНовостей.Вставить("ПорядокСортировки",  "Прочтена Возр, ДатаПубликации Убыв, Важность Убыв");
		
		Справочники.Новости.ПолучитьСписокНовостей(ТаблицаНовостейЛенты,,ПараметрыПолученияНовостей,ИнтерактивныеДействия);
		
		Для Каждого НовостьЛенты Из ТаблицаНовостейЛенты Цикл
			
			Если НовостьЛенты.Ссылка.ДатаСбросаВажности > НовостьЛенты.ДатаПубликации 
				И НовостьЛенты.Ссылка.ДатаСбросаВажности < ТекущаяДатаСеанса() Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстНовостиХТМЛ = Справочники.Новости.ПолучитьХТМЛТекстНовостей(НовостьЛенты.Ссылка, 
				Новый Структура("ОтображатьЗаголовок", Ложь));
			
			КороткийТекстДляБаннера = КороткийТекстДляБаннера(ТекстНовостиХТМЛ);
			
			Если Не ЗначениеЗаполнено(КороткийТекстДляБаннера) Тогда
				Продолжить;
			КонецЕсли;
			
			НовыйБаннер = Новости.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйБаннер, НовостьЛенты,,"Действия");
			НовыйБаннер.ТекстНовостиХТМЛ = КороткийТекстДляБаннера;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Новости.Сортировать("ДатаПубликации УБЫВ");
	
	Если РазделениеВключено Тогда
		ДобавитьБаннерИнформацияИПоддержка();
	КонецЕсли;
	
	Если УстановленыНастройкиПользователя Тогда
		СтруктураНастроекПоказаНовостей.ОтображаемыеНовости_Вариант = "Все";
		ХранилищаНастроек.НастройкиНовостей.Сохранить(
			"НастройкиПоказаНовостей", // КлючОбъекта
			"",
			СтруктураНастроекПоказаНовостей);
	Иначе
		// Настройки по-умолчанию.
	КонецЕсли;
	
	ЭтотОбъект.КоличествоНовостейДляЛистания = Новости.Количество();
	УправлениеФормой();

КонецПроцедуры

&НаСервере
Функция КороткийТекстДляБаннера(ТекстХтмл)
	
	НачалоТекста = СтрНайти(ТекстХтмл,"<div style = ""display: none"" id=""UNF_BannerText"">");
	
	Если НачалоТекста = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	КонецТекста = СтрНайти(ТекстХтмл,"</div>",,НачалоТекста);
	СтрокаДляЗамены = Сред(ТекстХтмл, НачалоТекста, КонецТекста - НачалоТекста);
	СтрокаДляЗамены = СтрЗаменить(СтрокаДляЗамены, "style = ""display: none""","");
	СтрокаДляЗамены = СтрЗаменить(СтрокаДляЗамены, "<br/>","");
	
	НачалоНовости = СтрНайти(ТекстХтмл,"<div id=""newsText"">");
	КонецНовости = СтрНайти(ТекстХтмл,"</div>",,НачалоТекста);
	ПодстрокаНовости = Сред(ТекстХтмл, НачалоНовости, КонецНовости - НачалоНовости);

	ТекстХтмл = СтрЗаменить(ТекстХтмл, ПодстрокаНовости, СтрокаДляЗамены);
	
	СтрокаДата = "<div id=""importanceData""> </div>";
	НачалоДаты = СтрНайти(ТекстХтмл,"<div id=""importanceData"">");
	КонецДаты  = СтрНайти(ТекстХтмл,"</div>",,НачалоДаты);
	СтрокаДляЗамены = Сред(ТекстХтмл, НачалоДаты, КонецДаты + 6 - НачалоДаты);
	
	ТекстХтмл = СтрЗаменить(ТекстХтмл, 
		СтрокаДляЗамены, 
		"");
		
	НачалоЛинк = СтрНайти(ТекстХтмл,"<div id=""newsHyperlink"">");
	КонецЛинк  = СтрНайти(ТекстХтмл,"</div>",,НачалоЛинк);
	СтрокаДляЗамены = Сред(ТекстХтмл, НачалоЛинк, КонецЛинк + 6 - НачалоЛинк);
	
	ТекстХтмл = СтрЗаменить(ТекстХтмл, 
		СтрокаДляЗамены, 
		"");
		
	НачалоСтили = СтрНайти(ТекстХтмл,"/* Здесь можно вставить свои стили");
	КонецСтили = СтрНайти(ТекстХтмл,"*/",,НачалоСтили);
	СтрокаДляЗамены = Сред(ТекстХтмл, НачалоСтили, КонецСтили + 2 - НачалоСтили);
	
	Если НЕ ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		ТекстХтмл = СтрЗаменить(ТекстХтмл, 
		СтрокаДляЗамены, 
		"body {
		|padding: 0;
		|margin: 0;
		|}");
	Иначе
		ТекстХтмл = СтрЗаменить(ТекстХтмл, 
		СтрокаДляЗамены, 
		"body {
		|padding-left: 45px;
		|margin-right: 30px;
		|text-align:centr;
		|width: 200px;}");
	КонецЕсли;
	
	Возврат ТекстХтмл;
	
КонецФункции

&НаСервере
Процедура ДобавитьБаннерИнформацияИПоддержка()
	
	ИнформацияИПоддержка = Новости.Вставить(0);
	ИнформацияИПоддержка.Наименование = НСтр("ru = 'Информация и поддержка'");	
	
	ИнформацияИПоддержка.ТекстНовости = СтрШаблон("<div style = ""margin: 0;padding: 0;"" id=""newsText"">
	|<img src = ""data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAANIAAAA8CAYAAAAT1+SwAAAACXBIWXMAAAsSAAALEgHS3X78AAAPF0lEQVR4nO2dC1RVVRrH/zzEBS6FUSJ8JJjDYJqpaJlCainTiISrJl8ZPpePZsBmco1vfERaPphGQ7MmWz4qrcAVpGXqrNBkZlWUS0MLXyPq4BMFzIv4gFn/zdnXw+FeL3Dv5V5w/9a66957Hvvss+/37W/v73zfvh43C9//GgqFwi68AQxQTahQ2Ienaj+Fwn6UIikUDkApkkLhAJQiKRQOwFs1oqKm7D+Yj/SMHOQdO4dr18pc0m4tf9MMYR3vx3NxvdC1czu3+e3o/q5wg3oo3JwVq77EvNfS4evrh379+sHf398lFS4oKEB2djaa+fngzTdeQPyIvm7RcMoiKWyS9tn3mL3oU8TGxuKTTz6Br6+vSxutrKwMzz77LCb86T14AHjRDZRJWSSFTbpHJsFU5oMjR47A29s9+t78/HyEhYWhb++O2J0xw+X1Uc4GhU1+OXIWUVFRbqNEJCQkBKGhobhx45Yb1EYpkqIG3C4vh5eXl9s1FRW7/Ha5G9REKZJC4RCUs0FRZ+h8uHbtmlMbMCgoCB999JFbWkQ9SpEUdaaiosLpiuTp6QkPDw+3/5GcokgbN2cj//Ql8XnMyEiEtA8071u1dheKS0wW9ykaFtu3b1e/mIZTFOnVZRnIP1WpSEXFJqQsHiU+c9v0uZvNx/WL7KQUSdEocIqzQSoR2bMvz/w544v94j3A309Jj6JR4dQ5Uv/ITtiT/YtQLFoeDvmoRN0ebi+26zmQe0oM+/JPFSKkfSvMnzFUnEMLduCn09XKHjMqUrxo8VhupqakcTE9xHa9slorY3fmDHHd6XO2mL+TQXHLbB4jy5T1wF3uQcJ2WPXOLnNdkmYORf/IcPP1UpaMFG3zx/i3UFxcim5dHxDWfE92HjK/+FGc5+/vi6ExEeZrQrP6+jaQTJsaLdrDWcydOxcXLlxwWvmkQ4cOmDlz5r3tbOgfFS4UhoIQ5+8nBI0/LIUE0nJFhgsBoPBIJZNCkZO1UAiPUelk2RQgCiHLlfBYnk+Bl8pkrQwIISytso/nGo81HqMvk/Ugd7sHKpPcr2fMqUhx/7JsXoeKyGN5TtqmBLF9+pzNKCoxIfSBynL44hw0acZQ0QZhPWaId57DY9ge/M66OVORGPN2+/Ztp5UPLYJh+vTp954i6Yd1FChowi1hb0ohE8dqDolXtHlT2qZE0UNTmNjjs/fWWwmWM3/mUCFA0BwXFBoKEAWW9BqwUGzjNdgj69mdOVMra6nV+nN+Jy1pbbjbPdCqyP20JOtSJ4rPFHZj2yUvyxDKmL4pwdwRyE6B+zduycarSzOwYXO2aIcDuafN5bANeJxsK2eTlZXl9Gs0FByuSCdPF4p3KlG/yMremhZJwh5SKhK04ZBUvuSlGUjWCZil4ZgeOZQZqxvK8TMFjfukIp08femu5UhYT9aFioBsy8ewvrw3vRLYugf9ful4gYW5IhWP51HZZCdE9mbnCUXUd1LyM+vKY3mN5+NThRWq6f26ipycHCxZsgRXrlzBrVt1D/GhtfLyLMeAmNdrfa63tydatWyOl1+KRt/eYXa3hNOGdhQSOczhj7xHUy6j8BRpwzzjubQKnCPYCwVTL3R6pTaycfM+cW291ZTncTvL6tV/YbXzbN2Dpf2WCHkgEEXFp8xzSTk/kkNGWjLWQe/5hGaxaIkrh9HOt0T2sHTpUiQnJwsFGjhwINq0aVPn0jp16lTnc4uKirBjxw58tv0HJE6JxorXRtp1X05/IDt0SA9zjzxN643l3AkGIaW1klaE321593g8y+Ewh41BNmgKIOcGb72zq8p3a8h5Da/vb+G6FFbpSGD9M7bvN8/NanIPcj+VQD+0099jypJRKC42CcXhtaZNicZerZ2oZLRU3G6ElojtK4eN9TW027ZtW62cDQcPHsTKlSsRFxeHtLQ0NGnSxKn1s8WNGzcwevRorHw7DRHdQvHCsMfrXJZT50jQnhUBGeLzUIMw0zWeNKNyuDMxYZ0QMn1vyzlNf214aAkKDoWfAn3fgwnmI6gUcp8UPA71mrSaUKUUXisuJkJ8lkpB4ZXDUz0sUyoADG59wPY9yP2sk7R23Gacx1EROeeT8yHZfqwf62/sXHicVBr9sLE+mDBhAgoLq7eVNcrLy+Hj44M1a9a4XIkI67JlyxZh2d74+zb3UiRobm85LKMQ0UFApCuYvav+GAp9pXt8n+jxCfd1e/jO0E4ey3MlFCpaCmvubzoz2OtbGlJS+KT7un9k5RCBlob1oIdMbrOGsT627sG4n27sbl3bm9sL4n4qE+aozLyfk6fomQtH+qZE0SHwHLaldMVDezbH81l3eY+W2soZ1Nb1zVAfurPbtm1rd22Y3Jebm4vg4GC7yqM3sG/fvvhg0ya76tOoE/voAaPjwZJlY+9OAZReQYV1Nmzehx59/oyIiAi7WomK9NBDD+Hw4cN2lfPNN99g/PjxOH78uChz6tSpSE1NFXF5dYFlrV+/HjcL369znVTQqsImo4f1gU9gd7doqNLSUgwZMgQ9H76Bdxbfh5+P3ULivLdF+ntKSorL6tWoFYnPWeQzJyP29D73Gl16z0H82DNYuLC6x7K+ycjIEBHnH7/dGkGBnhgYBWR/XyYeDrsSldinsMmJkxfFMxt3gE4Kpm+UXr8zIzGVVrg81UIN7RQ28fHxxvXr192ioWJiYtCiRQuMTizE8FhfXLxcjsydpZg7d6BL66UsksImvXs+iJ07d+Ly5csubyzOhViXs4Vt8fKCIixedVUEtS5atMil9VIWSWGTFYtHITI6GY899hg+//xz4XlzBYyGuHr1qliGi2FGkoCAAJcP7ZRFUtgkolsIVqeMQanpMjp37gw/Pz+xloL+1b17d5SUlDitMVevXo2mTZuiZcuW1V58FsSFK12JskiKGjEhvh9GPf84/v3dMRw9fh7XTHfW/t799SHszjqAs2fPivmLo6El4tAtLropxo9oVqV0uhzmLy8WoUfDhw932Y+pFElRY3x9fTCwf2fx0mMylWF31iGrxezduxcTJ06sc0MztIiRDMFBXmjR3BMBLTzRvcudEKN935Vhxz7nWcOa4HBF0meTGtFnkzKqWYbLMJt0zKioKtEH1srRlwFDNqtEZpZKZLKc8XwZ96b/zjCcaVN/L+rCOLaJCZXPmyxlszIEhwGj0LJRZaaqvD6vKWP95PEycVBmwDIcqLGuW8EQngULFmDr1q14MPS+OpfDmDhGLkyePBlrN12AfwtPFB22P8zIkThckSxlk0pkNimFiQGcZrIrtzEoVAq1tXJkGRJbUc5MezDnEJWYxHX5nYLOWDwRc6dllEqoAIxvk3F7PKdojgk5exZWyWbduDlDfOcxPJYhSXf2m8w5RLwWr89yGdDK0KST2rV530f3L2sU61hIF/nFixdFGvq6devQqmUzvPn6C5g8bgCatZ5c57Lj4+Px5JNPCoVa+Y/qnaercaqzgdEDfBkDQGV0NAWM+6X1MObZQAvotFSGpWvJ4FiJjAznuVQCmfZNK2HMTuV+liHTLRijB50Fk1m3ErH+wtpdQkEsRV0naysp8VwZ4c2yL55IFfF97DREgGyxqdpaCw2N9u1aiRonJiYKBerSpQvWr38fU8YPwL6v5iFh8iDxLMpe2rVrJ4Je3ZF699pxSCeFWAqYPn9HJt4Z0zEsYeuYjC9+FO/s/Tkk4zCsWGau5t7JvqUyyIxUDuugS6uAUKYo8a5XdPmZ+VZGayIXQSHzdSFKMh+JAbNMd5f1z3fzjFZbxI+MxLsrx+PM6V+QsmIZ+vRqh5w9i7Bq2Yt2DemMFBcX4/z58xb3cQ7lyudcbuv+NifNRVnPR5LKYM1aFVvITKXC8HiZsmALCvvz2qIl+rQE+VmmtRuR8x5mrkqFoSLTqtEyMSJdn07ekPH09MD4F5/AkR+W4mrBu0j/IBGdw+ue+WqJ9PR0YZE452piMG5NvD1w9OhRtG7dGosXL3ZJS9a7IslsUmhOAP17ZXp2uHnSDhuZrfI8a8fI7QEtmKY9QQyp+OLqPHohptJKxV21dmeVc7l4iViRRxseSpjNKoeSrxiGpCz72P5l5rUduKAKdPO5saOiRJ5SUUnV4WVjwBnPRW/evIlJkybhuT9U4OtPg5CVHlRl/18nNRfbxw3zQVJSEg4dsu5BdBYucX9byyaVcw0Ow+Twz7hGwgatR9cvm2UsR85n9Bm0v+1xJ+/ImIdkaS0GqSRSoY2ZrNCWXKZFosXRDwUlPIf3SYsllZF1lstyqYUya0e59g8uJVcrkPWf6v9hW17huuBVhysSh0zGoZatbFKj+5tCKRdO0cO5DoWWHjO5wpDxWhRYsUbE6UsWM2jp3h6rzXkkMsOU6eOsCxWgchHLPPHOc6SF0mez8h54L7wHca4h85fnyOM5DKUlnC48eZXrPkh3uLMzWRs6jPieNWuWiKn7YOvdF+0fN26ccHbUN26ZIWste1Wf8cplr9i7G/OKKPycyOvXv7OGLE9lytYP/F3tyZBlnJ01ZwMJDAwUcXe1RWXIKhoU3t5edsXjNW/eXLwcDevk5+tjV6n39J8xy8gGYySEwjnEDn8TX/3rJ2RmZuKZZ55xi1amY6J3796IHhCOTzcm1OAMy6h/NVfUG7k/nxGOJP+AIDHfYZBpXYZijoDPnT788EMRDGu6dgVZ22chPKx1nUtWiqSoV46dOI9xL72Hb3MqVwBiSkZt4Z8wjxgxAsuXL7cZbX7u3Dn06dNHhC3pYTgT/wAgrOP94mFyVJ/f2dUMSpEU9U5Z2S189+MJFJy9gitXav/Xmbk//w//3JCF4ODWSEhIwOzZs60em5eXJxaAHBz9CGKiHzFv9/NritD2gXj80Y4OCV9SzgZFvdO0qTeesNMCJE4ZhL8lfYx58+YKbx0f2N6NRyM6YOrEp5x2q0qRFA0Szmcyt/wF/82/hNAObVFUmIfVa9MwePBguxeyrAsq1VzRoOkQEgiP8vP49cKXWJO6Aj179kRsbCwKCgrEbZlM9ROGpRRJ0SgIvt8fJw6uwOsLhyH34LdiPfBBgwYhOjoaXl6eeKSL/X8RdDeUs0HR6Ci8/CtS392Nnw6fQZvgAAx5uhueHtjVqbepFEmhcABqaKdQOAClSAqFA1CKpFA4AKVICoW9APg/D8mm9QU1lNwAAAAASUVORK5CYII="" />
	|<br/> %1 <a style = ""color:rgb(25, 85, 174)"" href=""UNF_BannerNewsInformCenter"">%2</a>
	|</div>", НСтр("ru = 'Вопросы по приложению, служба поддержки, центр идей, все сообщения'"), НСтр("ru = 'по ссылке'"));
	
	ТекстНовостиИнформация = Справочники.Новости.ПолучитьХТМЛТекстНовостей(ИнформацияИПоддержка.Ссылка, Новый Структура("ОтображатьЗаголовок", Ложь));
	НачалоТекста = СтрНайти(ТекстНовостиИнформация,"<div id=""newsText"">");
	КонецТекста = СтрНайти(ТекстНовостиИнформация,"</div>",,НачалоТекста);
	СтрокаДляЗамены = Сред(ТекстНовостиИнформация, НачалоТекста, КонецТекста - НачалоТекста);
	
	ИнформацияИПоддержка.ТекстНовостиХТМЛ = СтрЗаменить(ТекстНовостиИнформация, СтрокаДляЗамены, ИнформацияИПоддержка.ТекстНовости);
	
	ТекстЗаголовка  = "";
	
	СтрокаЗаголовок = СтрШаблон("<h1 id=""newsHeader""> %1 </h1>", ТекстЗаголовка);
	НачалоЗаголовка = СтрНайти(ТекстНовостиИнформация,"<h1 id=""newsHeader"">");
	КонецЗаголовка  = СтрНайти(ТекстНовостиИнформация,"</h1>",,НачалоЗаголовка);
	СтрокаДляЗамены = Сред(ТекстНовостиИнформация, НачалоЗаголовка, КонецЗаголовка - НачалоЗаголовка);
	
	ИнформацияИПоддержка.ТекстНовостиХТМЛ = СтрЗаменить(ИнформацияИПоддержка.ТекстНовостиХТМЛ, 
		СтрокаДляЗамены, 
		СтрокаЗаголовок);
		
	ТекстДаты = Формат(ТекущаяДатаСеанса(),"ДЛФ = ДД");
	
	СтрокаДата = "<div id=""importanceData""> </div>";
	НачалоДаты = СтрНайти(ТекстНовостиИнформация,"<div id=""importanceData"">");
	КонецДаты  = СтрНайти(ТекстНовостиИнформация,"</div>",,НачалоДаты);
	СтрокаДляЗамены = Сред(ТекстНовостиИнформация, НачалоДаты, КонецДаты + 6 - НачалоДаты);
	
	ИнформацияИПоддержка.ТекстНовостиХТМЛ = СтрЗаменить(ИнформацияИПоддержка.ТекстНовостиХТМЛ, 
		СтрокаДляЗамены, 
		"");
		
	НачалоЛинк = СтрНайти(ИнформацияИПоддержка.ТекстНовостиХТМЛ,"<div id=""newsHyperlink"">");
	КонецЛинк  = СтрНайти(ИнформацияИПоддержка.ТекстНовостиХТМЛ,"</div>",,НачалоЛинк);
	СтрокаДляЗамены = Сред(ИнформацияИПоддержка.ТекстНовостиХТМЛ, НачалоЛинк, КонецЛинк + 6 - НачалоЛинк);
	
	ИнформацияИПоддержка.ТекстНовостиХТМЛ = СтрЗаменить(ИнформацияИПоддержка.ТекстНовостиХТМЛ, 
		СтрокаДляЗамены, 
		"");

	НачалоСтили = СтрНайти(ИнформацияИПоддержка.ТекстНовостиХТМЛ,"/* Здесь можно вставить свои стили");
	КонецСтили = СтрНайти(ИнформацияИПоддержка.ТекстНовостиХТМЛ,"*/",,НачалоСтили);
	СтрокаДляЗамены = Сред(ИнформацияИПоддержка.ТекстНовостиХТМЛ, НачалоСтили, КонецСтили + 2 - НачалоСтили);
		
	Если НЕ ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		ТекстХтмл = СтрЗаменить(ИнформацияИПоддержка.ТекстНовостиХТМЛ, 
		СтрокаДляЗамены, 
		"body {
		|padding: 0;
		|margin: 0;
		|}");
	Иначе
		ТекстХтмл = СтрЗаменить(ИнформацияИПоддержка.ТекстНовостиХТМЛ, 
		СтрокаДляЗамены, 
		"body {
		|padding-left: 45px;
		|margin-right: 30px;
		|text-align:centr;
		|width: 200px;}");
	КонецЕсли;
	
	ИнформацияИПоддержка.ТекстНовостиХТМЛ = ТекстХтмл;
	
КонецПроцедуры

&НаСервере
Функция СтрокаБезПереносов(ИсходнаяСтрока)
	
	// Для того чтобы строка правильно переносилась в веб-клиенте,
	// вместо запятой используем символ - U+201A:Single Low-9 Quotation Mark (Keystroke: Alt+0130)
	ИсходнаяСтрока = СтрЗаменить(ИсходнаяСтрока, ",", "‚");
	Возврат СтрЗаменить(ИсходнаяСтрока, " ", Символы.НПП);
	
КонецФункции

&НаСервере
Функция МаксимальноеКоличествоНовостей(ЛентаНовостей)
	
	Если ЛентаНовостей = ОбработкаНовостейПовтИсп.ПолучитьЛентуНовостейПоКоду("WhatIsNew") Тогда
		Возврат 1;
	Иначе
		Возврат 5;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ВыполнитьАвтолистание()
	
	Если Новости.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Увеличить счетчик текущей новости.
	ЭтотОбъект.ИндексТекущейНовости = ЭтотОбъект.ИндексТекущейНовости + 1;
	Если ЭтотОбъект.ИндексТекущейНовости > (ЭтотОбъект.КоличествоНовостейДляЛистания - 1) Тогда
		ЭтотОбъект.ИндексТекущейНовости = 0;
	КонецЕсли;

	// Вывести текст текущей новости.
	ЭтотОбъект.ТекстТекущейНовостиХТМЛ = ЭтотОбъект.Новости.Получить(ЭтотОбъект.ИндексТекущейНовости).ТекстНовостиХТМЛ;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АвтообновлениеСпискаНовостей()

	ОбновитьСписокНовостейСервер();

	// Исправить количество новостей для листания.
	ЭтотОбъект.КоличествоНовостейДляЛистания = Мин(ЭтотОбъект.Новости.Количество(), ЭтотОбъект.КоличествоНовостейДляЛистанияМаксимум);

	// Подключить автолистание.
	ЭтотОбъект.ОтключитьОбработчикОжидания("Подключаемый_ВыполнитьАвтолистание");
	Если ЭтотОбъект.КоличествоНовостейДляЛистания > 1 Тогда
		ЭтотОбъект.ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьАвтолистание", ЭтотОбъект.ЧастотаАвтолистания, Ложь);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НажатиеНаНовость(Элемент)
	
	Фильтр = Новый Структура;
	Фильтр.Вставить("ИмяЭлементаФормы", Элемент.Имя);
	
	МассивСтрок = ТаблицаСообщенийСервиса.НайтиСтроки(Фильтр);
	Если МассивСтрок.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущееСообщение = МассивСтрок.Получить(0); // ДанныеФормыЭлементКоллекции
	ИдентификаторСообщения = ТекущееСообщение.Идентификатор;
	
	Если ТекущееСообщение.ТипИнформации = "Недоступность" Тогда 
		
		ВнешняяСсылка = ТекущееСообщение.ВнешняяСсылка;
		
		Если Не ПустаяСтрока(ВнешняяСсылка) Тогда 
			ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ВнешняяСсылка);
			Возврат;
		КонецЕсли;
		
		ИнформационныйЦентрКлиент.ПоказатьНовость(ИдентификаторСообщения);
		
	ИначеЕсли ТекущееСообщение.ТипИнформации = "УведомлениеОПожелании" Тогда 
		
		ИдентификаторИдеи = Строка(ИдентификаторСообщения);
		
		ИнформационныйЦентрКлиент.ПоказатьИдею(ИдентификаторИдеи);
		
	ИначеЕсли ТекущееСообщение.ТипИнформации = "Новость" Тогда
		
		ИнформационныйЦентрКлиент.ПоказатьНовость(ИдентификаторСообщения);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуМобильныйКлиент()
	
	Если НЕ ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ТекстТекущейНовостиХТМЛ.РастягиватьПоГоризонтали = Истина;
	Элементы.ТекстТекущейНовостиХТМЛ.Ширина = 50;
	Элементы.ТекстТекущейНовостиХТМЛ.МаксимальнаяШирина = 50;
	Элементы.СтраницыРабочегоСтола.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Центр;
	
КонецПроцедуры

#КонецОбласти