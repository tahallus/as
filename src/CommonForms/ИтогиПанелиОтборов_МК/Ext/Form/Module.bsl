
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Итоги
	РасчетыРаботаСФормамиВызовСервера.НастроитьПанельИтоговПриСозданииНаСервере(ЭтаФорма, Параметры.ИмяФормыРодителя, Истина);
	// Конец Итоги
	
	// Загрузка параметров итогов
	Если Параметры <> Неопределено Тогда
		 
		ИмяФормыРодителя = Параметры.ИмяФормыРодителя;
		
		Если Параметры.Свойство("ВариантОтображенияИтогов") Тогда
			ВариантОтображенияИтогов = Параметры.ВариантОтображенияИтогов;
			ОтборВалютаВедомости 	 = Параметры.ОтборВалютаВедомости;
			ОтборВалютаОстатков 	 = Параметры.ОтборВалютаОстатков;
			ОтборПериод				 = Параметры.ОтборПериод;
			ПредставлениеПериода = РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(ОтборПериод);
		КонецЕсли;
		
		Если Параметры.ТекущаяСтрокаОтбор <> Неопределено Тогда
			ТекущаяСтрокаОтбор 		 = Новый Структура("Дата,СчетКасса,БанковскийСчетКасса,Валюта,Ссылка");
			ЗаполнитьЗначенияСвойств(ТекущаяСтрокаОтбор, Параметры.ТекущаяСтрокаОтбор); 
		КонецЕсли; 
	КонецЕсли;
	
	НастройкиИзменены = Ложь;
	НастройкаИтоговЗавершениеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ТекущаяСтрокаОтбор <> Неопределено И ВариантОтображенияИтогов = ВедомостьЗаДень Тогда
		РасчетыРаботаСФормамиКлиент.ОбновлениеИтоговПриАктивизацииСтрокиСписка(ЭтаФорма, ТекущаяСтрокаОтбор, ИмяФормыРодителя, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область Итоги

&НаКлиенте
Процедура НастройкаИтогов(Команда)
	
	СтруктураПараметров = Новый Структура();
	
	СтруктураПараметров.Вставить("ВариантОтображенияИтогов", ВариантОтображенияИтогов);
	СтруктураПараметров.Вставить("ВалютаВедомости", ОтборВалютаВедомости);
	СтруктураПараметров.Вставить("ВалютаОстатков", ОтборВалютаОстатков);
	
	НастройкиИзменены = Истина;
	
	ОткрытьФорму("ОбщаяФорма.ФормаНастройкиИтоговБанкИКасса", 
		СтруктураПараметров, 
		ЭтотОбъект, 
		УникальныйИдентификатор,,,
		Новый ОписаниеОповещения("НастройкаИтоговЗавершение", ЭтотОбъект), 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаИтоговЗавершение(РезультатЗавершения, ПараметрыЗавершения) Экспорт
	
	Если ТипЗнч(РезультатЗавершения) = Тип("Структура") Тогда
		
		ВариантОтображенияИтогов = РезультатЗавершения.ВариантОтображенияИтогов;
		ОтборВалютаВедомости = РезультатЗавершения.ВалютаВедомости;
		ОтборВалютаОстатков = РезультатЗавершения.ВалютаОстатков;
		
		НастройкаИтоговЗавершениеНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастройкаИтоговЗавершениеНаСервере()
	
	Если ИмяФормыРодителя <> "ПланированиеДенег" Тогда
		РасчетыРаботаСФормамиВызовСервера.НастройкаИтоговЗавершениеНаСервере(ЭтаФорма, Истина);
	КонецЕсли; 
	
КонецПроцедуры
	
#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Дата1Нажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элементы.БанковскиеВыписки.ТекущиеДанные <> Неопределено Тогда
		ПериодОтчета = Новый СтандартныйПериод;
		ПериодОтчета.ДатаНачала = Элементы.БанковскиеВыписки.ТекущиеДанные.Дата;
		ПериодОтчета.ДатаОкончания = Элементы.БанковскиеВыписки.ТекущиеДанные.Дата;
		ПараметрыОтчета = Новый Структура("СтПериод", ПериодОтчета);
	Иначе
		ПериодОтчета = Новый СтандартныйПериод;
		ПериодОтчета.ДатаНачала = '00010101';
		ПериодОтчета.ДатаОкончания = '00010101';
		ПараметрыОтчета = Новый Структура("СтПериод", ПериодОтчета);
	КонецЕсли;
	
	ОткрытьФорму("Отчет.ДенежныеСредства.Форма", Новый Структура("КлючВарианта, СформироватьПриОткрытии, Отбор", "ВедомостьВВалюте", Истина, ПараметрыОтчета));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВалютаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НастройкаИтогов(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВалюта1Нажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НастройкаИтогов(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкаВыводаИтоговНажатие(Элемент)
	
	НастройкаИтогов(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОстатокВВалютеНажатие(Элемент)
	
	//НастройкаИтогов(Неопределено);
	ПериодОтчета = Новый СтандартныйПериод;
	ПериодОтчета.ДатаНачала = ТекущаяДата(); //Элементы.БанковскиеВыписки.ТекущиеДанные.Дата;
	ПериодОтчета.ДатаОкончания = ТекущаяДата(); //Элементы.БанковскиеВыписки.ТекущиеДанные.Дата;
	ПараметрыОтчета = Новый Структура("СтПериод, Валюта", ПериодОтчета, ОтборВалютаОстатков);
	
	ОткрытьФорму("Отчет.ДенежныеСредства.Форма", Новый Структура("КлючВарианта, СформироватьПриОткрытии, Отбор", "ОстаткиВВалюте", Истина, ПараметрыОтчета));
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВедомостьПоВалютеНажатие(Элемент)
	
	//НастройкаИтогов(Неопределено);
	ПараметрыОтчета = Новый Структура("СтПериод, Валюта", ОтборПериод, ОтборВалютаВедомости);
	ОткрытьФорму("Отчет.ДенежныеСредства.Форма", Новый Структура("КлючВарианта, СформироватьПриОткрытии, Отбор", "ВедомостьВВалюте", Истина, ПараметрыОтчета));
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОткрытьФормуНастройкиИтоговНажатие(Элемент)
	
	НастройкаИтогов(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НастройкиИзменены Тогда
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ВариантОтображенияИтогов", ВариантОтображенияИтогов);
		СтруктураПараметров.Вставить("ОтборВалютаВедомости", 	 ОтборВалютаВедомости);
		СтруктураПараметров.Вставить("ОтборВалютаОстатков", 	 ОтборВалютаОстатков);
		Оповестить("НастройкиИтоговИзменены", СтруктураПараметров);
	КонецЕсли; 
	  
КонецПроцедуры

#КонецОбласти
