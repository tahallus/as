#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Валюта") Тогда
		Валюта = Параметры.Валюта;
	Иначе
		Валюта = Параметры.ДоговорКонтрагента.ВалютаРасчетов;
	КонецЕсли;
	
	КратностьВалюты = Параметры.КратностьВзаиморасчетов;
	КурсВалюты      = Параметры.КурсВзаиморасчетов;
	
	Если Параметры.Свойство("ДокументРасчетов") И ЗначениеЗаполнено(Параметры.ДокументРасчетов) Тогда
		ДатаДокументаРасчетов = Параметры.ДокументРасчетов.Дата;
	Иначе
		ДатаДокументаРасчетов = Неопределено;
	КонецЕсли;
	
	ДатаДокумента = Параметры.Дата;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РасчетыРаботаСФормамиКлиент.ЗаполнитьСписокВыбораВалютЭлементаФормы(Элементы.КурсВалюты, Валюта, ДатаДокумента, ДатаДокументаРасчетов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	// Заполним структуру возвращаемых параметров
	СтруктураВозвращаемыхЗначений = Новый Структура();
	СтруктураВозвращаемыхЗначений.Вставить("КратностьВалюты", КратностьВалюты);
	СтруктураВозвращаемыхЗначений.Вставить("КурсВалюты",      КурсВалюты);

	// Все сделали, можно закрывать форму
	Закрыть(СтруктураВозвращаемыхЗначений);

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КурсВалютыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = 0 Тогда
		СтандартнаяОбработка = Ложь;
		ОбработчикОповещенияОЗакрытии = Новый ОписаниеОповещения("КурсРасчетовОбработкаВыбораЗавершение", ЭтотОбъект);
		ПоказатьВводДаты(ОбработчикОповещенияОЗакрытии, ДатаДокумента, "Укажите дату курса валюты", ЧастиДаты.Дата);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура КурсРасчетовОбработкаВыбораЗавершение(ДатаКурса, ДополнительныеПараметры) Экспорт
	
	Если ДатаКурса <> Неопределено Тогда
		КурсРасчетовОбработкаВыбораНаСервере(ДатаКурса);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КурсРасчетовОбработкаВыбораНаСервере(ДатаКурса)
	
	КурсНаДату = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, ДатаКурса);
	ЭтаФорма.КурсВалюты = КурсНаДату.Курс;
	ЭтаФорма.КратностьВалюты = КурсНаДату.Кратность;
	
КонецПроцедуры

#КонецОбласти


