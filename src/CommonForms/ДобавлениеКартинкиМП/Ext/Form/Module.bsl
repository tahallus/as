
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипКартинки = Параметры.ТипКартинки;
	ОбновитьКартинку();
	
	#Если Не МобильноеПриложениеСервер Тогда
		Элементы.КартинкаСсылка.РастягиватьПоВертикали = Ложь;
		Элементы.КартинкаУдалить.Ширина = 2;
	#КонецЕсли 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура КартинкаДобавитьНажатие(Элемент)
	
	ОповещениеОДобавлении = Новый ОписаниеОповещения("ДобавитьКартинкуНаСервере", ЭтотОбъект);
	ОбщегоНазначенияМПКлиент.ДобавитьФото(ОповещениеОДобавлении);
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаУдалитьНажатие(Элемент)
	
	УдалитьКартинкуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьКартинкуНаСервере()
	
	Если ТипКартинки = "ФаксимилеРуководителя" Тогда
		Константы.ФаксимилеРуководителяМП.Установить(Неопределено);
	ИначеЕсли ТипКартинки = "ФаксимилеБухгалтера" Тогда
		Константы.ФаксимилеБухгалтераМП.Установить(Неопределено);
	КонецЕсли;
	ОбновитьКартинку();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКартинку()
	
	Если ТипКартинки = "ФаксимилеРуководителя" Тогда
		КартинкаДвоичныеДанные = Константы.ФаксимилеРуководителяМП.Получить().Получить();
	ИначеЕсли ТипКартинки = "ФаксимилеБухгалтера" Тогда
		КартинкаДвоичныеДанные = Константы.ФаксимилеБухгалтераМП.Получить().Получить();
	КонецЕсли;
	
	Если КартинкаДвоичныеДанные <> Неопределено Тогда
		КартинкаСсылка = ПоместитьВоВременноеХранилище(КартинкаДвоичныеДанные);
	Иначе
		КартинкаСсылка = "";
	КонецЕсли;
	
	УстановитьПараметрыКартинки();

КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыКартинки()
	
	Если ЗначениеЗаполнено(КартинкаСсылка) Тогда
		Элементы.ГруппаКартинкаСсылка.Видимость = Истина;
		Элементы.КартинкаДобавить.Видимость = Ложь;
	Иначе
		Элементы.ГруппаКартинкаСсылка.Видимость = Ложь;
		Элементы.КартинкаДобавить.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКартинкуНаСервере(Результат, Адрес, ИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		
		АдресВременногоХранилища= Адрес;
		ИмяВыбранногоФайла 		= ИмяФайла;
		Расширение 				= ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ИмяВыбранногоФайла));
		
		Если Расширение = "bmp"
			ИЛИ Расширение = "gif"
			ИЛИ Расширение = "png"
			ИЛИ Расширение = "jpeg"
			ИЛИ Расширение = "dib"
			ИЛИ Расширение = "rle"
			ИЛИ Расширение = "tif"
			ИЛИ Расширение = "jpg"
			ИЛИ Расширение = "ico"
			ИЛИ Расширение = "wmf"
			ИЛИ Расширение = "emf" Тогда
			
			Если ПолучитьИзВременногоХранилища(АдресВременногоХранилища).Размер() > 5000000 Тогда
				ТекстПредупреждения = НСтр("ru ='Размер не должен превышать 5 МБ.'");
				ПоказатьПредупреждение(, ТекстПредупреждения); 
				Возврат;
			КонецЕсли;
			
			СохранитьИОтобразитьКартинку(АдресВременногоХранилища);
			
		Иначе
			
			ТекстПредупреждения = НСтр("ru ='Данный формат не поддерживается.'");
			ПоказатьПредупреждение(, ТекстПредупреждения); 
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьИОтобразитьКартинку(Знач АдресВременногоХранилища)
	
	Перем ДвоичныеДанные;
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	Если ТипКартинки = "ФаксимилеРуководителя" Тогда
		Константы.ФаксимилеРуководителяМП.Установить(Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9)));
	ИначеЕсли ТипКартинки = "ФаксимилеБухгалтера" Тогда
		Константы.ФаксимилеБухгалтераМП.Установить(Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9)));
	КонецЕсли;
	
	ОбновитьКартинку();

КонецПроцедуры

#КонецОбласти
