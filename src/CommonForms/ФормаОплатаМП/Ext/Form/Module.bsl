
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Сбор статистики
	СборСтатистикиМПКлиентСерверПереопределяемый.ОтправитьОткрытиеЭкранаВGA(ЭтаФорма.ИмяФормы);
	// Конец Сбор статистики
	#Если МобильноеПриложениеСервер Тогда
		
		ОборудованиеПечати = ОбщегоНазначенияМПВызовСервера.ПолучитьЗначениеКонстанты("ОборудованиеПечати");
		ОборудованиеПлатежнойСистемы = ОбщегоНазначенияМПВызовСервера.ПолучитьЗначениеКонстанты("ОборудованиеПлатежнойСистемы");
		
	#КонецЕсли
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЧекККМ.СлипЧек,
	|	ЧекККМ.ДатаОперацииЭТ
	|ИЗ
	|	Документ.ЧекККММП КАК ЧекККМ
	|ГДЕ
	|	ЧекККМ.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Параметры.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		РезультатОперацииПоПлатежнойСистеме = Новый Структура("СлипЧек, ДатаОперации", Выборка.СлипЧек.Получить(), Выборка.ДатаОперацииЭТ);
	КонецЕсли;
	
	ПолучитьЗначенияПараметровФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РассчитатьСдачу();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	// Сбор статистики
	СборСтатистикиМПКлиентПереопределяемый.ОтправитьДействиеВGA(ЭтаФорма.ИмяФормы + ".Закрытие",,,ЗавершениеРаботы);
	// Конец Сбор статистики
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура НаличнымиПриИзменении(Элемент)
	
	РассчитатьСдачу();
	
КонецПроцедуры

&НаКлиенте
Процедура КартойПриИзменении(Элемент)
	
	РассчитатьСдачу();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПробитьЧек(Команда)
	
	// Сбор статистики
	СборСтатистикиМПКлиентСерверПереопределяемый.ОтправитьДействиеВGA(ЭтаФорма.ИмяФормы + ".Команда." + Команда.Имя);
	// Конец Сбор статистики

	Если СуммаНаличными + СуммаКартой < КОплате Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru='Ошибка. Сумма принятых денег меньше суммы к оплате!'"),,ОбщегоНазначенияМПВызовСервераПовтИсп.ПолучитьСинонимКонфигурации());
		Возврат;
	КонецЕсли;
	
	ЗаблокироватьФорму();
	ОтобразитьПустуюСтраницуПечатиЧека();
	
	#Если МобильноеПриложениеКлиент Тогда
		ОборудованиеПлатежнойСистемы = ОбщегоНазначенияМПВызовСервера.ПолучитьЗначениеКонстанты("ОборудованиеПлатежнойСистемы");
	#Иначе
		ОборудованиеПлатежнойСистемы = Неопределено;
	#КонецЕсли
	
	ОплатаПоПлатежнойСистемеПроведена = ЗначениеЗаполнено(ВладелецФормы.ПолучитьСсылочныйНомер());
	
	Если СуммаКартой > 0 Тогда
		Если ЗначениеЗаполнено(ОборудованиеПлатежнойСистемы) Тогда
			Если ОплатаПоПлатежнойСистемеПроведена Тогда
				ПоказатьРаспечататьСлипЧек();
			Иначе
				ОтобразитьСтраницуПроцесса();
				Если ЭтоВозврат Тогда
					ПодключитьОбработчикОжидания("Подключаемый_ВозвратитьОплатуПоПлатежнойСистеме", 0.1, Истина);
				Иначе
					ПодключитьОбработчикОжидания("Подключаемый_ОплатитьПоПлатежнойСистеме", 0.1, Истина);
				КонецЕсли;
			КонецЕсли;
		Иначе
			ОписаниеОповещения = Новый ОписаниеОповещения("ОповещениеПробитьЧекВопрос", ЭтотОбъект);
			ТекстВопроса = НСтр("ru = 'Операция оплаты на эквайринговом терминале прошла успешно?'");
			ПоказатьВопрос(
				ОписаниеОповещения,
				ТекстВопроса,
				РежимДиалогаВопрос.ДаНет,
				,
				,
				НСтр("ru = 'Подтверждение'")
			);
		КонецЕсли;
	Иначе
		ОтобразитьПечатьЧека();
		ПодключитьОбработчикОжидания("Подключаемый_ПробитьЧек", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОплату(Команда)
	
	// Сбор статистики
	СборСтатистикиМПКлиентСерверПереопределяемый.ОтправитьДействиеВGA(ЭтаФорма.ИмяФормы + ".Команда." + Команда.Имя);
	// Конец Сбор статистики

	СуммаНаличными = КОплате - СуммаКартой;
	РассчитатьСдачу();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОплатуКартой(Команда)
	
	// Сбор статистики
	СборСтатистикиМПКлиентСерверПереопределяемый.ОтправитьДействиеВGA(ЭтаФорма.ИмяФормы + ".Команда." + Команда.Имя);
	// Конец Сбор статистики

	СуммаКартой = КОплате - СуммаНаличными;
	РассчитатьСдачу();
	
КонецПроцедуры

#КонецОбласти

#Область ОтображениеСтраниц

&НаКлиенте
Процедура ОтобразитьПустуюСтраницуПечатиЧека()
	
	Элементы.СтраницаПечатьЧекаОшибка.Видимость = Ложь;
	Элементы.СтраницыПечатьЧека.Видимость = Ложь;
	Элементы.СтраницыПечатьЧека.ТекущаяСтраница = Элементы.СтраницаПечатьЧекаПустая;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьПечатьСлипЧека()
	
	Если ЗначениеЗаполнено(ОборудованиеПечати) Тогда
		Элементы.СтраницыПечатьЧека.Видимость = Истина;
		Элементы.СтраницыПечатьЧека.ТекущаяСтраница = Элементы.СтраницаПечатьЧекаПечатьСлип;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьОшибкуПечатиСлипЧека(ОписаниеОшибки)
	
	ОшибкаПечати = ОписаниеОшибки;
	Если ЗначениеЗаполнено(ОборудованиеПечати) Тогда
		Элементы.СтраницаПечатьЧекаОшибкаСлип.Видимость = Истина;
		Элементы.СтраницыПечатьЧека.Видимость = Истина;
		Элементы.СтраницыПечатьЧека.ТекущаяСтраница = Элементы.СтраницаПечатьЧекаОшибкаСлип;
	КонецЕсли;
	
	Элементы.ОтменитьОплату.Видимость = Истина;
	РазблокироватьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьПечатьЧека()
	
	Элементы.СтраницыПечатьЧека.Видимость = Истина;
	Элементы.СтраницыПечатьЧека.ТекущаяСтраница = Элементы.СтраницаПечатьЧекаПечать;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьОшибкуПечатиЧека(ОписаниеОшибки)
	
	ОшибкаПечати = ОписаниеОшибки;
	Если ЗначениеЗаполнено(ОборудованиеПечати) Тогда
		Элементы.СтраницаПечатьЧекаОшибка.Видимость = Истина;
		Элементы.СтраницыПечатьЧека.Видимость = Истина;
		Элементы.СтраницыПечатьЧека.ТекущаяСтраница = Элементы.СтраницаПечатьЧекаОшибка;
	КонецЕсли;
	
	РазблокироватьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьСтраницуПроцесса()
	
	СкрытьСтраницыПлатежнойСистемы();
	
	Элементы.СтраницаОплатаПроцесс.Видимость = Истина;
	Элементы.СтраницыПлатежнаяСистема.Видимость = Истина;
	Элементы.СтраницыПлатежнаяСистема.ТекущаяСтраница = Элементы.СтраницаОплатаПроцесс;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРаспечататьСлипЧек()
	
	ОтобразитьПечатьСлипЧека();
	ПодключитьОбработчикОжидания("Подключаемый_ПоказатьРаспечататьСлипЧек", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьСтраницыПлатежнойСистемы()
	
	ОбщегоНазначенияМПКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма, 
		"СтраницаОплатаПроцесс, СтраницаОплатаУспешно, СтраницаОперацияОшибка, СтраницаОтменаОплатыУспешно, СтраницаВозвратОплатыУспешно",
		"Видимость",
		Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьОплатаЗавершенаУспешно()
	
	СкрытьСтраницыПлатежнойСистемы();
	
	Элементы.СтраницаОплатаУспешно.Видимость = Истина;
	Элементы.СтраницыПлатежнаяСистема.ТекущаяСтраница = Элементы.СтраницаОплатаУспешно;
	Элементы.Картой.ТолькоПросмотр = Истина;
	ПолученоПлатежнаяКартаЗаблокировано = Истина;
	
	Элементы.ОтменитьОплату.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьОшибкаОперации()
	
	СкрытьСтраницыПлатежнойСистемы();
	
	Элементы.СтраницаОперацияОшибка.Видимость = Истина;
	Элементы.СтраницыПлатежнаяСистема.ТекущаяСтраница = Элементы.СтраницаОперацияОшибка;
	
	РазблокироватьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ПробитьЧек()
	
	ОборудованиеПечати = ОбщегоНазначенияМПВызовСервера.ПолучитьЗначениеКонстанты("ОборудованиеПечати");
	РезультатПечатиЧека = РозничныеПродажиМПКлиент.РезультатПечатиЧека();
	
	Если ЗначениеЗаполнено(ОборудованиеПечати) Тогда
		
		СтруктураПараметровДляПечати.ОбщиеПараметры.ТаблицаОплат.Очистить();
		
		Если ЗначениеЗаполнено(СуммаНаличными) Тогда
			СтрокаОплаты = Новый Структура("ТипОплаты, Сумма", ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Наличные"), СуммаНаличными);
			СтруктураПараметровДляПечати.ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СуммаКартой) Тогда
			СтрокаОплаты = Новый Структура("ТипОплаты, Сумма", ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Электронно"), СуммаКартой);
			СтруктураПараметровДляПечати.ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
		КонецЕсли;
		
		СтруктураПараметровДляПечати.ОбщиеПараметры.Вставить("ПокупательEmail", АдресЭП);
		СтруктураПараметровДляПечати.ОбщиеПараметры.Вставить("ПокупательНомер", Телефон);
		СтруктураПараметровДляПечати.ОбщиеПараметры.Вставить("ЭтоВозврат", ЭтоВозврат);
		
		СтруктураПараметровДляПечати.Вставить("ОборудованиеПечати", ОборудованиеПечати);
		РозничныеПродажиМПКлиент.ПечатьЧекаПродажи(СтруктураПараметровДляПечати, РезультатПечатиЧека);
		
	Иначе
		
		РезультатПечатиЧека.Успешно = Истина;
		РезультатПечатиЧека.НомерЧека = "1";
		
	КонецЕсли;
	
	Если РезультатПечатиЧека.Успешно Тогда
		ПараметрыЗакрытия = Новый Структура;
		ПараметрыЗакрытия.Вставить("Успешно", Истина);
		ПараметрыЗакрытия.Вставить("НомерЧека", РезультатПечатиЧека.НомерЧека);
		ПараметрыЗакрытия.Вставить("НомерСмены", РезультатПечатиЧека.НомерСмены);
		ПараметрыЗакрытия.Вставить("АдресEmailПокупателя", АдресЭП);
		ПараметрыЗакрытия.Вставить("НомерТелефонаПокупателя", Телефон);
		ПараметрыЗакрытия.Вставить("СуммаОплаты", СуммаНаличными - Сдача);
		ПараметрыЗакрытия.Вставить("СуммаКартой", СуммаКартой);
		
		Оплата = Новый Массив;
		
		Если ЗначениеЗаполнено(СуммаНаличными) Тогда
			СтруктураОплаты = Новый Структура();
			СтруктураОплаты.Вставить("ТипОплаты", ПредопределенноеЗначение("Перечисление.ТипыОплатыМП.Наличные"));
			СтруктураОплаты.Вставить("Сумма", СуммаНаличными - Сдача);
			Оплата.Добавить(СтруктураОплаты);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СуммаКартой) Тогда
			СтруктураОплаты = Новый Структура();
			СтруктураОплаты.Вставить("ТипОплаты", ПредопределенноеЗначение("Перечисление.ТипыОплатыМП.ПлатежнаяКарта"));
			СтруктураОплаты.Вставить("Сумма", СуммаКартой);
			СтруктураОплаты.Вставить("РезультатОперацииПоПлатежнойСистеме", РезультатОперацииПоПлатежнойСистеме);
			Оплата.Добавить(СтруктураОплаты);
		КонецЕсли;
		
		ПараметрыЗакрытия.Вставить("Оплата", Оплата);
		РазблокироватьФорму();
		Закрыть(ПараметрыЗакрытия);
	Иначе
		ОтобразитьОшибкуПечатиЧека(РезультатПечатиЧека.ОписаниеОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказатьРаспечататьСлипЧек()
	
	Если ЗначениеЗаполнено(ОборудованиеПечати) Тогда
		
		// функция разреза [cut] будет перенесена в мБПО, временное решение.
		
		МассивРазрезанныхСлипЧеков = ОбщегоНазначенияМПКлиентСервер.РазложитьСтрокуВМассивПодстрок(РезультатОперацииПоПлатежнойСистеме.СлипЧек, "[cut]", Ложь, Ложь);
		
		Для каждого РазрезанныйСлипЧек Из МассивРазрезанныхСлипЧеков Цикл
			
			ОчиститьСообщения();
			ВыходныеПараметры = Неопределено;
			ВходныеПараметры = Новый Структура("СтрокиТекста", РазрезанныйСлипЧек);
			
			#Если МобильноеПриложениеКлиент Тогда
				// АПК:488-выкл методы безопасного запуска обеспечиваются этой функцией
				МодульМенеджерОборудованияКлиент = Вычислить("МенеджерОборудованияКлиент");
				// АПК:488-вкл
				Если ТипЗнч(МодульМенеджерОборудованияКлиент) = Тип("ОбщийМодуль") Тогда
					
					Если МодульМенеджерОборудованияКлиент.ВыполнитьПечатьТекста(
						УникальныйИдентификатор,
						ОборудованиеПечати,
						ВходныеПараметры,
						ВыходныеПараметры) Тогда
						
						ОтобразитьПустуюСтраницуПечатиЧека();
					Иначе
						
						ОтобразитьОшибкуПечатиСлипЧека(ВыходныеПараметры.ТекстОшибки);
						Возврат;
						
					КонецЕсли;
				КонецЕсли;
			#КонецЕсли
			
		КонецЦикла;
		
		ПоказатьРаспечататьСлипЧекЗавершение(Неопределено, Неопределено);
		
	Иначе
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТекстСлипЧека", РезультатОперацииПоПлатежнойСистеме.СлипЧек);
		ПараметрыФормы.Вставить("ДатаОперации", РезультатОперацииПоПлатежнойСистеме.ДатаОперации);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьРаспечататьСлипЧекЗавершение", ЭтотОбъект);
		ОткрытьФорму("ОбщаяФорма.СлипЧекПлатежнойСистемыМП", ПараметрыФормы, ЭтаФорма,,,, ОписаниеОповещения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРаспечататьСлипЧекЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ВыполняемаяОперацияПлатежнойСистемы <> "ОтменаОплаты" Тогда
		ОтобразитьПечатьЧека();
		ПодключитьОбработчикОжидания("Подключаемый_ПробитьЧек", 0.1, Истина);
	Иначе
		РазблокироватьФорму();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеПробитьЧекВопрос(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат = КодВозвратаДиалога.Да Тогда
		РазблокироватьФорму();
		Возврат;
	КонецЕсли;
	
	ОтобразитьПечатьЧека();
	ПодключитьОбработчикОжидания("Подключаемый_ПробитьЧек", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОплатитьПоПлатежнойСистеме()
	
	ВыполняемаяОперацияПлатежнойСистемы = "Оплата";
	
	ПараметрыОплаты = РозничныеПродажиМПКлиент.ПараметрыОплатыПоПлатежнойСистеме();
	ПараметрыОплаты.Сумма = СуммаКартой;
	ПараметрыОплаты.УникальныйИдентификатор = УникальныйИдентификатор;
	ПараметрыОплаты.Оборудование = ОборудованиеПлатежнойСистемы;
	
	РезультатОперацииПоПлатежнойСистеме = РозничныеПродажиМПКлиент.ОплатитьПоПлатежнойСистеме(ПараметрыОплаты);
	
	ВладелецФормы.ПослеПолучениеРезультатаПоПлатежнойСистеме(СуммаКартой, РезультатОперацииПоПлатежнойСистеме);
	
	Если РезультатОперацииПоПлатежнойСистеме.Успешно Тогда
		ПоказатьРаспечататьСлипЧек();
		ОтобразитьОплатаЗавершенаУспешно();
	Иначе
		ОшибкаОплаты = РезультатОперацииПоПлатежнойСистеме.ТекстОшибки;
		ОтобразитьОшибкаОперации();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВозвратитьОплатуПоПлатежнойСистеме()
	
	ВыполняемаяОперацияПлатежнойСистемы = "ВозвратОплаты";
	
	ПараметрыВозвратаОплаты                     = РозничныеПродажиМПКлиент.ПараметрыВозвратаОплатыПоПлатежнойСистеме();
	ПараметрыВозвратаОплаты.Сумма               = СуммаКартой;
	ПараметрыВозвратаОплаты.УникальныйИдентификатор = УникальныйИдентификатор;
	ПараметрыВозвратаОплаты.Оборудование        = ОборудованиеПлатежнойСистемы;
	ПараметрыВозвратаОплаты.НомерКарты          = ДанныеОплатыПоПлатежнойКартеЧекаПродажи.НомерКарты;
	ПараметрыВозвратаОплаты.НомерСсылкиОперации = ДанныеОплатыПоПлатежнойКартеЧекаПродажи.НомерСсылкиОперации;
	ПараметрыВозвратаОплаты.КодАвторизации      = ДанныеОплатыПоПлатежнойКартеЧекаПродажи.КодАвторизации;
	ПараметрыВозвратаОплаты.НомерЧека           = СтруктураПараметровДляПечати.ОбщиеПараметры.НомерЧека;
	
	РезультатОперацииПоПлатежнойСистеме = РозничныеПродажиМПКлиент.ВозвратитьОплатуПоПлатежнойСистеме(ПараметрыВозвратаОплаты);
	
	ВладелецФормы.ПослеПолучениеРезультатаПоПлатежнойСистеме(СуммаКартой, РезультатОперацииПоПлатежнойСистеме);
	
	Если РезультатОперацииПоПлатежнойСистеме.Успешно Тогда
		ПоказатьРаспечататьСлипЧек();
		ОтобразитьВозвратОплатыУспешно();
	Иначе
		ОшибкаОплаты = РезультатОперацииПоПлатежнойСистеме.ТекстОшибки;
		ОтобразитьОшибкаОперации();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьВозвратОплатыУспешно()
	
	СкрытьСтраницыПлатежнойСистемы();
	
	Элементы.СтраницаВозвратОплатыУспешно.Видимость = Истина;
	Элементы.СтраницыПлатежнаяСистема.ТекущаяСтраница = Элементы.СтраницаВозвратОплатыУспешно;
	Элементы.Картой.ТолькоПросмотр = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаблокироватьФорму()
	
	ДоступноЗакрытиеФормы = Ложь;
	
	ОбщегоНазначенияМПКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма,
		"ФормаПробитьЧек", "Доступность",
		Ложь
	);

КонецПроцедуры

&НаКлиенте
Процедура РазблокироватьФорму()
	
	ДоступноЗакрытиеФормы = Истина;
	
	ОбщегоНазначенияМПКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма,
		"ФормаПробитьЧек", "Доступность",
		Истина
	);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСдачу()
	
	Сдача = СуммаКартой + СуммаНаличными - КОплате;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗначенияПараметровФормы()
	
	КОплате = Параметры.СуммаДокумента;
	СуммаНаличными = Параметры.СуммаОплаты;
	СуммаКартой = Параметры.СуммаКартой;
	АдресЭП = Параметры.АдресЭП;
	Телефон = Параметры.Телефон;
	ЭтоВозврат = Параметры.ЭтоВозврат;
	
	СтруктураПараметровДляПечати = Новый Структура;
	СтруктураПараметровДляПечати.Вставить("ОбщиеПараметры", Параметры.ОбщиеПараметры);
	СтруктураПараметровДляПечати.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	СтруктураПараметровДляПечати.Вставить("ЭтоВозврат", ЭтоВозврат);
	
	Если ЭтоВозврат Тогда
		ЭтаФорма.Заголовок = НСтр("ru='Возврат оплаты'");
		ДанныеОплатыПоПлатежнойКартеЧекаПродажи = Параметры.ДанныеОплатыПоПлатежнойКартеЧекаПродажи;
	Иначе
		ЭтаФорма.Заголовок = НСтр("ru='Прием оплаты'");
	КонецЕсли;
	
КонецПроцедуры // ПолучитьЗначенияПараметровФормы()

#КонецОбласти
