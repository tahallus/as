
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВариантОтображенияИтогов = Параметры.ВариантОтображенияИтогов;
	ВалютаВедомости = Параметры.ВалютаВедомости;
	ВалютаОстатков = Параметры.ВалютаОстатков;
	
	НастроитьЭлементыНаСервере();
	
	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантОтображенияИтоговПриИзменении(Элемент)
	
	НастроитьЭлементыНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если ВалютаВедомости.Пустая() И ВариантОтображенияИтогов = ПредопределенноеЗначение("Перечисление.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаПериод") Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Выберите валюту ведомости.'");
		Сообщение.Поле = "ВалютаВедомости";
		Сообщение.Сообщить();
		
		Возврат;
		
	ИначеЕсли ВалютаОстатков.Пустая() И ВариантОтображенияИтогов = ПредопределенноеЗначение("Перечисление.ВариантыОтображенияИтоговБанкИКасса.ОстаткиПересчитанныеВВалюту") Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Выберите валюту остатков.'");
		Сообщение.Поле = "ВалютаОстатков";
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрЗакрытия = Новый Структура("ВариантОтображенияИтогов, ВалютаВедомости, ВалютаОстатков", ВариантОтображенияИтогов, ВалютаВедомости, ВалютаОстатков);
	Закрыть(ПараметрЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьЭлементыНаСервере()
	
	Если ВариантОтображенияИтогов = Перечисления.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаПериод Тогда
		Элементы.ВалютаВедомости.Видимость = Истина;
		Элементы.ВалютаОстатков.Видимость = Ложь;
		Элементы.Декорация1.Высота = 1;
		Элементы.Декорация1.Видимость = Ложь;
		Элементы.Группа2.Высота = 3;
	ИначеЕсли ВариантОтображенияИтогов = Перечисления.ВариантыОтображенияИтоговБанкИКасса.ОстаткиПересчитанныеВВалюту Тогда
		Элементы.ВалютаВедомости.Видимость = Ложь;
		Элементы.ВалютаОстатков.Видимость = Истина;
		Элементы.Декорация1.Высота = 3;
		Элементы.Декорация1.Видимость = Истина;
		Элементы.Группа2.Высота = 0;
	Иначе
		Элементы.ВалютаВедомости.Видимость = Ложь;
		Элементы.ВалютаОстатков.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьПояснительнуюНадпись();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПояснительнуюНадпись()

	Если ВариантОтображенияИтогов = Перечисления.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаДень Тогда
		Элементы.ДекорацияПояснительнаяНадпись.Заголовок = НСтр("ru = 'Дата, р/счет и валюта определяются по текущему документу.'");
	ИначеЕсли ВариантОтображенияИтогов = Перечисления.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаПериод Тогда
		Элементы.ДекорацияПояснительнаяНадпись.Заголовок = НСтр("ru = 'Будет использоваться период журнала.'");
	ИначеЕсли ВариантОтображенияИтогов = Перечисления.ВариантыОтображенияИтоговБанкИКасса.ОстаткиПересчитанныеВВалюту Тогда
		Элементы.ДекорацияПояснительнаяНадпись.Заголовок = НСтр("ru = 'Пересчет выполняется по текущему курсу.'");
	Иначе
		Элементы.ДекорацияПояснительнаяНадпись.Заголовок = НСтр("ru = 'ИТОГИ В панели отображаться НЕ будут.'");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


