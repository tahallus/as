&НаКлиенте
Перем ШтрихкодДляПроверки;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	СброситьРазмерыИПоложениеОкна();
	
	СобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкиСобытийЭлементовФормы

&НаКлиенте
Процедура ШтрихкодИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	ШтрихкодДляПроверки = Текст;
	
	ПодключитьОбработчикОжидания("ПроверитьФорматШтрихкода", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Отказ = Ложь;
	ПроверитьЗаполнениеРеквизитов(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ФорматBase64", ФорматBase64);
	Результат.Вставить("Штрихкод",     Штрихкод);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ХранилищеСистемныхНастроек.Удалить("ОбщаяФорма.УточнениеСертификацииИС", "", ИмяПользователя);
	КонецЕсли;
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеРеквизитов(Отказ)
	
	ОчиститьСообщения();
	
	ШаблонСообщения = Нстр("ru='Поле ""%1"" не заполнено'");
	
	Если Не ЗначениеЗаполнено(Штрихкод) Тогда
		ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Штрихкод'"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "Штрихкод",, Отказ);
	КонецЕсли;
	
	Если ФорматBase64 Тогда
		РезультатРаспаковки = ШтрихкодированиеИСКлиентСервер.Base64ВШтрихкод(Штрихкод);
		ПримечаниеКРезультатуРазбора = Неопределено;
		РезультатРазбора = РазборКодаМаркировкиИССлужебныйКлиент.РазобратьКодМаркировки(РезультатРаспаковки, Неопределено, ПримечаниеКРезультатуРазбора);
		Если РезультатРазбора = Неопределено
			И ПримечаниеКРезультатуРазбора <> Неопределено
			И ЗначениеЗаполнено(ПримечаниеКРезультатуРазбора.ТекстОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ПримечаниеКРезультатуРазбора.ТекстОшибки,, "Штрихкод",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьФорматШтрихкода()
	
	ФорматИзменен = ШтрихкодированиеИСКлиент.ПроверитьФорматШтрихкода(ШтрихкодДляПроверки, ФорматBase64);
	
	Если ФорматИзменен Тогда
		Штрихкод = ШтрихкодДляПроверки;
		Элементы.Штрихкод.ОбновитьТекстРедактирования();
		ТекущийЭлемент = Элементы.ФорматBase64;
		ТекущийЭлемент = Элементы.Штрихкод;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти