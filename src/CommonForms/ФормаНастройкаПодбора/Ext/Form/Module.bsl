#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Пользователь = Пользователи.ТекущийПользователь();
	СпособВыводаОстатков = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь,
		"СпособВыводаОстатков");
	СпособВыводаОстатков = ?(ЗначениеЗаполнено(СпособВыводаОстатков), СпособВыводаОстатков,
		Перечисления.СпособВыводаОстатковВПодборе.ВТаблице);

	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ПоказыватьОстатки",
		УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ПоказыватьОстатки"));
	СтруктураНастроек.Вставить("ПоказыватьЦены",
		УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ПоказыватьЦены"));
	СтруктураНастроек.Вставить("СпособВыводаОстатков", СпособВыводаОстатков);
	СтруктураНастроек.Вставить("ЗапоминатьТекущуюИерархию",
		УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь,
		"ЗапоминатьТекущуюИерархию"));
				
	// Если резервирование выключено, то нет смысла работать с переключателями резерва и свободными остатками
	ЗначениеКонстантыРезервированиеЗапасов = ПолучитьФункциональнуюОпцию("РезервированиеЗапасов");

	СтруктураНастроек.Вставить("ПоказыватьРезерв", ?(ЗначениеКонстантыРезервированиеЗапасов,
		УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ПоказыватьРезерв"),
		Ложь));

	СтруктураНастроек.Вставить("ПоказыватьСвободныйОстаток", ?(ЗначениеКонстантыРезервированиеЗапасов,
		УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь,
		"ПоказыватьСвободныйОстаток"), Ложь));

	Элементы.ПоказыватьРезерв.Доступность = ЗначениеКонстантыРезервированиеЗапасов;
	Элементы.ПоказыватьСвободныйОстаток.Доступность = ЗначениеКонстантыРезервированиеЗапасов;
	
	// Заполним значения
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураНастроек);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьДоступностьПереключателей(ПоказыватьОстатки Или ПоказыватьРезерв Или ПоказыватьСвободныйОстаток
		Или ПоказыватьЦены);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьОстаткиПриИзменении(Элемент)

	УстановитьДоступностьПереключателей(ПоказыватьОстатки Или ПоказыватьРезерв Или ПоказыватьСвободныйОстаток
		Или ПоказыватьЦены);

КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьРезервПриИзменении(Элемент)

	УстановитьДоступностьПереключателей(ПоказыватьОстатки Или ПоказыватьРезерв Или ПоказыватьСвободныйОстаток
		Или ПоказыватьЦены);

КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьСвободныйОстатокПриИзменении(Элемент)

	УстановитьДоступностьПереключателей(ПоказыватьОстатки Или ПоказыватьРезерв Или ПоказыватьСвободныйОстаток
		Или ПоказыватьЦены);

КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьЦеныПриИзменении(Элемент)

	УстановитьДоступностьПереключателей(ПоказыватьОстатки Или ПоказыватьРезерв Или ПоказыватьСвободныйОстаток
		Или ПоказыватьЦены);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ЗаписатьНастройкиОтчета();
	Результат = Новый Структура;
	Результат.Вставить("ПоказыватьОстатки", ПоказыватьОстатки);
	Результат.Вставить("ПоказыватьРезерв", ПоказыватьРезерв);
	Результат.Вставить("ПоказыватьСвободныйОстаток", ПоказыватьСвободныйОстаток);
	Результат.Вставить("ПоказыватьЦены", ПоказыватьЦены);
	Результат.Вставить("СпособВыводаОстатков", СпособВыводаОстатков);
	Результат.Вставить("ЗапоминатьТекущуюИерархию", ЗапоминатьТекущуюИерархию);
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура записывает настройки подбора в пользовательские настройки
&НаСервере
Процедура ЗаписатьНастройкиОтчета()

	РегистрыСведений.НастройкиПользователей.Установить(ПоказыватьОстатки, "ПоказыватьОстатки");
	РегистрыСведений.НастройкиПользователей.Установить(ПоказыватьРезерв, "ПоказыватьРезерв");
	РегистрыСведений.НастройкиПользователей.Установить(ПоказыватьСвободныйОстаток,
		"ПоказыватьСвободныйОстаток");
	РегистрыСведений.НастройкиПользователей.Установить(ПоказыватьЦены, "ПоказыватьЦены");
	РегистрыСведений.НастройкиПользователей.Установить(СпособВыводаОстатков, "СпособВыводаОстатков");
	РегистрыСведений.НастройкиПользователей.Установить(ЗапоминатьТекущуюИерархию,
		"ЗапоминатьТекущуюИерархию");

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьПереключателей(ПризнакДоступности)

	Элементы.СпособВыводаОстатков.Доступность = ПризнакДоступности;

КонецПроцедуры

#КонецОбласти
