
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ПустаяКартинка = ПоместитьВоВременноеХранилище(БиблиотекаКартинок.ПросмотрНедоступен);
	ВариантОбразец = ПустаяКартинка;
	
	НастройкиФормы = Новый Структура;
	Контекст = Новый Структура;
	Контекст.Вставить("РежимНастройки", Параметры.Свойство("РежимНастройки") И Параметры.РежимНастройки);
	
	Если НЕ Контекст.РежимНастройки И (НЕ Параметры.Свойство("Настройки") ИЛИ НЕ Параметры.Свойство("АдресСхемы")) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Контекст.РежимНастройки И НЕ ЗначениеЗаполнено(Параметры.Вариант) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	Контекст.Вставить("КлючОбъекта", Параметры.КлючОбъекта);
	
	Если ТипЗнч(Параметры.КлючОбъекта) = Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных")
		Или ТипЗнч(Параметры.КлючОбъекта) = Тип("СправочникСсылка.ИдентификаторыОбъектовРасширений") Тогда
		ПолноеИмя = Параметры.КлючОбъекта.ПолноеИмя;
	ИначеЕсли ТипЗнч(Параметры.КлючОбъекта) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") Тогда
		ПолноеИмя = "ВнешнийОтчет."+Параметры.КлючОбъекта.ИмяОбъекта;
	Иначе
		ПолноеИмя = Параметры.КлючОбъекта;
	КонецЕсли;
	
	Контекст.Вставить("ПолноеИмя", ПолноеИмя);
	Контекст.Вставить("Прототип", Параметры.Вариант);
	Контекст.Вставить("КлючПрототипа", Параметры.КлючТекущихНастроек);
	Контекст.Вставить("ТекущийПользователь", Пользователи.ТекущийПользователь());
	Контекст.Вставить("ПолныеПраваНаВарианты", ВариантыОтчетов.ПолныеПраваНаВарианты());
	Контекст.Вставить("ЭтоМобильныйКлиент", ОбщегоНазначения.ЭтоМобильныйКлиент());
	ВариантИнтерфейса = ТекущийВариантИнтерфейсаКлиентскогоПриложения();
	Контекст.Вставить("ВариантИнтерфейса", ВариантИнтерфейса);
	Контекст.Вставить("ЭтоИнтерфейсТакси", ВариантИнтерфейса = ВариантИнтерфейсаКлиентскогоПриложения.Такси);
	Контекст.Вставить("ЭтоПолныеПрава", Пользователи.ЭтоПолноправныйПользователь());
	Контекст.Вставить("ЭтоДополнительныйОтчет", ТипЗнч(Параметры.КлючОбъекта)=Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки"));
	Контекст.Вставить("БезПривязки", Контекст.ЭтоДополнительныйОтчет);
	Контекст.Вставить("ИдентификаторТекущейСтроки");
	
	ПрочитатьДанныеОтчета();
	Если НЕ Контекст.РежимНастройки Тогда
		ЗаполнитьСписокВариантов();
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСохранение;
		ТекущаяСтраница = 1;
	Иначе
		ВариантСсылка = Контекст.Прототип;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаНастройки;
		ТекущаяСтраница = 2;
	КонецЕсли;
	ПрочитатьДанныеВарианта();
	
	Если Параметры.Свойство("Настройки") И Параметры.Свойство("АдресСхемы") Тогда
		АдресСхемы = Параметры.АдресСхемы;
		Настройки = Параметры.Настройки;
	ИначеЕсли ЗначениеЗаполнено(Контекст.ПолноеИмя) Тогда
		Если ТипЗнч(Контекст.КлючОбъекта) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") Тогда
			ОтчетОбъект = ДополнительныеОтчетыИОбработки.ОбъектВнешнейОбработки(Контекст.КлючОбъекта);
		Иначе
			ОтчетОбъект = Новый(СтрЗаменить(Контекст.ПолноеИмя, "Отчет.", "ОтчетОбъект."));
		КонецЕсли;
		Если НЕ ОтчетОбъект=Неопределено Тогда
			МетаданныеОтчета = ОтчетОбъект.Метаданные();
			Если НЕ МетаданныеОтчета.ОсновнаяСхемаКомпоновкиДанных=Неопределено Тогда
				Схема = ОтчетОбъект.ПолучитьМакет(МетаданныеОтчета.ОсновнаяСхемаКомпоновкиДанных);
				АдресСхемы = ПоместитьВоВременноеХранилище(Схема, УникальныйИдентификатор);
				Если Найти(МетаданныеОтчета.ПолноеИмя(), "ВнешнийОтчет.")>0 Тогда
					Если Параметры.Свойство("Настройки") Тогда
						Настройки = Параметры.Настройки;
					ИначеЕсли Контекст.РежимНастройки И (Контекст.Пользовательский ИЛИ Контекст.ЭтоДополнительныйОтчет) И ТипЗнч(Контекст.Прототип)=Тип("СправочникСсылка.ВариантыОтчетов") Тогда
						Настройки = Контекст.Прототип.Настройки.Получить();
					Иначе
						Настройки = Схема.ВариантыНастроек[Контекст.КлючПрототипа].Настройки;
					КонецЕсли;
				ИначеЕсли НЕ Контекст.Пользовательский Тогда
					МетаданныеОтчета = ОтчетОбъект.Метаданные();
					Настройки = Схема.ВариантыНастроек[Контекст.КлючПрототипа].Настройки;
				КонецЕсли; 
			КонецЕсли;
			Если ТипЗнч(Контекст.Прототип)=Тип("СправочникСсылка.ВариантыОтчетов") И Контекст.Пользовательский Тогда
				Настройки = Контекст.Прототип.Настройки.Получить();
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	
	Если НЕ АдресСхемы=Неопределено Тогда
		КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
		Если НЕ Настройки=Неопределено Тогда
			КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
		КонецЕсли; 
	КонецЕсли; 
	
	ОбновитьПанельКнопокДалееНазад(Элементы, Контекст);
	
	Если НЕ Контекст.РежимНастройки Тогда
		ВариантыОтчетаОбработчикАктивизацииСтроки();
	Иначе
		Если НЕ ЗначениеЗаполнено(ВариантАвтор) И НЕ ЗначениеЗаполнено(ВариантСсылка) Тогда
			// При сохранении нового варианта автором устанавливается текущий пользователь
			ВариантАвтор = Контекст.ТекущийПользователь;
		КонецЕсли;
		УстановитьДоступностьАвтора(ЭтотОбъект);
		Заголовок = НСтр("ru = 'Настройки отчета'");
	КонецЕсли; 
	
	Если НЕ Контекст.ЭтоВнешний Тогда
		Справочники.ТегиОтчетов.ОбновитьЭлементыТегов(ЭтотОбъект);
		ОбновитьЭлементыПривязок();
	КонецЕсли;
	
	Если Контекст.ЭтоВнешний Тогда
		КлючСохраненияПоложенияОкна = "ОбщаяФорма.СохранениеВариантаОтчетаУНФ/Внешний";
	ИначеЕсли Контекст.ЭтоДополнительныйОтчет Тогда 
		КлючСохраненияПоложенияОкна = "ОбщаяФорма.СохранениеВариантаОтчетаУНФ/Дополнительный";
	Иначе
		КлючСохраненияПоложенияОкна = "ОбщаяФорма.СохранениеВариантаОтчетаУНФ/";
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ВариантАвтор) Тогда 
		КлючСохраненияПоложенияОкна = КлючСохраненияПоложенияОкна + "Предопределенный";
	КонецЕсли; 
	
	РегистрыСведений.НастройкиВариантовОтчетов.ПрочитатьНастройкиДоступностиВариантаОтчета(
		ВариантСсылка, ПользователиВарианта, ИспользоватьГруппыПользователей, ИспользоватьВнешнихПользователей);
		
	ВариантыОтчетов.ОпределитьПоведениеСпискаПользователейВариантаОтчета(ЭтотОбъект);
	Если НЕ Контекст.Пользовательский И Контекст.РежимНастройки Тогда
		Элементы.ВариантНаименование.ТолькоПросмотр = Истина;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если НЕ ЗначениеЗаполнено(ВариантНаименование) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Поле ""Наименование"" не заполнено'"),
			,
			"Наименование");
		Отказ = Истина;
	ИначеЕсли ВариантыОтчетов.НаименованиеЗанято(Контекст.ОтчетСсылка, ВариантСсылка, ВариантНаименование) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			СтрШаблон(
				НСтр("ru = '""%1"" занято, необходимо указать другое Наименование.'"),
				ВариантНаименование
			),
			,
			"Наименование");
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ Контекст.РежимНастройки Тогда
		ТекущийЭлемент = Элементы.Наименование;
	КонецЕсли;
	
	ОформитьПользователейВариантаОтчета(Ложь);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если Контекст.РежимНастройки Тогда
		// Для режима настройки выбор варианта не выполняется
		Возврат;	
	КонецЕсли;
	
	Найденные = ВариантыОтчета.НайтиСтроки(Новый Структура("Наименование", ВариантНаименование));
	Если Найденные.Количество() > 0 Тогда
		Элементы.ВариантыОтчета.ТекущаяСтрока = Найденные[0].ПолучитьИдентификатор();
		ВариантыОтчетаОбработчикАктивизацииСтроки();
	Иначе
		ВариантСсылка = Неопределено;
		УстановитьЧтоБудетДальше(ЭтотОбъект, Ложь);
	КонецЕсли;
	УстановитьДоступностьАвтора(ЭтотОбъект);
	ОформитьПользователейВариантаОтчета(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Найденные = ВариантыОтчета.НайтиСтроки(Новый Структура("Наименование", Текст));
	УстановитьЧтоБудетДальше(ЭтотОбъект, Найденные.Количество() > 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступенПриИзменении(Элемент)
	
	ВариантТолькоДляАвтора = (Доступен = "ТолькоДляАвтора");
	Если ВариантТолькоДляАвтора Тогда
		Для каждого ЭлементСписка Из ПользователиВарианта Цикл
			ЭлементСписка.Пометка = (ЭлементСписка.Значение = Контекст.ТекущийПользователь);
		КонецЦикла; 
	КонецЕсли; 
	ОформитьПользователейВариантаОтчета(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Оповещение = Новый ОписаниеОповещения("ОписаниеНачалоВыбораЗавершение", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(Оповещение, Элементы.Описание.ТекстРедактирования,
		НСтр("ru = 'Описание'"));
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеНачалоВыбораЗавершение(Знач ВведенныйТекст, Знач ДополнительныеПараметры) Экспорт
	
	Если ВведенныйТекст = Неопределено Тогда
		Возврат;
	КонецЕсли;	

	ВариантОписание = ВведенныйТекст;
	
КонецПроцедуры

#Область ТаблицаВариантов

&НаКлиенте
Процедура ВариантыОтчетаПриАктивизацииСтроки(Элемент)
	
	Если Контекст.ИдентификаторТекущейСтроки = Элементы.ВариантыОтчета.ТекущаяСтрока Тогда
		Возврат;
	КонецЕсли;
	ВариантыОтчетаОбработчикАктивизацииСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыОтчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Пакет = Новый Структура;
	Пакет.Вставить("ПроверитьСтраницуСохранение", Истина);
	Пакет.Вставить("ПроверитьИЗаписать", Истина);
	Пакет.Вставить("ЗакрытьПослеЗаписи", Истина);
	ВыполнитьПакет(Неопределено, Пакет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыОтчетаПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыОтчетаПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	Вариант = Элементы.ВариантыОтчета.ТекущиеДанные;
	Если Вариант = Неопределено ИЛИ НЕ ЗначениеЗаполнено(Вариант.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Контекст.ПолныеПраваНаВарианты И НЕ Вариант.АвторТекущийПользователь Тогда
		ТекстПредупреждения = НСтр("ru = 'Недостаточно прав для удаления варианта отчета ""%1"".'");
		ТекстПредупреждения = СтрШаблон(ТекстПредупреждения, Вариант.Наименование);
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если Не Вариант.Пользовательский Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Невозможно удалить предопределенный вариант отчета.'"));
		Возврат;
	КонецЕсли;
	
	Если Вариант.ПометкаУдаления Тогда
		ТекстВопроса = НСтр("ru = 'Снять с ""%1"" пометку на удаление?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Пометить ""%1"" на удаление?'");
	КонецЕсли;
	ТекстВопроса = СтрШаблон(ТекстВопроса, Вариант.Наименование);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Вариант", Вариант);
	Обработчик = Новый ОписаниеОповещения("ВариантыОтчетаПередУдалениемЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыОтчетаПередУдалениемЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Вариант = ДополнительныеПараметры.Вариант;
		УдалитьВариантНаСервере(Вариант.Ссылка, Вариант.ИндексКартинки, Вариант.ПометкаУдаления);
		ВариантыОтчетаОбработчикАктивизацииСтроки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыОтчетаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти 

#Область ПользователиВарианта

&НаКлиенте
Процедура ПользователиВариантаПриИзменении(Элемент)
	
	ОформитьПользователейВариантаОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиВариантаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиВариантаПередУдалением(Элемент, Отказ)
	
	Если Не ИспользоватьГруппыПользователей
		И Не ИспользоватьВнешнихПользователей Тогда 
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиВариантаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВариантыОтчетовКлиент.ПользователиВариантаОтчетаОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиВариантаПометкаПриИзменении(Элемент)
	
	ОформитьПользователейВариантаОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьПользователейВариантаОтчета(СбрасыватьПризнакИспользования = Истина) 
	
	ПараметрыКлиента = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента();
	ЦветНеактивныхЗначений = ПараметрыКлиента.ЭлементыСтиля.ТекстЗапрещеннойЯчейкиЦвет;
	
	Если Не СбрасыватьПризнакИспользования Тогда 
		
		Элементы.ПользователиВарианта.ЦветТекста = ?(ВариантТолькоДляАвтора, ЦветНеактивныхЗначений, Новый Цвет);
		Возврат;
		
	КонецЕсли;
	
	КоличествоПомеченных = 0;
	
	Для Каждого Строка Из ПользователиВарианта Цикл 
		
		Если Строка.Значение = Контекст.ТекущийПользователь Тогда
			Продолжить;
		КонецЕсли; 
		
		КоличествоПомеченных = КоличествоПомеченных + Булево(Строка.Пометка);
		
	КонецЦикла;
	
	ВариантТолькоДляАвтора = (КоличествоПомеченных = 0);
	Доступен = ?(ВариантТолькоДляАвтора, "ТолькоДляАвтора", "УказаннымПользователям");
	
	Элементы.ПользователиВарианта.ЦветТекста = ?(ВариантТолькоДляАвтора, ЦветНеактивныхЗначений, Новый Цвет);
	
КонецПроцедуры

#КонецОбласти

#Область Теги

&НаКлиенте
Процедура ПолеВводаТегаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПолеВводаТегаОбработкаВыбораСервер(Элемент.Имя, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПолеВводаТегаОбработкаВыбораСервер(ИмяЭлемента, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Справочники.ТегиОтчетов.ПолеВводаТегаОбработкаВыбора(ЭтотОбъект, ИмяЭлемента, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеВводаТегаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПолеВводаТегаОкончаниеВводаТекстаСервер(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПолеВводаТегаОкончаниеВводаТекстаСервер(Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Справочники.ТегиОтчетов.ПолеВводаТегаОкончаниеВводаТекста(ЭтотОбъект, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОблакоТеговОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОблакоТеговОбработкаНавигационнойСсылкиСервер(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ОблакоТеговОбработкаНавигационнойСсылкиСервер(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
	Справочники.ТегиОтчетов.ОблакоТеговОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти 

#Область ПривязкиПолейОтчетов

&НаКлиенте
Процедура ПривязкаОбъектМетаданныхПриИзменении(Элемент)
	
	Если НЕ Контекст.БезПривязки Тогда
		ЗаполнитьСписокВыбораПолейПривязки();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПривязкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПривязкаИД = Сред(НавигационнаяСсылкаФорматированнойСтроки, СтрДлина("Привязка_")+1);
	СтрокаПривязки = ПривязкиПолей.НайтиПоИдентификатору(ПривязкаИД);
	ПривязкиПолей.Удалить(СтрокаПривязки);
	
	ОбновитьЭлементыПривязок();
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти 

#Область Образец

&НаКлиенте
Процедура ВариантОбразецНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьКартинкуЗавершение", ЭтотОбъект);
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = НСтр("ru = 'Выберите файл изображения'");
	Диалог.ПредварительныйПросмотр = Истина;
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Фильтр = РаботаСФайламиУНФКлиентСервер.ФильтрФайловИзображений();

	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.Диалог = Диалог;
	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
	
	ФайловаяСистемаКлиент.ЗагрузитьФайл(ОписаниеОповещения, ПараметрыЗагрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКартинкуЗавершение(РазмещенныйФайл, ДополнительныеДанные) Экспорт
	
	Если РазмещенныйФайл=Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если ЭтоАдресВременногоХранилища(ВариантОбразец) И НЕ ВариантОбразец=ПустаяКартинка Тогда
		УдалитьИзВременногоХранилища(ВариантОбразец);
	КонецЕсли;
	ВариантОбразец = РазмещенныйФайл.Хранение;
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

#КонецОбласти 

#Область ДеревоПодсистем

&НаКлиенте
Процедура ДеревоПодсистемИспользованиеПриИзменении(Элемент)
	
	ВариантыОтчетовКлиент.ДеревоПодсистемИспользованиеПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПодсистемВажностьПриИзменении(Элемент)
	
	ВариантыОтчетовКлиент.ДеревоПодсистемВажностьПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодобратьПользователей(Команда)
	
	ВариантыОтчетовКлиент.ПодобратьПользователейВариантаОтчета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьГруппыВнешнихПользователей(Команда)
	
	ВариантыОтчетовКлиент.ПодобратьПользователейВариантаОтчета(ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Если Контекст.РежимНастройки И ТекущаяСтраница = 2 Тогда
		Возврат;
	КонецЕсли; 
	
	Пакет = Новый Структура;
	Если ТекущаяСтраница = 2 Тогда
		Пакет.Вставить("ПроверитьСтраницуНастройки", Истина);
		Пакет.Вставить("ПерейтиНаСтраницуСохранение", Истина);
	ИначеЕсли ТекущаяСтраница = 3 Тогда
		Пакет.Вставить("ПроверитьСтраницуПодсистемы", Истина);
		Пакет.Вставить("ПерейтиНаСтраницуНастройки", Истина);
	ИначеЕсли ТекущаяСтраница = 4 Тогда
		Пакет.Вставить("ПроверитьСтраницуТегиОбразец", Истина);
		Пакет.Вставить("ПерейтиНаСтраницуПодсистемы", Истина);
	ИначеЕсли ТекущаяСтраница = 5 Тогда
		Пакет.Вставить("ПроверитьСтраницуКонтекстноеОткрытие", Истина);
		Пакет.Вставить("ПерейтиНаСтраницуТегиОбразец", Истина);
	КонецЕсли; 
	ВыполнитьПакет(Неопределено, Пакет);
	ТекущаяСтраница = ТекущаяСтраница - 1;
	
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	
	Если ТекущаяСтраница > 4 Тогда
		Возврат;
	КонецЕсли;
	
	Пакет = Новый Структура;
	Если ТекущаяСтраница = 1 Тогда
		Пакет.Вставить("ПроверитьСтраницуСохранение", Истина);
		Пакет.Вставить("ПерейтиНаСтраницуНастройки", Истина);
	ИначеЕсли ТекущаяСтраница = 2 Тогда
		Пакет.Вставить("ПроверитьСтраницуНастройки", Истина);
		Пакет.Вставить("ПерейтиНаСтраницуПодсистемы", Истина);
	ИначеЕсли ТекущаяСтраница = 3 Тогда
		Пакет.Вставить("ПроверитьСтраницуПодсистемы", Истина);
		Пакет.Вставить("ПерейтиНаСтраницуТегиОбразец", Истина);
	ИначеЕсли ТекущаяСтраница = 4 Тогда
		Пакет.Вставить("ПроверитьСтраницуТегиОбразец", Истина);
		Пакет.Вставить("ПерейтиНаСтраницуКонтекстноеОткрытие", Истина);
	КонецЕсли; 
	ВыполнитьПакет(Неопределено, Пакет);
	ТекущаяСтраница = ТекущаяСтраница + 1;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	Пакет = Новый Структура;
	Если ТекущаяСтраница = 1 Тогда
		Пакет.Вставить("ПроверитьСтраницуСохранение", Истина);
	ИначеЕсли ТекущаяСтраница = 2 Тогда
		Пакет.Вставить("ПроверитьСтраницуНастройки", Истина);
	ИначеЕсли ТекущаяСтраница = 3 Тогда
		Пакет.Вставить("ПроверитьСтраницуПодсистемы", Истина);
	ИначеЕсли ТекущаяСтраница = 4 Тогда
		Пакет.Вставить("ПроверитьСтраницуТегиОбразец", Истина);
	ИначеЕсли ТекущаяСтраница = 5 Тогда
		Пакет.Вставить("ПроверитьСтраницуКонтекстноеОткрытие", Истина);
	КонецЕсли; 
	Пакет.Вставить("ПроверитьИЗаписать", Истина);
	ВыполнитьПакет(Неопределено, Пакет);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПривязку(Команда)
	
	ДобавитьПривязкуСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПривязкаСохранить(Команда)
	
	Если НЕ ЗначениеЗаполнено(ПривязкаОбъектМетаданных) ИЛИ (НЕ ЗначениеЗаполнено(ПривязкаПолеОтчета) И НЕ Контекст.БезПривязки) Тогда
		Возврат;
	КонецЕсли; 
	
	ПривязкаСохранитьСервер();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПривязкаОтменить(Команда)
	
	ПривязкаОбъектМетаданных = Неопределено;
	ПривязкаПолеОтчета = Неопределено;
	Элементы.ГруппаДобавлениеПривязки.Видимость = Ложь;
	Элементы.ДобавитьПривязку.Видимость = Истина;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ВариантыОтчетов.УстановитьУсловноеОформлениеСпискаПользователейВариантаОтчета(ЭтотОбъект);
	ВариантыОтчетов.УстановитьУсловноеОформлениеДереваПодсистем(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПанельКнопокДалееНазад(Элементы, Контекст)

	Если Контекст.ЭтоВнешний Тогда
		ВидимостьНазад = Ложь;
		ВидимостьДалее = Ложь;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСохранение
		И НЕ Контекст.РежимНастройки Тогда
		ВидимостьНазад = Ложь;
		ВидимостьДалее = Истина;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаНастройки
		И Контекст.РежимНастройки Тогда
		// В режиме настройки не требуется список отчетов
		ВидимостьНазад = Ложь;
		ВидимостьДалее = Истина;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаКонтекстноеОткрытие Тогда
		ВидимостьНазад = Истина;
		ВидимостьДалее = Ложь;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОбразецТеги 
		И Контекст.БезПривязки Тогда
		// Для дополнительных отчетов недоступно контекстное открытие
		ВидимостьНазад = Истина;
		ВидимостьДалее = Ложь;
	Иначе
		ВидимостьНазад = Истина;
		ВидимостьДалее = Истина;
	КонецЕсли;
	Если Элементы.Назад.Видимость <> ВидимостьНазад Тогда
		Элементы.Назад.Видимость = ВидимостьНазад;
	КонецЕсли; 
	Если Элементы.Далее.Видимость <> ВидимостьДалее Тогда
		Элементы.Далее.Видимость = ВидимостьДалее;
	КонецЕсли; 
	
КонецПроцедуры 

&НаСервере
Процедура ПрочитатьДанныеОтчета()
	
	Если Контекст.ЭтоДополнительныйОтчет Тогда
		ОтчетИнформация = СформироватьИнформациюОбОбДополнительномОтчете(Контекст.КлючОбъекта);
		Контекст.Вставить("ВидОтчета", ОтчетИнформация.Вид);
		// Для дополнительных отчетов используется контекстная привязка БСП, нет возможности указать поле отчета
		Элементы.ПривязкаПолеОтчета.Видимость = НЕ Контекст.БезПривязки;
		Элементы.ДобавитьПривязку.Видимость = НЕ Контекст.БезПривязки;
		Элементы.ДекорацияПривязка.Видимость = НЕ Контекст.БезПривязки;
	Иначе
		ОтчетИнформация = ВариантыОтчетов.ИнформацияОбОтчете(Контекст.ПолноеИмя);
	КонецЕсли; 
	
	Контекст.Вставить("ОтчетСсылка", ОтчетИнформация.Отчет);
	Контекст.Вставить("ОтчетИмя",    ОтчетИнформация.ОтчетИмя);
	Контекст.Вставить("ТипОтчета",   ОтчетИнформация.ТипОтчета);
	Контекст.Вставить("ЭтоВнешний",  ОтчетИнформация.ТипОтчета = Перечисления.ТипыОтчетов.Внешний);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЧтоБудетДальше(Форма, Перезапись)
	
	Если Перезапись Тогда
		Форма.Элементы.ПерезаписьНовый.ТекущаяСтраница = Форма.Элементы.Перезапись;
	Иначе
		Форма.Элементы.ПерезаписьНовый.ТекущаяСтраница = Форма.Элементы.Новый;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьАвтора(Форма)
	
	Элементы = Форма.Элементы;
	
	АвторЗаполнен = ЗначениеЗаполнено(Форма.ВариантАвтор);
	Элементы.ВариантАвтор.Видимость = АвторЗаполнен;
	Элементы.СтраницыДоступен.Видимость = АвторЗаполнен;
	Если Форма.Контекст.ЭтоДополнительныйОтчет Тогда
		Элементы.СтраницыДоступен.ТекущаяСтраница = Элементы.СтраницаДополнительныйОтчет;
		Элементы.ДоступенДополнительныйОтчет.ТолькоПросмотр = Не Форма.Контекст.ПолныеПраваНаВарианты;
	Иначе
		Элементы.СтраницыДоступен.ТекущаяСтраница = Элементы.СтраницаОсновнойОтчет;
		Элементы.Доступен.ТолькоПросмотр = Не Форма.Контекст.ПолныеПраваНаВарианты;
		Элементы.ПользователиВарианта.ТолькоПросмотр = Не Форма.Контекст.ПолныеПраваНаВарианты;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьИнформациюОбОбДополнительномОтчете(Отчет)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Отчет);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДополнительныеОтчетыИОбработки.Ссылка КАК Отчет,
	|	ДополнительныеОтчетыИОбработки.ИмяОбъекта КАК ОтчетИмя,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Дополнительный) КАК ТипОтчета,
	|	ДополнительныеОтчетыИОбработки.Вид КАК Вид
	|ИЗ
	|	Справочник.ДополнительныеОтчетыИОбработки КАК ДополнительныеОтчетыИОбработки
	|ГДЕ
	|	ДополнительныеОтчетыИОбработки.Ссылка = &Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	Результат = Новый Структура("Отчет, ОтчетИмя, ТипОтчета, Вид");
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли; 
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокВариантов()
	
	ТекущийКлючВарианта = Контекст.КлючПрототипа;
	Если ЗначениеЗаполнено(Элементы.ВариантыОтчета.ТекущаяСтрока) Тогда
		ТекущаяСтрока = ВариантыОтчета.НайтиПоИдентификатору(Элементы.ВариантыОтчета.ТекущаяСтрока);
		Если ЗначениеЗаполнено(ТекущаяСтрока.КлючВарианта) Тогда
			ТекущийКлючВарианта = ТекущаяСтрока.КлючВарианта;
		КонецЕсли;
	КонецЕсли;
	
	ВариантыОтчета.Очистить();
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВариантыОтчетов.Ссылка КАК Ссылка,
	|	ВариантыОтчетов.Пользовательский КАК Пользовательский,
	|	ВариантыОтчетов.Наименование КАК Наименование,
	|	ВариантыОтчетов.Автор КАК Автор,
	|	ВариантыОтчетов.Описание КАК Описание,
	|	ВариантыОтчетов.ТипОтчета КАК Тип,
	|	ВариантыОтчетов.КлючВарианта КАК КлючВарианта,
	|	ВариантыОтчетов.ТолькоДляАвтора КАК ТолькоДляАвтора,
	|	ВариантыОтчетов.ВидимостьПоУмолчанию КАК ВидимостьПоУмолчанию,
	|	ВариантыОтчетов.ПометкаУдаления КАК ПометкаУдаления,
	|	ВЫБОР
	|		КОГДА ВариантыОтчетов.Автор = &ТекущийПользователь
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК АвторТекущийПользователь,
	|	ВЫБОР
	|		КОГДА ВариантыОтчетов.ПометкаУдаления
	|			ТОГДА 4
	|		КОГДА ВариантыОтчетов.Пользовательский
	|			ТОГДА 3
	|		ИНАЧЕ 5
	|	КОНЕЦ КАК ИндексКартинки,
	|	ВЫБОР
	|		КОГДА ВариантыОтчетов.ПометкаУдаления
	|			ТОГДА 3
	|		КОГДА ВариантыОтчетов.Пользовательский
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.Отчет = &Отчет
	|	И (ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ
	|			ИЛИ ВариантыОтчетов.Пользовательский = ИСТИНА)
	|	И (ВариантыОтчетов.КлючВарианта = &ТекущийКлючВарианта
	|			ИЛИ ВариантыОтчетов.ТолькоДляАвтора
	|				И ВариантыОтчетов.Автор = &ТекущийПользователь
	|			ИЛИ ВариантыОтчетов.Автор В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка), ЗНАЧЕНИЕ(Справочник.ВнешниеПользователи.ПустаяСсылка))
	|			ИЛИ ВариантыОтчетов.Ссылка В
	|				(ВЫБРАТЬ
	|					НастройкиВариантовОтчетов.Вариант
	|				ИЗ
	|					РегистрСведений.НастройкиВариантовОтчетов КАК НастройкиВариантовОтчетов
	|				ГДЕ
	|					НастройкиВариантовОтчетов.Пользователь = &ТекущийПользователь
	|					И НастройкиВариантовОтчетов.Подсистема В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Справочник.ИдентификаторыОбъектовМетаданных.ПустаяСсылка), ЗНАЧЕНИЕ(Справочник.ИдентификаторыОбъектовРасширений.ПустаяСсылка))
	|					И НастройкиВариантовОтчетов.Видимость))
	|	И НЕ ВариантыОтчетов.ПредопределенныйВариант В (&ОтключенныеВариантыПрограммы)";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Отчет", Контекст.ОтчетСсылка);
	Запрос.УстановитьПараметр("ТекущийКлючВарианта", ТекущийКлючВарианта);
	Запрос.УстановитьПараметр("ТекущийПользователь", Контекст.ТекущийПользователь);
	Запрос.УстановитьПараметр("ОтключенныеВариантыПрограммы", ВариантыОтчетовПовтИсп.ОтключенныеВариантыПрограммы());
	
	Запрос.Текст = ТекстЗапроса;
	
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	
	ВариантыОтчета.Загрузить(ТаблицаЗначений);
	
	// Добавить предопределенные варианты внешнего отчета.
	Если Контекст.ЭтоВнешний Тогда
		Попытка
			// АПК:553-выкл Создание в области данных дополнительных отчетов, одобренных администратором сервиса.
			ОтчетОбъект = ВнешниеОтчеты.Создать(Контекст.ОтчетИмя);
			// АПК:553-вкл
		Исключение
			ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Не удалось получить список предопределенных вариантов
			|внешнего отчета ""%1"":
			|%2'"),
			Контекст.ОтчетСсылка,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
			"СохранениеВариантаОтчетаУНФ",
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.ИдентификаторыОбъектовМетаданных,
			Контекст.ОтчетСсылка,
			ТекстОшибки);
			Возврат;
		КонецПопытки;
		
		Если ОтчетОбъект.СхемаКомпоновкиДанных = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Для Каждого ВариантНастроекКД Из ОтчетОбъект.СхемаКомпоновкиДанных.ВариантыНастроек Цикл
			Вариант = ВариантыОтчета.Добавить();
			Вариант.Пользовательский = Ложь;
			Вариант.Наименование = ВариантНастроекКД.Представление;
			Вариант.КлючВарианта = ВариантНастроекКД.Имя;
			Вариант.ТолькоДляАвтора = Ложь;
			Вариант.АвторТекущийПользователь = Ложь;
			Вариант.ИндексКартинки = 5;
		КонецЦикла;
	КонецЕсли;
	
	ВариантыОтчета.Сортировать("Наименование Возр");
	
	Контекст.ИдентификаторТекущейСтроки = -1;
	Найденные = ВариантыОтчета.НайтиСтроки(Новый Структура("КлючВарианта", ТекущийКлючВарианта));
	Если Найденные.Количество() > 0 Тогда
		Вариант = Найденные[0];
		ВариантСсылка = Вариант.Ссылка;
		Контекст.ИдентификаторТекущейСтроки = Вариант.ПолучитьИдентификатор();
		Элементы.ВариантыОтчета.ТекущаяСтрока = Контекст.ИдентификаторТекущейСтроки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВариантыОтчетаОбработчикАктивизацииСтроки()
	
	Если Контекст.РежимНастройки Тогда
		Возврат;
	КонецЕсли; 
	Контекст.ИдентификаторТекущейСтроки = Элементы.ВариантыОтчета.ТекущаяСтрока;
	Если Контекст.ИдентификаторТекущейСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Вариант = ВариантыОтчета.НайтиПоИдентификатору(Контекст.ИдентификаторТекущейСтроки);
	Если Вариант = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПравоНастройкиВарианта = ПравоНастройкиВарианта(Вариант, Контекст.ПолныеПраваНаВарианты);
	ПравоЗаписиВарианта    = ПравоЗаписиВарианта(Вариант, ПравоНастройкиВарианта, Контекст.ЭтоПолныеПрава, Контекст.РежимНастройки);
	Если ПравоЗаписиВарианта Тогда
		ВариантСсылка = Вариант.Ссылка;
		ПрочитатьДанныеВарианта();
	Иначе
		ВариантСсылка = Неопределено;
		ВариантАвтор = Контекст.ТекущийПользователь;
		ВариантНаименование = СформироватьСвободноеНаименование(Вариант, ВариантыОтчета);
		ВариантОписание = "";
		ВариантТолькоДляАвтора = Истина;
		ВариантВидимостьПоУмолчанию = Истина;
		Доступен = "ТолькоДляАвтора";
	КонецЕсли;
	УстановитьЧтоБудетДальше(ЭтаФорма, ЗначениеЗаполнено(ВариантСсылка));
	УстановитьДоступностьАвтора(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьСвободноеНаименование(Вариант, ВариантыОтчета)
	
	ШаблонИмениВарианта = СтрШаблон(НСтр("ru = '%1 - копия'"), СокрЛП(Вариант.Наименование));
	
	СвободноеНаименование = ШаблонИмениВарианта;
	Найденные = ВариантыОтчета.НайтиСтроки(Новый Структура("Наименование", СвободноеНаименование));
	Если Найденные.Количество() = 0 Тогда
		Возврат СвободноеНаименование;
	КонецЕсли;
	
	НомерВарианта = 1;
	Пока Истина Цикл
		НомерВарианта = НомерВарианта + 1;
		СвободноеНаименование = ШаблонИмениВарианта +" (" + Формат(НомерВарианта, "") + ")";
		Найденные = ВариантыОтчета.НайтиСтроки(Новый Структура("Наименование", СвободноеНаименование));
		Если Найденные.Количество() = 0 Тогда
			Возврат СвободноеНаименование;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПравоНастройкиВарианта(Вариант, ПолныеПраваНаВарианты)
	Возврат (ПолныеПраваНаВарианты ИЛИ Вариант.АвторТекущийПользователь) И ЗначениеЗаполнено(Вариант.Ссылка);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПравоЗаписиВарианта(Вариант, ПравоНастройкиВарианта, ЭтоПолныеПрава, РежимНастройки)
	Возврат (Вариант.Пользовательский ИЛИ (РежимНастройки И ЭтоПолныеПрава)) И ПравоНастройкиВарианта;
КонецФункции

&НаСервере
Процедура ПрочитатьДанныеВарианта()
	
	Запрос = Новый Запрос;
	Если ТипЗнч(ВариантСсылка) = Тип("СправочникСсылка.ВариантыДополнительныхОтчетов") Тогда
		Запрос.УстановитьПараметр("Ссылка", ВариантСсылка);
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВариантыДополнительныхОтчетов.Ссылка,
		|	ВариантыДополнительныхОтчетов.Наименование,
		|	ВариантыДополнительныхОтчетов.Автор,
		|	ВариантыДополнительныхОтчетов.ТолькоДляАвтора КАК ТолькоДляАвтора,
		|	ИСТИНА КАК Пользовательский,
		|	ВариантыДополнительныхОтчетов.Описание,
		|	ЕСТЬNULL(НастройкиВариантовОтчетовУНФ.ФункциональнаяОпция, """") КАК ФункциональнаяОпция,
		|	ЕСТЬNULL(НастройкиВариантовОтчетовУНФ.Образец, НЕОПРЕДЕЛЕНО) КАК Образец,
		|	ЕСТЬNULL(НастройкиВариантовОтчетовУНФ.Теги, """") КАК Теги,
		|	ЕСТЬNULL(НастройкиВариантовОтчетовУНФ.Рекомендуемый, ЛОЖЬ) КАК Рекомендуемый
		|ИЗ
		|	Справочник.ВариантыДополнительныхОтчетов КАК ВариантыДополнительныхОтчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВариантовОтчетовУНФ КАК НастройкиВариантовОтчетовУНФ
		|		ПО ВариантыДополнительныхОтчетов.Ссылка = НастройкиВариантовОтчетовУНФ.Вариант
		|ГДЕ
		|	ВариантыДополнительныхОтчетов.Ссылка = &Ссылка";
	ИначеЕсли ТипЗнч(ВариантСсылка) = Тип("СправочникСсылка.ВариантыОтчетов") Тогда 
		Запрос.УстановитьПараметр("Ссылка", ВариантСсылка);
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВариантыОтчетов.Ссылка,
		|	ВариантыОтчетов.Наименование,
		|	ВариантыОтчетов.Автор,
		|	ВариантыОтчетов.ТолькоДляАвтора,
		|	ВариантыОтчетов.Пользовательский,
		|	ВЫБОР
		|		КОГДА ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) = """"
		|				И НЕ ВариантыОтчетов.ПредопределенныйВариант = НЕОПРЕДЕЛЕНО
		|			ТОГДА ВариантыОтчетов.ПредопределенныйВариант.Описание
		|		ИНАЧЕ ВариантыОтчетов.Описание
		|	КОНЕЦ КАК Описание,
		|	ЕСТЬNULL(НастройкиВариантовОтчетовУНФ.ФункциональнаяОпция, """") КАК ФункциональнаяОпция,
		|	ЕСТЬNULL(НастройкиВариантовОтчетовУНФ.Образец, НЕОПРЕДЕЛЕНО) КАК Образец,
		|	ЕСТЬNULL(НастройкиВариантовОтчетовУНФ.Теги, """") КАК Теги,
		|	ЕСТЬNULL(НастройкиВариантовОтчетовУНФ.Рекомендуемый, ЛОЖЬ) КАК Рекомендуемый
		|ИЗ
		|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВариантовОтчетовУНФ КАК НастройкиВариантовОтчетовУНФ
		|		ПО ВариантыОтчетов.Ссылка = НастройкиВариантовОтчетовУНФ.Вариант
		|ГДЕ
		|	ВариантыОтчетов.Ссылка = &Ссылка";
	Иначе
		Возврат;
	КонецЕсли; 
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВариантАвтор           = Выборка.Автор;
		ВариантНаименование    = Выборка.Наименование;
		ВариантОписание        = Выборка.Описание;
		ВариантТолькоДляАвтора = Выборка.ТолькоДляАвтора;
		Доступен = ?(ВариантТолькоДляАвтора, "ТолькоДляАвтора", "УказаннымПользователям");
		ВариантФункциональнаяОпция = Выборка.ФункциональнаяОпция;
		ВариантТеги            = Выборка.Теги;
		ВариантРекомендуемый   = Выборка.Рекомендуемый;
		Контекст.Вставить("Пользовательский", Выборка.Пользовательский);
		Если Контекст.РежимНастройки И ТипЗнч(Выборка.Образец)=Тип("ХранилищеЗначения") Тогда
			Образец = Выборка.Образец.Получить();
			Если ТипЗнч(Образец)=Тип("Картинка") Тогда
				ВариантОбразец = ПоместитьВоВременноеХранилище(Образец, УникальныйИдентификатор);				
			КонецЕсли; 
		КонецЕсли;
		Справочники.ТегиОтчетов.ОбновитьТаблицуТегов(ЭтотОбъект);
		УстановитьДоступностьАвтора(ЭтотОбъект);
	Иначе
		Контекст.Вставить("Пользовательский", Ложь);
	КонецЕсли;
	
	ПрочитатьДанныеПривязок();
	
КонецПроцедуры

#Область Навигация

&НаКлиенте
Процедура ВыполнитьПакет(Результат, Пакет) Экспорт
	
	Если Не Пакет.Свойство("ВариантЭтоНовый") Тогда
		Пакет.Вставить("ВариантЭтоНовый", НЕ ЗначениеЗаполнено(ВариантСсылка));
	КонецЕсли;
	
	// Обработка результата предыдущего шага.
	Если Пакет.Свойство("ОтобразитьЗапросНаПерезапись") И Пакет.ОтобразитьЗапросНаПерезапись = Истина Тогда
		Пакет.ОтобразитьЗапросНаПерезапись = Ложь;
		Если Результат = КодВозвратаДиалога.Да Тогда
			Пакет.Вставить("ВопросНаПерезаписьПройден", Истина);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Проверка заполнения страниц 
	
	Если Пакет.Свойство("ПроверитьСтраницуСохранение") И Пакет.ПроверитьСтраницуСохранение Тогда
		
		// Наименование не введено.
		Если Не ЗначениеЗаполнено(ВариантНаименование) Тогда
			ТекстОшибки = НСтр("ru = 'Поле ""Наименование"" не заполнено'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "ВариантНаименование");
			Возврат;
		КонецЕсли;
		
		// Введено наименование существующего варианта отчета.
		Если Не Пакет.ВариантЭтоНовый Тогда
			Найденные = ВариантыОтчета.НайтиСтроки(Новый Структура("Ссылка", ВариантСсылка));
			Вариант = Найденные[0];
			Если НЕ ПравоЗаписиВарианта(Вариант, ПравоНастройкиВарианта(Вариант, Контекст.ПолныеПраваНаВарианты), Контекст.ЭтоПолныеПрава, Контекст.РежимНастройки) Тогда
				ТекстОшибки = НСтр("ru = 'Недостаточно прав для изменения варианта ""%1"". Необходимо выбрать другой вариант или изменить Наименование.'");
				ТекстОшибки = СтрШаблон(ТекстОшибки, ВариантНаименование);
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "ВариантНаименование");
				Возврат;
			КонецЕсли;
			Если Не Пакет.Свойство("ВопросНаПерезаписьПройден") Тогда
				Если Вариант.ПометкаУдаления = Истина Тогда
					ТекстВопроса = НСтр("ru = 'Вариант отчета ""%1"" помечен на удаление.
					|Заменить помеченный на удаление вариант отчета?'");
					КнопкаПоУмолчанию = КодВозвратаДиалога.Нет;
				Иначе
					ТекстВопроса = НСтр("ru = 'Заменить ранее сохраненный вариант отчета ""%1""?'");
					КнопкаПоУмолчанию = КодВозвратаДиалога.Да;
				КонецЕсли;
				ТекстВопроса = СтрШаблон(ТекстВопроса, ВариантНаименование);
				Пакет.Вставить("ОтобразитьЗапросНаПерезапись", Истина);
				Обработчик = Новый ОписаниеОповещения("ВыполнитьПакет", ЭтотОбъект, Пакет);
				ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КнопкаПоУмолчанию);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		// Проверка завершена.
		Пакет.ПроверитьСтраницуСохранение = Ложь;
		
	КонецЕсли;
	
	Если Пакет.Свойство("ПроверитьСтраницуНастройки") И Пакет.ПроверитьСтраницуНастройки Тогда
		
		// Наименование не введено.
		Если Не ЗначениеЗаполнено(ВариантНаименование) Тогда
			ТекстОшибки = НСтр("ru = 'Поле ""Наименование"" не заполнено'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "ВариантНаименование");
			Возврат;
		КонецЕсли;
		
		// Проверка завершена.
		Пакет.ПроверитьСтраницуНастройки = Ложь;
		
	КонецЕсли;
	
	Если Пакет.Свойство("ПроверитьСтраницуПодсистемы") И Пакет.ПроверитьСтраницуПодсистемы Тогда
		
		// Проверка завершена.
		Пакет.ПроверитьСтраницуПодсистемы = Ложь;
		
	КонецЕсли;
	
	Если Пакет.Свойство("ПроверитьСтраницуТегиОбразец") И Пакет.ПроверитьСтраницуТегиОбразец Тогда
		
		// Проверка завершена.
		Пакет.ПроверитьСтраницуТегиОбразец = Ложь;
		
	КонецЕсли;
	
	Если Пакет.Свойство("ПроверитьСтраницуКонтекстноеОткрытие") И Пакет.ПроверитьСтраницуКонтекстноеОткрытие Тогда
		
		Если НЕ Элементы.ДобавитьПривязку.Видимость Тогда
			ТекстОшибки = НСтр("ru = 'Отмените или завершите редактирование описания контекстного открытия отчета'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "ДобавитьПривязку");
			Возврат;
		КонецЕсли; 
		
		// Проверка завершена.
		Пакет.ПроверитьСтраницуКонтекстноеОткрытие = Ложь;
		
	КонецЕсли;
	
	// Переключение страниц
	
	Если Пакет.Свойство("ПерейтиНаСтраницуСохранение") И Пакет.ПерейтиНаСтраницуСохранение Тогда
		// Для режима настройки список отчетов недоступен.
		Если Не Контекст.РежимНастройки Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСохранение;
		КонецЕсли;
		Пакет.ПерейтиНаСтраницуСохранение = Ложь;
	КонецЕсли; 
	
	Если Пакет.Свойство("ПерейтиНаСтраницуНастройки") И Пакет.ПерейтиНаСтраницуНастройки Тогда
		// Для внешних отчетов настройки недоступны.
		Если Не Контекст.ЭтоВнешний Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаНастройки;
		КонецЕсли;
		Пакет.ПерейтиНаСтраницуНастройки = Ложь;
	КонецЕсли;
	
	Если Пакет.Свойство("ПерейтиНаСтраницуПодсистемы") И Пакет.ПерейтиНаСтраницуПодсистемы Тогда
		// Для внешних отчетов настройки недоступны.
		Если Не Контекст.ЭтоВнешний Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПодсистемы;
			ПерезаполнитьДеревоПодсистем();
			СтрокиДерева = ДеревоПодсистем.ПолучитьЭлементы();
			Для Каждого СтрокаДерева Из СтрокиДерева Цикл
				Элементы.ДеревоПодсистем.Развернуть(СтрокаДерева.ПолучитьИдентификатор(), Истина);
			КонецЦикла;
		КонецЕсли;
		Элементы.СброситьНастройки.Видимость = ЗначениеЗаполнено(ВариантСсылка);
		Пакет.ПерейтиНаСтраницуПодсистемы = Ложь;
	КонецЕсли;
	
	Если Пакет.Свойство("ПерейтиНаСтраницуТегиОбразец") И Пакет.ПерейтиНаСтраницуТегиОбразец Тогда
		// Для внешних отчетов настройки недоступны.
		Если Не Контекст.ЭтоВнешний Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОбразецТеги;
		КонецЕсли;
		Пакет.ПерейтиНаСтраницуТегиОбразец = Ложь;
	КонецЕсли;
	
	Если Пакет.Свойство("ПерейтиНаСтраницуКонтекстноеОткрытие") И Пакет.ПерейтиНаСтраницуКонтекстноеОткрытие Тогда
		// Для внешних отчетов настройки недоступны.
		Если Не Контекст.ЭтоВнешний Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаКонтекстноеОткрытие;
		КонецЕсли;
		Пакет.ПерейтиНаСтраницуКонтекстноеОткрытие = Ложь;
	КонецЕсли;
	
	ОбновитьПанельКнопокДалееНазад(Элементы, Контекст);
	
	// Сохранение изменений
	
	Если Пакет.Свойство("ПроверитьИЗаписать") И Пакет.ПроверитьИЗаписать Тогда 
		Если Не Пакет.ВариантЭтоНовый И НЕ Контекст.ЭтоПолныеПрава И Контекст.ПолныеПраваНаВарианты И НЕ ВариантАвтор = Контекст.ТекущийПользователь Тогда 
			ТекстОшибки = НСтр("ru = 'Недостаточно прав для изменения варианта ""%1"".'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, ВариантНаименование);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "ВариантНаименование");
			Возврат;
		КонецЕсли; 
		Пакет.Вставить("Отказ", Ложь);
		ПроверитьИЗаписатьНаСервере(Пакет);
		Если Пакет.Отказ = Истина Тогда
			Возврат;
		КонецЕсли;
		СохранитьНастройкиВарианта();
		ОповеститьОВыборе(ВариантСсылка);
		Пакет.ПроверитьИЗаписать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьДеревоПодсистем() 
	
	Если НЕ ЗначениеЗаполнено(ВариантСсылка) Тогда
		ВариантОснование = Контекст.Прототип;
	Иначе
		ВариантОснование = ВариантСсылка;
	КонецЕсли;
	ДеревоПриемник = ВариантыОтчетов.ДеревоПодсистемСформировать(ЭтотОбъект, ВариантОснование);
	ЗначениеВРеквизитФормы(ДеревоПриемник, "ДеревоПодсистем");
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиВарианта()
	
	// Настройки СКД
	Если НЕ Контекст.РежимНастройки Тогда
		ХранилищаНастроек.ХранилищеВариантовОтчетов.Сохранить(Контекст.ПолноеИмя, ВариантКлючВарианта, КомпоновщикНастроек.Настройки);
	КонецЕсли; 
	
	// Теги
	ВариантТеги = "";
	Для каждого Стр Из ДанныеТегов Цикл
		ВариантТеги = ВариантТеги + ?(ПустаяСтрока(ВариантТеги), "", ",") + Строка(Стр.Тег);
	КонецЦикла; 
	
	// Настройки вариантов УНФ
	Менеджер = РегистрыСведений.НастройкиВариантовОтчетовУНФ.СоздатьМенеджерЗаписи();
	Менеджер.Вариант = ВариантСсылка;
	
	Если ЭтоАдресВременногоХранилища(ВариантОбразец) И НЕ ВариантОбразец=ПустаяКартинка Тогда
		ДанныеВариантОбразец = ПолучитьИзВременногоХранилища(ВариантОбразец);
		Если ТипЗнч(ДанныеВариантОбразец) = Тип("Картинка") Тогда
			Менеджер.Образец = Новый ХранилищеЗначения(ДанныеВариантОбразец);
		ИначеЕсли ТипЗнч(ДанныеВариантОбразец) = Тип("ДвоичныеДанные") Тогда
			Менеджер.Образец = Новый ХранилищеЗначения(Новый Картинка(ДанныеВариантОбразец));
		КонецЕсли;
	Иначе
		Менеджер.Образец = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли;
	
	Менеджер.ФункциональнаяОпция = ВариантФункциональнаяОпция;
	Менеджер.Теги = ВариантТеги;
	Менеджер.Рекомендуемый = ВариантРекомендуемый;
	Менеджер.ИзмененаПользователем = Истина;
	Менеджер.Записать(Истина);
	
	// Привязка полей отчетов
	Набор = РегистрыСведений.ПривязкаПолейОтчетовУНФ.СоздатьНаборЗаписей();
	Набор.Отбор.Вариант.Установить(ВариантСсылка);
	Для каждого Стр Из ПривязкиПолей Цикл
		Запись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, Стр);
		Запись.Вариант = ВариантСсылка;
		Запись.ИзмененаПользователем = Истина;
		Запись.Использовать = Истина;
	КонецЦикла; 
	Набор.Записать(Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьВариантНаСервере(Ссылка, ИндексКартинки, ПометкаУдаления)
	ВариантОбъект = Ссылка.ПолучитьОбъект();
	ВариантОбъект.УстановитьПометкуУдаления(НЕ ВариантОбъект.ПометкаУдаления);
	ПометкаУдаления = ВариантОбъект.ПометкаУдаления;
	ИндексКартинки = ?(ПометкаУдаления, 4, ?(ВариантОбъект.Пользовательский, 3, 5));
КонецПроцедуры

&НаСервере
Процедура ПроверитьИЗаписатьНаСервере(Пакет)
	
	ВариантЭтоНовый = НЕ ЗначениеЗаполнено(ВариантСсылка);
	
	Если ВариантЭтоНовый И ВариантыОтчетов.НаименованиеЗанято(Контекст.ОтчетСсылка, 
		?(ВариантСсылка=Неопределено, Справочники.ВариантыОтчетов.ПустаяСсылка(), ВариантСсылка), ВариантНаименование) Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = '""%1"" занято, необходимо указать другое Наименование.'"), ВариантНаименование);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "ВариантНаименование");
		Пакет.Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		Если ВариантЭтоНовый Тогда
			ВариантОбъект = Справочники.ВариантыОтчетов.СоздатьЭлемент();
			ВариантОбъект.Отчет            = Контекст.ОтчетСсылка;
			ВариантОбъект.ТипОтчета        = Контекст.ТипОтчета;
			ВариантОбъект.КлючВарианта     = Строка(Новый УникальныйИдентификатор());
			ВариантОбъект.Пользовательский = Истина;
			ВариантОбъект.Автор            = Контекст.ТекущийПользователь;
			ВариантОбъект.ЗаполнитьРодителя();
		Иначе
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(Метаданные.Справочники.ВариантыОтчетов.ПолноеИмя());
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ВариантСсылка);
			Блокировка.Заблокировать();
			ВариантОбъект = ВариантСсылка.ПолучитьОбъект();
		КонецЕсли;
		ВариантОбъект.Наименование = ВариантНаименование;
		ВариантОбъект.Описание     = ВариантОписание;
		ВариантОбъект.ТолькоДляАвтора      = ВариантТолькоДляАвтора;
		ВариантОбъект.Записать();
		ВариантСсылка = ВариантОбъект.Ссылка;
		Если ТипЗнч(ВариантСсылка) = Тип("СправочникСсылка.ВариантыОтчетов") Тогда
			ВариантКлючВарианта = ВариантОбъект.КлючВарианта;
		Иначе
			ВариантКлючВарианта = ВариантОбъект.ИдентификаторКоманды;
		КонецЕсли; 
		РегистрыСведений.НастройкиВариантовОтчетов.ЗаписатьНастройкиДоступностиВариантаОтчета(
			ВариантСсылка,
			ВариантЭтоНовый,
			ПользователиВарианта);
		// Подсистемы варианта
		ДеревоПриемник = РеквизитФормыВЗначение("ДеревоПодсистем", Тип("ДеревоЗначений"));
		Если ВариантЭтоНовый Тогда
			ИзмененныеРазделы = ДеревоПриемник.Строки.НайтиСтроки(Новый Структура("Использование", 1), Истина);
		Иначе
			ИзмененныеРазделы = ДеревоПриемник.Строки.НайтиСтроки(Новый Структура("Модифицированность", Истина), Истина);
		КонецЕсли;
		ВариантыОтчетов.ДеревоПодсистемЗаписать(ВариантОбъект, ИзмененныеРазделы);
		
		Если НЕ ВариантЭтоНовый И СброситьНастройки Тогда
			ВариантыОтчетов.СброситьПользовательскиеНастройки(ВариантОбъект.Ссылка);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки; 
		
КонецПроцедуры

#КонецОбласти 

#Область ПривязкиПолейОтчетов

&НаСервере
Процедура ПрочитатьДанныеПривязок()
	
	Запрос = Новый Запрос;
	Если НЕ ЗначениеЗаполнено(ВариантСсылка) Тогда
		Запрос.УстановитьПараметр("Вариант", Контекст.Прототип);
	Иначе
		Запрос.УстановитьПараметр("Вариант", ВариантСсылка);
	КонецЕсли; 
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПривязкаПолейОтчетовУНФ.Поле,
	|	ПривязкаПолейОтчетовУНФ.Объект,
	|	ПривязкаПолейОтчетовУНФ.СложныйАлгоритм,
	|	ПривязкаПолейОтчетовУНФ.ДополнительныйРазрез,
	|	ПривязкаПолейОтчетовУНФ.Объект.Синоним КАК Представление
	|ИЗ
	|	РегистрСведений.ПривязкаПолейОтчетовУНФ КАК ПривязкаПолейОтчетовУНФ
	|ГДЕ
	|	ПривязкаПолейОтчетовУНФ.Вариант = &Вариант";
	ПривязкиПолей.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Для каждого Стр Из ПривязкиПолей Цикл
		Стр.Представление = ПредставлениеПривязки(Стр.ПолучитьИдентификатор(), Стр.Представление);
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеПривязки(Идентификатор, Синоним = "")
	
	Стр = ПривязкиПолей.НайтиПоИдентификатору(Идентификатор);
	Если ПустаяСтрока(Синоним) Тогда
		Синоним = Стр.Объект.Синоним;
	КонецЕсли; 
	ОбъектМетаданных = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(Стр.Объект);
	Если Метаданные.Справочники.Содержит(ОбъектМетаданных) Тогда
		Представление = НСтр("ru = 'Из справочника '")+""""+Синоним+"""";
	ИначеЕсли Метаданные.Документы.Содержит(ОбъектМетаданных) Тогда
		Представление = НСтр("ru = 'Из документа '")+""""+Синоним+"""";
	Иначе
		Представление = НСтр("ru = 'Из '")+""""+Синоним+"""";
	КонецЕсли; 
	НавигационнаяСсылкаФС = "Привязка_"+Идентификатор;
	Возврат ПредставлениеОтметки(Представление,, НавигационнаяСсылкаФС, Контекст.БезПривязки);
	
КонецФункции

&НаСервере
Процедура ОбновитьЭлементыПривязок()
	
	УдалитьЭлементы(Элементы.ГруппаПривязанныеПоляЭлементы);
	
	Если ОтключитьВидимостьГруппыПривязанныхПолей() Тогда
		Элементы.ГруппаПривязанныеПоля.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Для Каждого Стр Из ПривязкиПолей Цикл
		
		Индекс = ПривязкиПолей.Индекс(Стр);
		ПолеТега = Элементы.Добавить("Привязка_" + Индекс, Тип("ДекорацияФормы"), Элементы.ГруппаПривязанныеПоляЭлементы);
		ПолеТега.Вид = ВидДекорацииФормы.Надпись;
		ПолеТега.Заголовок = Стр.Представление;
		ПолеТега.АвтоМаксимальнаяШирина = Ложь;
		ПолеТега.МаксимальнаяШирина = 0;
		ПолеТега.УстановитьДействие("ОбработкаНавигационнойСсылки", "Подключаемый_ПривязкаОбработкаНавигационнойСсылки");
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОтключитьВидимостьГруппыПривязанныхПолей()
	
	Для каждого Элемент Из КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора.Элементы Цикл
		
		Если ТипЗнч(Элемент) <> Тип("ДоступноеПолеОтбораКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		
		Для каждого Тип Из Элемент.ТипЗначения.Типы() Цикл
			
			Если НЕ ОбщегоНазначения.ЭтоСсылка(Тип) Тогда
				Продолжить;
			КонецЕсли;
			
			Если Перечисления.ТипВсеСсылки().СодержитТип(Тип) Тогда
				Продолжить;
			КонецЕсли;
			
			Возврат Ложь;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура УдалитьЭлементы(Группа)
	
	УдаляемыеЭлементы = Новый Массив;
	Для Каждого СтрокаТегов Из Группа.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(СтрокаТегов);
	КонецЦикла;
	Для Каждого УдаляемыйЭлемент Из УдаляемыеЭлементы Цикл
		Элементы.Удалить(УдаляемыйЭлемент);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПривязкуСервер()
	
	ЗаполнитьСписокВыбораОбъектовПривязки();
	Если Элементы.ПривязкаОбъектМетаданных.СписокВыбора.Количество()=0 Тогда
		Возврат;
	КонецЕсли; 
	ПривязкаОбъектМетаданных = Элементы.ПривязкаОбъектМетаданных.СписокВыбора[0].Значение;
	ЗаполнитьСписокВыбораПолейПривязки();
	Элементы.ГруппаДобавлениеПривязки.Видимость = Истина;
	Элементы.ДобавитьПривязку.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораОбъектовПривязки()
	
	Если НЕ НастройкиФормы.Свойство("ПоляИИдентификаторы") Тогда
		НастройкиФормы.Вставить("ПоляИИдентификаторы", Новый Массив);
		ОбновитьСписокПолей = Истина;
	Иначе
		ОбновитьСписокПолей = Ложь;
	КонецЕсли; 
	Элементы.ПривязкаОбъектМетаданных.СписокВыбора.Очистить();
	ДоступныеПоля = КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора;
	Для каждого Элемент Из ДоступныеПоля.Элементы Цикл
		Если ТипЗнч(Элемент) <> Тип("ДоступноеПолеОтбораКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		Для каждого Тип Из Элемент.ТипЗначения.Типы() Цикл
			Если НЕ ОбщегоНазначения.ЭтоСсылка(Тип) Тогда
				Продолжить;
			КонецЕсли;
			Если Перечисления.ТипВсеСсылки().СодержитТип(Тип) Тогда
				Продолжить;
			КонецЕсли;
			Идентификатор = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип);
			Если ОбновитьСписокПолей Тогда
				СтруктураПоля = Новый Структура;
				СтруктураПоля.Вставить("Поле", Строка(Элемент.Поле));
				СтруктураПоля.Вставить("Представление", Элемент.Заголовок);
				СтруктураПоля.Вставить("Идентификатор", Идентификатор);
				НастройкиФормы.ПоляИИдентификаторы.Добавить(СтруктураПоля);
			КонецЕсли; 
			Если Элементы.ПривязкаОбъектМетаданных.СписокВыбора.НайтиПоЗначению(Идентификатор)=Неопределено Тогда
				Элементы.ПривязкаОбъектМетаданных.СписокВыбора.Добавить(Идентификатор);
			КонецЕсли; 
		КонецЦикла; 
	КонецЦикла;
	Элементы.ПривязкаОбъектМетаданных.СписокВыбора.СортироватьПоЗначению(НаправлениеСортировки.Возр);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораПолейПривязки()
	
	Элементы.ПривязкаПолеОтчета.СписокВыбора.Очистить();
	ПривязкаПолеОтчета = Неопределено;
	Если НЕ ЗначениеЗаполнено(ПривязкаОбъектМетаданных) И НЕ НастройкиФормы.Свойство("ПоляИИдентификаторы") Тогда
		Возврат;
	КонецЕсли;
	Для каждого СтруктураПоля Из НастройкиФормы.ПоляИИдентификаторы Цикл
		Если НЕ СтруктураПоля.Идентификатор=ПривязкаОбъектМетаданных Тогда
			Продолжить;
		КонецЕсли;
		Элементы.ПривязкаПолеОтчета.СписокВыбора.Добавить(СтруктураПоля.Поле, СтруктураПоля.Представление);
	КонецЦикла;
	Если Элементы.ПривязкаПолеОтчета.СписокВыбора.Количество()=1 Тогда
		ПривязкаПолеОтчета = Элементы.ПривязкаПолеОтчета.СписокВыбора[0].Значение;
	КонецЕсли;
	Элементы.ПривязкаПолеОтчета.Доступность = (Элементы.ПривязкаПолеОтчета.СписокВыбора.Количество()>1);
	
КонецПроцедуры

&НаСервере
Процедура ПривязкаСохранитьСервер()
	
	Если Контекст.БезПривязки Тогда
		Возврат;
	КонецЕсли; 
	Стр = ПривязкиПолей.Добавить();
	Стр.Объект = ПривязкаОбъектМетаданных;
	Стр.Поле = ПривязкаПолеОтчета;
	Стр.Представление = ПредставлениеПривязки(Стр.ПолучитьИдентификатор());
	ОбновитьЭлементыПривязок();
	Элементы.ГруппаДобавлениеПривязки.Видимость = Ложь;
	Элементы.ДобавитьПривязку.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеОтметки(НаименованиеТега, ПометкаУдаления = Ложь, НавигационнаяСсылкаФС = "" , БезПривязки = Ложь)
	
	Цвет = ЦветаСтиля.ТекстВторостепеннойНадписи;
	БазовыйШрифт = ШрифтыСтиля.ОбычныйШрифтТекста;
	
	Шрифт = Новый Шрифт(БазовыйШрифт, , , Истина, , ?(ПометкаУдаления, Истина, Неопределено));
	
	КомпонентыФС = Новый Массив;
	КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(НаименованиеТега + Символы.НПП, Шрифт, Цвет));
	Если НЕ БезПривязки Тогда
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(БиблиотекаКартинок.ОчиститьЗначение12х12, , , , НавигационнаяСсылкаФС));
	КонецЕсли; 
	
	// АПК:1356 Используется локализованное имя тега + картинка
	Возврат Новый ФорматированнаяСтрока(КомпонентыФС);
	
КонецФункции

#КонецОбласти 

#КонецОбласти
 