#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КоличествоОставшихсяДней = ПереходВБольшойУНФИзМобильногоПриложения20МПУНФ.ПолучитьКоличествоДнейДоОкончанияПробногоПериода();
	
	Если КоличествоОставшихсяДней > 1 Тогда
		ЗакрытиеРазрешено = Ложь;
		Элементы.ДекорацияВПоследнийДень.Видимость = Ложь;
		Элементы.ДекорацияВПоследнийДень1.Видимость = Ложь;
		ЭтаФорма.Элементы.ЗавершитьРаботу.Видимость = Ложь;
		Если КоличествоОставшихсяДней < 6 Тогда
			Элементы.НапоминаниеОКоличествеДней.Видимость = Истина;
			НапоминаниеОКоличествеДней = НСтр("ru = 'До окончания пробного периода осталось'") + КоличествоОставшихсяДней + НСтр("ru = 'дней.'");
		КонецЕсли;
	Иначе
		ЗакрытиеРазрешено = Истина;
		Элементы.ДекорацияВоВремяПробногоПериода.Видимость = Ложь;
		ЭтаФорма.Элементы.Пропустить.Видимость = Ложь;
	КонецЕсли;
	
	ТекстДней = "";
	Если КоличествоОставшихсяДней > 5 ИЛИ КоличествоОставшихсяДней = 0 Тогда
		ТекстДней = НСтр("ru='дней'");
	ИначеЕсли КоличествоОставшихсяДней > 1 Тогда
		ТекстДней = НСтр("ru='дня'");
	ИначеЕсли КоличествоОставшихсяДней = 1 Тогда
		ТекстДней = НСтр("ru='день'");
	КонецЕсли;
	
	Элементы.ДекорацияВоВремяПробногоПериода.Заголовок = СтрШаблон(Элементы.ДекорацияВоВремяПробногоПериода.Заголовок, Строка(КоличествоОставшихсяДней), ТекстДней);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗакрытиеРазрешено Тогда
		ПрекратитьРаботуСистемы();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Пропустить(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьсяВМобильноеПриложение20(Команда)
	
	Текст = НСтр("ru = 'При обратном переходе, данные, созданные в пробной версии, не перенесутся в мобильное приложение.
	|Продолжить?'");
	Результат = Неопределено;
	ПоказатьВопрос(Новый ОписаниеОповещения("ВернутьсяВМобильноеПриложение20ПриСогласии", ЭтотОбъект), Текст, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРаботу(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВернутьсяВМобильноеПриложение20ПриСогласии(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ЗакрытиеРазрешено = Ложь;
		ВключитьМобильный20ПриСогласииНаСервере();
		МассивПользователей = МобильноеПриложениеВызовСервера.ПолучитьМассивПользователей();
		МобильноеПриложениеВызовСервера.УстановитьМинимальныйИнтерфейс(МассивПользователей);
		МобильноеПриложениеВызовСервера.УстановитьСоставФормДляПользователей(МассивПользователей, Истина);
		
		ПланПоиска = ГлобальныйПоиск.ПолучитьПлан();
		ПланПоиска.Удалить(СтандартныйВидГлобальногоПоиска.МенюФункций);
		ПланПоиска.Удалить(СтандартныйВидГлобальногоПоиска.Данные);
		ГлобальныйПоиск.УстановитьПлан(ПланПоиска);

		ОбновитьИнтерфейс();
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВключитьМобильный20ПриСогласииНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Константы.ЭтоМобильноеПриложение20.Установить(Истина);
	Константы.ЭтоОбычноеПриложение.Установить(Ложь);
	Константы.ЭтоМобильноеПриложение.Установить(Ложь);	
	Константы.ВключенПробныйПериодМПУНФ.Установить(Ложь);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Оплатить(Команда)
	
	ПерейтиПоНавигационнойСсылке("https://1cfresh.com/register");
	
КонецПроцедуры

#КонецОбласти

