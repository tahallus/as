
#Область ПрограммныйИнтерфейс

&НаКлиенте
Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru = 'При подключении оборудования произошла ошибка:""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкодуЗавершение(ДанныеШтрихкода, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ДанныеШтрихкода) = Тип("Структура") Тогда
		Если ДанныеШтрихкода.Свойство("Штрихкод") Тогда
			КодАкцизнойМарки = ДанныеШтрихкода.Штрихкод;
			Если ЗначениеЗаполнено(КодАкцизнойМарки) Тогда
				ЗакрытьФормуПриСканировании();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("КодыМаркировки") Тогда
		НадписьОтсканируйте = НСтр("ru = 'Отсканируйте код маркировки'");
		Элементы.СтраницыТиповЗапросов.ТекущаяСтраница = Элементы.СтраницаКодМаркировки;
		РежимЗапросаТабачныхКодов = Истина;
	Иначе
		НадписьОтсканируйте = НСтр("ru = 'Отсканируйте код акцизной марки'");
		Элементы.СтраницыТиповЗапросов.ТекущаяСтраница = Элементы.СтраницаАкциз;
		РежимЗапросаТабачныхКодов = Ложь;
	КонецЕсли;
	
	ОткрытоИзФормыПодбора = Параметры.Свойство("ОткрытоИзФормыПодбора");
	
	ИспользоватьПодключаемоеОборудование = УправлениеНебольшойФирмойПовтИсп.ИспользоватьПодключаемоеОборудование();
	
	Если Параметры.Свойство("ТекущийКлючСвязи") Тогда
		ТекущийКлючСвязи = Параметры.ТекущийКлючСвязи;
	КонецЕсли;
	
	Если Параметры.Свойство("ПредставлениеНоменклатуры") Тогда
		ПредставлениеНоменклатуры = Параметры.ПредставлениеНоменклатуры;
		ЭтотОбъект.Заголовок = ПредставлениеНоменклатуры;
	КонецЕсли;
	
	Если Параметры.Свойство("Номенклатура") Тогда
		Номенклатура = Параметры.Номенклатура;
	КонецЕсли;
	
	Если Параметры.Свойство("Операция") Тогда
		Операция = Параметры.Операция;
	Иначе
		Операция = "Продажа";
	КонецЕсли;
	
	// Режим отладки
	РежимОтладки = ОбщегоНазначения.РежимОтладки()
	             И Пользователи.ЭтоПолноправныйПользователь();
	
	Элементы.ФормаПоискПоШтрихкоду.Видимость = РежимОтладки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ПодключаемоеОборудование
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");
	ОповещенияПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ЭтотОбъект);  
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПоТипу(ОповещенияПриПодключении, ЭтотОбъект, ПоддерживаемыеТипыВО);
	// Конец ПодключаемоеОборудование
	
	Если РежимЗапросаТабачныхКодов Тогда
		ЭтотОбъект.ТекущийЭлемент = Элементы.ДекорацияКартинкаМаркировка;
	Иначе
		ЭтотОбъект.ТекущийЭлемент = Элементы.ДекорацияКартинка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// ПодключаемоеОборудование
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПоТипу(, ЭтотОбъект, ПоддерживаемыеТипыВО);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если ВводДоступен() И НЕ ТолькоПросмотр Тогда
		
		// Заменяем спец.символы, которые потом не сможем передать на сервер.
		РазделительGS1 = МенеджерОборудованияМаркировкаКлиентСервер.РазделительGS1();
		ЭкранированныйСимволGS1 = МенеджерОборудованияМаркировкаКлиентСервер.ЭкранированныйСимволGS1();
		Данные = СтрЗаменить(Данные, РазделительGS1, ЭкранированныйСимволGS1);
		
		ОписаниеСобытия = Новый Структура();
		ОписаниеОшибки  = "";
		ОписаниеСобытия.Вставить("Источник", Источник);
		ОписаниеСобытия.Вставить("Событие",  Событие);
		ОписаниеСобытия.Вставить("Данные",   Данные);
		
		Результат = МенеджерОборудованияКлиент.ПолучитьСобытиеОтУстройства(ОписаниеСобытия, ОписаниеОшибки);
		Если Результат = Неопределено Тогда 
			ТекстСообщения = НСтр("ru = 'При обработке внешнего события от устройства произошла ошибка:'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Иначе
			Если Результат.Источник = "ПодключаемоеОборудование" Тогда
				Если Результат.ИмяСобытия = "ScanData" Тогда
					Если Результат.Параметр[1] = Неопределено Тогда
						КодАкцизнойМарки = Результат.Параметр[0];
					Иначе
						КодАкцизнойМарки = Результат.Параметр[1][1];
					КонецЕсли;
					ПодключитьОбработчикОжидания("ЗакрытьФормуПриСканировании", 0.1, Истина);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияКартинкаНажатие(Элемент)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоискПоШтрихкодуВыполнить(Команда)
	
	ОчиститьСообщения();
	
	ШтрихкодированиеИСКлиентПереопределяемый.ПоказатьВводШтрихкода(
		Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", ЭтотОбъект));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗакрытьФормуПриСканировании()
	
	Если РежимЗапросаТабачныхКодов Тогда
		ДанныеМаркировки = МенеджерОборудованияМаркировкаКлиентСервер.РазобратьШтриховойКодТовара(КодАкцизнойМарки);
		Если НЕ ДанныеМаркировки.Разобран Тогда
			ТекстСообщения = НСтр("ru = 'Штрихкод не соответствует формату.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	Иначе
		Если НЕ ЭтоШтрихкодАкцизнойМарки(КодАкцизнойМарки) Тогда
			ТекстСообщения = НСтр("ru = 'Штрихкод не соответствует формату.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ШтрихкодМаркиПрисутствуетВЧеке() Тогда
		Если РежимЗапросаТабачныхКодов Тогда
			ТекстСообщения = НСтр("ru = 'Код маркировки уже присутствует в чеке.'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Штрихкод марки уже присутствует в чеке.'");
		КонецЕсли;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Закрыть(КодАкцизнойМарки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоШтрихкодАкцизнойМарки(КодАкцизнойМарки)
	
	Возврат ШтрихкодированиеЕГАИС.ЭтоШтрихкодАкцизнойМарки(КодАкцизнойМарки);
	
КонецФункции

&НаКлиенте
Функция ШтрихкодМаркиПрисутствуетВЧеке()
	
	Если ОткрытоИзФормыПодбора Тогда
		ИмяТаблицы = "КорзинаЦенаОстатокРезервХарактеристика";
		ИмяТаблицыДокумента = "Запасы";
		ИмяКолонки = "КодМаркировки";
	ИначеЕсли РежимЗапросаТабачныхКодов Тогда
		ИмяТаблицы = "Запасы";
		ИмяКолонки = "КодМаркировки";
	Иначе
		ИмяТаблицы = "АкцизныеМарки";
		ИмяКолонки = "КодАкцизнойМарки";
	КонецЕсли;
	МассивАкцизныхМарок = ВладелецФормы.Объект[ИмяТаблицы].НайтиСтроки(Новый Структура(ИмяКолонки, КодАкцизнойМарки));
		
	Если ОткрытоИзФормыПодбора Тогда
		МассивАкцизныхМарокДокумента = ВладелецФормы.ВладелецФормы.Объект[ИмяТаблицыДокумента].НайтиСтроки(Новый Структура(ИмяКолонки, КодАкцизнойМарки));
		Если МассивАкцизныхМарок.Количество() > 0 ИЛИ МассивАкцизныхМарокДокумента.Количество() > 0 Тогда
			Возврат Истина;
		КонецЕсли;
	Иначе
		Для Каждого СтрокаМарки Из МассивАкцизныхМарок Цикл
			Если СтрокаМарки.КлючСвязи <> ТекущийКлючСвязи Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти
