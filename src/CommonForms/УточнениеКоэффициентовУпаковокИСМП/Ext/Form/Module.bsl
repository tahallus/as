#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВходящаяТаблицаОписаниеGTIN = ПолучитьИзВременногоХранилища(Параметры.АдресУточнениеКоэффициентовУпаковок);
	
	Если ТипЗнч(ВходящаяТаблицаОписаниеGTIN) = Тип("ТаблицаЗначений") Тогда
		
		ОписаниеGTIN.Загрузить(ВходящаяТаблицаОписаниеGTIN);
		
	ИначеЕсли ТипЗнч(ВходящаяТаблицаОписаниеGTIN) = Тип("Массив") Тогда
		
		Для Каждого СтрокаДанных Из ВходящаяТаблицаОписаниеGTIN Цикл
			НоваяСтрока = ОписаниеGTIN.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);
		КонецЦикла;
		
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из ОписаниеGTIN Цикл
		Если ТипЗнч(СтрокаТЧ.ПредставлениеСодержимоеУпаковки) <> Тип("Строка") Тогда
			СтрокаТЧ.НоменклатураЗаполнена = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Отказ = Ложь;
	Для Каждого СтрокаТЧ Из ОписаниеGTIN Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТЧ.Коэффициент) Тогда
			
			Индекс = ОписаниеGTIN.Индекс(СтрокаТЧ) + 1;
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Не заполнено поле ""Количество вложенных потребительских упаковок""'"),,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОписаниеGTIN", Индекс, "Коэффициент"),,
				Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = СохранитьДанные();
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть("УточненыКоэффициентыУпаковокИСМП");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОписаниеGTINПредставлениеСодержимоеУпаковки.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ОписаниеGTIN.НоменклатураЗаполнена");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОписаниеGTINВидУпаковки.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ОписаниеGTIN.ВидУпаковки");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст",      НСтр("ru = '<нет данных>'"));
	
КонецПроцедуры

&НаСервере
Функция СохранитьДанные()
	
	ИсточникДанных = ОписаниеGTIN.Выгрузить();
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОписаниеGTINИС");
		ЭлементБлокировки.ИсточникДанных = ИсточникДанных;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("GTIN", "GTIN");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Для Каждого СтрокаТЧ Из ИсточникДанных Цикл
			
			НаборЗаписей = РегистрыСведений.ОписаниеGTINИС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.GTIN.Установить(СтрокаТЧ.GTIN, Истина);
			
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаписьНабора.GTIN        = СтрокаТЧ.GTIN;
			Если ЗначениеЗаполнено(СтрокаТЧ.ВидУпаковки) Тогда
				ЗаписьНабора.ВидУпаковки = СтрокаТЧ.ВидУпаковки;
			ИначеЕсли СтрокаТЧ.Коэффициент > 1 Тогда
				ЗаписьНабора.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая;
			Иначе
				ЗаписьНабора.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская;
			КонецЕсли;
			ЗаписьНабора.Коэффициент = СтрокаТЧ.Коэффициент;
			
			НаборЗаписей.Записать(Истина);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
		Возврат Ложь;
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось записать набор записей регистра сведений ОписаниеGTIN ИС по причине: %1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
				НСтр("ru = 'ГосИС: Запись коэффициентов вложенных потребительских упаковок'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.ОписаниеGTINИС, Неопределено,
			ТекстСообщения);
		
		Возврат Истина;
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти