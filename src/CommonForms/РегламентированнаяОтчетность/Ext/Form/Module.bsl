
&НаКлиенте
Перем ФормаДлительнойОперации;

&НаКлиенте
// Переменная инициализируется в ОМ "ОнлайнСервисыРегламентированнойОтчетностиКлиент".
Перем ОписаниеОповещенияПослеПроверкиПодключенияКИПП Экспорт;

&НаКлиенте
// Переменная используется извне формы механизмом длительных операций алкоотчетности.
Перем ПараметрыОбработчикаОжидания Экспорт;

&НаКлиенте
// Переменная инициализируется по первому запросу
Перем КонтекстЭДОКлиент, КонтекстУжеПодключался;

&НаКлиенте
Перем ФормаОтчета; // Обход ошибки в платформе.

&НаКлиенте
Перем ВыбранныеОрганизации; // для обмена.

&НаКлиенте
Перем НомерОбъекта; // для обмена.

&НаКлиенте
Перем ВыполняетсяЗакрытиеФормы; // флаг для отключения обработки оповещения при закрытии формы.

&НаКлиенте
// Переменная используется механизмом длительных операций алкоотчетности.
Перем СтруктураВспомДанныхДлитОперацииАЛКО Экспорт;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ЭтоРежимВыбора", ЭтоРежимВыбора);
	Параметры.Свойство("ОтборОрган", ОтборОрган);
	Параметры.Свойство("ТолькоОтправленные", ТолькоОтправленные);
	
	#Область Используется1СОтчетность
	Используется1СОтчетность = РегламентированнаяОтчетность.Используется1СОтчетность();
	Если НЕ Используется1СОтчетность Тогда
		ПроверкаСостоянияИнтеграцииВФонеПриОткрытии
			= РегламентированнаяОтчетностьВызовСервера.СостояниеИнтеграцииСБанкомВФоне(УникальныйИдентификатор);
	КонецЕсли;
	#КонецОбласти 
	ДоступнаОтправкаРегламентированногоОтчета = РегламентированнаяОтчетностьПереопределяемый.ДоступнаОтправкаРегламентированногоОтчета();
	
	ОпределитьПоказыватьЛиПредложениеНаПодключение();
	
	// Инициализируем цвета
	Желтый	= Новый Цвет(251, 225, 81);
	Серый	= Новый Цвет(242, 242, 242);
	Черный	= Новый Цвет(0, 0, 0);
	
	// Определяем организацию по-умолчанию в случае использования одной организации
	Организация = Неопределено;
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Если РегламентированнаяОтчетностьВызовСервера.ИспользуетсяОднаОрганизация() Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("Справочники.Организации");
			Организация = Модуль.ОрганизацияПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
	// Если организаций больше одной, то используем основную организацию
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = РегламентированнаяОтчетность.ПолучитьОрганизациюПоУмолчанию();
	КонецЕсли;
	
	СохраненныйСпособОтбораПериода = ХранилищеНастроекДанныхФорм.Загрузить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_ТолькоУказанныйПериод");
	Если СохраненныйСпособОтбораПериода <> Неопределено Тогда
		ТолькоУказанныйПериод = СохраненныйСпособОтбораПериода;
	КонецЕсли;
	СохраненныйПериодОтчета = ХранилищеНастроекДанныхФорм.Загрузить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_ПериодОтчета");
	Если СохраненныйПериодОтчета <> Неопределено Тогда
		ПериодОтчета = СохраненныйПериодОтчета;
	КонецЕсли;
	СохраненныйНачалоПериода = ХранилищеНастроекДанныхФорм.Загрузить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_НачалоПериода");
	Если СохраненныйНачалоПериода <> Неопределено Тогда
		НачалоПериода = СохраненныйНачалоПериода;
	КонецЕсли;
	СохраненныйКонецПериода = ХранилищеНастроекДанныхФорм.Загрузить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_КонецПериода");
	Если СохраненныйКонецПериода <> Неопределено Тогда
		КонецПериода = СохраненныйКонецПериода;
	КонецЕсли;
	
	СохраненнаяОрганизация = ХранилищеНастроекДанныхФорм.Загрузить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_Организация");
	Если СохраненнаяОрганизация <> Неопределено И НЕ Параметры.Свойство("Организация") И НЕ Параметры.Свойство("ВидОтчета") Тогда
		Организация = СохраненнаяОрганизация;
	КонецЕсли;
	СохраненныйВидОтчета = ХранилищеНастроекДанныхФорм.Загрузить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_ВидОтчета");
	Если СохраненныйВидОтчета <> Неопределено И НЕ Параметры.Свойство("Организация") И НЕ Параметры.Свойство("ВидОтчета") Тогда
		МассивВидовОтчетов = ПолучитьСписокОтчетовИзРегистраСведений();
		Элементы.ОтборВидОтчета.СписокВыбора.ЗагрузитьЗначения(МассивВидовОтчетов);
		ВидОтчета = СохраненныйВидОтчета;
	КонецЕсли;
	
	СохраненныеПользовательскиеНастройкиОтчетов = ХранилищеНастроекДанныхФорм.Загрузить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_ПользовательскиеНастройкиОтчетов");
	Если ТипЗнч(СохраненныеПользовательскиеНастройкиОтчетов) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
		ВостановитьНастройкиОтборовИСортировкиОтчетов(ЭтотОбъект, СохраненныеПользовательскиеНастройкиОтчетов);
	КонецЕсли;
	
	// Заполняем предварительно параметры и настройки онлайн-сервисов регламентированной отчетности.
	ПараметрыИНастройкиОнлайнСервисов =
		ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.ПолучитьПараметрыИНастройкиМеханизмаОнлайнСервисовРО();
	
	// Заполняем признак использования внешней программы для ведения персонифицированного учета.
	ПерсонифицированныйУчетВедетсяВоВнешнейПрограмме = РегламентированнаяОтчетность.ПерсонифицированныйУчетВедетсяВоВнешнейПрограмме();
	
	// Заполняем список выбора для видов сверок.
	СписокВыбораВидаСверки = Элементы.ОтборВидСверки.СписокВыбора;
	
	СписокВыбораВидаСверки.Добавить(Строка(Перечисления.ВидыУслугПриИОН.ПредставлениеАктовСверкиРасчетов));
	СписокВыбораВидаСверки.Добавить(Строка(Перечисления.ВидыУслугПриИОН.ПредставлениеВыпискиОперацийИзКарточкиРасчетыСБюджетом));
	СписокВыбораВидаСверки.Добавить(Строка(Перечисления.ВидыУслугПриИОН.ПредставлениеПеречняБухгалтерскойИНалоговойОтчетности));
	СписокВыбораВидаСверки.Добавить(Строка(Перечисления.ВидыУслугПриИОН.ПредставлениеСправкиОСостоянииРасчетовСБюджетом));
	СписокВыбораВидаСверки.Добавить(Строка(Перечисления.ВидыУслугПриИОН.ПредставлениеСправкиОбИсполненииОбязанностейПоУплате));
	СписокВыбораВидаСверки.Добавить(Строка(Перечисления.ВидыУслугПриИОС.СверкаФИОиСНИЛС));
	СписокВыбораВидаСверки.Добавить(Строка(Перечисления.ВидыУслугПриИОС.СправкаОСостоянииРасчетов));
	
	ИмяТипаДокументаРеестрДанныхЭЛН = "РеестрДанныхЭЛНЗаполняемыхРаботодателем";
	Если Метаданные.Документы.Найти(ИмяТипаДокументаРеестрДанныхЭЛН) = Неопределено Тогда
		ИмяТипаДокументаРеестрДанныхЭЛН = "";
	КонецЕсли;
	
	ОткрытьНаНужнойСтранице(Параметры);
	
	НазначитьВидУведомленияИзПараметра(Параметры);
	НазначитьВидОтчетаИзПараметра(Параметры);
	
	ДействияПриИзмененииОтборов(ЭтотОбъект);
	
	СделатьВсеГиперссылкиНеактивными();
	Для каждого Раздел Из Разделы(ЭтотОбъект, Истина) Цикл
		Элементы["Гиперссылка" + Раздел].Заголовок = Раздел;
	КонецЦикла; 
	
	УправлениеЭУ(ЭтотОбъект, Истина);
	
	УправлениеЭУВРежимеСервиса();
	
	РегламентированнаяОтчетность.ФормаРегламентированнойОтчетности_ПриСозданииНаСервере(ЭтотОбъект);
	
	// Обеспечим корректную работу при переопределении формы выбора вида отчета.
	Если Элементы.ОтборВидОтчета.РежимВыбораИзСписка Тогда
		// Преобразуем ВидОтчета к типу "Строка".
		Если ТипЗнч(ВидОтчета) = Тип("СписокЗначений") Тогда
			ВидОтчета = ?(ЗначениеЗаполнено(ВидОтчета), ВидОтчета[0].Значение, "");
		Иначе
			ВидОтчета = Строка(ВидОтчета);
		КонецЕсли;
	Иначе
		// Преобразуем ВидОтчета к типу "СписокЗначений".
		Если ТипЗнч(ВидОтчета) = Тип("СписокЗначений") Тогда
			ВидОтчета = ВидОтчета.Скопировать();
		Иначе
			СтрВидОтчета = Строка(ВидОтчета);
			ВидОтчета = Новый СписокЗначений;
			ВидОтчета.Добавить(СтрВидОтчета);
		КонецЕсли;
	КонецЕсли;
	
	Элементы.УведомленияЗагрузитьИзФайлаВФормуУведомление.Видимость = ДокументооборотСКОВызовСервера.ИспользуетсяРежимТестирования();
	
	// Запуск фонового задания для поиска объектов, не записанных в регистры
	РезультатПроверкиКоличестваНезаписанныхОбъектов = ПроверитьКоличествоОбъектовНеЗаписанныхВРегистр();
	
	// Скрываем некоторые элементы при отключенной 1С-Отчетности
	УправлениеВидимостьюЭлементовПриОтключенномЭДО();
	
	УправлениеВидимостьюЛичныхКабинетов();
	
	ЗаполнитьДеревоНовое(Истина);
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Новости") Тогда
		
		ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
		
		МодульОбработкаНовостей = ОбщегоНазначения.ОбщийМодуль("ОбработкаНовостей");
		
		МодульОбработкаНовостей.КонтекстныеНовости_ПриСозданииНаСервере(
			ЭтотОбъект,
			"БП.ОбщаяФорма.РегламентированнаяОтчетность",
			"ОбщаяФорма",
			,
			НСтр("ru='Новости: Регламентированная отчетность'"),
			Ложь,
			Новый Структура("ПолучатьНовостиНаСервере, ХранитьМассивНовостейТолькоНаСервере", Истина, Ложь),
			ИдентификаторыСобытийПриОткрытии);
		
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	ОшибкиПоследнегоОбмена = ДлительнаяОтправкаВызовСервера.ПолучитьОшибкиПоследнегоОбмена();
	Если ОшибкиПоследнегоОбмена <> Неопределено Тогда
		АдресСведенийПоОшибкам = ПоместитьВоВременноеХранилище(ОшибкиПоследнегоОбмена, Новый УникальныйИдентификатор);
	КонецЕсли;
	УстановитьВидимостьПанелиОшибокОбновления(ЭтотОбъект);
	
	ТекстОшибкиИнициализацииКонтекстаЭДО = НСтр("ru = 'Подождите, пожалуйста. Выполняется обновление модуля 1С-Отчетности...'");
	
	#Область Используется1СОтчетность
	Элементы.НастройкиЭлектронногоДокументооборота.Видимость = Используется1СОтчетность;
	
	Элементы.ОтчетыПроверитьВИнтернете.Видимость = Используется1СОтчетность;
	Элементы.ГиперссылкаПисьма.Видимость         = Используется1СОтчетность;
	Элементы.ГиперссылкаСверки.Видимость         = Используется1СОтчетность;
	Элементы.ГиперссылкаЕГРЮЛ.Видимость          = Используется1СОтчетность;
	Элементы.ГиперссылкаВходящие.Видимость       = Используется1СОтчетность;
	#КонецОбласти
	
	СкрытьЛишниеЭлементыВрежимеВыбора(Параметры);
	
	УстановленыОграниченияПрав = ПроверитьДоступностьНастроекОграниченияПравНаОбмен();
	УстановитьВидимостьКнопкиНастроекПрав(ЭтотОбъект);
	
	// Скрываем некоторые элементы для ИП на УСН 6%
	УпроститьФормуРегОтчетности();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицуУведомленийСХешем()
	ТаблицаУведомленийСХешем.Очистить();
	ЗначениеВРеквизитФормы(УведомлениеОСпецрежимахНалогообложения.ТаблицаУведомленийСХешем(), "ТаблицаУведомленийСХешем");
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии()
	
	КонтекстУжеПодключался = Ложь;
	
	Если Элементы.Разделы.ТекущаяСтраница = Элементы.СтраницаНастройки Тогда
		УправлениеЭУГруппаНастройки();
	КонецЕсли;
	
	#Если ВебКлиент Тогда 
		ЭтоВебКлиент = Истина;
	#КонецЕсли
	
	ПараметрыОбработчикаОжидания = Новый Структура;
	ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
	ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);
	
	Если РезультатПроверкиКоличестваНезаписанныхОбъектов <> Неопределено Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ОповещениеПроверитьВыполнение = Новый ОписаниеОповещения(
			"ПроверитьВыполнениеПодсчетаКоличестваНезаписанныхОбъектов", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			РезультатПроверкиКоличестваНезаписанныхОбъектов, ОповещениеПроверитьВыполнение, ПараметрыОжидания);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Новости") Тогда
		МодульОбработкаНовостейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбработкаНовостейКлиент");
		МодульОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьНадписьВремяОбновления", 500);
	ПодключитьОбработчикОжидания("Подключаемый_ПроизвестиДействияПослеПоказаФормы", 1, Истина);
	
	Если ЗначениеЗаполнено(СсылкаВКлюче) и ЗначениеЗаполнено(ОрганизацияВКлюче) Тогда
		СпозиционироватьсяНаСсылка(СсылкаВКлюче, ОрганизацияВКлюче, "Позиционирование");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьКонтекстЭДО(ОповещениеЗавершения = Неопределено) Экспорт
	Перем ДополнительныеПараметры;
	
	Если ОповещениеЗавершения <> Неопределено Тогда 
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИнициализироватьКонтекстЭДОЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьКонтекстЭДОЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	ТекстОшибкиИнициализацииКонтекстаЭДО = Результат.ТекстОшибки;
	КонтекстУжеПодключался = Истина;
	Если ДополнительныеПараметры <> Неопределено Тогда 
		Если ДополнительныеПараметры.Свойство("ОповещениеЗавершения") Тогда 
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещенияПредварительная(Результат, ПараметрыОповещения) Экспорт
	
	ОбработкаОповещения(
		ПараметрыОповещения.ИмяСобытия, 
		ПараметрыОповещения.Параметр, 
		ПараметрыОповещения.Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ЭтоРежимВыбора Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыполняетсяЗакрытиеФормы = Истина // отменяем выполнение обработчика в закрываемой форме.
	 ИЛИ ИмяСобытия = "Получение контекста ЭДО" Тогда
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ИмяСобытия", ИмяСобытия);
		ПараметрыОповещения.Вставить("Параметр", Параметр);
		ПараметрыОповещения.Вставить("Источник", Источник);
		Оповещение = Новый ОписаниеОповещения("ОбработкаОповещенияПредварительная", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
		
	Если ИмяСобытия = "Открытие формы 1С-Отчетность" Тогда
		
		Раздел = Параметр.Раздел;
		Если Элементы.Разделы.ТекущаяСтраница <> Элементы["Страница" + ИмяСтраницы(Раздел)] Тогда
			ОткрытьНаНужнойСтранице(Параметр);
			СделатьВсеГиперссылкиНеактивными();
			УправлениеЭУ(ЭтотОбъект, Истина);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИзменениеНастроекЭДООрганизации" ИЛИ ИмяСобытия = "ОбновитьУчетнуюЗапись" Тогда
		
		ОпределитьДоступностьЭДО();
		УправлениеВидимостьюЭлементовПриОтключенномЭДО();
		
		Если Параметр = Организация Тогда
			УправлениеЭУГруппаНастройки();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Завершение обновления"
		ИЛИ ИмяСобытия = "Завершение расшифровки"
		ИЛИ ИмяСобытия = "Завершение групповой отправки" Тогда
		
		ТекущаяСтраница = ТекущаяСтраница(ЭтотОбъект);
		Если ТекущаяСтраница <> ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Новое")
			И ТекущаяСтраница <> ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Настройки")
			И ТекущаяСтраница <> ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Сервисы") Тогда
			ТекущаяТаблица = ТекущаяТаблица(ЭтотОбъект);
			ТекущаяТаблица.Обновить();
		КонецЕсли;
		
		ПереопределитьОформлениеСверки();
		
	ИначеЕсли ТипЗнч(Источник) <> Тип("ФормаКлиентскогоПриложения")
		И (СтрНайти(ИмяСобытия, "Запись_") > 0 
		ИЛИ ИмяСобытия = "Завершение автообмена с сервером ФСС" 
		ИЛИ ИмяСобытия = "Запись_ОтправкиФСС" 
		ИЛИ ИмяСобытия = "Редактирование_ОтправкиФСС" 
		ИЛИ ИмяСобытия = "Завершение автообмена с сервером ФСРАР" 
		ИЛИ ИмяСобытия = "Запись_ОтправкиФСРАР" 
		ИЛИ ИмяСобытия = "Редактирование_ОтправкиФСРАР"
		ИЛИ ИмяСобытия = "Завершение автообмена с сервером РПН" 
		ИЛИ ИмяСобытия = "Запись_ОтправкиРПН" 
		ИЛИ ИмяСобытия = "Редактирование_ОтправкиРПН"
		ИЛИ ИмяСобытия = "Завершение автообмена с сервером ФТС" 
		ИЛИ ИмяСобытия = "Запись_ОтправкиФТС" 
		ИЛИ ИмяСобытия = "Редактирование_ОтправкиФТС"
		ИЛИ ИмяСобытия = "Завершение отправки в контролирующий орган"
		ИЛИ ИмяСобытия = "Завершение отправки"
		ИЛИ ИмяСобытия = "Изменение пометки удаления объекта"
		ИЛИ ИмяСобытия = "Скрытие/восстановление объекта"
		ИЛИ ИмяСобытия = "Позиционирование в списке отчетов"
		ИЛИ ИмяСобытия = "Пометка прочтенным"
		ИЛИ ИмяСобытия = "Пометка непрочтенным"
		ИЛИ ИмяСобытия = "Актуализация состояния отправки") Тогда
		
		ОрганизацияВКлюче = Неопределено;
		
		СсылкаВКлюче = Неопределено;
		Если ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("Организация") И Параметр.Свойство("Ссылка") Тогда
			ОрганизацияВКлюче 	= Параметр.Организация;
			СсылкаВКлюче 		= Параметр.Ссылка;
		ИначеЕсли Параметр = Неопределено Тогда
			Если ЗначениеЗаполнено(Организация) Тогда
				ОрганизацияВКлюче 	= Организация;
			КонецЕсли;
			СсылкаВКлюче = Источник;
		Иначе			
			Если КонтекстЭДОКлиент <> Неопределено И Источник <> Неопределено Тогда
				ОрганизацияВКлюче 	= КонтекстЭДОКлиент.СведенияПоОтправляемымОбъектам(Источник).Организация;
				СсылкаВКлюче 		= Источник;
			КонецЕсли;
		КонецЕсли;
		
		ДополнительноеУсловиеТолстыйКлиент = Истина;
#Если ТолстыйКлиентУправляемоеПриложение Тогда
		Если ОрганизацияВКлюче = Неопределено Тогда 
			Попытка
				ОрганизацияВКлюче = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
				ДополнительноеУсловиеТолстыйКлиент = (ОрганизацияВКлюче <> ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
			Исключение
			КонецПопытки;
		КонецЕсли;
#КонецЕсли

		СпозиционироватьсяНаСсылка(СсылкаВКлюче, ОрганизацияВКлюче, ИмяСобытия);
		
		Если ИмяСобытия = "Пометка прочтенным"
			ИЛИ ИмяСобытия = "Пометка непрочтенным"
			ИЛИ ИмяСобытия = "Скрытие/восстановление объекта" Тогда
			
			// Обновление количества непрочтенных.
			ОбновитьКоличествоНепрочтенныхОбъектов(ЭтотОбъект);
			
			// Закладка Новое
			Если ИмяСобытия = "Пометка непрочтенным" ИЛИ ИмяСобытия = "Скрытие/восстановление объекта" Тогда
				ЗаполнитьДеревоНовое(Истина);
			Иначе
				СтрокиДерева = Новое.ПолучитьЭлементы();
				Для Каждого СтрокаДерева Из СтрокиДерева Цикл
					Если СтрокаДерева.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНовыхСобытийДокументооборотаСКонтролирующимиОрганами.НезавершенныеОтправки") Тогда
						Прервать;				
					КонецЕсли;
					
					Если СтрокаДерева.Ссылка = Источник Тогда
						СтрокаДерева.НеПрочитано = Ложь;
						СписокСсылок = Новый Массив;
						СписокСсылок.Добавить(Источник);
						ОбновитьКоличествоНовых(ЭтотОбъект);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если ИмяСобытия = "Завершение автообмена с сервером ФСС" ИЛИ ИмяСобытия = "Завершение автообмена с сервером ФСРАР"
			ИЛИ ИмяСобытия = "Завершение автообмена с сервером РПН" ИЛИ ИмяСобытия = "Завершение автообмена с сервером ФТС"
			ИЛИ ИмяСобытия = "Завершение отправки в контролирующий орган" ИЛИ ИмяСобытия = "Завершение отправки"  Тогда
			
			ЗаполнитьДеревоНовое();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ПриЗакрытии_ЭлектронныеПредставленияРегламентированныхОтчетов" Тогда	
		Если ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("ЭтоУведомление") Тогда
			ШаблонПредупреждения = НСтр("ru = '""%1"" отображается в разделе ""%2""'");
			Если Параметр.ЭтоУведомление И Элементы.Разделы.ТекущаяСтраница = Элементы.СтраницаОтчеты Тогда
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредупреждения, Строка(Источник), НСтр("ru = 'Уведомления'"));
				ПоказатьПредупреждение(, ТекстПредупреждения);
			ИначеЕсли Не Параметр.ЭтоУведомление И Элементы.Разделы.ТекущаяСтраница = Элементы.СтраницаУведомления Тогда
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредупреждения, Строка(Источник), НСтр("ru = 'Отчеты'"));
				ПоказатьПредупреждение(, ТекстПредупреждения);	
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли (ИмяСобытия = "Получены новые сообщения 1С-Отчетности"
		ИЛИ ИмяСобытия = "Получены новые сообщения 1С-Отчетности без смены страницы") И Источник <> ЭтотОбъект Тогда
		
		Оповестить("Закрыть форму новых сообщений 1С-Отчетности",, ЭтотОбъект);
		
		Если ЭтоАдресВременногоХранилища(Параметр) Тогда
			АдресСведенийПоОшибкам = Параметр;
			УстановитьВидимостьПанелиОшибокОбновления(ЭтотОбъект);
		КонецЕсли;
		
		ЗаполнитьДеревоНовое(Истина);
		// Обновление количества непрочтенных.
		ОбновитьКоличествоНепрочтенныхОбъектов(ЭтотОбъект);
		ОтметитьДеревоПросмотренным();
		
		Если ИмяСобытия <> "Получены новые сообщения 1С-Отчетности без смены страницы" Тогда
			ПриПереключенииРаздела(Элементы.ГиперссылкаНовое);
			Если АктивноеОкно() <> Окно Тогда
				Активизировать();
			КонецЕсли;	
		КонецЕсли;
		
		ПереопределитьОформлениеСверки();
		
	ИначеЕсли ИмяСобытия = "Обновить ошибки обмена" Тогда
		
		Если ЭтоАдресВременногоХранилища(Параметр) Тогда
			АдресСведенийПоОшибкам = Параметр;
			УстановитьВидимостьПанелиОшибокОбновления(ЭтотОбъект);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "РеализованаНестандартнаяОбработкаОткрытияУведомления" Тогда
		Попытка
			Если Открыта() Тогда 
				РегламентированнаяОтчетностьКлиентПереопределяемый.ОбработчикСозданияУведомления(ЭтотОбъект, Параметр);
			КонецЕсли;
		Исключение
		КонецПопытки;
		
	ИначеЕсли ИмяСобытия = "ИзменилосьСостояниеДокументаИнтеграцииСБанком" Тогда
		#Область Используется1СОтчетность
		Если НЕ Используется1СОтчетность Тогда
			ОтключитьОбработчикОжидания("Подключаемый_ЗапуститьПроверкуСостояниеИнтеграцииВФоне");
			Результат = РегламентированнаяОтчетностьВызовСервера.СостояниеИнтеграцииСБанкомВФоне(УникальныйИдентификатор);
			ОжидатьЗавершенияФоновогоЗаданияПолученияСостоянияИнтеграцииСБанком(Результат);
		КонецЕсли;
		#КонецОбласти
		
	ИначеЕсли ИмяСобытия = "Изменены пользователи учетной записи" Тогда
		
		УстановленыОграниченияПрав = ПроверитьДоступностьНастроекОграниченияПравНаОбмен();
		УстановитьВидимостьКнопкиНастроекПрав(ЭтотОбъект);
		
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ФормаРегламентированнойОтчетности_ОбработкаОповещения(ИмяСобытия, Параметр, Источник, ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Новости") Тогда
		
		МодульОбработкаНовостейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбработкаНовостейКлиент");
		
		МодульОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
		
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВидОтчетаВИзбранные(Отбор)
	
	МассивВидовОтчетов = ПолучитьСписокОтчетовИзРегистраСведений();
	Элементы.ОтборВидОтчета.СписокВыбора.ЗагрузитьЗначения(МассивВидовОтчетов);
	
	МенеджерЗаписиОтчета = РегистрыСведений.ЖурналОтчетовСтатусы.СоздатьМенеджерЗаписи();
	МенеджерЗаписиОтчета.Организация = Отбор.Организация;
	МенеджерЗаписиОтчета.Ссылка      = Отбор.Ссылка;
	МенеджерЗаписиОтчета.Прочитать();
	
	ДанныеНайденногоВидаОтчета = Неопределено;
	
	Если МенеджерЗаписиОтчета.Выбран() Тогда
		Если ЗначениеЗаполнено(МенеджерЗаписиОтчета.НаименованиеОтчета) Тогда
			НайденныеОтчеты = РанееОткрытыеВидыОтчетов.НайтиСтроки(Новый Структура("Наименование", МенеджерЗаписиОтчета.НаименованиеОтчета));
			Для каждого НайденныйОтчет Из НайденныеОтчеты Цикл
				Если НайденныйОтчет.ЭтоГруппа <> Истина Тогда
					ДанныеНайденногоВидаОтчета = Новый Структура("Наименование,ПолноеИмя,Ссылка");
					ЗаполнитьЗначенияСвойств(ДанныеНайденногоВидаОтчета, НайденныйОтчет);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеНайденногоВидаОтчета <> Неопределено Тогда
		
		ТаблицаРанееСозданныхОтчетов = ХранилищеНастроекДанныхФорм.Загрузить(
			"Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаСозданияОтчета",
			"ФормаОтчетность_ФормаСозданияОтчета_РанееСозданныеОтчеты");
		
		Если ТипЗнч(ТаблицаРанееСозданныхОтчетов) = Тип("ТаблицаЗначений") Тогда
			НайденныеОтчеты = ТаблицаРанееСозданныхОтчетов.НайтиСтроки(
				Новый Структура("ПолноеИмя,Ссылка", ДанныеНайденногоВидаОтчета.ПолноеИмя, ДанныеНайденногоВидаОтчета.Ссылка));
			Если НайденныеОтчеты.Количество() = 0 Тогда
				НоваяСтрока = ТаблицаРанееСозданныхОтчетов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеНайденногоВидаОтчета);
				
				ХранилищеНастроекДанныхФорм.Сохранить(
					"Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаСозданияОтчета",
					"ФормаОтчетность_ФормаСозданияОтчета_РанееСозданныеОтчеты", ТаблицаРанееСозданныхОтчетов);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	// Устанавливаем признак для отключения обработки оповещения при закрытии формы.
	ВыполняетсяЗакрытиеФормы = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПриПереключенииРаздела(Элемент)
	
	ПредыдущаяГиперссылка 	= ТекущийРазделФормы(ЭтотОбъект);
	ТекущаяГиперссылка 		= Элемент;
	
	ПриПереключенииРазделаСервер(ПредыдущаяГиперссылка.Имя, ТекущаяГиперссылка.Имя);
	
	ВывестиТаблицуРаздела(ЭтотОбъект, Элемент.Имя);
	
	Если Элемент.Имя = "ГиперссылкаНастройки" Тогда
		УправлениеЭУГруппаНастройки();
	ИначеЕсли Элемент.Имя = "ГиперссылкаНовое" Тогда
		ОтметитьДеревоПросмотренным();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриПереключенииРазделаСервер(ПредыдущаяГиперссылкаИмя, ТекущаяГиперссылкаИмя)
	
	ПредыдущаяГиперссылка 	= Элементы[ПредыдущаяГиперссылкаИмя];
	ТекущаяГиперссылка 		= Элементы[ТекущаяГиперссылкаИмя];
	
	// Делаем серой предыдущую гиперссылку.
	ПредыдущаяГиперссылка.ЦветФона		= Серый;
	ПредыдущаяГиперссылка.Гиперссылка	= Истина;

	// Делаем желтой текущую гиперссылку.
	ТекущаяГиперссылка.ЦветФона		= Желтый;
	ТекущаяГиперссылка.Гиперссылка	= Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПриПереключенииРаздела(Элемент);
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭУ(Форма, ВыполнитьУстановкуСтраницы = Ложь)
	
	Форма.Элементы.ГруппаСсылокРазделы.ЦветФона = Форма.Серый;
	
	// Кнопку текущего раздела делаем желтой
	Если ВыполнитьУстановкуСтраницы Тогда
		
		// Кнопку текущего раздела делаем желтой
		ТекущийРаздел = Форма.Элементы["Гиперссылка" + СтрЗаменить(Форма.Элементы.Разделы.ТекущаяСтраница.Имя, "Страница", "")];
		ОформитьКнопку(Форма, ТекущийРаздел, ТекущийРаздел);
		
	Иначе
		ТекущийРаздел = ТекущийРазделФормы(Форма);
	КонецЕсли;
	
	// Количество в разделе "Новое"
	Если Форма.КоличествоНовых > 0 Тогда
		Форма.Элементы.ГиперссылкаНовое.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
		Форма.Элементы.ГиперссылкаНовое.Заголовок = СтрШаблон(НСтр("ru = 'Новое (%1)'"), ?(Форма.КоличествоНовых > 99, "99+", Форма.КоличествоНовых));
	КонецЕсли;
	
	// Раздел "Входящие"
	ОбновитьКоличествоНепрочтенныхОбъектов(Форма);
	
	// Выводим таблицу, соотвествующую разделу
	Если ТекущийРаздел <> Неопределено Тогда 
		ВывестиТаблицуРаздела(Форма, ТекущийРаздел.Имя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОформитьКнопку(Форма, Гиперссылка, ТекущаяГиперссылка)
	
	АктивнаКнопка = (Гиперссылка = ТекущаяГиперссылка);
	
	Гиперссылка.ЦветФона	= ?(АктивнаКнопка, Форма.Желтый, Форма.Серый);
	Гиперссылка.Гиперссылка	= НЕ АктивнаКнопка;
	Гиперссылка.ЦветТекста	= Форма.Черный;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ДействияПриИзмененииОтборов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияНастройкиПриИзменении(Элемент)
	
	ДействияПриИзмененииОтборов(ЭтотОбъект);
	УправлениеЭУГруппаНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидОтчетаПриИзменении(Элемент)
	
	УстановитьОтборыПоОтчетам(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидОтчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	МассивВидовОтчетов = ПолучитьСписокОтчетовИзРегистраСведений();
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтборВидОтчетаНачалоВыбора(МассивВидовОтчетов);
	
	Элемент.СписокВыбора.ЗагрузитьЗначения(МассивВидовОтчетов);
	Элемент.СписокВыбора.СортироватьПоЗначению();
	
	ПараметрыФормыВыбораВидаОтчета = Новый Структура;
	ПараметрыФормыВыбораВидаОтчета.Вставить("Заголовок", "Выберите виды отчетов");
	ПараметрыФормыВыбораВидаОтчета.Вставить("ИсходныйСписок", ВидОтчета);
	ПараметрыФормыВыбораВидаОтчета.Вставить("ТипИсточника", "Список");
	ПараметрыФормыВыбораВидаОтчета.Вставить("СписокЗначенийДляВыбора", Элемент.СписокВыбора.Скопировать());
	
	ПолноеИмяФормыВыбораВидаОтчета = "";
	
	РегламентированнаяОтчетностьКлиентПереопределяемый.ФормаРегламентированнойОтчетности_ОпределениеФормыВыбораВидаОтчета(
		ПолноеИмяФормыВыбораВидаОтчета, ПараметрыФормыВыбораВидаОтчета);
	
	Если ЗначениеЗаполнено(ПолноеИмяФормыВыбораВидаОтчета) Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму(ПолноеИмяФормыВыбораВидаОтчета, ПараметрыФормыВыбораВидаОтчета, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидОтчетаОчистка(Элемент, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодОтчетаПриИзменении(Элемент)
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОтборПериодНачалоВыбора_Завершение", 
		ЭтотОбъект);
	
	РегламентированнаяОтчетностьКлиент.ОтборПериодНачалоВыбора(ЭтотОбъект, ОписаниеОповещения, СтандартнаяОбработка, ТекущаяДатаНаСервере());
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодНачалоВыбора_Завершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатВыбора);
	
	УстановитьОтборыПоОтчетам(ЭтотОбъект);
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодОтчетаОчистка(Элемент, СтандартнаяОбработка)
	
	НачалоПериода = Дата(1, 1, 1);
	КонецПериода  = Дата(1, 1, 1);
	
	УстановитьОтборыПоОтчетам(ЭтотОбъект);
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура СортироватьОтчетыПоПериоду(Команда)
	
	УстановитьСортировкуПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОтборыИСортировку(Команда)
	
	СохранитьНастройкиОтбораИСортировки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявлениеНаПодключениеК1СОтчетностиНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуМастераЗаявленияНаПодключение(Организация, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиОбменаНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуНастроекОбмена(Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаявленийОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуСпискаЗаявленийНаПодключение(Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиДокументооборотаСГосОрганамиНажатиеПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	НастройкиДокументооборотаСГосОрганамиНажатие(ПараметрыОповещения.Элемент);
	
КонецПроцедуры
	
&НаКлиенте
Процедура НастройкиДокументооборотаСГосОрганамиНажатие(Элемент)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Элемент", Элемент);
		Оповещение = Новый ОписаниеОповещения("НастройкиДокументооборотаСГосОрганамиНажатиеПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОКлиент.ОткрытьФормуОбщихНастроекЭДО();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияСверкиПриИзменении(Элемент)
	 ДействияПриИзмененииОтборов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидСверкиПриИзменении(Элемент)
	ДействияПриИзмененииОтборов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияЕГРЮЛПриИзменении(Элемент)
	ДействияПриИзмененииОтборов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПисьмаПриИзменении(Элемент)
	ДействияПриИзмененииОтборов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияУведомленияПриИзменении(Элемент)
	ДействияПриИзмененииОтборов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияВходящиеПриИзменении(Элемент)
	
	ДействияПриИзмененииОтборовИОбновлениеВходящих(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидУведомленияПриИзменении(Элемент)
	Элемент.СписокВыбора.Очистить();
	Элемент.РежимВыбораИзСписка = Ложь;
	Элемент.ОбновлениеТекстаРедактирования = ОбновлениеТекстаРедактирования.Авто;
	ВидУведомления = ПредставлениеВидаУведомления;
	Если Не ЗначениеЗаполнено(ВидУведомления) И ТаблицаУведомленийСХешем.Количество() > 0 Тогда 
		ОтбСтроки = ТаблицаУведомленийСХешем.НайтиСтроки(Новый Структура("Синоним", ПредставлениеВидаУведомления));
		Если ОтбСтроки.Количество() > 0 Тогда 
			ВидУведомления = ОтбСтроки[0].ВидУведомления;
		КонецЕсли;
	КонецЕсли;
	ДействияПриИзмененииОтборов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидУведомленияИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	Если ТаблицаУведомленийСХешем.Количество() = 0 Или Не ЗначениеЗаполнено(Текст) Тогда 
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	Элемент.ОбновлениеТекстаРедактирования = ОбновлениеТекстаРедактирования.НеИспользовать;
	Элемент.СписокВыбора.Очистить();
	ТермыПоиска = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, " ", Истина, Истина);
	Для Каждого Стр Из ТаблицаУведомленийСХешем Цикл 
		ВключатьВСписок = Истина;
		Для Каждого Терм Из ТермыПоиска Цикл 
			Если СтрНайти(Стр.Хеш, ПолучитьХеш(Терм)) = 0 Тогда 
				ВключатьВСписок = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ВключатьВСписок Тогда 
			Элемент.СписокВыбора.Добавить(Стр.Синоним);
		КонецЕсли;
	КонецЦикла;
	Элемент.РежимВыбораИзСписка = (Элемент.СписокВыбора.Количество() > 0);
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидУведомленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВыбораВидаУведомления", ЭтотОбъект);
	
	ПараметрыФормы 	= Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
  	ПараметрыФормы.Вставить("УчитыватьУведомленияНеВходящиеВБРО", Истина);
	
	Если ЗначениеЗаполнено(ПредставлениеВидаУведомления) И ЗначениеЗаполнено(ВидУведомления) Тогда
		ПараметрыФормы.Вставить("ВидУведомления", ВидУведомления);
	КонецЕсли;
	
	ОткрытьФорму("Документ.УведомлениеОСпецрежимахНалогообложения.Форма.ФормаВыбораВидаСообщения", ПараметрыФормы,,,,, 
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ПодсказкиПроПроверкуПодписиОбработкаНавигационнойСсылкиПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ПодсказкиПроПроверкуПодписиОбработкаНавигационнойСсылки(
		ПараметрыОповещения.Элемент,
		ПараметрыОповещения.НавигационнаяСсылкаФорматированнойСтроки,
		ПараметрыОповещения.СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказкиПроПроверкуПодписиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Элемент", Элемент);
		ПараметрыОповещения.Вставить("НавигационнаяСсылкаФорматированнойСтроки", НавигационнаяСсылкаФорматированнойСтроки);
		ПараметрыОповещения.Вставить("СтандартнаяОбработка", СтандартнаяОбработка);
		Оповещение = Новый ОписаниеОповещения(
			"ПодсказкиПроПроверкуПодписиОбработкаНавигационнойСсылкиПредварительное", 
			ЭтотОбъект, 
			ПараметрыОповещения
		);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.ЧтоОзначаетПодписьНедействительна");
	
КонецПроцедуры

&НаКлиенте
Процедура СообщениеПроОшибкиОбменаОбработкаНавигационнойСсылкиПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	СообщениеПроОшибкиОбменаОбработкаНавигационнойСсылки(
		ПараметрыОповещения.Элемент,
		ПараметрыОповещения.НавигационнаяСсылкаФорматированнойСтроки,
		ПараметрыОповещения.СтандартнаяОбработка);
	
КонецПроцедуры
	
&НаКлиенте
Процедура СообщениеПроОшибкиОбменаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Элемент", Элемент);
		ПараметрыОповещения.Вставить("НавигационнаяСсылкаФорматированнойСтроки", НавигационнаяСсылкаФорматированнойСтроки);
		ПараметрыОповещения.Вставить("СтандартнаяОбработка", СтандартнаяОбработка);
		Оповещение = Новый ОписаниеОповещения(
			"СообщениеПроОшибкиОбменаОбработкаНавигационнойСсылкиПредварительное", 
			ЭтотОбъект, 
			ПараметрыОповещения
		);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент <> Неопределено Тогда 		
		ВходящийКонтекст = ПолучитьИзВременногоХранилища(АдресСведенийПоОшибкам);	
		ДлительнаяОтправкаКлиент.ПоказатьОшибки(КонтекстЭДОКлиент.ПутьКОбъекту, ВходящийКонтекст);		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#Область ОбработчикиСобытийЭлементовТаблицыНовое

&НаКлиенте
Процедура ТаблицаНовоеВыборПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ТаблицаНовоеВыбор(
		ПараметрыОповещения.Элемент,
		ПараметрыОповещения.ВыбраннаяСтрока,
		ПараметрыОповещения.Поле,
		ПараметрыОповещения.СтандартнаяОбработка);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ТаблицаНовоеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Ссылка = Элемент.ТекущиеДанные.Ссылка;
	Если Поле.Имя = "ТаблицаНовоеОбъектНаименованиеСсылка" Тогда
		Если Элемент.ТекущиеДанные.ЕстьОтвет Тогда // есть ответ
			
			Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
				ПараметрыОповещения = Новый Структура;
				ПараметрыОповещения.Вставить("Элемент", Элемент);
				ПараметрыОповещения.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
				ПараметрыОповещения.Вставить("Поле", Поле);
				ПараметрыОповещения.Вставить("СтандартнаяОбработка", СтандартнаяОбработка);
				Оповещение = Новый ОписаниеОповещения("ТаблицаНовоеВыборПредварительное", ЭтотОбъект, ПараметрыОповещения);
				ИнициализироватьКонтекстЭДО(Оповещение);
				Возврат;
			КонецЕсли;
	
			Если КонтекстЭДОКлиент <> Неопределено И НЕ ПереопределитьОткрытие(Элемент) Тогда
				Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика") Тогда
					КонтекстЭДОКлиент.ПоказатьОтветНаЗапросИОН(Ссылка);
				ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеСтрахователя") Тогда
					КонтекстЭДОКлиент.ПоказатьОтветНаЗапросИОС(Ссылка);
				ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП") Тогда
					КонтекстЭДОКлиент.ПоказатьОтветНаЗапросВыпискиЕГРЮЛ_ЕГРИП(Ссылка);
				КонецЕсли;
			КонецЕсли;
			ПометитьПрочтенными();
		ИначеЕсли НЕ ПереопределитьОткрытие(Элемент) Тогда
			РедактироватьОбъект(Элемент);
		КонецЕсли;
	ИначеЕсли Поле.Имя = "ТаблицаНовоеСтатус" Тогда
		ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
	ИначеЕсли Поле.Имя = "ТаблицаНовоеЕстьКритическиеОшибкиОтправки" И Элемент.ТекущиеДанные.ЕстьКритическиеОшибкиОтправки Тогда
		ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
	ИначеЕсли НЕ ПереопределитьОткрытие(Элемент) Тогда
		РедактироватьОбъект(Элемент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыОтчеты

&НаКлиенте
Процедура ОтчетыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		
		РегламентированнаяОтчетностьКлиент.ОтчетыВыбор(ЭтотОбъект, Элемент, Поле, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
		
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	ДокументооборотСКОКлиент.СоздатьЭлектронноеПредставлениеРегламентированныхОтчетовИзФайлов(ПараметрыПеретаскивания.Значение, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		
		ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено Тогда
			
			Ссылка = ТекущиеДанные.РегламентированныйОтчет;
			ПараметрыФормы = Новый Структура("ЗначениеКопирования", Ссылка); 
			
			Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
				
				ОткрытьФорму("Справочник.ЭлектронныеПредставленияРегламентированныхОтчетов.ФормаОбъекта", ПараметрыФормы);
				
			ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
				
				ОткрытьФорму("Документ.РегламентированныйОтчет.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
				
			Иначе
				
				ИмяФормыОбъекта = ИмяФормыОбъектаПоУмолчанию(Ссылка);
				ОткрытьФорму(ИмяФормыОбъекта, ПараметрыФормы);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		СоздатьОтчет();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.РегламентированныйОтчет;
		ПоказатьЗначение(,Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	ПометитьНаУдалениеОбъект(Элемент);

КонецПроцедуры

&НаСервере
Процедура ОтчетыПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	ПользовательскиеНастройкиДинамическогоСписка = ЭтотОбъект.Отчеты.КомпоновщикНастроек.ПользовательскиеНастройки;
	
	ЭтоТипСтрока = Элементы.ОтборВидОтчета.РежимВыбораИзСписка;
	
	ЗначениеВидОтчета     = Новый СписокЗначений;
	ЗначениеВидОтчетаСтр  = "";
	ЗначениеОрганизация   = Справочники.Организации.ПустаяСсылка();
	ЗначениеНачалоПериода = Дата(1, 1, 1);
	ЗначениеКонецПериода  = Дата(1, 1, 1);
	
	МассивЗначенийКонецПериода = Новый Массив;
	
	ПолеКомпоновкиНаименованиеОтчета = Новый ПолеКомпоновкиДанных("НаименованиеОтчета");
	ПолеКомпоновкиОрганизация        = Новый ПолеКомпоновкиДанных("Организация");
	ПолеКомпоновкиДатаНачала         = Новый ПолеКомпоновкиДанных("ДатаНачала");
	ПолеКомпоновкиДатаОкончания      = Новый ПолеКомпоновкиДанных("ДатаОкончания");
	
	Для Каждого ЭлементПользовательскойНастройки Из ПользовательскиеНастройкиДинамическогоСписка.Элементы Цикл
		Если ТипЗнч(ЭлементПользовательскойНастройки) = Тип("ОтборКомпоновкиДанных")
			И ЭлементПользовательскойНастройки.ИдентификаторПользовательскойНастройки =
				ЭтотОбъект.Отчеты.КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки Тогда
			Для Каждого ЭлементОтбора Из ЭлементПользовательскойНастройки.Элементы Цикл
				Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
					Если ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиНаименованиеОтчета Тогда
						Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
							ЗначениеВидОтчета = ЭлементОтбора.ПравоеЗначение;
						ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
							ЗначениеВидОтчетаСтр = ЭлементОтбора.ПравоеЗначение;
						ИначеЕсли СокрЛП(ЭлементОтбора.ПравоеЗначение) = "Макет пенсионного дела"
							ИЛИ СокрЛП(ЭлементОтбора.ПравоеЗначение) = "Заявление о назначении пенсии" Тогда
							ЗначениеВидОтчетаСтр = ЭлементОтбора.ПравоеЗначение;
						КонецЕсли;
					ИначеЕсли ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиОрганизация Тогда
						ЗначениеОрганизация = ЭлементОтбора.ПравоеЗначение;
					ИначеЕсли ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиДатаНачала Тогда
						ЗначениеНачалоПериода = ЭлементОтбора.ПравоеЗначение;
					ИначеЕсли ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиДатаОкончания Тогда
						МассивЗначенийКонецПериода.Добавить(ЭлементОтбора.ПравоеЗначение);
					КонецЕсли;
				ИначеЕсли ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
					Для Каждого ЭлементОтбораВГруппе Из ЭлементОтбора.Элементы Цикл
						Если ТипЗнч(ЭлементОтбораВГруппе.ПравоеЗначение) = Тип("СписокЗначений") Тогда
							ЗначениеВидОтчета = ЭлементОтбораВГруппе.ПравоеЗначение;
						Иначе
							ЗначениеВидОтчетаСтр = ЭлементОтбораВГруппе.ПравоеЗначение;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ЗначениеВидОтчетаСтр) Тогда
		Если ЗначениеВидОтчета.НайтиПоЗначению(ЗначениеВидОтчетаСтр) = Неопределено Тогда
			ЗначениеВидОтчета.Добавить(ЗначениеВидОтчетаСтр);
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоТипСтрока Тогда
		ВидОтчета = ?(ЗначениеЗаполнено(ЗначениеВидОтчета), ЗначениеВидОтчета[0].Значение, "");
	Иначе
		ВидОтчета = ЗначениеВидОтчета;
	КонецЕсли;
	
	Организация = ЗначениеОрганизация;
	
	Если МассивЗначенийКонецПериода.Количество() > 0 Тогда
		ЗначениеКонецПериода = МассивЗначенийКонецПериода[МассивЗначенийКонецПериода.ВГраница()];
		Если МассивЗначенийКонецПериода.Количество() > 1 И НЕ ТолькоУказанныйПериод Тогда
			ЗначениеНачалоПериода = МассивЗначенийКонецПериода[0];
		КонецЕсли;
	КонецЕсли;
	
	ОбновлНачалоПериода = ?(ЗначениеЗаполнено(ЗначениеНачалоПериода), НачалоДня(ЗначениеНачалоПериода), ЗначениеНачалоПериода);
	ОбновлКонецПериода  = ?(ЗначениеЗаполнено(ЗначениеКонецПериода),  КонецДня(ЗначениеКонецПериода),   ЗначениеКонецПериода);
	
	Если ОбновлНачалоПериода <> НачалоПериода ИЛИ ОбновлКонецПериода <> КонецПериода Тогда
		НачалоПериода = ОбновлНачалоПериода;
		КонецПериода  = ОбновлКонецПериода;
		ПериодОтчета  = ПредставлениеПериода(НачалоПериода, КонецПериода, "ФП=Истина");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодменитьСсылку(Ссылка) Экспорт
	Попытка
		Если ОбщегоНазначения.ПодсистемаСуществует("РегламентированнаяОтчетность.СообщенияВКонтролирующиеОрганы.КонвертацияОтчетовПриПереходеС82") Тогда 
			ОМ_УведомлениеОСпецрежимахНалогообложенияПовтИсп = ОбщегоНазначения.ОбщийМодуль("УведомлениеОСпецрежимахНалогообложенияПовтИсп");
			Ссылка = ОМ_УведомлениеОСпецрежимахНалогообложенияПовтИсп.ПодменитьСсылкуНаОтчет(Ссылка);
		КонецЕсли;
	Исключение
	КонецПопытки;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыУведомления

&НаКлиенте
Процедура УведомленияПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	ПометитьНаУдалениеОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура УведомленияПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	РедактироватьОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура УведомленияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда

		Ссылка = ТекущиеДанные.Ссылка;
		
		Если Поле.Имя = "УведомленияСтатусОтправки" Тогда
			Если РегламентированнаяОтчетностьКлиентСервер.ОбъектНеОтправлялсяЧерез1СОтчетность(
				ТекущиеДанные.СостояниеСдачиОтчетности) Тогда
				ВыбратьСтатусОтправкиИзСписка(Элемент);
			Иначе
				ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
			КонецЕсли;
		ИначеЕсли Поле.Имя = "УведомленияЕстьКритическиеОшибкиОтправки" И ТекущиеДанные.ЕстьКритическиеОшибкиОтправки Тогда
			ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
		Иначе
			РедактироватьОбъект(Элемент);
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УведомленияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	Если Копирование Тогда
		
		ТекущиеДанные = Элементы.Уведомления.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			
			Ссылка 			= ТекущиеДанные.Ссылка;
			ТипыСсылки 		= ТипЗнч(Ссылка);
			
			ПараметрыФормы 	= Новый Структура;
			ПараметрыФормы.Вставить("ЗначениеКопирования", Ссылка);
			
			Если ТипыСсылки = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") Тогда
				// Это уведомления из БРО
				ПараметрыФормы.Вставить("Организация", 		ТекущиеДанные.Организация);
				ПараметрыФормы.Вставить("ВидУведомления", 	ВидУведомленияОСпецрежимахНалогообложения(ТекущиеДанные.Ссылка));
				ОткрытьФорму("Документ.УведомлениеОСпецрежимахНалогообложения.ФормаОбъекта", ПараметрыФормы);
			ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
				// Это электронные представления
				ОткрытьФорму("Справочник.ЭлектронныеПредставленияРегламентированныхОтчетов.ФормаОбъекта", ПараметрыФормы);
			Иначе
				// Это объекты из ЗУПа
				ИмяТипа = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолноеИмяТипа(ТипыСсылки);
				ОткрытьФорму(ИмяТипа + ".ФормаОбъекта", ПараметрыФормы);
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		СоздатьНовоеУведомление();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УведомленияПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УведомленияПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	ДокументооборотСКОКлиент.СоздатьЭлектронноеПредставлениеРегламентированныхОтчетовИзФайлов(ПараметрыПеретаскивания.Значение, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыПисьма

&НаКлиенте
Процедура ПисьмаПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	ПометитьНаУдалениеОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПисьмаПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	РедактироватьОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПисьмаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.Ссылка;
		
		Если Поле.Имя = "ПисьмаСтатусОтправки" Тогда
			ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
		ИначеЕсли Поле.Имя = "ПисьмаЕстьКритическиеОшибкиОтправки" И ТекущиеДанные.ЕстьКритическиеОшибкиОтправки Тогда
			ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
		Иначе
			РедактироватьОбъект(Элемент);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПисьмаПередНачаломДобавленияПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ПисьмаПередНачаломДобавления(
		ПараметрыОповещения.Элемент,
		ПараметрыОповещения.Отказ,
		ПараметрыОповещения.Копирование,
		ПараметрыОповещения.Родитель,
		ПараметрыОповещения.Группа,
		ПараметрыОповещения.Параметр
	);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПисьмаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	Если Копирование Тогда
		
		ТекущиеДанные = Элементы.Письма.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			
			Ссылка = ТекущиеДанные.Ссылка;
			ПараметрыФормы = Новый Структура("ЗначениеКопирования", Ссылка);
			Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ПерепискаСКонтролирующимиОрганами") Тогда
				ОткрытьФорму("Справочник.ПерепискаСКонтролирующимиОрганами.ФормаОбъекта", ПараметрыФормы);
			ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.ОписиИсходящихДокументовВНалоговыеОрганы") Тогда
				ОткрытьФорму("Справочник.ОписиИсходящихДокументовВНалоговыеОрганы.ФормаОбъекта", ПараметрыФормы);
			ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ПоясненияКДекларацииПоНДС") Тогда
				
				Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
					ПараметрыОповещения = Новый Структура;
					ПараметрыОповещения.Вставить("Элемент", Элемент);
					ПараметрыОповещения.Вставить("Отказ", Отказ);
					ПараметрыОповещения.Вставить("Копирование", Копирование);
					ПараметрыОповещения.Вставить("Родитель", Родитель);
					ПараметрыОповещения.Вставить("Группа", Группа);
					ПараметрыОповещения.Вставить("Параметр", Параметр);
					Оповещение = Новый ОписаниеОповещения("ПисьмаПередНачаломДобавленияПредварительное", ЭтотОбъект, ПараметрыОповещения);
					ИнициализироватьКонтекстЭДО(Оповещение);
					Возврат;
				КонецЕсли;
				
				Если КонтекстЭДОКлиент <> Неопределено Тогда 
					КонтекстЭДОКлиент.СоздатьПоясненияКДекларацииПоНДС(Ссылка);
				КонецЕсли;				
			КонецЕсли;
				
		КонецЕсли;
		
	Иначе
		
		СоздатьНовоеПисьмо();

	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыСверки

&НаКлиенте
Процедура СверкиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	ПометитьНаУдалениеОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СверкиПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	РедактироватьОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СверкиВыборПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	СверкиВыбор(
		ПараметрыОповещения.Элемент,
		ПараметрыОповещения.ВыбраннаяСтрока,
		ПараметрыОповещения.Поле,
		ПараметрыОповещения.СтандартнаяОбработка
	);
	
КонецПроцедуры

&НаКлиенте
Процедура СверкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Ссылка = Элемент.ТекущиеДанные.Ссылка;

		Если Поле.Имя = "СверкиЕстьОтвет" Тогда
			Если Элемент.ТекущиеДанные.ЕстьОтвет Тогда // есть ответ
				
				Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
					ПараметрыОповещения = Новый Структура;
					ПараметрыОповещения.Вставить("Элемент", Элемент);
					ПараметрыОповещения.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
					ПараметрыОповещения.Вставить("Поле", Поле);
					ПараметрыОповещения.Вставить("СтандартнаяОбработка", СтандартнаяОбработка);
					Оповещение = Новый ОписаниеОповещения("СверкиВыборПредварительное", ЭтотОбъект, ПараметрыОповещения);
					ИнициализироватьКонтекстЭДО(Оповещение);
					Возврат;
				КонецЕсли;
				
				Если КонтекстЭДОКлиент <> Неопределено Тогда
					
					Ссылка = Элемент.ТекущиеДанные.Ссылка;
					Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика") Тогда
						КонтекстЭДОКлиент.ПоказатьОтветНаЗапросИОН(Ссылка);
					ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеСтрахователя") Тогда
						КонтекстЭДОКлиент.ПоказатьОтветНаЗапросИОС(Ссылка);
					КонецЕсли;
					
				КонецЕсли;
			Иначе
				РедактироватьОбъект(Элемент);
			КонецЕсли;
		ИначеЕсли Поле.Имя = "СверкиСтатусОтправки" Тогда
			ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
		ИначеЕсли Поле.Имя = "СверкиЕстьКритическиеОшибкиОтправки" И ТекущиеДанные.ЕстьКритическиеОшибкиОтправки Тогда
			ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
		Иначе
			РедактироватьОбъект(Элемент);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СверкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		
		ТекущиеДанные = Элементы.Сверки.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			Ссылка = ТекущиеДанные.Ссылка;
			ПараметрыФормы = Новый Структура("ЗначениеКопирования", Ссылка); 
			Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика") Тогда
				ОткрытьФорму("Документ.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика.Форма.ФормаДокумента", ПараметрыФормы);
			ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеСтрахователя") Тогда
				ОткрытьФорму("Документ.ЗапросНаИнформационноеОбслуживаниеСтрахователя.Форма.ФормаДокумента", ПараметрыФормы);
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		СоздатьНовуюСверку();

	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыЕГРЮЛ

&НаКлиенте
Процедура ЕГРЮЛПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	Если Копирование Тогда
		
		ТекущиеДанные = Элементы.ЕГРЮЛ.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			Ссылка = ТекущиеДанные.Ссылка;
			ПараметрыФормы = Новый Структура("ЗначениеКопирования", Ссылка);
			ОткрытьФорму("Документ.ЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП.Форма.ФормаДокумента", ПараметрыФормы);
		КонецЕсли;
		
	Иначе
		
		СоздатьНовыйЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП();

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЕГРЮЛПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	РедактироватьОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ЕГРЮЛПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	ПометитьНаУдалениеОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ЕГРЮЛВыборПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ЕГРЮЛВыбор(
		ПараметрыОповещения.Элемент,
		ПараметрыОповещения.ВыбраннаяСтрока,
		ПараметрыОповещения.Поле,
		ПараметрыОповещения.СтандартнаяОбработка
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕГРЮЛВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = Элемент.ТекущиеДанные.Ссылка;
		
		СтандартнаяОбработка = Ложь;
		Если Поле.Имя = "ЕГРЮЛЕстьОтвет" Тогда
			Если Элемент.ТекущиеДанные.ЕстьОтвет Тогда // есть ответ
				
				Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
					ПараметрыОповещения = Новый Структура;
					ПараметрыОповещения.Вставить("Элемент", Элемент);
					ПараметрыОповещения.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
					ПараметрыОповещения.Вставить("Поле", Поле);
					ПараметрыОповещения.Вставить("СтандартнаяОбработка", СтандартнаяОбработка);
					Оповещение = Новый ОписаниеОповещения("ЕГРЮЛВыборПредварительное", ЭтотОбъект, ПараметрыОповещения);
					ИнициализироватьКонтекстЭДО(Оповещение);
					Возврат;
				КонецЕсли;
				
				Если КонтекстЭДОКлиент <> Неопределено Тогда
					КонтекстЭДОКлиент.ПоказатьОтветНаЗапросВыпискиЕГРЮЛ_ЕГРИП(Ссылка);
				КонецЕсли;
			Иначе
				РедактироватьОбъект(Элемент);
			КонецЕсли;
		ИначеЕсли Поле.Имя = "ЕГРЮЛСтатусОтправки" Тогда
			ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
		ИначеЕсли Поле.Имя = "ЕГРЮЛЕстьКритическиеОшибкиОтправки" И ТекущиеДанные.ЕстьКритическиеОшибкиОтправки Тогда
			ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
		Иначе
			РедактироватьОбъект(Элемент);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыВходящие

&НаКлиенте
Процедура ВходящиеПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВходящиеПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	РедактироватьОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ВходящиеВыборПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ВходящиеВыбор(
		ПараметрыОповещения.Элемент,
		ПараметрыОповещения.ВыбраннаяСтрока,
		ПараметрыОповещения.Поле,
		ПараметрыОповещения.СтандартнаяОбработка
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ВходящиеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Ссылка = Элемент.ТекущиеДанные.Ссылка;

		Если СтрНачинаетсяС(Поле.Имя, "ВходящиеСтатусОтправки") Тогда
			ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
		ИначеЕсли СтрНачинаетсяС(Поле.Имя, "ВходящиеЕстьОтвет") 
			И ТипЗнч(Ссылка) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов") 
			И Элемент.ТекущиеДанные.ЕстьОтвет Тогда
			
				Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
					ПараметрыОповещения = Новый Структура;
					ПараметрыОповещения.Вставить("Элемент", Элемент);
					ПараметрыОповещения.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
					ПараметрыОповещения.Вставить("Поле", Поле);
					ПараметрыОповещения.Вставить("СтандартнаяОбработка", СтандартнаяОбработка);
					Оповещение = Новый ОписаниеОповещения("ВходящиеВыборПредварительное", ЭтотОбъект, ПараметрыОповещения);
					ИнициализироватьКонтекстЭДО(Оповещение);
					Возврат;
				КонецЕсли;
				
				Если КонтекстЭДОКлиент <> Неопределено Тогда
					КонтекстЭДОКлиент.НажатиеНаКнопкуПоказатьОтветыПоТребованию(Ссылка);
				КонецЕсли;
		ИначеЕсли СтрНачинаетсяС(Поле.Имя, "ВходящиеЕстьКритическиеОшибкиОтправки") И ТекущиеДанные.ЕстьКритическиеОшибкиОтправки Тогда
			ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
		Иначе
			РедактироватьОбъект(Элемент);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВходящиеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьДокумент(Команда)
	
	ТекущаяТаблица = ТекущаяТаблица(ЭтотОбъект);
	ТекущиеДанные = ТекущаяТаблица.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		
		ПоказатьПредупреждение(,НСтр("ru = 'Не выбрано ни одного документа'"));
		
	Иначе
		
		Если Элементы.Отчеты = ТекущаяТаблица Тогда
			Ссылка = ТекущиеДанные.РегламентированныйОтчет;
		Иначе
			Ссылка = ТекущиеДанные.Ссылка;
		КонецЕсли;
		
		Закрыть(Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

#Область КомандыЗакладкиОтчетность

&НаКлиенте
Процедура Создать(Команда)
	
	СоздатьОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОтчет(знач ПараметрыФормы = Неопределено)
	
	Если ПараметрыФормы = Неопределено Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Организация);
		ПараметрыФормы.Вставить("ДатаНачалаПериодаОтчета", НачалоПериода);
		ПараметрыФормы.Вставить("ДатаКонцаПериодаОтчета", КонецПериода);
		Если ТипЗнч(ВидОтчета) = Тип("СписокЗначений") Тогда
			ПараметрыФормы.Вставить("ПредставлениеВидаОтчета", ?(ВидОтчета.Количество() = 1, ВидОтчета[0].Значение, ""));
		Иначе
			ПараметрыФормы.Вставить("ПредставлениеВидаОтчета", Строка(ВидОтчета));
		КонецЕсли;
		ПараметрыФормы.Вставить("ИмяФормы","");
		
	КонецЕсли;
	
	Если ПараметрыФормы.ИмяФормы <> "" Тогда
		
		// Сначала попробуем найти его среди открытых стартовых форм.
		// Необходимо для предотвращения
		// открытия нескольких стартовых форм одного отчета.
		НайденоОкно = Ложь;
		РегламентированнаяОтчетностьКлиент.ВебКлиентНайтиАктивизироватьОкно(ПараметрыФормы.ИмяФормы, ЭтотОбъект, НайденоОкно);
		
		Если НайденоОкно <> Неопределено Тогда
			Если НайденоОкно Тогда
			
				Возврат;
			
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли; 
	
	ЭтотОбъект.ПараметрыВыбораФормыСоздания = Неопределено;
	ОписаниеОповещенияОЗакрытииФормыСозданияОтчета = Новый ОписаниеОповещения("ВыполнитьПослеЗакрытияСозданияОтчета", ЭтотОбъект);
	ОткрытьФорму("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаСозданияОтчета", ПараметрыФормы, ЭтотОбъект,,,, 
				ОписаниеОповещенияОЗакрытииФормыСозданияОтчета, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеЗакрытияСозданияОтчета(Параметр  = Неопределено, ДопПараметр = Неопределено) Экспорт

	Если ЭтотОбъект.ПараметрыВыбораФормыСоздания = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	СоздатьОтчет(ЭтотОбъект.ПараметрыВыбораФормыСоздания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереустановитьКомпонентыПечатиМашиночитаемыхФорм(Команда)
	
	КаталогКомпонентыМЧБ = "";
	
	#Если ВебКлиент Тогда
		
		Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ПереустановитьКомпонентыПечатиМашиночитаемыхФормЗавершение", ЭтотОбъект);
			НачатьУстановкуРасширенияРаботыСФайлами(ОписаниеОповещения);
		Иначе
			ДиалогВыбораКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
			
			ДиалогВыбораКаталога.Заголовок = НСтр("ru = 'Выберите каталог для сохранения дистрибутива компоненты печати машиночитаемых форм.'");
			
			Если ДиалогВыбораКаталога.Выбрать() Тогда
				КаталогКомпонентыМЧБ = ДиалогВыбораКаталога.Каталог;
			КонецЕсли;
			
			Если КаталогКомпонентыМЧБ = Неопределено
				ИЛИ ПустаяСтрока(КаталогКомпонентыМЧБ)
				ИЛИ КаталогКомпонентыМЧБ = Ложь Тогда	
				Возврат;
			КонецЕсли;
			
			КаталогКомпонентыМЧБ = КаталогКомпонентыМЧБ + "\";
			УстановитьКомпонентуПечати(КаталогКомпонентыМЧБ);
			
		КонецЕсли;
		
	#Иначе
		
		УстановитьКомпонентуПечати(КаталогКомпонентыМЧБ);
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПереустановитьКомпонентыПечатиМашиночитаемыхФормЗавершение(ДополнительныеПараметры) Экспорт
	
	Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		ПоказатьПредупреждение(,НСтр("ru='Не удалось подключить расширение работы с файлами!
		|Установка компоненты печати машиночитаемых форм невозможна.'"));
	Иначе
		ДиалогВыбораКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		
		ДиалогВыбораКаталога.Заголовок = НСтр("ru = 'Выберите каталог для сохранения дистрибутива компоненты печати машиночитаемых форм.'");
		
		Если ДиалогВыбораКаталога.Выбрать() Тогда
			КаталогКомпонентыМЧБ = ДиалогВыбораКаталога.Каталог;
		КонецЕсли;
		
		Если КаталогКомпонентыМЧБ = Неопределено
			ИЛИ ПустаяСтрока(КаталогКомпонентыМЧБ)
			ИЛИ КаталогКомпонентыМЧБ = Ложь Тогда	
			Возврат;
		КонецЕсли;
		
		КаталогКомпонентыМЧБ = КаталогКомпонентыМЧБ + "\";
		УстановитьКомпонентуПечати(КаталогКомпонентыМЧБ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКомпонентуПечати(КаталогКомпонентыМЧБ)
	
	ДистрибутивКомпонентыПечатиМашиночитаемыхФорм = Новый СписокЗначений;
	РегламентированнаяОтчетностьВызовСервера.ПолучитьДистрибутивКомпонентыПечатиМашиночитаемыхФорм(ДистрибутивКомпонентыПечатиМашиночитаемыхФорм);
	РегламентированнаяОтчетностьКлиент.УстановитьКомпонентуПечатиМашиночитаемыхФорм(Истина, ДистрибутивКомпонентыПечатиМашиночитаемыхФорм, КаталогКомпонентыМЧБ);
	УправлениеЭУПанельУстановкаКомпонентыПечати();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ОбновитьДанные(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ОбновитьДанныеПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
				
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ВыбранныеОрганизации = ВсеОрганизации();
	
	Если ВыбранныеОрганизации.Количество() = 0 Тогда
		ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(НСтр("ru = 'Организация не выбрана'"), ,"Организация");
		Возврат;
	КонецЕсли;
	
	СостояниеВозможностиОбмена = КонтекстЭДОКлиент.СостояниеВозможностиОбмена(ВыбранныеОрганизации);
	
	Если СостояниеВозможностиОбмена.Состояние = 1 Тогда
		ОбновитьДанныеПродолжить(ВыбранныеОрганизации);
	ИначеЕсли СостояниеВозможностиОбмена.Состояние = 2 Тогда
		Возврат;
	Иначе
		ПараметрыЦикла = Новый Структура;
		ПараметрыЦикла.Вставить("ВыбранныеОрганизации", ВыбранныеОрганизации);
		СледующееОповещение = Новый ОписаниеОповещения("НастроитьОграниченияОбмена", ЭтотОбъект, ПараметрыЦикла);
		ОткрытьФормуНастройкиОбновления(Истина, СледующееОповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиОбновления(Команда)
	
	ОписаниеСледующее = Новый ОписаниеОповещения("НастройкиОбновленияПосле", ЭтотОбъект);
	ПодключитьКлиентскийКонтекст(ОписаниеСледующее);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	РегламентированнаяОтчетностьКлиентПереопределяемый.ФормаРегламентированнойОтчетности_ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьНадписьВремяОбновления()
	
	НадписьВремяОбновления = ОбновитьНадписьВремяОбновления(ДатаПоследнегоОбновления);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроизвестиДействияПослеПоказаФормыПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	Подключаемый_ПроизвестиДействияПослеПоказаФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроизвестиДействияПослеПоказаФормы()
	
	Если НЕ ДлительнаяОтправкаКлиентСервер.ЭтоФормаГрупповойОтправки(, Ложь) Тогда
		Активизировать();
	КонецЕсли;
	
	Если ПоказатьПредложениеОформитьЗаявлениеНаПодключение Тогда
		
		Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
			Оповещение = Новый ОписаниеОповещения("Подключаемый_ПроизвестиДействияПослеПоказаФормыПредварительное", ЭтотОбъект);
			ИнициализироватьКонтекстЭДО(Оповещение);
			Возврат;
		КонецЕсли;
		
		// Показываем предложение на подключение в случае, если ни одна организация
		// не подключена к ЭДО.
		Если КонтекстЭДОКлиент <> Неопределено Тогда
			
			// Не показывается, если есть отправленные заявления
			ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ПоказатьФормуПредложениеОформитьЗаявлениеНаПодключение(, Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.АктуализироватьСлужебнуюИнформациюМеханизмаОнлайнСервисовРОПриНеобходимости(
		ЭтотОбъект, ПараметрыИНастройкиОнлайнСервисов);
	
	Если ТипЗнч(ВидОтчета) = Тип("СписокЗначений") Тогда
		Если ВидОтчета.Количество() = 1 И ЗначениеЗаполнено(ВидОтчета[0].Значение) Тогда
			Если Элементы.ОтборВидОтчета.СписокВыбора.НайтиПоЗначению(ВидОтчета[0].Значение) = Неопределено Тогда
				СоздатьОтчет();
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(ВидОтчета) Тогда
			Если Элементы.ОтборВидОтчета.СписокВыбора.НайтиПоЗначению(ВидОтчета) = Неопределено Тогда
				СоздатьОтчет();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	#Область Используется1СОтчетность
	Если НЕ Используется1СОтчетность И ПроверкаСостоянияИнтеграцииВФонеПриОткрытии <> Неопределено Тогда
		ОжидатьЗавершенияФоновогоЗаданияПолученияСостоянияИнтеграцииСБанком(ПроверкаСостоянияИнтеграцииВФонеПриОткрытии);
	КонецЕсли;
	#КонецОбласти
	
	ОбновитьТаблицуУведомленийСХешем();
	
КонецПроцедуры

#Область Используется1СОтчетность

&НаКлиенте
Процедура ОжидатьЗавершенияФоновогоЗаданияПолученияСостоянияИнтеграцииСБанком(Результат)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПослеПолученияИнформацииОВнешнейКомпоненте = Новый ОписаниеОповещения(
		"ОбработатьРезультатПроверкиСостоянияИнтеграцииСБанком", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ПослеПолученияИнформацииОВнешнейКомпоненте, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПроверкиСостоянияИнтеграцииСБанком(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ПоказатьБаннерСостоянияИнтеграцииСБанком(Результат);
	КонецЕсли;
	
	Если ИнтервалПроверкиСостоянияИнтеграцииСБанком > 0 Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ЗапуститьПроверкуСостояниеИнтеграцииВФоне", ИнтервалПроверкиСостоянияИнтеграцииСБанком, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьБаннерСостоянияИнтеграцииСБанком(Результат)
	
	Если Результат.Статус = "Выполнено" Тогда
		ДанныеБаннера = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		РегламентированнаяОтчетностьПереопределяемый.ПоказатьБаннерСостоянияОтправки(ЭтотОбъект, ДанныеБаннера);
	Иначе
		Элементы.ГруппаБаннерСостоянияОтправки.Видимость = Ложь;
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗапуститьПроверкуСостояниеИнтеграцииВФоне() Экспорт
	
	Результат = РегламентированнаяОтчетностьВызовСервера.СостояниеИнтеграцииСБанкомВФоне(УникальныйИдентификатор);
	ОжидатьЗавершенияФоновогоЗаданияПолученияСостоянияИнтеграцииСБанком(Результат);
	
КонецПроцедуры

#КонецОбласти 

&НаКлиенте
Процедура Подключаемый_ПроверитьПодключениеИнтернетПоддержки() Экспорт
	
	// Если Форма авторизации ИПП была открыта при начале работы приложения - закроем
	// ее и предложим пользователю для авторизации новую после показа формы "1С-Отчетность".
	ФормаАвторизации = ФормаОкнаАвторизацииИППЕслиОткрыто();
	
	Если НЕ ДлительнаяОтправкаКлиентСервер.ЭтоФормаГрупповойОтправки(, Ложь) Тогда
		Активизировать();
	КонецЕсли;
	
	Если ФормаАвторизации <> Неопределено Тогда
		ФормаАвторизации.Закрыть();
	КонецЕсли;
	
	ИнтернетПоддержкаПользователейБРОКлиент.ПроверитьВозможностьВыполненияОперации(
		ОписаниеОповещенияПослеПроверкиПодключенияКИПП, ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбновитьНадписьВремяОбновления(ДатаПоследнегоОбновления)
	
	ДатаПоследнегоОбновления = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ДатаПоследнегоОбновленияСобытий();
	Возврат ЭлектронныйДокументооборотСКонтролирующимиОрганами.ПолучитьНадписьВремяОбновления(ДатаПоследнегоОбновления);
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьИзФайлаВФормуОтчета(Команда)
	
	РегламентированнаяОтчетностьЗагрузкаКлиент.ЗагрузитьИзФайлаОтчет(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаВФормуУведомление(Команда)
	
	УведомлениеОСпецрежимахНалогообложенияКлиент.ЗагрузитьИзФайлаУведомление(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаВЭлектронноеПредставлениеПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ЗагрузитьИзФайлаВЭлектронноеПредставление(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаВЭлектронноеПредставление(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ЗагрузитьИзФайлаВЭлектронноеПредставлениеПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОКлиент.ПолучениеФайловДляИмпортаНачало(УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьМашиночитаемуюДоверенностьФНС(Команда)
	
	ОткрытьФорму(
		"Справочник.МашиночитаемыеДоверенностиФНС.Форма.ФормаЗагрузкиИзФайла",,
		ЭтаФорма,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	ОнлайнПроверка(Элементы.Отчеты);
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.РегламентированныйОтчет;
		
		Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
			
			Ссылка = ТекущиеДанные.РегламентированныйОтчет;
			НаименованиеОтчета = ТекущиеДанные.НаименованиеОтчета;
			ВыгрузкаЭлектронногоПредставления(Ссылка, НаименованиеОтчета);
			
		ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
			
			Ссылка               = ТекущиеДанные.РегламентированныйОтчет;
			НаименованиеОтчета   = ТекущиеДанные.НаименованиеОтчета;
			ВариантОтчета        = ТекущиеДанные.ВариантОтчета;
			ПредставлениеПериода = ТекущиеДанные.ФинансовыйПериод;
			ОрганизацияСсылка	 = ТекущиеДанные.Организация;
						
			ВыгрузкаРегламентированногоОтчета(Ссылка, ОрганизацияСсылка, НаименованиеОтчета, ВариантОтчета, ПредставлениеПериода);
			
		ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.МакетыПенсионныхДел") Тогда
			
			ПоказатьПредупреждение(,НСтр("ru = 'Выгрузка макетов пенсионных дел не предусмотрена'"));
			
		ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЗаявлениеОНазначенииПенсии") Тогда
			
			ПоказатьПредупреждение(,НСтр("ru = 'Выгрузка заявлений о назначении пенсии не предусмотрена'"));
			
		ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗаявленияПоЭлДокументооборотуСПФР") Тогда
			
			ПоказатьПредупреждение(,НСтр("ru = 'Выгрузка заявлений не предусмотрена'"));
			
		Иначе
			
			// Остальные виды рег отчетов
			РегламентированнаяОтчетностьКлиент.Выгрузить(Ссылка, ЭтотОбъект.УникальныйИдентификатор);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьОтчет(Команда)
	
	ПечатьОтчетности(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОтчетПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ОтправитьОтчет(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОтчет(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ОтправитьОтчетПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;	

	РегламентированнаяОтчетностьКлиент.ОтправитьОтчетИзФормыСписка(ЭтотОбъект, Команда.Имя, КонтекстЭДОКлиент, ТекущиеДанные); 
	
КонецПроцедуры
&НаКлиенте
Процедура ОтправитьОтчетПодтверждениеОтправкиОтчетаЗавершение(Отказ, ДополнительныеПараметры) Экспорт

	// Внимание!
	// ---------
	// В модуле объекта обработки ДокументооборотСКонтролирующимиОрганами есть процедура ОтправитьРегламентированныйОтчет(),
	// которая используется при отправке отчетов из мобильной бухгалтерии без взаимодействия с клиентским контекстом.
	// При изменениях необходимо синхронно менять обе процедуры.
	
	Если Отказ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	Ссылка = ДополнительныеПараметры.Ссылка;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой", ЭтотОбъект, ДополнительныеПараметры);
		РегламентированнаяОтчетностьКлиент.ПередОтправкойРегламентированногоОтчета(ОписаниеОповещения, Ссылка);
		Возврат;
		
	Иначе
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьОтчетЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Ложь);
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой(Отказ, ДополнительныеПараметры) Экспорт

	// Внимание!
	// ---------
	// В модуле объекта обработки ДокументооборотСКонтролирующимиОрганами есть процедура ОтправитьРегламентированныйОтчет(),
	// которая используется при отправке отчетов из мобильной бухгалтерии без взаимодействия с клиентским контекстом.
	// При изменениях необходимо синхронно менять обе процедуры.
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Ссылка = ДополнительныеПараметры.Ссылка;
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьОтчетЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РегламентированнаяОтчетностьКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОтчетЗавершение(Отказ, ДополнительныеПараметры) Экспорт

	// Внимание!
	// ---------
	// В модуле объекта обработки ДокументооборотСКонтролирующимиОрганами есть процедура ОтправитьРегламентированныйОтчет(),
	// которая используется при отправке отчетов из мобильной бухгалтерии без взаимодействия с клиентским контекстом.
	// При изменениях необходимо синхронно менять обе процедуры.
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтотОбъект, ДополнительныеПараметры.КонтролирующийОрган, , 
		Истина, ДополнительныеПараметры.Ссылка, ДополнительныеПараметры.Организация);
	
КонецПроцедуры	

&НаКлиенте
Процедура ПечатьОтчетШтрих(Команда)
	
	ПечатьОтчетности(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ПроверитьВыгрузку(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузку(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ПроверитьВыгрузкуПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;
	Закладка = Элементы.Отчеты;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.РегламентированныйОтчет;
		
		Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
			
			ПредупредитьОбОграничениях(Закладка, НСтр("ru = 'Проверка выгрузки не предусмотрена'"));
			Возврат;
			
		ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
			
			СтандартнаяОбработка = Истина;
			
			Если ВыполнениеКомандыОтменено(?(ТекущиеДанные.Свойство("НаименованиеОтчета"), ТекущиеДанные.НаименованиеОтчета, ТекущиеДанные.Наименование), ТекущиеДанные.Организация) Тогда
				Возврат;
			КонецЕсли;
			
			ТекущиеДанныеНаименованиеОтчета = ?(ТекущиеДанные.Свойство("НаименованиеОтчета"), ТекущиеДанные.НаименованиеОтчета, ТекущиеДанные.Наименование);
									
			ОписаниеОтчета = ОписаниеРегОтчета(ТекущиеДанные.РегламентированныйОтчет);
		
			Если ОбработаноДействиеДляАлкоОтчета(ТекущиеДанные.РегламентированныйОтчет,  
												ОписаниеОтчета, "ПроверкаВыгрузки") Тогда
			    Возврат;		
			КонецЕсли;
			
			// Обход ошибки платформы.
			Если РегламентированнаяОтчетностьКлиент.ВебБраузер() = "MSIE" Тогда
				Если ТекущиеДанныеНаименованиеОтчета = "РСВ-1 ПФР"
					ИЛИ ТекущиеДанныеНаименованиеОтчета = "Декларация по НДС"
					ИЛИ ТекущиеДанныеНаименованиеОтчета = "Бухгалтерская отчетность (с 2011 года)"
					ИЛИ ТекущиеДанныеНаименованиеОтчета = "Бухгалтерская отчетность малых предприятий" Тогда
					ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
						"ru='Для выгрузки (проверки выгрузки, проверки в интернете, отправки) в браузере ""Internet Explorer"" необходимо открыть форму регламентированного отчета ""%1""'"),
						ТекущиеДанныеНаименованиеОтчета);
					ПоказатьПредупреждение(, ТекстПредупреждения);
					Возврат;
				КонецЕсли;
			КонецЕсли;	
				
			Если ТекущиеДанныеНаименованиеОтчета
				= "Реестр для налоговых вычетов по акцизам на этиловый спирт (денатурированный этиловый спирт)" Тогда
				ПоказатьПредупреждение(, НСтр(
				"ru='Проверка выгрузки в электронном виде формы
				|отчета, утвержденной приказом ФНС России
				|от 26.04.2021 № ЕД-7-15/418@, будет
				|реализована в будущих версиях программы'"));
				Возврат;
			КонецЕсли;
			
			Если ТекущиеДанныеНаименованиеОтчета = "Движение средств по счету в банке за пределами РФ" Тогда
				ОписаниеРегОтчета = ОписаниеРегОтчета(Ссылка);
				Если ОписаниеРегОтчета.ВыбраннаяФорма = "ФормаОтчета2015Кв3" Тогда
					ПоказатьПредупреждение(, НСтр(
					"ru='Проверка выгрузки в электронном виде формы отчета
					|в редакции постановления Правительства РФ
					|от 25.07.2015 № 761 не предусмотрена
					|законодательством. Форма сдается в бумажном виде'"));
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			Если ТекущиеДанныеНаименованиеОтчета = "Доходы, выплаченные иностранным организациям" Тогда
				ОписаниеДохИно = ОписаниеРегОтчета(Ссылка);
				Если (ОписаниеДохИно.ВыбраннаяФорма = "ФормаОтчета2020Кв1"
				  ИЛИ ОписаниеДохИно.ВыбраннаяФорма = "ФормаОтчета2016Кв1")
					И ОписаниеДохИно.Свойство("ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении") Тогда
					Если НЕ ОписаниеДохИно.ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении Тогда
						Если ОписаниеДохИно.Свойство("ТекстСообщения") Тогда
							Сообщение = Новый СообщениеПользователю;
							Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
							"ru='%1 Проверка выгрузки прервана.'"), ОписаниеДохИно.ТекстСообщения);
							Сообщение.Сообщить();
						КонецЕсли;
						Возврат;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если ТекущиеДанныеНаименованиеОтчета = "Сведения о доле доходов от туристско-рекреационной деятельности" Тогда
				Если ОписаниеРегОтчета(Ссылка).ВыбраннаяФорма = "ФормаОтчета2018Кв1" Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
					"ru='Выгрузка, проверка выгрузки, проверка в интернете, отправка в электронном виде, печать с двухмерным
					|штрихкодом PDF417 формы отчета ""%1"",
					|рекомендованной письмом ФНС России от 16.01.2019 № СД-4-3/441@, не предусмотрены'"),
					ТекущиеДанныеНаименованиеОтчета);
					Сообщение.Сообщить();
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			Если ТекущиеДанныеНаименованиеОтчета = "РСВ-1 ПФР" Тогда
				ОписаниеРСВ_1 = ОписаниеРегОтчета(Элементы.Отчеты.ТекущиеДанные.РегламентированныйОтчет);
				Если ОписаниеРСВ_1.ДатаКонцаПериодаОтчета >= '20140101' Тогда
					Если НЕ (ПерсонифицированныйУчетВедетсяВоВнешнейПрограмме И ОписаниеРСВ_1.ОтчетСтарогоОбразца_20141101) Тогда
						РегламентированнаяОтчетностьКлиент.ПриПроверкеВыгрузкиРСВ_1ИзЖурналаРеглОтчетов(Ссылка, ОписаниеРСВ_1, СтандартнаяОбработка);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если (ТекущиеДанныеНаименованиеОтчета = "Статистика: Форма учета перемещения товаров"
				ИЛИ ТекущиеДанныеНаименованиеОтчета = "Статистическая форма учета перемещения товаров") И КонтекстЭДОКлиент <> Неопределено Тогда
				ДокументЗаявлениеОВвозеТоваров = Неопределено;
				ИмяДокументаЗаявлениеОВвозеТоваров = Неопределено;
				РезультатПолученияЗаявленияОВвозеТоваров = КонтекстЭДОКлиент.ПопытатьсяПолучитьЗаявлениеОВвозеТоваровИзСтатформы(
					Ссылка, ДокументЗаявлениеОВвозеТоваров, ИмяДокументаЗаявлениеОВвозеТоваров);
				Если РезультатПолученияЗаявленияОВвозеТоваров = Неопределено Тогда
					Возврат;
				ИначеЕсли РезультатПолученияЗаявленияОВвозеТоваров Тогда
					ДополнительныеПараметры = Новый Структура();
					ДополнительныеПараметры.Вставить("ДокументЗаявлениеОВвозеТоваров", ДокументЗаявлениеОВвозеТоваров);
					ДополнительныеПараметры.Вставить("ИмяДокументаЗаявлениеОВвозеТоваров", ИмяДокументаЗаявлениеОВвозеТоваров);
					ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуЗавершениеИзСтатформы", ЭтотОбъект, ДополнительныеПараметры);
					РегламентированнаяОтчетностьКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, Ссылка);
					Возврат;
				КонецЕсли;
			КонецЕсли;
									
			Если СтандартнаяОбработка Тогда
				
				ДополнительныеПараметры = Новый Структура();
				ДополнительныеПараметры.Вставить("Ссылка", Ссылка);
				
				ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
				
				РегламентированнаяОтчетностьКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, Ссылка);
				Возврат;
				
			КонецЕсли;
			
		Иначе
			
			// Остальные виды отчетов
			ПредупредитьОбОграничениях(Закладка, НСтр("ru = 'Проверка выгрузки не предусмотрена'"));
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СписокОтчетов = Новый СписокЗначений;
	СписокОтчетов.Добавить(ДополнительныеПараметры.Ссылка);
	
	Если НЕ РегламентированнаяОтчетностьКлиент.ПроверитьРегламентированныеОтчеты(СписокОтчетов) Тогда
		ПоказатьПредупреждение(,НСтр("ru='Проверка прервана!'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуЗавершениеИзСтатформыПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ПроверитьВыгрузкуЗавершениеИзСтатформы(
		ПараметрыОповещения.Отказ,
		ПараметрыОповещения.ДополнительныеПараметры
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуЗавершениеИзСтатформы(Отказ, ДополнительныеПараметры) Экспорт
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Отказ", Отказ);
		ПараметрыОповещения.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
		Оповещение = Новый ОписаниеОповещения("ПроверитьВыгрузкуЗавершениеИзСтатформыПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	ДокументЗаявлениеОВвозеТоваров = ДополнительныеПараметры.ДокументЗаявлениеОВвозеТоваров;
	ИмяДокументаЗаявлениеОВвозеТоваров = ДополнительныеПараметры.ИмяДокументаЗаявлениеОВвозеТоваров;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	АдресФайлаВыгрузки = Неопределено;
	ИмяФайлаВыгрузки = "";
	
	РезультатВыгрузки = КонтекстЭДОКлиент.ПопытатьсяВыгрузитьСтатформуИзЗаявленияОВвозеТоваров(
		АдресФайлаВыгрузки,
		ИмяФайлаВыгрузки,
		,
		Истина,
		Истина,
		ДокументЗаявлениеОВвозеТоваров,
		ИмяДокументаЗаявлениеОВвозеТоваров);
	
	Если РезультатВыгрузки = Истина Тогда
		ПоказатьПредупреждение(,НСтр("ru='Ошибок не обнаружено!'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьПротоколовОтчетов(Команда)
	
	ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка 								= ТекущиеДанные.РегламентированныйОтчет;
		ВидКонтролирующегоОргана 			= ТекущиеДанные.ВидКонтролирующегоОргана;
		СостояниеСдачиОтчетности			= ТекущиеДанные.СостояниеСдачиОтчетности;
		НеОтправляетсяВКонтролирующийОрган	= ТекущиеДанные.НеОтправляетсяВКонтролирующийОрган;
		
		ПечатьПротоколовПоСсылке(
			Элементы.Отчеты, 
			Ссылка, 
			ВидКонтролирующегоОргана, 
			НеОтправляетсяВКонтролирующийОрган, 
			СостояниеСдачиОтчетности);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПрисоединенныеФайлыОтчета(Команда)
	
	ОткрытьПрисоединенныеФайлы();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрупповойРежимПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ГрупповойРежим(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрупповойРежим(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ГрупповойРежимПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("НачалоПериода", НачалоПериода);
	ДополнительныеПараметры.Вставить("КонецПериода", КонецПериода);
	ДополнительныеПараметры.Вставить("Организация", Организация);
	ДополнительныеПараметры.Вставить("ВидОтчета", ВидОтчета);
	ДополнительныеПараметры.Вставить("ПериодОтчета", ПериодОтчета);
	
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.ГрупповаяОтправка", ДополнительныеПараметры, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЗакладкиСверки

&НаКлиенте
Процедура ОтправитьСверкуПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ОтправитьСверку(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСверку(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ОтправитьСверкуПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Сверки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОтправляемыйОбъект = ТекущиеДанные.Ссылка;
		ТекущаяОрганизация = ТекущиеДанные.Организация;
		//Если запрос уже отправлялся, то повторно отправлять не позволяем
		Если ЕщеНеОтправлялся(ОтправляемыйОбъект) Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьСверкуЗавершение", ЭтотОбъект);
			Если ТипЗнч(ОтправляемыйОбъект) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика") Тогда
				КонтекстЭДОКлиент.ОтправкаЗапросаНаИнформационноеОбслуживание(ОтправляемыйОбъект, ТекущаяОрганизация,  ОписаниеОповещения);
			ИначеЕсли ТипЗнч(ОтправляемыйОбъект) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеСтрахователя") Тогда
				КонтекстЭДОКлиент.ОтправкаЗапросаИОС(ОтправляемыйОбъект, ТекущаяОрганизация, ОписаниеОповещения);
			Иначе
				КонтекстЭДОКлиент.ОбъектЗаполненКорректно(ОтправляемыйОбъект);
			КонецЕсли;
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Запрос на сверку уже отправлен'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСверкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Сверки.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПредставлениеАктовСверкиРасчетов(Команда)
	СоздатьСверку(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеАктовСверкиРасчетов"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПредставлениеВыпискиОперацийИзКарточкиРасчетыСБюджетом(Команда)
	СоздатьСверку(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеВыпискиОперацийИзКарточкиРасчетыСБюджетом"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПредставлениеПеречняБухгалтерскойИНалоговойОтчетности(Команда)
	СоздатьСверку(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеПеречняБухгалтерскойИНалоговойОтчетности"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПредставлениеСправкиОСостоянииРасчетовСБюджетом(Команда)
	СоздатьСверку(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеСправкиОСостоянииРасчетовСБюджетом"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПредставлениеСправкиОбИсполненииОбязанностейПоУплате(Команда)
	СоздатьСверку(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеСправкиОбИсполненииОбязанностейПоУплате"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСверкаФИОиСНИЛС(Команда)
	СоздатьСверку(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОС.СверкаФИОиСНИЛС"));
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьСверкуПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ВыгрузитьСверку(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьСверку(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ВыгрузитьСверкуПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Сверки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ОтправляемыйОбъект = ТекущиеДанные.Ссылка;

		Если ТипЗнч(ОтправляемыйОбъект) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика") Тогда
			КонтекстЭДОКлиент.ВыгрузитьЗапросИОНВФайл(ОтправляемыйОбъект, УникальныйИдентификатор);
		Иначе
			КонтекстЭДОКлиент.ВыгрузитьЗапросИОСВФайл(ОтправляемыйОбъект, УникальныйИдентификатор);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьСверкиПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ПечатьСверки(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьСверки(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ПечатьСверкиПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ТекущаяТаблица(ЭтотОбъект).ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ПечатаемыйОбъект = ТекущиеДанные.Ссылка;
		
		Если НЕ КонтекстЭДОКлиент.ОбъектЗаполненКорректно(ПечатаемыйОбъект) Тогда
			Возврат;
		КонецЕсли;
		
		ПечатнаяФормаСверки = ТабличныйДокументСверки(ПечатаемыйОбъект);
		КонтекстЭДОКлиент.НапечататьДокумент(ПечатнаяФормаСверки, Строка(ПечатаемыйОбъект));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЗакладкиЕГРЮЛ

&НаКлиенте
Процедура СоздатьЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП(Команда)
	
	СоздатьНовыйЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросНаВыпискуИзЕГРЮЛ_ЕГРИППредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ОтправитьЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ОтправитьЗапросНаВыпискуИзЕГРЮЛ_ЕГРИППредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ЕГРЮЛ.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОтправляемыйОбъект = ТекущиеДанные.Ссылка;
		ТекущаяОрганизация = ТекущиеДанные.Организация;
		//Если запрос уже отправлялся, то повторно отправлять не позволяем
		Если ЕщеНеОтправлялся(ОтправляемыйОбъект) Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьЗапросНаВыпискуИзЕГРЮЛ_ЕГРИПЗавершение", ЭтотОбъект);
			КонтекстЭДОКлиент.ОтправкаЗапросаНаВыпискуИзЕГР(ОтправляемыйОбъект, ТекущаяОрганизация, ОписаниеОповещения);
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Запрос уже отправлен'"));
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросНаВыпискуИзЕГРЮЛ_ЕГРИПЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.ЕГРЮЛ.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЗакладкиПисьма

&НаКлиенте
Процедура СоздатьИсходящееПисьмо(Команда)
	
	СоздатьНовоеИсходящееПисьмо();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПисьмаПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ОтправитьПисьма(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПисьма(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ОтправитьПисьмаПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
 
	ТекущиеДанные = Элементы.Письма.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОтправляемыйОбъект = ТекущиеДанные.Ссылка;
		ТекущаяОрганизация = ТекущиеДанные.Организация;
		//Если письмо уже отправлялось, то повторно отправлять не позволяем
		Если ЕщеНеОтправлялся(ОтправляемыйОбъект) Тогда
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПисьмаЗавершение", ЭтотОбъект);
			
			Если ТипЗнч(ОтправляемыйОбъект) = Тип("СправочникСсылка.ПерепискаСКонтролирующимиОрганами") Тогда
				
				ВидКонтролирующегоОргана = ТекущиеДанные.ВидКонтролирующегоОргана;
				Если ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС") Тогда
					КонтекстЭДОКлиент.ОтправкаНеформализованногоДокументаВФНС(ОтправляемыйОбъект, ТекущаяОрганизация, ОписаниеОповещения);
				ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ПФР") Тогда
					КонтекстЭДОКлиент.ОтправкаНеформализованногоДокументаВПФР(ОтправляемыйОбъект, ТекущаяОрганизация, ОписаниеОповещения);
				ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСГС") Тогда
					КонтекстЭДОКлиент.ОтправкаНеформализованногоДокументаВФСГС(ОтправляемыйОбъект, ТекущаяОрганизация, ОписаниеОповещения);
				Иначе
					КонтекстЭДОКлиент.ОбъектЗаполненКорректно(ОтправляемыйОбъект);
				КонецЕсли;
			ИначеЕсли ТипЗнч(ОтправляемыйОбъект) = Тип("СправочникСсылка.ОписиИсходящихДокументовВНалоговыеОрганы") Тогда
				
				КонтекстЭДОКлиент.ОтправкаОписиИсходящихДокументовВФНС(
					ОтправляемыйОбъект,
					ТекущаяОрганизация,
					УникальныйИдентификатор,
					ОписаниеОповещения);
				
			ИначеЕсли ТипЗнч(ОтправляемыйОбъект) = Тип("ДокументСсылка.ПоясненияКДекларацииПоНДС") Тогда
				ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьПояснение(КонтекстЭДОКлиент, ОтправляемыйОбъект, ТекущаяОрганизация, УникальныйИдентификатор, ОписаниеОповещения);
			КонецЕсли;
		Иначе
			
			Если ТипЗнч(ОтправляемыйОбъект) = Тип("СправочникСсылка.ОписиИсходящихДокументовВНалоговыеОрганы") Тогда
				ТекстСообщения = НСтр("ru = 'Ответ на требование уже отправлен'");
			ИначеЕсли ТипЗнч(ОтправляемыйОбъект) = Тип("ДокументСсылка.ПоясненияКДекларацииПоНДС") Тогда
				ТекстСообщения = НСтр("ru = 'Пояснение уже отправлено'");
			Иначе
				ТекстСообщения = НСтр("ru = 'Письмо уже отправлено'");
			КонецЕсли;
			
			ПоказатьПредупреждение( , ТекстСообщения);
			
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПисьмаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОбработкаОповещения("Завершение отправки в контролирующий орган", , Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьПисьмаПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ПечатьПисьма(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьПисьма(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ПечатьПисьмаПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ТекущаяТаблица(ЭтотОбъект).ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ПечатаемыйОбъект = ТекущиеДанные.Ссылка;
		
		Если ТипЗнч(ПечатаемыйОбъект) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов") Тогда
			ПечатнаяФормаДокументаНО = ТабличныйДокументНО(ПечатаемыйОбъект);
			Если ПечатнаяФормаДокументаНО = Неопределено Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Печать требования не предусмотрена'"));
				Возврат;
			КонецЕсли;
			КонтекстЭДОКлиент.НапечататьДокумент(ПечатнаяФормаДокументаНО, Строка(ПечатаемыйОбъект));
			Возврат;
		ИначеЕсли ТипЗнч(ПечатаемыйОбъект) = Тип("СправочникСсылка.ОписиИсходящихДокументовВНалоговыеОрганы") Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Печать ответа на требование не предусмотрена'"));
		ИначеЕсли ТипЗнч(ПечатаемыйОбъект) = Тип("ДокументСсылка.ПоясненияКДекларацииПоНДС") Тогда
			КонтекстЭДОКлиент.ПечатьПояснениеПоНДС(ПечатаемыйОбъект);
			Возврат;
		Иначе
			
			Если НЕ ПровереноЗаполнение(ПечатаемыйОбъект) Тогда
				Возврат;
			КонецЕсли;
			
			ПечатнаяФормаПисьма = ТабличныйДокументПисьма(ПечатаемыйОбъект);
			КонтекстЭДОКлиент.НапечататьДокумент(ПечатнаяФормаПисьма, Строка(ПечатаемыйОбъект));

		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПровереноЗаполнение(Ссылка)
	
	СсылкаНаОбъект = Ссылка.ПолучитьОбъект();
	Возврат СсылкаНаОбъект.ПроверитьЗаполнение();
	
КонецФункции

#КонецОбласти

#Область КомандыЗакладкиУведомления

&НаКлиенте
Процедура СоздатьУведомление(Команда)
	
	СоздатьНовоеУведомление();
		
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьУведомленияПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ОтправитьУведомления(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьУведомления(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ОтправитьУведомленияПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	Закладка 		= Элементы.Уведомления;
	ТекущиеДанные 	= Элементы.Уведомления.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка 			= ТекущиеДанные.Ссылка;
		ТипыСсылки 		= ТипЗнч(Ссылка);
		
		ТекущиеДанныеНаименованиеОтчета = ?(ТекущиеДанные.Свойство("НаименованиеОтчета"), ТекущиеДанные.НаименованиеОтчета,
			ТекущиеДанные.Наименование);
		
		Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗаявленияПоЭлДокументооборотуСПФР")
			И НЕ ПровереноЗаполнение(Ссылка) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Зайдите в заявление и исправьте найденные ошибки'"), Ссылка);
			Возврат;
		ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗаявленияПоЭлДокументооборотуСПФР")
			И Не ЕщеНеОтправлялся(Ссылка) Тогда
			
			ТекстСообщения = НСтр("ru = 'Заявление уже отправлено'");
			ПоказатьПредупреждение( , ТекстСообщения);
			Возврат;
			
		ИначеЕсли ТекущиеДанные.НеОтправляетсяВКонтролирующийОрган
			ИЛИ ТекущиеДанныеНаименованиеОтчета = "Уведомление о наличии счета"
			ИЛИ ТекущиеДанныеНаименованиеОтчета = "Отчет о движении средств по счету в банке за пределами РФ"
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.ув.ОДДС")
			ИЛИ ТекущиеДанныеНаименованиеОтчета = "Уведомление об освобождении от налога на прибыль для участников ""Сколково"""
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.ув.осв.приб.Сколково")
			ИЛИ ТекущиеДанныеНаименованиеОтчета = "Уведомление об освобождении от НДС для участников ""Сколково"""
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.ув.осв.НДС Сколково")
			ИЛИ ТекущиеДанныеНаименованиеОтчета = "Заявление на получение субсидии на профилактику"
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.заяв.субс.профилак.")
			ИЛИ ТекущиеДанныеНаименованиеОтчета = "Заявление о выдаче справки о состоянии расчетов по налогам и сборам"
			ИЛИ (ТекущиеДанныеНаименованиеОтчета = "Заявление о постановке на учет (ЕНВД-1)"
				ИЛИ ТекущиеДанныеНаименованиеОтчета = "Постановка на учет организации (ЕНВД-1)")
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.ЕНВД-1")
			ИЛИ (ТекущиеДанныеНаименованиеОтчета = "Заявление о постановке на учет (ЕНВД-2)"
				ИЛИ ТекущиеДанныеНаименованиеОтчета = "Постановка на учет ИП (ЕНВД-2)")
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.ЕНВД-2")
			ИЛИ (ТекущиеДанныеНаименованиеОтчета = "Заявление о снятии с учета (ЕНВД-3)"
				ИЛИ ТекущиеДанныеНаименованиеОтчета = "Снятие с учета организации (ЕНВД-3)")
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.ЕНВД-3")
			ИЛИ (ТекущиеДанныеНаименованиеОтчета = "Заявление о снятии с учета (ЕНВД-4)"
				ИЛИ ТекущиеДанныеНаименованиеОтчета = "Снятие с учета ИП (ЕНВД-4)")
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.ЕНВД-4")
			ИЛИ ТекущиеДанныеНаименованиеОтчета = "Заявление о заявительном порядке возмещения НДС и акциза"
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.заяв.пор.воз.НДС,ак.")
			ИЛИ ТекущиеДанныеНаименованиеОтчета = "Заявление о предоставлении налоговой льготы по налогу на имущество"
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.льгота нал.на имущ.")
			ИЛИ ТекущиеДанныеНаименованиеОтчета = "Прекращение исчисления налога в связи с изъятием транспортного средства"
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.изъятие ТС")
			ИЛИ ТекущиеДанныеНаименованиеОтчета = "Заявление о гибели имущества-объекта налогообложения"
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.заяв.о гибели имущ.")
			ИЛИ (ТекущиеДанныеНаименованиеОтчета = "Передача сообщений о суммах транспортного и земельного налога"
				ИЛИ ТекущиеДанныеНаименованиеОтчета = "Заявление о передаче сообщений об исчисленных налоговым органом суммах транспортного налога и земельного налога")
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.пер.сооб.о сумм.нал.")
			ИЛИ ТекущиеДанныеНаименованиеОтчета = "Уведомление об освобождении от уплаты НДС при ЕСХН"
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.осв.от НДС при ЕСХН")
			ИЛИ (ТекущиеДанныеНаименованиеОтчета = "Уведомление об освобождении от уплаты НДС"
				ИЛИ ТекущиеДанныеНаименованиеОтчета = "Освобождение от НДС")
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.осв.от уплаты НДС")
			ИЛИ ТекущиеДанныеНаименованиеОтчета = "Заявление об освобождении от уплаты страховых взносов"
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.осв.от страх.взносов") Тогда
			
			ПоказатьПредупреждение(, ?(ТекущиеДанныеНаименованиеОтчета = "Заявление на получение субсидии на профилактику",
				НСтр("ru = 'Отправка заявления в электронном виде предусмотрена законодательством с 15 июля по 15 августа 2020 г. (включительно)'"),
				НСтр("ru = 'Отправка уведомления в электронном виде не предусмотрена законодательством.
						   |Уведомление сдается в бумажном виде'")));
			
		Иначе
			
			ДополнительныеПараметры = Новый Структура();
			ДополнительныеПараметры.Вставить("ТипыСсылки", ТипыСсылки);
			ДополнительныеПараметры.Вставить("ВидКонтролирующегоОргана", ТекущиеДанные.ВидКонтролирующегоОргана);
			ДополнительныеПараметры.Вставить("Ссылка", Ссылка);
			ДополнительныеПараметры.Вставить("Организация", ТекущиеДанные.Организация);
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ОтправитьУведомленияПодтверждениеОтправкиОтчетаЗавершение", 
				ЭтотОбъект, 
				ДополнительныеПараметры);
				
			КонтекстЭДОКлиент.ПодтверждениеОтправкиОтчета(
				ЭтотОбъект, 
				ОписаниеОповещения, 
				Ссылка,
				Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.Уведомления.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьУведомленияПодтверждениеОтправкиОтчетаЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	ВидКонтролирующегоОргана 	= ДополнительныеПараметры.ВидКонтролирующегоОргана;
	Ссылка 						= ДополнительныеПараметры.Ссылка;
	ОрганизацияВДокументе 		= ДополнительныеПараметры.Организация;
	
	КонтролирующийОрган = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ИмяПеречисления(ВидКонтролирующегоОргана);
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(
		ЭтотОбъект, 
		КонтролирующийОрган, 
		, 
		Истина, 
		Ссылка, 
		ОрганизацияВДокументе);
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернетеУведомления(Команда)
	
	ОнлайнПроверка(Элементы.Уведомления);
	
КонецПроцедуры

&НаСервере
Функция ЗаголовокФормыПечати(Ссылка)
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ВидУведомления, Организация");
	Возврат Новый Структура("ЗаголовокФормы", "" + Реквизиты.ВидУведомления + " (" + Реквизиты.Организация + ")")
КонецФункции

&НаКлиенте
Процедура ПечатьУведомлений(Команда)
	
	ТекущиеДанные = Элементы.Уведомления.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		УведомлениеДляПечати = ТекущиеДанные.Ссылка;
		Если Не ЗначениеЗаполнено(УведомлениеДляПечати) Тогда 
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(УведомлениеДляПечати) = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") Тогда
		
			Попытка
				ДополнительныеПараметры = ЗаголовокФормыПечати(УведомлениеДляПечати);
				СписокПечатаемыхЛистов = УведомлениеОСпецрежимахНалогообложенияВызовСервера.ПечатьУведомленияБРО(УведомлениеДляПечати);
				РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(ЭтотОбъект, , Ложь, 
					СписокПечатаемыхЛистов, ДополнительныеПараметры);
			Исключение
				МассивПечати = Новый Массив;
				МассивПечати.Добавить(УведомлениеДляПечати);
				
				УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
					"Документ.УведомлениеОСпецрежимахНалогообложения",
					"Уведомление", МассивПечати, Неопределено);
			КонецПопытки;
				
		ИначеЕсли ТипЗнч(УведомлениеДляПечати) = Тип("ДокументСсылка.УведомлениеОПолучателеДокументов") Тогда
			
			ТекстПредупреждения = НСтр("ru = 'Печать уведомления о получателе документов не предусмотрена.'");
			ПоказатьПредупреждение(,ТекстПредупреждения);
			
		ИначеЕсли ТипЗнч(УведомлениеДляПечати) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиФНС") Тогда
			
			ТекстПредупреждения = НСтр("ru = 'Печать машиночитаемой доверенности налогоплательщика не предусмотрена.'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
			
		ИначеЕсли ТипЗнч(УведомлениеДляПечати) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
			
			// Печать электронного представления
			Ссылка 			= ТекущиеДанные.Ссылка;
			Наименование 	= ТекущиеДанные.Наименование;
			
			ПечатьЭлектронногоПредставления(Ссылка, Наименование);
			
		ИначеЕсли ТипЗнч(УведомлениеДляПечати) = Тип("ДокументСсылка.ЗаявленияПоЭлДокументооборотуСПФР") Тогда
			
			Ссылка 			= ТекущиеДанные.Ссылка;
			Наименование 	= ТекущиеДанные.Наименование;
			
			ПечатьЗаявленияПоЭлДокументооборотуСПФР(Ссылка);
			
		Иначе
			
			СведенияПоОбъекту = РегламентированнаяОтчетностьВызовСервера.СведенияПоОбъектуНеВходящемуВБРО(УведомлениеДляПечати);
			Если СведенияПоОбъекту <> Неопределено Тогда // это объект, не входящий в БРО
				НапечататьОбъектНеВходящийВБРО(УведомлениеДляПечати, СведенияПоОбъекту);
			КонецЕсли;
				
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьУведомленияПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ВыгрузитьУведомления(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьУведомления(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ВыгрузитьУведомленияПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Уведомления.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		УведомлениеДляВыгрузки = ТекущиеДанные.Ссылка;
		Если Не ЗначениеЗаполнено(УведомлениеДляВыгрузки) Тогда 
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(УведомлениеДляВыгрузки) = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") Тогда
			
			ВыгружаемыеДанные = СформироватьXMLНаСервере(УникальныйИдентификатор, УведомлениеДляВыгрузки);
			Если ВыгружаемыеДанные <> Неопределено Тогда 
				РегламентированнаяОтчетностьКлиент.ВыгрузитьФайлы(ВыгружаемыеДанные);
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(УведомлениеДляВыгрузки) = Тип("ДокументСсылка.УведомлениеОПолучателеДокументов") Тогда
			
			Если КонтекстЭДОКлиент = Неопределено Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
				Возврат;
			КонецЕсли;
			
			КонтекстЭДОКлиент.ВыгрузитьУведомлениеОПолучателеДокументовВФайл(УведомлениеДляВыгрузки, УникальныйИдентификатор);
			
		ИначеЕсли ТипЗнч(УведомлениеДляВыгрузки) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиФНС") Тогда
			
			ОпределятьКодНалоговогоОрганаПолучателя = Истина;
			ФорматДоверенностиПоПриказуФНС 			= Истина;
			ПараметрыВозврата = Новый Структура;
			ПараметрыВозврата.Вставить("ВозвращатьАдресВоВременномХранилище", 	Истина);
			ПараметрыВозврата.Вставить("ВозвращатьИмяФайлаСРасширением", 		Истина);
			ПараметрыВозврата.Вставить("ВозвращатьПриложеннуюПодпись", 			Истина);
			РезультатВыгрузки = ДокументооборотСКОВызовСервера.ВыгрузитьМЧД(
				УведомлениеДляВыгрузки,
				Новый УникальныйИдентификатор,,
				ОпределятьКодНалоговогоОрганаПолучателя,
				ФорматДоверенностиПоПриказуФНС,
				Истина,
				ПараметрыВозврата);
			
			Если ЗначениеЗаполнено(РезультатВыгрузки) Тогда
				ОперацииСФайламиЭДКОКлиент.СохранитьФайлы(РезультатВыгрузки);
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(УведомлениеДляВыгрузки) = Тип("ДокументСсылка.ЗаявленияПоЭлДокументооборотуСПФР") Тогда
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выгрузка заявления не предусмотрена'"));
			
		ИначеЕсли ТипЗнч(УведомлениеДляВыгрузки) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
			
			Наименование = ТекущиеДанные.Наименование;
			ВыгрузкаЭлектронногоПредставления(УведомлениеДляВыгрузки, Наименование);
				
		Иначе
				
			РегламентированнаяОтчетностьКлиент.ВыгрузитьПереопределяемый(УведомлениеДляВыгрузки);
				
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьПротоколовУведомлений(Команда)
	
	ТекущаяТаблица = ТекущаяТаблица(ЭтотОбъект);
	ТекущиеДанные  = ТекущаяТаблица.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка 								= ТекущиеДанные.Ссылка;
		ВидКонтролирующегоОргана 			= ТекущиеДанные.ВидКонтролирующегоОргана;
		СостояниеСдачиОтчетности			= ТекущиеДанные.СостояниеСдачиОтчетности;
		НеОтправляетсяВКонтролирующийОрган	= ТекущиеДанные.НеОтправляетсяВКонтролирующийОрган;
		
		ПечатьПротоколовПоСсылке(
			ТекущаяТаблица, 
			Ссылка, 
			ВидКонтролирующегоОргана, 
			НеОтправляетсяВКонтролирующийОрган, 
			СостояниеСдачиОтчетности);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Уведомления);
КонецПроцедуры

&НаКлиенте
Процедура УведомленияПриАктивизацииСтроки(Элемент)
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПрисоединенныеФайлыУведомления(Команда)
	
	ОткрытьПрисоединенныеФайлы();
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЗакладкиВходящие

&НаКлиенте
Процедура СоздатьОтветПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	СоздатьОтвет(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОтвет(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("СоздатьОтветПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Входящие.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.Ссылка;
		Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ПерепискаСКонтролирующимиОрганами") Тогда
			
			Если КонтекстЭДОКлиент.ТребуетсяОтправкаПакетаСДопДокументами(Ссылка) Тогда 
				КонтекстЭДОКлиент.СоздатьПисьмоОтветСВыборомВидаОтвета(Ссылка);
			Иначе
				КонтекстЭДОКлиент.СоздатьПисьмоОтвет(Ссылка);
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов") Тогда
			
			ДополнительныеПараметры = Новый Структура();
			ДополнительныеПараметры.Вставить("ЗапретитьСозданиеПисьмаВОтветНаТребованиеПоДекларации", Истина);
			
			КонтекстЭДОКлиент.СоздатьОтветНаДокументРеализацииПолномочий(Ссылка, ДополнительныеПараметры);
			
		ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ТранспортноеСообщение") Тогда
			
			ТекстПредупреждения = НСтр("ru = 'Создание ответа на это письмо не предусмотрено'");
			ПоказатьПредупреждение(,ТекстПредупреждения);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьПрочитаннымПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ПометитьПрочитанным(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьПрочитанным(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ПометитьПрочитаннымПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	Результат = ПометитьПрочтенными();
	
	Если ТекущаяСтраница(ЭтотОбъект) <> ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Входящие")
		И Результат.ВсеВыделенныеОбъекты.Количество() > 0
		И Результат.СтавшиеПрочтенными.Количество() = 0
		И Результат.СтавшиеНепрочтенными.Количество() = 0 Тогда
		СообщитьОНевозможностиИзменитьПрочтенность();
		Возврат;
	КонецЕсли;

	Для каждого ИзменившийсяОбъект Из Результат.СтавшиеПрочтенными Цикл
		Оповестить("Пометка прочтенным", , ИзменившийсяОбъект);
	КонецЦикла;
	
	Для каждого ИзменившийсяОбъект Из Результат.СтавшиеНепрочтенными Цикл
		Оповестить("Пометка непрочтенным", , ИзменившийсяОбъект);
	КонецЦикла;
	
	// Обновление динамического списка.
	ТекущаяТаблица = ТекущаяТаблица(ЭтотОбъект);
	ТекущаяТаблица.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЗакладкиНастройки

&НаКлиенте
Процедура ПроиндексироватьОбъекты(Команда)
	
	ДописатьОбъектыОтсутствующиеВРегистре(Истина);
	УправлениеЭУ(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьНастройкиМеханизмаОнлайнСервисовРОНажатие(Элемент)
	
	ОткрытьФорму("Обработка.ОнлайнСервисыРегламентированнойОтчетности.Форма.НастройкиМеханизмаОнлайнСервисов");
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьИнформацияОбОбновленияхНажатие(Элемент)
	
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ОткрытьФормуДоступныхОбновленийРО();
	
КонецПроцедуры

&НаКлиенте
Процедура ЖурналОбменаСКонтролирующимиОрганамиПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ЖурналОбменаСКонтролирующимиОрганами(
		ПараметрыОповещения.Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ЖурналОбменаСКонтролирующимиОрганами(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения("ЖурналОбменаСКонтролирующимиОрганамиПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.УправлениеОбменом");
КонецПроцедуры

&НаКлиенте
Процедура СкрытыеОтчетыИДокументыПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	СкрытыеОтчетыИДокументы();
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытыеОтчетыИДокументы()
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		Оповещение = Новый ОписаниеОповещения("СкрытыеОтчетыИДокументыПредварительное", ЭтотОбъект);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
			
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.СкрытыеОбъекты");
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКонтекстЭДОДляЗапускаДиагностики(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Свойство("КонтекстЭДО") Тогда 
		КонтекстЭДОКлиент = Результат.КонтекстЭДО;
		КонтекстЭДОКлиент.ОткрытьФормуДиагностикиОтчетности(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Диагностика1СОтчетностьНажатие(Элемент)
			
	ОписаниеОповещения = Новый ОписаниеОповещения("ПолучитьКонтекстЭДОДляЗапускаДиагностики", ЭтотОбъект, Неопределено);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЗакладкиСервисы

&НаКлиенте
Процедура СервисНажатие(Элемент)
	
	ПерейтиПоНавигационнойСсылке(Элемент.Подсказка);
	
КонецПроцедуры

&НаКлиенте
Процедура ФНССервисПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ФНССервис(
		ПараметрыОповещения.Элемент
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ФНССервис(Элемент)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Элемент", Элемент);
		Оповещение = Новый ОписаниеОповещения("ФНССервисПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;

	КонтекстЭДОКлиент.ПерейтиПоСсылкеВИнтернетЭксплорере(Элемент.Подсказка);
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеКоманды

&НаКлиенте
Процедура ОтправитьПакетСДопДокументамиДляФНС(Команда)
	
	Основание = ОснованиеДляСозданияПакетаСДопДокументамиДляФНС();
	
	Если Основание = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Сначала выберите документ, для которого создается пакет с доп. документами для ФНС'"));
		Возврат;
	КонецЕсли;
	
	СоздатьПакетСДопДокументамиВФНС(Основание);
	
КонецПроцедуры

&НаСервере
Функция ОснованиеДляСозданияПакетаСДопДокументамиДляФНС()

	Основание = Неопределено;
	ТекущаяТаблица = ТекущаяТаблица(ЭтотОбъект);
	Для каждого Строка Из ТекущаяТаблица.ВыделенныеСтроки Цикл
		Основание = Строка.Ссылка;
		Прервать;
	КонецЦикла;

	Возврат Основание;
	
КонецФункции

&НаКлиенте
Процедура Скрыть(Команда)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПослеПодтвержденияСкрытия", 
		ЭтотОбъект);
	
	ПодтвердитьСкрытие(ОписаниеОповещения);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьОтборПоРассылке(Куда, Знач ТолькоРассылка)
	
	Для Каждого ЭлОтбора Из Куда.Элементы Цикл
		Если ЭлОтбора.ИдентификаторПользовательскойНастройки = "ЭтоРассылкаГруппа" Тогда 
			ДобавитьОтборПоРассылке(ЭлОтбора, ТолькоРассылка);
			Возврат;
		ИначеЕсли ЭлОтбора.ИдентификаторПользовательскойНастройки = "ЭтоРассылкаЭлемент" Тогда 
			ЭлОтбора.ПравоеЗначение = ТолькоРассылка;
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	ГруппаОтбора = Куда.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ИдентификаторПользовательскойНастройки = "ЭтоРассылкаГруппа";
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ИдентификаторПользовательскойНастройки = "ЭтоРассылкаЭлемент";
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтоРассылка");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = ТолькоРассылка;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаКатегориииВхПриСменеСтраницы(Элемент, ТекущаяСтраница)	
	
	ДополнитьОтборПоТипуВходящих(Входящие, 
		ЭтотОбъект.Элементы.ГруппаКатегориииВх.ТекущаяСтраница, 
		ЭтотОбъект.Элементы.СтраницаКатегорияРассылки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДополнитьОтборПоТипуВходящих(ТабВходящие, ТекущаяСтраница, СтраницаРассылки)
	
	АктивнаСтраницаРассылки = ТекущаяСтраница = СтраницаРассылки;	
	ДобавитьОтборПоРассылке(ТабВходящие.Отбор, АктивнаСтраницаРассылки);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьХеш(Стр)
	Хеш = СтрЗаменить(НРег(Стр), " ", "");
	Хеш = СтрЗаменить(Хеш, """", "");
	Хеш = СтрЗаменить(Хеш, ",", "");
	Хеш = СтрЗаменить(Хеш, "ё", "е");
	Возврат Хеш;
КонецФункции

&НаСервере
Процедура УпроститьФормуРегОтчетности() Экспорт
	
	ФлагиУчета = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ПолучитьФлагиИнтеграцииПоУмолчанию();
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиПереопределяемый.ПолучитьЗначенияКонстантИнтеграции(ФлагиУчета);
	
	Элементы.СсылкаЗаявленияНаПодключениеИзменение.Видимость = Ложь;
	
	Если ФлагиУчета.ИнтеграцияСБанком Тогда //Доступна и активна упрощенная отчетность
		
		Использовать1СОтчетность = ФлагиУчета.Использовать1СОтчетностьПриИнтеграцииСБанком;
		Элементы.ГиперссылкаОтчеты.Видимость = Использовать1СОтчетность;
		
		Если Использовать1СОтчетность Тогда
			Элементы.Разделы.ТекущаяСтраница = Элементы.СтраницаОтчеты;
		КонецЕсли;
		
		Элементы.ГиперссылкаУведомления.Видимость = Ложь;
		Элементы.ГиперссылкаСервисы.Видимость = Ложь;
		Элементы.ГиперссылкаНастройки.Видимость = Ложь;
		
		Элементы.СтраницаУведомления.Видимость = Ложь;
		Элементы.СтраницаСервисы.Видимость = Ложь;
		Элементы.СтраницаНастройки.Видимость = Ложь;
		
		Элементы.СтраницаНастройки.Видимость = Ложь;
		
		Элементы.ГруппаОтборовОтчетов.Видимость = Ложь;
		
		Элементы.ОтчетыЗагрузитьИзФайла.Видимость = Ложь;
		Элементы.ОтчетыГруппаЗагрузитьИзФайла.Видимость = Ложь;
		Элементы.ОтчетыВыгрузить.Видимость = Ложь;
		
		Элементы.ОтчетыГруппаПрисоединенныеФайлы.Видимость = Ложь;
		
		Элементы.КнопкаСоздатьСверкаФИОиСНИЛС.Видимость = Ложь;
		
		ЭлектронныйДокументооборотДоступен = Ложь;
		КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
		Если КонтекстЭДОСервер <> Неопределено Тогда
			ЭлектронныйДокументооборотДоступен = КонтекстЭДОСервер.ХотяБыОднаОрганизацияИспользуетДокументооборот();
		КонецЕсли;
		
		ЕстьОтправленныеЗаявления = ОбработкаЗаявленийАбонентаВызовСервера.ЕстьОтправленныеИлиОдобренныеЗаявления();
		УжеПодключен = ЭлектронныйДокументооборотДоступен Или ЕстьОтправленныеЗаявления;
		Если УжеПодключен Тогда 
			Элементы.СсылкаЗаявленияНаПодключениеИзменение.Заголовок = 
				НСтр("ru='Заявления на изменение и продление подключения к 1С-Отчетности'");
		Иначе
			Элементы.СсылкаЗаявленияНаПодключениеИзменение.Заголовок = 
				НСтр("ru='Заявления на подключение к 1С-Отчетности'");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ФлагиУчета.СервисЭлектронныхТрудовыхКнижек Тогда
		
		// ЭДО
		Элементы.ГиперссылкаПисьма.Видимость = Ложь;
		Элементы.СтраницаПисьма.Видимость = Ложь;
		
		Элементы.ГиперссылкаСверки.Видимость = Ложь;
		Элементы.СтраницаСверки.Видимость = Ложь;
		
		Элементы.ГиперссылкаЕГРЮЛ.Видимость = Ложь;
		Элементы.СтраницаЕГРЮЛ.Видимость = Ложь;
		
		Элементы.ГиперссылкаСервисы.Видимость = Ложь;
		Элементы.СтраницаСервисы.Видимость = Ложь;
		
		Элементы.ЖурналОбменаСКонтролирующимиОрганами.Видимость = Ложь;
		Элементы.УведомленияЗагрузитьИзФайла.Видимость = Ложь;
		
		// Отчетность
		Элементы.НастройкиРегламентированнойОтчетности.Видимость = Ложь;
		Элементы.Декорация1.Видимость = Ложь;
		Элементы.ОтчетыГруппаЗагрузитьИзФайла.Видимость = Ложь;
		
		// Переключаем на раздел отчеты
		ПредыдущаяГиперссылка = ТекущийРазделФормы(ЭтотОбъект);
		ТекущаяГиперссылка    = Элементы.ГиперссылкаОтчеты;
		ПриПереключенииРазделаСервер(ПредыдущаяГиперссылка.Имя, ТекущаяГиперссылка.Имя);
		ВывестиТаблицуРаздела(ЭтотОбъект, ТекущаяГиперссылка.Имя);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПакетСДопДокументамиВФНСПредварительное(Результат, ОснованиеПакета) Экспорт
	
	СоздатьПакетСДопДокументамиВФНС(ОснованиеПакета);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПакетСДопДокументамиВФНС(ОснованиеПакета = Неопределено)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		Оповещение = Новый ОписаниеОповещения(
			"СоздатьПакетСДопДокументамиВФНСПредварительное", 
			ЭтотОбъект,
			ОснованиеПакета);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОКлиент.СоздатьПакетСДопДокуменами(ОснованиеПакета);
	
КонецПроцедуры

&НаСервере
Процедура СкрытьЛишниеЭлементыВрежимеВыбора(Параметры)

	Если Параметры.Свойство("СкрытьЕГРЮЛ") Тогда
		СкрытьЕГРЮЛ = Параметры.СкрытьЕГРЮЛ;
	Иначе
		СкрытьЕГРЮЛ = Ложь;
	КонецЕсли;
			
	Элементы.ГруппаВыбора.Видимость = ЭтоРежимВыбора;
	
	Если ЭтоРежимВыбора Тогда
		
		Заголовок = НСтр("ru = 'Выберите документ'");
		
		Элементы.ВыбратьДокумент.КнопкаПоУмолчанию = Истина;
		
		Элементы.ГруппаПанельИнформацииСКнопкой.Видимость = Ложь;
		Элементы.ГиперссылкаСервисы.Видимость = Ложь;
		Элементы.ГиперссылкаНастройки.Видимость = Ложь;
		
		Таблицы = Новый Массив;
		Таблицы.Добавить("Отчеты");
		Таблицы.Добавить("Уведомления");
		Таблицы.Добавить("Письма");
		Таблицы.Добавить("Сверки");
		Таблицы.Добавить("ЕГРЮЛ");
		Таблицы.Добавить("Входящие");
		Таблицы.Добавить("ВходящиеРассылки");
		
		Для каждого ИмяТаблицы Из Таблицы Цикл
			
			ОбрабатываемыйЭлемент = Элементы[ИмяТаблицы];
			
			ОбрабатываемыйЭлемент.КоманднаяПанель.Видимость = Ложь;
			ОбрабатываемыйЭлемент.КонтекстноеМеню.Видимость = Ложь;
			ОбрабатываемыйЭлемент.ИзменятьСоставСтрок = Ложь;
			ОбрабатываемыйЭлемент.ТолькоПросмотр = Истина;
			ОбрабатываемыйЭлемент.РежимВыделения = РежимВыделенияТаблицы.Одиночный;
			
			Для каждого Подменю Из ОбрабатываемыйЭлемент.КонтекстноеМеню.ПодчиненныеЭлементы Цикл
				Подменю.Видимость = Ложь;
			КонецЦикла;
			
		КонецЦикла;
		
		Элементы.ЕГРЮЛКоманднаяПанель1.Видимость = Ложь;
		
		Если СкрытьЕГРЮЛ Тогда
			Элементы.СтраницаЕГРЮЛ.Видимость = Ложь;
			Элементы.ГиперссылкаЕГРЮЛ.Видимость = Ложь;
		КонецЕсли;
		
		Если Параметры.Свойство("Ссылка") Тогда
			СсылкаВКлюче      = Параметры.Ссылка;
			ОрганизацияВКлюче = Параметры.Организация;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СпозиционироватьсяНаСсылка(СсылкаВКлюче, ОрганизацияВКлюче, ИмяСобытия = Неопределено)

	// Обновляем текущую таблицу
	Если Элементы.Разделы.ТекущаяСтраница = Элементы.СтраницаОтчеты Тогда
		
		ДополнительноеУсловиеТолстыйКлиент = Истина;
#Если ТолстыйКлиентУправляемоеПриложение Тогда
		Если ОрганизацияВКлюче = Неопределено Тогда 
			Попытка
				ДополнительноеУсловиеТолстыйКлиент = (ОрганизацияВКлюче <> ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
			Исключение
			КонецПопытки;
		КонецЕсли;
#КонецЕсли
		
		Если ОрганизацияВКлюче <> Неопределено И ДополнительноеУсловиеТолстыйКлиент Тогда
			
			Отборы = Новый Массив;
			Отборы.Добавить(Новый Структура("Организация, Ссылка", ОрганизацияВКлюче, СсылкаВКлюче));
			
			КлючЗаписи = Новый(Тип("РегистрСведенийКлючЗаписи.ЖурналОтчетовСтатусы"), Отборы);
			
			Если НЕ КлючЗаписи.Пустой() Тогда
				
				ОповеститьОбИзменении(КлючЗаписи);
				Если ИмяСобытия <> "Пометка прочтенным"
					И ИмяСобытия <> "Пометка непрочтенным"
					И ИмяСобытия <> "Скрытие/восстановление объекта" Тогда
					ДобавитьВидОтчетаВИзбранные(Отборы[0]);
					Элементы.Отчеты.ТекущаяСтрока = КлючЗаписи;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		// Определем текущую страницу
		ИмяРаздела 		= СтрЗаменить(Элементы.Разделы.ТекущаяСтраница.Имя, "Страница", "");
		СтраницаЖурнала = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность." + ИмяРаздела);
		
		// Вызываем обновление только одной строки только данной страницы
		Если СтраницаЖурнала <> Неопределено Тогда
		
			Отборы = Новый Массив;
			Отборы.Добавить(Новый Структура("СтраницаЖурнала, Ссылка, Организация", СтраницаЖурнала, СсылкаВКлюче, ОрганизацияВКлюче));
			
			КлючЗаписи = Новый(Тип("РегистрСведенийКлючЗаписи.ЖурналОтправокВКонтролирующиеОрганы"), Отборы);
			
			Если НЕ КлючЗаписи.Пустой() Тогда
				ОповеститьОбИзменении(КлючЗаписи);
				
				СтраницыБезСписков = СтрРазделить("Настройки,Сервисы,Новое", ",");
				// При записи объекта позиционируемся на нем в таблице
				Если (СтрНайти(ИмяСобытия, "Запись_") > 0 ИЛИ ИмяСобытия = "Позиционирование")
					И СтраницыБезСписков.Найти(ИмяРаздела) = Неопределено Тогда
					
					Если ИмяРаздела = "Входящие" Тогда //Есть вложенные таблицы/вкладки
						АктивнаСтраницаРассылки = 
							Элементы.ГруппаКатегориииВх.ТекущаяСтраница = Элементы.СтраницаКатегорияРассылки;	
						Если АктивнаСтраницаРассылки Тогда 
							ИмяРаздела = ИмяРаздела + "Рассылки";
						КонецЕсли;
					КонецЕсли;

					Элементы[ИмяРаздела].ТекущаяСтрока = КлючЗаписи;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	

КонецПроцедуры 

&НаСервере
Процедура ОтметитьДеревоПросмотренным()
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганами.ОтметитьДеревоПросмотренным(Новое);
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОНевозможностиИзменитьПрочтенность()
	
	ТекущаяСтраница = ТекущаяСтраница(ЭтотОбъект);
	ТекстСообщения = "";
	Если ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Отчеты") Тогда
		ТекстСообщения = НСтр("ru = 'Отчет уже прочитан'");
	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Уведомления") Тогда
		ТекстСообщения = НСтр("ru = 'Уведомление уже прочитано'");
	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Сверки") Тогда
		ТекстСообщения = НСтр("ru = 'Запрос на сверку уже прочитан'");
	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.ЕГРЮЛ") Тогда
		ТекстСообщения = НСтр("ru = 'Запрос выписки из ЕГРЮЛ уже прочитан'");
	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Входящие") Тогда
		ТекстСообщения = НСтр("ru = 'Письмо уже прочитано'");
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		ПоказатьПредупреждение(,ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяСтраницы(Страница)
	
	Если Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Новое") Тогда
		ИмяРаздела = "Новое";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Отчеты") Тогда
		ИмяРаздела = "Отчеты";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Уведомления") Тогда
		ИмяРаздела = "Уведомления";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Письма") Тогда
		ИмяРаздела = "Письма";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Сверки") Тогда
		ИмяРаздела = "Сверки";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.ЕГРЮЛ") Тогда
		ИмяРаздела = "ЕГРЮЛ";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Входящие") Тогда
		ИмяРаздела = "Входящие";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Настройки") Тогда
		ИмяРаздела = "Настройки";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Сервисы") Тогда
		ИмяРаздела = "Сервисы";
	КонецЕсли;
	
	Возврат ИмяРаздела;
	
КонецФункции

&НаСервере
Функция СкрытьОбъект1СОтчетности()

	СкрытыеОбъекты = Новый Массив;
	
	ТекущаяТаблица = ТекущаяТаблица(ЭтотОбъект);
	Для каждого Строка Из ТекущаяТаблица.ВыделенныеСтроки Цикл
		
		СкрытыйОбъект = Новый Структура;
		СкрытыйОбъект.Вставить("Ссылка", 		Строка.Ссылка);
		СкрытыйОбъект.Вставить("Организация", 	Строка.Организация);
		
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.СкрытьВосстановитьОбъект1СОтчетности(
			СкрытыйОбъект.Ссылка, 
			Истина);
			
		СкрытыеОбъекты.Добавить(СкрытыйОбъект);
				
	КонецЦикла;

	Возврат СкрытыеОбъекты;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТабличныйДокументПисьма(Письмо)
	
	Возврат Справочники.ПерепискаСКонтролирующимиОрганами.ПечатнаяФорма(Письмо);

КонецФункции

&НаСервереБезКонтекста
Функция ТабличныйДокументНО(ДокументНО)
	
	Возврат Справочники.ДокументыРеализацииПолномочийНалоговыхОрганов.ПечатнаяФорма(ДокументНО);
	
КонецФункции

&НаСервереБезКонтекста
Функция ТабличныйДокументСверки(Сверка)
	
	Если ТипЗнч(Сверка) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеСтрахователя") Тогда
		Источник = Документы.ЗапросНаИнформационноеОбслуживаниеСтрахователя;
	Иначе
		Источник = Документы.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика;
	КонецЕсли;
	ТабличныйДокумент = Источник.ПечатнаяФорма(Сверка);
	
	Возврат ТабличныйДокумент;

КонецФункции

&НаСервереБезКонтекста
Функция ТекущаяДатаНаСервере()
	
	Возврат ТекущаяДатаСеанса();
	
КонецФункции

&НаКлиенте
Процедура ПодтвердитьСкрытие(ВыполняемоеОповещение)
	
	ТекущаяСтраница = ТекущаяСтраница(ЭтотОбъект);
	
	ТекущаяТаблица 			= ТекущаяТаблица(ЭтотОбъект);
	ВыделеноНесколькоСтрок 	= ТекущаяТаблица.ВыделенныеСтроки.Количество() > 1;
	
	Если ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Отчеты") Тогда
		
		Если ВыделеноНесколькоСтрок Тогда 
			ВидОбъекта = НСтр("ru = 'отчеты'");
		Иначе
			ВидОбъекта = НСтр("ru = 'отчет'");
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Уведомления") Тогда
		
		Если ВыделеноНесколькоСтрок Тогда 
			ВидОбъекта = НСтр("ru = 'уведомления'");
		Иначе
			ВидОбъекта = НСтр("ru = 'уведомление'");
		КонецЕсли;

	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Сверки") Тогда
		
		Если ВыделеноНесколькоСтрок Тогда 
			ВидОбъекта = НСтр("ru = 'запросы на сверку'");
		Иначе
			ВидОбъекта = НСтр("ru = 'запрос на сверку'");
		КонецЕсли;

	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.ЕГРЮЛ") Тогда
		
		Если ВыделеноНесколькоСтрок Тогда 
			ВидОбъекта = НСтр("ru = 'выписки из ЕГРЮЛ'");
		Иначе
			ВидОбъекта = НСтр("ru = 'выписку из ЕГРЮЛ'");
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Входящие")
		ИЛИ ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Письма") Тогда
		
		Если ВыделеноНесколькоСтрок Тогда 
			ВидОбъекта = НСтр("ru = 'письма'");
		Иначе
			ВидОбъекта = НСтр("ru = 'письмо'");
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = 'Вы уверены, что хотите скрыть %1?
                           |
                           |Скрытые отчеты и документы можно восстановить в разделе
                           |""Настройки"" - ""Скрытые отчеты и документы"" формы 1С-Отчетность'");
	ТекстСообщения = СтрШаблон(ТекстСообщения, ВидОбъекта);
	
	ПоказатьВопрос(ВыполняемоеОповещение,ТекстСообщения, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодтвержденияСкрытия(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		СкрытыеОбъекты = СкрытьОбъект1СОтчетности();
	
		Для каждого СкрытыйОбъект Из СкрытыеОбъекты Цикл
			
			Оповестить("Скрытие/восстановление объекта", 
				СкрытыйОбъект, 
				СкрытыйОбъект.Ссылка);

		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция Разделы(Форма, ТолькоРазделыСТаблицами = Ложь)
	
	ОписаниеРазделов = Новый Массив;
	
	Если НЕ ТолькоРазделыСТаблицами Тогда
		ОписаниеРазделов.Добавить("Новое");
	КонецЕсли;
	
	ОписаниеРазделов.Добавить("Отчеты");
	ОписаниеРазделов.Добавить("Уведомления");
	ОписаниеРазделов.Добавить("Письма");
	ОписаниеРазделов.Добавить("Сверки");
	ОписаниеРазделов.Добавить("ЕГРЮЛ");
	ОписаниеРазделов.Добавить("Входящие");
	
	Если НЕ ТолькоРазделыСТаблицами Тогда
		ОписаниеРазделов.Добавить("Сервисы");
		ОписаниеРазделов.Добавить("Настройки");
	КонецЕсли;
	
	Возврат ОписаниеРазделов;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьКоличествоНовых(Форма)
	
	Форма.КоличествоНовых = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ОбновитьКоличествоНовых(Форма);

	// Количество в разделе "Новое"
	Если Форма.КоличествоНовых > 0 Тогда
		ТекстНовое = НСтр("ru = 'Новое (%1)'");
		ТекстНовое = СтрШаблон(ТекстНовое, ?(Форма.КоличествоНовых > 99, "99+", Форма.КоличествоНовых));
		Форма.Элементы.ГиперссылкаНовое.Заголовок = ТекстНовое;
	Иначе
		Форма.Элементы.ГиперссылкаНовое.Заголовок = НСтр("ru = 'Новое'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьДеревоНовое(ПервоначальноеЗаполнение = Ложь)
	
	ЕстьИзмененияВНовыхСообщениях = Ложь;
	
	ДеревоНовое = РеквизитФормыВЗначение("Новое");
	
	ТекущиеСобытия = Новый Соответствие;
	Для Каждого СтрокаДерева Из ДеревоНовое.Строки Цикл
		Если ЗначениеЗаполнено(СтрокаДерева.Ссылка) Тогда
			ТекущиеСобытия.Вставить(Строка(СтрокаДерева.Ссылка.УникальныйИдентификатор()) + СтрокаДерева.Статус, Истина);
		КонецЕсли;
		Для Каждого ПодСтрокаДерева Из СтрокаДерева.Строки Цикл
			Если ЗначениеЗаполнено(ПодСтрокаДерева.Ссылка) Тогда
				ТекущиеСобытия.Вставить(Строка(ПодСтрокаДерева.Ссылка.УникальныйИдентификатор()) + ПодСтрокаДерева.Статус, Истина);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	ЭлектронныйДокументооборотСКонтролирующимиОрганами.ЗаполнитьДеревоНовое(ДеревоНовое);
	
	Для Каждого СтрокаДерева Из ДеревоНовое.Строки Цикл
		Если ЗначениеЗаполнено(СтрокаДерева.Ссылка) Тогда
			КлючПоиска = Строка(СтрокаДерева.Ссылка.УникальныйИдентификатор()) + СтрокаДерева.Статус; 
			Если ТекущиеСобытия.Получить(КлючПоиска) = Неопределено Тогда
				ЕстьИзмененияВНовыхСообщениях = Истина;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоНовое, "Новое");
	
	ДатаПоследнегоОбновления = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ДатаПоследнегоОбновленияСобытий();
	НадписьВремяОбновления = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ПолучитьНадписьВремяОбновления(ДатаПоследнегоОбновления);
	
	ОбновитьКоличествоНовых(ЭтотОбъект);

	Возврат ЕстьИзмененияВНовыхСообщениях И Не ПервоначальноеЗаполнение;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСписокОтчетовИзРегистраСведений(ПараметрыОтбора = Неопределено)
	
	Запрос = Новый Запрос;
	ТекстЗапроса     = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	                   |	Журнал.НаименованиеОтчета КАК НаименованиеОтчета
	                   |ИЗ
	                   |	РегистрСведений.ЖурналОтчетовСтатусы КАК Журнал
	                   |";
	ТекстУпорядочить = "
	                   |УПОРЯДОЧИТЬ ПО
	                   |	НаименованиеОтчета";
	
	ТекстУсловия = "";
	
	Если НЕ ПараметрыОтбора = Неопределено Тогда
		
		ЕстьПараметры = Ложь;
		
		Если НЕ ПараметрыОтбора.Организация = Справочники.Организации.ПустаяСсылка() Тогда
			ТекстУсловия  = Символы.ПС + "	Журнал.Организация = &Организация ";
			Запрос.УстановитьПараметр("Организация", ПараметрыОтбора.Организация);
			ЕстьПараметры = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.НачалоПериода)Тогда
			ТекстУсловия  = ТекстУсловия  + Символы.ПС + "	" + ?(ЕстьПараметры, "И ", "") + "Журнал.ДатаНачала >= &НачалоПериода";
			Запрос.УстановитьПараметр("НачалоПериода", ПараметрыОтбора.НачалоПериода);
			ЕстьПараметры = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.КонецПериода)Тогда
			ТекстУсловия  = ТекстУсловия  + Символы.ПС + "	" + ?(ЕстьПараметры, "И ", "") + "Журнал.ДатаОкончания <= &КонецПериода";
			Запрос.УстановитьПараметр("КонецПериода", ПараметрыОтбора.КонецПериода);
			ЕстьПараметры = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекстУсловия) Тогда
			ТекстЗапроса = ТекстЗапроса + "ГДЕ " + ТекстУсловия;
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса + ТекстУпорядочить;
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса.ВыгрузитьКолонку("НаименованиеОтчета");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиТаблицуРаздела(Форма, РазделОтчетности)
	
	ТекущийРаздел 		= ТекущийРазделФормы(Форма);
	ИмяТекущейТаблицы 	= СтрЗаменить(ТекущийРаздел.Имя, "Гиперссылка", ""); 
	
	Форма.Элементы.Разделы.ТекущаяСтраница = Форма.Элементы["Страница" + ИмяТекущейТаблицы];
	
	ТекущийРаздел = Форма.Элементы.Разделы.ТекущаяСтраница;
	
	// Эсли форма открыта на закладке с таблицей, то активизируем эту таблицу
	Если ТекущийРаздел <> Форма.Элементы.СтраницаНовое И ТекущийРаздел <> Форма.Элементы.СтраницаНастройки Тогда 
		
		ИмяЭлемента = СтрЗаменить(РазделОтчетности, "Гиперссылка", "");
		
		ЭлементТаблица = Форма.Элементы.Найти(ИмяЭлемента);
		Если ЭлементТаблица <> Неопределено Тогда 
			Форма.ТекущийЭлемент = ЭлементТаблица;
		КонецЕсли;
		
	КонецЕсли;		
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеЭУПанельУстановкаКомпонентыПечати(ОперацияСКомпонентойСовершена = Ложь)
	
	Если РегламентированнаяОтчетностьКлиент.КомпонентаПечатиМашиночитаемыхФормДоступна() Тогда
		Элементы.ПереустановитьКомпонентыПечатиМашиночитаемыхФорм.Заголовок = НСтр("ru = 'Переустановить компоненты печати машиночитаемых форм'");
	Иначе
		Элементы.ПереустановитьКомпонентыПечатиМашиночитаемыхФорм.Заголовок = НСтр("ru = 'Установка компоненты печати'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеЭУГруппаНастройки()
	
	ЗаголовкиГруппы = ЗаголовкиГруппыНастройки(Организация);
	
	Элементы.ЗаявлениеНаПодключениеК1СОтчетности.Заголовок = ЗаголовкиГруппы.ЗаголовокГиперссылкиЗаявления;
	
	ОткрывающаясяСкобка = Новый ФорматированнаяСтрока("(");
	СсылкаСписокЗавлений = Новый ФорматированнаяСтрока(
		ЗаголовкиГруппы.ЗаголовокГиперссылкиСпискаЗаявлений,,,,
		"Список");
	ЗакрывающаясяСкобка = Новый ФорматированнаяСтрока(")");
	Элементы.СписокЗаявлений.Заголовок = Новый ФорматированнаяСтрока(ОткрывающаясяСкобка, СсылкаСписокЗавлений, ЗакрывающаясяСкобка);
	
	ВебБраузер = РегламентированнаяОтчетностьКлиент.ВебБраузер();
	
	Если ЗначениеЗаполнено(ВебБраузер)
	   И НЕ ВебБраузер = "MSIE" Тогда
	   
	   Элементы.ГруппаДвухмерногоШтрихкода.Видимость = Ложь;
		
	КонецЕсли;			
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблоныПечатиМашиночитаемыхФормНажатие()
	
	ОткрытьФорму("РегистрСведений.ШаблоныПечатиМашиночитаемыхФорм.Форма.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаXMLШаблоновНажатие(Элемент)
	
	ОткрытьФорму("РегистрСведений.ШаблоныЭВФОтчетовСтатистики.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаФорматаВыгрузкиНажатие(Элемент)
	
	РегламентированнаяОтчетностьКлиент.ПоказатьФормуНастроекФорматаВыгрузкиСтатистическойОтчетностиВЭлектронномВиде(Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ЖурналВыгруженныхОтчетовНажатие(Элемент)
	
	ОткрытьФорму("Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаСписка", , ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СправочникРегламентированныхОтчетовНажатие(Элемент)
	
	ОткрытьФорму("Справочник.РегламентированныеОтчеты.Форма.ФормаСписка");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборыПоОтчетам(Форма)
	
	ОтборДинамическогоСписка = Форма.Отчеты.КомпоновщикНастроек.Настройки.Отбор;
	
	ДобавляемыеЭлементыОтбора          = Новый Массив;
	ДобавляемыеЭлементыОтбораВидОтчета = Новый Массив;
	
	ПолеКомпоновкиНаименованиеОтчета = Новый ПолеКомпоновкиДанных("НаименованиеОтчета");
	ПолеКомпоновкиОрганизация        = Новый ПолеКомпоновкиДанных("Организация");
	ПолеКомпоновкиДатаНачала         = Новый ПолеКомпоновкиДанных("ДатаНачала");
	ПолеКомпоновкиДатаОкончания      = Новый ПолеКомпоновкиДанных("ДатаОкончания");
	
	СтрМакетПенсионногоДела = "Макет пенсионного дела";
	СтрЗаявлениеОНазначенииПенсии = "Заявление о назначении пенсии";
	
	// Отбираем в массив пользовательские элементы отбора.
	Для Каждого ЭлементПользовательскойНастройки Из Форма.Отчеты.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(ЭлементПользовательскойНастройки) = Тип("ОтборКомпоновкиДанных")
			И ЭлементПользовательскойНастройки.ИдентификаторПользовательскойНастройки
				= ОтборДинамическогоСписка.ИдентификаторПользовательскойНастройки Тогда
			Для Каждого ЭлементОтбора Из ЭлементПользовательскойНастройки.Элементы Цикл
				Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
					Если (ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиНаименованиеОтчета
							И  (ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке
							ИЛИ СокрЛП(ЭлементОтбора.ПравоеЗначение) = СтрМакетПенсионногоДела))
					 ИЛИ (ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиНаименованиеОтчета
							И  (ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке
							ИЛИ СокрЛП(ЭлементОтбора.ПравоеЗначение) = СтрЗаявлениеОНазначенииПенсии))
					 ИЛИ (ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиОрганизация
							И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно)
					 ИЛИ (ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиДатаНачала
							И  (ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно
							ИЛИ ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно))
					 ИЛИ (ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиДатаОкончания) Тогда
						Продолжить;
					КонецЕсли;
					Если ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиНаименованиеОтчета Тогда
						ДобавляемыеЭлементыОтбораВидОтчета.Добавить(ЭлементОтбора);
					Иначе
						ДобавляемыеЭлементыОтбора.Добавить(ЭлементОтбора);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	// Для отбора отчета "Макет пенсионного дела" создается отдельный элемент с видом сравнения "Содержит".
	СписокОтбораВидОтчета = Новый СписокЗначений;
	ВидОтчетаМакетПенсионногоДела = Неопределено;
	Если ТипЗнч(Форма.ВидОтчета) = Тип("СписокЗначений") Тогда
		Для Каждого ЭлементВидОтчета Из Форма.ВидОтчета Цикл
			Если СокрЛП(ЭлементВидОтчета.Значение) = СтрМакетПенсионногоДела
				ИЛИ СокрЛП(ЭлементВидОтчета.Значение) = СтрЗаявлениеОНазначенииПенсии Тогда
				ВидОтчетаМакетПенсионногоДела = ЭлементВидОтчета.Значение;
			ИначеЕсли ЗначениеЗаполнено(ЭлементВидОтчета.Значение) Тогда
				СписокОтбораВидОтчета.Добавить(ЭлементВидОтчета.Значение);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если СокрЛП(Форма.ВидОтчета) = СтрМакетПенсионногоДела
			ИЛИ СокрЛП(Форма.ВидОтчета) = СтрЗаявлениеОНазначенииПенсии Тогда
			ВидОтчетаМакетПенсионногоДела = Строка(Форма.ВидОтчета);
		ИначеЕсли ЗначениеЗаполнено(Форма.ВидОтчета) Тогда
			СписокОтбораВидОтчета.Добавить(Строка(Форма.ВидОтчета));
		КонецЕсли;
	КонецЕсли;
	
	// Формируем фиксированные элементы отбора.
	ОтборДинамическогоСписка.Элементы.Очистить();
	
	ОтборГруппа = Неопределено;
	ОтборВидОтчета = Неопределено;
	ОтборВидОтчетаМакетПенсионногоДела = Неопределено;
	
	Если ВидОтчетаМакетПенсионногоДела <> Неопределено Тогда
		Если СписокОтбораВидОтчета.Количество() > 0 Тогда // создадим группу для отбора по "ИЛИ"
			ОтборГруппа = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ОтборГруппа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
			ОтборГруппа.Использование = Истина;
			ОтборВидОтчета = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборВидОтчетаМакетПенсионногоДела = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборВидОтчета.ЛевоеЗначение = ПолеКомпоновкиНаименованиеОтчета;
		Иначе
			ОтборВидОтчетаМакетПенсионногоДела = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		КонецЕсли;
		ОтборВидОтчетаМакетПенсионногоДела.ЛевоеЗначение = ПолеКомпоновкиНаименованиеОтчета;
	Иначе
		ОтборВидОтчета = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборВидОтчета.ЛевоеЗначение = ПолеКомпоновкиНаименованиеОтчета;
	КонецЕсли;
	
	ОтборОрганизация      = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборДатаНачала       = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборДатаОкончания1   = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборДатаОкончания2   = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ОтборОрганизация.ЛевоеЗначение    = ПолеКомпоновкиОрганизация;
	ОтборДатаНачала.ЛевоеЗначение     = ПолеКомпоновкиДатаНачала;
	ОтборДатаОкончания1.ЛевоеЗначение = ПолеКомпоновкиДатаОкончания;
	ОтборДатаОкончания2.ЛевоеЗначение = ПолеКомпоновкиДатаОкончания;
	
	Если НЕ ЗначениеЗаполнено(Форма.Организация) Тогда
		ОтборДинамическогоСписка.Элементы.Удалить(ОтборОрганизация);
	Иначе
		ОтборОрганизация.Использование  = Истина;
		ОтборОрганизация.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
		ОтборОрганизация.ПравоеЗначение = Форма.Организация;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Форма.ВидОтчета)
		ИЛИ (ТипЗнч(Форма.ВидОтчета) = Тип("СписокЗначений")
			И Форма.ВидОтчета.Количество() = 1 И Форма.ВидОтчета[0].Значение = "") Тогда
		Если ОтборГруппа <> Неопределено Тогда
			ОтборДинамическогоСписка.Элементы.Удалить(ОтборГруппа);
		ИначеЕсли ОтборВидОтчета <> Неопределено Тогда
			ОтборДинамическогоСписка.Элементы.Удалить(ОтборВидОтчета);
		ИначеЕсли ОтборВидОтчетаМакетПенсионногоДела <> Неопределено Тогда
			ОтборДинамическогоСписка.Элементы.Удалить(ОтборВидОтчетаМакетПенсионногоДела);
		КонецЕсли;
	Иначе
		Если ОтборВидОтчетаМакетПенсионногоДела <> Неопределено Тогда
			ОтборВидОтчетаМакетПенсионногоДела.Использование = Истина;
			ОтборВидОтчетаМакетПенсионногоДела.ВидСравнения = ВидСравненияКомпоновкиДанных.Содержит;
			ОтборВидОтчетаМакетПенсионногоДела.ПравоеЗначение = ВидОтчетаМакетПенсионногоДела;
		КонецЕсли;
		Если ОтборВидОтчета <> Неопределено Тогда
			ОтборВидОтчета.Использование = Истина;
			ОтборВидОтчета.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			ОтборВидОтчета.ПравоеЗначение = СписокОтбораВидОтчета;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Форма.ПериодОтчета) Тогда
		ОтборДинамическогоСписка.Элементы.Удалить(ОтборДатаНачала);
		ОтборДинамическогоСписка.Элементы.Удалить(ОтборДатаОкончания1);
		ОтборДинамическогоСписка.Элементы.Удалить(ОтборДатаОкончания2);
	Иначе
		Если ЗначениеЗаполнено(Форма.НачалоПериода) Тогда
			ОтборДатаНачала.Использование      = Истина;
			ОтборДатаНачала.ВидСравнения       = ?(Форма.ТолькоУказанныйПериод, ВидСравненияКомпоновкиДанных.Равно, ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
			ОтборДатаНачала.ПравоеЗначение     = ?(Форма.ТолькоУказанныйПериод, Форма.НачалоПериода, НачалоГода(Форма.НачалоПериода));
			
			ОтборДатаОкончания1.Использование  = НЕ Форма.ТолькоУказанныйПериод;
			ОтборДатаОкончания1.ВидСравнения   = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
			ОтборДатаОкончания1.ПравоеЗначение = Форма.НачалоПериода;
		Иначе
			ОтборДинамическогоСписка.Элементы.Удалить(ОтборДатаНачала);
			Если Форма.ТолькоУказанныйПериод Тогда
				ОтборДатаОкончания1.Использование = Ложь;
			Иначе
				ОтборДинамическогоСписка.Элементы.Удалить(ОтборДатаОкончания1);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Форма.КонецПериода) Тогда
			Если Форма.ТолькоУказанныйПериод Тогда
				ОтборДатаОкончания1.Использование  = Истина;
				ОтборДатаОкончания1.ВидСравнения   = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
				ОтборДатаОкончания1.ПравоеЗначение = НачалоДня(Форма.КонецПериода);
			КонецЕсли;
			
			ОтборДатаОкончания2.Использование  = Истина;
			ОтборДатаОкончания2.ВидСравнения   = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
			ОтборДатаОкончания2.ПравоеЗначение = Форма.КонецПериода;
		Иначе
			ОтборДинамическогоСписка.Элементы.Удалить(ОтборДатаОкончания2);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ОтборДатаОкончания1) = Тип("ЭлементОтбораКомпоновкиДанных")
		И НЕ ОтборДатаОкончания1.Использование Тогда
		ОтборДинамическогоСписка.Элементы.Удалить(ОтборДатаОкончания1);
	КонецЕсли;
	
	// Добавляем пользовательские элементы отбора "Вид отчета".
	Для Каждого ДобавляемыйЭлементОтбораВидОтчета Из ДобавляемыеЭлементыОтбораВидОтчета Цикл
		НовыйЭлементОтбора = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЗаполнитьЗначенияСвойств(НовыйЭлементОтбора, ДобавляемыйЭлементОтбораВидОтчета, , "ИдентификаторПользовательскойНастройки");
		// Сохраним идентификатор пользовательской настройки элемента в его пользовательском представлении.
		НовыйЭлементОтбора.ПредставлениеПользовательскойНастройки = ДобавляемыйЭлементОтбораВидОтчета.ИдентификаторПользовательскойНастройки;
	КонецЦикла;
	
	// Добавляем пользовательские элементы отбора.
	Для Каждого ДобавляемыйЭлементОтбора Из ДобавляемыеЭлементыОтбора Цикл
		НовыйЭлементОтбора = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЗаполнитьЗначенияСвойств(НовыйЭлементОтбора, ДобавляемыйЭлементОтбора, , "ИдентификаторПользовательскойНастройки");
		// Сохраним идентификатор пользовательской настройки элемента в его пользовательском представлении.
		НовыйЭлементОтбора.ПредставлениеПользовательскойНастройки = ДобавляемыйЭлементОтбора.ИдентификаторПользовательскойНастройки;
	КонецЦикла;
	
	УстановитьДопОтборы(Форма, ОтборДинамическогоСписка);

	ОтборДинамическогоСписка.ИдентификаторПользовательскойНастройки = Строка(Новый УникальныйИдентификатор);
	
	// Назначаем идентификаторы пользовательских настроек для элементов установленного отбора (быстрые отборы в форме).
	Для Каждого ЭлементПользовательскойНастройки Из Форма.Отчеты.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(ЭлементПользовательскойНастройки) = Тип("ОтборКомпоновкиДанных")
			И ЭлементПользовательскойНастройки.ИдентификаторПользовательскойНастройки
				= ОтборДинамическогоСписка.ИдентификаторПользовательскойНастройки Тогда
			Для Каждого ЭлементОтбора Из ЭлементПользовательскойНастройки.Элементы Цикл
				Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
					Если ЗначениеЗаполнено(ЭлементОтбора.ПредставлениеПользовательскойНастройки) Тогда
						ЭлементОтбора.ИдентификаторПользовательскойНастройки = ЭлементОтбора.ПредставлениеПользовательскойНастройки;
						ДоступноеПолеОтбора = ЭлементПользовательскойНастройки.ДоступныеПоляОтбора.НайтиПоле(ЭлементОтбора.ЛевоеЗначение);
						ЭлементОтбора.ПредставлениеПользовательскойНастройки =
							?(ДоступноеПолеОтбора = Неопределено, Строка(ЭлементОтбора.ЛевоеЗначение), ДоступноеПолеОтбора.Заголовок);
					КонецЕсли
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	//Очищаем для удаления элементов пользовательского отбора при установке стандартных настроек.
	ОтборДинамическогоСписка.Элементы.Очистить();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСортировкуПоУмолчанию(Форма)
	
	ПорядокДинамическогоСписка = Форма.Отчеты.КомпоновщикНастроек.Настройки.Порядок;
	
	ПользовательскиеНастройкиДинамическогоСписка = Форма.Отчеты.КомпоновщикНастроек.ПользовательскиеНастройки;
	
	ПользовательскийПорядокДинамическогоСписка = ПользовательскиеНастройкиДинамическогоСписка.Элементы.Найти(
		ПорядокДинамическогоСписка.ИдентификаторПользовательскойНастройки);
		
	Если ПользовательскийПорядокДинамическогоСписка <> Неопределено
		И ПорядокДинамическогоСписка.Элементы.Количество() = ПользовательскийПорядокДинамическогоСписка.Элементы.Количество() Тогда
		
		Для Каждого ЭлементПорядка Из ПользовательскийПорядокДинамическогоСписка.Элементы Цикл
			Если ЭлементПорядка.Поле <> ПорядокДинамическогоСписка.Элементы[ПользовательскийПорядокДинамическогоСписка.Элементы.Индекс(ЭлементПорядка)].Поле Тогда
				ПорядокДинамическогоСписка.ИдентификаторПользовательскойНастройки = Строка(Новый УникальныйИдентификатор);
				Возврат;
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ЭлементПорядка Из ПользовательскийПорядокДинамическогоСписка.Элементы Цикл
			Если ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр Тогда
				ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;
			Иначе
				ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
			КонецЕсли;
			ЭлементПорядка.Использование = Истина;
		КонецЦикла;
		
	Иначе
		
		ПорядокДинамическогоСписка.ИдентификаторПользовательскойНастройки = Строка(Новый УникальныйИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВостановитьНастройкиОтборовИСортировкиОтчетов(Форма, СохраненныеПользовательскиеНастройки)
	
	Для Каждого ЭлементПользовательскойНастройки Из СохраненныеПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(ЭлементПользовательскойНастройки) = Тип("ОтборКомпоновкиДанных") Тогда
			ЭлементПользовательскойНастройки.ИдентификаторПользовательскойНастройки =
				Форма.Отчеты.КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки;
		ИначеЕсли ТипЗнч(ЭлементПользовательскойНастройки) = Тип("ПорядокКомпоновкиДанных") Тогда
			ЭлементПользовательскойНастройки.ИдентификаторПользовательскойНастройки =
				Форма.Отчеты.КомпоновщикНастроек.Настройки.Порядок.ИдентификаторПользовательскойНастройки;
		КонецЕсли;
	КонецЦикла;
	
	Форма.Отчеты.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(СохраненныеПользовательскиеНастройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВсеОрганизации()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Организации.Ссылка КАК Организация
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ПометкаУдаления = ЛОЖЬ";

	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаОрганизаций = РезультатЗапроса.Выгрузить();
	
	МассивОрганизаций = ТаблицаОрганизаций.ВыгрузитьКолонку("Организация");
	
	Возврат МассивОрганизаций;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОсуществитьОбменПоВыбраннымОрганизациям()

	Если НомерОбъекта + 1 > ВыбранныеОрганизации.Количество() Тогда
		
		УправлениеЭУ(ЭтотОбъект);
		ДлительнаяОтправкаКлиент.ОповеститьОЗавершенииОбмена(); // из формы 1С-Отчетность
		
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьНадписьВремяОбновления", 0.1, Истина);
		
	Иначе
		
		// Получим очередную организацию для обмена
		ОрганизацияДляОбмена = ВыбранныеОрганизации[НомерОбъекта];
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОсуществитьОбменПоВыбраннымОрганизациямЗавершение", 
			ЭтотОбъект);
			
		//КонтекстЭДО инициализируется в ОбновитьДанные
		КонтекстЭДОКлиент.ОсуществитьОбменПоОрганизации(
			ЭтотОбъект, 
			ОрганизацияДляОбмена, 
			ОписаниеОповещения);
			
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОсуществитьОбменПоВыбраннымОрганизациямЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	НомерОбъекта = НомерОбъекта + 1;
	ПодключитьОбработчикОжидания("Подключаемый_ОсуществитьОбменПоВыбраннымОрганизациям", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСверку(ВидУслуги)

	Если ТипЗнч(ВидУслуги) = Тип("ПеречислениеСсылка.ВидыУслугПриИОН") Тогда
		// Это запрос ИОН
		ФормаДокумента = "Документ.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика.Форма.ФормаДокумента";
		
	Иначе
		// Это запрос ИОС
		ФормаДокумента = "Документ.ЗапросНаИнформационноеОбслуживаниеСтрахователя.Форма.ФормаДокумента";
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("ВидУслуги, Организация", ВидУслуги, Организация));
	ОткрытьФорму(ФормаДокумента, ПараметрыФормы, ЭтотОбъект, Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьОбъект(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.Ссылка;
		Если ЗначениеЗаполнено(Ссылка) Тогда
			ПоказатьЗначение(,Ссылка);
			Оповестить("Пометка прочтенным", , Ссылка);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеОбъект(Таблица)
	
	ТекущиеДанные = Таблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Таблица = Элементы.Отчеты Тогда 
		Ссылка = ТекущиеДанные.РегламентированныйОтчет;
	Иначе
		Ссылка = ТекущиеДанные.Ссылка;
	КонецЕсли;
	
	ПометитьНаУдалениеОбъектНаСервере(Ссылка);
	
	// Обновление списка "точечно"
	ПараметрОповещения = Новый Структура(); 
	ПараметрОповещения.Вставить("Ссылка", Ссылка);
	ПараметрОповещения.Вставить("Организация", ТекущиеДанные.Организация);
	Оповестить("Изменение пометки удаления объекта", ПараметрОповещения, Ссылка);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПометитьНаУдалениеОбъектНаСервере(Ссылка)
	
	УдаляемыйОбъект = Ссылка.ПолучитьОбъект();
	УдаляемыйОбъект.УстановитьПометкуУдаления(НЕ Ссылка.ПометкаУдаления);
	
КонецПроцедуры

&НаСервере
Функция ЕщеНеОтправлялся(Ссылка)
	
	Возврат ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ОтчетЕщеНеОтправлялся(Ссылка);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВыбранРазделФормы(Форма)
		
	Если Форма.Элементы.Разделы.ТекущаяСтраница = Неопределено Тогда 
		Возврат Форма.Элементы["ГиперссылкаОтчеты"];
	КонецЕсли;
	
	ИмяТекущейСтраницы = Форма.Элементы.Разделы.ТекущаяСтраница.Имя;
	ИмяРаздела = СтрЗаменить(ИмяТекущейСтраницы, "Страница", "Гиперссылка");
	ЭлементРаздела = Форма.Элементы.Найти(ИмяРаздела);
	Если ЭлементРаздела <> Неопределено Тогда 
		Возврат ЭлементРаздела;
	КонецЕсли;
	
	Возврат Форма.Элементы["ГиперссылкаОтчеты"];
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекущийРазделФормы(Форма)
	
	Результат = Неопределено;
	
	Для каждого Раздел Из Разделы(Форма) Цикл
		
		Гиперссылка = Форма.Элементы["Гиперссылка" + Раздел];
		
		Если Гиперссылка.ЦветФона = Форма.Желтый Тогда
			Результат = Гиперссылка;
		КонецЕсли;
	
	КонецЦикла; 
	
	Если Результат = Неопределено Тогда 
		Возврат ВыбранРазделФормы(Форма);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекущаяТаблица(Форма)
	
	ТекущийРаздел 		= ТекущийРазделФормы(Форма);
	ИмяТекущейТаблицы 	= СтрЗаменить(ТекущийРаздел.Имя, "Гиперссылка", ""); 
		
	Если ИмяТекущейТаблицы = "Входящие" Тогда //Есть вложенные таблицы/вкладки
		АктивнаСтраницаРассылки = Форма.Элементы.ГруппаКатегориииВх.ТекущаяСтраница = 
			Форма.Элементы.СтраницаКатегорияРассылки;
		Если АктивнаСтраницаРассылки Тогда 
			ИмяТекущейТаблицы = ИмяТекущейТаблицы + "Рассылки";
		КонецЕсли;
	КонецЕсли;
	
	Возврат Форма.Элементы[ИмяТекущейТаблицы];
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекущаяСтраница(Форма)
	
	ТекущийРаздел 		= ТекущийРазделФормы(Форма);
	ИмяТекущейТаблицы 	= СтрЗаменить(ТекущийРаздел.Имя, "Гиперссылка", ""); 
	
	Возврат ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность." + ИмяТекущейТаблицы);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборыПоПрочимТаблицам(Форма, ДинамическийСписок, ВидОбъектаДляОтбора = Неопределено)
	
	ДинамическийСписок.Отбор.Элементы.Очистить();
	ГруппаОтбора = ДинамическийСписок.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;

	// отбор по организации
	Если ЗначениеЗаполнено(Форма.Организация) Тогда
		
		ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ПравоеЗначение = Форма.Организация;
		
	КонецЕсли;

	// отбор по виду сверки
	Если ЗначениеЗаполнено(ВидОбъектаДляОтбора) Тогда
		
		ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Наименование");
		Если ТекущаяСтраница(Форма) = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Уведомления")
				И ТипЗнч(ВидОбъектаДляОтбора) = Тип("Строка")
				И СтрНайти(ВидОбъектаДляОтбора, " ") = 0 Тогда 
				
			ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Содержит;
		Иначе
			ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		КонецЕсли;
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ПравоеЗначение = ВидОбъектаДляОтбора;
		
	КонецЕсли;
	
	УстановитьДопОтборы(Форма, ДинамическийСписок.Отбор);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДопОтборы(Форма, ОтборДинамическогоСписка)
	
	Если ЗначениеЗаполнено(Форма.ОтборОрган) Тогда
		
		ЭлементОтбора = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ВидКонтролирующегоОргана");
		ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование  = Истина;
		ЭлементОтбора.ПравоеЗначение = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС");
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидУведомленияОСпецрежимахНалогообложения(Ссылка)

	Возврат Ссылка.ВидУведомления;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ДействияПриИзмененииОтборов(Форма)
	
	УстановитьОтборыПоОтчетам(Форма);
	УстановитьОтборыПоПрочимТаблицам(Форма, Форма.Уведомления, Форма.ПредставлениеВидаУведомления);
	УстановитьОтборыПоПрочимТаблицам(Форма, Форма.Письма);
	УстановитьОтборыПоПрочимТаблицам(Форма, Форма.Сверки, Форма.ВидСверки);
	УстановитьОтборыПоПрочимТаблицам(Форма, Форма.ЕГРЮЛ);
	УстановитьОтборыПоПрочимТаблицам(Форма, Форма.Входящие);
	
	ДополнитьОтборПоТипуВходящих(Форма.Входящие, 
		Форма.Элементы.ГруппаКатегориииВх.ТекущаяСтраница, 
		Форма.Элементы.СтраницаКатегорияРассылки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораВидаУведомления(Результат, Параметры) Экспорт
	Элементы.ОтборВидУведомления.СписокВыбора.Очистить();
	Элементы.ОтборВидУведомления.РежимВыбораИзСписка = Ложь;
	Элементы.ОтборВидУведомления.ОбновлениеТекстаРедактирования = ОбновлениеТекстаРедактирования.Авто;
	Если Результат <> Неопределено Тогда
		Если ТипЗнч(Результат) = Тип("ПеречислениеСсылка.ВидыУведомленийОСпецрежимахНалогообложения") Тогда
			
			ВидУведомления 					= Результат;
			ПредставлениеВидаУведомления 	= Строка(Результат);
			Если ВидУведомления =
				ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ЭлементДляВыпускаНовыхПатчей") Тогда 
				
				УведомлениеОСпецрежимахНалогообложенияКлиент.ЗаменитьПредставлениеСлужебногоУведомления(ПредставлениеВидаУведомления);
			КонецЕсли;
		Иначе
			ВидУведомления 					= Результат.Тип;
			ПредставлениеВидаУведомления 	= Результат.Наименование;
		КонецЕсли;
	КонецЕсли;
	
	ДействияПриИзмененииОтборов(ЭтотОбъект);

КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьXMLНаСервере(УникальныйИдентификатор, Объект)
	ДокОбъект = Объект.ПолучитьОбъект();
	Возврат ДокОбъект.ВыгрузитьДокумент(УникальныйИдентификатор);
КонецФункции

&НаСервереБезКонтекста
Функция КоличествоНепрочитанныхОбъектов(ОтборОрганизацияВходящие)
	
	ДетализацияВходящих = Новый Структура("ВходящиеПисьма, ВходящиеРассылки", 0, 0);
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЖурналОтчетовСтатусы.Ссылка) КАК КоличествоНепрочтенных,
		|	ЗНАЧЕНИЕ(Перечисление.СтраницыЖурналаОтчетность.Отчеты) КАК СтраницаЖурнала
		|ИЗ
		|	РегистрСведений.ЖурналОтчетовСтатусы КАК ЖурналОтчетовСтатусы
		|ГДЕ
		|	ЖурналОтчетовСтатусы.Непрочтенное = ИСТИНА
		|	И НЕ ЖурналОтчетовСтатусы.Скрыт
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЖурналОтправокВКонтролирующиеОрганы.Ссылка),
		|	ЖурналОтправокВКонтролирующиеОрганы.СтраницаЖурнала
		|ИЗ
		|	РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
		|ГДЕ
		|	ЖурналОтправокВКонтролирующиеОрганы.Непрочтенное = ИСТИНА
		|	И НЕ ЖурналОтправокВКонтролирующиеОрганы.Скрыт
		|
		|СГРУППИРОВАТЬ ПО
		|	ЖурналОтправокВКонтролирующиеОрганы.СтраницаЖурнала";
		
		
	ТаблицаНепрочтенных = Запрос.Выполнить().Выгрузить();
	МассивНепрочтенных = ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаНепрочтенных);
	
	Стр = ТаблицаНепрочтенных.Найти(Перечисления.СтраницыЖурналаОтчетность.Входящие, "СтраницаЖурнала");
	Если (Стр <> Неопределено) И (Стр.КоличествоНепрочтенных > 0) Тогда 
		ЗапросВходящих = Новый Запрос;
		
		ЗапросВходящих.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА ЦО.Тип ЕСТЬ NULL
		|					ТОГДА 0
		|				КОГДА ЦО.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыЦикловОбмена.ПустаяСсылка)
		|					ТОГДА 0
		|				КОГДА ЦО.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыЦикловОбмена.Рассылка)
		|						ИЛИ ЦО.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыЦикловОбмена.РассылкаГрупповая)
		|						ИЛИ ЦО.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыЦикловОбмена.РассылкаПФР)
		|						ИЛИ ЦО.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыЦикловОбмена.РассылкаФСГС)
		|						ИЛИ ЦО.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыЦикловОбмена.РассылкаШаблоновФСГС)
		|					ТОГДА 1
		|				ИНАЧЕ 0
		|			КОНЕЦ), 0) КАК КолСтраницаВхРассылки,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЖурналОтправокВКонтролирующиеОрганы.Ссылка) КАК КолСтраницаВхВсего,
		|	ЖурналОтправокВКонтролирующиеОрганы.СтраницаЖурнала КАК СтраницаЖурнала
		|ПОМЕСТИТЬ Количества
		|ИЗ
		|	РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЦиклыОбмена КАК ЦО
		|		ПО ЖурналОтправокВКонтролирующиеОрганы.Ссылка = ЦО.Предмет
		|ГДЕ
		|	ЖурналОтправокВКонтролирующиеОрганы.Непрочтенное = ИСТИНА
		|	И НЕ ЖурналОтправокВКонтролирующиеОрганы.Скрыт
		|	И ЖурналОтправокВКонтролирующиеОрганы.СтраницаЖурнала = ЗНАЧЕНИЕ(Перечисление.СтраницыЖурналаОтчетность.Входящие)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЖурналОтправокВКонтролирующиеОрганы.СтраницаЖурнала
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Количества.КолСтраницаВхВсего - Количества.КолСтраницаВхРассылки КАК КолСтраницаВхПисьма,
		|	Количества.КолСтраницаВхРассылки КАК КолСтраницаВхРассылки,
		|	Количества.КолСтраницаВхВсего КАК КолСтраницаВхВсего
		|ИЗ
		|	Количества КАК Количества";
		
		Если ЗначениеЗаполнено(ОтборОрганизацияВходящие) Тогда 
			УсловиеОтбораПоОрг = "И ЖурналОтправокВКонтролирующиеОрганы.Организация = &ОрганизацияОтбор";
			ЗапросВходящих.Текст = СтрЗаменить(ЗапросВходящих.Текст, "//%ОтборОрганизация%", УсловиеОтбораПоОрг);
			ЗапросВходящих.Параметры.Вставить("ОрганизацияОтбор", ОтборОрганизацияВходящие);			
		КонецЕсли;
		
		ТаблицаВходящихСРазбивкой = ЗапросВходящих.Выполнить().Выгрузить();
		Если ТаблицаВходящихСРазбивкой.Количество() > 0 Тогда 
			ПерваяЗапись = ТаблицаВходящихСРазбивкой[0];
			ДетализацияВходящих.ВходящиеПисьма = ПерваяЗапись.КолСтраницаВхПисьма;
			ДетализацияВходящих.ВходящиеРассылки = ПерваяЗапись.КолСтраницаВхРассылки;
		КонецЕсли;
		
	КонецЕсли;

	Для Каждого Элемент Из МассивНепрочтенных Цикл 
		Элемент.СтраницаЖурнала = ИмяСтраницы(Элемент.СтраницаЖурнала);
		Если Элемент.СтраницаЖурнала = "Входящие" Тогда 
			Элемент.Вставить("ДетализацияВходящих", ДетализацияВходящих);
		КонецЕсли;
	КонецЦикла;

	Возврат МассивНепрочтенных;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьКоличествоНепрочтенныхОбъектов(Форма)
	Перем ЗаголовокКатегории;
	
	// Сбрасываем количества во всех заголовках.
	Для каждого Раздел Из Разделы(Форма, Истина) Цикл
		Форма.Элементы["Гиперссылка" + Раздел].Заголовок = Раздел;
	КонецЦикла;
	
	// Сбрасываем количества в заголовках вложенных закладок
	ЗаголовокКатегории = Форма.Элементы["СтраницаКатегорияНесортированные"].Заголовок;
	Если СтрНайти(ЗаголовокКатегории, "(") > 0 Тогда 
		Форма.Элементы["СтраницаКатегорияНесортированные"].Заголовок = Лев(ЗаголовокКатегории, СтрНайти(ЗаголовокКатегории, "(") - 2);
	КонецЕсли;
	ЗаголовокКатегории = Форма.Элементы["СтраницаКатегорияРассылки"].Заголовок;
	Если СтрНайти(ЗаголовокКатегории, "(") > 0 Тогда 
		Форма.Элементы["СтраницаКатегорияРассылки"].Заголовок = Лев(ЗаголовокКатегории, СтрНайти(ЗаголовокКатегории, "(") - 2);
	КонецЕсли;
	
	// Устанавливаем количества только там, где есть непрочтенные.
	НепрочтенныеОбъектыПоРазделам = КоличествоНепрочитанныхОбъектов(Форма.Организация);
	
	Для каждого НепрочтенныеОбъектыРаздела Из НепрочтенныеОбъектыПоРазделам Цикл
		
		КоличествоНепрочитанныхОбъектов = НепрочтенныеОбъектыРаздела.КоличествоНепрочтенных;
		СтраницаЖурнала 				= НепрочтенныеОбъектыРаздела.СтраницаЖурнала;
		
		Форма.Элементы["Гиперссылка" + СтраницаЖурнала].Заголовок = 
				СтраницаЖурнала + ПредставлениеКоличества(КоличествоНепрочитанныхОбъектов);
				
		Если СтраницаЖурнала = "Входящие" Тогда 
			Форма.Элементы["СтраницаКатегорияНесортированные"].Заголовок = 
				Форма.Элементы["СтраницаКатегорияНесортированные"].Заголовок + 
					ПредставлениеКоличества(НепрочтенныеОбъектыРаздела.ДетализацияВходящих.ВходящиеПисьма);
			Форма.Элементы["СтраницаКатегорияРассылки"].Заголовок = 
				Форма.Элементы["СтраницаКатегорияРассылки"].Заголовок + 
					ПредставлениеКоличества(НепрочтенныеОбъектыРаздела.ДетализацияВходящих.ВходящиеРассылки);
		КонецЕсли;
			
	КонецЦикла; 

КонецПроцедуры

&НаКлиенте
Процедура ПечатьОтчетности(ВидПечати)
	
	ТекущиеДанные 	= Элементы.Отчеты.ТекущиеДанные;
	Закладка	 	= Элементы.Отчеты;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.РегламентированныйОтчет;
		
		Если ВидПечати = "ПечатьОтчет" Тогда
			
			Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
				// Печать электронного представления
				Ссылка 				= ТекущиеДанные.РегламентированныйОтчет;
				НаименованиеОтчета 	= ТекущиеДанные.НаименованиеОтчета;
				
				ПечатьЭлектронногоПредставления(Ссылка, НаименованиеОтчета);
				
			ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
				
				// Печать регламентированного отчета
				Ссылка             = ТекущиеДанные.РегламентированныйОтчет;
				НаименованиеОтчета = ТекущиеДанные.НаименованиеОтчета;
				
				ОписаниеОтчета = ОписаниеРегОтчета(Ссылка);
		
				Если ОбработаноДействиеДляАлкоОтчета(Ссылка, ОписаниеОтчета, "Печать", ВидПечати) Тогда
				    Возврат;		
				КонецЕсли;
				
				Если НаименованиеОтчета = "Доходы, выплаченные иностранным организациям" Тогда
					ОписаниеДохИно = ОписаниеРегОтчета(Ссылка);
					Если (ОписаниеДохИно.ВыбраннаяФорма = "ФормаОтчета2020Кв1"
					  ИЛИ ОписаниеДохИно.ВыбраннаяФорма = "ФормаОтчета2016Кв1")
						И ОписаниеДохИно.Свойство("ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении") Тогда
						Если НЕ ОписаниеДохИно.ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении Тогда
							Если ОписаниеДохИно.Свойство("ТекстСообщения") Тогда
								Сообщение = Новый СообщениеПользователю;
								Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
								"ru='%1 Печать прервана.'"), ОписаниеДохИно.ТекстСообщения);
								Сообщение.Сообщить();
							КонецЕсли;
							Возврат;
						КонецЕсли;
					КонецЕсли;
				ИначеЕсли НаименованиеОтчета = "Уведомление об участии в международной группе компаний"
					ИЛИ НаименованиеОтчета = "Страновой отчет" Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
					"ru='Для отчета ""%1"" не предусмотрена печать бланка формы без двухмерного штрихкода PDF417!'"),
					НаименованиеОтчета);
					Сообщение.Сообщить();
					Возврат;
				КонецЕсли;
				
				ДополнительныеПараметры = Новый Структура();
				ДополнительныеПараметры.Вставить("НаименованиеОтчета", НаименованиеОтчета);
				ДополнительныеПараметры.Вставить("Ссылка", Ссылка);
				
				ОписаниеОповещения = Новый ОписаниеОповещения("ПечатьОтчетностиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
				
				РегламентированнаяОтчетностьКлиент.ПередПечатьюРегламентированногоОтчета(ОписаниеОповещения, Ссылка);
				Возврат;
				
			Иначе
				// Печать объекта, не входящего в БРО
				СведенияПоОбъекту = РегламентированнаяОтчетностьВызовСервера.СведенияПоОбъектуНеВходящемуВБРО(Ссылка);
				Если СведенияПоОбъекту <> Неопределено Тогда
					НапечататьОбъектНеВходящийВБРО(Ссылка, СведенияПоОбъекту);
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ВидПечати = "ПечатьОтчетШтрих" Тогда
			
			Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
				// Печать электронного представления
				Ссылка 				= ТекущиеДанные.РегламентированныйОтчет;
				НаименованиеОтчета 	= ТекущиеДанные.НаименованиеОтчета;
				
				ПечатьЭлектронногоПредставления(Ссылка, НаименованиеОтчета);
			ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
				// Печать регламентированного отчета со штрихкодом PDF417
				Ссылка             = ТекущиеДанные.РегламентированныйОтчет;
				НаименованиеОтчета = ТекущиеДанные.НаименованиеОтчета;
				
				Если ВыполнениеКомандыОтменено(?(ТекущиеДанные.Свойство("НаименованиеОтчета"), ТекущиеДанные.НаименованиеОтчета, ТекущиеДанные.Наименование), ТекущиеДанные.Организация) Тогда
					Возврат;
				КонецЕсли;
				
				ПечатьРегламентированногоОтчетаШтрих(Ссылка, НаименованиеОтчета);
			Иначе
				
				// Печать объекта, не входящего в БРО
				ПредупредитьОбОграничениях(Закладка, НСтр("ru = 'Печать со штрихкодом PDF417 не предусмотрена'"));
			
			КонецЕсли;
		
		ИначеЕсли ВидПечати = "ПечатьПротоколы" Тогда
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ВыполнениеКомандыОтменено(НаименованиеОтчета, Организация, НаименованиеКоманды = "")
	
	Возврат РегламентированнаяОтчетностьКлиент.ВыполнениеКомандыОтменено(ЭтотОбъект, НаименованиеОтчета, Организация, НаименованиеКоманды);
	
КонецФункции

&НаКлиенте
Процедура ПечатьОтчетностиЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПечатьРегламентированногоОтчета(ДополнительныеПараметры.Ссылка, ДополнительныеПараметры.НаименованиеОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЭлектронногоПредставленияПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ПечатьЭлектронногоПредставления(
		ПараметрыОповещения.Ссылка,
		ПараметрыОповещения.Наименование
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЭлектронногоПредставления(Ссылка, Наименование)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Ссылка", Ссылка);
		ПараметрыОповещения.Вставить("Наименование", Наименование);
		Оповещение = Новый ОписаниеОповещения("ПечатьЭлектронногоПредставленияПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
 
	КонтекстЭДОКлиент.ПечатьЭлектронногоПредставления(Ссылка, , Наименование);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЗаявленияПоЭлДокументооборотуСПФРПредварительное(Результат, Ссылка) Экспорт
	
	ПечатьЗаявленияПоЭлДокументооборотуСПФР(Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЗаявленияПоЭлДокументооборотуСПФР(Ссылка)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		
		Оповещение = Новый ОписаниеОповещения(
			"ПечатьЗаявленияПоЭлДокументооборотуСПФРПредварительное", 
			ЭтотОбъект, 
			Ссылка);
			
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
		
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
 
	КонтекстЭДОКлиент.ПечатьЗаявленияПоЭлДокументооборотуСПФР(Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьРегламентированногоОтчетаПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ПечатьРегламентированногоОтчета(
		ПараметрыОповещения.Ссылка,
		ПараметрыОповещения.Наименование
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьРегламентированногоОтчета(Ссылка, Наименование)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Ссылка", Ссылка);
		ПараметрыОповещения.Вставить("Наименование", Наименование);
		Оповещение = Новый ОписаниеОповещения("ПечатьРегламентированногоОтчетаПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Истина;
	
	Если Наименование = "РСВ-1 ПФР" Тогда
		
		ОписаниеРСВ_1 = ОписаниеРегОтчета(Ссылка);
		Если ОписаниеРСВ_1.ДатаКонцаПериодаОтчета >= '20140101' Тогда
			Если НЕ (ПерсонифицированныйУчетВедетсяВоВнешнейПрограмме И ОписаниеРСВ_1.ОтчетСтарогоОбразца_20141101) Тогда
				РегламентированнаяОтчетностьКлиент.ПриПечатиРСВ_1ИзЖурналаРеглОтчетов(Ссылка, ОписаниеРСВ_1, СтандартнаяОбработка);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если (Наименование = "Статистика: Форма учета перемещения товаров"
		ИЛИ Наименование = "Статистическая форма учета перемещения товаров") И КонтекстЭДОКлиент <> Неопределено Тогда
		
		ДокументЗаявлениеОВвозеТоваров = Неопределено;
		ИмяДокументаЗаявлениеОВвозеТоваров = Неопределено;
		РезультатПолученияЗаявленияОВвозеТоваров = КонтекстЭДОКлиент.ПопытатьсяПолучитьЗаявлениеОВвозеТоваровИзСтатформы(
			Ссылка, ДокументЗаявлениеОВвозеТоваров, ИмяДокументаЗаявлениеОВвозеТоваров);
		Если РезультатПолученияЗаявленияОВвозеТоваров = Неопределено Тогда
			Возврат;
		ИначеЕсли РезультатПолученияЗаявленияОВвозеТоваров Тогда
			МассивОбъектов = Новый Массив;
			МассивОбъектов.Добавить(ДокументЗаявлениеОВвозеТоваров);
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ." + ИмяДокументаЗаявлениеОВвозеТоваров, "СтатистическаяФормаУчетаПеремещенияТоваров", МассивОбъектов, ЭтотОбъект);
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтандартнаяОбработка Тогда
		
		СписокОтчетов = Новый СписокЗначений;
		СписокОтчетов.Добавить(Ссылка);
		
		РегламентированнаяОтчетностьКлиент.НапечататьОтчеты(СписокОтчетов, "ПоказатьБланк");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьРегламентированногоОтчетаШтрих(Ссылка, Наименование)
	
	ФормаОтчета = Неопределено; // Обход ошибки в платформе.
	
	ПредставлениеДокументаРеглОтч = "";
	
	ФормаОтчета = РегламентированнаяОтчетностьКлиент.ИнициализацияОтчета(Ссылка, ПредставлениеДокументаРеглОтч);
	Если ФормаОтчета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Наименование = "Налог на игорный бизнес" Тогда
		Если ОписаниеРегОтчета(Ссылка).ВыбраннаяФорма = "ФормаОтчета2021Кв4" Тогда
			ПоказатьПредупреждение(, НСтр(
			"ru='Печать с двухмерным штрихкодом PDF417 формы
			|отчета, утвержденной приказом ФНС России
			|от 05.07.2021 № ЕД-7-3/634@, будет
			|реализована в будущих версиях программы'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Наименование = "Движение средств по счету в банке за пределами РФ" Тогда
		Если ОписаниеРегОтчета(Ссылка).ВыбраннаяФорма = "ФормаОтчета2021Кв4" Тогда
			ПоказатьПредупреждение(, НСтр(
			"ru='Печать со штрихкодом PDF417 формы отчета
			|в ред. постановления Правительства РФ
			|от 27.09.2021 № 1618 будет реализована
			|в будущих версиях программы'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Наименование = "Декларация по налогу на имущество" Тогда
		Если ОписаниеРегОтчета(Ссылка).ВыбраннаяФорма = "ФормаОтчета2022Кв1" Тогда
			ПоказатьПредупреждение(, НСтр(
			"ru='Печать со штрихкодом PDF417 формы
			|отчета в редакции приказа ФНС России
			|от 09.08.2021 № ЕД-7-21/739@ будет
			|реализована в будущих версиях программы'"));
			Возврат;
		ИначеЕсли ОписаниеРегОтчета(Ссылка).ВыбраннаяФорма = "ФормаОтчета2021Кв4" Тогда
			ПоказатьПредупреждение(, НСтр(
			"ru='Печать со штрихкодом PDF417 формы
			|отчета в редакции приказа ФНС России
			|от 18.06.2021 № ЕД-7-21/574@ будет
			|реализована в будущих версиях программы'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Наименование = "Декларация по УСН" Тогда
		ОписаниеУСН = ОписаниеРегОтчета(Ссылка);
		Если ОписаниеУСН.ВыбраннаяФорма = "ФормаОтчета2014Кв1" И ОписаниеУСН.РекомендованнаяФорма Тогда
			ПоказатьПредупреждение(, НСтр("ru='Печать с двухмерным штрихкодом PDF417 формы, рекомендованной письмом ФНС от 20.05.2015 № ГД-4-3/8533@, не предусмотрена.'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Наименование = "Доходы, выплаченные иностранным организациям" Тогда
		ОписаниеДохИно = ОписаниеРегОтчета(Ссылка);
		Если (ОписаниеДохИно.ВыбраннаяФорма = "ФормаОтчета2020Кв1"
		  ИЛИ ОписаниеДохИно.ВыбраннаяФорма = "ФормаОтчета2016Кв1")
			И ОписаниеДохИно.Свойство("ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении") Тогда
			Если НЕ ОписаниеДохИно.ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении Тогда
				Если ОписаниеДохИно.Свойство("ТекстСообщения") Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
					"ru='%1 Печать формы с двухмерным штрихкодом PDF417 прервана.'"), ОписаниеДохИно.ТекстСообщения);
					Сообщение.Сообщить();
				КонецЕсли;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Наименование = "Сведения о доле доходов от туристско-рекреационной деятельности" Тогда
		Если ОписаниеРегОтчета(Ссылка).ВыбраннаяФорма = "ФормаОтчета2018Кв1" Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
			"ru='Выгрузка, проверка выгрузки, проверка в интернете, отправка в электронном виде, печать с двухмерным
			|штрихкодом PDF417 формы отчета ""%1"",
			|рекомендованной письмом ФНС России от 16.01.2019 № СД-4-3/441@, не предусмотрены'"), Наименование);
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Наименование = "Декларация 3-НДФЛ" Тогда
		Описание3НДФЛ = ОписаниеРегОтчета(Ссылка);
		Если Не Описание3НДФЛ.ДоступнаПечатьPDF417 Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Печать формы со штрихкодом PDF417 будет реализована в будущих версиях программы.'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ФормаОтчета.СтруктураРеквизитовФормы.Свойство("ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417")
	   И СтрНайти(ФормаОтчета.ИмяФормы, "РегламентированныйОтчетСтатистика") = 0 Тогда
		РегламентированнаяОтчетностьКлиент.ВывестиМашиночитаемуюФорму(ФормаОтчета, "ПоказатьСДвухмернымШтрихкодомPDF417");
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Для отчета """ + Наименование + """ не предусмотрена печать бланка формы с двухмерным штрихкодом PDF417!'");
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаЭлектронногоПредставленияПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ВыгрузкаЭлектронногоПредставления(
		ПараметрыОповещения.Ссылка,
		ПараметрыОповещения.НаименованиеОтчета
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаЭлектронногоПредставления(Ссылка, НаименованиеОтчета)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Ссылка", Ссылка);
		ПараметрыОповещения.Вставить("НаименованиеОтчета", НаименованиеОтчета);
		Оповещение = Новый ОписаниеОповещения("ВыгрузкаЭлектронногоПредставленияПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОКлиент.ВыгрузкаЭлектронногоПредставления(Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаРегламентированногоОтчетаПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ВыгрузкаРегламентированногоОтчета(
		ПараметрыОповещения.Ссылка,
		ПараметрыОповещения.ОрганизацияСсылка,
		ПараметрыОповещения.НаименованиеОтчета,
		ПараметрыОповещения.ВариантОтчета,
		ПараметрыОповещения.ПредставлениеПериода
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаРегламентированногоОтчета(Ссылка, ОрганизацияСсылка, НаименованиеОтчета, ВариантОтчета, ПредставлениеПериода);
	
	Это_РСВ_1_2014_И_ЗагруженыВнешниеСведенияРаздела6 = Ложь;
	
	СтандартнаяОбработка = Истина;
	
	Если ВыполнениеКомандыОтменено(НаименованиеОтчета, ОрганизацияСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Ссылка", Ссылка);
		ПараметрыОповещения.Вставить("ОрганизацияСсылка", ОрганизацияСсылка);
		ПараметрыОповещения.Вставить("НаименованиеОтчета", НаименованиеОтчета);
		ПараметрыОповещения.Вставить("ВариантОтчета", ВариантОтчета);
		ПараметрыОповещения.Вставить("ПредставлениеПериода", ПредставлениеПериода);
		Оповещение = Новый ОписаниеОповещения("ВыгрузкаРегламентированногоОтчетаПредварительное", ЭтотОбъект, ПараметрыОповещения);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		
		ОписаниеОтчета = ОписаниеРегОтчета(Ссылка);
		
		Если ОбработаноДействиеДляАлкоОтчета(Ссылка, ОписаниеОтчета, "Выгрузка") Тогда
		    Возврат;		
		КонецЕсли;
		
	КонецЕсли;
	
	// Обход ошибки платформы.
	Если РегламентированнаяОтчетностьКлиент.ВебБраузер() = "MSIE" Тогда
		Если НаименованиеОтчета = "РСВ-1 ПФР"
			ИЛИ НаименованиеОтчета = "Декларация по НДС"
			ИЛИ НаименованиеОтчета = "Бухгалтерская отчетность (с 2011 года)"
			ИЛИ НаименованиеОтчета = "Бухгалтерская отчетность малых предприятий" Тогда
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
				"ru='Для выгрузки (проверки выгрузки, проверки в интернете, отправки) в браузере ""Internet Explorer"" необходимо открыть форму регламентированного отчета ""%1""'"),
				НаименованиеОтчета);
			ПоказатьПредупреждение(, ТекстПредупреждения);
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если НаименованиеОтчета
		= "Реестр для налоговых вычетов по акцизам на этиловый спирт (денатурированный этиловый спирт)" Тогда
		ПоказатьПредупреждение(, НСтр(
		"ru='Выгрузка в электронном виде формы
		|отчета, утвержденной приказом ФНС России
		|от 26.04.2021 № ЕД-7-15/418@, будет
		|реализована в будущих версиях программы'"));
		Возврат;
	КонецЕсли;
	
	Если НаименованиеОтчета = "Движение средств по счету в банке за пределами РФ" Тогда
		ОписаниеРегОтчета = ОписаниеРегОтчета(Ссылка);
		Если ОписаниеРегОтчета.ВыбраннаяФорма = "ФормаОтчета2015Кв3" Тогда
			ПоказатьПредупреждение(, НСтр(
			"ru='Выгрузка в электронном виде формы отчета
			|в редакции постановления Правительства РФ
			|от 25.07.2015 № 761 не предусмотрена
			|законодательством. Форма сдается в бумажном виде'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если НаименованиеОтчета = "Доходы, выплаченные иностранным организациям" Тогда
		ОписаниеДохИно = ОписаниеРегОтчета(Ссылка);
		Если (ОписаниеДохИно.ВыбраннаяФорма = "ФормаОтчета2020Кв1"
		  ИЛИ ОписаниеДохИно.ВыбраннаяФорма = "ФормаОтчета2016Кв1")
			И ОписаниеДохИно.Свойство("ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении") Тогда
			Если НЕ ОписаниеДохИно.ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении Тогда
				Если ОписаниеДохИно.Свойство("ТекстСообщения") Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
					"ru='%1 Выгрузка прервана.'"), ОписаниеДохИно.ТекстСообщения);
					Сообщение.Сообщить();
				КонецЕсли;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НаименованиеОтчета = "Сведения о доле доходов от туристско-рекреационной деятельности" Тогда
		Если ОписаниеРегОтчета(Ссылка).ВыбраннаяФорма = "ФормаОтчета2018Кв1" Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
			"ru='Выгрузка, проверка выгрузки, проверка в интернете, отправка в электронном виде, печать с двухмерным
			|штрихкодом PDF417 формы отчета ""%1"",
			|рекомендованной письмом ФНС России от 16.01.2019 № СД-4-3/441@, не предусмотрены'"), НаименованиеОтчета);
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если НаименованиеОтчета = "РСВ-1 ПФР" Тогда
		
		ОписаниеРСВ_1 = ОписаниеРегОтчета(Ссылка);
		
		Если ОписаниеРСВ_1.ДатаКонцаПериодаОтчета >= '20140101' Тогда
			
			Если НЕ (ПерсонифицированныйУчетВедетсяВоВнешнейПрограмме И ОписаниеРСВ_1.ОтчетСтарогоОбразца_20141101) Тогда
				РегламентированнаяОтчетностьКлиент.ПриВыгрузкеРСВ_1ИзЖурналаРеглОтчетов(
					Ссылка, ОписаниеРСВ_1, СтандартнаяОбработка);
			КонецЕсли;
			
			Если ОписаниеРСВ_1.ЗагруженыВнешниеСведенияРаздела6 Тогда
				Это_РСВ_1_2014_И_ЗагруженыВнешниеСведенияРаздела6 = Истина;
			КонецЕсли;
			
		Иначе
			
			Если СокрЛП(ВариантОтчета) = "П"
				И НЕ РегламентированнаяОтчетностьКлиентСерверПереопределяемый.ИмяОсновнойФормыРСВ1() = "ОсновнаяФорма" Тогда
				
				ПоказатьПредупреждение(,НСтр("ru='Внимание! Выгружать в электронном виде первичную форму РСВ-1 необходимо
				|в комплекте с документами квартальной отчетности персонифицированного
				|учета из специализированного рабочего места подготовки квартальной
				|отчетности в ПФР. Выгрузка прервана.'"));
				
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если (НаименованиеОтчета = "Статистика: Форма учета перемещения товаров"
		ИЛИ НаименованиеОтчета = "Статистическая форма учета перемещения товаров") И КонтекстЭДОКлиент <> Неопределено Тогда
		ДокументЗаявлениеОВвозеТоваров = Неопределено;
		ИмяДокументаЗаявлениеОВвозеТоваров = Неопределено;
		РезультатПолученияЗаявленияОВвозеТоваров = КонтекстЭДОКлиент.ПопытатьсяПолучитьЗаявлениеОВвозеТоваровИзСтатформы(
			Ссылка, ДокументЗаявлениеОВвозеТоваров, ИмяДокументаЗаявлениеОВвозеТоваров);
		Если РезультатПолученияЗаявленияОВвозеТоваров = Неопределено Тогда
			Возврат;
		ИначеЕсли РезультатПолученияЗаявленияОВвозеТоваров Тогда
			ДополнительныеПараметры = Новый Структура();
			ДополнительныеПараметры.Вставить("ДокументЗаявлениеОВвозеТоваров", ДокументЗаявлениеОВвозеТоваров);
			ДополнительныеПараметры.Вставить("ИмяДокументаЗаявлениеОВвозеТоваров", ИмяДокументаЗаявлениеОВвозеТоваров);
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузкаРегламентированногоОтчетаЗавершениеИзСтатформы", ЭтотОбъект, ДополнительныеПараметры);
			РегламентированнаяОтчетностьКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, Ссылка);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если НаименованиеОтчета = "Статистика: Форма П-1" Тогда
			
		Если НЕ ПроверитьВозможностьВыгрузкиП1ИзФормыОтчетностьНаСервере(Ссылка) Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Для отчета ""Статистика: Форма П-1 за %1"" не выбрано Приложение №3.
				|Необходимо сначала осуществить выгрузку из формы отчета! Выгрузка прервана.'"), ПредставлениеПериода);
			Сообщение.Сообщить();
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НаименованиеОтчета = "Статистика: Форма П-1 Приложение 3" Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Выгрузка отчета ""Статистика: форма П-1 (приложение 3) за %1"" производится из формы П-1 за соответствующий период.'"), ПредставлениеПериода);
		Сообщение.Сообщить();
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
			
	Если СтандартнаяОбработка Тогда
	
		СписокОтчетов = Новый СписокЗначений;
		СписокОтчетов.Добавить(Ссылка);
		
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("СписокОтчетов", СписокОтчетов);
		ДополнительныеПараметры.Вставить("Это_РСВ_1_2014_И_ЗагруженыВнешниеСведенияРаздела6", Это_РСВ_1_2014_И_ЗагруженыВнешниеСведенияРаздела6);
			
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузкаРегламентированногоОтчетаПослеПодключенияРасширенияРаботыСФайлами", ЭтотОбъект, ДополнительныеПараметры);
	
		НачатьПодключениеРасширенияРаботыСФайлами(ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаРегламентированногоОтчетаПослеПодключенияРасширенияРаботыСФайлами(Подключено, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.Вставить("ПодключеноРасширениеРаботыСФайлами", Подключено);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузкаРегламентированногоОтчетаПродолжение", ЭтотОбъект, ДополнительныеПараметры);
	
	ТекстВопроса = "Выгрузить выбранный отчет?";
	ПоказатьВопрос(ОписаниеОповещения, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru='%1'"), ТекстВопроса), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаРегламентированногоОтчетаПродолжение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	СписокОтчетов = ДополнительныеПараметры.СписокОтчетов;

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузкаРегламентированногоОтчетаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		РегламентированнаяОтчетностьКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, СписокОтчетов[0].Значение)
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаРегламентированногоОтчетаЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СписокОтчетов = ДополнительныеПараметры.СписокОтчетов;
	
	ВВебКлиентеСохранятьФайлВыгрузкиИнтерактивно = Истина;
	
	Если ДополнительныеПараметры.Это_РСВ_1_2014_И_ЗагруженыВнешниеСведенияРаздела6
		И ДополнительныеПараметры.ПодключеноРасширениеРаботыСФайлами Тогда
		ВВебКлиентеСохранятьФайлВыгрузкиИнтерактивно = Ложь;
	КонецЕсли;
	
	Если НЕ РегламентированнаяОтчетностьКлиент.ВыгрузитьОтчеты(
		СписокОтчетов, , , ВВебКлиентеСохранятьФайлВыгрузкиИнтерактивно) Тогда
		ПоказатьПредупреждение(,НСтр("ru='Выгрузка прервана!'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаРегламентированногоОтчетаЗавершениеИзСтатформыПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ВыгрузкаРегламентированногоОтчетаЗавершениеИзСтатформы(
		ПараметрыОповещения.Отказ,
		ПараметрыОповещения.ДополнительныеПараметры
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаРегламентированногоОтчетаЗавершениеИзСтатформы(Отказ, ДополнительныеПараметры) Экспорт
	
	ДокументЗаявлениеОВвозеТоваров = ДополнительныеПараметры.ДокументЗаявлениеОВвозеТоваров;
	ИмяДокументаЗаявлениеОВвозеТоваров = ДополнительныеПараметры.ИмяДокументаЗаявлениеОВвозеТоваров;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Отказ", Отказ);
		ПараметрыОповещения.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
		Оповещение = Новый ОписаниеОповещения(
			"ВыгрузкаРегламентированногоОтчетаЗавершениеИзСтатформыПредварительное", 
			ЭтотОбъект, 
			ПараметрыОповещения
		);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	АдресФайлаВыгрузки = Неопределено;
	ИмяФайлаВыгрузки = "";
	
	РезультатВыгрузки = КонтекстЭДОКлиент.ПопытатьсяВыгрузитьСтатформуИзЗаявленияОВвозеТоваров(
		АдресФайлаВыгрузки,
		ИмяФайлаВыгрузки,
		,
		Истина,
		,
		ДокументЗаявлениеОВвозеТоваров,
		ИмяДокументаЗаявлениеОВвозеТоваров);
	
	Если РезультатВыгрузки = Истина Тогда
		ПолучитьФайл(АдресФайлаВыгрузки, ИмяФайлаВыгрузки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьВозможностьВыгрузкиП1ИзФормыОтчетностьНаСервере(ОтчетСсылка)
	
	Перем ВыбранныйОтчетПриложение3;
	
	ДанныеОтчетаХЗ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОтчетСсылка, "ДанныеОтчета, ДатаНачала");
	Если Год(ДанныеОтчетаХЗ.ДатаНачала) >= 2017 Тогда 
		Возврат Истина;
	КонецЕсли;
	
	ДанныеВыгружаемогоОтчета = ДанныеОтчетаХЗ.ДанныеОтчета.Получить();
	
	Если НЕ ДанныеВыгружаемогоОтчета.Свойство("ВыбранныйОтчетПриложение3", ВыбранныйОтчетПриложение3) Тогда
		Возврат Ложь;
	ИначеЕсли ВыбранныйОтчетПриложение3 = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьФормуСтатусовОтправкиИзСпискаПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ПоказатьФормуСтатусовОтправкиИзСписка(
		ПараметрыОповещения.Элемент
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьФормуСтатусовОтправкиИзСписка(Элемент) Экспорт
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Элемент", Элемент);
		Оповещение = Новый ОписаниеОповещения(
			"ПоказатьФормуСтатусовОтправкиИзСпискаПредварительное", 
			ЭтотОбъект, 
			ПараметрыОповещения
		);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОКлиент.ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеРегОтчета(ДокументРегОтчет)
	
	Возврат РегламентированнаяОтчетностьВызовСервера.ОписаниеРегОтчета(ДокументРегОтчет);
	
КонецФункции

&НаСервереБезКонтекста
Функция ОбъектВАрхиве(Знач ОбъектОтправки, Знач ИмяФайла)
	
	КонтекстМодуля = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Возврат КонтекстМодуля.ОбъектВАрхиве(ОбъектОтправки, ИмяФайла);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПротоколЕсть(Отправка, ВАрхиве = Ложь)
	
	ВАрхиве = Ложь;
	Если ЗначениеЗаполнено(Отправка) Тогда
		ВАрхиве = ОбъектВАрхиве(Отправка, "Протокол");
		Возврат ЗначениеЗаполнено(Отправка.Протокол.Получить());
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура НапечататьОбъектНеВходящийВБРО(Ссылка, СведенияПоОбъекту)
	
	МакетПечати = СведенияПоОбъекту.ИмяОсновногоМакетаДляПечати;
	
	СтандартнаяПечатьОбъекта = Истина;
	РегламентированнаяОтчетностьКлиент.ПечатьПереопределяемый(Ссылка, МакетПечати, СтандартнаяПечатьОбъекта);
	
	Если СтандартнаяПечатьОбъекта Тогда
		
		Если МакетПечати <> Неопределено Тогда
			
			ИмяМенеджера = ИмяМенеджераПечати(Ссылка);
			
			МассивПечати = Новый Массив;
			МассивПечати.Добавить(Ссылка);
			
			Попытка
				УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ИмяМенеджера, МакетПечати, МассивПечати, Неопределено);
			Исключение
				ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(НСтр("ru = 'При печати возникла ошибка'"));
			КонецПопытки;
			
		Иначе
			
			ПредупредитьОбОграничениях(Элементы.Отчеты, НСтр("ru = 'Печать без штрихкода PDF417 не предусмотрена'"));
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяМенеджераПечати(Ссылка)
	
	Возврат Ссылка.Метаданные().ПолноеИмя();
	
КонецФункции

&НаКлиенте
Процедура ПечатьПротоколовПоСсылкеПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ПечатьПротоколовПоСсылке(
		ПараметрыОповещения.Закладка,
		ПараметрыОповещения.Ссылка,
		ПараметрыОповещения.ВидКонтролирующегоОргана,
		ПараметрыОповещения.НеОтправляетсяВКонтролирующийОрган,
		ПараметрыОповещения.СостояниеСдачиОтчетности
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьПротоколовПоСсылке(
	Закладка, 
	Ссылка, 
	ВидКонтролирующегоОргана, 
	НеОтправляетсяВКонтролирующийОрган, 
	СостояниеСдачиОтчетности)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Закладка", Закладка);
		ПараметрыОповещения.Вставить("Ссылка", Ссылка);
		ПараметрыОповещения.Вставить("ВидКонтролирующегоОргана", ВидКонтролирующегоОргана);
		ПараметрыОповещения.Вставить("НеОтправляетсяВКонтролирующийОрган", НеОтправляетсяВКонтролирующийОрган);
		ПараметрыОповещения.Вставить("СостояниеСдачиОтчетности", СостояниеСдачиОтчетности);
		Оповещение = Новый ОписаниеОповещения(
			"ПечатьПротоколовПоСсылкеПредварительное", 
			ЭтотОбъект, 
			ПараметрыОповещения
		);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	Если НеОтправляетсяВКонтролирующийОрган Тогда
		ПредупредитьОбОграничениях(Закладка, НСтр("ru = 'Печать протоколов не предусмотрена'"));
	Иначе
		
		Если ТипЗнч(Ссылка) <> Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов")
			И ТипЗнч(Ссылка) <> Тип("СправочникСсылка.ПерепискаСКонтролирующимиОрганами") Тогда
			ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтметитьКакПрочтенное(Ссылка);
		КонецЕсли;

		ПоследнийЦиклИлиОтправка = Неопределено;
		ВАрхиве	= Ложь;
		
		// ФСС
		Если ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСС") Тогда
			ПоследнийЦиклИлиОтправка = КонтекстЭДОКлиент.ПолучитьПоследнююОтправкуОтчетаВФСС(Ссылка);
			ПротоколЕсть 			 = ПротоколЕсть(ПоследнийЦиклИлиОтправка, ВАрхиве);
		// ФСРАР
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСРАР") Тогда
			ПоследнийЦиклИлиОтправка = КонтекстЭДОКлиент.ПолучитьПоследнююОтправкуОтчетаВФСРАР(Ссылка);
			ПротоколЕсть 			 = ПротоколЕсть(ПоследнийЦиклИлиОтправка, ВАрхиве);
		// РПН
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.РПН") Тогда
			ПоследнийЦиклИлиОтправка = КонтекстЭДОКлиент.ПолучитьПоследнююОтправкуОтчетаВРПН(Ссылка);
			ПротоколЕсть 			 = ПротоколЕсть(ПоследнийЦиклИлиОтправка, ВАрхиве);
		// ФТС
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФТС") Тогда
			ПоследнийЦиклИлиОтправка = КонтекстЭДОКлиент.ПолучитьПоследнююОтправкуОтчетаВФТС(Ссылка);
			ПротоколЕсть 			 = ПротоколЕсть(ПоследнийЦиклИлиОтправка, ВАрхиве);
		// Банк России
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.БанкРоссии") Тогда
		    МодульДокументооборотСБанкомРоссииКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ДокументооборотСБанкомРоссииКлиент");
			ПоследнийЦиклИлиОтправка = МодульДокументооборотСБанкомРоссииКлиент.ПолучитьПоследнююОтправкуОтчета(Ссылка);
			ПротоколЕсть 			 = ПротоколЕсть(ПоследнийЦиклИлиОтправка, ВАрхиве);
		// Остальные контролирующие органы
		Иначе
			ПоследнийЦиклИлиОтправка = ДокументооборотСКОВызовСервера.ПолучитьПоследнийЦиклОбмена(Ссылка);
			ПротоколЕсть 			 = Истина;
		КонецЕсли;
		
		// Предупреждаем об ошибке, если нет протокола  
		Если НЕ ЗначениеЗаполнено(ПоследнийЦиклИлиОтправка) ИЛИ НЕ ПротоколЕсть Или ВАрхиве Тогда
			Если СостояниеСдачиОтчетности = ПредопределенноеЗначение("Перечисление.СостояниеСдачиОтчетности.ДокументооборотНеНачат") Тогда
				КонтекстЭДОКлиент.ПредупредитьОбОтсутствииПротокола();
			Иначе
				Если ВАрхиве Тогда 
					КонтекстЭДОКлиент.ПоказатьУведомлениеАрхивныхФайлов(, 16, 5, Истина);
				Иначе 
					ТекстПредупреждения = НСтр("ru = 'У пользователя недостаточно прав для просмотра и печати протоколов.'");
					ПоказатьПредупреждение(,ТекстПредупреждения);
				КонецЕсли;
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		// ФСС
		Если ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСС") Тогда
			КонтекстЭДОКлиент.ПоказатьПротоколОбработкиПоСсылкеИсточникаДляФСС(ПоследнийЦиклИлиОтправка);
		// ФСРАР
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСРАР") Тогда
			КонтекстЭДОКлиент.ПоказатьПротоколОбработкиПоСсылкеИсточникаДляФСРАР(ПоследнийЦиклИлиОтправка);
		// РПН
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.РПН") Тогда
			КонтекстЭДОКлиент.ПоказатьПротоколОбработкиПоСсылкеИсточникаДляРПН(ПоследнийЦиклИлиОтправка);
		// ФТС
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФТС") Тогда
			КонтекстЭДОКлиент.ПоказатьПротоколОбработкиПоСсылкеИсточникаДляФТС(ПоследнийЦиклИлиОтправка);
		// Банк России
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.БанкРоссии") Тогда
			МодульДокументооборотСБанкомРоссииКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ДокументооборотСБанкомРоссииКлиент");
			МодульДокументооборотСБанкомРоссииКлиент.ПоказатьПротокол(ПоследнийЦиклИлиОтправка);
		// Остальные контролирующие органы
		Иначе
			КонтекстЭДОКлиент.ПоказатьДокументыЦикловОбмена(ПоследнийЦиклИлиОтправка, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРезультатПроверкиОбновленияИнформацииМОС(
	Результат = Неопределено,
	ДополнительныеПараметры = Неопределено) Экспорт
	
	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗаданияМОС) Тогда // задание выполнено
			
			РезультатПроверки = ПолучитьИзВременногоХранилища(АдресХранилищаМОС);
			Если НЕ ЗначениеЗаполнено(РезультатПроверки) Тогда
				Возврат;
			КонецЕсли;
			
			Если ТипЗнч(РезультатПроверки) <> Тип("Массив") Тогда
				Возврат;
			КонецЕсли;
				
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Обновлены внешние формы отчетов - %1'"),
								Формат(РезультатПроверки.Количество(), "ЧГ="));
				
			Для каждого СтрокаСообщения Из РезультатПроверки Цикл
				
				ТекстСообщения = ТекстСообщения + Символы.ПС + Символы.ПС
							   + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1'"), СтрокаСообщения);
				
			КонецЦикла;
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
			
		ТекстСообщенияОбОшибке =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при проверке результата обновления информации механизма онлайн-сервисов регламентированной отчетности
					 |%1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
					 
		ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
			НСтр("ru = 'Регламентированная отчетность. Проверка результата обновления информации механизма онлайн-сервисов регламентированной отчетности'"), 
			"Ошибка", ТекстСообщенияОбОшибке);
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьКоличествоОбъектовНеЗаписанныхВРегистр()
	
	ИдентификаторЗадания = Неопределено;
	
	ПараметрыЗапуска = Новый Структура;
	ПараметрыЗапуска.Вставить("УникальныйИдентификаторФормы", УникальныйИдентификатор);
	
	Результат = РегламентированнаяОтчетность.КоличествоОбъектовНеЗаписанныхВРегистр(ПараметрыЗапуска);
	
	Если Результат = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	АдресХранилища       = Результат.АдресРезультата;
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьВыполнениеПодсчетаКоличестваНезаписанныхОбъектов(Результат, ДополнительныеДанные = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		
		// Обрабатываем результат запуска фонового задания по проверке наличия объектов, не записанных в регистры
		Если Результат.Статус = "Выполнено" Тогда
			ПоказатьРезультатПроверки();
		КонецЕсли;
		
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ТекстСообщенияОбОшибке =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при подсчете количества объектов, не отображаемых в форме 1С-Отчетность 
					 |%1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
					 
		ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
			НСтр("ru = 'Регламентированная отчетность. Проверка количества незаписанных объектов'"), 
			"Ошибка", 
			ТекстСообщенияОбОшибке,
			,
			Истина);
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРезультатПроверки()
	
	РезультатПроверки = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если НЕ ЗначениеЗаполнено(РезультатПроверки) Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатПроверки.КоличествоНезаписанныхОбъектов = 0 И РезультатПроверки.КоличествоНекорректныхЗаписей = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДописатьОбъектыОтсутствующиеВРегистре();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаКлиенте
Процедура ДописатьОбъектыОтсутствующиеВРегистре(СОчисткойРегистров = Ложь)
	
	Результат = ЗапуститьДобавлениеОбъектовВРегистр(СОчисткойРегистров);
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		// Задание отработало, результат получен
		ДействияПослеЗавершенияИндексированияОбъектов();
	ИначеЕсли ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ОповещениеПроверитьРезультат = Новый ОписаниеОповещения(
			"ПроверитьРезультатДобавленияОбъектовОтсутствующихВРегистре", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ОповещениеПроверитьРезультат, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьРезультатДобавленияОбъектовОтсутствующихВРегистре(Результат, ДополнительныеДанные = Неопределено) Экспорт
	
	Если Результат <> Неопределено И Результат.Статус = "Выполнено" Тогда
		ДействияПослеЗавершенияИндексированияОбъектов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьДобавлениеОбъектовВРегистр(СОчисткойРегистров = Ложь)
	
	ИдентификаторЗадания = Неопределено;
	
	ПараметрыЗапуска = Новый Структура;
	ПараметрыЗапуска.Вставить("УникальныйИдентификаторФормы", УникальныйИдентификатор);
	ПараметрыЗапуска.Вставить("СОчисткойРегистров", СОчисткойРегистров);
	
	Результат = РегламентированнаяОтчетность.ДобавлениеОбъектовОтсутствующихВРегистре(ПараметрыЗапуска);
	
	Если Результат = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	АдресХранилища       = Результат.АдресРезультата;
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяФормыОбъектаПоУмолчанию(Ссылка)
	
	Возврат Ссылка.Метаданные().ПолноеИмя() + ".ФормаОбъекта";
	
КонецФункции

&НаКлиенте
Процедура ОбновитьТекущуюТаблицу()
	
	Для каждого Раздел Из Разделы(ЭтотОбъект, Истина) Цикл
		
		ТекущаяСтраницаРаздела = Элементы["Страница" + Раздел];
		
		Если Элементы.Разделы.ТекущаяСтраница = ТекущаяСтраницаРаздела Тогда
			Элементы[Раздел].Обновить();
		КонецЕсли;
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОнлайнПроверкаПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ОнлайнПроверка(
		ПараметрыОповещения.Закладка		
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ОнлайнПроверка(Закладка)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Закладка", Закладка);
		Оповещение = Новый ОписаниеОповещения(
			"ОнлайнПроверкаПредварительное", 
			ЭтотОбъект, 
			ПараметрыОповещения
		);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные 	= Закладка.ТекущиеДанные;
	ЭтоОтчеты = Закладка = Элементы.Отчеты;	
	
	РегламентированнаяОтчетностьКлиент.ОнлайнПроверка(ЭтотОбъект, КонтекстЭДОКлиент, ТекущиеДанные, ЭтоОтчеты);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредупредитьОбОграничениях(Закладка, Знач ТекстПредупреждения, Знач ВидОбъекта = Неопределено)
	
	// Переводим первый символ в нижний регистр
	ТекстПредупреждения = НРег(Лев(ТекстПредупреждения, 1)) + Сред(ТекстПредупреждения, 2);
	
	ТекстПредупреждения = НСтр("ru = 'Для %1 ""%2"" '") + ТекстПредупреждения;
	
	ЭтоОтчеты = Закладка = Элементы.Отчеты;
	ТекущиеДанные = Закладка.ТекущиеДанные;
	
	Если ВидОбъекта = Неопределено Тогда
		Если ЭтоОтчеты Тогда
			ВидОбъекта = НСтр("ru = 'отчета'");
		Иначе
			ВидОбъекта = НСтр("ru = 'уведомления'"); 
		КонецЕсли;
	КонецЕсли;
	
	Наименование = "";
	Если ЭтоОтчеты Тогда
		Наименование = ТекущиеДанные.НаименованиеОтчета;
	Иначе
		Наименование = ТекущиеДанные.Наименование;
	КонецЕсли;
	
	ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%1", ВидОбъекта);
	ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%2", Наименование);
	
	ПоказатьПредупреждение(,ТекстПредупреждения);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_ТекущаяСтраницаРазделов", Элементы.Разделы.ТекущаяСтраница.Имя);
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_ТолькоУказанныйПериод", ТолькоУказанныйПериод);
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_ПериодОтчета",  ПериодОтчета);
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_НачалоПериода", НачалоПериода);
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_КонецПериода",  КонецПериода);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиОтбораИСортировки()
	
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_Организация", Организация);
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_ВидОтчета",   ВидОтчета);
	
	ПользовательскиеНастройкиДинамическогоСписка = ЭтотОбъект.Отчеты.КомпоновщикНастроек.ПользовательскиеНастройки;
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_ПользовательскиеНастройкиОтчетов", ПользовательскиеНастройкиДинамическогоСписка);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюЭлементовПриОтключенномЭДО()
	
	ЕстьПравоОбмена = 
		РольДоступна("ПравоНаЗащищенныйДокументооборотСКонтролирующимиОрганами")
		ИЛИ РольДоступна("АдминистраторСистемы")
		ИЛИ РольДоступна("ПолныеПрава");
	
	Элементы.ГруппаПанельИнформацииСКнопкой.Видимость = ЭлектронныйДокументооборотДоступен;
	Элементы.ОбновитьДанные.Видимость = ЕстьПравоОбмена;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюЛичныхКабинетов()
	
	УправлениеВидимостьюЛичныхКабинетовНаправлений();
	УправлениеВидимостьюЛичныхКабинетовСпецоператоров();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюЛичныхКабинетовНаправлений()
	
	СтрокаСписокПоддерживаемыхКО = "ФНСЮЛ,ФНСИП,ФСС,ФСРАР,РПН,ФТС";
	НаправленияЭталон = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаСписокПоддерживаемыхКО, ",");
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиПереопределяемый.ПереопределитьСписокПоддерживаемыхНаправлений(СтрокаСписокПоддерживаемыхКО);
	НаправленияПереопределенный = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаСписокПоддерживаемыхКО, ",");
	
	Для каждого СтрокаНаправление Из НаправленияЭталон Цикл
		Если НаправленияПереопределенный.Найти(СтрокаНаправление) = Неопределено Тогда
			ИмяЭлемента = "Группа" + СтрокаНаправление + "Сервис";
			
			ЭлементГруппа = Элементы.Найти(ИмяЭлемента);
			Если ЭлементГруппа <> Неопределено Тогда 
				ЭлементГруппа.Видимость = Ложь;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюЛичныхКабинетовСпецоператоров()
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	
	Если КонтекстЭДОСервер = Неопределено Тогда
		Возврат;
	КонецЕсли;

	//получение списка используемых операторов
	МассивИспользуемыхСпецоператоров = КонтекстЭДОСервер.ПолучитьМассивИспользуемыхСпецоператоров();
	
	//заполнение таблицы свойств операторов
	ТаблицаСвойствОператоров = Новый ТаблицаЗначений;
	ТаблицаСвойствОператоров.Колонки.Добавить("ЛичныйКабинетНаименованиеСервиса");
	ТаблицаСвойствОператоров.Колонки.Добавить("ЛичныйКабинетАдресСервиса");
	ТаблицаСвойствОператоров.Колонки.Добавить("ЛичныйКабинетОписаниеСервиса");
	
	Для каждого ИспользуемыйСпецоператор Из МассивИспользуемыхСпецоператоров Цикл
		ПараметрыСтруктура = Новый Структура("ЛичныйКабинетНаименованиеСервиса, ЛичныйКабинетАдресСервиса, ЛичныйКабинетОписаниеСервиса");
		КонтекстЭДОСервер.ЗаполнитьПараметрыСпецоператора(ИспользуемыйСпецоператор, ПараметрыСтруктура);
		
		Если ЗначениеЗаполнено(ПараметрыСтруктура.ЛичныйКабинетАдресСервиса) Тогда
			//в таблицу попадают лишь спецоператоры, у которых есть личный кабинет
			ЗаполнитьЗначенияСвойств(ТаблицаСвойствОператоров.Добавить(), ПараметрыСтруктура);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаСвойствОператоров.Сортировать("ЛичныйКабинетАдресСервиса");	
	
	//заполнение таблицы личных кабинетов на основе свойств операторов
	//в этой таблице будут сгруппированы в одну строку спецоператоры с одинаковым адресом личных кабинетов
	//наименование и описание сервиса - по первому из операторов
	ТаблицаЛичныхКабинетов = Новый ТаблицаЗначений;
	ТаблицаЛичныхКабинетов.Колонки.Добавить("ЛичныйКабинетНаименованиеСервиса");
	ТаблицаЛичныхКабинетов.Колонки.Добавить("ЛичныйКабинетАдресСервиса");
	ТаблицаЛичныхКабинетов.Колонки.Добавить("ЛичныйКабинетОписаниеСервиса");
	
	ТекущееНазваниеСервиса 		= "";
	ТекущийАдресСервиса 		= "";
	ТекущееОписаниеСервиса 		= "";
	Для каждого СвойстваОператора Из ТаблицаСвойствОператоров Цикл
		
		Если ТекущийАдресСервиса <> СвойстваОператора.ЛичныйКабинетАдресСервиса Тогда
			//новый личный кабинет
			НовыйЛичныйКабинет = ТаблицаЛичныхКабинетов.Добавить();
			ТекущееНазваниеСервиса 		= СвойстваОператора.ЛичныйКабинетНаименованиеСервиса;
			ТекущийАдресСервиса 		= СвойстваОператора.ЛичныйКабинетАдресСервиса;
			ТекущееОписаниеСервиса 		= СвойстваОператора.ЛичныйКабинетОписаниеСервиса;
		КонецЕсли;
		
		НовыйЛичныйКабинет.ЛичныйКабинетНаименованиеСервиса = ТекущееНазваниеСервиса;
		НовыйЛичныйКабинет.ЛичныйКабинетАдресСервиса 	= ТекущийАдресСервиса;
		НовыйЛичныйКабинет.ЛичныйКабинетОписаниеСервиса = ТекущееОписаниеСервиса;
		
	КонецЦикла;

	// заполнение свойств элементов формы на основе таблицы личных кабинетов 
	Для каждого СтрокаЛичныйКабинет Из ТаблицаЛичныхКабинетов Цикл
		
		НомерЛК = ТаблицаЛичныхКабинетов.Индекс(СтрокаЛичныйКабинет) + 1;
		ЦветГиперссылкаБРО = Новый Цвет(28, 85, 174);
		НазваниеСервиса = Новый ФорматированнаяСтрока(СтрокаЛичныйКабинет.ЛичныйКабинетНаименованиеСервиса, , ЦветГиперссылкаБРО, , СтрокаЛичныйКабинет.ЛичныйКабинетАдресСервиса);
		
		ЭлементНазваниеСервиса = Элементы.Найти("ЛичныйКабинет1Сотчетность" + НомерЛК);
		Если ЭлементНазваниеСервиса <> Неопределено Тогда 
			ЭлементНазваниеСервиса.Заголовок = НазваниеСервиса;
			ЭлементНазваниеСервиса.Подсказка = СтрокаЛичныйКабинет.ЛичныйКабинетАдресСервиса;
		КонецЕсли;

		ЭлементОписаниеСервиса = Элементы.Найти("ПояснениеЛичныйКабинет1Сотчетность" + НомерЛК);
		Если ЭлементОписаниеСервиса <> Неопределено Тогда 
			ЭлементОписаниеСервиса.Заголовок = СтрокаЛичныйКабинет.ЛичныйКабинетОписаниеСервиса;
		КонецЕсли;

		ЭлементГруппаЛичныйКабинет = Элементы.Найти("ГруппаЛичныйКабинет1Сотчетность" + НомерЛК);
		Если ЭлементОписаниеСервиса <> Неопределено Тогда 
			ЭлементГруппаЛичныйКабинет.Видимость = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКритическиеОшибкиПоСсылкеПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	ПоказатьКритическиеОшибкиПоСсылке(
		ПараметрыОповещения.Ссылка		
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКритическиеОшибкиПоСсылке(Ссылка) Экспорт
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Ссылка", Ссылка);
		Оповещение = Новый ОписаниеОповещения(
			"ПоказатьКритическиеОшибкиПоСсылкеПредварительное", 
			ЭтотОбъект, 
			ПараметрыОповещения
		);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОКлиент.ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОткрытьНаНужнойСтранице(Параметры)
	
	// Если в качестве параметра открытия формы 1С-Отчетность была передана закладка, то открываем на этой закладке,
	// инчае открываем на той, на которой форму закрыли в последний раз,
	// либо на закладке Отчеты, если форма открывается впервые
	Если Параметры.Свойство("ВидУведомления") И ЗначениеЗаполнено(Параметры.ВидУведомления) Тогда
		
		Элементы.Разделы.ТекущаяСтраница = Элементы.СтраницаУведомления;
	
	ИначеЕсли Параметры.Свойство("Раздел") Тогда
		
		Элементы.Разделы.ТекущаяСтраница = Элементы["Страница" + ИмяСтраницы(Параметры.Раздел)];
			
	Иначе
		
		// Восстанавливаем запомненную страницу
		ТекСтраница = ХранилищеНастроекДанныхФорм.Загрузить(ЭтотОбъект.ИмяФормы, "Форма1СОтчетность_ТекущаяСтраницаРазделов");
		Если ТекСтраница <> Неопределено И Элементы.Разделы.ПодчиненныеЭлементы.Найти(ТекСтраница) <> Неопределено Тогда
			Элементы.Разделы.ТекущаяСтраница = Элементы[ТекСтраница];
		Иначе
			Элементы.Разделы.ТекущаяСтраница = Элементы.СтраницаОтчеты;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура НазначитьВидОтчетаИзПараметра(Параметры)
	
	Если Параметры.Свойство("ВидОтчета") Тогда
		
		ПараметрВидОтчета = СокрЛП(Параметры.ВидОтчета);
		
		Если ЗначениеЗаполнено(ПараметрВидОтчета) Тогда
			
			МассивВидовОтчетов = ПолучитьСписокОтчетовИзРегистраСведений();
			Элементы.ОтборВидОтчета.СписокВыбора.ЗагрузитьЗначения(МассивВидовОтчетов);
			// Тип реквизита может быть переопределен при необходимости (при переопределении формы выбора вида отчета).
			ВидОтчета = ПараметрВидОтчета;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НазначитьВидУведомленияИзПараметра(Параметры)
	
	Если Параметры.Свойство("ВидУведомления") И ЗначениеЗаполнено(Параметры.ВидУведомления) Тогда
		
		ВидУведомления = Параметры.ВидУведомления;
		ПредставлениеВидаУведомления = СокрЛП(Параметры.ВидУведомления);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СделатьВсеГиперссылкиНеактивными()
	
	Для каждого Раздел Из Разделы(ЭтотОбъект) Цикл
		
		Гиперссылка = Элементы["Гиперссылка" + Раздел];
		
		Гиперссылка.ЦветФона		= Серый;
		Гиперссылка.Гиперссылка		= Истина;
		Гиперссылка.ЦветТекста		= Черный;
	
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовоеУведомление_Предварительное(Результат, ПараметрыОповещения) Экспорт
	
	СоздатьНовоеУведомление();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовоеЗаявлениеПФРПоПредставлениюВидаУведомления(ПредставлениеВидаУведомления)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда
					
		Оповещение = Новый ОписаниеОповещения(
			"СоздатьНовоеУведомление_Предварительное", 
			ЭтотОбъект);
			
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
		
	КонецЕсли;
		
	ЗаявлениеПФР = КонтекстЭДОКлиент.СоздатьЗаявлениеВПФР(
		ПредставлениеВидаУведомления, 
		Организация);
		
	ПоказатьЗначение(, ЗаявлениеПФР);
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоЗаявлениеПФР(ВидУведомления)
	
	Тип  = Тип("ДокументСсылка.ЗаявленияПоЭлДокументооборотуСПФР");
	Типы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип);
	Возврат ВидУведомления = Новый ОписаниеТипов(Типы);
			
КонецФункции

&НаКлиенте
Процедура СоздатьНовоеУведомление()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("Создание_УведомлениеОСпецрежимахНалогообложения", Истина);
	ПараметрыФормы.Вставить("УчитыватьУведомленияНеВходящиеВБРО", Истина);
	ПараметрыФормы.Вставить("UIDФорма1СОтчетность", УникальныйИдентификатор);
	
	Если ЗначениеЗаполнено(ПредставлениеВидаУведомления) И ЗначениеЗаполнено(ВидУведомления) Тогда
		
		Если ЭтоЗаявлениеПФР(ВидУведомления) Тогда
			СоздатьНовоеЗаявлениеПФРПоПредставлениюВидаУведомления(ПредставлениеВидаУведомления);
			Возврат;
		ИначеЕсли ТипЗнч(ВидУведомления) = Тип("ОписаниеТипов")
			И ВидУведомления.СодержитТип(Тип("СправочникСсылка.МашиночитаемыеДоверенностиФНС"))
			И ВидУведомления.Типы().Количество() = 1 Тогда
			
			ОткрытьФорму(
				"Справочник.МашиночитаемыеДоверенностиФНС.ФормаОбъекта",
				Новый Структура("Организация", Организация),
				ЭтотОбъект,,,,,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			Возврат;
		Иначе
			ПараметрыФормы.Вставить("ВидУведомления", ВидУведомления);
		КонецЕсли;
	КонецЕсли;
	
	ОткрытьФорму("Документ.УведомлениеОСпецрежимахНалогообложения.Форма.ФормаВыбораВидаСообщения", ПараметрыФормы,
		ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйЗапросНаВыпискуИзЕГРЮЛ_ЕГРИППредварительное(Результат, ПараметрыОповещения) Экспорт
	
	СоздатьНовыйЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП()
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		Оповещение = Новый ОписаниеОповещения(
			"СоздатьНовыйЗапросНаВыпискуИзЕГРЮЛ_ЕГРИППредварительное", 
			ЭтотОбъект
		);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("Организация", Организация));
	
	ОткрытьФорму("Документ.ЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП.Форма.ФормаДокумента", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовоеПисьмо()
	
	СписокВидовПисьма = Новый СписокЗначений;
	СписокВидовПисьма.Добавить(НСтр("ru = 'Письмо'"));
	СписокВидовПисьма.Добавить(НСтр("ru = 'Ответ на требование'"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВыбораВидаПисьма", ЭтотОбъект);
	СписокВидовПисьма.ПоказатьВыборЭлемента(ОписаниеОповещения, НСтр("ru = 'Выберите вид письма'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыбораВидаПисьма(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		// Значит пользователь нажал Отмена
	ИначеЕсли Результат.Значение = НСтр("ru = 'Письмо'") Тогда
		 СоздатьНовоеИсходящееПисьмо();
	ИначеЕсли Результат.Значение = НСтр("ru = 'Ответ на требование'") Тогда 
		 СоздатьОтветНаТребование();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовоеИсходящееПисьмо()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("Отправитель", Организация));
	
	ОткрытьФорму("Справочник.ПерепискаСКонтролирующимиОрганами.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовуюСверку()
	
	СписокВыбораВидаСверки = Новый СписокЗначений;
	
	СписокВыбораВидаСверки.Добавить(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеАктовСверкиРасчетов"));
	СписокВыбораВидаСверки.Добавить(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеВыпискиОперацийИзКарточкиРасчетыСБюджетом"));
	СписокВыбораВидаСверки.Добавить(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеПеречняБухгалтерскойИНалоговойОтчетности"));
	СписокВыбораВидаСверки.Добавить(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеСправкиОСостоянииРасчетовСБюджетом"));
	СписокВыбораВидаСверки.Добавить(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеСправкиОбИсполненииОбязанностейПоУплате"));
	СписокВыбораВидаСверки.Добавить(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОС.СверкаФИОиСНИЛС"));
	СписокВыбораВидаСверки.Добавить(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОС.СправкаОСостоянииРасчетов"));

	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВыбораВидаСверки", ЭтотОбъект);
	
	СписокВыбораВидаСверки.ПоказатьВыборЭлемента(ОписаниеОповещения, НСтр("ru = 'Выберите вид сверки'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыбораВидаСверки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		СоздатьСверку(Результат.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭУВРежимеСервиса()
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда 
		Элементы.НадписьИнформацияОбОбновлениях.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьПоказыватьЛиПредложениеНаПодключение()
	
	ОпределитьДоступностьЭДО();
	
	ЕстьОтправленныеЗаявления = ОбработкаЗаявленийАбонентаВызовСервера.ЕстьОтправленныеИлиОдобренныеЗаявления();
	
	НеОтказалисьОтПредложения = ХранилищеОбщихНастроек.Загрузить("Обработка.ОбщиеОбъектыРеглОтчетности", "НеПоказыватьПредложениеОформитьЗаявлениеНаПодключение") <> Истина;
	
	ПоказатьПредложениеОформитьЗаявлениеНаПодключение = 
		НЕ ЭлектронныйДокументооборотДоступен
		И НеОтказалисьОтПредложения
		И НЕ ЕстьОтправленныеЗаявления
		И Используется1СОтчетность
		И НЕ ЭтоРежимВыбора
		И ДоступнаОтправкаРегламентированногоОтчета;
		
КонецПроцедуры

&НаСервере
Процедура ОпределитьДоступностьЭДО()
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Если КонтекстЭДОСервер <> Неопределено Тогда
		ЭлектронныйДокументооборотДоступен = КонтекстЭДОСервер.ХотяБыОднаОрганизацияИспользуетДокументооборот();
	КонецЕсли;
	
	ЕстьОтправленныеЗаявления = ОбработкаЗаявленийАбонентаВызовСервера.ЕстьОтправленныеИлиОдобренныеЗаявления();
	
	НеОтказалисьОтПредложения = ХранилищеОбщихНастроек.Загрузить("Обработка.ОбщиеОбъектыРеглОтчетности", "НеПоказыватьПредложениеОформитьЗаявлениеНаПодключение") <> Истина;
	
	ПоказатьПредложениеОформитьЗаявлениеНаПодключение = 
		НЕ ЭлектронныйДокументооборотДоступен
		И НеОтказалисьОтПредложения
		И НЕ ЕстьОтправленныеЗаявления
		И ДоступнаОтправкаРегламентированногоОтчета;
		
КонецПроцедуры

&НаСервере
Функция ПометитьПрочтенными()
	
	ВсеВыделенныеОбъекты 	= Новый Массив;
	СтавшиеПрочтенными 		= Новый Массив;
	СтавшиеНепрочтенными 	= Новый Массив;
	
	ТекущаяСтраница = ТекущаяСтраница(ЭтотОбъект);
	ТекущаяТаблица  = ТекущаяТаблица(ЭтотОбъект);
	
	Для каждого Строка Из ТекущаяТаблица.ВыделенныеСтроки Цикл
		Если ТекущаяСтраница = Перечисления.СтраницыЖурналаОтчетность.Новое Тогда
			СвойстваСтроки = Новое.НайтиПоИдентификатору(Строка);
			ВсеВыделенныеОбъекты.Добавить(СвойстваСтроки.Ссылка);
		Иначе
			ВсеВыделенныеОбъекты.Добавить(Строка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Если ТекущаяСтраница = Перечисления.СтраницыЖурналаОтчетность.Входящие Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЖурналОтправокВКонтролирующиеОрганы.Ссылка,
			|	ЖурналОтправокВКонтролирующиеОрганы.Непрочтенное
			|ИЗ
			|	РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
			|ГДЕ
			|	ЖурналОтправокВКонтролирующиеОрганы.Ссылка В(&Ссылки)";
		
		Запрос.УстановитьПараметр("Ссылки", ВсеВыделенныеОбъекты);
		
		ВходящиеПисьма = Запрос.Выполнить().Выгрузить();
		ЕстьНепрочтенные 	= ВходящиеПисьма.Найти(Истина, 	"Непрочтенное") <> Неопределено;
		ЕстьПрочтенные 		= ВходящиеПисьма.Найти(Ложь, 	"Непрочтенное") <> Неопределено;
		ВсеНепрочтенные  	= НЕ ЕстьПрочтенные;
		
		Для каждого ВходящийОбъект Из ВходящиеПисьма Цикл
			
			Если ЕстьНепрочтенные И ЕстьПрочтенные Тогда
				
				Если ВходящийОбъект.Непрочтенное Тогда
					
					СтавшиеПрочтенными.Добавить(ВходящийОбъект.Ссылка);
			
					ЭлектронныйДокументооборотСКонтролирующимиОрганами.УстановитьНепрочтенность(
						ВходящийОбъект.Ссылка, 
						Ложь);
						
				КонецЕсли;
					
			Иначе
				
				СтавшиеПрочтенными.Добавить(ВходящийОбъект.Ссылка);
				
				Непрочтенное = НЕ ВходящийОбъект.Непрочтенное;
				
				Если Непрочтенное Тогда
					СтавшиеНепрочтенными.Добавить(ВходящийОбъект.Ссылка);
				Иначе
					СтавшиеПрочтенными.Добавить(ВходящийОбъект.Ссылка);
				КонецЕсли;
				
				ЭлектронныйДокументооборотСКонтролирующимиОрганами.УстановитьНепрочтенность(
					ВходящийОбъект.Ссылка, 
					Непрочтенное);
					
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Запрос = Новый Запрос;
		Если ТекущаяСтраница = Перечисления.СтраницыЖурналаОтчетность.Отчеты Тогда
		
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ЖурналОтчетовСтатусы.Ссылка,
			|	ЖурналОтчетовСтатусы.Непрочтенное
			|ИЗ
			|	РегистрСведений.ЖурналОтчетовСтатусы КАК ЖурналОтчетовСтатусы
			|ГДЕ
			|	ЖурналОтчетовСтатусы.Ссылка В(&Ссылки)";
			
		ИначеЕсли ТекущаяСтраница = Перечисления.СтраницыЖурналаОтчетность.Новое Тогда
			
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ЖурналНовыхСобытий.Ссылка,
			|	НЕ ЖурналНовыхСобытий.Просмотрено КАК Непрочтенное
			|ИЗ
			|	РегистрСведений.ЖурналНовыхСобытий КАК ЖурналНовыхСобытий
			|ГДЕ
			|	ЖурналНовыхСобытий.Ссылка В(&Ссылки)";
			
		Иначе
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЖурналОтправокВКонтролирующиеОрганы.Ссылка,
			|	ЖурналОтправокВКонтролирующиеОрганы.Непрочтенное
			|ИЗ
			|	РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
			|ГДЕ
			|	ЖурналОтправокВКонтролирующиеОрганы.Ссылка В(&Ссылки)";

		КонецЕсли;
	
		Запрос.УстановитьПараметр("Ссылки", ВсеВыделенныеОбъекты);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.Непрочтенное Тогда
				
				СтавшиеПрочтенными.Добавить(Выборка.Ссылка);
				
				ЭлектронныйДокументооборотСКонтролирующимиОрганами.УстановитьНепрочтенность(
					Выборка.Ссылка, 
					Ложь);
					
			КонецЕсли;
			
		КонецЦикла;
			
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ВсеВыделенныеОбъекты", 		ВсеВыделенныеОбъекты);
	Результат.Вставить("СтавшиеПрочтенными", 		СтавшиеПрочтенными);
	Результат.Вставить("СтавшиеНепрочтенными", 		СтавшиеНепрочтенными);

	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьПанелиОшибокОбновления(Форма)

	Форма.Элементы.ОшибкиОбновления.Видимость = ЭтоАдресВременногоХранилища(Форма.АдресСведенийПоОшибкам);

КонецПроцедуры

&НаКлиенте
Процедура ОтчетыВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	РегламентированнаяОтчетностьКлиент.ОповеститьСписокОтчетовОбИзмененииИндексаКартинки(
	ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПрисоединенныеФайлы()
	
	ТекущиеДанные = Неопределено;
	
	ТекущаяСтраница = ТекущаяСтраница(ЭтотОбъект);
	
	Если ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Отчеты") Тогда
		
		ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		СсылкаНаОбъект = ТекущиеДанные.РегламентированныйОтчет;
		Наименование = ТекущиеДанные.НаименованиеОтчета;
		
	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Уведомления") Тогда
		
		ТекущиеДанные = Элементы.Уведомления.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		СсылкаНаОбъект = ТекущиеДанные.Ссылка;
		Наименование = ТекущиеДанные.Наименование;
		
	КонецЕсли;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ВладелецФайла",  СсылкаНаОбъект);
		ДопПараметры.Вставить("Организация",    ТекущиеДанные.Организация);
		ДопПараметры.Вставить("ТолькоПросмотр", Ложь);
		ДопПараметры.Вставить("ЗаголовокФормы", "Присоединенные файлы: " + Наименование);
		
		РегламентированнаяОтчетностьКлиент.ОткрытьФормуПрисоединенныеФайлы(, ДопПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДействияПослеЗавершенияИндексированияОбъектов()
	
	ОбновитьТекущуюТаблицу();
	Если ЗаполнитьДеревоНовое() Тогда
		ПриПереключенииРаздела(Элементы.ГиперссылкаНовое);
		Оповестить("Получены новые сообщения 1С-Отчетности", , ЭтотОбъект);
	КонецЕсли;
	
	ОбновитьКоличествоНепрочтенныхОбъектов(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаголовкиГруппыНастройки(Организация)
	
	ЗаголовокГиперссылкиЗаявления = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ЗаголовокГиперссылкиЗаявления(Организация);
	
	ЗаголовокГиперссылкиСпискаЗаявлений = НСтр("ru = 'Список заявлений'");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
		Если КонтекстЭДОСервер <> Неопределено И КонтекстЭДОСервер.ПоддерживаетсяВторичноеЗаявление(Организация) Тогда
			ЗаголовокГиперссылкиСпискаЗаявлений = НСтр("ru = 'Список'");
		КонецЕсли;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ЗаголовокГиперссылкиЗаявления", 		ЗаголовокГиперссылкиЗаявления);
	Результат.Вставить("ЗаголовокГиперссылкиСпискаЗаявлений", 	ЗаголовокГиперссылкиСпискаЗаявлений);
	Возврат Результат;
	
КонецФункции

#Область ДлительныеОперацииАЛКО

////////////////////////////////////////////////////////////////////////////////
//
// Механизм длительных операций отчетов подсистемы ОтчетностьПоАлкогольнойПродукции
//		  
////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Функция ОбработаноДействиеДляАлкоОтчета(ДокументСсылкаАлкоОтчет, ОписаниеОтчета, 
										Действие, ВидПечати = Неопределено)

	Возврат РегламентированнаяОтчетностьКлиент.ОбработаноДействиеДляАлкоОтчета(ЭтотОбъект, ДокументСсылкаАлкоОтчет, ОписаниеОтчета, 
				Действие, ВидПечати);
	
КонецФункции
 
&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеДлительнойОперацииНаКлиенте()
	
	РегламентированнаяОтчетностьКлиент.Подключаемый_ПроверитьВыполнениеДлительнойАлкоОперацииНаКлиенте(ЭтотОбъект);
	
КонецПроцедуры

// Конец области ДлительныеОперацииАЛКО.
#КонецОбласти

#Область РучнойВводСтатусовОтправки

&НаКлиенте
Процедура ВыбратьСтатусОтправкиИзСписка(Элемент)
	
	Если ТекущаяСтраница(ЭтотОбъект) = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Уведомления") Тогда
		ТекСтатус = СокрЛП(Элемент.ТекущиеДанные.СтатусОтправки);
		СсылкаНаОбъект = Элемент.ТекущиеДанные.Ссылка;
		ЭтоОтчет = Ложь;
	Иначе
		ТекСтатус = СокрЛП(Элемент.ТекущиеДанные.Статус);
		СсылкаНаОбъект = Элемент.ТекущиеДанные.РегламентированныйОтчет;
		ЭтоОтчет = Истина;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ВыбратьСтатусОтправкиИзСписка(Элемент.ТекущиеДанные, ТекСтатус, СсылкаНаОбъект, ЭтоОтчет, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиПравОбновления

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьКнопкиНастроекПрав(Форма)
	
	Форма.Элементы.НастройкиОбновления.Видимость = Форма.УстановленыОграниченияПрав;
	
КонецПроцедуры	

&НаСервереБезКонтекста
Функция ПроверитьДоступностьНастроекОграниченияПравНаОбмен()
	
	Результат = Ложь;
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		КонтекстСервера = ДокументооборотСКО.ПолучитьОбработкуЭДО();
		Если КонтекстСервера <> Неопределено Тогда
			ВсеОрганизации = КонтекстСервера.ОрганизацииСОграничениемПрав();
			Результат = ВсеОрганизации.Количество() > 0;
		КонецЕсли;
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции	

&НаКлиенте
Процедура ОбновитьДанныеПродолжить(ВыбранныеОрганизации)
	
	НомерОбъекта = 0;
	АдресСведенийПоОшибкам = "";
	УстановитьВидимостьПанелиОшибокОбновления(ЭтотОбъект);
	
	ПараметрыДлительногоОбмена = ДлительнаяОтправкаКлиент.ПараметрыДлительногоОбмена();
	ПараметрыДлительногоОбмена.Организации 					= ВыбранныеОрганизации;
	ПараметрыДлительногоОбмена.ЭтоОбменИзФормы1СОтчетность 	= Истина;
	
	Если НЕ ДлительнаяОтправкаКлиент.ПоказатьФормуДлительногоОбмена(ПараметрыДлительногоОбмена) Тогда // Обмен из 1С-Отчетность.
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ОсуществитьОбменПоВыбраннымОрганизациям", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОграниченияОбмена(РезультатВыбора, ПараметрыЦикла) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		ВыбранныеОрганизации = ПараметрыЦикла.ВыбранныеОрганизации;
		ОбновитьДанныеПродолжить(ВыбранныеОрганизации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьКлиентскийКонтекст(ОповещениеПроцедуры, ПараметрыОперации = Неопределено)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ИнициализироватьКонтекстЭДО(ОповещениеПроцедуры);
		
	ИначеЕсли КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеПроцедуры, ПараметрыОперации);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НастройкиОбновленияПосле(РезультатРаботы, ДополнительныеПараметры) Экспорт
	
	ОткрытьФормуНастройкиОбновления(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройкиОбновления(АвтоматическийВызов, СледующееОповещение = Неопределено)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АвтоматическийВызов", АвтоматическийВызов);
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.НастройкаОграниченийОбмена", ПараметрыФормы, , , , , СледующееОповещение);
	
КонецПроцедуры

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Процедура ДействияПриИзмененииОтборовИОбновлениеВходящих(Форма)
	
	ДействияПриИзмененииОтборов(Форма);
	ОбновитьКоличествоНепрочтенныхОбъектов(Форма);
					
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеКоличества(Знач КоличествоНепрочитанныхОбъектов)
	
	НадписьКоличество = "";
	Если КоличествоНепрочитанныхОбъектов > 99 Тогда  
		НадписьКоличество = " (99+)";
	ИначеЕсли КоличествоНепрочитанныхОбъектов <> 0 Тогда
		НадписьКоличество = " (" + Строка(КоличествоНепрочитанныхОбъектов) + ")";
	КонецЕсли;
	
	Возврат НадписьКоличество;
	
КонецФункции

&НаКлиенте
Функция ПереопределитьОткрытие(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.Ссылка;
	
		Переопределить = Ложь;
		
		Попытка
			МодульСверкаСФНС = ОбщегоНазначенияКлиент.ОбщийМодуль("СверкаСФНСКлиент");
		Исключение
		    МодульСверкаСФНС = Неопределено;
		КонецПопытки;
		
		Если МодульСверкаСФНС <> Неопределено Тогда
			СтандартнаяОбработка = Истина;
			МодульСверкаСФНС.РегламентированнаяОтчетность_ТаблицаНовоеПриОткрытии(ЭтотОбъект, Ссылка, СтандартнаяОбработка);
			
			Если НЕ СтандартнаяОбработка Тогда
				Переопределить = Истина;
			КонецЕсли;
				
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Переопределить;

КонецФункции

&НаСервере
Процедура ПереопределитьОформлениеСверки()
	
	Попытка
		МодульСверкаСФНС = ОбщегоНазначения.ОбщийМодуль("СверкаСФНС");
	Исключение
	    МодульСверкаСФНС = Неопределено;
	КонецПопытки; 
	
	Если МодульСверкаСФНС <> Неопределено Тогда
		МодульСверкаСФНС.РегламентированнаяОтчетность_ТаблицаНовоеПриОбновлении(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Новости

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Новости") Тогда
		
		МодульОбработкаНовостейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбработкаНовостейКлиент");
		
		МодульОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыНовости(Команда)
		
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Новости") Тогда
		
		МодульОбработкаНовостейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбработкаНовостейКлиент");
		
		МодульОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(ЭтотОбъект, Команда);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СоздатьОтветНаТребованиеПредварительное(Результат, ПараметрыОповещения) Экспорт
	
	СоздатьОтветНаТребование(
		ПараметрыОповещения.Команда		
	);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОтветНаТребование(Команда = Неопределено)
	
	Если КонтекстЭДОКлиент = Неопределено И Не КонтекстУжеПодключался Тогда 
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Команда", Команда);
		Оповещение = Новый ОписаниеОповещения(
			"СоздатьОтветНаТребованиеПредварительное", 
			ЭтотОбъект, 
			ПараметрыОповещения
		);
		ИнициализироватьКонтекстЭДО(Оповещение);
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ЗапретитьСозданиеПисьмаВОтветНаТребованиеПоДекларации", Истина);
			
	КонтекстЭДОКлиент.СоздатьОтветНаТребованиеСВыборомТребования(,Организация,ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПакетСДопДокументами(Команда)
	
	СоздатьПакетСДопДокументамиВФНС();
	
КонецПроцедуры

&НаКлиенте
Функция ФормаОкнаАвторизацииИППЕслиОткрыто()
	
	ОткрытыеОкна = ПолучитьОкна();
	
	Для каждого ОткрытоеОкно Из ОткрытыеОкна Цикл
		
		Если ОткрытоеОкно = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Содержимое = ОткрытоеОкно.Содержимое;
		
		Если Содержимое.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущаяФорма = Содержимое[0];
		
		Если ТекущаяФорма.ИмяФормы = "ОбщаяФорма.АвторизацияНаСайтеПоддержкиПользователей"
		 ИЛИ ТекущаяФорма.ИмяФормы = "ОбщаяФорма.ПодключениеИнтернетПоддержки" Тогда
			
			Возврат ТекущаяФорма;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Функция ПометитьНаУдалениеРассылки()
	
	ЗапросРассылок = Новый Запрос;
	Текст = ЭтотОбъект.Входящие.ТекстЗапроса;	
	Условие = "
	|ГДЕ
	|	ВЫБОР
	|		КОГДА (ЦО.Тип ЕСТЬ NULL) ТОГДА Ложь
	|		КОГДА (ЦО.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыЦикловОбмена.Рассылка)
	|			Или ЦО.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыЦикловОбмена.РассылкаГрупповая)
	|			Или ЦО.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыЦикловОбмена.РассылкаПФР)) ТОГДА Истина 
	|	ИНАЧЕ 
	|		Ложь
	|	КОНЕЦ
	|";
	
	Если ЗначениеЗаполнено(ЭтотОбъект.Организация) Тогда
		Условие = Условие + "
		|	И ЖурналОтправокВКонтролирующиеОрганы.Организация = &ОтборОрганизация";		
		ЗапросРассылок.УстановитьПараметр("ОтборОрганизация", ЭтотОбъект.Организация);
	КонецЕсли;
	
	Текст = СтрЗаменить(Текст, Символы.ПС + "ГДЕ" + Символы.ПС, Условие + " И ");
	ЗапросРассылок.Текст = Текст;	
	
	ТаблицаРезультатов = ЗапросРассылок.Выполнить().Выгрузить();
	ТаблицаРезультатов.Свернуть("Ссылка");
	ПредметыРассылок = ТаблицаРезультатов.ВыгрузитьКолонку("Ссылка");
	
	Если ПредметыРассылок.Количество() = 0 Тогда 
		Возврат Новый Массив;
	КонецЕсли;
	
	ЗапросРассылок = Новый Запрос;
	ЗапросРассылок.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	                       |	ЦиклыОбмена.Ссылка КАК Ссылка
						   |	,ЦиклыОбмена.Предмет КАК Предмет
	                       |ИЗ
	                       |	Справочник.ЦиклыОбмена КАК ЦиклыОбмена
	                       |ГДЕ
	                       |	ЦиклыОбмена.Предмет В (&ОтборПредметов)";
	
	ЗапросРассылок.УстановитьПараметр("ОтборПредметов", ПредметыРассылок);
	ТаблицаРезультатов2 = ЗапросРассылок.Выполнить().Выгрузить();
	ТаблицаРезультатов2.Свернуть("Ссылка, Предмет");
	ЦиклыОбменаРассылок = ТаблицаРезультатов2.ВыгрузитьКолонку("Ссылка");
	
	КонтекстЭДО = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого ПредметСсылка Из ПредметыРассылок Цикл 
		
		ЦиклОбменаОбъект = Неопределено;
		
		Стр = ТаблицаРезультатов2.Найти(ПредметСсылка, "Предмет");
		Если Стр <> Неопределено Тогда 
			ЦиклОбменаОбъект = Стр.Ссылка.ПолучитьОбъект();
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Если Не ПредметСсылка.ПометкаУдаления Тогда
			ПредметОбъект = ПредметСсылка.ПолучитьОбъект();
			ПредметОбъект.УстановитьПометкуУдаления(Истина);
			КонтекстЭДО.ПриЗаписиОбъекта(ПредметОбъект, Ложь);
		КонецЕсли;
		
		Если ЦиклОбменаОбъект <> Неопределено Тогда
			ЦиклОбменаОбъект.УстановитьПометкуУдаления(Истина);
			ЦиклОбменаОбъект.ИзмененаПометкаУдаления = Истина;
			КонтекстЭДО.ПриЗаписиОбъекта(ЦиклОбменаОбъект, Ложь);
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.ЖурналОтправокВКонтролирующиеОрганы.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Ссылка.Установить(ПредметСсылка);
		НаборЗаписей.Прочитать();
		
		Для каждого ЗаписьНабора Из НаборЗаписей Цикл
			ЗаписьНабора["Скрыт"] = Истина;
			ЗаписьНабора["Непрочтенное"] = Ложь;
			ЗаписьНабора["ПометкаУдаления"] = Истина;		
		КонецЦикла;
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ОтменитьТранзакцию();
			Продолжить;
		КонецПопытки;
		
		ЗафиксироватьТранзакцию();
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ПредметыРассылок;
		
КонецФункции

&НаКлиенте
Процедура ОчисткаРассылокПослеВопроса(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатВопроса) = Тип("КодВозвратаДиалога") И РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		ПредметыРассылок = ПометитьНаУдалениеРассылки();
		Если ПредметыРассылок.Количество() > 0 Тогда 
			Для Каждого Ссылка Из ПредметыРассылок Цикл 
				Оповестить("Скрытие/восстановление объекта",, Ссылка);
			КонецЦикла;		
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчисткаРассылок(Команда)
	
	ВидОбъекта = НСтр("ru = 'все рассылки'");
	
	ТекстСообщения = НСтр("ru = 'Вы уверены, что хотите пометить на удаление и скрыть %1?
                           |
						   |Помеченные на удаление рассылки в дальнейшем могут быть удалены навсегда при удалении помеченных объектов.
                           |Скрытые отчеты и документы можно восстановить в разделе
                           |""Настройки"" - ""Скрытые отчеты и документы"" формы 1С-Отчетность'");
	
	ТекстСообщения = СтрШаблон(ТекстСообщения, ВидОбъекта);
	
	ВыполняемоеОповещение = Новый ОписаниеОповещения("ОчисткаРассылокПослеВопроса", ЭтотОбъект);
	ПоказатьВопрос(ВыполняемоеОповещение, ТекстСообщения, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияВходящиеОчистка(Элемент, СтандартнаяОбработка)
	
	ДействияПриИзмененииОтборовИОбновлениеВходящих(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаЗаявленияНаПодключениеИзменениеНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуСпискаЗаявленийНаПодключение(Организация);
	
КонецПроцедуры

#КонецОбласти