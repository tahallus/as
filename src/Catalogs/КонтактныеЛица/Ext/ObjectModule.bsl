#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Контрагенты") Тогда
		
		РеквизитыКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "ЭтоГруппа,Ответственный");
		
		Если Не РеквизитыКонтрагента.ЭтоГруппа Тогда
			Ответственный = РеквизитыКонтрагента.Ответственный;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
		Если ДанныеЗаполнения.Свойство("НомерТелефона") Тогда
			УправлениеКонтактнойИнформацией.ЗаписатьКонтактнуюИнформацию(
				ЭтотОбъект,
				УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(ДанныеЗаполнения.НомерТелефона, Справочники.ВидыКонтактнойИнформации.ТелефонКонтактногоЛица),
				Справочники.ВидыКонтактнойИнформации.ТелефонКонтактногоЛица,
				Перечисления.ТипыКонтактнойИнформации.Телефон
			);
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьПоУмолчанию();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	// Дополнительно заполним реквизиты
	Если ЭтоНовый() Тогда
		ДатаСоздания = ТекущаяДатаСеанса();
	КонецЕсли;
	
	СписокРолей = "";
	Для Каждого РольТЧ Из Роли Цикл
		СписокРолей = СписокРолей + ?(СписокРолей = "","",", ") + РольТЧ.Роль;
	КонецЦикла;
	
	ЗаполнитьНомерТелефона();
	ЗаполнитьАдресЭП();
	СформироватьОсновныеСведения();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("СвязьСКонтрагентом") Тогда
		РегистрыСведений.СвязиКонтрагентКонтакт.НоваяСвязь(
			ДополнительныеСвойства.СвязьСКонтрагентом.Контрагент, Ссылка, 
			ДополнительныеСвойства.СвязьСКонтрагентом.Должность,,,
			ДополнительныеСвойства.СвязьСКонтрагентом.Порядок);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьРеквизитОсновноеКонтактноеЛицо();
	РегистрыСведений.ТекущиеВходящиеЗвонки.ПередУдалениемКонтакта(Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоУмолчанию()
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "ОсновнойОтветственный");
	КонецЕсли;
	
	ДатаСоздания = ТекущаяДатаСеанса();
	
	Если Не ЗначениеЗаполнено(ГруппаДоступа) Тогда
		ГруппаДоступа = Справочники.ГруппыДоступаКонтрагентов.ГруппаДоступаПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет вспомогательный реквизит "НомерТелефона"
//
Процедура ЗаполнитьНомерТелефона() Экспорт
	
	НомераТелефонов = Новый Массив;
	
	Для Каждого СтрокаКИ Из КонтактнаяИнформация Цикл
		Если СтрокаКИ.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
			НомераТелефонов.Добавить(СтрокаКИ.НомерТелефона);
		КонецЕсли;
	КонецЦикла;
	
	НомерТелефонаДляПоиска = СтрСоединить(НомераТелефонов, ", ");
	
КонецПроцедуры

// Процедура заполняет вспомогательный реквизит "АдресЭП"
//
Процедура ЗаполнитьАдресЭП() Экспорт
	
	АдресаЭП = Новый Массив;
	
	Для Каждого СтрокаКИ Из КонтактнаяИнформация Цикл
		Если СтрокаКИ.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда
			АдресаЭП.Добавить(СтрокаКИ.Представление);
		КонецЕсли;
	КонецЦикла;
	
	АдресЭПДляПоиска = СтрСоединить(АдресаЭП, ", ");
	
КонецПроцедуры

Процедура ОчиститьРеквизитОсновноеКонтактноеЛицо()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.КонтактноеЛицо = &КонтактноеЛицо";
	
	Запрос.УстановитьПараметр("КонтактноеЛицо", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СправочникОбъект.КонтактноеЛицо = Неопределено;
		СправочникОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьОсновныеСведения()
	
	МассивСтрок = Новый Массив;
	Если ЗначениеЗаполнено(Наименование) Тогда
		МассивСтрок.Добавить(Наименование);
	КонецЕсли;
	
	КИ = КонтактнаяИнформация.Выгрузить();
	КИ.Сортировать("Вид");
	Для Каждого СтрокаКИ Из КИ Цикл
		Если ПустаяСтрока(СтрокаКИ.Представление) Тогда
			Продолжить;
		КонецЕсли;
		МассивСтрок.Добавить(СтрокаКИ.Представление);
	КонецЦикла;
	
	Если Не ПустаяСтрока(Комментарий) Тогда
		МассивСтрок.Добавить(Комментарий);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ответственный) Тогда
		МассивСтрок.Добавить(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ответственный, "Наименование"));
	КонецЕсли;
	
	ОсновныеСведения = СтрСоединить(МассивСтрок, Символы.ПС);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли