
#Область ОписаниеПеременных

&НаКлиенте
Перем ДанныеСчитывателя; // Кэш данных считывателя магнитной карты

#КонецОбласти

#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнформацияУпрВалюта = Константы.ВалютаУчета.Получить();
	
	Если ЗначениеЗаполнено(Параметры.Контрагент) Тогда
		Заголовок = СтрШаблон(НСтр("ru = 'Дисконтные карты. %1'"), СокрЛП(Параметры.Контрагент.Наименование));
	Иначе
		Заголовок = НСтр("ru = 'Дисконтные карты. <Контрагент не выбран>'");
		Элементы.ОтборТолькоИменные.Видимость = Ложь;
	КонецЕсли;
	
	// ОтборПоКонтрагенту
	Список.Отбор.Элементы.Очистить();
	
	// Если контрагент не выбран, то показываем только обезличенные карты!!!
	Если Параметры.Свойство("ОтборПоКонтрагенту") И Параметры.ОтборПоКонтрагенту Тогда
		
		СЗ = Новый СписокЗначений;
		Если Не Параметры.Контрагент.Пустая() Тогда
			СЗ.Добавить(Параметры.Контрагент);
		КонецЕсли;
		СЗ.Добавить(ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ВладелецКарты", СЗ,
			ВидСравненияКомпоновкиДанных.ВСписке);
		
	КонецЕсли;
	// Конец ОтборПоКонтрагенту
	
	// ПодключаемоеОборудование
	ИспользоватьПодключаемоеОборудование = УправлениеНебольшойФирмойПовтИсп.ИспользоватьПодключаемоеОборудование();
	// Конец ПодключаемоеОборудование	
	
	// МобильныйКлиент
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокКомментарий", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РасшифровкаСпискаПроцентСкидкиИВладелец", 
			"Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РасшифровкаСпискаТелефонЭлПочта", 
			"Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РасшифровкаДокументы", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СвернутьОтборы", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияРазвернутьОтборы", 
			"Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
			"СписокКонтекстноеМенюПоказатьДополнительнуюИнформацию", "Видимость", Истина);
		
		РаботаСОтборами.НастроитьПанельОтборовМобильныйКлиент(ЭтаФорма,,,,,Истина);
		
	КонецЕсли;
	// Конец МобильныйКлиент
	
	ЦветАвтоскидки = ЦветаСтиля.ЦветНедоступногоТекстаТабличнойЧасти;
	
КонецПроцедуры

// Процедура - обработчик события ПриЗакрытии.
//
&НаКлиенте
Процедура ПриЗакрытии()
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование

КонецПроцедуры

// Процедура - обработчик события ПриЗагрузкеДанныхИзНастроекНаСервере.
//
&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец", ОтборВидКарты, , ,
		ЗначениеЗаполнено(ОтборВидКарты));
	Если ОтборТолькоИменные Тогда
		Список.Отбор.Элементы.Очистить();
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ВладелецКарты",
			Параметры.Контрагент);
	Иначе
		// Эта ветка всегда выполняется в процедуре "ПриСозданииНаСервере()", которая вызывается перед этой процедурой.
	КонецЕсли;
	// Отбор по штрихкоду и магнитному коду не сохраняем.
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "КодКартыШтрихкод", ОтборШтрихКод,
		ВидСравненияКомпоновкиДанных.Содержит, , ЗначениеЗаполнено(ОтборШтрихКод));
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "КодКартыМагнитный",
		ОтборМагнитныйКод, ВидСравненияКомпоновкиДанных.Содержит, , ЗначениеЗаполнено(ОтборМагнитныйКод));
	
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект, "СканерШтрихкода,СчитывательМагнитныхКарт");
	// Конец ПодключаемоеОборудование

КонецПроцедуры

// Процедура - обработчик события ПередЗакрытием.
//
&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	СохраняемыеВНастройкахДанныеМодифицированы = Истина;
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаОповещения.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			ОбработатьШтрихкоды(ДисконтныеКартыУНФКлиент.ПреобразоватьДанныеСоСканераВМассив(Параметр));
			ТекущийЭлемент = Элементы.ОтборШтрихКод;
		ИначеЕсли ИмяСобытия ="TracksData" Тогда
			// Обработка ситуации, когда считыватель магнитных карт имитирует нажатие клавиши "Enter" после считывания магнитной карты.
			ТекДата = ТекущаяДата();
			
			ОбработатьДанныеСчитывателяМагнитныхКарт(Параметр);
			ТекущийЭлемент = Элементы.ОтборМагнитныйКод;
			
			// Обработка ситуации, когда считыватель магнитных карт имитирует нажатие клавиши "Enter" после считывания магнитной карты.
			// Символ перевода строки можно обрезать с помощью настроек подключаемого оборудования для считывателя магнитных карт.
			Пока (ТекущаяДата() - ТекДата) < 1 Цикл КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	Если ИмяСобытия = "ПослеЗаписи_ДисконтнаяКарта" Тогда
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено И ТекущиеДанные.Ссылка = Источник Тогда
			Комментарий = СтрЗаменить(Параметр, Символы.ПС, " ");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// Обрабатывает событие активизации строки списка элементов справочника.
//
&НаКлиенте
Процедура ОбработатьАктивизациюСтрокиСписка()
	
	ПараметрыИнфоПанели = Новый Структура;
	ПараметрыИнфоПанели.Вставить("РеквизитКИ", "ВладелецКарты");
	ПараметрыИнфоПанели.Вставить("ДисконтнаяКарта", Элементы.Список.ТекущаяСтрока);
	ПараметрыИнфоПанели.Вставить("ПроцентСкидкиПоДисконтнойКарте");
	ПараметрыИнфоПанели.Вставить("Контрагент");
	ПараметрыИнфоПанели.Вставить("КонтактноеЛицо");
	ДисконтныеКартыУНФКлиент.ДисконтныеКартыИнформационнаяПанельОбработатьАктивизациюСтрокиСписка(ЭтотОбъект,
		ПараметрыИнфоПанели);
	
	Если ИнформацияПроцентСкидкиПоДисконтнойКарте = "<Автоскидки>" Тогда
		Элементы.РасшифровкаПроцентСкидкиПоДисконтнойКарте.ЦветТекста = ЦветАвтоскидки;
	Иначе
		Элементы.РасшифровкаПроцентСкидкиПоДисконтнойКарте.ЦветТекста = Новый Цвет;
	КонецЕсли;
	
КонецПроцедуры // ОбработатьАктивизациюСтрокиСписка()

#Область ОбработчикиСобытийДинамическогоСписка

// Процедура - обработчик события ПриАктивизацииСтроки динамического списка Список.
//
&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если ТипЗнч(Элемент.ТекущаяСтрока) <> Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		
		Если Элемент.ТекущиеДанные = Неопределено Тогда
			Комментарий = "";
		Иначе
			Комментарий = СтрЗаменить(Элемент.ТекущиеДанные.Комментарий, Символы.ПС, " ");
		КонецЕсли;
		
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ОбработатьАктивизациюСтрокиСписка", 0.2, Истина);
	
КонецПроцедуры

#КонецОбласти

// Процедура - обработчик события ПриИзменении элемента ОтборВидКарты.
//
&НаКлиенте
Процедура ОтборВидКартыПриИзменении(Элемент)
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени("СписокДисконтныхКартОтборВидКарты");
	// Конец СтандартныеПодсистемы.ОценкаПроизводительности
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец", ОтборВидКарты, , ,
		ЗначениеЗаполнено(ОтборВидКарты));
	
	#Если МобильныйКлиент Тогда
	ОбновитьЗаголовокПанелиПослеУстановкиОтбора();
	#КонецЕсли
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении элемента ОтборТолькоИменные.
//
&НаКлиенте
Процедура ОтборТолькоИменныеПриИзменении(Элемент)
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени("СписокДисконтныхКартОтборВидКарты");
	// Конец СтандартныеПодсистемы.ОценкаПроизводительности
	
	Если ОтборТолькоИменные Тогда
		Список.Отбор.Элементы.Очистить();
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ВладелецКарты",
			Параметры.Контрагент);
	Иначе
		Список.Отбор.Элементы.Очистить();
		СЗ = Новый СписокЗначений;
		СЗ.Добавить(Параметры.Контрагент);
		СЗ.Добавить(ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
		
		// Если контрагент не выбран, то показываем только обезличенные карты!!!
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ВладелецКарты", СЗ,
			ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	
	#Если МобильныйКлиент Тогда
		ОбновитьЗаголовокПанелиПослеУстановкиОтбора();
	#КонецЕсли
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении элемента ОтборШтрихКод.
//
&НаКлиенте
Процедура ОтборШтрихКодПриИзменении(Элемент)
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени("СписокДисконтныхКартОтборШтрихКод");
	// Конец СтандартныеПодсистемы.ОценкаПроизводительности
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "КодКартыШтрихкод", ОтборШтрихКод,
		ВидСравненияКомпоновкиДанных.Содержит, , ЗначениеЗаполнено(ОтборШтрихКод));
	
	#Если МобильныйКлиент Тогда
	ОбновитьЗаголовокПанелиПослеУстановкиОтбора();
	#КонецЕсли
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении элемента ОтборМагнитныйКод.
//
&НаКлиенте
Процедура ОтборМагнитныйкодПриИзменении(Элемент)
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени("СписокДисконтныхКартОтборМагнитныйКод");
	// Конец СтандартныеПодсистемы.ОценкаПроизводительности
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "КодКартыМагнитный",
		ОтборМагнитныйКод, ВидСравненияКомпоновкиДанных.Содержит, , ЗначениеЗаполнено(ОтборМагнитныйКод));
	
	#Если МобильныйКлиент Тогда
	ОбновитьЗаголовокПанелиПослеУстановкиОтбора();
	#КонецЕсли
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку ОтправитьEmailКонтрагенту.
//
&НаКлиенте
Процедура ОтправитьEmailКонтрагенту(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если СписокТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Получатели = Новый Массив;
	Если ЗначениеЗаполнено(ИнформацияКонтрагентЭП) Тогда
		СтруктураПолучателя = Новый Структура;
		СтруктураПолучателя.Вставить("Представление", СписокТекущиеДанные.Ссылка);
		СтруктураПолучателя.Вставить("Адрес", ИнформацияКонтрагентЭП);
		Получатели.Добавить(СтруктураПолучателя);
	КонецЕсли;
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Получатель", Получатели);
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыОтправки);
	
КонецПроцедуры // ОтправитьEmailКонтрагенту()

&НаКлиенте
Процедура СвернутьРазвернутьПанельОтборов(Элемент)
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфо.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость);
		
КонецПроцедуры

#КонецОбласти

#Область МеткиОтборов

&НаСервере
Процедура ОбновитьЗаголовокПанелиПослеУстановкиОтбора()
	
	РеквизитыОтбора = "ОтборТолькоИменные, ОтборШтрихКод, ОтборМагнитныйКод, ОтборВидКарты";
	РаботаСОтборами.УстановитьЗаголовокПравойПанелиМобильныйКлиент(ЭтотОбъект, "ПраваяПанель", "ДанныеМеток",
		РеквизитыОтбора);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПродажиПоДисконтнойКарте(Команда)
	
	Если Не ЗначениеЗаполнено(Элементы.Список.ТекущаяСтрока) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючВарианта", "ПродажиПоДисконтнойКарте");
	ПараметрыФормы.Вставить("Отбор", Новый Структура("СтПериод,ДисконтнаяКарта", Новый СтандартныйПериод, Элементы.Список.ТекущаяСтрока));
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.ПродажиПоДисконтнымКартам.Форма", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДополнительнуюИнформацию(Команда)
	
	Если Не ЗначениеЗаполнено(Элементы.Список.ТекущаяСтрока) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ДисконтнаяКарта", Элементы.Список.ТекущаяСтрока);
	ПараметрыОткрытия.Вставить("ИнформацияКонтрагентТелефон", ИнформацияКонтрагентТелефон);
	ПараметрыОткрытия.Вставить("ИнформацияКонтрагентЭП", ИнформацияКонтрагентЭП);
	ПараметрыОткрытия.Вставить("ИнформацияПроцентСкидкиПоДисконтнойКарте", ИнформацияПроцентСкидкиПоДисконтнойКарте);
	ПараметрыОткрытия.Вставить("ИнформацияСуммаПродажПоДисконтнойКарте", ИнформацияСуммаПродажПоДисконтнойКарте);
	ПараметрыОткрытия.Вставить("ИнформацияУпрВалюта", ИнформацияУпрВалюта);
	ПараметрыОткрытия.Вставить("ЭлементыСписокТекущиеДанныеВладелецКарты", Элементы.Список.ТекущиеДанные.ВладелецКарты);
	
	ОткрытьФорму("Обработка.ДисконтныеКарты.Форма.ДополнительнаяИнформация_МК", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ШтрихкодыИТорговоеОборудование

// Процедура обрабатывает данные штрихкода, которые передаются из обработки оповещения формы.
//
&НаКлиенте
Процедура ОбработатьШтрихкоды(ДанныеШтрихкодов)
	
	Если ТипЗнч(ДанныеШтрихкодов) = Тип("Массив") Тогда
		МассивШтрихкодов = ДанныеШтрихкодов;
	Иначе
		МассивШтрихкодов = Новый Массив;
		МассивШтрихкодов.Добавить(ДанныеШтрихкодов);
	КонецЕсли;
	
	ОбработатьПолученныйКодНаКлиенте(МассивШтрихкодов[0].Штрихкод, ПредопределенноеЗначение("Перечисление.ТипыКодовКарт.Штрихкод"), Ложь);
	
КонецПроцедуры

// Процедура обрабатывает данные со считывателя магнитных карт, которые передаются из обработки оповещения формы.
//
&НаКлиенте
Процедура ОбработатьДанныеСчитывателяМагнитныхКарт(Данные)
	
	ДанныеСчитывателя = Данные;
	ОбработатьПолученныйКодНаКлиенте(ДанныеСчитывателя, ПредопределенноеЗначение("Перечисление.ТипыКодовКарт.МагнитныйКод"), Истина);
	
КонецПроцедуры

// Функция проверяет магнитный код на соответствие шаблону и возвращает список ДК, магнитный код или штрихкод.
//
&НаСервере
Функция ОбработатьПолученныйКодНаСервере(Данные, ТипКодаКарты, Предобработка, ЕстьНайденныеКарты = Ложь, ЕстьШаблон = Ложь)
	
	ЕстьНайденныеКарты = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТипКода = ТипКодаКарты;
	Если ТипКода = Перечисления.ТипыКодовКарт.МагнитныйКод Тогда
		// При вызове функции параметр "Предобработка" будем устанавливать в значение Ложь, чтобы не использовались шаблоны
		// магнитных карт. В качестве кода карты будет использоваться строка, полученная конкатенацией строк со всех магнитных дорожек.
		// В большинстве дисконтных карт используется только одна дорожка, на которой записан только номер карты в формате ";КодКарты?".
		Если Предобработка Тогда
			ТекКодКарты = Данные[0]; // Данные 3х дорожек магнитной карты. На данный момент не используется. Можно использовать если карта не найдена.
			ДисконтныеКартыСтруктура = ДисконтныеКартыУНФВызовСервера.НайтиВидыДисконтныхКартПоДаннымСоСчитывателяМагнитныхКарт(Данные, ТипКода, Справочники.ВидыДисконтныхКарт.ПустаяСсылка());
			
			Возврат ДисконтныеКартыСтруктура;
		Иначе
			Если ТипЗнч(Данные) = Тип("Массив") Тогда
				ТекКодКарты = Данные[0];
			Иначе
				ТекКодКарты = Данные;
			КонецЕсли;
			ДисконтныеКартыУНФВызовСервера.ПодготовитьКодКартыПоНастройкамПоУмолчанию(ТекКодКарты);
			
			Возврат ТекКодКарты;
		КонецЕсли;
	Иначе
		Возврат Данные;
	КонецЕсли;
	
КонецФункции

// Функция проверяет магнитный код на соответствие шаблону и устанавливает магнитный код или штрихкод элемента справочника.
// Если ДК несколько, то пользователь выбирает нужный код из списка.
//
&НаКлиенте
Процедура ОбработатьПолученныйКодНаКлиенте(Данные, ПолученныйТипКода, Предобработка)
	
	Перем ЕстьНайденныеКарты, ЕстьШаблон;
	
	Результат = ОбработатьПолученныйКодНаСервере(Данные, ПолученныйТипКода, Предобработка, ЕстьНайденныеКарты,
		ЕстьШаблон);
	Если ПолученныйТипКода = ПредопределенноеЗначение("Перечисление.ТипыКодовКарт.МагнитныйКод") Тогда
		Если ТипЗнч(Результат) = Тип("Строка") Тогда
			ОтборМагнитныйКод = Результат;
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "КодКартыМагнитный",
				ОтборМагнитныйКод, ВидСравненияКомпоновкиДанных.Содержит, , ЗначениеЗаполнено(ОтборМагнитныйКод));
		Иначе
			Если Результат.Количество() = 1 Тогда
				ОтборМагнитныйКод = Результат.Получить(0).Значение;
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "КодКартыМагнитный",
					ОтборМагнитныйКод, ВидСравненияКомпоновкиДанных.Содержит, , ЗначениеЗаполнено(ОтборМагнитныйКод));
			ИначеЕсли Результат.Количество() = 0 Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(
					НСтр("ru = 'Код карты не соответствует ни одному из шаблонов магнитных карт.'"));
			Иначе
				Оповещение = Новый ОписаниеОповещения("ОбработатьПолученныйКодНаКлиентеЗавершение", ЭтотОбъект);
				Результат.ПоказатьВыборЭлемента(Оповещение, НСтр("ru = 'Выбор кода магнитной карты'"));
			КонецЕсли;
		КонецЕсли;
	Иначе
		ОтборШтрихКод = Результат;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "КодКартыШтрихкод",
			ОтборШтрихКод, ВидСравненияКомпоновкиДанных.Содержит, , ЗначениеЗаполнено(ОтборШтрихКод));
	КонецЕсли;
	
КонецПроцедуры

// Обработка выбора кода магнитной карты из списка, если найдено несколько дисконтных карт.
//
&НаКлиенте
Процедура ОбработатьПолученныйКодНаКлиентеЗавершение(ВыбранныйЭлемент, Параметры) Экспорт
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ОтборМагнитныйКод = ВыбранныйЭлемент.Значение;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "КодКартыМагнитный",
			ОтборМагнитныйКод, ВидСравненияКомпоновкиДанных.Содержит, , ЗначениеЗаполнено(ОтборМагнитныйКод));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецОбласти
