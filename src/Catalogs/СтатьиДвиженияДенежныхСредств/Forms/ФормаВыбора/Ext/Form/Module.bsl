
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	УстановитьОтборНедействительные(ЭтотОбъект);
	ВосстановитьНастройкиФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СвернутьПравуюПанельНажатие(Элемент)
	
	Элементы.ГруппаСтраницы.Видимость = Не Элементы.ГруппаСтраницы.Видимость;
	Элементы.ДекорацияРазвернутьПравуюПанель.Видимость = Не Элементы.ГруппаСтраницы.Видимость;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьПравуюПанельНажатие(Элемент)
	
	Элементы.ГруппаСтраницы.Видимость = Не Элементы.ГруппаСтраницы.Видимость;
	Элементы.ДекорацияРазвернутьПравуюПанель.Видимость = Не Элементы.ДекорацияРазвернутьПравуюПанель.Видимость;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьНедействительных(Команда)
	
	Элементы.ПоказыватьНедействительных.Пометка = Не Элементы.ПоказыватьНедействительных.Пометка;
	
	УстановитьОтборНедействительные(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗакрытьПодсказкуНажатие(Элемент)
	
	Элементы.ГруппаПодсказкаАссистента.Видимость = Ложь;
	СохранитьЗначениеНастройкиФормы("_ОтображатьПодсказкуАссистента", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияГиперссылкаДобавитьОписаниеНажатие(Элемент)
	
	Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда
		
		СсылкаНаТекущийЭлемент = Элементы.Список.ТекущиеДанные.Ссылка;
		ПараметрыОткрытия = Новый Структура("Ключ, АктивизироватьОписание", СсылкаНаТекущийЭлемент, Истина);
		
		ПослеЗакрытияФормыСтатьи = Новый ОписаниеОповещения("ПослеЗакрытияФормыСтатьи", ЭтотОбъект);
		Если Элементы.Список.ТекущиеДанные.ЭтоГруппа Тогда
			ОткрытьФорму("Справочник.СтатьиДвиженияДенежныхСредств.Форма.ФормаГруппы", ПараметрыОткрытия,,,,,ПослеЗакрытияФормыСтатьи);
		Иначе
			ОткрытьФорму("Справочник.СтатьиДвиженияДенежныхСредств.Форма.ФормаЭлемента", ПараметрыОткрытия,,,,,ПослеЗакрытияФормыСтатьи);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если Элемент <> Неопределено И Элемент.ТекущиеДанные <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.Описание) Тогда
			ТекстОписания = Элемент.ТекущиеДанные.Описание;
			Гиперссылка = НСтр("ru = '<span style=""color: ПанельНавигацииНеактивнаяСсылка"">Изменить описание</span>'");
			ТекстРедактированияОписания = СтроковыеФункцииКлиент.ФорматированнаяСтрока(Гиперссылка);
		Иначе
			ТекстОписания = НСтр("ru = 'Здесь могло быть полезное описание текущей статьи, которое поможет избежать ошибок'");
			Гиперссылка = НСтр("ru = '<span style=""color: ПанельНавигацииНеактивнаяСсылка"">Добавить описание</span>'");
			ТекстРедактированияОписания = СтроковыеФункцииКлиент.ФорматированнаяСтрока(Гиперссылка);
		КонецЕсли;
		
		Элементы.ДекорацияЗаголовокТекущегоПоказателя.Заголовок = ВРег(Элемент.ТекущиеДанные.Наименование);
		Элементы.ДекорацияОписаниеТекущегоПоказателя.Заголовок = ТекстОписания;
		Элементы.ДекорацияГиперссылкаДобавитьОписание.Заголовок = ТекстРедактированияОписания;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// 1. Недействительная номенклатура отображается серым
	НовоеУсловноеОформление = Список.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы.Добавить();
	
	Оформление = НовоеУсловноеОформление.Оформление.Элементы.Найти("ЦветТекста");
	Оформление.Значение 		= ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет;
	Оформление.Использование 	= Истина;
	
	Отбор = НовоеУсловноеОформление.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование 	= Истина;
	Отбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Недействителен");
	Отбор.ПравоеЗначение 	= Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборНедействительные(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.Список,
		"Недействителен",
		Ложь,
		,
		,
		Не Форма.Элементы.ПоказыватьНедействительных.Пометка);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьЗначениеНастройкиФормы(ИмяОпции, ЗначениеОпции)
	
	ИмяКлючаОбъекта = Справочники.СтатьиДвиженияДенежныхСредств.ПолучитьКлючОбъекта();
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяКлючаОбъекта, ИмяКлючаОбъекта+ИмяОпции, ЗначениеОпции);
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройкиФормы()
	
	ИмяКлючаОбъекта = Справочники.СтатьиДвиженияДенежныхСредств.ПолучитьКлючОбъекта();
	ИмяОпции = ИмяКлючаОбъекта+"_ОтображатьПодсказкуАссистента";
	ОтображатьПодсказку = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяКлючаОбъекта, ИмяОпции);
	Если ОтображатьПодсказку <> Неопределено Тогда
		Элементы.ГруппаПодсказкаАссистента.Видимость = ОтображатьПодсказку;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыСтатьи(ПараметрЗакрытия, ДополнительныйПараметр) Экспорт
	
	СписокПриАктивизацииСтроки(Элементы.Список);
	
КонецПроцедуры

#КонецОбласти
