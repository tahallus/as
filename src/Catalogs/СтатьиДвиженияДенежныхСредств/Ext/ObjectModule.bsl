#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РодительДолженБытьЗаполнен = НЕ Справочники.СтатьиДвиженияДенежныхСредств.ЭтоГруппаВидаДеятельности(Ссылка);
	
	Если Справочники.СтатьиДвиженияДенежныхСредств.ИзмененРодительПредопределеннойГруппы(Ссылка, Родитель)
		ИЛИ (РодительДолженБытьЗаполнен И Родитель.Пустая()) ИЛИ (НЕ РодительДолженБытьЗаполнен И НЕ Родитель.Пустая()) Тогда
		
		ТекстСообщения = НСтр("ru = 'Родителя данной группы менять запрещено.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда
		
		Если Не ПустаяСтрока(КорреспондирующийСчет) Тогда
			Если ПустаяСтрока(СтрЗаменить(КорреспондирующийСчет, ".", "")) Тогда
				КорреспондирующийСчет = "";
			ИначеЕсли Прав(СокрЛП(КорреспондирующийСчет), 1) = "." Тогда
				КорреспондирующийСчет = Лев(СокрЛП(КорреспондирующийСчет), СтрДлина(СокрЛП(КорреспондирующийСчет)) - 1);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли