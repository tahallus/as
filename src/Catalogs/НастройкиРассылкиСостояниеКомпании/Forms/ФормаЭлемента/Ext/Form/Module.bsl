
#Область ОписаниеПеременных

&НаКлиенте
Перем ИмяЗаданияПредварительногоПросмотра;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ПриСозданииПриЧтенииНаСервере(ТекущийОбъектИзРеквизитаФормы());
	КонецЕсли;
	
	ПериодОтчета = ТекущаяДатаСеанса();
	
	Если Константы.УчетПоКомпании.Получить() Тогда
		Элементы.Организация.Видимость = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.УчетнаяЗапись) Тогда
		Элементы.УчетнаяЗапись.Видимость = Ложь;
	КонецЕсли;
	
	ЗаполнитьИнформациюОПрограмме();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.СтраницыПредварительныйПросмотр.ТекущаяСтраница = Элементы.СтраницаПустая;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииПриЧтенииНаСервере(ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НеЗаполненаОрганизация() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		НСтр("ru = 'Не заполнена организация.'"),,
		"Организация",
		"Объект",
		Отказ);
		Возврат;
	КонецЕсли;

	Если АдресаПолучателейСодержатОшибки() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Объект.Использование = Не ПараметрыДляОтправкиСодержатОшибки();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Получатели.Очистить();
	
	Для Каждого ТекСтрокаПолучатели Из Получатели Цикл
		Если ЗначениеЗаполнено(ТекСтрокаПолучатели.Получатель) Тогда
			ТекСтрокаОбъектПолучатели = ТекущийОбъект.Получатели.Добавить();
			ТекСтрокаОбъектПолучатели.Получатель = ТекСтрокаПолучатели.Получатель;
		КонецЕсли;
	КонецЦикла;
	
	ТекущийОбъект.Расписание = Новый ХранилищеЗначения(Расписание);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СохранениеНастройки" Тогда
		ОбработатьСохранениеНастройки(Параметр);
	КонецЕсли;
	
	Если ИмяСобытия = "СохранениеНастройкиСекции" Тогда
		ОбработатьСохранениеНастройкиСекции(Параметр);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПолучательАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Текст) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбора = ДанныеАвтоПодбораАдресаПолучателя(Текст);
	СтандартнаяОбработка = Не ЗначениеЗаполнено(ДанныеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательПриИзменении(Элемент)
	
	// В веб-клиенте требуется явно взводить флаг при изменении
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.КлассификаторКонтактов") Тогда
		ВыбранноеЗначение = АдресЭПИзКлассификатора(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСекцийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ПараметрыФормы = Новый Структура;
	Если Копирование Тогда
		ПараметрыФормы.Вставить("ЗаголовокСекции", Элемент.ТекущиеДанные.ЗаголовокСекции);
		ПараметрыФормы.Вставить("ТипСекции", Элемент.ТекущиеДанные.ТипСекции);
		
		ПараметрыФормы.Вставить("НастройкиФильтров", Новый Массив);
		Для Каждого ТекСтрокаНастройкиФильтров Из Объект.НастройкиФильтров.НайтиСтроки(Новый Структура("ИдентификаторСекции", Элемент.ТекущиеДанные.ИдентификаторСекции)) Цикл
			НастройкаФильтра = Новый Структура;
			НастройкаФильтра.Вставить("ИмяОбласти", ТекСтрокаНастройкиФильтров.ИмяОбласти);
			НастройкаФильтра.Вставить("ИмяПоля", ТекСтрокаНастройкиФильтров.ИмяПоля);
			НастройкаФильтра.Вставить("Путь", ТекСтрокаНастройкиФильтров.Путь);
			НастройкаФильтра.Вставить("ПредставлениеПоля", ТекСтрокаНастройкиФильтров.ПредставлениеПоля);
			НастройкаФильтра.Вставить("ВидСравнения", ТекСтрокаНастройкиФильтров.ВидСравнения);
			НастройкаФильтра.Вставить("Значение", ТекСтрокаНастройкиФильтров.Значение);
			ПараметрыФормы.НастройкиФильтров.Добавить(НастройкаФильтра);
		КонецЦикла;
		
	КонецЕсли;
	ОткрытьФорму("Справочник.НастройкиРассылкиСостояниеКомпании.Форма.ФормаНастройкиСекции", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСекцийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИдентификаторСекции", Элемент.ТекущиеДанные.ИдентификаторСекции);
	ПараметрыФормы.Вставить("ТипСекции", Элемент.ТекущиеДанные.ТипСекции);
	ПараметрыФормы.Вставить("ЗаголовокСекции", Элемент.ТекущиеДанные.ЗаголовокСекции);
	ПараметрыФормы.Вставить("ВидЦенПродажи", Элемент.ТекущиеДанные.ВидЦенПродажи);
	
	ПараметрыФормы.Вставить("НастройкиФильтров", Новый Массив);
	Для Каждого ТекСтрокаНастройкиФильтров Из Объект.НастройкиФильтров.НайтиСтроки(Новый Структура("ИдентификаторСекции", Элемент.ТекущиеДанные.ИдентификаторСекции)) Цикл
		НастройкаФильтра = Новый Структура;
		НастройкаФильтра.Вставить("ИмяОбласти", ТекСтрокаНастройкиФильтров.ИмяОбласти);
		НастройкаФильтра.Вставить("ИмяПоля", ТекСтрокаНастройкиФильтров.ИмяПоля);
		НастройкаФильтра.Вставить("Путь", ТекСтрокаНастройкиФильтров.Путь);
		НастройкаФильтра.Вставить("ПредставлениеПоля", ТекСтрокаНастройкиФильтров.ПредставлениеПоля);
		НастройкаФильтра.Вставить("ВидСравнения", ТекСтрокаНастройкиФильтров.ВидСравнения);
		НастройкаФильтра.Вставить("Значение", ТекСтрокаНастройкиФильтров.Значение);
		ПараметрыФормы.НастройкиФильтров.Добавить(НастройкаФильтра);
	КонецЦикла;
	
	ОткрытьФорму("Справочник.НастройкиРассылкиСостояниеКомпании.Форма.ФормаНастройкиСекции", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Настройки(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПериодОтчета", ПериодОтчета);
	ПараметрыФормы.Вставить("УчетнаяЗапись", Объект.УчетнаяЗапись);
	
	ОткрытьФорму(
	"Справочник.НастройкиРассылкиСостояниеКомпании.Форма.ФормаНастройки",
	ПараметрыФормы,
	ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПолучателя(Команда)
	
	ДобавитьПолучателяНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРасписание(Команда)
	
	ДиалогНастройкиРасписания = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	ДиалогНастройкиРасписания.Показать(Новый ОписаниеОповещения("ОбработатьИзменениеРасписания", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотр(Команда)
	
	ЗапуститьПредварительныйПросмотр("НаЭкране");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочте(Команда)
	
	Если ПараметрыДляОтправкиСодержатОшибки() Тогда
		Возврат;
	КонецЕсли;
	
	Если АдресаПолучателейСодержатОшибки() Тогда
		Возврат;
	КонецЕсли;
	
	ЗапуститьПредварительныйПросмотр("ПоЭлектроннойПочте");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьИнформациюОПрограмме()
	
	СисИнфо = Новый СистемнаяИнформация;
	
	ИнформацияОПрограмме = СтрШаблон(
	НСтр("ru = 'Конфигурация: %1 (%2)
	|Платформа: 1С:Предприятие (%3)
	|Идентификатор базы: %4'"),
	Метаданные.Синоним,
	Метаданные.Версия,
	СисИнфо.ВерсияПриложения,
	СтандартныеПодсистемыСервер.ИдентификаторИнформационнойБазы());
	
КонецПроцедуры

&НаСервере
Функция ТекущийОбъектИзРеквизитаФормы()
	
	Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Результат = Параметры.ЗначениеКопирования.ПолучитьОбъект();
	Иначе
		Результат = РеквизитФормыВЗначение("Объект");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПараметрыДляОтправкиСодержатОшибки()
	
	Перем Ошибки;
	
	Если Не ЗначениеЗаполнено(Объект.УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
		Ошибки,
		"Объект.УчетнаяЗапись",
		НСтр("ru = 'Не указана учетная запись для отправки почты.'"),
		Неопределено);
	КонецЕсли;
	
	Если Не УказанХотяБыОдинПолучатель() Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
		Ошибки,
		"Получатели[%1].Получатель",
		НСтр("ru = 'Не указан хотя бы один адрес электронной почты получателя.'"),
		Неопределено);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	
	Возврат ЗначениеЗаполнено(Ошибки);
	
КонецФункции

&НаКлиенте
Функция АдресаПолучателейСодержатОшибки()
	
	Перем Ошибки;
	
	Для Индекс = 0 По Получатели.Количество() - 1 Цикл
		
		Если Не ЗначениеЗаполнено(Получатели[Индекс].Получатель) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(
			Получатели[Индекс].Получатель) Тогда
			Продолжить;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
		Ошибки,
		"Получатели[%1].Получатель",
		НСтр("ru = 'Адрес электронной почты указан некорректно.'"),
		"Получатели",
		Индекс,
		НСтр("ru = 'Адрес электронной почты в строке %1 указан некорректно.'"),
		Индекс);
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	
	Возврат ЗначениеЗаполнено(Ошибки);
	
КонецФункции

&НаКлиенте
Функция УказанХотяБыОдинПолучатель()
	
	Для Каждого ТекСтрокаПолучатели Из Получатели Цикл
		
		Если ЗначениеЗаполнено(ТекСтрокаПолучатели.Получатель) Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура СоздатьЭлементыПолучатели(ТекущийОбъект)
	
	Получатели.Очистить();
	
	ЭлементыКУдалению = Новый Массив;
	
	Для Каждого ТекЭлемент Из Элементы.Получатели.ПодчиненныеЭлементы Цикл
		
		Если ТекЭлемент = Элементы.Получатель Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементыКУдалению.Добавить(ТекЭлемент);
		
	КонецЦикла;
	
	Для Каждого ТекЭлемент Из ЭлементыКУдалению Цикл
		Элементы.Удалить(ТекЭлемент);
	КонецЦикла;
	
	Для Каждого ТекСтрокаОбъектПолучатели Из ТекущийОбъект.Получатели Цикл
		Если ЗначениеЗаполнено(ТекСтрокаОбъектПолучатели.Получатель) Тогда
			ТекСтрокаПолучатели = Получатели.Добавить();
			ТекСтрокаПолучатели.Получатель = ТекСтрокаОбъектПолучатели.Получатель;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(Получатели) Тогда
		Получатели.Добавить();
	КонецЕсли;
	
	Для ТекИндекс = 1 По Получатели.Количество() - 1 Цикл
		ДобавитьЭлементПолучатель(ТекИндекс);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере(ТекущийОбъект)
	
	Если Параметры.Свойство("ЗначениеКопирования")
		И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Объект.Наименование = СостояниеКомпании.АвтоНаименованиеНастройки();
	КонецЕсли;
	
	СоздатьЭлементыПолучатели(ТекущийОбъект);
	
	ПрочитанноеРасписание = ТекущийОбъект.Расписание.Получить();
	Если ТипЗнч(ПрочитанноеРасписание) = Тип("РасписаниеРегламентногоЗадания") Тогда
		Расписание = ПрочитанноеРасписание;
	Иначе
		Расписание = СостояниеКомпании.РасписаниеПоУмолчанию();
	КонецЕсли;
	
	ОбновитьЗаголовокРасписания();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПолучателяНаСервере()
	
	СтрокаПолучатель = Получатели.Добавить();
	
	ДобавитьЭлементПолучатель(Получатели.Количество() - 1);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементПолучатель(Знач ИндексЭлемента)
	
	ИмяЭлемента = СтрШаблон("Получатель%1", Формат(ИндексЭлемента, "ЧГ="));
	
	ЭлементПолучатель = Элементы.Добавить(ИмяЭлемента, Тип("ПолеФормы"), Элементы.Получатели);
	ЭлементПолучатель.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементПолучатель.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементПолучатель.ПутьКДанным = СтрШаблон("Получатели[%1].Получатель", Формат(ИндексЭлемента, "ЧГ="));
	ЭлементПолучатель.ПодсказкаВвода = СтрШаблон(Элементы.Получатель.ПодсказкаВвода);
	ЭлементПолучатель.АвтоМаксимальнаяШирина = Элементы.Получатель.АвтоМаксимальнаяШирина;
	ЭлементПолучатель.МаксимальнаяШирина = Элементы.Получатель.МаксимальнаяШирина;
	ЭлементПолучатель.УстановитьДействие("ПриИзменении", "ПолучательПриИзменении");
	ЭлементПолучатель.УстановитьДействие("ОбработкаВыбора", "ПолучательОбработкаВыбора");
	ЭлементПолучатель.УстановитьДействие("АвтоПодбор", "ПолучательАвтоПодбор");
	
	ТекущийЭлемент = ЭлементПолучатель;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеРасписания(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("РасписаниеРегламентногоЗадания") Тогда
		ОбработатьИзменениеРасписанияНаСервере(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеРасписанияНаСервере(Знач НовоеРасписание) Экспорт
	
	Расписание = НовоеРасписание;
	
	Расписание.ДетальныеРасписанияДня = Новый Массив;
	
	Если ЗначениеЗаполнено(Расписание.ПериодПовтораВТечениеДня)
		И Расписание.ПериодПовтораВТечениеДня < 60 * 60 Тогда
		Расписание.ПериодПовтораВТечениеДня = 60 * 60;
	КонецЕсли;
	
	ОбновитьЗаголовокРасписания();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьСохранениеНастройки(Знач ДанныеДляСохранения)
	
	ПериодОтчета = ДанныеДляСохранения.ПериодОтчета;
	
	Если Объект.УчетнаяЗапись <> ДанныеДляСохранения.УчетнаяЗапись Тогда
		Модифицированность = Истина;
		Объект.УчетнаяЗапись = ДанныеДляСохранения.УчетнаяЗапись;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьСохранениеНастройкиСекции(Знач ДанныеДляСохранения)
	
	Модифицированность = Истина;
	
	НайденныеСтрокиНастройкиСекций = Объект.НастройкиСекций.НайтиСтроки(Новый Структура("ИдентификаторСекции", ДанныеДляСохранения.ИдентификаторСекции));
	Если ЗначениеЗаполнено(НайденныеСтрокиНастройкиСекций) Тогда
		ТекНастройкаСекции = НайденныеСтрокиНастройкиСекций[0];
	Иначе
		ТекНастройкаСекции = Объект.НастройкиСекций.Добавить();
		ТекНастройкаСекции.ИдентификаторСекции = ДанныеДляСохранения.ИдентификаторСекции;
	КонецЕсли;
	
	ТекНастройкаСекции.ЗаголовокСекции = ДанныеДляСохранения.ЗаголовокСекции;
	ТекНастройкаСекции.ТипСекции = ДанныеДляСохранения.ТипСекции;
	ТекНастройкаСекции.ВидЦенПродажи = ДанныеДляСохранения.ВидЦенПродажи;
	
	Для Каждого ТекСтрока Из Объект.НастройкиФильтров.НайтиСтроки(Новый Структура("ИдентификаторСекции", ДанныеДляСохранения.ИдентификаторСекции)) Цикл
		Объект.НастройкиФильтров.Удалить(ТекСтрока);
	КонецЦикла;
	
	Для Каждого ТекФильтр Из ДанныеДляСохранения.НастройкиФильтров Цикл
		ТекСтрокаНастройкиФильтров = Объект.НастройкиФильтров.Добавить();
		ТекСтрокаНастройкиФильтров.ИдентификаторСекции = ДанныеДляСохранения.ИдентификаторСекции;
		ТекСтрокаНастройкиФильтров.ИмяОбласти = ТекФильтр.ИмяОбласти;
		ТекСтрокаНастройкиФильтров.ИмяПоля = ТекФильтр.ИмяПоля;
		ТекСтрокаНастройкиФильтров.Путь = ТекФильтр.Путь;
		ТекСтрокаНастройкиФильтров.ПредставлениеПоля = ТекФильтр.ПредставлениеПоля;
		ТекСтрокаНастройкиФильтров.ВидСравнения = ТекФильтр.ВидСравнения;
		ТекСтрокаНастройкиФильтров.Значение = ТекФильтр.Значение;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьПредварительныйПросмотр(ИмяВарианта)
	
	Если НеЗаполненаОрганизация() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		НСтр("ru = 'Не заполнена организация.'"),,
		"Организация",
		"Объект");
		Возврат;
	КонецЕсли;
	
	Элементы.СтраницыПредварительныйПросмотр.ТекущаяСтраница = Элементы.СтраницаОтчетФормируется;
	Элементы.ГруппаПредварительныйПросмотр.Доступность = Ложь;
	ИмяЗаданияПредварительногоПросмотра = СостояниеКомпанииКлиентСервер.ВариантыПредварительногоПросмотра()[ИмяВарианта];
	ПодключитьОбработчикОжидания("ЗапуститьПредварительныйПросмотрПослеПаузы", 2, Истина);
	
КонецПроцедуры

&НаКлиенте
Функция НеЗаполненаОрганизация()
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЗначениеСвойстваЭлементаФормы(Элементы, "Организация", "Видимость") <> Истина Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьПредварительныйПросмотрПослеПаузы()
	
	ЗаданиеПредварительныйПросмотр = ЗапуститьПредварительныйПросмотрНаСервере(ИмяЗаданияПредварительногоПросмотра);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
	ЗаданиеПредварительныйПросмотр,
	Новый ОписаниеОповещения("ОбработатьЗавершениеЗадания", ЭтотОбъект),
	ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьПредварительныйПросмотрНаСервере(ИмяЗадания)
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ПериодОтчета", ПериодОтчета);
	ПараметрыПроцедуры.Вставить("УчетнаяЗапись", Объект.УчетнаяЗапись);
	ПараметрыПроцедуры.Вставить("Организация", Объект.Организация);
	ПараметрыПроцедуры.Вставить("Получатели", СостояниеКомпании.ПолучателиОтчета(Получатели.Выгрузить()));
	ПараметрыПроцедуры.Вставить("Секции", СостояниеКомпании.СекцииОтчета(Объект.НастройкиФильтров.Выгрузить(), Объект.НастройкиСекций.Выгрузить()));
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(ИмяЗадания, ПараметрыПроцедуры, ПараметрыВыполнения);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьЗавершениеЗадания(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Результат.Свойство("Статус") Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ОтобразитьОшибку(Результат.ПодробноеПредставлениеОшибки);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаПредварительныйПросмотр.Доступность = Истина;
	
	Если ИмяЗаданияПредварительногоПросмотра = СостояниеКомпанииКлиентСервер.ВариантыПредварительногоПросмотра().ПоЭлектроннойПочте Тогда
		Элементы.СтраницыПредварительныйПросмотр.ТекущаяСтраница = Элементы.СтраницаОтчетОтправлен;
		Возврат;
	КонецЕсли;
	
	Элементы.СтраницыПредварительныйПросмотр.ТекущаяСтраница = Элементы.СтраницаПустая;
	ТабличныйДокумент = ТабличныйДокументИзРезультатаВыполненияЗадания(Результат.АдресРезультата);
	Если ТипЗнч(ТабличныйДокумент) <> Тип("ТабличныйДокумент") Тогда
		Возврат;
	КонецЕсли;
	
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.ОтображатьСетку = Ложь;
	ТабличныйДокумент.Показать(НСтр("ru = 'Состояние компании (предварительный просмотр)'"));
	
КонецПроцедуры

&НаСервере
Функция ТабличныйДокументИзРезультатаВыполненияЗадания(АдресРезультата)
	
	РезультатВыполненияЗадания = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ТипЗнч(РезультатВыполненияЗадания) = Тип("ХранилищеЗначения") Тогда
		Возврат РезультатВыполненияЗадания.Получить();
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОтобразитьОшибку(ОписаниеОшибки)
	
	Элементы.СтраницыПредварительныйПросмотр.ТекущаяСтраница = Элементы.СтраницаОшибка;
	Элементы.ДекорацияОшибка.Подсказка = ОписаниеОшибки;
	Элементы.ГруппаПредварительныйПросмотр.Доступность = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроизошлаОшибка(Знач Прогресс)
	
	Если ТипЗнч(Прогресс) <> Тип("Структура") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не Прогресс.Свойство("ДополнительныеПараметры") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не Прогресс.ДополнительныеПараметры.Свойство("ПроизошлаОшибка") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Прогресс.ДополнительныеПараметры.ПроизошлаОшибка;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДанныеАвтоПодбораАдресаПолучателя(Знач СтрокаПоиска)
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("Отбор", Новый Структура("ПометкаУдаления", Ложь));
	ПараметрыВыбора.Вставить("СтрокаПоиска", СтрокаПоиска);
	
	Возврат Справочники.КлассификаторКонтактов.ПолучитьДанныеВыбора(ПараметрыВыбора);
	
КонецФункции

&НаСервереБезКонтекста
Функция АдресЭПИзКлассификатора(Знач ЭлементКлассификатораКонтактов)
	
	Возврат Справочники.КлассификаторКонтактов.КонтактКакСвязаться(ЭлементКлассификатораКонтактов).КакСвязаться;
	
КонецФункции

&НаСервере
Процедура ОбновитьЗаголовокРасписания()
	
	Элементы.НастроитьРасписание.Заголовок = Строка(Расписание);
	
КонецПроцедуры

#КонецОбласти

#Область БольшеВозможностей

&НаКлиенте
Процедура ПредложитьВозможностьНажатие(Элемент)
	
	ТекстДляПисьма = НСтр("ru = 'Предлагаю добавить в отчет ""Состояние компании"" следующее:'");
	Тег = НСтр("ru = 'Отчет ""Состояние компании""'");
	
	УправлениеНебольшойФирмойКлиент.ПредложитьВозможностьНажатие(Тег, ТекстДляПисьма);
	
КонецПроцедуры

#КонецОбласти