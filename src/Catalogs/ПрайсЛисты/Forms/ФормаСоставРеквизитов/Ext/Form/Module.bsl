
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПрайсЛист.ПредставлениеНоменклатуры.Загрузить(Параметры.ПредставлениеНоменклатуры.Выгрузить());
	ПрайсЛист.ПредставлениеОстатков = Параметры.ПредставлениеОстатков;
	
	Параметры.Свойство("ОтборОрганизация",							ПрайсЛист.ОтборОрганизация);
	Параметры.Свойство("ОтборСклад",								ПрайсЛист.ОтборСклад);
	
	Параметры.Свойство("ТекстовоеПредставлениеОстатковМало",		ПрайсЛист.ТекстовоеПредставлениеОстатковМало);
	Параметры.Свойство("ТекстовоеПредставлениеОстатковДостаточно",	ПрайсЛист.ТекстовоеПредставлениеОстатковДостаточно);
	Параметры.Свойство("ТекстовоеПредставлениеОстатковМного",		ПрайсЛист.ТекстовоеПредставлениеОстатковМного);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаТекстПредставлений", "Доступность", (ПрайсЛист.ПредставлениеОстатков = 2));
	
	ПрайсЛист.КоличествоКолонок = Параметры.КоличествоКолонок;
	ПрайсЛист.КартинкаШирина = Параметры.КартинкаШирина;
	ПрайсЛист.КартинкаВысота = Параметры.КартинкаВысота;
	ПрайсЛист.ИзменятьРазмерПропорционально = Параметры.ИзменятьРазмерПропорционально;
	
	МассивКолонок = ПрайсЛист.ПредставлениеНоменклатуры.НайтиСтроки(Новый Структура("РеквизитНоменклатуры", "СвободныйОстаток"));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВыводитьОстатки", "Доступность", МассивКолонок[0].Использование);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрайсЛистОтборОрганизация", "Доступность", МассивКолонок[0].Использование);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрайсЛистОтборСклад", "Доступность", МассивКолонок[0].Использование);
	
	МассивКолонок = ПрайсЛист.ПредставлениеНоменклатуры.НайтиСтроки(Новый Структура("РеквизитНоменклатуры", "Картинка"));
	ИспользоватьКартинки = МассивКолонок[0].Использование;
	
	Если ИспользоватьКартинки Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрайсЛистКартинкаВысота", "Доступность", НЕ ПрайсЛист.ИзменятьРазмерПропорционально);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрайсЛистКартинкаШирина", "Доступность", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрайсЛистКартинкаВысота", "Доступность", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрайсЛистИзменятьРазмерПропорционально", "Доступность", Ложь);
		
	КонецЕсли;
	
	ФОИспользоватьХарактеристики = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики");
	МассивСтрок = ПрайсЛист.ПредставлениеНоменклатуры.НайтиСтроки(Новый Структура("РеквизитНоменклатуры", "Характеристика"));
	МассивСтрок[0].СлужебныйУправлениеВидимостью = ФОИспользоватьХарактеристики;
	
	Элементы.ПрайсЛистПредставлениеНоменклатуры.ОтборСтрок = Новый ФиксированнаяСтруктура("СлужебныйУправлениеВидимостью", Истина);
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		
		ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Авто;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Декорация2", "Видимость", Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПрайсЛистПредставлениеНоменклатурыПриИзменении(Элемент)
	
	ДанныеТекущейСтроки = Элементы.ПрайсЛистПредставлениеНоменклатуры.ТекущиеДанные;
	Если ДанныеТекущейСтроки <> Неопределено Тогда
		
		Если ДанныеТекущейСтроки.РеквизитНоменклатуры = "СвободныйОстаток" Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВыводитьОстатки", "Доступность", ДанныеТекущейСтроки.Использование);
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрайсЛистОтборОрганизация", "Доступность", ДанныеТекущейСтроки.Использование);
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрайсЛистОтборСклад", "Доступность", ДанныеТекущейСтроки.Использование);
			
			Если ДанныеТекущейСтроки.Использование = Ложь Тогда
				
				ПрайсЛист.ОтборОрганизация = Неопределено;
				ПрайсЛист.ОтборСклад = Неопределено;
				
			КонецЕсли;
			
		ИначеЕсли ДанныеТекущейСтроки.РеквизитНоменклатуры = "Картинка" Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрайсЛистКартинкаШирина", "Доступность", ДанныеТекущейСтроки.Использование);
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрайсЛистКартинкаВысота", "Доступность", ДанныеТекущейСтроки.Использование И (НЕ ПрайсЛист.ИзменятьРазмерПропорционально));
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрайсЛистИзменятьРазмерПропорционально", "Доступность", ДанныеТекущейСтроки.Использование);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрайсЛистКартинкаШиринаПриИзменении(Элемент)
	
	Если ПрайсЛист.ИзменятьРазмерПропорционально Тогда
		
		ПрайсЛист.КартинкаВысота = ПрайсЛист.КартинкаШирина * 5;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрайсЛистИзменятьРазмерПропорциональноПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрайсЛистКартинкаВысота", "Доступность", НЕ ПрайсЛист.ИзменятьРазмерПропорционально);
	Если ПрайсЛист.ИзменятьРазмерПропорционально Тогда
		
		ПрайсЛист.КартинкаВысота = ПрайсЛист.КартинкаШирина * 5;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьОстаткиПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаТекстПредставлений", "Доступность", (ПрайсЛист.ПредставлениеОстатков = 2));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	МассивСтрок = ПрайсЛист.ПредставлениеНоменклатуры.НайтиСтроки(Новый Структура("Использование", Истина));
	Отказ = (МассивСтрок.Количество() = 0);
	Если Отказ Тогда
		
		ТекстОшибки = НСтр("ru ='Необходимо выбрать минимум одно поле...'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		
	Иначе
		
		ПараметрыЗакрытия = Новый Структура;
		ПараметрыЗакрытия.Вставить("РезультатЗакрытия",							КодВозвратаДиалога.ОК);
		ПараметрыЗакрытия.Вставить("ПредставлениеНоменклатуры",					ПрайсЛист.ПредставлениеНоменклатуры);
		ПараметрыЗакрытия.Вставить("КоличествоКолонок",							ПрайсЛист.КоличествоКолонок);
		ПараметрыЗакрытия.Вставить("КартинкаШирина",							ПрайсЛист.КартинкаШирина);
		ПараметрыЗакрытия.Вставить("КартинкаВысота", 							ПрайсЛист.КартинкаВысота);
		ПараметрыЗакрытия.Вставить("ИзменятьРазмерПропорционально",				ПрайсЛист.ИзменятьРазмерПропорционально);
		ПараметрыЗакрытия.Вставить("ПредставлениеОстатков", 					ПрайсЛист.ПредставлениеОстатков);
		ПараметрыЗакрытия.Вставить("ОтборОрганизация", 							ПрайсЛист.ОтборОрганизация);
		ПараметрыЗакрытия.Вставить("ОтборСклад", 								ПрайсЛист.ОтборСклад);
		ПараметрыЗакрытия.Вставить("ТекстовоеПредставлениеОстатковМало", 		ПрайсЛист.ТекстовоеПредставлениеОстатковМало);
		ПараметрыЗакрытия.Вставить("ТекстовоеПредставлениеОстатковДостаточно",	ПрайсЛист.ТекстовоеПредставлениеОстатковДостаточно);
		ПараметрыЗакрытия.Вставить("ТекстовоеПредставлениеОстатковМного", 		ПрайсЛист.ТекстовоеПредставлениеОстатковМного);

		Закрыть(ПараметрыЗакрытия);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
