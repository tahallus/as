#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область МетодыЗаполнений

Процедура ПроверитьОбязательныеПоля()
	Перем ТаблицаПредставлений;
	
	Если Индивидуальный
		И НЕ ЗначениеЗаполнено(Автор) Тогда
		
		Автор = Пользователи.АвторизованныйПользователь();
		
	КонецЕсли;
	
	Если ПустаяСтрока(Наименование) Тогда
		
		Наименование = НСтр("ru ='Прайс-лист'");
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Валюта) Тогда
		
		Валюта = Константы.НациональнаяВалюта.Получить();
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(УсловнаяЦенаУсловие) Тогда
		
		УсловнаяЦенаУсловие = Перечисления.ВидСравненияЗначений.Меньше;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидЦенУсловие) Тогда
		
		ВидЦенУсловие = Перечисления.ВидСравненияЗначений.Равно;
		
	КонецЕсли;
	
	Если ПредставлениеНоменклатуры.Количество() = 0 Тогда
		
		Справочники.ПрайсЛисты.ДоступныеПоляНоменклатуры(ТаблицаПредставлений);
		ПредставлениеНоменклатуры.Загрузить(ТаблицаПредставлений);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПредставлениеНоменклатуры(ТаблицаИлиМассивПредставленияНоменклатуры)
	
	Если ТипЗнч(ТаблицаИлиМассивПредставленияНоменклатуры) = Тип("ТаблицаЗначений") Тогда
		
		ПредставлениеНоменклатуры.Загрузить(ТаблицаИлиМассивПредставленияНоменклатуры);
		
	ИначеЕсли ТипЗнч(ТаблицаИлиМассивПредставленияНоменклатуры) = Тип("Массив") Тогда
		
		Для каждого Строка Из ТаблицаИлиМассивПредставленияНоменклатуры Цикл // Элементом массива может быть структура
			
			ЗаполнитьЗначенияСвойств(ПредставлениеНоменклатуры.Добавить(), Строка);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьВидыЦен(ТаблицаИлиМассивВидовЦен)
	
	Если ТипЗнч(ТаблицаИлиМассивВидовЦен) = Тип("ТаблицаЗначений") Тогда
		
		ВидыЦен.Загрузить(ТаблицаИлиМассивВидовЦен);
		
	ИначеЕсли ТипЗнч(ТаблицаИлиМассивВидовЦен) = Тип("Массив") Тогда
		
		Для каждого ЭлементМассива Из ТаблицаИлиМассивВидовЦен Цикл
			
			НоваяСтрока = ВидыЦен.Добавить();
			НоваяСтрока.ВидЦен = ЭлементМассива;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьОтборыНоменклатуры(КоллекцияЭлементов)
	
	Если ТипЗнч(КоллекцияЭлементов) = Тип("ТаблицаЗначений") Тогда
		
		ОтборНоменклатуры.Загрузить(КоллекцияЭлементов);
		
	ИначеЕсли ТипЗнч(КоллекцияЭлементов) = Тип("ОтборКомпоновкиДанных") Тогда
		
		ПрайсЛистыСлужебный.СКДОтборНоменклатурыВТабличнуюЧасть(КоллекцияЭлементов.Элементы, ОтборНоменклатуры);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьОтборыНоменклатурыСтандартнымЗначением()
	
	НоваяСтрока = ОтборНоменклатуры.Добавить();
	НоваяСтрока.Имя = "Номенклатура.Недействителен";
	НоваяСтрока.Представление = "Номенклатура.Недействителен";
	НоваяСтрока.ВидСравнения = "Равно";
	НоваяСтрока.Значение = Ложь;
	НоваяСтрока.Использование = Истина;
	
	НоваяСтрока = ОтборНоменклатуры.Добавить();
	НоваяСтрока.Имя = "Номенклатура.ИсключитьИзПрайсЛистов";
	НоваяСтрока.Представление = "Номенклатура.ИсключитьИзПрайсЛистов";
	НоваяСтрока.ВидСравнения = "Равно";
	НоваяСтрока.Значение = Ложь;
	НоваяСтрока.Использование = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьОбъектПоСтруктуре(ДанныеЗаполнения)
	
	ДанныеЗаполнения.Свойство("Автор", 						Автор);
	ДанныеЗаполнения.Свойство("Валюта",						Валюта);
	ДанныеЗаполнения.Свойство("ВариантыСортировки",			ВариантыСортировки);
	ДанныеЗаполнения.Свойство("ВыводитьДатуФормирования",	ВыводитьДатуФормирования);
	ДанныеЗаполнения.Свойство("ДатаРасчетаКурсаВалюты",		ДатаРасчетаКурсаВалюты);
	ДанныеЗаполнения.Свойство("ДатаФормирования",			ДатаФормирования);
	ДанныеЗаполнения.Свойство("ИерархияСодержимого",		ИерархияСодержимого);
	ДанныеЗаполнения.Свойство("Индивидуальный",				Индивидуальный);
	ДанныеЗаполнения.Свойство("НазваниеЦеныПредставлением",	НазваниеЦеныПредставлением);
	ДанныеЗаполнения.Свойство("НаименованиеПрайсЛиста",		Наименование);
	ДанныеЗаполнения.Свойство("НоменклатураБезЦен", 		НоменклатураБезЦен);
	ДанныеЗаполнения.Свойство("Описание",					Описание);
	ДанныеЗаполнения.Свойство("Организация",				Организация);
	ДанныеЗаполнения.Свойство("ПечатьПрайсЛиста",			ПечатьПрайсЛиста);
	ДанныеЗаполнения.Свойство("ПериодЦен", 					ПериодЦен);
	ДанныеЗаполнения.Свойство("ФормироватьПоНаличию",		ФормироватьПоНаличию);
	
	ДанныеЗаполнения.Свойство("МаксимальнаяЦена", 			МаксимальнаяЦена);
	ДанныеЗаполнения.Свойство("МинимальнаяЦена", 			МинимальнаяЦена);
	
	ДанныеЗаполнения.Свойство("УсловнаяЦена",				УсловнаяЦена);
	ДанныеЗаполнения.Свойство("УсловнаяЦенаУсловие",		УсловнаяЦенаУсловие);
	ДанныеЗаполнения.Свойство("УсловнаяЦенаЗначение",		УсловнаяЦенаЗначение);
	ДанныеЗаполнения.Свойство("УсловнаяЦенаЗначениеДо",		УсловнаяЦенаЗначениеДо);
	
	ДанныеЗаполнения.Свойство("ВидЦенСравнить", 			ВидЦенСравнить);
	ДанныеЗаполнения.Свойство("ВидЦен1",					ВидЦен1);
	ДанныеЗаполнения.Свойство("ВидЦенУсловие",				ВидЦенУсловие);
	ДанныеЗаполнения.Свойство("ВидЦен2", 					ВидЦен2);
	
	ДанныеЗаполнения.Свойство("КоличествоКолонок",			КоличествоКолонок);
	ДанныеЗаполнения.Свойство("КартинкаШирина",				КартинкаШирина);
	ДанныеЗаполнения.Свойство("КартинкаВысота",				КартинкаВысота);
	ДанныеЗаполнения.Свойство("ИзменятьРазмерПропорционально", ИзменятьРазмерПропорционально);
	
	ДанныеЗаполнения.Свойство("ПредставлениеОстатков",		ПредставлениеОстатков);
	ДанныеЗаполнения.Свойство("ТекстовоеПредставлениеОстатковМало", ТекстовоеПредставлениеОстатковМало);
	ДанныеЗаполнения.Свойство("ТекстовоеПредставлениеОстатковДостаточно", ТекстовоеПредставлениеОстатковДостаточно);
	ДанныеЗаполнения.Свойство("ТекстовоеПредставлениеОстатковМного",ТекстовоеПредставлениеОстатковМного);
	ДанныеЗаполнения.Свойство("ОтборОрганизация",			ОтборОрганизация);
	ДанныеЗаполнения.Свойство("ОтборСклад",					ОтборСклад);
	ДанныеЗаполнения.Свойство("УказатьКонтактыОтветственного", УказатьКонтактыОтветственного);
	ДанныеЗаполнения.Свойство("ИсключитьПустыеВидыЦен",		ИсключитьПустыеВидыЦен);
	
	Если ДанныеЗаполнения.Свойство("ПредставлениеНоменклатуры") Тогда
		
		ЗаполнитьПредставлениеНоменклатуры(ДанныеЗаполнения.ПредставлениеНоменклатуры);
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("ВидыЦен") Тогда
		
		ЗаполнитьВидыЦен(ДанныеЗаполнения.ВидыЦен);
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("ОтборНоменклатуры") Тогда
		
		ЗаполнитьОтборыНоменклатуры(ДанныеЗаполнения.ОтборНоменклатуры);
		
	Иначе
		
		ЗаполнитьОтборыНоменклатурыСтандартнымЗначением();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Перем ТаблицаПредставлений;
	
	ПредставлениеНоменклатуры.Очистить();
	ВидыЦен.Очистить();
	ОтборНоменклатуры.Очистить();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		ЗаполнитьОбъектПоСтруктуре(ДанныеЗаполнения);
		
	Иначе
		
		ЗаполнитьОтборыНоменклатурыСтандартнымЗначением();
		
	КонецЕсли;
	
	ПроверитьОбязательныеПоля();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Индивидуальный = Истина;
	Автор = Пользователи.АвторизованныйПользователь();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Индивидуальный
		И НЕ ЗначениеЗаполнено(Автор) Тогда
		
		Автор = Пользователи.АвторизованныйПользователь();
		
	КонецЕсли;
	
	Если Недействителен Тогда
		
		ПрайсЛистыСлужебный.ПроверитьВозможностьУстановкиПризнакаНедействителен(Ссылка, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли