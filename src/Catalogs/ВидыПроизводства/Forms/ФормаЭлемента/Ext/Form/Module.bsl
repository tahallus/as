
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформлениеФормы();
	
	МобильныйКлиентУНФ.НастроитьФормуОбъектаМобильныйКлиент(Элементы);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЭтапыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не Копирование Тогда
		
		Отказ = Истина;
		
		СтруктураОткрытия = Новый Структура;
		СтруктураОткрытия.Вставить("РежимВыбора", Истина);
		СтруктураОткрытия.Вставить("МножественныйВыбор", Истина);
		
		ОткрытьФорму("Справочник.ЭтапыПроизводства.ФормаВыбора", СтруктураОткрытия, Элементы.Этапы, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Для каждого Этап Из ВыбранноеЗначение Цикл
		
		СтруктураДанные = Новый Структура();
		СтруктураДанные.Вставить("Этап", Этап);
		СуществующиеСтроки = Объект.Этапы.НайтиСтроки(СтруктураДанные);
		Если СуществующиеСтроки.Количество()<>0 Тогда
			Продолжить;
		КонецЕсли; 
		
		НоваяСрока = Объект.Этапы.Добавить();
		НоваяСрока.Этап = Этап;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	ЗапрещеноИзменениеПорядка = Ложь;
	
	ТекущаяСтрока = Объект.Этапы.НайтиПоИдентификатору(ПараметрыПеретаскивания.Значение);
	Если ТекущаяСтрока <> Неопределено
		И ТекущаяСтрока.Этап = ПредопределенноеЗначение("Справочник.ЭтапыПроизводства.ЗавершениеПроизводства") Тогда
		ЗапрещеноИзменениеПорядка = Истина;
	КонецЕсли;
	
	Если Не ЗапрещеноИзменениеПорядка И Строка <> Неопределено Тогда
		СтрокаПеретаскивания = Объект.Этапы.НайтиПоИдентификатору(Строка);
		Если СтрокаПеретаскивания <> Неопределено
			И СтрокаПеретаскивания.Этап = ПредопределенноеЗначение("Справочник.ЭтапыПроизводства.ЗавершениеПроизводства") Тогда
			ЗапрещеноИзменениеПорядка = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗапрещеноИзменениеПорядка Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборЭтапы(Команда)
	
	СтруктураОткрытия = Новый Структура;
	СтруктураОткрытия.Вставить("РежимВыбора", Истина);
	СтруктураОткрытия.Вставить("ЗакрыватьПриВыборе", Ложь);
	СтруктураОткрытия.Вставить("МножественныйВыбор", Истина);
	Если Элементы.Этапы.ТекущиеДанные<>Неопределено И ЗначениеЗаполнено(Элементы.Этапы.ТекущиеДанные.Этап) Тогда
		СтруктураОткрытия.Вставить("ТекущаяСтрока", Элементы.Этапы.ТекущиеДанные.Этап);
	КонецЕсли; 
	ОткрытьФорму("Справочник.ЭтапыПроизводства.ФормаВыбора", СтруктураОткрытия, Элементы.Этапы, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СместитьВверх(Команда)
	
	ТекущаяСтрока = Элементы.Этапы.ТекущиеДанные;
	Если ТекущаяСтрока=Неопределено Тогда
		Возврат;
	КонецЕсли;
	Индекс = Объект.Этапы.Индекс(ТекущаяСтрока);
	Если ТекущаяСтрока.Этап=ПредопределенноеЗначение("Справочник.ЭтапыПроизводства.ЗавершениеПроизводства") ИЛИ Индекс=0 Тогда
		Возврат;
	КонецЕсли; 
	
	Объект.Этапы.Сдвинуть(Индекс, -1);
	
КонецПроцедуры

&НаКлиенте
Процедура СместитьВниз(Команда)
	
	ТекущаяСтрока = Элементы.Этапы.ТекущиеДанные;
	Если ТекущаяСтрока=Неопределено Тогда
		Возврат;
	КонецЕсли;
	Индекс = Объект.Этапы.Индекс(ТекущаяСтрока);
	Если Индекс>=Объект.Этапы.Количество()-2 Тогда
		Возврат;
	КонецЕсли; 
	
	Объект.Этапы.Сдвинуть(Индекс, 1);
	
КонецПроцедуры

#КонецОбласти 

#Область УправлениеВнешнимВидомФормы

&НаСервере
Процедура УстановитьУсловноеОформлениеФормы()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Колонка "Резерв" ТЧ "Продукция"
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Этапы.Этап", Справочники.ЭтапыПроизводства.ЗавершениеПроизводства);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЭтапыЭтап.Имя);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Шрифт", Новый Шрифт(Новый Шрифт, , , Истина));
	
КонецПроцедуры

#КонецОбласти 
