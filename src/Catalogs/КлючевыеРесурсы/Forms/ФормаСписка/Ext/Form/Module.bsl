
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьВидыРесурсов()
	
	ТаблицаВидыРесурсов.Очистить();
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыРесурсовПредприятия.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыРесурсовПредприятия КАК ВидыРесурсовПредприятия
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидыРесурсовПредприятия.Наименование";
	
	ТаблицаВидыРесурсов.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокРесурсов()
	
	ТекСтрока = Элементы.ТаблицаВидыРесурсов.ТекущиеДанные;
	
	Если ТекСтрока <> Неопределено Тогда
		СписокРесурсов.Параметры.УстановитьЗначениеПараметра("ТекущийВидРесурса", ТекСтрока.Ссылка);
	Иначе
		СписокРесурсов.Параметры.УстановитьЗначениеПараметра("ТекущийВидРесурса", ПредопределенноеЗначение("Справочник.ВидыРесурсовПредприятия.ВсеРесурсы"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеФормы(Результат, ПараметрыОповещения) Экспорт
	
	ЗаполнитьВидыРесурсов();
	
	Если Не ПараметрыОповещения.ТекРесурс = Неопределено Тогда
		
		ПараметрыОтбора = Новый Структура("Ссылка", ПараметрыОповещения.ТекРесурс);
		НайденныеСтроки = ТаблицаВидыРесурсов.НайтиСтроки(ПараметрыОтбора);
		
		Элементы.ТаблицаВидыРесурсов.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		
	КонецЕсли;
		
	ОбновитьСписокРесурсов();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРесурс(Команда)
	
	ВидРесурса = ?(Не Элементы.ТаблицаВидыРесурсов.ТекущиеДанные = Неопределено, Элементы.ТаблицаВидыРесурсов.ТекущиеДанные.Ссылка
	, Неопределено);
	
	ПараметрыОткрытия = Новый Структура("ВидРесурса",ВидРесурса);
	ОткрытьФорму("Справочник.КлючевыеРесурсы.ФормаОбъекта", ПараметрыОткрытия);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, СписокРесурсов);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, СписокРесурсов, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, СписокРесурсов);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
	СписокРесурсов, "Недействителен", Ложь, , , Истина);
	
	СписокРесурсов.Параметры.УстановитьЗначениеПараметра("ТекущийВидРесурса", ПредопределенноеЗначение("Справочник.ВидыРесурсовПредприятия.ВсеРесурсы"));
	
	Элементы.ГруппаСписокВидовРесурсовОбщий.Видимость = Истина;
	Элементы.ГруппаРазвернуть.Видимость = Ложь;
	
	ЗаполнитьВидыРесурсов();
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Элементы.ДекорацияСвернуть.Видимость = Ложь;
	Иначе
		Элементы.ДекорацияСвернуть.Видимость = Истина;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

		НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "СписокРесурсов.Недействителен", Истина, ВидСравненияКомпоновкиДанных.Равно);
		РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "Наименование");
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекстаТабличнойЧасти); 

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ВидыРесурсовПредприятия" Тогда
		
		ОбновитьСписокРесурсов();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьНедействительную(Команда)
	
	Элементы.ПоказыватьНедействительную.Пометка = Не Элементы.ПоказыватьНедействительную.Пометка;
	УстановитьОтборНедействительная(ЭтотОбъект)
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборНедействительная(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.СписокРесурсов,
		"Недействителен",
		Ложь,
		,
		,
		Не Форма.Элементы.ПоказыватьНедействительную.Пометка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВидРесурса(Команда)
	
	ТекСтрока = Элементы.ТаблицаВидыРесурсов.ТекущиеДанные;
	
	ТекРесурс = ?(Не ТекСтрока = Неопределено,ТекСтрока.ссылка, Неопределено);
	
	ПараметрыОповещения = Новый Структура("ТекРесурс", ТекРесурс);
	Оповещение = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект, ПараметрыОповещения);
	
	ПараметрыОткрытия = Новый Структура();
	ОткрытьФорму("Справочник.ВидыРесурсовПредприятия.ФормаОбъекта", ПараметрыОткрытия, ЭтаФорма,,,,Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидРесурса(Команда)
	
	ТекСтрока = Элементы.ТаблицаВидыРесурсов.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено Тогда Возврат КонецЕсли;
	
	ТекРесурс = ТекСтрока.ссылка;
	
	ПараметрыОповещения = Новый Структура("ТекРесурс", ТекРесурс);
	Оповещение = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект, ПараметрыОповещения);
	
	ПараметрыОткрытия = Новый Структура("Ключ", ТекРесурс);
	ОткрытьФорму("Справочник.ВидыРесурсовПредприятия.ФормаОбъекта", ПараметрыОткрытия, ЭтаФорма,,,,Оповещение);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВидовРесурсовПриАктивизацииСтроки(Элемент)
	
	ОбновитьСписокРесурсов();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРазвернутьНажатие(Элемент)
	Элементы.ГруппаСписокВидовРесурсовОбщий.Видимость = Истина;
	Элементы.ГруппаРазвернуть.Видимость = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСвернутьНажатие(Элемент)
	
	Элементы.ГруппаСписокВидовРесурсовОбщий.Видимость = Ложь;
	Элементы.ГруппаРазвернуть.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВидыРесурсовПриАктивизацииСтроки(Элемент)
	ОбновитьСписокРесурсов();
КонецПроцедуры

&НаКлиенте
Процедура СписокРесурсовПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

#КонецОбласти

