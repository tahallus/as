#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НезапланированныеЗатратыВключатьВСчет Тогда
		ПроверяемыеРеквизиты.Добавить("НезапланированныеЗатратыПредставлениеВСчете");
		
		Если НезапланированныеЗатратыФормированиеЦены = Перечисления.БиллингФормированиеЦеныЗатрат.ФиксированноеЗначение Тогда
			ПроверяемыеРеквизиты.Добавить("НезапланированныеЗатратыФиксированнаяЦена");
		ИначеЕсли НезапланированныеЗатратыФормированиеЦены = Перечисления.БиллингФормированиеЦеныЗатрат.ПоСебестоимостиСНаценкой Тогда
			ПроверяемыеРеквизиты.Добавить("НезапланированныеЗатратыНаценка");
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого Стр Из УчетНоменклатуры Цикл
		Если Стр.ФормированиеЦены = Перечисления.БиллингФормированиеЦеныНоменклатуры.ФиксированноеЗначение
			И Не ЗначениеЗаполнено(Стр.Цена) Тогда
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не заполнена колонка ""Цена"" в строке %1 списка ""Номенклатура"".'"), Стр.НомерСтроки);
			КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("УчетНоменклатуры", Стр.НомерСтроки,
				"ПредставлениеЦены");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Стр Из УчетЗатрат Цикл

		Если Стр.ФормированиеЦены = Перечисления.БиллингФормированиеЦеныЗатрат.ФиксированноеЗначение
			И Не ЗначениеЗаполнено(Стр.Цена) Тогда
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не заполнена колонка ""Цена"" в строке %1 списка ""Затраты"".'"), Стр.НомерСтроки);
			КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("УчетЗатрат", Стр.НомерСтроки,
				"ПредставлениеЦены");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли