#Область ПроцедурыИФункцииОбщегоНазначения

&НаКлиенте
// Возвращает текстовое представление интервала.
//
Функция ПолучитьПредставлениеИнтервала(ТабличнаяЧасть, ТекущаяСтрока)

	МаксНижняяГраница = Неопределено;
	СледующаяСтрока = Неопределено;
	Для Каждого СтрокаТЧ Из ТабличнаяЧасть Цикл
		Если (МаксНижняяГраница = Неопределено Или МаксНижняяГраница >= СтрокаТЧ.НижняяГраница)
		   И ТекущаяСтрока.НижняяГраница <= СтрокаТЧ.НижняяГраница
		   И СтрокаТЧ <> ТекущаяСтрока Тогда
			СледующаяСтрока = СтрокаТЧ;
			МаксНижняяГраница = СтрокаТЧ.НижняяГраница;
		КонецЕсли;
	КонецЦикла;

	Если СледующаяСтрока = Неопределено Тогда
		ПредставлениеИнтервала = "От " + СокрЛП(ТекущаяСтрока.НижняяГраница) + " " + ВалютаУчета;
	ИначеЕсли ТекущаяСтрока.НижняяГраница = СледующаяСтрока.НижняяГраница Тогда
		ПредставлениеИнтервала = "ОШИБКА: такая нижняя граница уже есть!";
	Иначе
		ПредставлениеИнтервала = "От " + СокрЛП(ТекущаяСтрока.НижняяГраница) + " до " + СокрЛП(СледующаяСтрока.НижняяГраница) + " " + ВалютаУчета;
	КонецЕсли;

	Возврат ПредставлениеИнтервала;

КонецФункции // ПолучитьПредставлениеИнтервала()

// Процедура обновляет значение в колонке ПредставлениеИнтервала табличной части ПорогиНакопительныхСкидок.
//
&НаКлиенте
Процедура ОбновитьПредставлениеВсехСтрок()

	Для каждого ТекСтр Из Объект.ПорогиНакопительныхСкидок Цикл
		ТекСтр.ПредставлениеИнтервала = ПолучитьПредставлениеИнтервала(Объект.ПорогиНакопительныхСкидок, ТекСтр);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Включение опции автоматических скидок при создании нового вида ДК
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ДисконтныеКартыУНФСервер.ПроверитьИУстановитьОпциюАвтоматическиеСкидки();
		ДисконтныеКартыУНФСервер.ПроверитьИСоздатьУсловиеПоДисконтнойКарте();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ВидСкидкиВДисконтныхКартах) Тогда
		Объект.ВидСкидкиВДисконтныхКартах = ПредопределенноеЗначение("Перечисление.ВидыСкидокВДисконтныхКартах.ФиксированнаяСкидка");
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.ТипКарты) Тогда
		Объект.ТипКарты = Перечисления.ТипыКарт.Штриховая;
	КонецЕсли;	
	
	// Установка доступности цен для редактирования.
	РазрешеноРедактированиеЦенДокументов = УправлениеДоступомУНФ.РазрешеноРедактированиеЦенДокументов();
	
	ЭтаФорма.ТолькоПросмотр = НЕ РазрешеноРедактированиеЦенДокументов;
	ВалютаУчета = Константы.ВалютаУчета.Получить();
	
	РаботаСФормойКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.ГруппаДополнительно, Объект.Комментарий);
	
	ВариантСовместногоПримененияСкидок = Константы.ВариантыСовместногоПримененияСкидокНаценок.Получить();
	Если ВариантСовместногоПримененияСкидок.Пустая() Тогда
		ВариантСовместногоПримененияСкидок = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Сложение;
		Константы.ВариантыСовместногоПримененияСкидокНаценок.Установить(ВариантСовместногоПримененияСкидок);
	КонецЕсли;
	
	УправлениеВидимостьюНаСервере();
	
	СписокАвтоСкидок.Параметры.УстановитьЗначениеПараметра("ВидДисконтнойКарты", Объект.Ссылка);
	
КонецПроцедуры

// Процедура - обработчик события ПриЧтенииНаСервере.
//
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	УправлениеВидимостьюНаСервере();
	
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьПредставлениеВсехСтрок();
	
	Если ВариантСовместногоПримененияСкидок = ПредопределенноеЗначение("Перечисление.ВариантыСовместногоПримененияСкидокНаценок.Вытеснение")
		ИЛИ ВариантСовместногоПримененияСкидок = ПредопределенноеЗначение("Перечисление.ВариантыСовместногоПримененияСкидокНаценок.Умножение") Тогда
		УстановитьСортировкуСпискаПоПолюПорядок();
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПослеЗаписи.
//
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьПредставлениеВсехСтрок();
	Оповестить("ПослеЗаписи_ВидыДисконтныхКарт", Объект.Комментарий, Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбработчикиСобытийЭлементовФормы

// Процедура - обработчик события ПриИзменении элемента ТипКарты.
//
&НаКлиенте
Процедура ТипКартыПриИзменении(Элемент)
	
	Элементы.ШаблонДисконтнойКарты.Видимость = (Объект.ТипКарты <> ПредопределенноеЗначение("Перечисление.ТипыКарт.Штриховая"));
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Комментарий.
//
&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьКартинкуДляКомментария", 0.5, Истина);
	
КонецПроцедуры // КомментарийПриИзменении()

&НаКлиенте
Процедура Подключаемый_УстановитьКартинкуДляКомментария()
	
	РаботаСФормойКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.ГруппаДополнительно, Объект.Комментарий);
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении элемента ВидСкидкиВДисконтныхКартах.
//
&НаКлиенте
Процедура ВидСкидкиВДисконтныхКартахПриИзменении(Элемент)
	
	УправлениеВидимостьюНаСервере();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении элемента ВидПериода.
//
&НаКлиенте
Процедура ВидПериодаПриИзменении(Элемент)
	
	ВидПериодаПриИзмененииНаСервере();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении элемента ВидПериода (серверная часть).
//
&НаСервере
Процедура ВидПериодаПриИзмененииНаСервере()
	
	Элементы.Периодичность.Видимость = (Объект.ВидПериода <> ПредопределенноеЗначение("Перечисление.ВидыПериодовДляНакопительныхСкидок.ВесьПериод"));
	Если Не ЗначениеЗаполнено(Объект.Периодичность) И Элементы.Периодичность.Видимость Тогда
		Объект.Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Год");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбработчикиСобытийТабличнойЧасти

// Процедура - обработчик события "ПриОкончанииРедактирования" строки в ТЧ "ПорогиНакопительныхСкидок".
//
&НаКлиенте
Процедура ПорогиНакопительныхСкидокПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	// После ввода строки отсортируем ТЧ по возрастанию нижней границы.
	Объект.ПорогиНакопительныхСкидок.Сортировать("НижняяГраница");
	ОбновитьПредставлениеВсехСтрок();
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" для колонки "НижняяГраница" строки в ТЧ "ПорогиНакопительныхСкидок".
//
&НаКлиенте
Процедура ПорогиНакопительныхСкидокНижняяГраницаПриИзменении(Элемент)
	
	ТекСтрЭлемент = Элементы.ПорогиНакопительныхСкидок.ТекущиеДанные;
	ТекСтрЭлемент.ПредставлениеИнтервала = ПолучитьПредставлениеИнтервала(Объект.ПорогиНакопительныхСкидок, ТекСтрЭлемент);
	
КонецПроцедуры

// Процедура - обработчик события "ПослеУдаления" ТЧ "ПорогиНакопительныхСкидок".
//
&НаКлиенте
Процедура ПорогиНакопительныхСкидокПослеУдаления(Элемент)
	
	ОбновитьПредставлениеВсехСтрок();
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииДляУправленияВнешнимВидомФормы

// Процедура отвечает за видимость элементов формы в зависимости от вида и типа дисконтной карты.
&НаСервере
Процедура УправлениеВидимостьюНаСервере()

	Если Объект.ВидСкидкиВДисконтныхКартах = ПредопределенноеЗначение("Перечисление.ВидыСкидокВДисконтныхКартах.НакопительнаяСкидка") И
		Объект.ВидПериода.Пустая() Тогда
		Объект.ВидПериода = ПредопределенноеЗначение("Перечисление.ВидыПериодовДляНакопительныхСкидок.ВесьПериод");
	КонецЕсли;
	
	Элементы.Скидка.Видимость = (Объект.ВидСкидкиВДисконтныхКартах = ПредопределенноеЗначение("Перечисление.ВидыСкидокВДисконтныхКартах.ФиксированнаяСкидка"));
	ЭтоНакопительныеСкидки = (Объект.ВидСкидкиВДисконтныхКартах = ПредопределенноеЗначение("Перечисление.ВидыСкидокВДисконтныхКартах.НакопительнаяСкидка"));
	Элементы.ПорогиНакопительныхСкидок.Видимость = ЭтоНакопительныеСкидки;
	Элементы.ВидПериода.Видимость = ЭтоНакопительныеСкидки;
	Элементы.Периодичность.Видимость = (Объект.ВидПериода <> ПредопределенноеЗначение("Перечисление.ВидыПериодовДляНакопительныхСкидок.ВесьПериод")) И ЭтоНакопительныеСкидки;
	Элементы.ШаблонДисконтнойКарты.Видимость = (Объект.ТипКарты <> ПредопределенноеЗначение("Перечисление.ТипыКарт.Штриховая"));
	
	// Старый механизм скидок
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаВидСкидки",						"Видимость",	Объект.СтарыйМеханизмСкидок);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Скидка",								"Видимость",	Объект.СтарыйМеханизмСкидок);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПериодичностьНакопления",			"Видимость",	Объект.СтарыйМеханизмСкидок);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПорогиНакопительныхСкидок",				"Видимость",	Объект.СтарыйМеханизмСкидок);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВариантСовместногоПримененияСкидок",	"Видимость",	Не Объект.СтарыйМеханизмСкидок);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПерейтиКНастройке",						"Видимость",	Не Объект.СтарыйМеханизмСкидок);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаАвтоСкидки",						"Видимость",	Не Объект.СтарыйМеханизмСкидок);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "БонуснаяПрограмма",						"Видимость",	Не Объект.СтарыйМеханизмСкидок);
	
	// Список действующих скидок
	Если Не Объект.СтарыйМеханизмСкидок Тогда
		ЕстьДействующиеСкидки = ДисконтныеКартыУНФСервер.ЕстьСкидкиПоВидуКарт(Объект.Ссылка);
		Элементы.СписокАвтоСкидок.Видимость = ЕстьДействующиеСкидки;
		Элементы.ГруппаЗаглушка.Видимость = Не ЕстьДействующиеСкидки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиКНастройке(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПерейтиКНастройкеЗавершение", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура("ДисконтныеКарты", Истина);
	ОткрытьФорму("Обработка.ВидыСкидокНаценокРучныеИАвтоматические.Форма", ПараметрыОткрытия, ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКНастройкеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	УправлениеВидимостьюНаСервере();
	СписокАвтоСкидок.Параметры.УстановитьЗначениеПараметра("ВидДисконтнойКарты", Объект.Ссылка);
	Элементы.СписокАвтоСкидок.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СортировкаСписка

// Процедура обрабатывает ответ пользователя на вопрос об установке сортировки по реквизиту РеквизитДопУпорядочивания.
//
&НаКлиенте
Процедура ПроверитьСписокПередОперациейОтветПоСортировкеПолучен(РезультатОтвета, ДополнительныеПараметры) Экспорт
	
	Если РезультатОтвета <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьСортировкуСпискаПоПолюПорядок();
	
КонецПроцедуры

// Процедура устанавливает сортировку по полю РеквизитДопУпорядочивания.
//
&НаКлиенте
Процедура УстановитьСортировкуСпискаПоПолюПорядок()
	
	СписокРеквизит = СписокАвтоСкидок;
	
	ПользовательскиеНастройкиПорядка = Неопределено;
	Для Каждого Элемент Из СписокРеквизит.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ПорядокКомпоновкиДанных") Тогда
			ПользовательскиеНастройкиПорядка = Элемент;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.Проверить(ПользовательскиеНастройкиПорядка <> Неопределено, НСтр("ru = 'Пользовательская настройка порядка не найдена.'"));
	
	ПользовательскиеНастройкиПорядка.Элементы.Очистить();
	Элемент = ПользовательскиеНастройкиПорядка.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	Элемент.Использование = Истина;
	Элемент.Поле = Новый ПолеКомпоновкиДанных("РеквизитДопУпорядочивания");
	Элемент.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	
КонецПроцедуры

#КонецОбласти