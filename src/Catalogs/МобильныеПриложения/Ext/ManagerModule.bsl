#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ОбновитьПоставляемыеПриложения() Экспорт
	
	ИспользоватьОбменСМобильнымиПриложениями = ПолучитьФункциональнуюОпцию("ИспользоватьОбменСМобильнымиПриложениями");
	Если Не ИспользоватьОбменСМобильнымиПриложениями Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПоставляемыхПриложений = Новый Структура;
	ПараметрыПоставляемыхПриложений.Вставить("ПоставляемыеВариантыНастроек", Новый Массив);
	ПараметрыПоставляемыхПриложений.Вставить("ОписаниеПриложений", Новый Массив);
	
	УправлениеМобильнымиПриложениямиПереопределяемый.ПередЗаполнениемПоставляемыхПриложений(
		ПараметрыПоставляемыхПриложений);
	
	Если ПараметрыПоставляемыхПриложений.ОписаниеПриложений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	МобильныеПриложения.Ссылка КАК Ссылка,
	|	МобильныеПриложения.ВариантНастройки КАК ВариантНастройки,
	|	МобильныеПриложения.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Справочник.МобильныеПриложения КАК МобильныеПриложения
	|ГДЕ
	|	МобильныеПриложения.ВариантНастройки В(&ПоставляемыеВариантыНастроек)";
	
	Запрос.УстановитьПараметр("ПоставляемыеВариантыНастроек",
		ПараметрыПоставляемыхПриложений.ПоставляемыеВариантыНастроек);
	
	Результат = Запрос.Выполнить();
	МобильныеПриложения = Результат.Выгрузить();
	
	Для Каждого ОписаниеПриложения Из ПараметрыПоставляемыхПриложений.ОписаниеПриложений Цикл
		СуществующееПриложение = МобильныеПриложения.Найти(ОписаниеПриложения.ВариантНастройки, "ВариантНастройки");
		Если СуществующееПриложение = Неопределено Тогда
			МобильноеПриложение = Справочники.МобильныеПриложения.СоздатьЭлемент();
		Иначе
			МобильноеПриложение = СуществующееПриложение.Ссылка.ПолучитьОбъект();
			МобильноеПриложение.ПометкаУдаления = Ложь;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(МобильноеПриложение, ОписаниеПриложения);
		МобильноеПриложение.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли