#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ВсеСобытия = События.ВыгрузитьКолонку("СобытиеИдентификатор");
	Если ВсеСобытия.Количество() <> ОбщегоНазначенияКлиентСервер.СвернутьМассив(ВсеСобытия).Количество() Тогда
		ТекстСообщения = НСтр("ru='Не может быть указано несколько одинаковых событий задачи.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		Наименование = "" + ДействиеИдентификатор;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Автор) Тогда
		Автор = Пользователи.АвторизованныйПользователь();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаСоздания) Тогда
		ДатаСоздания = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ДанныеЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ПометкаУдаления,Используется");
		ЗадачаИспользуется = ДанныеЗадачи.Используется И НЕ ДанныеЗадачи.ПометкаУдаления;
		ДополнительныеСвойства.Вставить("ЗадачаИспользуется", ЗадачаИспользуется);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ЗадачаИспользуется") И ДополнительныеСвойства.ЗадачаИспользуется = Истина Тогда
		ЗадачаИспользуется = Используется И НЕ ПометкаУдаления;
		Если НЕ ЗадачаИспользуется Тогда
			РегистрыСведений.ОчередьЗадачАссистентаУправления.УдалитьЗадачуИзОчереди(Ссылка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли