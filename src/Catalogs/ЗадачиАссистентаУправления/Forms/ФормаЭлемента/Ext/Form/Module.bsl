
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьЗначенияЗаполнения();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	СохранитьЗначенияЗаполнения(ТекущийОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьЗначенияЗаполнения()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиАссистентаУправленияЗначенияЗаполнения.Ссылка КАК Ссылка,
	|	ЗадачиАссистентаУправленияЗначенияЗаполнения.НомерСтроки КАК НомерСтроки,
	|	ЗадачиАссистентаУправленияЗначенияЗаполнения.Параметр КАК Параметр,
	|	ЗадачиАссистентаУправленияЗначенияЗаполнения.ВариантЗаполнения КАК ВариантЗаполнения,
	|	ЗадачиАссистентаУправленияЗначенияЗаполнения.Значение КАК Значение
	|ИЗ
	|	Справочник.ЗадачиАссистентаУправления.ЗначенияЗаполнения КАК ЗадачиАссистентаУправленияЗначенияЗаполнения
	|ГДЕ
	|	ЗадачиАссистентаУправленияЗначенияЗаполнения.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Объект.ЗначенияЗаполнения.Очистить();
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		НоваяСтрока = Объект.ЗначенияЗаполнения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат, "Параметр,ВариантЗаполнения");
		НоваяСтрока.ЗначениеСсылка = Результат.Значение.Получить();
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьЗначенияЗаполнения(ТекущийОбъект)
	
	ТекущийОбъект.ЗначенияЗаполнения.Очистить();
	
	Для каждого СтрокаФормы Из Объект.ЗначенияЗаполнения Цикл
		НоваяСтрока = ТекущийОбъект.ЗначенияЗаполнения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаФормы, "Параметр,ВариантЗаполнения");
		НоваяСтрока.Значение = Новый ХранилищеЗначения(СтрокаФормы.ЗначениеСсылка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
