#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция определяет, используются или нет группы доступа контрагентов.
//
//	Возвращаемое значение:
//		Булево - если ИСТИНА, значит группы доступа используются
//
Функция ИспользуютсяГруппыДоступа() Экспорт
	
	Возврат
		ПолучитьФункциональнуюОпцию("ОграничиватьДоступНаУровнеЗаписей")
		И ПолучитьФункциональнуюОпцию("ИспользоватьГруппыДоступаКонтрагентов");
	
КонецФункции

Функция ГруппаДоступаПоУмолчанию(Пользователь = Неопределено) Экспорт

	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	ОсновнаяГруппаДоступа = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновнаяГруппаДоступаКонтрагентов");
	
	Если Не ЗначениеЗаполнено(ОсновнаяГруппаДоступа) Тогда
		Возврат ПерваяРазрешеннаяГруппаДоступа();
	КонецЕсли;
	
	ЕстьДоступ = ОсновнаяГруппаДоступаРазрешена(ОсновнаяГруппаДоступа);
	
	Если ЕстьДоступ Тогда
		Возврат ОсновнаяГруппаДоступа;
	КонецЕсли;
	
	Возврат ПерваяРазрешеннаяГруппаДоступа();
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Ссылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеФункции

Функция ОсновнаяГруппаДоступаРазрешена(ОсновнаяГруппаДоступа)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГруппыДоступаКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ГруппыДоступаКонтрагентов КАК ГруппыДоступаКонтрагентов
	|ГДЕ
	|	ГруппыДоступаКонтрагентов.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ОсновнаяГруппаДоступа);
	
	Результат = Запрос.Выполнить();
	
	ГруппаДоступаРазрешена = Не Результат.Пустой();
	
	Возврат ГруппаДоступаРазрешена;

КонецФункции

Функция ПерваяРазрешеннаяГруппаДоступа()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ГруппыДоступаКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ГруппыДоступаКонтрагентов КАК ГруппыДоступаКонтрагентов";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Ссылка;
	КонецЦикла;
	
	Возврат Справочники.ГруппыДоступаКонтрагентов.ПустаяСсылка();
	
КонецФункции

#КонецОбласти

#КонецЕсли