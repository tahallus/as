
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	Если Не ЗначениеЗаполнено(ПараметрКоманды) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЗаполнения = Новый Структура();
	СтруктураЗаполнения.Вставить("Контрагент", ПараметрКоманды);
	ПолучитьПараметрыЗаполнения(СтруктураЗаполнения);
	
	Если СтруктураЗаполнения.ЭтоГруппа Тогда
		ВызватьИсключение НСтр("ru = 'Нельзя выбирать группу контрагентов.'");
	КонецЕсли;
	
	ОткрытьФорму("Документ.ЗаказПокупателя.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", СтруктураЗаполнения));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьПараметрыЗаполнения(СтруктураЗаполнения)
	
	ДоговорПоУмолчанию = Справочники.ДоговорыКонтрагентов.ДоговорПоУмолчанию(СтруктураЗаполнения.Контрагент);
	
	Если ЗначениеЗаполнено(ДоговорПоУмолчанию)
		И Константы.ДобавитьСловоДоговорВПредставлениеДоговораОснованияПечати.Получить() Тогда
		НаименованиеДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорПоУмолчанию, "Наименование");
		ОснованиеПечатиСтрокой = СтрШаблон(НСтр("ru='Договор: %1'"), НаименованиеДоговора);
	Иначе
		ОснованиеПечатиСтрокой = "";
	КонецЕсли;
	
	СтруктураЗаполнения.Вставить("Договор", ДоговорПоУмолчанию);
	СтруктураЗаполнения.Вставить("ОснованиеПечатиСсылка", ДоговорПоУмолчанию);
	СтруктураЗаполнения.Вставить("ОснованиеПечати", ОснованиеПечатиСтрокой);
	СтруктураЗаполнения.Вставить("ВидОперации", Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд);
	СтруктураЗаполнения.Вставить("ЭтоГруппа", СтруктураЗаполнения.Контрагент.ЭтоГруппа);
	
КонецПроцедуры

#КонецОбласти


