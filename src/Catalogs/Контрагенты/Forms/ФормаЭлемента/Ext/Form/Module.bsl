
#Область ОписаниеПеременных

#Область ПеременныеМодуля

&НаКлиенте
Перем ФормаДлительнойОперации Экспорт;

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	// УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтотОбъект, Объект); // для проверки внедрения БСП
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПечатьДокументовУНФ.УстановитьОтображениеПодменюПечати(Элементы.ПодменюПечать);
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = УправлениеСвойствамиУНФ.ЗаполнитьДополнительныеПараметры(Объект, "ДополнительныеРеквизиты", , "КомандыОбычные");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереКонтрагент(ЭтотОбъект, Параметры);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	АвтоподборКонтактов.ПодготовитьРеквизитыДляАвтоподбораИзКлассификатора(ЭтотОбъект, Параметры);
	
	Параметры.Свойство("СопоставитьКонтактИАдресЭПВСобытияхПослеЗаписиОбъекта", СопоставитьКонтактИАдресЭПВСобытияхПослеЗаписиОбъекта);
	ЭтотОбъект.СобытиеДляПоказаПослеЗакрытия = Параметры.СобытиеДляПоказаПослеЗакрытия;
	ТекущийЭлемент = Элементы.НаименованиеПолное;
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Параметры.Свойство("Контакт",ПривязанныйКонтакт);
		Параметры.Свойство("Наименование",Объект.НаименованиеПолное);
		
		ПриСозданииПриЧтенииНаСервере();
		
		Если Не ПустаяСтрока(Параметры.ТекстЗаполнения) Тогда
			ЗаполнитьРеквизитыПоТекстуЗаполнения(Параметры.ТекстЗаполнения);
		КонецЕсли;
		
		// Создание из документа с установленным признаком вид операции.
		Если Параметры.ДополнительныеПараметры.Свойство("ВидОперации") Тогда
			Отношения = КлассификацияКонтактов.ТипОтношенийСКонтрагентомПоВидуОперации(Параметры.ДополнительныеПараметры.ВидОперации);
			ЗаполнитьЗначенияСвойств(Объект, Отношения, "Покупатель,Поставщик,ПрочиеОтношения");
		ИначеЕсли Параметры.ДополнительныеПараметры.Свойство("ЧекККМ") Тогда
			Объект.ВидКонтрагента = Перечисления.ВидыКонтрагентов.ФизическоеЛицо;
			Объект.Покупатель = Истина;
		КонецЕсли;
		
		Если Параметры.Свойство("ЭтоКомиссия") Тогда
			ВидОперацииКомиссия = Истина;
			Объект.Покупатель = Истина;
			Объект.Наименование = Параметры.Наименование;
		КонецЕсли;
		
		// Взаиморасчеты
		Если Параметры.ЗначениеКопирования.Пустая() Тогда
			ЗаполнитьЗначенияСвойств(Объект, 
				РасчетыРаботаСФормамиВызовСервера.ПолучитьЗначенияРеквизитовПоУмолчаниюДляНовогоКонтрагента());
		КонецЕсли;
		
		ЭтотКонтрагентЯвляетсяГоловным = Ложь;
		
	Иначе
		
		ЭтотКонтрагентЯвляетсяГоловным = КонтрагентЯвляетсяГоловным(Объект.Ссылка);
		
	КонецЕсли;
	
	ПерсональныеДанные = НСтр("ru ='Персональные данные'");
	ЦветТекстаНекорректногоКонтрагента = ЦветаСтиля.ЦветТекстаНекорректногоКонтрагента;
	ЦветГиперссылки = ЦветаСтиля.ЦветГиперссылки;
	
	ВыполнитьВсеПроверки(ЭтотОбъект);
	
	УстановитьЗаголовокФормы(ЭтотОбъект);
	
	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	УправлениеНебольшойФирмойЭлектронныеДокументыСервер.КомандыЭДО_ФормаЭлементаПриСоздании(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	СПАРКРискиПриСозданииНаСервере();
	
	Если ЗначениеЗаполнено(ПривязанныйКонтакт) Тогда
		Параметры.Свойство("Наименование",Объект.Наименование);
		
		Если НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
			Объект.Наименование = ПривязанныйКонтакт.Наименование;
		КонецЕсли;
		
		ТекущийЭлемент = Элементы.НаименованиеПолное;
		Модифицированность = Истина;
	КонецЕсли;
		
	ОбязательноЗаполнятьИсточникВПокупателях = 
		РегистрыСведений.ОбязательностьЗаполненияРеквизитов.ОбязательностьЗаполненияРеквизита("Покупатель", "ИсточникПривлечения");
		
	// Взаиморасчеты
	ЗаполнитьДополнительныеРеквизитыВзаиморасчетов();
	
	АвтоподборКонтактов.ПодключитьОбработчикиСобытияАвтоподбор(ЭтотОбъект, ОписаниеПолейДляАвтоподбора());
	
	ПечатьДокументовУНФ.КорректировкаРазмещениеПодчиненнойГруппыКомандПечати(ЭтаФорма, Элементы.ПодменюПечать, Элементы.ПодменюДоговорКонтрагента);
	Элементы.КомандыПечатиДоговорКонтрагента.Вид = ВидГруппыФормы.ГруппаКнопок;
	ШаблоныПечатиОфисныхДокументов.ОпределитьВидимостьКомандШаблоновПечати(Элементы.ФормаОткрытьШаблоныПечатиДоговоровКонтрагентов);
	
	УстановитьВидимостьИДоступностьМобильноеПриложениеИРМК();
	УстановитьВидимостьИДоступность();
	НастроитьФормуМобильныйКлиент();
	
	ТекущийКонтакт = Неопределено;
	Если Параметры.Свойство("ТекущийКонтакт", ТекущийКонтакт) Тогда
		ТекущиеКонтакты = ДанныеКонтактныхЛиц.НайтиСтроки(Новый Структура("КонтактноеЛицо", ТекущийКонтакт));
		Если ТекущиеКонтакты.Количество() <> 0 Тогда
			ТекущийЭлемент = Элементы.Найти("НаименованиеКонтакт_" + ДанныеКонтактныхЛиц.Индекс(ТекущиеКонтакты[0]));
		КонецЕсли;
	КонецЕсли;

	УстановитьНастройкиКонтроляДублей();
	СозданКопированием = ЗначениеЗаполнено(Параметры.ЗначениеКопирования);
	
	// КабинетКлиента
	УстановитьОтображениеКабинетаКлиента();
	// Конец КабинетКлиента
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ПустаяСтрока(ОписаниеОшибкиЗаполнения) Тогда
		ОбработчикЗавершенияОбработкиОшибки = Новый ОписаниеОповещения(
		"ОбработатьОшибкуЗаполнитьРеквизитыПоИННЗавершение",
		ЭтотОбъект,
		Объект.ИНН);
		РаботаСКонтрагентамиКлиент.ОбработатьОшибку(ОписаниеОшибкиЗаполнения, ОбработчикЗавершенияОбработкиОшибки);
	КонецЕсли;
	
	УправлениеФормой();
	
	Если ИспользоватьСпаркРиски Тогда
		// ИнтернетПоддержкаПользователей.СПАРКРиски
		СПАРКРискиКлиент.ПриОткрытии(ЭтотОбъект, Объект);
		// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	КонецЕсли;
	
	// Отключаем проверку контрагентов для РМК
	Если НЕ УправлениеДоступомУНФКлиентПовтИсп.ЕстьПрофильРабочееМестоКассира() Тогда
		// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
		ПроверкаКонтрагентовКлиент.ПриОткрытииКонтрагент(ЭтотОбъект);
		// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ЭлектронноеВзаимодействиеУНФКлиент.КомандыЭДО_ПриОткрытии(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы И ЗначениеЗаполнено(СобытиеДляПоказаПослеЗакрытия) Тогда
		ПоказатьЗначение(, СобытиеДляПоказаПослеЗакрытия);
	КонецЕсли;
	
	Если КонтрагентЗамененНаДубль Тогда
		ОповеститьОЗаписиНового(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	//  Загрузили данные контрагента из XML (БЭД)
	Если СтрНайти("ОбновитьСостояниеЭД", ИмяСобытия) > 0 Тогда
		Прочитать();
	КонецЕсли;
	
	Если ИмяСобытия = "ИзменилисьСчетаРасчетов" И Источник = Объект.Ссылка Тогда
		
		Объект.СчетУчетаРасчетовСПокупателем = Параметр.СчетУчетаРасчетовСПокупателем;
		Объект.СчетУчетаАвансовПокупателя = Параметр.СчетУчетаАвансовПокупателя;
		Объект.СчетУчетаРасчетовСПоставщиком = Параметр.СчетУчетаРасчетовСПоставщиком;
		Объект.СчетУчетаАвансовПоставщику = Параметр.СчетУчетаАвансовПоставщику;
		Модифицированность = Истина;
		
	ИначеЕсли ИмяСобытия = "УстановкаОсновногоСчета" И Параметр.Владелец = Объект.Ссылка Тогда
		
		Объект.БанковскийСчетПоУмолчанию = Параметр.НовыйОсновнойСчет;
		Если НЕ Модифицированность Тогда
			Записать();
		КонецЕсли;
		Оповестить("УстановкаОсновногоСчетаВыполнена");
		
	ИначеЕсли ИмяСобытия = "УстановкаКонтактногоЛица" И Параметр.Контрагент = Объект.Ссылка Тогда
		
		Объект[Параметр.Реквизит] = Параметр.КонтактноеЛицо;
		Если НЕ Модифицированность Тогда
			Записать();
		КонецЕсли;
		Оповестить("УстановкаРеквизитаВыполнена",Параметр.КонтактноеЛицо);
		
	ИначеЕсли ИмяСобытия = "Запись_КонтактноеЛицо" 
		И (ДанныеКонтактныхЛиц.НайтиСтроки(Новый Структура("КонтактноеЛицо",Параметр.КонтактноеЛицо)).Количество() <> 0
		ИЛИ (Источник <> Неопределено И Источник.ВладелецФормы = ЭтотОбъект)) Тогда
		
		ОбновитьДанныеОбъекта();
		ЗаполнитьИОбновитьКонтактныеЛица();
		
	ИначеЕсли ИмяСобытия = "Выбор_КонтактноеЛицо" Тогда
		
		Если Источник.ВладелецФормы = Неопределено Тогда
			Возврат;
		КонецЕсли;
		Если Источник.ВладелецФормы = ЭтотОбъект Тогда
			ОбновитьЭлементТаблицыКонтактныхЛиц(ИндексВыбранногоКонтактногоЛица,Параметр.Контакт);
			ТекущийЭлемент = Элементы.НаименованиеПолное;
			ПриИзмененииНаименованияКонтактногоЛица();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "УстановитьСвязь_Дубли" И (Источник <> Неопределено И Источник.ВладелецФормы = ЭтотОбъект) Тогда
		
		НовоеКонтактноеЛицо = ДанныеКонтактныхЛиц.Добавить();		
		ИндексКонтактногоЛица = ДанныеКонтактныхЛиц.Индекс(НовоеКонтактноеЛицо);
		ОбновитьЭлементТаблицыКонтактныхЛиц(ИндексКонтактногоЛица,Параметр.КонтактноеЛицо);

	ИначеЕсли ИмяСобытия = "Запись_ШаблоныПечатиОфисныхДокументов" И Параметр.Свойство("Назначение") Тогда
		
		Если Параметр.Назначение = ПредопределенноеЗначение("Перечисление.НазначенияШаблоновПечатиОфисныхДокументов.ДоговорКонтрагента") Тогда
			ГруппаКомандПечати = Элементы.ПодменюДоговорКонтрагента;
			ШаблоныПечатиОфисныхДокументовКлиент.УстановитьПризнакПоявленияНовойКомандыПечати(ГруппаКомандПечати);
		КонецЕсли;
		
	Иначе
		
		// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
		ЭлектронноеВзаимодействиеУНФКлиент.КомандыЭДО_ФормаЭлементаОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
		// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
		
	КонецЕсли;
	
	// Обсуждения
	ОбсужденияУНФКлиент.ОбработкаОповещения(ИмяСобытия, Параметр, Источник, ЭтотОбъект, Объект.Ссылка);
	// Конец Обсуждения
	
	Если ИспользоватьСпаркРиски Тогда
		// ИнтернетПоддержкаПользователей.СПАРКРиски
		СПАРКРискиКлиент.ОбработкаОповещения(ЭтотОбъект, Объект, ИмяСобытия, Параметр, Источник);
		// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	// УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект); // для проверки внедрения БСП
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриСозданииПриЧтенииНаСервере();
	ЗаполнитьДополнительныеРеквизитыВзаиморасчетов();
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если СозданКопированием И Элементы.ФормаПроверитьКонтрагентаНаДубли.Видимость Тогда
		
		Состояние(НСтр("ru='Проверка контрагента на дубли'"), 49);
		ПроверитьКонтрагентаНаДублиСервер();
		Состояние(НСтр("ru='Проверка контрагента на дубли'"), 100);
		
		ПоказатьСообщениеОДублях();
	КонецЕсли;
	
	Если Элементы.ДублиНаименованиеПолное.Видимость
		ИЛИ Элементы.ДублиНаименование.Видимость ИЛИ ДублиКИ.Количество() > 0 Тогда
		ПоказатьПредупреждениеОДублях();
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	// СтандартныеПодсистемы.ОценкаПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени("СправочникКонтрагентыЗапись");
	// Конец СтандартныеПодсистемы.ОценкаПроизводительности
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	// УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект); // для проверки внедрения БСП
	КонтактнаяИнформацияУНФ.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПередЗаписьюНаСервереКонтрагент(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ТегированиеОбъектов.ПередЗаписьюНаСервере(ЭтотОбъект,ТекущийОбъект);
	
	ОсновноеКонтактноеЛицо = ДанныеКонтактныхЛиц.НайтиСтроки(Новый Структура("Основной", Истина));
	Если ОсновноеКонтактноеЛицо.Количество() > 0 Тогда
		Если ЗначениеЗаполнено(ОсновноеКонтактноеЛицо[0].КонтактноеЛицо) Тогда
			ТекущийОбъект.КонтактноеЛицо = ОсновноеКонтактноеЛицо[0].КонтактноеЛицо
		Иначе
			ТекущийОбъект.КонтактноеЛицо = Справочники.КонтактныеЛица.ПолучитьСсылку();
			ТекущийОбъект.ДополнительныеСвойства.Вставить("НовоеОсновноеКонтактноеЛицо", ТекущийОбъект.КонтактноеЛицо);
		КонецЕсли;
	КонецЕсли;
	
	КонтактноеЛицоПодписант = ДанныеКонтактныхЛиц.НайтиСтроки(Новый Структура("Подписант", Истина));
	Если КонтактноеЛицоПодписант.Количество() > 0 Тогда
		Если ЗначениеЗаполнено(КонтактноеЛицоПодписант[0].КонтактноеЛицо) Тогда
			ТекущийОбъект.КонтактноеЛицоПодписант = КонтактноеЛицоПодписант[0].КонтактноеЛицо;
		Иначе
			ТекущийОбъект.КонтактноеЛицоПодписант = Справочники.КонтактныеЛица.ПолучитьСсылку();
			ТекущийОбъект.ДополнительныеСвойства.Вставить("НовоеКонтактноеЛицоПодписант", ТекущийОбъект.КонтактноеЛицо);
		КонецЕсли;
	КонецЕсли;

	ПараметрыОтбораКонтактноеЛицо = Новый Структура;
	ПараметрыОтбораКонтактноеЛицо.Вставить("КонтактноеЛицо",ТекущийОбъект.КонтактноеЛицо);
	НайденноеКонтактноеЛицо = НедействительныеСвязиКонтактныеЛица.НайтиСтроки(ПараметрыОтбораКонтактноеЛицо);
	
	ПараметрыОтбораКонтактноеЛицоПодписант = Новый Структура;
	ПараметрыОтбораКонтактноеЛицоПодписант.Вставить("КонтактноеЛицо",ТекущийОбъект.КонтактноеЛицоПодписант);
	НайденноеКонтактноеЛицоПодписант = НедействительныеСвязиКонтактныеЛица.НайтиСтроки(ПараметрыОтбораКонтактноеЛицоПодписант);
	
	Если НайденноеКонтактноеЛицо.Количество() <> 0 Тогда
		ТекущийОбъект.КонтактноеЛицо = Неопределено;
	КонецЕсли;
	
	Если НайденноеКонтактноеЛицоПодписант.Количество() <> 0 Тогда
		ТекущийОбъект.КонтактноеЛицоПодписант = Неопределено;
	КонецЕсли;
			
	// Заполним основное контактное лицо первым имеющимся
	Если Не ЗначениеЗаполнено(ТекущийОбъект.КонтактноеЛицо) Тогда
		Для Каждого ДанныеКЛ Из ДанныеКонтактныхЛиц Цикл
			Если ЗначениеЗаполнено(ДанныеКЛ.КонтактноеЛицо) Тогда
				ТекущийОбъект.КонтактноеЛицо = ДанныеКЛ.КонтактноеЛицо;
				Прервать;
			ИначеЕсли Не ПустаяСтрока(ДанныеКЛ.Наименование) И ТипЗнч(ДанныеКЛ.Наименование) = Тип("Строка") Тогда
				ТекущийОбъект.КонтактноеЛицо = Справочники.КонтактныеЛица.ПолучитьСсылку();
				ТекущийОбъект.ДополнительныеСвойства.Вставить("НовоеОсновноеКонтактноеЛицо", ТекущийОбъект.КонтактноеЛицо);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	// Заполним информацию о записываемых контактных лицах для реквизита "ОсновныеСведения"
	МассивСтрок = Новый Массив;
	Для Каждого ДанныеКЛ Из ДанныеКонтактныхЛиц Цикл
		
		Если ПустаяСтрока(ДанныеКЛ.Наименование) Тогда
			Продолжить;
		КонецЕсли;
		
		Если МассивСтрок.Количество() > 0 Тогда
			МассивСтрок.Добавить(Символы.ПС);
		КонецЕсли;
		
		МассивСтрок.Добавить(ДанныеКЛ.Наименование);
		
		Для Каждого ДанныеКИ Из ДанныеКЛ.КонтактнаяИнформация Цикл
			Если ПустаяСтрока(ДанныеКИ.Представление) Тогда
				Продолжить;
			КонецЕсли;
			МассивСтрок.Добавить(ДанныеКИ.Представление);
		КонецЦикла;
		
	КонецЦикла;
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ОсновныеСведенияКонтактныхЛиц", МассивСтрок);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ЭтоГоловной", ЭтотКонтрагентЯвляетсяГоловным);
	
	Если ПроигнорированоСообщениеОДублях Тогда 
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ЭтоЗаписьДубля");
		ТекущийОбъект.ДополнительныеСвойства.Вставить("СообщениеОДублированииИнформации", СообщениеОДублированииИнформации);
	КонецЕсли;
	
	ПроигнорированоСообщениеОДублях = Ложь;
	СообщениеОДублированииИнформации = "";

	// Обсуждения
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Модифицированность", Модифицированность);
	// Конец Обсуждения
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ВидОперацииКомиссия", ВидОперацииКомиссия);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьДанныеКонтактныхЛиц(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПрочитатьДанныеПанелиДопИнформации();
	
	ЗаполнитьИОбновитьКонтактныеЛица();
	
	Если СопоставитьКонтактИАдресЭПВСобытияхПослеЗаписиОбъекта Тогда
		ЭлектроннаяПочтаУНФ.СопоставитьВФонеКонтактИАдресЭПВСобытиях(
		ТекущийОбъект.Ссылка,
		АдресЭП(),
		УникальныйИдентификатор);
		
		СопоставитьКонтактИАдресЭПВСобытияхПослеЗаписиОбъекта = Ложь;
	КонецЕсли;
	
	Если ИспользоватьСпаркРиски Тогда
		// ИнтернетПоддержкаПользователей.СПАРКРиски
		СПАРКРиски.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
		// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	КонецЕсли;
	
	//Обсуждения
	ОбсужденияУНФ.ПослеЗаписиНаСервере(ТекущийОбъект);	
	// Конец Обсуждения
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	
	УстановитьЗаголовокФормы(ЭтотОбъект);
	Оповестить("Запись_Контрагент", Объект.Ссылка, ЭтотОбъект);
	
	Для Каждого КонтактноеЛицо Из ДанныеКонтактныхЛиц Цикл
		Оповестить("ИзменениеКонтактногоЛица_Контрагент",КонтактноеЛицо.КонтактноеЛицо,ЭтотОбъект);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	// УправлениеКонтактнойИнформацией.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Объект, Отказ); // для проверки
	// внедрения БСП
	КонтактнаяИнформацияУНФ.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Отказ);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
	ПроверитьЗаполнениеКонтактныхЛиц(Отказ);
	ПроверитьЗаполнениеКонтактнойИнформацииКонтактныхЛиц(Отказ);
	
	ПроверитьЗаполнениеОбязательныхРеквизитов(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.Контрагенты.Форма.РедактированиеИсторииНаименований") Тогда
		
		УстановитьНаименованиеПослеРедактированияИстории(ВыбранноеЗначение.ИсторияНаименований);
		
	ИначеЕсли ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.Контрагенты.Форма.РедактированиеИсторииКПП") Тогда
		
		УстановитьКПППослеРедактированияИстории(ВыбранноеЗначение.ИсторияКПП);
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КППНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура("ТекущийКПП, ИсторияКПП, ТолькоПросмотр", 
					Объект.КПП, Объект.ИсторияКПП, ТолькоПросмотр);
	
	ОткрытьФорму("Справочник.Контрагенты.Форма.РедактированиеИсторииКПП", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура("ТекущееНаименованиеПолное, ИсторияНаименований, ТолькоПросмотр", 
					Объект.НаименованиеПолное, Объект.ИсторияНаименований, ТолькоПросмотр);
	
	ОткрытьФорму("Справочник.Контрагенты.Форма.РедактированиеИсторииНаименований", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ГоловнойКонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение)
		И Объект.Ссылка = ВыбранноеЗначение Тогда
		
		ВыбранноеЗначение = Неопределено;
		
		ТекстСообщения = НСтр("ru ='Выбрано некорректное значение для головного контрагента.
			|Головной контрагент совпадает с текущим.'");
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , , "Объект.ГоловнойКонтрагент");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументУдостоверяющийЛичностьПриИзменении(Элемент)
	УстановитьЗаголовокЮридическихДанных(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОстатокВзаиморасчетовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("СформироватьПриОткрытии", Истина);
	ОтборРасшифровки = Новый Структура;
	ОтборРасшифровки.Вставить("Контрагент", Объект.Ссылка);
	ПараметрыОткрытия.Вставить("Отбор", ОтборРасшифровки);
	Вариант =  СсылкаВариантаОтчета("АктСверки", "АктСверкиКонтекст");
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтаФорма, Вариант, ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПродажОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючВарианта", "ДинамикаПродажПоПокупателям");
	ПараметрыФормы.Вставить("Отбор", Новый Структура("Период,Контрагент", Новый СтандартныйПериод, Объект.Ссылка));
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.Продажи.Форма", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеКонтактногоЛица_ПриИзменении(Элемент)
	
	ПриИзмененииНаименованияКонтактногоЛица();
	ОпределитьВидКонтрагента(ЭтотОбъект);
	УстановитьЗаголовокЮридическихДанных(ЭтотОбъект);
	УправлениеФормой();
	
	ИндексКЛ = Число(Сред(Элемент.Имя, СтрДлина("НаименованиеКонтакт_")+1));
	ДанныеКонтактныхЛиц[ИндексКЛ].Изменен = Истина;
	
	ЗаголовкиГрупп = ЗаголовкиГруппКонтакта(ДанныеКонтактныхЛиц[ИндексКЛ]);
	
	Элементы["Контакт_"+ИндексКЛ].ЗаголовокСвернутогоОтображения = ЗаголовкиГрупп.ЗаголовокСвернутойГруппы;
	Элементы["Контакт_"+ИндексКЛ].Заголовок 					 = ЗаголовкиГрупп.ЗаголовокГруппы;
		
	Если НЕ ПроверятьПредставлениеНаДублиКонтакт Тогда
		Возврат;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ДанныеКонтактныхЛиц[ИндексКЛ].Наименование) Тогда
		ПоказатьСкрытьНадписьОДубляхНаименованияКЛ(ИндексКЛ, Ложь);
	КонецЕсли;
	
	ПроверитьНаименованиеКонтактаНаДубли(ИндексВыбранногоКонтактногоЛица);
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьКонтактПриИзменении(Элемент)
	
	ИндексКЛ = Число(Сред(Элемент.Имя, СтрДлина("ДолжностьКонтакт_")+1));
	ТекущийКонтакт = ДанныеКонтактныхЛиц[ИндексКЛ];
	ТекущийКонтакт.Изменен = Истина;
	
	ЗаголовкиГрупп = ЗаголовкиГруппКонтакта(ДанныеКонтактныхЛиц[ИндексКЛ]);
	
	Элементы["Контакт_"+ИндексКЛ].ЗаголовокСвернутогоОтображения = ЗаголовкиГрупп.ЗаголовокСвернутойГруппы;
	Элементы["Контакт_"+ИндексКЛ].Заголовок 					 = ЗаголовкиГрупп.ЗаголовокГруппы;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеКонтактногоЛица_ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	// УНФ Автоподбор контактов
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ОписаниеОповещения") Тогда
		СтандартнаяОбработка = Ложь;
		ИндексВыбранногоКонтактногоЛица = Число(Сред(Элемент.Имя, СтрДлина("НаименованиеКонтакт_")+1));
		ВыполнитьОбработкуОповещения(ВыбранноеЗначение);
		ПриИзмененииНаименованияКонтактногоЛица();
		
		Если ВыбранноеЗначение.ИмяПроцедуры = "СозданиеНовогоКонтакта" Тогда
			ЭлементКИ = Элементы.Найти("ПредставлениеКонтакт_"+ИндексВыбранногоКонтактногоЛица+"_КИ_0");
			Если ЭлементКИ <> Неопределено Тогда
				ТекущийЭлемент = ЭлементКИ;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.КлассификаторКонтактов") Тогда
		
		СтандартнаяОбработка = Ложь;
		ИндексВыбранногоКонтактногоЛица = Число(Сред(Элемент.Имя, СтрДлина("НаименованиеКонтакт_")+1));
		ЗаполнитьНаименованиеИКонтактнуюИнформацию(ВыбранноеЗначение, Элемент.Имя);
		ОбновитьЭлементыКонтактныхЛиц();
		ПриИзмененииНаименованияКонтактногоЛица();
		
		ТекущийЭлемент = Элементы.НаименованиеПолное;
		
	КонецЕсли;
	// Конец УНФ Автоподбор контактов
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
		
		СтандартнаяОбработка = Ложь;
		ИндексВыбранногоКонтактногоЛица = Число(Сред(Элемент.Имя, СтрДлина("НаименованиеКонтакт_")+1));
		ОбновитьЭлементТаблицыКонтактныхЛиц(ИндексВыбранногоКонтактногоЛица,ВыбранноеЗначение);
		ТекущийЭлемент = Элементы.НаименованиеПолное;
		
		ПриИзмененииНаименованияКонтактногоЛица();
		ОпределитьВидКонтрагента(ЭтотОбъект);
		УстановитьЗаголовокЮридическихДанных(ЭтотОбъект);
		УправлениеФормой();
		
	КонецЕсли;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		
		СтандартнаяОбработка = Ложь;
		ИндексВыбранногоКонтактногоЛица = Число(Сред(Элемент.Имя, СтрДлина("НаименованиеКонтакт_")+1));
		
		ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].Наименование = ВыбранноеЗначение;
		ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].Изменен = Истина;
		
		ЭлементКИ = Элементы.Найти("ПредставлениеКонтакт_"+ИндексВыбранногоКонтактногоЛица+"_КИ_0");
		Если ЭлементКИ <> Неопределено Тогда
			ТекущийЭлемент = ЭлементКИ;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеКонтактногоЛица_ОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Индекс = Число(Сред(Элемент.Имя, СтрДлина("НаименованиеКонтакт_")+1));

	Элементы.Найти("УстановитьСвязь_" + Индекс).Доступность = Ложь;
	Элементы.Найти("РазорватьСвязь_" + Индекс).Доступность = Истина;
	
	ЭлементКИ = Элементы.Найти("ПредставлениеКонтакт_"+Индекс+"_КИ_0");
	Если ЭлементКИ <> Неопределено Тогда
		ТекущийЭлемент = ЭлементКИ;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеКонтактногоЛица_ИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Индекс = Число(Сред(Элемент.Имя, СтрДлина("НаименованиеКонтакт_")+1));
	Если ЗначениеЗаполнено(ДанныеКонтактныхЛиц[Индекс].КонтактноеЛицо) Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеКонтактногоЛица_НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ИндексВыбранногоКонтактногоЛица = Число(Сред(Элемент.Имя, СтрДлина("НаименованиеКонтакт_")+1));
	ПараметрыФормы.Вставить("ОткрытиеИзФормыКонтрагента",Истина);
	ПараметрыФормы.Вставить("РежимВыбора",Истина);
	ОткрытьФорму("Справочник.КонтактныеЛица.ФормаСписка",ПараметрыФормы,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеКонтактногоЛица_Открытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ИндексВыбранногоКонтактногоЛица = Число(Сред(Элемент.Имя, СтрДлина("НаименованиеКонтакт_")+1));
	
	Если ЗначениеЗаполнено(ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].КонтактноеЛицо) Тогда
		ПараметрыФормы.Вставить("Ключ",ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].КонтактноеЛицо);
		ПараметрыФормы.Вставить("ТекущийКонтрагент", Объект.Ссылка);
		ОткрытьФорму("Справочник.КонтактныеЛица.ФормаОбъекта",ПараметрыФормы);
		Возврат;
	КонецЕсли;
	
	ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].Изменен = Истина;
	ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].Наименование = Элемент.ТекстРедактирования;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Оповещение = Новый ОписаниеОповещения("ОбработатьВопросСозданиеКЛ",ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные еще не записаны." + Символы.ПС + "Переход к созданию контакта возможен только после записи данных." + Символы.ПС + "Данные будут записаны'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы.Вставить("НаименованиеКонтакта",Элемент.ТекстРедактирования);
	ПараметрыФормы.Вставить("Контрагент",Объект.Ссылка);
	
	ОткрытьФорму("Справочник.КонтактныеЛица.ФормаОбъекта",ПараметрыФормы,ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеПриИзменении(Элемент)
	
	Объект.НаименованиеПолное = СтрЗаменить(Объект.НаименованиеПолное, Символы.ПС, " ");
	ПриИзмененииНаименованияКонтрагента();
		
	ОпределитьВидКонтрагента(ЭтотОбъект);
	УстановитьЗаголовокЮридическихДанных(ЭтотОбъект);
	УправлениеФормой();
	
	Если НЕ ПроверятьЮрНазваниеНаДубли И ПроверятьПредставлениеНаДубли Тогда
		ПроверитьНаДубли("Наименование");
	КонецЕсли;
	
	Если НЕ ПроверятьЮрНазваниеНаДубли Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Объект.НаименованиеПолное)) Тогда
		Элементы.ДублиНаименованиеПолное.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ПроверитьНаДубли("НаименованиеПолное");

КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если НЕ ПроверятьПредставлениеНаДубли Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Объект.Наименование)) Тогда
		Элементы.ДублиНаименование.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	НаименованиеДляПоискаДублей = НаименованиеДляПоискаДублей(Объект.Наименование);
	ПроверитьНаДубли("Наименование");

КонецПроцедуры

&НаКлиенте
Процедура ВидКонтрагентаПриИзменении(Элемент)
	
	ВыполнитьВсеПроверки(ЭтотОбъект);
	
	УстановитьЗаголовокЮридическихДанных(ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Объект.СтранаРегистрации)
		И Объект.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ЮридическоеЛицо") Тогда
		Объект.СтранаРегистрации = ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
	КонецЕсли;
	
	УправлениеФормой();
	СохранитьВосстановитьВведенноеЗначениеКПП();
	
	Если ИспользоватьСпаркРиски Тогда
		// ИнтернетПоддержкаПользователей.СПАРКРиски
		ЭтотОбъект.ИндексыСПАРКРиски = Неопределено;
		ОбновитьОтображениеИндексыСПАРК();
		// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	УстановитьРеквизитыПроверкиКонтрагентов(ЭтотОбъект);
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентаВСправочнике(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура СтранаРегистрацииПриИзменении(Элемент)
	
	СформироватьПредставлениеПроверкиОГРН(ЭтотОбъект);
	РаботаСКонтрагентамиКлиентСерверПереопределяемый.СформироватьПредставлениеПроверкиДанных(ЭтотОбъект);
	
	УправлениеФормой();
	
	Если ИспользоватьСпаркРиски Тогда
		// ИнтернетПоддержкаПользователей.СПАРКРиски
		ЭтотОбъект.ИндексыСПАРКРиски = Неопределено;
		ОбновитьОтображениеИндексыСПАРК();
		// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	УстановитьРеквизитыПроверкиКонтрагентов(ЭтотОбъект);
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентаВСправочнике(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	
	СформироватьПредставлениеПроверкиИНН(ЭтотОбъект);
	
	Если Объект.ИННВведенКорректно И ПустаяСтрока(Объект.КПП) И Элементы.КПП.Видимость Тогда
		РаботаСКонтрагентамиКлиентСерверПереопределяемый.ЗаполнитьКПППоИНН(Объект.ИНН, Объект.КПП);
	КонецЕсли;
	
	СформироватьПредставлениеПроверкиДублей(ЭтотОбъект);
	
	РаботаСКонтрагентамиКлиентСерверПереопределяемый.СформироватьПредставлениеПроверкиДанных(ЭтотОбъект);
	УстановитьЗаголовокЮридическихДанных(ЭтотОбъект);
	
	Если Не ПустаяСтрока(Объект.ИНН) И Объект.ИННВведенКорректно И Не КлючевыеРеквизитыЗаполнены(ЭтотОбъект) Тогда
		ВыполнитьЗаполнениеРеквизитовПоИНН(Объект.ИНН);
	КонецЕсли;
	
	Если ИспользоватьСпаркРиски Тогда
		// ИнтернетПоддержкаПользователей.СПАРКРиски
		ЭтотОбъект.ИндексыСПАРКРиски = Неопределено;
		ОбновитьОтображениеИндексыСПАРК();
		// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	УстановитьРеквизитыПроверкиКонтрагентов(ЭтотОбъект);
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентаВСправочнике(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура КПППриИзменении(Элемент)
	
	СформироватьПредставлениеПроверкиКПП(ЭтотОбъект);
	СформироватьПредставлениеПроверкиДублей(ЭтотОбъект);
	
	РаботаСКонтрагентамиКлиентСерверПереопределяемый.СформироватьПредставлениеПроверкиДанных(ЭтотОбъект);
	
	УстановитьЗаголовокЮридическихДанных(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	УстановитьРеквизитыПроверкиКонтрагентов(ЭтотОбъект);
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентаВСправочнике(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура КодПоОКПОПриИзменении(Элемент)
	
	СформироватьПредставлениеПроверкиОКПО(ЭтотОбъект);
	РаботаСКонтрагентамиКлиентСерверПереопределяемый.СформироватьПредставлениеПроверкиДанных(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрационныйНомерПриИзменении(Элемент)
	
	СформироватьПредставлениеПроверкиОГРН(ЭтотОбъект);
	РаботаСКонтрагентамиКлиентСерверПереопределяемый.СформироватьПредставлениеПроверкиДанных(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидГосударственногоОрганаПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПроверкиДанныхОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если СтрНайти(НавигационнаяСсылкаФорматированнойСтроки, "ПоказатьДубли") > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ИНН", СокрЛП(Объект.ИНН));
		ПараметрыФормы.Вставить("КПП", СокрЛП(Объект.КПП));
		ПараметрыФормы.Вставить("СсылкаНаРедактируемыйОбъект", Объект.Ссылка);
		ПараметрыФормы.Вставить("ИсключаяКонтакты", Новый Массив);
		ПараметрыФормы.Вставить("ЭтоЮрЛицо", ЭтоЮрЛицо(Объект.ВидКонтрагента));
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыСпискаДублей", ЭтотОбъект);
		ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаВыбораДублей", ПараметрыФормы, Элемент,,,,ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОблакоТеговОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОблакоТеговОбработкаНавигационнойСсылкиСервер(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПокупательПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИндексыСПАРКРискиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	СПАРКРискиКлиент.ОбработкаНавигационнойСсылки(ЭтотОбъект, Элемент,
		НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	
КонецПроцедуры

&НаКлиенте
Процедура ВестиРасчетыПоДокументамПриИзменении(Элемент)
	
	Если ВключенаОтчетность() И НЕ Объект.ВестиРасчетыПоДокументам Тогда
		Объект.ВестиРасчетыПоДокументам = Истина;
		ПоказатьПредупреждение(,НСтр("ru = 'При включенном разделе ""Налоги"" необходимо всегда вести расчеты по документам'"));
		
	КонецЕсли;
	
	ВестиУчетОплатыПоНакладным = Объект.ВестиРасчетыПоДокументам;
	
	СформироватьЗаголовокГруппыДеньги();
	
КонецПроцедуры

&НаКлиенте
Процедура ВестиРасчетыПоЗаказамПриИзменении(Элемент)
	
	Если Объект.ВестиРасчетыПоЗаказам И ЕстьКомиссияБезЗаказов() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'У контрагента есть договоры комиссии по которым не ведется учет по заказам. Изменение не возможно.'"));
		Объект.ВестиРасчетыПоЗаказам = Ложь;
		Возврат;
	КонецЕсли;
	
	ВестиУчетОплатыПоЗаказам = Объект.ВестиРасчетыПоЗаказам;
	СформироватьЗаголовокГруппыДеньги();
	
КонецПроцедуры

&НаКлиенте
Процедура ВестиРасчетыПоДоговорамПриИзменении(Элемент)
	
	СформироватьЗаголовокГруппыДеньги();
	УстановитьЗаголовкиИВидимостьПриИзмененииФлагаРасчетовПоДоговорам();
	
КонецПроцедуры

&НаКлиенте
Процедура ВестиУчетОплатыПоСчетамПриИзменении(Элемент)
	
	СформироватьЗаголовокГруппыДеньги();
	
КонецПроцедуры

&НаКлиенте
Процедура ГоловнойКонтрагентПриИзменении(Элемент)
	
	ГоловнойКонтрагентПриИзмененииНаСервере();
	
	УправлениеФормой();
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	УстановитьРеквизитыПроверкиКонтрагентов(ЭтотОбъект);
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентаВСправочнике(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ДублиПоПолномуНаименованиюНажатие(Элемент)
	
	ПараметрыДублей = Новый Структура;	
	МассивКонтактов = Новый Массив;
	
	Для Каждого Контакт Из ЭтотОбъект.ДанныеКонтактныхЛиц Цикл
		МассивКонтактов.Добавить(Контакт.КонтактноеЛицо);
	КонецЦикла;
	
	ПараметрыДублей.Вставить("ИсключаяКонтакты", МассивКонтактов);
	ПараметрыДублей.Вставить("НаименованиеПолное", НаименованиеПолноеДляПоискаДублей);
	ПараметрыДублей.Вставить("СсылкаНаРедактируемыйОбъект", Объект.Ссылка);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыСпискаДублей", ЭтотОбъект);
	ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаВыбораДублей", ПараметрыДублей, ЭтотОбъект,,,,ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДублиПоПредставлениюНажатие(Элемент)
	
	ПараметрыДублей = Новый Структура;
	МассивКонтактов = Новый Массив;
	
	Для Каждого Контакт Из ЭтотОбъект.ДанныеКонтактныхЛиц Цикл
		МассивКонтактов.Добавить(Контакт.КонтактноеЛицо);
	КонецЦикла;
	
	ПараметрыДублей.Вставить("ИсключаяКонтакты", МассивКонтактов);
	ПараметрыДублей.Вставить("Наименование", НаименованиеДляПоискаДублей);
	ПараметрыДублей.Вставить("СсылкаНаРедактируемыйОбъект", Объект.Ссылка);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыСпискаДублей", ЭтотОбъект);
	ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаВыбораДублей", ПараметрыДублей, ЭтотОбъект,,,,ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ПодразделенияНажатие(Элемент)
	ОповещениеОВыбореКонтрагента = Новый ОписаниеОповещения("ВыборКонтрагентаИзИерархии",ЭтотОбъект);
	СписокИерархии = СписокЗначенийИерархииКонтрагента(Объект.ГоловнойКонтрагент,
		Объект.Ссылка);
	ПоказатьВыборИзМеню(ОповещениеОВыбореКонтрагента,СписокИерархии,Элементы.Подразделения);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьВсеКонтакты(Команда)
	
	ПоказыватьВсеКонтакты = Истина;
	ОбновитьЭлементыКонтактныхЛиц();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьШаблоныПечатиДоговоровКонтрагентов(Команда)
	
	ШаблоныПечатиОфисныхДокументовКлиент.ОткрытьШаблоныПечатиОфисныхДокументов(
	ПредопределенноеЗначение("Перечисление.НазначенияШаблоновПечатиОфисныхДокументов.ДоговорКонтрагента"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоДаннымЕГР(Команда)
	
	Если Не ПустаяСтрока(Объект.ИНН) И Объект.ИННВведенКорректно Тогда
	// Выполняем заполнение по ИНН без открытия вспомогательной формы
		
		Если КлючевыеРеквизитыЗаполнены(ЭтотОбъект) Тогда
			ТекстВопроса = НСтр("ru='Перезаполнить текущие реквизиты?'");
			ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПерезаполнитьРеквизитыПоИННЗавершение", ЭтотОбъект);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			ВыполнитьЗаполнениеРеквизитовПоИНН(Объект.ИНН);
		КонецЕсли;
		
	Иначе
	// Открываем форму заполнения реквизитов
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СтрокаПоиска", ?(ПустаяСтрока(Объект.НаименованиеПолное), Объект.ИНН, Объект.НаименованиеПолное));
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнениеРеквизитовКонтрагентВыбран", ЭтотОбъект);
		ОткрытьФорму("ОбщаяФорма.ЗаполнениеРеквизитовКонтрагента", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоляКонтактногоЛица(Команда)
	
	ДанныеКЛ = ДанныеКонтактныхЛиц.Добавить();
	
	ЗаполнитьВсегдаВыводимыеВидыКИ(
		ДанныеКЛ.КонтактнаяИнформация,
		СвойстваВидовКонтактнойИнформацииКонтактныхЛиц);
		
	ПоказыватьВсеКонтакты = ДанныеКонтактныхЛиц.Индекс(ДанныеКЛ) > 5;
	
	ОбновитьЭлементыКонтактныхЛиц();
	ТекущийЭлемент = Элементы["НаименованиеКонтакт_" + ДанныеКонтактныхЛиц.Индекс(ДанныеКЛ)];
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПолеКонтактнойИнформацииКонтактногоЛица(Команда)
	
	ИндексКонтакта = Число(Сред(ТекущийЭлемент.Имя, СтрДлина("ДобавитьПолеКонтактнойИнформацииКонтактногоЛица_")+1));
	ДанныеКонтакта = ДанныеКонтактныхЛиц[ИндексКонтакта];
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИндексКонтакта", ИндексКонтакта);
	ДополнительныеПараметры.Вставить("МножественнаяФормаВладельца", НСтр("ru='контактных лиц'"));
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьКонтактнуюИнформациюКонтактаВидВыбран", ЭтотОбъект, ДополнительныеПараметры);
	
	СписокДоступныхВидов = Новый СписокЗначений;
	Отбор = Новый Структура("Вид");
	Для Каждого СтрокаТаблицы Из СвойстваВидовКонтактнойИнформацииКонтактныхЛиц Цикл
		Отбор.Вид = СтрокаТаблицы.Вид;
		Если СтрокаТаблицы.РазрешитьВводНесколькихЗначений Или ДанныеКонтакта.КонтактнаяИнформация.НайтиСтроки(Отбор).Количество() = 0 Тогда
			СписокДоступныхВидов.Добавить(СтрокаТаблицы.Вид, СтрокаТаблицы.ПредставлениеВида);
		КонецЕсли;
	КонецЦикла;
	
	ПоказатьВыборИзСписка(ОписаниеОповещения, СписокДоступныхВидов, ТекущийЭлемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДополнительныйРеквизит(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущийНаборСвойств", ПредопределенноеЗначение("Справочник.НаборыДополнительныхРеквизитовИСведений.Справочник_Контрагенты"));
	ПараметрыФормы.Вставить("ЭтоДополнительноеСведение", Ложь);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта", ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСвязь(Команда)
	
	ИндексВыбранногоКонтактногоЛица = Число(Сред(Команда.Имя, СтрДлина("КомандаУстановитьСвязь_")+1));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОткрытиеИзФормыКонтрагента",Истина);
	ПараметрыФормы.Вставить("РежимВыбора",Истина);
	ОткрытьФорму("Справочник.КонтактныеЛица.ФормаСписка",ПараметрыФормы,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РазорватьСвязь(Команда)
	
	ИндексВыбранногоКонтактногоЛица = Число(Сред(Команда.Имя, СтрДлина("КомандаРазорватьСвязь_")+1));
	ДанныеКонтактногоЛица = ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица];
	
	КонтактОсновноеКЛ = ДанныеКонтактногоЛица.КонтактноеЛицо = Объект.КонтактноеЛицо И ЗначениеЗаполнено(Объект.КонтактноеЛицо);
	КонтактПодписант = ДанныеКонтактногоЛица.КонтактноеЛицо = Объект.КонтактноеЛицоПодписант И ЗначениеЗаполнено(Объект.КонтактноеЛицоПодписант);
	Если НЕ КонтактОсновноеКЛ И НЕ КонтактПодписант Тогда
		РазорватьСвязьНаСервере();
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьВопросОсновноеКЛВыборКонтрагента", ЭтотОбъект);
	ПроверитьКонтактНаОсновнойИПодписанта(ДанныеКонтактногоЛица, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерсональныеДанныеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("ЖурналДокументов.СогласияНаОбработкуПерсональныхДанных.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКонтрагентаНаДубли(Команда)
	
	Состояние(НСтр("ru='Проверка контрагента на дубли'"), 49);
	ПроверитьКонтрагентаНаДублиСервер();
	Состояние(НСтр("ru='Проверка контрагента на дубли'"), 100);
	
	ПоказатьСообщениеОДублях();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьКонтактнуюИнформациюПослеВыбораСервер()
	КонтактнаяИнформацияУНФ.ОбновитьЭлементыКонтактнойИнформации(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНаименованиеПослеРедактированияИстории(НаборЗаписей)
	
	Модифицированность = Истина;
	
	НаборЗаписей.Сортировать("Период");
	
	Объект.ИсторияНаименований.Очистить();
	Если НаборЗаписей.Количество() > 1 Тогда
		Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
			ЗаписьИстории = Объект.ИсторияНаименований.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьИстории, ЗаписьНабора);
		КонецЦикла;
	КонецЕсли;
	
	ФормироватьНаименованиеАвтоматически = ПустаяСтрока(Объект.НаименованиеПолное)
		ИЛИ (Объект.НаименованиеПолное = Объект.Наименование);
		
	Объект.НаименованиеПолное = НаборЗаписей[НаборЗаписей.Количество()-1].НаименованиеПолное;
	
	ПриИзмененииНаименованияКонтрагента();
	
	ОпределитьВидКонтрагента(ЭтотОбъект);
	УстановитьЗаголовокЮридическихДанных(ЭтотОбъект);
	УправлениеФормой();
	
	Если НЕ ПроверятьЮрНазваниеНаДубли И ПроверятьПредставлениеНаДубли Тогда
		ПроверитьНаДубли("Наименование");
	КонецЕсли;
	
	Если НЕ ПроверятьЮрНазваниеНаДубли Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Объект.НаименованиеПолное)) Тогда
		Элементы.ДублиНаименованиеПолное.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ПроверитьНаДубли("НаименованиеПолное");
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКПППослеРедактированияИстории(НаборЗаписей)
	
	Модифицированность = Истина;
	
	НаборЗаписей.Сортировать("Период");
	
	Объект.ИсторияКПП.Очистить();
	Если НаборЗаписей.Количество() > 1 Тогда
		Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
			ЗаписьИстории = Объект.ИсторияКПП.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьИстории, ЗаписьНабора);
		КонецЦикла;
	КонецЕсли;
	
	Объект.КПП = НаборЗаписей[НаборЗаписей.Количество()-1].КПП;
	
	СформироватьПредставлениеПроверкиКПП(ЭтотОбъект);
	СформироватьПредставлениеПроверкиДублей(ЭтотОбъект);
	
	РаботаСКонтрагентамиКлиентСерверПереопределяемый.СформироватьПредставлениеПроверкиДанных(ЭтотОбъект);
	
	УстановитьЗаголовокЮридическихДанных(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	УстановитьРеквизитыПроверкиКонтрагентов(ЭтотОбъект);
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентаВСправочнике(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаСервере
Функция ЕстьКомиссияБезЗаказов()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец", Объект.Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Владелец = &Владелец
	|	И ДоговорыКонтрагентов.НеУчитыватьЗаказыПриПередачеНаКомиссию";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	// 1. Инициализация реквизитов формы
	
	КлассификаторОПФ.Загрузить(РегламентированныеДанныеПовтИсп.КлассификаторОрганизационноПравовыхФорм());
	
	ПросмотрВзаиморасчетов = ПравоДоступа("Просмотр", Метаданные.Отчеты.Взаиморасчеты);
	ПросмотрПродаж = ПравоДоступа("Просмотр", Метаданные.Отчеты.Продажи);
	
	// 2. Чтение дополнительных данных
	
	ПрочитатьСвойстваВидовКонтактнойИнформацииКонтактныхЛиц();
	ЗаполнитьТаблицуКонтактнойИнформацииКонтактныхЛиц();
	ОбновитьЭлементыКонтактныхЛиц();
	
	ТегированиеОбъектов.ПриСозданииПриЧтенииНаСервере(ЭтотОбъект,Объект);
	
	ПрочитатьДанныеПанелиДопИнформации();
	
	УстановитьЗаголовокЮридическихДанных(ЭтотОбъект);
	
	// Заполним список возможных кратких наименований и определим флаг автоматической смены краткого наименования
	
	ЗаполнитьСписокВыбораНаименования(ЭтотОбъект);
	
	ФормироватьНаименованиеАвтоматически = ПустаяСтрока(Объект.Наименование)
		Или Элементы.Наименование.СписокВыбора.НайтиПоЗначению(Объект.Наименование) <> Неопределено
		Или (ДанныеКонтактныхЛиц.Количество() > 0 И Элементы.Наименование.СписокВыбора.НайтиПоЗначению(ДанныеКонтактныхЛиц[0].Наименование) <> Неопределено);
	
	// УНФ.КонтактнаяИнформация
	КонтактнаяИнформацияУНФ.ПриСозданииПриЧтенииНаСервере(ЭтотОбъект);
	// Конец УНФ.КонтактнаяИнформация
	
	Если Параметры.Свойство("ДанныеИзКонтактнойФормы") Тогда
		ЗаполнитьПоДаннымКонтактнойФормы(Параметры);
	КонецЕсли;

	Если Параметры.Свойство("КонтактКакСвязаться", КонтактКакСвязаться) Тогда
		ЗаполнитьКонтактКакСвязаться();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормой()
	
	ЭтоЮридическоеЛицо = (Объект.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ЮридическоеЛицо"));
	
	// Изменяем видимость реквизитов формы в зависимости от вида контрагента
	Если ЭтоЮридическоеЛицо Тогда
		
		КонтрагентЗарегистрированВРФ = Объект.СтранаРегистрации = ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
		
		Элементы.СтранаРегистрации.Видимость				= Истина;
		Элементы.ФИО.Видимость								= Ложь;
		Элементы.ИНН.Видимость								= Истина;
		Элементы.КПП.Видимость								= КонтрагентЗарегистрированВРФ;
		Элементы.КодПоОКПО.Видимость						= КонтрагентЗарегистрированВРФ;
		Элементы.РегистрационныйНомер.Видимость				= Истина;
		Элементы.ВидГосударственногоОргана.Видимость		= Ложь;
		Элементы.КодГосударственногоОргана.Видимость		= Ложь;
		Элементы.СвидетельствоСерияНомер.Видимость			= Ложь;
		Элементы.СвидетельствоДатаВыдачи.Видимость			= Ложь;
		Элементы.ДокументУдостоверяющийЛичность.Видимость	= Ложь;
		Элементы.Пол.Видимость								= Ложь;
		Элементы.ДатаРождения.Видимость						= Ложь;
		
		Элементы.ИНН.ПодсказкаВвода							= НСтр("ru = '10 цифр'");
		Элементы.ИНН.ОграничениеТипа						= Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(10));
		Элементы.КодПоОКПО.ПодсказкаВвода					= НСтр("ru = '8 цифр'");
		Элементы.КодПоОКПО.ОграничениеТипа					= Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(8));
		Элементы.НаименованиеПолное.ПодсказкаВвода			= НСтр("ru = 'Юридическое название'");
		
		Если КонтрагентЗарегистрированВРФ Тогда
			Элементы.РегистрационныйНомер.Заголовок			= НСтр("ru = 'ОГРН'");
			Элементы.РегистрационныйНомер.Подсказка			= НСтр("ru = 'Основной государственный регистрационный номер юридического лица'");
			Элементы.РегистрационныйНомер.ПодсказкаВвода	= НСтр("ru = '13 цифр'");
			Элементы.РегистрационныйНомер.ОграничениеТипа	= Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(13));
			Элементы.ИНН.Подсказка							= НСтр("ru = 'Идентификационный номер налогоплательщика'");
		Иначе
			Элементы.РегистрационныйНомер.Заголовок			= НСтр("ru='Рег. номер'");
			Элементы.РегистрационныйНомер.Подсказка			= НСтр("ru='Регистрационный номер, присвоенный иностранной организации в стране регистрации (инкорпорации)'");
			Элементы.РегистрационныйНомер.ПодсказкаВвода	= НСтр("ru='Рег. номер в стране регистрации'");
			Элементы.РегистрационныйНомер.ОграничениеТипа	= Новый ОписаниеТипов("Строка");
			Элементы.ИНН.Подсказка							= НСтр("ru = 'Идентификационный номер налогоплательщика, присваивается иностранной организации 
																	|при первой постановке на учет в налоговом органе Российской Федерации'");
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ИНН", "Доступность", НЕ ЗначениеЗаполнено(Объект.ГоловнойКонтрагент));
		
	ИначеЕсли Объект.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель") Тогда
		
		Элементы.СтранаРегистрации.Видимость				= Ложь;
		Элементы.ФИО.Видимость								= Истина;
		Элементы.ИНН.Видимость								= Истина;
		Элементы.КПП.Видимость								= Ложь;
		Элементы.КодПоОКПО.Видимость						= Истина;
		Элементы.РегистрационныйНомер.Видимость				= Истина;
		Элементы.ВидГосударственногоОргана.Видимость		= Ложь;
		Элементы.КодГосударственногоОргана.Видимость		= Ложь;
		Элементы.СвидетельствоСерияНомер.Видимость			= Истина;
		Элементы.СвидетельствоДатаВыдачи.Видимость			= Истина;
		Элементы.ДокументУдостоверяющийЛичность.Видимость	= Истина;
		Элементы.Пол.Видимость								= Истина;
		Элементы.ДатаРождения.Видимость						= Истина;
		Элементы.НаименованиеПолное.ПодсказкаВвода			= НСтр("ru = 'Юридическое название'");
		
		Элементы.РегистрационныйНомер.Заголовок				= НСтр("ru = 'ОГРН ИП'");
		Элементы.РегистрационныйНомер.Подсказка 			= НСтр("ru = 'Основной государственный регистрационный номер индивидуального предпринимателя, 
																	|указан в Свидетельстве о государственной регистрации физического лица в качестве ИП'");
		Элементы.РегистрационныйНомер.ПодсказкаВвода		= НСтр("ru = '15 цифр'");
		Элементы.РегистрационныйНомер.ОграничениеТипа		= Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(15));
		Элементы.ИНН.ПодсказкаВвода							= НСтр("ru = '12 цифр'");
		Элементы.ИНН.ОграничениеТипа						= Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(12));
		Элементы.КодПоОКПО.ПодсказкаВвода					= НСтр("ru = '10 цифр'");
		Элементы.КодПоОКПО.ОграничениеТипа					= Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(10));
		
	ИначеЕсли Объект.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ФизическоеЛицо") Тогда
		
		Элементы.СтранаРегистрации.Видимость				= Ложь;
		Элементы.ФИО.Видимость								= Истина;
		Элементы.ИНН.Видимость								= Истина;
		Элементы.КПП.Видимость								= Ложь;
		Элементы.КодПоОКПО.Видимость						= Ложь;
		Элементы.РегистрационныйНомер.Видимость				= Ложь;
		Элементы.ВидГосударственногоОргана.Видимость		= Ложь;
		Элементы.КодГосударственногоОргана.Видимость		= Ложь;
		Элементы.СвидетельствоСерияНомер.Видимость			= Ложь;
		Элементы.СвидетельствоДатаВыдачи.Видимость			= Ложь;
		Элементы.ДокументУдостоверяющийЛичность.Видимость	= Истина;
		Элементы.Пол.Видимость								= Истина;
		Элементы.ДатаРождения.Видимость						= Истина;
		
		Элементы.ИНН.ПодсказкаВвода					= НСтр("ru = '12 цифр'");
		Элементы.ИНН.ОграничениеТипа				= Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(12));
		Элементы.НаименованиеПолное.ПодсказкаВвода	= НСтр("ru = 'Фамилия Имя Отчество'");
		
	ИначеЕсли Объект.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ГосударственныйОрган") Тогда
		
		Элементы.СтранаРегистрации.Видимость				= Ложь;
		Элементы.ФИО.Видимость								= Ложь;
		Элементы.ИНН.Видимость								= Истина;
		Элементы.КПП.Видимость								= Истина;
		Элементы.КодПоОКПО.Видимость						= Ложь;
		Элементы.РегистрационныйНомер.Видимость				= Ложь;
		Элементы.ВидГосударственногоОргана.Видимость		= Истина;
		Элементы.КодГосударственногоОргана.Видимость		= Истина;
		Элементы.СвидетельствоСерияНомер.Видимость			= Ложь;
		Элементы.СвидетельствоДатаВыдачи.Видимость			= Ложь;
		Элементы.ДокументУдостоверяющийЛичность.Видимость	= Ложь;
		Элементы.Пол.Видимость								= Ложь;
		Элементы.ДатаРождения.Видимость						= Ложь;
		
		Элементы.ИНН.ПодсказкаВвода					= НСтр("ru = '10 цифр'");
		Элементы.ИНН.ОграничениеТипа				= Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(10));
		Элементы.НаименованиеПолное.ПодсказкаВвода	= НСтр("ru = 'Юридическое название'");
		
		Если ЗначениеЗаполнено(Объект.ВидГосударственногоОргана) Тогда
			
			Если Объект.ВидГосударственногоОргана = ПредопределенноеЗначение("Перечисление.ВидыГосударственныхОрганов.НалоговыйОрган") Тогда
				Элементы.КодГосударственногоОргана.ОграничениеТипа	= Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(4));
				Элементы.КодГосударственногоОргана.Заголовок		= НСтр("ru = 'Код инспекции'");
			ИначеЕсли Объект.ВидГосударственногоОргана = ПредопределенноеЗначение("Перечисление.ВидыГосударственныхОрганов.ОрганПФР") Тогда
				Элементы.КодГосударственногоОргана.ОграничениеТипа	= Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(3));
				Элементы.КодГосударственногоОргана.Заголовок		= НСтр("ru = 'Код отделения'");
			ИначеЕсли Объект.ВидГосударственногоОргана = ПредопределенноеЗначение("Перечисление.ВидыГосударственныхОрганов.ОрганФСС") Тогда
				Элементы.КодГосударственногоОргана.ОграничениеТипа	= Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(4));
				Элементы.КодГосударственногоОргана.Заголовок		= НСтр("ru = 'Код отделения'");
			Иначе
				Элементы.КодГосударственногоОргана.ОграничениеТипа	= Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10));
				Элементы.КодГосударственногоОргана.Заголовок		= НСтр("ru = 'Код'");
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ИсточникПривлеченияПокупателя.Видимость = Объект.Покупатель;
	Если ОбязательноЗаполнятьИсточникВПокупателях И Объект.Покупатель Тогда
		Элементы.ИсточникПривлеченияПокупателя.АвтоОтметкаНезаполненного = Истина;
	КонецЕсли;
	Элементы.ФормаПроверитьКонтрагентаНаДубли.Видимость = ПроверятьПредставлениеНаДубли ИЛИ ПроверятьАдресЭПНаДубли 
		ИЛИ ПроверятьНомерТелефонаНаДубли ИЛИ ПроверятьЮрНазваниеНаДубли;
	Элементы.ОстатокВзаиморасчетов.Видимость	= ЭтотОбъект.ПросмотрВзаиморасчетов;
	Элементы.СуммаПродаж.Видимость				= ЭтотОбъект.ПросмотрПродаж И Объект.Покупатель;
	Элементы.ПоследняяПродажа.Видимость			= ЭтотОбъект.ПросмотрПродаж И Объект.Покупатель;
	Элементы.ПокупательПоставщикПрочие.Заголовок = ЗаголовокГруппыВидКонтрагентаМК(ЭтотОбъект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГоловнойКонтрагент", "Видимость", ЭтоЮридическоеЛицо И НЕ ЭтотКонтрагентЯвляетсяГоловным);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"Подразделения", 
		"Видимость", 
		ЭтоЮридическоеЛицо И (ЭтотКонтрагентЯвляетсяГоловным ИЛИ ЗначениеЗаполнено(Объект.ГоловнойКонтрагент)));
	
	// Взаиморасчеты
	УстановитьВидимостьФлагаВ_УЕ();
	УстановитьЗаголовкиИВидимостьПриИзмененииФлагаРасчетовПоДоговорам();
	// Конец Взаиморасчеты
	
	// КабинетКлиента
	УстановитьВидимостьГруппыКабинетКлиента();
	// Конец КабинетКлиента
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗаполнениеОбязательныхРеквизитов(Отказ)
	
	ОбязательныеДляЗаполненияРеквизиты = РегистрыСведений.ОбязательностьЗаполненияРеквизитов.ОбязательныеДляЗаполненияРеквизитыОбъекта("Покупатель");
	
	Для Каждого Реквизит Из ОбязательныеДляЗаполненияРеквизиты Цикл
		
		Если Реквизит = "ИсточникПривлечения" И НЕ Объект.Покупатель Тогда
			Продолжить;
		КонецЕсли;

		Если Реквизит = "ИсточникПривлечения" Тогда
			Реквизит = "ИсточникПривлеченияПокупателя";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект[Реквизит]) Тогда
			Продолжить;
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(
			ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Заполнение", 
			"Источник привлечения"),,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Объект[%1]", Реквизит),,
			Отказ);
			
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокФормы(Форма)
	
	Объект = Форма.Объект;
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Форма.Автозаголовок = Истина;
		Возврат;
	КонецЕсли;
	
	Форма.Автозаголовок = Ложь;
	ВидыОтношений = Новый Массив;
	
	Если Объект.Покупатель Тогда
		ВидыОтношений.Добавить(НСтр("ru='Покупатель'"));
	КонецЕсли;
	
	Если Объект.Поставщик Тогда
		ВидыОтношений.Добавить(НСтр("ru='Поставщик'"));
	КонецЕсли;
	
	Если Объект.ПрочиеОтношения Тогда
		ВидыОтношений.Добавить(НСтр("ru='Прочие отношения'"));
	КонецЕсли;
	
	Заголовок = Объект.Наименование + " (" + НСтр("ru='Контрагент'");
	
	Если ВидыОтношений.Количество() > 0 Тогда
		Заголовок = Заголовок + ": ";
		Для Каждого Вид Из ВидыОтношений Цикл
			Заголовок = Заголовок + Вид + ", ";
		КонецЦикла;
		СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(Заголовок, 2);
	КонецЕсли;
	
	Заголовок = Заголовок + ")";
	
	Форма.Заголовок = Заголовок;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораНаименования(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	СписокВыбора = Элементы.Наименование.СписокВыбора;
	СписокВыбора.Очистить();
	
	Если Не ПустаяСтрока(Объект.НаименованиеПолное) Тогда
		
		Форма.ОрганизационноПравоваяФорма = УправлениеНебольшойФирмойКлиентСервер.ВыделитьИзНаименованияОПФ(Форма.КлассификаторОПФ, Объект.НаименованиеПолное);
		
		СписокВыбора.Добавить(Форма.ОрганизационноПравоваяФорма.НаименованиеБезОПФ);
		
		Если НЕ ПустаяСтрока(Форма.ОрганизационноПравоваяФорма.КраткаяФорма) Тогда
			СписокВыбора.Вставить(0, Форма.ОрганизационноПравоваяФорма.НаименованиеБезОПФ + " " + Форма.ОрганизационноПравоваяФорма.КраткаяФорма);
			СписокВыбора.Добавить(Форма.ОрганизационноПравоваяФорма.КраткаяФорма + " " + Форма.ОрганизационноПравоваяФорма.НаименованиеБезОПФ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Форма.ДанныеКонтактныхЛиц.Количество() > 0 И Не ПустаяСтрока(Форма.ДанныеКонтактныхЛиц[0].Наименование) Тогда
		СписокВыбора.Добавить(Строка(Форма.ДанныеКонтактныхЛиц[0].Наименование));
	КонецЕсли;
	
	Форма.Элементы.Наименование.КнопкаВыпадающегоСписка = СписокВыбора.Количество() > 0;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОпределитьВидКонтрагента(Форма)
	
	Если ТипЗнч(Форма.ОрганизационноПравоваяФорма) <> Тип("Структура") 
		Или Не Форма.ОрганизационноПравоваяФорма.Свойство("ПолнаяФорма") Тогда
		
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Если Форма.ОрганизационноПравоваяФорма.ПолнаяФорма = "Индивидуальный предприниматель" Тогда
		Объект.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель");
	ИначеЕсли Не ПустаяСтрока(Форма.ОрганизационноПравоваяФорма.ПолнаяФорма) Тогда
		Объект.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ЮридическоеЛицо");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыПоТекстуЗаполнения(Знач ТекстЗаполнения)
	
	Если (СтрДлина(ТекстЗаполнения) = 10 ИЛИ СтрДлина(ТекстЗаполнения) = 12)
		И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ТекстЗаполнения) Тогда
		
		Объект.Наименование = "";
		Объект.ИНН = ТекстЗаполнения;
		Объект.ВидКонтрагента = ?(СтрДлина(ТекстЗаполнения) = 10,
			Перечисления.ВидыКонтрагентов.ЮридическоеЛицо,
			Перечисления.ВидыКонтрагентов.ИндивидуальныйПредприниматель);
		ТекстЗаполнения = Неопределено;
		
		ОписаниеОшибкиЗаполнения = "";
		ЗаполнитьРеквизитыПоИНННаСервере(Объект.ИНН);
		
		УстановитьЗаголовокЮридическихДанных(ЭтотОбъект);
		
	Иначе
		
		Объект.НаименованиеПолное = ТекстЗаполнения;
		ТекущийЭлемент = Элементы.НаименованиеПолное;
		
		ФормироватьНаименованиеАвтоматически = Истина;
		ЗаполнитьСписокВыбораНаименования(ЭтотОбъект);
		Если Элементы.Наименование.СписокВыбора.Количество() > 0 Тогда
			Объект.Наименование = Элементы.Наименование.СписокВыбора[0];
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКонтактКакСвязаться()
	
	Если КонтактКакСвязаться.ВидКонтакта = "Контрагент" Тогда
		
		Если КонтактКакСвязаться.ТипКИ = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда
			ВидКИ = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.EmailКонтрагента");
		ИначеЕсли КонтактКакСвязаться.ТипКИ = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
			ВидКИ = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.ТелефонКонтрагента");
		КонецЕсли;
		Если ВидКИ = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Объект.Наименование = КонтактКакСвязаться.Контакт;
		Объект.НаименованиеПолное = КонтактКакСвязаться.Контакт;
		
		СтрокаКИ = НайтиИлиДобавитьНовуюСтрокуКИ(ЭтотОбъект.КонтактнаяИнформация, КонтактКакСвязаться.ТипКИ);
		СтрокаКИ.Представление = КонтактКакСвязаться.КакСвязаться;
		СтрокаКИ.Значение = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(КонтактКакСвязаться.КакСвязаться, ВидКИ);
		СтрокаКИ.Вид = ВидКИ;
		
	КонецЕсли;
	
	Если КонтактКакСвязаться.ВидКонтакта = "КонтактноеЛицо" Тогда
		
		Если КонтактКакСвязаться.ТипКИ = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда
			ВидКИ = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.EmailКонтактногоЛица");
		ИначеЕсли КонтактКакСвязаться.ТипКИ = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
			ВидКИ = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.ТелефонКонтактногоЛица");
		КонецЕсли;
		Если ВидКИ = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.Наименование) Тогда
			Объект.Наименование = КонтактКакСвязаться.Контакт;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.ФИО) Тогда
			Объект.ФИО = КонтактКакСвязаться.Контакт;
		КонецЕсли;
		
		Если ДанныеКонтактныхЛиц.Количество() = 0 ИЛИ ЗначениеЗаполнено(ДанныеКонтактныхЛиц[0].Наименование) Тогда
			ДанныеКЛ = ДанныеКонтактныхЛиц.Добавить();
			ЗаполнитьВсегдаВыводимыеВидыКИ(
				ДанныеКЛ.КонтактнаяИнформация,
				СвойстваВидовКонтактнойИнформацииКонтактныхЛиц);
		Иначе
			ДанныеКЛ = ДанныеКонтактныхЛиц[0];
		КонецЕсли;
		
		ДанныеКЛ.ГруппаРазвернута = Истина;
		
		Если ТипЗнч(КонтактКакСвязаться.Контакт) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
			ДанныеКЛ.Наименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КонтактКакСвязаться.Контакт, "Наименование");
			ДанныеКЛ.КонтактноеЛицо = КонтактКакСвязаться.Контакт;
			ДанныеКЛ.Изменен = Истина;
		Иначе
			ДанныеКЛ.Наименование = КонтактКакСвязаться.Контакт;
			ДанныеКЛ.Изменен = Истина;
		КонецЕсли;
		
		ОбновитьЭлементыКонтактныхЛиц();
		ТекущийЭлемент = Элементы["НаименованиеКонтакт_" + ДанныеКонтактныхЛиц.Индекс(ДанныеКЛ)];
		
		СтрокаКИ = НайтиИлиДобавитьНовуюСтрокуКИ(ДанныеКЛ.КонтактнаяИнформация, КонтактКакСвязаться.ТипКИ);
		СтрокаКИ.Представление = КонтактКакСвязаться.КакСвязаться;
		СтрокаКИ.Значение = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(КонтактКакСвязаться.КакСвязаться, ВидКИ);
		СтрокаКИ.Вид = ВидКИ;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СписокЗначенийИерархииКонтрагента(ТекущийГоловной, ТекущийКонтрагент)
	Возврат Справочники.Контрагенты.СписокПодразделенийГоловногоКонтрагента(ТекущийГоловной, ТекущийКонтрагент);	
КонецФункции

&НаКлиенте
Процедура ВыборКонтрагентаИзИерархии(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыКонтрагента = Новый Структура("Ключ", Результат.Значение);
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыКонтрагента);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоДаннымКонтактнойФормы(Параметры)
		
	Если Параметры.Свойство("Связь") Тогда
		ДанныеКЛ = ДанныеКонтактныхЛиц.Добавить();		
		ОбъектДляДобавления = ДанныеКЛ.КонтактнаяИнформация;
		ВидКИТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонКонтактногоЛица;
		ВидКИАдресЭП = Справочники.ВидыКонтактнойИнформации.EmailКонтактногоЛица;
		ВидКИСкайп   = Справочники.ВидыКонтактнойИнформации.SkypeКонтактногоЛица;
		ВидКИДругое  = Справочники.ВидыКонтактнойИнформации.МессенджерКонтактногоЛица;
	Иначе
		ОбъектДляДобавления = ЭтотОбъект.КонтактнаяИнформация;
		ВидКИТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
		ВидКИАдресЭП = Справочники.ВидыКонтактнойИнформации.EmailКонтрагента;
		ВидКИСкайп   = Справочники.ВидыКонтактнойИнформации.SkypeКонтрагента;
		ВидКИДругое  = Справочники.ВидыКонтактнойИнформации.ДругаяИнформацияКонтрагента;
		Объект.Наименование = Параметры.Контакт;
	КонецЕсли;
	
	ДанныеКФ = Параметры.ДанныеИзКонтактнойФормы;
	Если ДанныеКФ["НомераТелефонов"].Количество()>0 Тогда
		КИТелефон = ДанныеКФ["НомераТелефонов"][0];
		ТипКИ = Перечисления.ТипыКонтактнойИнформации.Телефон;
		СтрокаКИ = НайтиИлиДобавитьНовуюСтрокуКИ(ОбъектДляДобавления, ТипКИ);
		СтрокаКИ.Представление = КИТелефон;
		СтрокаКИ.Значение = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(КИТелефон, ВидКИТелефон);
		СтрокаКИ.Вид = ВидКИТелефон;
	КонецЕсли;
	
	Для Каждого КИ Из ДанныеКФ["АдресаЭП"] Цикл
		ТипКИ = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
		СтрокаКИ = НайтиИлиДобавитьНовуюСтрокуКИ(ОбъектДляДобавления, ТипКИ);
		СтрокаКИ.Представление = КИ;
		СтрокаКИ.Значение = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(КИ, ВидКИАдресЭП);
		СтрокаКИ.Вид = ВидКИАдресЭП;
	КонецЦикла;
	
	Для Каждого КИ Из ДанныеКФ["Скайп"] Цикл
		ТипКИ = Перечисления.ТипыКонтактнойИнформации.Skype;
		СтрокаКИ = НайтиИлиДобавитьНовуюСтрокуКИ(ОбъектДляДобавления, ТипКИ);
		СтрокаКИ.Представление = КИ;
		СтрокаКИ.Значение = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(КИ, ВидКИСкайп);
		СтрокаКИ.Вид = ВидКИСкайп;
	КонецЦикла;
	
	Для Каждого КИ Из ДанныеКФ["Другое"] Цикл
		ТипКИ = Перечисления.ТипыКонтактнойИнформации.Другое;
		СтрокаКИ = НайтиИлиДобавитьНовуюСтрокуКИ(ОбъектДляДобавления, ТипКИ);
		СтрокаКИ.Представление = КИ;
		СтрокаКИ.Значение = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(КИ, ВидКИДругое);
		СтрокаКИ.Вид = ВидКИДругое;
	КонецЦикла;
	
	Если Параметры.Свойство("Связь") Тогда
		ДанныеКЛ.Изменен = Истина;
		ОбновитьЭлементыКонтактныхЛиц();
	КонецЕсли;
	
	ПроверитьКонтрагентаНаДублиСервер();

КонецПроцедуры

&НаСервере
Функция НайтиИлиДобавитьНовуюСтрокуКИ(КонтактнаяИнформация, ТипКонтактнойИнформации)
	
	НайденныеСтроки = КонтактнаяИнформация.НайтиСтроки(
	Новый Структура("Тип,Значение", ТипКонтактнойИнформации, ""));
	
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		Если Не ЗначениеЗаполнено(НайденнаяСтрока.Представление) Тогда
			Возврат НайденнаяСтрока;
		КонецЕсли;
	КонецЦикла;
	
	// Добавляем новую строку КИ с группировкой по типу КИ
	КоличествоЭлементовКоллекции = КонтактнаяИнформация.Количество();
	ИндексВставки = КоличествоЭлементовКоллекции;
	
	Для ОбратныйИндекс = 1 По КоличествоЭлементовКоллекции Цикл
		ТекущийИндекс = КоличествоЭлементовКоллекции - ОбратныйИндекс;
		Если КонтактнаяИнформация[ТекущийИндекс].Тип = ТипКонтактнойИнформации Тогда
			ИндексВставки = ТекущийИндекс + 1;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Результат = КонтактнаяИнформация.Вставить(ИндексВставки);
	Результат.Тип = ТипКонтактнойИнформации;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьВсегдаВыводимыеВидыКИ(КИ, СвойстваВидовКонтактнойИнформацииКонтактныхЛиц)
	
	НайденныеСтроки = СвойстваВидовКонтактнойИнформацииКонтактныхЛиц.НайтиСтроки(Новый Структура("ВыводитьВФормеВсегда", Истина));
	
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		
		НоваяСтрокаКИ = КИ.Добавить();
		НоваяСтрокаКИ.Вид = НайденнаяСтрока.Вид;
		НоваяСтрокаКИ.Тип = НайденнаяСтрока.Тип;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СсылкаВариантаОтчета(ИмяОтчета, КлючВарианта)
	
	Отчет = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Отчет." + ИмяОтчета);
	Возврат ВариантыОтчетов.ВариантОтчета(Отчет, КлючВарианта);
	
КонецФункции

&НаСервере
Функция АдресЭП()
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Тип", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	
	Если ДанныеКонтактныхЛиц.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	НайденныеСтроки = ДанныеКонтактныхЛиц[0].КонтактнаяИнформация.НайтиСтроки(ПараметрыОтбора);
	
	Если Не ЗначениеЗаполнено(НайденныеСтроки) Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат НайденныеСтроки[0].Значение;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьИДоступностьМобильноеПриложениеИРМК()
	
	// МобильноеПриложение
	Если МобильноеПриложениеВызовСервера.НужноОграничитьФункциональность() Тогда
		
		Элементы.ПанельДополнительнойИнформации.Видимость = Ложь;
		Элементы.ФормаЗаполнитьРеквизитыПоДаннымЕГР.Видимость = Ложь;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "ФормаСправочникКонтрагентыДосьеКонтрагента", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "ПроверитьКонтрагента", "Видимость", Ложь);
		Элементы.ЮридическиеДанные.Видимость = Ложь;
		Элементы.Взаиморасчеты.Видимость = Ложь;
		Элементы.Ответственный.Видимость = Ложь;
		Элементы.ДобавитьДополнительныйРеквизит.Видимость = Ложь;
		Элементы.ИсточникПривлеченияПокупателя.Видимость = Ложь;
		Элементы.ВсеКонтакты.Видимость = Ложь;
		Элементы.ПокупательПоставщикПрочие.Видимость = Ложь;
		Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Объект.ВестиРасчетыПоДоговорам = Ложь;
			Объект.ВестиРасчетыПоДокументам = Ложь;
			Объект.Покупатель = Истина;
			Объект.Поставщик = Истина;
			Объект.ПрочиеОтношения = Истина;
		КонецЕсли;
	// Конец МобильноеПриложение
	
	ИначеЕсли УправлениеДоступомУНФ.ЕстьПрофильРабочееМестоКассира() Тогда
		
		Элементы.ПанельДополнительнойИнформации.Видимость = Ложь;
		Элементы.ФормаЗаполнитьРеквизитыПоДаннымЕГР.Видимость = Ложь;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "ФормаСправочникКонтрагентыДосьеКонтрагента", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "ПроверитьКонтрагента", "Видимость", Ложь);
		Элементы.Взаиморасчеты.Видимость = Ложь;
		Элементы.Ответственный.Видимость = Ложь;
		Элементы.ДобавитьДополнительныйРеквизит.Видимость = Ложь;
		Элементы.ИсточникПривлеченияПокупателя.Видимость = Ложь;
		Элементы.ВсеКонтакты.Видимость = Ложь;
		Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Объект.ВестиРасчетыПоДоговорам = Константы.ВестиРасчетыПоДоговорам.Получить();
			Объект.ВестиРасчетыПоДокументам = Константы.ВестиРасчетыПоДокументам.Получить();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьИДоступность()
	
	Элементы.СтатьяДДСПоУмолчанию.Видимость = ПравоДоступа("Чтение",
		Метаданные.Справочники.СтатьиДвиженияДенежныхСредств);
	
КонецПроцедуры

&НаСервере
Функция ВключенаОтчетность()
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьОтчетность");
КонецФункции

&НаСервере
Процедура ЗаполнитьДополнительныеРеквизитыВзаиморасчетов()
	
	ВестиУчетОплатыПоЗаказам = Объект.ВестиРасчетыПоЗаказам;
	ВестиУчетОплатыПоНакладным = Объект.ВестиРасчетыПоДокументам;
	
	СформироватьЗаголовокГруппыДеньги();
	
	
	НациональнаяВалюта = Константы.НациональнаяВалюта.Получить();
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ВалютаРасчетовПоУмолчанию = НациональнаяВалюта;
	КонецЕсли;
	
	Если Объект.СпособЗачетаПредоплатыПоУмолчанию.Пустая() Тогда
		Объект.СпособЗачетаПредоплатыПоУмолчанию = РасчетыРаботаСФормамиВызовСервера.ПолучитьСпособЗачетаПредоплатыПоУмолчанию();
	КонецЕсли;
	Если Объект.СпособРазнесенияОплатыПоУмолчанию.Пустая() Тогда
		Объект.СпособРазнесенияОплатыПоУмолчанию = РасчетыРаботаСФормамиВызовСервера.ПолучитьСпособРазнесенияОплатыПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьЗаголовокГруппыДеньги()
	
	РасчетыРаботаСФормамиВызовСервера.СформироватьЗаголовокГруппыДеньги(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура РазорватьСвязьНаСервере()
	
	ДанныеКонтактногоЛица = ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица];
	
	Если ЗначениеЗаполнено(ДанныеКонтактногоЛица.КонтактноеЛицо) Тогда
		НедействительнаяСвязьСКонтрагентом = НедействительныеСвязиКонтактныеЛица.Добавить();
		НедействительнаяСвязьСКонтрагентом.КонтактноеЛицо = ДанныеКонтактногоЛица.КонтактноеЛицо;
		НедействительнаяСвязьСКонтрагентом.Должность = ДанныеКонтактногоЛица.Должность;
	КонецЕсли;
	
	ДанныеКонтактногоЛица.Наименование = Неопределено;
	ДанныеКонтактногоЛица.КонтактноеЛицо = Неопределено;
	ДанныеКонтактногоЛица.Должность = Неопределено;
	ДанныеКонтактногоЛица.КонтактнаяИнформация.Очистить();
	
	ЗаполнитьВсегдаВыводимыеВидыКИ(
		ДанныеКонтактногоЛица.КонтактнаяИнформация,
		СвойстваВидовКонтактнойИнформацииКонтактныхЛиц);
		
	ОбновитьЭлементыКонтактныхЛиц();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПолеВводаТегаОкончаниеВводаТекстаСервер(Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ТегированиеОбъектов.ПолеВводаТегаОкончаниеВводаТекста(ЭтотОбъект, Текст, СтандартнаяОбработка);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПолеВводаТегаОбработкаВыбораСервер(ИмяЭлемента, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТегированиеОбъектов.ПолеВводаТегаОбработкаВыбора(ЭтотОбъект, ИмяЭлемента, ВыбранноеЗначение, СтандартнаяОбработка);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОблакоТеговОбработкаНавигационнойСсылкиСервер(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
	ТегированиеОбъектов.ОблакоТеговОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ГоловнойКонтрагентПриИзмененииНаСервере()
	
	УстановитьИННОбособленногоПодразделения(Объект);
	УстановитьЗаголовокЮридическихДанных(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьИННОбособленногоПодразделения(Объект)
	
	Если ЗначениеЗаполнено(Объект.ГоловнойКонтрагент) Тогда
		
		ИННГоловногоКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ГоловнойКонтрагент, "ИНН");
		Если Объект.ИНН <> ИННГоловногоКонтрагента Тогда
			
			Объект.ИНН = ИННГоловногоКонтрагента;
			
		КонецЕсли;
		
	Иначе
		
		Объект.ИНН = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПунктСозданиеКонтакта(ЭтотОбъект, ДанныеВыбора, Параметры, Текст,СтандартнаяОбработка)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Текст", Текст);
	
	Если Не ЭтотОбъект.ПоддержкаАвтоподбораКонтактов.ЕстьКонтактыСозданныеПоСобытию И Не ПустаяСтрока(Текст) Тогда
		
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ПолучитьДанныеВыбора(Тип("СправочникСсылка.КонтактныеЛица"), Параметры);
		
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СозданиеНовогоКонтакта", ЭтотОбъект, ДополнительныеПараметры);
	
	Если ДанныеВыбора = Неопределено Тогда
		ДанныеВыбора = Новый СписокЗначений;
	КонецЕсли;
	
	ПредставлениеПункта = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
		НСтр("ru = 'Добавить <span style=""color:ЗеленыйТекстСтрок;font:ВажнаяНадписьШрифт"">%1</span>'"), Текст);
	
	СтрокаПриЗагрузкеКонтактов = ДанныеВыбора.НайтиПоЗначению(Текст);
	Если СтрокаПриЗагрузкеКонтактов <> Неопределено Тогда
		ДанныеВыбора.Удалить(0);
		ДанныеВыбора.Вставить(0, ОписаниеОповещения, ПредставлениеПункта, , БиблиотекаКартинок.Плюс);
		Возврат;
	КонецЕсли;
	
	ДанныеВыбора.Добавить(ОписаниеОповещения, ПредставлениеПункта, , БиблиотекаКартинок.Плюс);
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеНовогоКонтакта(Результат, Параметры) Экспорт
	
	ЭлементКИ = Элементы.Найти("ПредставлениеКонтакт_"+ИндексВыбранногоКонтактногоЛица+"_КИ_0");
	
	ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].Наименование = Параметры.Текст;
	ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].Изменен = Истина;
	
	Модифицированность = Истина;
	Элементы.Найти("УстановитьСвязь_" + ИндексВыбранногоКонтактногоЛица).Доступность = Ложь;
	Элементы.Найти("РазорватьСвязь_" + ИндексВыбранногоКонтактногоЛица).Доступность = Истина;
	
	Если ЭлементКИ <> Неопределено Тогда	
		ТекущийЭлемент = ЭлементКИ;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция КонтрагентЯвляетсяГоловным(КонтрагентСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонтрагентСсылка", КонтрагентСсылка);
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА ИЗ Справочник.Контрагенты КАК СпрКонтрагенты ГДЕ СпрКонтрагенты.ГоловнойКонтрагент = &КонтрагентСсылка";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	ЯвляетсяГоловным = НЕ РезультатЗапроса.Пустой();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ЯвляетсяГоловным
	
КонецФункции

&НаСервере
Процедура ОбновитьДанныеОбъекта()
	НовыйОбъект = Объект.Ссылка.ПолучитьОбъект();
	ЗначениеВРеквизитФормы(НовыйОбъект, "Объект");
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКонтактНаОсновнойИПодписанта(ДанныеКонтактногоЛица,Оповещение)
	
	КонтактОсновноеКЛ = (ДанныеКонтактногоЛица.КонтактноеЛицо = Объект.КонтактноеЛицо);
	КонтактПодписант = (ДанныеКонтактногоЛица.КонтактноеЛицо = Объект.КонтактноеЛицоПодписант);
	
	Если КонтактОсновноеКЛ И КонтактПодписант Тогда
		ТекстВопроса = НСтр("ru = 'Контакт используется как основной и подписант для контрагента"+Символы.ПС+
			"При записи основной контакт и подписант контрагента станут незаполненными'");
	ИначеЕсли КонтактОсновноеКЛ Тогда
		ТекстВопроса = НСтр("ru = 'Контакт используется как основной для контрагента"+Символы.ПС+
			"При записи основной контакт контрагента станет незаполненным'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Контакт используется как подписант для контрагента"+Символы.ПС+
			"При записи подписант станет незаполненным'");
	КонецЕсли;
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВопросОсновноеКЛВыборКонтрагента(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		РазорватьСвязьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииНаименованияКонтактногоЛица()
	
	ЗаполнитьСписокВыбораНаименования(ЭтотОбъект);
	
	Если ЗначениеЗаполнено(Объект.НаименованиеПолное) Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьНаименованиеКонтрагента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииНаименованияКонтрагента()
	
	ЗаполнитьСписокВыбораНаименования(ЭтотОбъект);
	ОбновитьНаименованиеКонтрагента();
	Если НЕ ПроверятьПредставлениеНаДублиКонтакт И ПроверятьПредставлениеНаДубли Тогда
		ПроверитьНаДубли("Наименование");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНаименованиеКонтрагента()
	
	Если НЕ ФормироватьНаименованиеАвтоматически Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Наименование.СписокВыбора.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Наименование = Элементы.Наименование.СписокВыбора[0].Значение;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВопросСозданиеКЛ(Результат,ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Ок Тогда
		
		ПриИзмененииНаименованияКонтактногоЛица();
		
		Записать();
		
		Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		
		Если ЗначениеЗаполнено(ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].КонтактноеЛицо) Тогда
			ПараметрыФормы.Вставить("Ключ",ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].КонтактноеЛицо);
			ОткрытьФорму("Справочник.КонтактныеЛица.ФормаОбъекта",ПараметрыФормы);
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы.Вставить("Контрагент",Объект.Ссылка);
		ПараметрыФормы.Вставить("НаименованиеКонтакта",ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].Наименование);
		ОткрытьФорму("Справочник.КонтактныеЛица.ФормаОбъекта",ПараметрыФормы,ЭтотОбъект);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуМобильныйКлиент()
	
	Если НЕ ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Переместить(Элементы.ПанельДополнительнойИнформации, ЭтотОбъект);
	Элементы.ПанельДополнительнойИнформации.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаДолгиПродажи.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаСобытия.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ЗаголовокЮрДанные.Видимость = Ложь;
	Элементы.ДекорацияВзаиморасчеты.Видимость = Ложь;
	Элементы.ПокупательПоставщикПрочие.Поведение = ПоведениеОбычнойГруппы.Всплывающая;
	Элементы.ПокупательПоставщикПрочие.ОтображениеУправления = ОтображениеУправленияОбычнойГруппы.ГиперссылкаЗаголовка;
	Элементы.ПокупательПоставщикПрочие.Заголовок = ЗаголовокГруппыВидКонтрагентаМК(ЭтотОбъект);
	Элементы.ПокупательПоставщикПрочие.ОтображатьЗаголовок = Истина;
	Элементы.ПокупательПоставщикПрочие.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветТекстаФормы;
	
	Элементы.Переместить(Элементы.ГруппаГоловной, Элементы.ГруппаРеквизиты, Элементы.ИНН);
	
	Элементы.ОстатокВзаиморасчетов.РастягиватьПоГоризонтали = Истина;
	Элементы.СуммаПродаж.РастягиватьПоГоризонтали = Истина;
	Элементы.ПоследняяПродажа.РастягиватьПоГоризонтали = Истина;
	Элементы.ПоследнееСобытие.РастягиватьПоГоризонтали = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокГруппыВидКонтрагентаМК(Форма)
	ОбъектФормы = Форма.Объект;
	СтрокаЗаголовок = Новый Массив;
	Если ОбъектФормы.Покупатель Тогда
		СтрокаЗаголовок.Добавить(НСтр("ru = 'покупатель'"));
	КонецЕсли;
	
	Если ОбъектФормы.Поставщик Тогда
		СтрокаЗаголовок.Добавить(НСтр("ru = 'покупатель'"));
	КонецЕсли;
	
	Если ОбъектФормы.ПрочиеОтношения Тогда
		СтрокаЗаголовок.Добавить(НСтр("ru = 'прочие'"));
	КонецЕсли;
		
	Если НЕ ОбъектФормы.ПрочиеОтношения
		И НЕ ОбъектФормы.Поставщик
		И НЕ ОбъектФормы.Покупатель Тогда
		СтрокаЗаголовок.Добавить(НСтр("ru = '<Не указан>'"));
	КонецЕсли;
	
	Заголовок = СтрШаблон(НСтр("ru='Вид: %1'"), СтрСоединить(СтрокаЗаголовок, ", "));
	Возврат Заголовок;
	
КонецФункции

&НаКлиенте
Процедура СохранитьВосстановитьВведенноеЗначениеКПП()
	
	Если Элементы.КПП.Видимость Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.КПП) Тогда
			Объект.КПП = ВведенныйКППКэш;
		КонецЕсли;
		
		ВведенныйКППКэш = Неопределено;
		
	Иначе
		
		Если ЗначениеЗаполнено(Объект.КПП) Тогда
			ВведенныйКППКэш = Объект.КПП;
		КонецЕсли;
		
		Объект.КПП = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

#Область ПроверкаВнедренияБСП
// СтандартныеПодсистемы.КонтактнаяИнформация
// @skip-warning
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)
	Если 1=0 Тогда
		УправлениеКонтактнойИнформациейКлиент.ПриИзменении(ЭтотОбъект, Элемент);
	КонецЕсли;
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если 1=0 Тогда
		УправлениеКонтактнойИнформациейКлиент.НачалоВыбора(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриНажатии(Элемент, СтандартнаяОбработка)
	Если 1=0 Тогда
		УправлениеКонтактнойИнформациейКлиент.НачалоВыбора(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОчистка(Элемент, СтандартнаяОбработка)
	Если 1=0 Тогда
		УправлениеКонтактнойИнформациейКлиент.Очистка(ЭтотОбъект, Элемент.Имя);
	КонецЕсли;
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияВыполнитьКоманду(Команда)
	Если 1=0 Тогда
		УправлениеКонтактнойИнформациейКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда.Имя);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ОбновитьКонтактнуюИнформацию(Результат) Экспорт
	Если 1=0 Тогда
		УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Объект, Результат);
	КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.КонтактнаяИнформация

#КонецОбласти

#КонецОбласти

#Область КонтактныеЛица

&НаКлиенте
Процедура ИспользоватьКакОсновной(Команда)
	
	ИндексКЛ = Число(Сред(Команда.Имя, СтрДлина("ИспользоватьКакОсновной_")+1));
	ОсновнойКонтакт = ДанныеКонтактныхЛиц.НайтиСтроки(Новый Структура("Основной", Истина));
	Если ОсновнойКонтакт.Количество() > 0 Тогда
		ОсновнойКонтакт[0].Основной = Ложь;
	КонецЕсли;	
	
	ДанныеКонтактныхЛиц[ИндексКЛ].Основной = Истина;
	ДанныеКонтактныхЛиц[ИндексКЛ].Изменен = Истина;
	
	ЗаголовкиГрупп = ЗаголовкиГруппКонтакта(ДанныеКонтактныхЛиц[ИндексКЛ]);
	
	Элементы["Контакт_"+ИндексКЛ].Заголовок = ЗаголовкиГрупп.ЗаголовокГруппы;
	Элементы["Контакт_"+ИндексКЛ].ЗаголовокСвернутогоОтображения = ЗаголовкиГрупп.ЗаголовокСвернутойГруппы;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьКакПодписанта(Команда)
	
	ИндексКЛ = Число(Сред(Команда.Имя, СтрДлина("ИспользоватьКакПодписанта_")+1));
	
	Подписант = ДанныеКонтактныхЛиц.НайтиСтроки(Новый Структура("Подписант", Истина));
	Если Подписант.Количество() > 0 Тогда
		Подписант[0].Подписант = Ложь;
	КонецЕсли;
	
	ДанныеКонтактныхЛиц[ИндексКЛ].Подписант = Истина;
	ДанныеКонтактныхЛиц[ИндексКЛ].Изменен = Истина;
	
	ЗаголовкиГрупп = ЗаголовкиГруппКонтакта(ДанныеКонтактныхЛиц[ИндексКЛ]);
	
	Элементы["Контакт_"+ИндексКЛ].Заголовок = ЗаголовкиГрупп.ЗаголовокГруппы;
	Элементы["Контакт_"+ИндексКЛ].ЗаголовокСвернутогоОтображения = ЗаголовкиГрупп.ЗаголовокСвернутойГруппы;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВверх(Команда)
	
	ИндексВыбранногоКонтактногоЛица = Число(Сред(Команда.Имя, СтрДлина("ПереместитьВверх_")+1));
	Если ИндексВыбранногоКонтактногоЛица = 0 Тогда
		Возврат;
	КонецЕсли;

	ПорядокКЛ = ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].Порядок;
	ПорядокКЛ = ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].Порядок;
	ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].Порядок = 
		ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица - 1].Порядок;
	ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица - 1].Порядок = ПорядокКЛ;
	
	ДанныеКонтактныхЛиц.Сортировать("Порядок Возр");
	
	Для Каждого КонтактноеЛицо Из ДанныеКонтактныхЛиц Цикл
		КонтактноеЛицо.Изменен = Истина;
	КонецЦикла;
	
	ОбновитьЭлементыКонтактныхЛиц();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВниз(Команда)
	
	ИндексВыбранногоКонтактногоЛица = Число(Сред(Команда.Имя, СтрДлина("ПереместитьВниз_")+1));
	Если ИндексВыбранногоКонтактногоЛица = ДанныеКонтактныхЛиц.Количество() -1 Тогда
		Возврат;
	КонецЕсли;
	
	ПорядокКЛ = ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].Порядок;
	ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].Порядок = 
		ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица + 1].Порядок;
	ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица + 1].Порядок = ПорядокКЛ;
	
	Для Каждого КонтактноеЛицо Из ДанныеКонтактныхЛиц Цикл
		КонтактноеЛицо.Изменен = Истина;
	КонецЦикла;
	
	ДанныеКонтактныхЛиц.Сортировать("Порядок Возр");
	ОбновитьЭлементыКонтактныхЛиц();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВСписке(Команда)
	
	ИндексВыбранногоКонтактногоЛица = Число(Сред(Команда.Имя, СтрДлина("ПереместитьВниз_")+1));
	КонтактноеЛицо = ДанныеКонтактныхЛиц[ИндексВыбранногоКонтактногоЛица].КонтактноеЛицо;
	
	ПараметрыСписка = Новый Структура;
	ПараметрыСписка.Вставить("Отбор", Новый Структура);
	ПараметрыСписка.Отбор.Вставить("Контрагент", Объект.Ссылка);
	ПараметрыСписка.Отбор.Вставить("ТекущийКонтакт", КонтактноеЛицо);
	
	ОткрытьФорму("Справочник.КонтактныеЛица.Форма.ФормаСписка", ПараметрыСписка);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьСвойстваВидовКонтактнойИнформацииКонтактныхЛиц()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПорядокТиповКИ.Тип КАК Тип,
	|	ПорядокТиповКИ.Порядок КАК Порядок
	|ПОМЕСТИТЬ втПорядокТипов
	|ИЗ
	|	&ПорядокТиповКИ КАК ПорядокТиповКИ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыКонтактнойИнформации.Ссылка КАК Вид,
	|	ПРЕДСТАВЛЕНИЕ(ВидыКонтактнойИнформации.Ссылка) КАК ПредставлениеВида,
	|	ВидыКонтактнойИнформации.Тип КАК Тип,
	|	ЕСТЬNULL(НастройкиВидовКонтактнойИнформации.ВыводитьВФормеВсегда, ЛОЖЬ) КАК ВыводитьВФормеВсегда,
	|	ВидыКонтактнойИнформации.РазрешитьВводНесколькихЗначений КАК РазрешитьВводНесколькихЗначений,
	|	ВидыКонтактнойИнформации.ОбязательноеЗаполнение КАК ОбязательноеЗаполнение,
	|	ВидыКонтактнойИнформации.ПроверятьКорректность КАК ПроверятьКорректность,
	|	ВидыКонтактнойИнформации.ВидРедактирования КАК ВидРедактирования,
	|	ВидыКонтактнойИнформации.ВидПоляДругое КАК ВидПоляДругое
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокТипов КАК втПорядокТипов
	|		ПО ВидыКонтактнойИнформации.Тип = втПорядокТипов.Тип
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВидовКонтактнойИнформации КАК НастройкиВидовКонтактнойИнформации
	|		ПО ВидыКонтактнойИнформации.Ссылка = НастройкиВидовКонтактнойИнформации.Вид
	|ГДЕ
	|	ВидыКонтактнойИнформации.ПометкаУдаления = ЛОЖЬ
	|	И ВидыКонтактнойИнформации.Родитель = &ГруппаВидовКИ
	|
	|УПОРЯДОЧИТЬ ПО
	|	втПорядокТипов.Порядок,
	|	ВидыКонтактнойИнформации.РеквизитДопУпорядочивания");
	
	Запрос.УстановитьПараметр("ПорядокТиповКИ", КонтактнаяИнформацияУНФ.ПорядокТиповКИ());
	Запрос.УстановитьПараметр("ГруппаВидовКИ", Справочники.ВидыКонтактнойИнформации.СправочникКонтактныеЛица);
	
	ТаблицаСвойств = Запрос.Выполнить().Выгрузить();
	СвойстваВидовКонтактнойИнформацииКонтактныхЛиц.Загрузить(ТаблицаСвойств);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИОбновитьКонтактныеЛица()
	
	ЗаполнитьТаблицуКонтактнойИнформацииКонтактныхЛиц();
	ОбновитьЭлементыКонтактныхЛиц();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуКонтактнойИнформацииКонтактныхЛиц()
	
	Если ЗначениеЗаполнено(КлассификаторДляЗаполненияКИ) Тогда
		ЗаполнитьНаименованиеИКонтактнуюИнформацию(КлассификаторДляЗаполненияКИ);
		Возврат;
	КонецЕсли;
	
	ДанныеКонтактныхЛиц.Очистить();
	
	Если ЗначениеЗаполнено(ПривязанныйКонтакт) Тогда
		
		ДанныеКЛ = ДанныеКонтактныхЛиц.Добавить();
		ДанныеКЛ.Изменен = Истина;
		КонтактноеЛицо = Новый Структура;
		КонтактноеЛицо.Вставить("КонтактноеЛицо", ПривязанныйКонтакт);
		КонтактноеЛицо.Вставить("Наименование", ПривязанныйКонтакт.Наименование);
		КонтактноеЛицо.Вставить("Должность","");
		ЗаполнитьЗначенияСвойств(ДанныеКЛ, КонтактноеЛицо, "КонтактноеЛицо,Наименование,Должность");
		
		ЗаполнитьКонтактнуюИнформациюКонтактногоЛица(ПривязанныйКонтакт, ДанныеКЛ);
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СвязиКонтрагентКонтактСрезПервых.Контакт КАК Контакт,
	|	СвязиКонтрагентКонтактСрезПервых.Порядок КАК Порядок
	|ПОМЕСТИТЬ втПорядокКонтактовКонтрагента
	|ИЗ
	|	РегистрСведений.СвязиКонтрагентКонтакт.СрезПервых(, Контрагент = &Владелец) КАК СвязиКонтрагентКонтактСрезПервых
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтактныеЛица.Ссылка КАК КонтактноеЛицо,
	|	СвязиКонтрагентКонтакт.Контрагент КАК Владелец,
	|	СвязиКонтрагентКонтакт.Должность КАК Должность,
	|	КонтактныеЛица.Наименование КАК Наименование,
	|	втПорядокКонтактовКонтрагента.Порядок КАК РеквизитДопУпорядочивания
	|ПОМЕСТИТЬ втКонтакты
	|ИЗ
	|	РегистрСведений.СвязиКонтрагентКонтакт.СрезПоследних(, Контрагент = &Владелец) КАК СвязиКонтрагентКонтакт
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокКонтактовКонтрагента КАК втПорядокКонтактовКонтрагента
	|		ПО (втПорядокКонтактовКонтрагента.Контакт = СвязиКонтрагентКонтакт.Контакт)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица КАК КонтактныеЛица
	|		ПО СвязиКонтрагентКонтакт.Контакт = КонтактныеЛица.Ссылка
	|ГДЕ
	|	КонтактныеЛица.Недействителен = ЛОЖЬ
	|	И СвязиКонтрагентКонтакт.СвязьНедействительна = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втКонтакты.КонтактноеЛицо КАК КонтактноеЛицо,
	|	втКонтакты.Наименование КАК Наименование,
	|	втКонтакты.Должность КАК Должность,
	|	втКонтакты.РеквизитДопУпорядочивания КАК Порядок
	|ИЗ
	|	втКонтакты КАК втКонтакты
	|
	|УПОРЯДОЧИТЬ ПО
	|	втКонтакты.РеквизитДопУпорядочивания,
	|	Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокТипов.Тип КАК Тип,
	|	ПорядокТипов.Порядок КАК Порядок
	|ПОМЕСТИТЬ втПорядокТипов
	|ИЗ
	|	&ПорядокТипов КАК ПорядокТипов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втКонтакты.КонтактноеЛицо КАК КонтактноеЛицо,
	|	ВидыКонтактнойИнформации.Ссылка КАК Вид,
	|	ВидыКонтактнойИнформации.Тип КАК Тип,
	|	ВидыКонтактнойИнформации.РеквизитДопУпорядочивания КАК ПорядокВидов,
	|	втПорядокТипов.Порядок КАК ПорядокТипов
	|ПОМЕСТИТЬ втВсегдаВыводимыеВиды
	|ИЗ
	|	втКонтакты КАК втКонтакты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|			ЛЕВОЕ СОЕДИНЕНИЕ втПорядокТипов КАК втПорядокТипов
	|			ПО ВидыКонтактнойИнформации.Тип = втПорядокТипов.Тип
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ВидыКонтактнойИнформации.Ссылка В(&ВсегдаВыводимыеВиды)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КонтактноеЛицо,
	|	Вид
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВладелецКонтактнаяИнформация.Ссылка КАК КонтактноеЛицо,
	|	ВладелецКонтактнаяИнформация.Вид КАК Вид,
	|	ВладелецКонтактнаяИнформация.Тип КАК Тип,
	|	ВладелецКонтактнаяИнформация.Представление КАК Представление,
	|	ВладелецКонтактнаяИнформация.Значение КАК Значение,
	|	ВладелецКонтактнаяИнформация.ЗначенияПолей КАК ЗначенияПолей,
	|	ВладелецКонтактнаяИнформация.Вид.РеквизитДопУпорядочивания КАК ПорядокВидов,
	|	втПорядокТипов.Порядок КАК ПорядокТипов
	|ПОМЕСТИТЬ втДанныеКИ
	|ИЗ
	|	Справочник.КонтактныеЛица.КонтактнаяИнформация КАК ВладелецКонтактнаяИнформация
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокТипов КАК втПорядокТипов
	|		ПО ВладелецКонтактнаяИнформация.Тип = втПорядокТипов.Тип
	|ГДЕ
	|	ВладелецКонтактнаяИнформация.Ссылка В
	|			(ВЫБРАТЬ
	|				втКонтакты.КонтактноеЛицо
	|			ИЗ
	|				втКонтакты)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КонтактноеЛицо,
	|	Вид
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(втДанныеКИ.КонтактноеЛицо, втВсегдаВыводимыеВиды.КонтактноеЛицо) КАК КонтактноеЛицо,
	|	ЕСТЬNULL(втДанныеКИ.Вид, втВсегдаВыводимыеВиды.Вид) КАК Вид,
	|	ЕСТЬNULL(втДанныеКИ.Тип, втВсегдаВыводимыеВиды.Тип) КАК Тип,
	|	ЕСТЬNULL(втДанныеКИ.Представление, """") КАК Представление,
	|	ЕСТЬNULL(втДанныеКИ.Значение, """") КАК Значение,
	|	ЕСТЬNULL(втДанныеКИ.ЗначенияПолей, """") КАК ЗначенияПолей,
	|	ЕСТЬNULL(втДанныеКИ.ПорядокТипов, втВсегдаВыводимыеВиды.ПорядокТипов) КАК ПорядокТипов,
	|	ЕСТЬNULL(втДанныеКИ.ПорядокВидов, втВсегдаВыводимыеВиды.ПорядокВидов) КАК ПорядокВидов
	|ИЗ
	|	втВсегдаВыводимыеВиды КАК втВсегдаВыводимыеВиды
	|		ПОЛНОЕ СОЕДИНЕНИЕ втДанныеКИ КАК втДанныеКИ
	|		ПО втВсегдаВыводимыеВиды.Вид = втДанныеКИ.Вид
	|			И втВсегдаВыводимыеВиды.КонтактноеЛицо = втДанныеКИ.КонтактноеЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокТипов,
	|	ПорядокВидов";
	
	Запрос.УстановитьПараметр("Владелец",				Объект.Ссылка);
	Запрос.УстановитьПараметр("ОсновноеКонтактноеЛицо",	Объект.КонтактноеЛицо);
	Запрос.УстановитьПараметр("ПорядокТипов",			КонтактнаяИнформацияУНФ.ПорядокТиповКИ());
	Запрос.УстановитьПараметр("ВсегдаВыводимыеВиды",
		СвойстваВидовКонтактнойИнформацииКонтактныхЛиц.Выгрузить(Новый Структура("ВыводитьВФормеВсегда", Истина), "Вид"));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаКонтакты = МассивРезультатов[2].Выбрать();
	ВыборкаКонтактнаяИнформация = МассивРезультатов[6].Выбрать();
	Отбор = Новый Структура("КонтактноеЛицо");
	
	Пока ВыборкаКонтакты.Следующий() Цикл
		
		Отбор.КонтактноеЛицо = ВыборкаКонтакты.КонтактноеЛицо;
		НедействительнаяСвязь = НедействительныеСвязиКонтактныеЛица.НайтиСтроки(Отбор);
		
		Если НедействительнаяСвязь.Количество() <> 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеКЛ = ДанныеКонтактныхЛиц.Добавить();
		ЗаполнитьЗначенияСвойств(ДанныеКЛ, ВыборкаКонтакты, "КонтактноеЛицо,Наименование,Должность,Порядок");
		Если ЗначениеЗаполнено(ПривязанныйКонтакт) Тогда
			ДанныеКЛ.Порядок = ДанныеКЛ.Порядок + 1;
		КонецЕсли;
		
		ДанныеКЛ.Основной = Объект.КонтактноеЛицо = ВыборкаКонтакты.КонтактноеЛицо;
		ДанныеКЛ.Подписант = Объект.КонтактноеЛицоПодписант = ВыборкаКонтакты.КонтактноеЛицо;
		
		ВыборкаКонтактнаяИнформация.Сбросить();
		
		Пока ВыборкаКонтактнаяИнформация.НайтиСледующий(Отбор) Цикл
			
			ДанныеКИ = ДанныеКЛ.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(ДанныеКИ, ВыборкаКонтактнаяИнформация, "Тип,Вид,Представление,Значение");
			КонтактнаяИнформацияУНФ.КонвертироватьЗначениеПриНеобходимости(ВыборкаКонтактнаяИнформация.ЗначенияПолей, ВыборкаКонтактнаяИнформация.Представление, ВыборкаКонтактнаяИнформация.Тип, ДанныеКИ.Значение);
			ДанныеКИ.Комментарий = УправлениеКонтактнойИнформацией.КомментарийКонтактнойИнформации(ДанныеКИ.Значение);
			
		КонецЦикла;
		
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыКонтактныхЛиц()
	
	Элементы.КомандыДобавленияРастяжение_0.МаксимальнаяШирина = 40;
	УдалитьПрограммноСозданныеЭлементы();
	
	Для Каждого ДанныеКонтактногоЛица Из ДанныеКонтактныхЛиц Цикл
 
		ИндексКонтакта = ДанныеКонтактныхЛиц.Индекс(ДанныеКонтактногоЛица);
		// Для первого контактного лица элементы формы созданы в конфигураторе
		Если ИндексКонтакта > 0 Тогда
			
			Элементы.ПоказатьВсеКонтакты.Видимость = ИндексКонтакта > 5 И НЕ ПоказыватьВсеКонтакты;
			Если ИндексКонтакта > 5 И НЕ ПоказыватьВсеКонтакты Тогда
				Элементы.ДекорацияНетКонтактовОтступ.МаксимальнаяШирина = 33;
				Элементы.ПоказатьВсеКонтакты.Заголовок = СтрШаблон(НСтр("ru = 'показать еще (%1)'"), ДанныеКонтактныхЛиц.Количество()-6);
				Прервать;
			КонецЕсли;
			
			Элементы.ДекорацияНетКонтактовОтступ.МаксимальнаяШирина = 46;
			Элементы.ПоказатьВсеКонтакты.Видимость = Ложь;
			
			ГруппаКонтактногоЛица = Элементы.Добавить("Контакт_" + ИндексКонтакта, Тип("ГруппаФормы"), Элементы.ВсеКонтакты);
			ГруппаКонтактногоЛица.Вид = Элементы.Контакт_0.Вид;
			ГруппаКонтактногоЛица.Отображение = Элементы.Контакт_0.Отображение;
			ГруппаКонтактногоЛица.Группировка = Элементы.Контакт_0.Группировка;
			ГруппаКонтактногоЛица.ОтображатьЗаголовок = Элементы.Контакт_0.ОтображатьЗаголовок;
			ГруппаКонтактногоЛица.Поведение = Элементы.Контакт_0.Поведение;
			ГруппаКонтактногоЛица.ОтображениеУправления = Элементы.Контакт_0.ОтображениеУправления;
			ГруппаКонтактногоЛица.ЦветТекстаЗаголовка = Элементы.Контакт_0.ЦветТекстаЗаголовка;
			
			ЗаголовкиГрупп = ЗаголовкиГруппКонтакта(ДанныеКонтактныхЛиц[ИндексКонтакта]);
	
			ГруппаКонтактногоЛица.ЗаголовокСвернутогоОтображения = ЗаголовкиГрупп.ЗаголовокСвернутойГруппы;
			ГруппаКонтактногоЛица.Заголовок 					 = ЗаголовкиГрупп.ЗаголовокГруппы;
				
			Если НЕ ДанныеКонтактныхЛиц[ИндексКонтакта].ГруппаРазвернута 
				ИЛИ НЕ ЗначениеЗаполнено(ДанныеКонтактныхЛиц[ИндексКонтакта].КонтактноеЛицо) Тогда
				ГруппаКонтактногоЛица.Скрыть();
			КонецЕсли;
			
			ГруппаНаименованиеКонтактногоЛица = Элементы.Добавить("ГруппаНаименованиеКонтакт_" + ИндексКонтакта, Тип("ГруппаФормы"), ГруппаКонтактногоЛица);
			ГруппаНаименованиеКонтактногоЛица.Вид = Элементы.ГруппаНаименованиеКонтакт_0.Вид;
			ГруппаНаименованиеКонтактногоЛица.Отображение = Элементы.ГруппаНаименованиеКонтакт_0.Отображение;
			ГруппаНаименованиеКонтактногоЛица.Группировка = Элементы.ГруппаНаименованиеКонтакт_0.Группировка;
			
			ГруппаНаименованиеКонтактногоЛица.ОтображатьЗаголовок = Элементы.ГруппаНаименованиеКонтакт_0.ОтображатьЗаголовок;
			
			ПолеНаименование = Элементы.Добавить("НаименованиеКонтакт_" + ИндексКонтакта, Тип("ПолеФормы"), ГруппаНаименованиеКонтактногоЛица);
			ПолеНаименование.Вид = Элементы.НаименованиеКонтакт_0.Вид;
			ПолеНаименование.ПутьКДанным = "ДанныеКонтактныхЛиц[" + ИндексКонтакта + "].Наименование";
			ПолеНаименование.ПоложениеЗаголовка = Элементы.НаименованиеКонтакт_0.ПоложениеЗаголовка;
			ПолеНаименование.ПодсказкаВвода = Элементы.НаименованиеКонтакт_0.ПодсказкаВвода;
			
			ПолеНаименование.АвтоМаксимальнаяШирина = Элементы.НаименованиеКонтакт_0.АвтоМаксимальнаяШирина;
			ПолеНаименование.МаксимальнаяШирина = Элементы.НаименованиеКонтакт_0.МаксимальнаяШирина;
			ПолеНаименование.КнопкаВыбора = Элементы.НаименованиеКонтакт_0.КнопкаВыбора;
			ПолеНаименование.КнопкаВыпадающегоСписка = Элементы.НаименованиеКонтакт_0.КнопкаВыпадающегоСписка;
			ПолеНаименование.КнопкаСоздания = Элементы.НаименованиеКонтакт_0.КнопкаСоздания;
			ПолеНаименование.КнопкаОткрытия = Элементы.НаименованиеКонтакт_0.КнопкаОткрытия;
			
			ПолеНаименование.УстановитьДействие("ОбработкаВыбора", "НаименованиеКонтактногоЛица_ОбработкаВыбора");
			ПолеНаименование.УстановитьДействие("НачалоВыбора", "НаименованиеКонтактногоЛица_НачалоВыбора");
			ПолеНаименование.УстановитьДействие("ИзменениеТекстаРедактирования", "НаименованиеКонтактногоЛица_ИзменениеТекстаРедактирования");
			ПолеНаименование.УстановитьДействие("ОкончаниеВводаТекста", "НаименованиеКонтактногоЛица_ОкончаниеВводаТекста");
			ПолеНаименование.УстановитьДействие("ПриИзменении", "НаименованиеКонтактногоЛица_ПриИзменении");
			ПолеНаименование.УстановитьДействие("Открытие", "НаименованиеКонтактногоЛица_Открытие");
			
			КомандыСвязей = Элементы.Добавить("КоманднаяПанельСвязей_" + ИндексКонтакта, Тип("ГруппаФормы"), ГруппаНаименованиеКонтактногоЛица);
			КомандыСвязей.Вид = Элементы.КоманднаяПанельСвязей_0.Вид;
			КомандыСвязей.РастягиватьПоГоризонтали = Элементы.КоманднаяПанельСвязей_0.РастягиватьПоГоризонтали;
			
			ГруппаКомандСвязейСКонтактнымЛицом = Элементы.Добавить("ГруппаСвязей_" + ИндексКонтакта, Тип("ГруппаФормы"), КомандыСвязей);
			ГруппаКомандСвязейСКонтактнымЛицом.Вид = Элементы.ГруппаСвязей_0.Вид;
			ГруппаКомандСвязейСКонтактнымЛицом.Отображение = Элементы.ГруппаСвязей_0.Отображение;
			
			КнопкаУстановитьСвязь = Элементы.Добавить("УстановитьСвязь_" + ИндексКонтакта, Тип("КнопкаФормы"),ГруппаКомандСвязейСКонтактнымЛицом);
			КомандаУстановитьСвязь = ЭтаФорма.Команды.Добавить("КомандаУстановитьСвязь_" + ИндексКонтакта);
			КомандаУстановитьСвязь.Действие = "УстановитьСвязь";
			КомандаУстановитьСвязь.Подсказка = НСтр("ru = 'Установить связь с контактом'");
			КомандаУстановитьСвязь.ИзменяетСохраняемыеДанные = Истина;
			
			КнопкаУстановитьСвязь.ИмяКоманды = КомандаУстановитьСвязь.Имя;
			КнопкаУстановитьСвязь.Ширина = 3;
			КнопкаУстановитьСвязь.Картинка = БиблиотекаКартинок.УстановитьСвязь;
			КнопкаУстановитьСвязь.Отображение = ОтображениеКнопки.Картинка;
			
			КнопкаРазорватьСвязь = Элементы.Добавить("РазорватьСвязь_" + ИндексКонтакта, Тип("КнопкаФормы"),ГруппаКомандСвязейСКонтактнымЛицом);
			КомандаРазорватьСвязь = ЭтаФорма.Команды.Добавить("КомандаРазорватьСвязь_" + ИндексКонтакта);
			КомандаРазорватьСвязь.Действие = "РазорватьСвязь";
			КомандаРазорватьСвязь.Подсказка = НСтр("ru = 'Разорвать связь с контактом'");
			КомандаРазорватьСвязь.ИзменяетСохраняемыеДанные = Истина;
			
			КнопкаРазорватьСвязь.ИмяКоманды = КомандаРазорватьСвязь.Имя;
			КнопкаРазорватьСвязь.Ширина = 3;
			КнопкаРазорватьСвязь.Картинка = БиблиотекаКартинок.РазорватьСвязь;
			КнопкаРазорватьСвязь.Отображение = ОтображениеКнопки.Картинка;
			
			ГруппаНаименованиеКонтактногоЛица.ГоризонтальныйИнтервал = Элементы.ГруппаНаименованиеКонтакт_0.ГоризонтальныйИнтервал;
			СвязьУстановлена = ЗначениеЗаполнено(ДанныеКонтактногоЛица.КонтактноеЛицо) 
				ИЛИ ЗначениеЗаполнено(ДанныеКонтактногоЛица.Наименование);
			
			КнопкаУстановитьСвязь.Доступность = НЕ СвязьУстановлена;
			КнопкаРазорватьСвязь.Доступность = СвязьУстановлена;
						
			ГруппаДолжность = Элементы.Добавить("ГруппаДолжность_" + ИндексКонтакта, Тип("ГруппаФормы"), ГруппаКонтактногоЛица);
			ГруппаДолжность.Вид = Элементы.ГруппаДолжность_0.Вид;
			ГруппаДолжность.Отображение = Элементы.ГруппаДолжность_0.Отображение;
			ГруппаДолжность.Группировка = Элементы.ГруппаДолжность_0.Группировка;
			ГруппаДолжность.ГоризонтальныйИнтервал = Элементы.ГруппаДолжность_0.ГоризонтальныйИнтервал;
			
			ГруппаДолжность.ОтображатьЗаголовок = Элементы.ГруппаДолжность_0.ОтображатьЗаголовок;
			
			ПолеДолжность = Элементы.Добавить("ДолжностьКонтакт_" + ИндексКонтакта, Тип("ПолеФормы"), ГруппаДолжность);
			ПолеДолжность.Вид = Элементы.ДолжностьКонтакт_0.Вид;
			ПолеДолжность.ПутьКДанным = "ДанныеКонтактныхЛиц[" + ИндексКонтакта + "].Должность";
			ПолеДолжность.ПоложениеЗаголовка = Элементы.ДолжностьКонтакт_0.ПоложениеЗаголовка;
			ПолеДолжность.ПодсказкаВвода = Элементы.ДолжностьКонтакт_0.ПодсказкаВвода;
			
			ПолеДолжность.АвтоМаксимальнаяШирина = Элементы.ДолжностьКонтакт_0.АвтоМаксимальнаяШирина;
			ПолеДолжность.МаксимальнаяШирина = Элементы.ДолжностьКонтакт_0.МаксимальнаяШирина;
			ПолеДолжность.КнопкаВыбора = Элементы.ДолжностьКонтакт_0.КнопкаВыбора;
			ПолеДолжность.КнопкаВыпадающегоСписка = Элементы.ДолжностьКонтакт_0.КнопкаВыпадающегоСписка;
			ПолеДолжность.КнопкаСоздания = Элементы.ДолжностьКонтакт_0.КнопкаСоздания;
			ПолеДолжность.КнопкаОткрытия = Элементы.ДолжностьКонтакт_0.КнопкаОткрытия;
			ПолеДолжность.СписокВыбора.ЗагрузитьЗначения(ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("СпискиВыбора", "ДолжностиКонтактныхЛиц", Новый Массив));
			ПолеДолжность.КнопкаВыпадающегоСписка = ПолеДолжность.СписокВыбора.Количество() > 0;
			ПолеДолжность.УстановитьДействие("ПриИзменении", "ДолжностьКонтактПриИзменении");
			
			ДобавитьКоманднуюПанельКонтакта(ИндексКонтакта, ГруппаДолжность);
			
			ГруппаКИ = Элементы.Добавить("КонтактнаяИнформацияКонтакт_" + ИндексКонтакта, Тип("ГруппаФормы"), ГруппаКонтактногоЛица);
			ГруппаКИ.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ГруппаКИ.Отображение = ОтображениеОбычнойГруппы.Нет;
			ГруппаКИ.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
			ГруппаКИ.ОтображатьЗаголовок = Ложь;
			
			ГруппаДобавление = Элементы.Добавить("КомандыДобавленияКонтакт_" + ИндексКонтакта, Тип("ГруппаФормы"), ГруппаКонтактногоЛица);
			ГруппаДобавление.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ГруппаДобавление.Отображение = ОтображениеОбычнойГруппы.Нет;
			ГруппаДобавление.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
			ГруппаДобавление.ОтображатьЗаголовок = Ложь;
			
			ДекорацияРастяжение = Элементы.Добавить("КомандыДобавленияРастяжение_" + ИндексКонтакта, Тип("ДекорацияФормы"), ГруппаДобавление);
			ДекорацияРастяжение.Вид = ВидДекорацииФормы.Надпись;
			ДекорацияРастяжение.АвтоМаксимальнаяШирина = Ложь;
			ДекорацияРастяжение.МаксимальнаяШирина = 40;
			ДекорацияРастяжение.РастягиватьПоГоризонтали = Истина;
			
			Кнопка = Элементы.Добавить("ДобавитьПолеКонтактнойИнформацииКонтактногоЛица_" + ИндексКонтакта, Тип("КнопкаФормы"), ГруппаДобавление);
			Кнопка.ИмяКоманды = "ДобавитьПолеКонтактнойИнформацииКонтактногоЛица";
			Кнопка.ОтображениеФигуры = ОтображениеФигурыКнопки.Нет;
			Кнопка.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
		КонецЕсли;
		
		Если ИндексКонтакта = 0 Тогда
			ГруппаКИ = Элементы.КонтактнаяИнформацияКонтакт_0;
			СвязьУстановлена = ЗначениеЗаполнено(ДанныеКонтактногоЛица.КонтактноеЛицо)
			ИЛИ ЗначениеЗаполнено(ДанныеКонтактногоЛица.Наименование);
			
			Элементы.УстановитьСвязь_0.Доступность = НЕ СвязьУстановлена;
			Элементы.РазорватьСвязь_0.Доступность  = СвязьУстановлена;			
			Элементы.КоманднаяПанельКонтакт_0.Доступность = ЗначениеЗаполнено(ДанныеКонтактногоЛица.КонтактноеЛицо);
			Элементы.ДекорацияНетКонтактовОтступ.МаксимальнаяШирина = 46;
			
			ЗаголовкиГрупп = ЗаголовкиГруппКонтакта(ДанныеКонтактныхЛиц[0]);
			Элементы.Контакт_0.ЗаголовокСвернутогоОтображения = ЗаголовкиГрупп.ЗаголовокСвернутойГруппы;
			Элементы.Контакт_0.Заголовок 					  = ЗаголовкиГрупп.ЗаголовокГруппы;
			
		КонецЕсли;
		
		ДобавитьПоляКИКонтактногоЛица(ДанныеКонтактногоЛица, ИндексКонтакта, ГруппаКИ);
		
	КонецЦикла;
	
	ЗаполнитьСписокВыбораАдресовКонтактныхЛиц(ЭтотОбъект);
	
	АвтоподборКонтактов.ПодключитьОбработчикиСобытияАвтоподбор(ЭтотОбъект, ОписаниеПолейДляАвтоподбора());
	
	Элементы.Контакт_0.Видимость = ДанныеКонтактныхЛиц.Количество() > 0;
	Элементы.ГруппаНетКонтактныхЛиц.Видимость = ДанныеКонтактныхЛиц.Количество() = 0;
		
	ДобавитьЭлементыДублей();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПоляКИКонтактногоЛица(ДанныеКонтактногоЛица, ИндексКонтакта, ГруппаКИ)
	
	ШиринаВидаКИ = 8;
	ШиринаПоляКомментария = 11;
	
	Отбор = Новый Структура("Вид");
	
	Для Каждого ДанныеКИ Из ДанныеКонтактногоЛица.КонтактнаяИнформация Цикл
		
		ИндексКИ = ДанныеКонтактногоЛица.КонтактнаяИнформация.Индекс(ДанныеКИ);
		Отбор.Вид = ДанныеКИ.Вид;
		НайденныеСтроки = СвойстваВидовКонтактнойИнформацииКонтактныхЛиц.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		СвойстваВида = НайденныеСтроки[0];
		
		ГруппаЗначениеКИ = Элементы.Добавить("Контакт_" + ИндексКонтакта + "_КИ_" + ИндексКИ, Тип("ГруппаФормы"), ГруппаКИ);
		ГруппаЗначениеКИ.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаЗначениеКИ.Заголовок = ДанныеКИ.Вид;
		ГруппаЗначениеКИ.Отображение = ОтображениеОбычнойГруппы.Нет;
		ГруппаЗначениеКИ.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		ГруппаЗначениеКИ.ОтображатьЗаголовок = Ложь;
		ГруппаЗначениеКИ.Ширина = 33;
		
		ДекорацияДействие = Элементы.Добавить("ДействиеКонтакт_" + ИндексКонтакта + "_КИ_" + ИндексКИ, Тип("ДекорацияФормы"), ГруппаЗначениеКИ);
		ДекорацияДействие.Вид = ВидДекорацииФормы.Картинка;
		ДекорацияДействие.Картинка = КонтактнаяИнформацияУНФ.КартинкаДействияПоТипуКонтактнойИнформации(ДанныеКИ.Тип);
		ДекорацияДействие.РазмерКартинки = РазмерКартинки.АвтоРазмер;
		ДекорацияДействие.Гиперссылка = Истина;
		ДекорацияДействие.Ширина = 2;
		ДекорацияДействие.Высота = 1;
		ДекорацияДействие.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Авто;
		Если СвойстваВида.ВидПоляДругое = "МногострочноеШирокое" Тогда
			ДекорацияДействие.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Верх;
		КонецЕсли;
		ДекорацияДействие.УстановитьДействие("Нажатие", "Подключаемый_ДействиеКИКонтактаНажатие");
		
		ПолеВид = Элементы.Добавить("ВидКонтакт_" + ИндексКонтакта + "_КИ_" + ИндексКИ, Тип("ПолеФормы"), ГруппаЗначениеКИ);
		ПолеВид.Вид = ВидПоляФормы.ПолеНадписи;
		ПолеВид.ПутьКДанным = "ДанныеКонтактныхЛиц[" + ИндексКонтакта + "].КонтактнаяИнформация[" + ИндексКИ + "].Вид";
		ПолеВид.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ПолеВид.Ширина = ШиринаВидаКИ;
		ПолеВид.РастягиватьПоГоризонтали = Ложь;
		
		ДоступноРедактированиеВДиалоге = КонтактнаяИнформацияУНФ.ДляТипаКонтактнойИнформацииДоступноРедактированиеВДиалоге(ДанныеКИ.Тип);
		
		ПолеПредставление = Элементы.Добавить("ПредставлениеКонтакт_" + ИндексКонтакта + "_КИ_" + ИндексКИ, Тип("ПолеФормы"), ГруппаЗначениеКИ);
		ПолеПредставление.Вид = ВидПоляФормы.ПолеВвода;
		ПолеПредставление.ПутьКДанным = "ДанныеКонтактныхЛиц[" + ИндексКонтакта + "].КонтактнаяИнформация[" + ИндексКИ + "].Представление";
		ПолеПредставление.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ПолеПредставление.КнопкаВыбора = ДоступноРедактированиеВДиалоге;
		ПолеПредставление.АвтоОтметкаНезаполненного = СвойстваВида.ОбязательноеЗаполнение;
		ПолеПредставление.ШиринаВыпадающегоСписка = 38;
		ПолеПредставление.УстановитьДействие("ПриИзменении", "Подключаемый_ПредставлениеКИКонтактаПриИзменении");
		ПолеПредставление.УстановитьДействие("Очистка", "Подключаемый_ПредставлениеКИКонтактаОчистка");
		
		Если СвойстваВида.Тип = Перечисления.ТипыКонтактнойИнформации.Другое Тогда
			Если СвойстваВида.ВидПоляДругое = "МногострочноеШирокое" Тогда
				ПолеПредставление.МногострочныйРежим = Истина;
				ПолеПредставление.АвтоМаксимальнаяВысота = Ложь;
				ПолеПредставление.МаксимальнаяВысота = 2;
			КонецЕсли;
			Если СвойстваВида.ВидПоляДругое = "ОднострочноеУзкое" Тогда
				ПолеПредставление.АвтоМаксимальнаяШирина = Ложь;
				ПолеПредставление.МаксимальнаяШирина = 25;
			КонецЕсли;
		КонецЕсли;
		Если СвойстваВида.ВидРедактирования = "Диалог" И ДанныеКИ.Тип <> Перечисления.ТипыКонтактнойИнформации.ВебСтраница Тогда
			ПолеПредставление.РедактированиеТекста = Ложь;
			ПолеПредставление.ЦветФона = ЦветаСтиля.КонтактнаяИнформацияСРедактированиемВДиалогеЦвет;
		КонецЕсли;
		Если ДоступноРедактированиеВДиалоге Тогда
			ПолеПредставление.УстановитьДействие("НачалоВыбора", "Подключаемый_ПредставлениеКИКонтактаНачалоВыбора");
		КонецЕсли;
		
		Если КонтактнаяИнформацияУНФ.ДляТипаКонтактнойИнформацииДоступенВводКомментария(ДанныеКИ.Тип) Тогда
			
			ПолеПредставление.АвтоМаксимальнаяШирина = Ложь;
			ПолеПредставление.МаксимальнаяШирина = 26;
			
			ПолеКомментарий = Элементы.Добавить("КомментарийКонтакт_" + ИндексКонтакта + "_КИ_" + ИндексКИ, Тип("ПолеФормы"), ГруппаЗначениеКИ);
			ПолеКомментарий.Вид = ВидПоляФормы.ПолеВвода;
			ПолеКомментарий.ПутьКДанным = "ДанныеКонтактныхЛиц[" + ИндексКонтакта + "].КонтактнаяИнформация[" + ИндексКИ + "].Комментарий";
			ПолеКомментарий.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
			ПолеКомментарий.ПропускатьПриВводе = Истина;
			ПолеКомментарий.ПодсказкаВвода = НСтр("ru='Прим.'");
			ПолеКомментарий.АвтоМаксимальнаяШирина = Ложь;
			ПолеКомментарий.МаксимальнаяШирина = ШиринаПоляКомментария;
			ПолеКомментарий.УстановитьДействие("ПриИзменении", "Подключаемый_КомментарийКИКонтактаПриИзменении");
			
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УдалитьПрограммноСозданныеЭлементы()
	
	УдаляемыеЭлементы = Новый Массив;
	УдаляемыеКоманды = Новый Массив;
	// Группа первого контактного лица создана в конфигураторе
	Для ИндексГруппы = 1 По Элементы.ВсеКонтакты.ПодчиненныеЭлементы.Количество()-1 Цикл
		УдаляемыеЭлементы.Добавить(Элементы.ВсеКонтакты.ПодчиненныеЭлементы[ИндексГруппы]);
		УдаляемыеКоманды.Добавить(Команды.Найти("КомандаУстановитьСвязь_"+ИндексГруппы));
		УдаляемыеКоманды.Добавить(Команды.Найти("КомандаРазорватьСвязь_"+ИндексГруппы));
		УдаляемыеКоманды.Добавить(Команды.Найти("ИспользоватьКакПодписанта_"+ИндексГруппы));
		УдаляемыеКоманды.Добавить(Команды.Найти("ИспользоватьКакОсновной_"+ИндексГруппы));
		УдаляемыеКоманды.Добавить(Команды.Найти("ПереместитьВверх_"+ИндексГруппы));
		УдаляемыеКоманды.Добавить(Команды.Найти("ПереместитьВниз_"+ИндексГруппы));
		УдаляемыеКоманды.Добавить(Команды.Найти("ПоказатьВСписке_"+ИндексГруппы));
	КонецЦикла;
	Для Каждого ГруппаКИ Из Элементы.КонтактнаяИнформацияКонтакт_0.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(ГруппаКИ);
	КонецЦикла;
	Для Каждого УдаляемыйЭлемент Из УдаляемыеЭлементы Цикл
		Элементы.Удалить(УдаляемыйЭлемент);
	КонецЦикла;
	Для Каждого УдаляемаяКоманда Из УдаляемыеКоманды Цикл
		Команды.Удалить(УдаляемаяКоманда);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКоманднуюПанельКонтакта(ИндексКонтакта, ГруппаДолжность)
	
	КоманднаяПанельКонтакт = Элементы.Добавить("КоманднаяПанельКонтакт_" + ИндексКонтакта, Тип("ГруппаФормы"), ГруппаДолжность);
	КоманднаяПанельКонтакт.Вид = Элементы.КоманднаяПанельКонтакт_0.Вид;
	КоманднаяПанельКонтакт.РастягиватьПоГоризонтали = Элементы.КоманднаяПанельКонтакт_0.РастягиватьПоГоризонтали;
	КоманднаяПанельКонтакт.Доступность = ЗначениеЗаполнено(ДанныеКонтактныхЛиц[ИндексКонтакта].КонтактноеЛицо);
	
	КнопкаИспользоватьКакОсновной = Элементы.Добавить("ИспользоватьКакОсновной_" + ИндексКонтакта, Тип("КнопкаФормы"),КоманднаяПанельКонтакт);
	КомандаИспользоватьКакОсновной = ЭтаФорма.Команды.Добавить("ИспользоватьКакОсновной_" + ИндексКонтакта);
	КомандаИспользоватьКакОсновной.Действие = "ИспользоватьКакОсновной";
	КомандаИспользоватьКакОсновной.Подсказка = НСтр("ru = 'Использовать как основной'");
	КомандаИспользоватьКакОсновной.Заголовок = НСтр("ru = 'Использовать как основной'");
	КомандаИспользоватьКакОсновной.ИзменяетСохраняемыеДанные = Истина;
	
	КнопкаИспользоватьКакОсновной.ИмяКоманды = КомандаИспользоватьКакОсновной.Имя;
	КнопкаИспользоватьКакОсновной.Картинка = БиблиотекаКартинок.ПолностьюИсправлено16;
	КнопкаИспользоватьКакОсновной.ПоложениеВКоманднойПанели = Элементы.ИспользоватьКакОсновной_0.ПоложениеВКоманднойПанели;
	
	КнопкаИспользоватьКакПодписанта = Элементы.Добавить("ИспользоватьКакПодписанта_" + ИндексКонтакта, Тип("КнопкаФормы"),КоманднаяПанельКонтакт);
	КомандаИспользоватьКакПодписанта = ЭтаФорма.Команды.Добавить("ИспользоватьКакПодписанта_" + ИндексКонтакта);
	КомандаИспользоватьКакПодписанта.Действие = "ИспользоватьКакПодписанта";
	КомандаИспользоватьКакПодписанта.Подсказка = НСтр("ru = 'Использовать как подписанта'");
	КомандаИспользоватьКакПодписанта.Заголовок = НСтр("ru = 'Использовать как подписанта'");
	КомандаИспользоватьКакПодписанта.ИзменяетСохраняемыеДанные = Истина;
	
	КнопкаИспользоватьКакПодписанта.ИмяКоманды = КомандаИспользоватьКакПодписанта.Имя;
	КнопкаИспользоватьКакПодписанта.Картинка = БиблиотекаКартинок.РедактироватьМакет;
	КнопкаИспользоватьКакПодписанта.ПоложениеВКоманднойПанели = Элементы.ИспользоватьКакПодписанта_0.ПоложениеВКоманднойПанели;
	
	КнопкаПереместитьВверх = Элементы.Добавить("ПереместитьВверх_" + ИндексКонтакта, Тип("КнопкаФормы"),КоманднаяПанельКонтакт);
	КомандаПереместитьВверх = ЭтаФорма.Команды.Добавить("ПереместитьВверх_" + ИндексКонтакта);
	КомандаПереместитьВверх.Действие = "ПереместитьВверх";
	КомандаПереместитьВверх.Подсказка = НСтр("ru = 'Переместить вверх'");
	КомандаПереместитьВверх.Заголовок = НСтр("ru = 'Переместить вверх'");;
	КомандаПереместитьВверх.ИзменяетСохраняемыеДанные = Истина;
	
	КнопкаПереместитьВверх.ИмяКоманды = КомандаПереместитьВверх.Имя;
	КнопкаПереместитьВверх.Картинка = БиблиотекаКартинок.ПереместитьВверх;
	КнопкаПереместитьВверх.ПоложениеВКоманднойПанели = Элементы.ПереместитьВверх_0.ПоложениеВКоманднойПанели;
	
	КнопкаПереместитьВниз = Элементы.Добавить("ПереместитьВниз_" + ИндексКонтакта, Тип("КнопкаФормы"),КоманднаяПанельКонтакт);
	КомандаПереместитьВниз = ЭтаФорма.Команды.Добавить("ПереместитьВниз_" + ИндексКонтакта);
	КомандаПереместитьВниз.Действие = "ПереместитьВниз";
	КомандаПереместитьВниз.Подсказка = НСтр("ru = 'Переместить вниз'");
	КомандаПереместитьВниз.Заголовок = НСтр("ru = 'Переместить вниз'");
	КомандаПереместитьВниз.ИзменяетСохраняемыеДанные = Истина;
	
	КнопкаПереместитьВниз.ИмяКоманды = КомандаПереместитьВниз.Имя;
	КнопкаПереместитьВниз.Картинка = БиблиотекаКартинок.ПереместитьВниз;
	КнопкаПереместитьВниз.ПоложениеВКоманднойПанели = Элементы.ПереместитьВниз_0.ПоложениеВКоманднойПанели;
	
	КнопкаПоказатьВСписке = Элементы.Добавить("ПоказатьВСписке_" + ИндексКонтакта, Тип("КнопкаФормы"),КоманднаяПанельКонтакт);
	КомандаПоказатьВСписке = ЭтаФорма.Команды.Добавить("ПоказатьВСписке_" + ИндексКонтакта);
	КомандаПоказатьВСписке.Действие = "ПоказатьВСписке";
	КомандаПоказатьВСписке.Подсказка = НСтр("ru = 'Показать в списке'");
	КомандаПоказатьВСписке.Заголовок = НСтр("ru = 'Показать в списке'");
	
	КнопкаПоказатьВСписке.ИмяКоманды = КомандаПоказатьВСписке.Имя;
	КнопкаПоказатьВСписке.Картинка = БиблиотекаКартинок.ПоказатьВСписке;
	КнопкаПоказатьВСписке.ПоложениеВКоманднойПанели = Элементы.ПоказатьВСписке_0.ПоложениеВКоманднойПанели;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовкиГруппКонтакта(ДанныеКЛ)
	
	ЗаголовокСвернутойГруппы = Новый Массив;
	ЗаголовокГруппы			 = Новый Массив;

	Если ЗначениеЗаполнено(ДанныеКЛ.КонтактноеЛицо) Тогда
		
		Если ЗначениеЗаполнено(ДанныеКЛ.Наименование) Тогда
			ЗаголовокСвернутойГруппы.Добавить(ДанныеКЛ.Наименование);
			ЗаголовокГруппы.Добавить(ДанныеКЛ.Наименование);
		Иначе
			ЗаголовокСвернутойГруппы.Добавить(НСтр("ru = '<Не указано>'"));
			ЗаголовокГруппы.Добавить(НСтр("ru = '<Не указано>'"));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеКЛ.Должность) Тогда
			ЗаголовокСвернутойГруппы.Добавить(ДанныеКЛ.Должность);
		Иначе
			ЗаголовокСвернутойГруппы.Добавить(НСтр("ru = '<Не указана>'"));
		КонецЕсли;
		
		ОтборПоТелефону = Новый Структура("Тип", ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон"));
		ТипКИТелефон = ДанныеКЛ.КонтактнаяИнформация.НайтиСтроки(ОтборПоТелефону);
		Если ТипКИТелефон.Количество() > 0 Тогда
			
			МассивСтрокКИ = Новый Массив;
			
			Если ЗначениеЗаполнено(ТипКИТелефон[0].Представление) Тогда
				МассивСтрокКИ.Добавить(ТипКИТелефон[0].Представление);
			КонецЕсли;
			
			Если ТипКИТелефон.Количество() > 1 Тогда
				МассивСтрокКИ.Добавить(СтрШаблон(НСтр("ru = '(еще %1)'"), ТипКИТелефон.Количество() - 1));
			КонецЕсли;
			
			Если МассивСтрокКИ.Количество() > 0 Тогда
				ЗаголовокСвернутойГруппы.Добавить(СтрСоединить(МассивСтрокКИ, Символы.НПП));
			КонецЕсли;
		КонецЕсли;
		
		ОтборПоПочте = Новый Структура("Тип", ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты"));
		ТипКИПочта = ДанныеКЛ.КонтактнаяИнформация.НайтиСтроки(ОтборПоПочте);
		
		Если ТипКИПочта.Количество() > 0 Тогда
			
			МассивСтрокКИ = Новый Массив;
			Если ЗначениеЗаполнено(ТипКИПочта[0].Представление) Тогда
				МассивСтрокКИ.Добавить(ТипКИПочта[0].Представление);
			КонецЕсли;
			
			Если ТипКИПочта.Количество() > 1 Тогда
				МассивСтрокКИ.Добавить(СтрШаблон(НСтр("ru = '(еще %1)'"), ТипКИПочта.Количество() - 1));
			КонецЕсли;
			
			Если МассивСтрокКИ.Количество() > 0 Тогда
				ЗаголовокСвернутойГруппы.Добавить(СтрСоединить(МассивСтрокКИ, Символы.НПП));
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Если ЗначениеЗаполнено(ДанныеКЛ.Наименование) Тогда
			ЗаголовокСвернутойГруппы.Добавить(СтрШаблон(НСтр("ru = '%1 (создание)'"), ДанныеКЛ.Наименование));
			ЗаголовокГруппы.Добавить(СтрШаблон(НСтр("ru = '%1 (создание)'"), ДанныеКЛ.Наименование));
		Иначе
			ЗаголовокСвернутойГруппы.Добавить(НСтр("ru = 'Контакт (создание)'"));
			ЗаголовокГруппы.Добавить(НСтр("ru = 'Контакт (создание)'"));
		КонецЕсли;

	КонецЕсли;
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("ЗаголовокСвернутойГруппы", 	СтрСоединить(ЗаголовокСвернутойГруппы, ", "));
	Заголовки.Вставить("ЗаголовокГруппы",			СтрСоединить(ЗаголовокГруппы, ", "));
	ДобавитьДанныеОсновнойПодписант(Заголовки, ДанныеКЛ);
	
	Возврат Заголовки;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьДанныеОсновнойПодписант(Заголовки, ДанныеКЛ)
	
	СтрокаОсновнойПодписант = "";
	МассивОсновнойПодписант  = Новый Массив;
	
	Если НЕ ДанныеКЛ.Основной И НЕ ДанныеКЛ.Подписант Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеКЛ.Основной Тогда
		МассивОсновнойПодписант.Добавить(НСтр("ru = 'основной'"));
	КонецЕсли;
	
	Если ДанныеКЛ.Подписант Тогда
		МассивОсновнойПодписант.Добавить(НСтр("ru = 'подписант'"));
	КонецЕсли;
	
	СтрокаОсновнойПодписант = СтрСоединить(МассивОсновнойПодписант, ", ");
	
	Если НЕ ЗначениеЗаполнено(СтрокаОсновнойПодписант) Тогда
		Возврат;
	КонецЕсли;
	
	Заголовки.ЗаголовокСвернутойГруппы = СтрШаблон("%1 (%2)", Заголовки.ЗаголовокСвернутойГруппы, СтрокаОсновнойПодписант);
	Заголовки.ЗаголовокГруппы  = СтрШаблон("%1 (%2)", Заголовки.ЗаголовокГруппы, СтрокаОсновнойПодписант);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементТаблицыКонтактныхЛиц(Индекс,Контакт)
	
	ДанныеКонтактныхЛиц[Индекс].КонтактноеЛицо = Контакт;
	ДанныеКонтактныхЛиц[Индекс].Наименование = Контакт.Наименование;
	ДанныеКонтактныхЛиц[Индекс].Изменен = Истина;
	ДанныеКонтактныхЛиц[Индекс].ГруппаРазвернута = Истина;
	
	Отбор = Новый Структура;
	Отбор.Вставить("КонтактноеЛицо",Контакт);
	Строки = НедействительныеСвязиКонтактныеЛица.НайтиСтроки(Отбор);
	Если Строки.Количество()>0 Тогда
		Для Каждого Строка Из Строки Цикл
			НедействительныеСвязиКонтактныеЛица.Удалить(НедействительныеСвязиКонтактныеЛица.Индекс(Строка));
		КонецЦикла;
	КонецЕсли;
	
	ДанныеКонтактныхЛиц[Индекс].КонтактнаяИнформация.Очистить();
	ЗаполнитьКонтактнуюИнформациюКонтактногоЛица(Контакт, ДанныеКонтактныхЛиц[Индекс]);
	
	Модифицированность = Истина;
	ОбновитьЭлементыКонтактныхЛиц();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗаполнениеКонтактныхЛиц(Отказ)
	
	// Проверка на заполненность ФИО контактного лица, если введена контактная информация
	Для Каждого ДанныеКонтактногоЛица Из ДанныеКонтактныхЛиц Цикл
		
		Если Не ПустаяСтрока(ДанныеКонтактногоЛица.Наименование) Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяРеквизита = "ДанныеКонтактныхЛиц[" + ДанныеКонтактныхЛиц.Индекс(ДанныеКонтактногоЛица) + "].Наименование";
		Для Каждого СтрокаТаблицы Из ДанныеКонтактногоЛица.КонтактнаяИнформация Цикл
			Если Не ПустаяСтрока(СтрокаТаблицы.Значение) Или Не ПустаяСтрока(СтрокаТаблицы.Представление) Тогда
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'ФИО контакта не заполнено.'"),,,ИмяРеквизита, Отказ);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗаполнениеКонтактнойИнформацииКонтактныхЛиц(Отказ)
	
	ПараметрыСеанса.ИнтерактивнаяПроверкаЗаполненияКонтактнойИнформации = Истина;
	
	ЕстьОшибки = Ложь;
	Отбор = Новый Структура("Вид");
	
	Для Каждого ДанныеКонтактногоЛица Из ДанныеКонтактныхЛиц Цикл
		
		Если ПустаяСтрока(ДанныеКонтактногоЛица.Наименование) Тогда
			Продолжить;
		КонецЕсли;
		
		ИндексКонтактногоЛица = ДанныеКонтактныхЛиц.Индекс(ДанныеКонтактногоЛица);
		
		Для Каждого СтрокаТаблицы Из ДанныеКонтактногоЛица.КонтактнаяИнформация Цикл
			
			Отбор.Вид = СтрокаТаблицы.Вид;
			НайденныеСтроки = СвойстваВидовКонтактнойИнформацииКонтактныхЛиц.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			СвойстваВида = НайденныеСтроки[0];
			Индекс = ДанныеКонтактногоЛица.КонтактнаяИнформация.Индекс(СтрокаТаблицы);
			ИмяРеквизита = "ДанныеКонтактныхЛиц["+ИндексКонтактногоЛица+"].КонтактнаяИнформация["+Индекс+"].Представление";
			
			Если СвойстваВида.ОбязательноеЗаполнение И ПустаяСтрока(СтрокаТаблицы.Представление)
				И Не ЕстьДругиеЗаполненныеСтрокиВидаКИ(ДанныеКонтактногоЛица, СтрокаТаблицы, СтрокаТаблицы.Вид) Тогда
				// И нет других строк с данными для видов КИ с множественными значениями.
				
				ЕстьОшибки = Истина;
				ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(НСтр("ru = 'Вид контактной информации ""%1"" не заполнен.'"), СвойстваВида.ПредставлениеВида),,, ИмяРеквизита);
				
			ИначеЕсли НЕ ПустаяСтрока(СтрокаТаблицы.Представление) И СвойстваВида.ПроверятьКорректность Тогда
				
				РезультатПроверки = УправлениеКонтактнойИнформацией.ПроверитьКонтактнуюИнформацию(СтрокаТаблицы.Представление, СтрокаТаблицы.Значение, СтрокаТаблицы.Вид, СтрокаТаблицы.Тип, ИмяРеквизита);
				ЕстьОшибки = РезультатПроверки <> 0;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ЕстьОшибки Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЕстьДругиеЗаполненныеСтрокиВидаКИ(Знач ДанныеКонтактногоЛица, Знач ПроверяемаяСтрока, Знач ВидКонтактнойИнформации)
	
	ВсеСтрокиЭтогоВида = ДанныеКонтактногоЛица.КонтактнаяИнформация.НайтиСтроки(
		Новый Структура("Вид", ВидКонтактнойИнформации));
	
	Для Каждого СтрокаВида Из ВсеСтрокиЭтогоВида Цикл
		
		Если СтрокаВида <> ПроверяемаяСтрока И Не ПустаяСтрока(СтрокаВида.Представление) Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ЗаписатьДанныеКонтактныхЛиц(ТекущийОбъект)
	
	Для Каждого ДанныеКЛ Из ДанныеКонтактныхЛиц Цикл
		
		Если ПустаяСтрока(ДанныеКЛ.Наименование) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ДанныеКЛ.Изменен Тогда
			Продолжить;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		
		Если ЗначениеЗаполнено(ДанныеКЛ.КонтактноеЛицо) Тогда
			КонтактноеЛицоОбъект = ДанныеКЛ.КонтактноеЛицо.ПолучитьОбъект();
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеКЛ.КонтактноеЛицо) Или КонтактноеЛицоОбъект = Неопределено Тогда
			
			// Создание
			КонтактноеЛицоОбъект = Справочники.КонтактныеЛица.СоздатьЭлемент();
			КонтактноеЛицоОбъект.Заполнить(ТекущийОбъект.Ссылка);
			КонтактноеЛицоОбъект.ГруппаДоступа = Объект.ГруппаДоступа;
			
			// Установим ссылку нового по переданной ссылке основного контактного лица
			Если ТекущийОбъект.ДополнительныеСвойства.Свойство("НовоеОсновноеКонтактноеЛицо") Тогда
				КонтактноеЛицоОбъект.УстановитьСсылкуНового(ТекущийОбъект.ДополнительныеСвойства.НовоеОсновноеКонтактноеЛицо);
				ТекущийОбъект.ДополнительныеСвойства.Удалить("НовоеОсновноеКонтактноеЛицо");
			КонецЕсли;
			
			Если ТекущийОбъект.ДополнительныеСвойства.Свойство("НовоеКонтактноеЛицоПодписант") Тогда
				КонтактноеЛицоОбъект.УстановитьСсылкуНового(ТекущийОбъект.ДополнительныеСвойства.НовоеКонтактноеЛицоПодписант);
				ТекущийОбъект.ДополнительныеСвойства.Удалить("НовоеКонтактноеЛицоПодписант");
			КонецЕсли;
			
		КонецЕсли;
		
		// Внесение изменений
		ЗаполнитьЗначенияСвойств(КонтактноеЛицоОбъект, ДанныеКЛ, "Наименование");
		КонтактноеЛицоОбъект.КонтактнаяИнформация.Очистить();
		
		Если ЗначениеЗаполнено(Объект.ГруппаДоступа) И НЕ ЗначениеЗаполнено(КонтактноеЛицоОбъект.ГруппаДоступа) Тогда
			КонтактноеЛицоОбъект.ГруппаДоступа = Объект.ГруппаДоступа;
		КонецЕсли;
		
		Для Каждого ДанныеКИ Из ДанныеКЛ.КонтактнаяИнформация Цикл
			УправлениеКонтактнойИнформацией.ЗаписатьКонтактнуюИнформацию(КонтактноеЛицоОбъект, ДанныеКИ.Значение, ДанныеКИ.Вид, ДанныеКИ.Тип);
		КонецЦикла;
		
		// Запись объекта
		КонтактноеЛицоОбъект.ДополнительныеСвойства.Вставить("СвязьСКонтрагентом", 
			Новый Структура("Контрагент,Должность,Порядок", ТекущийОбъект.Ссылка, ДанныеКЛ.Должность, ДанныеКЛ.Порядок));
		КонтактноеЛицоОбъект.Записать();
				
		// Сохранение ссылки на созданный объект
		ДанныеКЛ.КонтактноеЛицо = КонтактноеЛицоОбъект.Ссылка;
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЦикла;
	
	КонтактныеЛица = Новый Массив;
	Для Каждого ДанныеКЛ Из НедействительныеСвязиКонтактныеЛица Цикл
		КонтактныеЛица.Добавить(ДанныеКЛ.КонтактноеЛицо);
	КонецЦикла;
	
	Если КонтактныеЛица.Количество() <> 0 Тогда
		РегистрыСведений.СвязиКонтрагентКонтакт.УстановитьНедействительной(ТекущийОбъект.Ссылка, КонтактныеЛица);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКонтактнуюИнформациюКонтактногоЛица(КонтактноеЛицо, ДанныеКЛ)
	
	ВсегдаВыводимыеВидыКИ = СвойстваВидовКонтактнойИнформацииКонтактныхЛиц.НайтиСтроки(Новый Структура("ВыводитьВФормеВсегда", Истина));
	
	Для Каждого ВидКи Из ВсегдаВыводимыеВидыКИ Цикл
		СтрокиКИ = КонтактноеЛицо.КонтактнаяИнформация.НайтиСтроки(Новый Структура("Вид",ВидКи.Вид));
		Если СтрокиКИ.Количество() = 0 Тогда
			ДанныеКИ = ДанныеКЛ.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(ДанныеКИ, ВидКи, "Вид,Тип");
			Продолжить;
		КонецЕсли;
		Для Каждого СтрокаКИ Из СтрокиКИ Цикл
			ДанныеКИ = ДанныеКЛ.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(ДанныеКИ, СтрокаКИ, "Тип,Вид,Представление,Значение");
			КонтактнаяИнформацияУНФ.КонвертироватьЗначениеПриНеобходимости(СтрокаКИ.ЗначенияПолей, СтрокаКИ.Представление, СтрокаКИ.Тип, ДанныеКИ.Значение);
			ДанныеКИ.Комментарий = УправлениеКонтактнойИнформацией.КомментарийКонтактнойИнформации(ДанныеКИ.Значение);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораАдресовКонтактныхЛиц(Форма) Экспорт
	
	МассивАдресов = Новый Массив;
	Отбор = Новый Структура("Вид");
	
	Для Каждого ДанныеКонтактногоЛица Из Форма.ДанныеКонтактныхЛиц Цикл
		
		Если Форма.ДанныеКонтактныхЛиц.Индекс(ДанныеКонтактногоЛица) > 5 И НЕ Форма.ПоказыватьВсеКонтакты Тогда
			Прервать;
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из ДанныеКонтактногоЛица.КонтактнаяИнформация Цикл
			
			Если СтрокаТаблицы.Тип <> ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес") Тогда
				Продолжить;
			КонецЕсли;
			
			Отбор.Вид = СтрокаТаблицы.Вид;
			НайденныеСтроки = Форма.СвойстваВидовКонтактнойИнформацииКонтактныхЛиц.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если Не ПустаяСтрока(СтрокаТаблицы.Представление)
				И МассивАдресов.Найти(СтрокаТаблицы.Представление) = Неопределено Тогда
				
				МассивАдресов.Добавить(СтрокаТаблицы.Представление);
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого ДанныеКонтактногоЛица Из Форма.ДанныеКонтактныхЛиц Цикл
		Для Каждого СтрокаТаблицы Из ДанныеКонтактногоЛица.КонтактнаяИнформация Цикл
			
			Если СтрокаТаблицы.Тип <> ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес") Тогда
				Продолжить;
			КонецЕсли;
			
			Отбор.Вид = СтрокаТаблицы.Вид;
			НайденныеСтроки = Форма.СвойстваВидовКонтактнойИнформацииКонтактныхЛиц.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ИмяПоля = "ПредставлениеКонтакт_" + Форма.ДанныеКонтактныхЛиц.Индекс(ДанныеКонтактногоЛица) + "_КИ_" + ДанныеКонтактногоЛица.КонтактнаяИнформация.Индекс(СтрокаТаблицы);
			ПолеПредставление = Форма.Элементы.Найти(ИмяПоля);
			Если ПолеПредставление = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ПолеПредставление.СписокВыбора.ЗагрузитьЗначения(МассивАдресов);
			ПолеПредставление.КнопкаВыпадающегоСписка = ПолеПредставление.СписокВыбора.Количество() > 0;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКонтактнуюИнформациюКонтактаВидВыбран(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("Вид", ВыбранныйЭлемент.Значение);
	НайденныеСтроки = СвойстваВидовКонтактнойИнформацииКонтактныхЛиц.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	СвойстваВида = НайденныеСтроки[0];
	
	Если СвойстваВида.ВыводитьВФормеВсегда = Ложь Тогда
		
		ДополнительныеПараметры.Вставить("ДобавляемыйВид", ВыбранныйЭлемент.Значение);
		ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьКонтактнуюИнформациюКонтактаВопросЗадан", ЭтотОбъект, ДополнительныеПараметры);
		
		ТекстВопроса = СтрШаблон(НСтр("ru='Возможность ввода ""%1"" будет добавлена для всех %2.
			|Продолжить?'"), ВыбранныйЭлемент.Значение, ДополнительныеПараметры.МножественнаяФормаВладельца);
		ЗаголовокВопроса = НСтр("ru='Подтверждение добавления'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена, , КодВозвратаДиалога.ОК, ЗаголовокВопроса);
		
	Иначе
		
		ДобавитьКонтактнуюИнформациюКонтактаСервер(ВыбранныйЭлемент.Значение, ДополнительныеПараметры.ИндексКонтакта);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКонтактнуюИнформациюКонтактаВопросЗадан(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьКонтактнуюИнформациюКонтактаСервер(ДополнительныеПараметры.ДобавляемыйВид, ДополнительныеПараметры.ИндексКонтакта, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКонтактнуюИнформациюКонтактаСервер(ДобавляемыйВид, ИндексКонтакта, УстановитьВыводВФормеВсегда = Ложь)
	
	ДобавляемыйТип = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДобавляемыйВид, "Тип");
	ДанныеКонтакта = ДанныеКонтактныхЛиц[ИндексКонтакта];
	
	КоличествоЭлементовКоллекции = ДанныеКонтакта.КонтактнаяИнформация.Количество();
	ИндексВставки = КоличествоЭлементовКоллекции;
	
	Для ОбратныйИндекс = 1 По КоличествоЭлементовКоллекции Цикл
		ТекущийИндекс = КоличествоЭлементовКоллекции - ОбратныйИндекс;
		Если ДанныеКонтакта.КонтактнаяИнформация[ТекущийИндекс].Вид = ДобавляемыйВид Тогда
			ИндексВставки = ТекущийИндекс+1;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ДанныеКИ = ДанныеКонтакта.КонтактнаяИнформация.Вставить(ИндексВставки);
	ДанныеКИ.Вид = ДобавляемыйВид;
	ДанныеКИ.Тип = ДобавляемыйТип;
	
	Если УстановитьВыводВФормеВсегда Тогда
		
		КонтактнаяИнформацияУНФ.УстановитьФлагВыводитьВФормеВсегда(ДобавляемыйВид);
		
		НайденныеСтроки = СвойстваВидовКонтактнойИнформацииКонтактныхЛиц.НайтиСтроки(Новый Структура("Вид", ДобавляемыйВид));
		Если НайденныеСтроки.Количество() > 0 Тогда
			НайденныеСтроки[0].ВыводитьВФормеВсегда = Истина;
		КонецЕсли;
		
		Для ТекущийИндекс = 0 По ДанныеКонтактныхЛиц.Количество()-1 Цикл
			
			Если ТекущийИндекс = ИндексКонтакта Тогда
				Продолжить;
			КонецЕсли;
			
			ДанныеКИ = ДанныеКонтактныхЛиц[ТекущийИндекс].КонтактнаяИнформация.Добавить();
			ДанныеКИ.Вид = ДобавляемыйВид;
			ДанныеКИ.Тип = ДобавляемыйТип;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбновитьЭлементыКонтактныхЛиц();
	ТекущийЭлемент = Элементы["ПредставлениеКонтакт_" + ИндексКонтакта + "_КИ_" + ИндексВставки];
	ДобавитьЭлементыДублей();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДействиеКИКонтактаНажатие(Элемент)
	
	ПозицияПодчеркиванияОдин = СтрНайти(Элемент.Имя, "_",,,1);
	ПозицияПодчеркиванияДва  = СтрНайти(Элемент.Имя, "_",,,2);
	ПозицияПодчеркиванияТри  = СтрНайти(Элемент.Имя, "_",,,3);
	
	ИндексКЛ = Число(Сред(Элемент.Имя, ПозицияПодчеркиванияОдин+1, ПозицияПодчеркиванияДва-ПозицияПодчеркиванияОдин-1));
	ИндексКИ = Число(Сред(Элемент.Имя, ПозицияПодчеркиванияТри+1));
	
	СтрокаКИ = ДанныеКонтактныхЛиц[ИндексКЛ].КонтактнаяИнформация[ИндексКИ];
	
	ДанныеКИ = Новый Структура;
	ДанныеКИ.Вставить("Тип", СтрокаКИ.Тип);
	ДанныеКИ.Вставить("Представление", СтрокаКИ.Представление);
	ДанныеКИ.Вставить("Владелец", ДанныеКонтактныхЛиц[ИндексКЛ].КонтактноеЛицо);
	
	КонтактнаяИнформацияУНФКлиент.ОбработатьНажатиеПиктограммы(ЭтотОбъект, Элемент, ДанныеКИ);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПредставлениеКИКонтактаПриИзменении(Элемент)
	
	ПозицияПодчеркиванияОдин = СтрНайти(Элемент.Имя, "_",,,1);
	ПозицияПодчеркиванияДва  = СтрНайти(Элемент.Имя, "_",,,2);
	ПозицияПодчеркиванияТри  = СтрНайти(Элемент.Имя, "_",,,3);
	
	ИндексКЛ = Число(Сред(Элемент.Имя, ПозицияПодчеркиванияОдин+1, ПозицияПодчеркиванияДва-ПозицияПодчеркиванияОдин-1));
	ИндексКИ = Число(Сред(Элемент.Имя, ПозицияПодчеркиванияТри+1));
	
	ДанныеКонтактныхЛиц[ИндексКЛ].Изменен = Истина;
	ДанныеКИ = ДанныеКонтактныхЛиц[ИндексКЛ].КонтактнаяИнформация[ИндексКИ];
	
	Если ПустаяСтрока(ДанныеКИ.Представление) Тогда
		ДанныеКИ.Значение = "";
	Иначе
		ДанныеКИ.Значение = КонтактнаяИнформацияУНФВызовСервера.КонтактнаяИнформацияПоПредставлению(ДанныеКИ.Представление, ДанныеКИ.Вид);
	КонецЕсли;
	
	Если ДанныеКИ.Тип = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес") Тогда
		ЗаполнитьСписокВыбораАдресовКонтактныхЛиц(ЭтотОбъект);
	КонецЕсли;
	
	ПроверитьКИНаДубли(ДанныеКИ.Представление, ИндексКИ, ДанныеКИ.Тип, ИндексКЛ);
	
	ЗаголовкиГрупп = ЗаголовкиГруппКонтакта(ДанныеКонтактныхЛиц[ИндексКЛ]);
	
	Элементы["Контакт_"+ИндексКЛ].ЗаголовокСвернутогоОтображения = ЗаголовкиГрупп.ЗаголовокСвернутойГруппы;
	Элементы["Контакт_"+ИндексКЛ].Заголовок 					 = ЗаголовкиГрупп.ЗаголовокГруппы;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПредставлениеКИКонтактаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПозицияПодчеркиванияОдин = СтрНайти(Элемент.Имя, "_",,,1);
	ПозицияПодчеркиванияДва  = СтрНайти(Элемент.Имя, "_",,,2);
	ПозицияПодчеркиванияТри  = СтрНайти(Элемент.Имя, "_",,,3);
	
	ИндексКЛ = Число(Сред(Элемент.Имя, ПозицияПодчеркиванияОдин+1, ПозицияПодчеркиванияДва-ПозицияПодчеркиванияОдин-1));
	ИндексКИ = Число(Сред(Элемент.Имя, ПозицияПодчеркиванияТри+1));
	
	ДанныеКонтактныхЛиц[ИндексКЛ].Изменен = Истина;
	ДанныеКИ = ДанныеКонтактныхЛиц[ИндексКЛ].КонтактнаяИнформация[ИндексКИ];
	
	// Если представление было изменено в поле и не соответствует реквизиту, то приводим в соответствие.
	Если ДанныеКИ.Представление <> Элемент.ТекстРедактирования Тогда
		ДанныеКИ.Представление = Элемент.ТекстРедактирования;
		Подключаемый_ПредставлениеКИКонтактаПриИзменении(Элемент);
		Модифицированность = Истина;
	КонецЕсли;
	
	ПараметрыФормы = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
						ДанныеКИ.Вид,
						ДанныеКИ.Значение,
						ДанныеКИ.Представление,
						ДанныеКИ.Комментарий);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИндексКЛ", ИндексКЛ);
	ДополнительныеПараметры.Вставить("ИндексКИ", ИндексКИ);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗначениеКИКонтактаРедактированиеВДиалогеЗавершено", ЭтотОбъект, ДополнительныеПараметры);
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыФормы,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПредставлениеКИКонтактаОчистка(Элемент, СтандартнаяОбработка)
	
	ПозицияПодчеркиванияОдин = СтрНайти(Элемент.Имя, "_",,,1);
	ПозицияПодчеркиванияДва  = СтрНайти(Элемент.Имя, "_",,,2);
	ПозицияПодчеркиванияТри  = СтрНайти(Элемент.Имя, "_",,,3);
	
	ИндексКЛ = Число(Сред(Элемент.Имя, ПозицияПодчеркиванияОдин+1, ПозицияПодчеркиванияДва-ПозицияПодчеркиванияОдин-1));
	ИндексКИ = Число(Сред(Элемент.Имя, ПозицияПодчеркиванияТри+1));
	
	ДанныеКонтактныхЛиц[ИндексКЛ].Изменен = Истина;
	ДанныеКИ = ДанныеКонтактныхЛиц[ИндексКЛ].КонтактнаяИнформация[ИндексКИ];
	ДанныеКИ.Значение = "";
	
	Если ДанныеКИ.Тип = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес") Тогда
		ЗаполнитьСписокВыбораАдресовКонтактныхЛиц(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийКИКонтактаПриИзменении(Элемент)
	
	ПозицияПодчеркиванияОдин = СтрНайти(Элемент.Имя, "_",,,1);
	ПозицияПодчеркиванияДва  = СтрНайти(Элемент.Имя, "_",,,2);
	ПозицияПодчеркиванияТри  = СтрНайти(Элемент.Имя, "_",,,3);
	
	ИндексКЛ = Число(Сред(Элемент.Имя, ПозицияПодчеркиванияОдин+1, ПозицияПодчеркиванияДва-ПозицияПодчеркиванияОдин-1));
	ИндексКИ = Число(Сред(Элемент.Имя, ПозицияПодчеркиванияТри+1));
	ДанныеКонтактныхЛиц[ИндексКЛ].Изменен = Истина;
	ДанныеКИ = ДанныеКонтактныхЛиц[ИндексКЛ].КонтактнаяИнформация[ИндексКИ];
	
	ОжидаемыйВид = ?(ПустаяСтрока(ДанныеКИ.Значение), ДанныеКИ.Вид, Неопределено);
	КонтактнаяИнформацияУНФВызовСервера.УстановитьКомментарийКонтактнойИнформации(ДанныеКИ.Значение, ДанныеКИ.Комментарий, ОжидаемыйВид);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеКИКонтактаРедактированиеВДиалогеЗавершено(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеКИ = ДанныеКонтактныхЛиц[ДополнительныеПараметры.ИндексКЛ].КонтактнаяИнформация[ДополнительныеПараметры.ИндексКИ];
	ДанныеКонтактныхЛиц[ДополнительныеПараметры.ИндексКЛ].Изменен = Истина;

	ДанныеКИ.Представление = РезультатЗакрытия.Представление;
	ДанныеКИ.Значение      = РезультатЗакрытия.КонтактнаяИнформация;
	ДанныеКИ.Комментарий   = РезультатЗакрытия.Комментарий;
	
	Модифицированность = Истина;
	
	Если ДанныеКИ.Тип = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес") Тогда
		ЗаполнитьСписокВыбораАдресовКонтактныхЛиц(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Теги

&НаКлиенте
Процедура ПолеВводаТегаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПолеВводаТегаОбработкаВыбораСервер(Элемент.Имя, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеВводаТегаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПолеВводаТегаОкончаниеВводаТекстаСервер(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкиКонтрагента

&НаКлиентеНаСервереБезКонтекста
Процедура ВыполнитьВсеПроверки(Форма)
	
	СформироватьПредставлениеПроверкиИНН(Форма);
	СформироватьПредставлениеПроверкиКПП(Форма);
	СформироватьПредставлениеПроверкиОКПО(Форма);
	СформироватьПредставлениеПроверкиОГРН(Форма);
	СформироватьПредставлениеПроверкиДублей(Форма);
	
	РаботаСКонтрагентамиКлиентСерверПереопределяемый.СформироватьПредставлениеПроверкиДанных(Форма);
	
	Форма.Элементы.ОтступПроверкиДанных.Видимость = ЗначениеЗаполнено(Форма.ПредставлениеПроверкиДанных);
	Если ЗначениеЗаполнено(Форма.ПредставлениеПроверкиДанных) Тогда
		Форма.Элементы.ЮридическиеДанные.ЦветТекстаЗаголовка = Форма.ЦветТекстаНекорректногоКонтрагента;
	Иначе
		Форма.Элементы.ЮридическиеДанные.ЦветТекстаЗаголовка = Форма.ЦветГиперссылки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьПредставлениеПроверкиИНН(Форма)
	
	Объект = Форма.Объект;
	ОписаниеОшибки = "";
	Если ПустаяСтрока(Объект.ИНН) Тогда
		Объект.ИННВведенКорректно = Истина;
	Иначе
		Объект.ИННВведенКорректно = РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(Объект.ИНН, ЭтоЮрЛицо(Объект.ВидКонтрагента), ОписаниеОшибки);
	КонецЕсли;
	Форма.ПредставлениеПроверкиИНН = Новый ФорматированнаяСтрока(ОписаниеОшибки, , Форма.ЦветТекстаНекорректногоКонтрагента);
	
	Форма.Элементы.ОтступПроверкиДанных.Видимость = ЗначениеЗаполнено(ОписаниеОшибки);
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Форма.Элементы.ЮридическиеДанные.ЦветТекстаЗаголовка = Форма.ЦветТекстаНекорректногоКонтрагента;
	Иначе
		Форма.Элементы.ЮридическиеДанные.ЦветТекстаЗаголовка = Форма.ЦветГиперссылки;
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьПредставлениеПроверкиКПП(Форма)
	
	Объект = Форма.Объект;
	ОписаниеОшибки = "";
	Если ПустаяСтрока(Объект.КПП) Тогда
		Объект.КППВведенКорректно = Истина;
	Иначе
		Объект.КППВведенКорректно = РегламентированныеДанныеКлиентСервер.КППСоответствуетТребованиям(Объект.КПП, ОписаниеОшибки);
	КонецЕсли;
	Форма.ПредставлениеПроверкиКПП = Новый ФорматированнаяСтрока(ОписаниеОшибки, , Форма.ЦветТекстаНекорректногоКонтрагента);
	
	Форма.Элементы.ОтступПроверкиДанных.Видимость = ЗначениеЗаполнено(ОписаниеОшибки);
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Форма.Элементы.ЮридическиеДанные.ЦветТекстаЗаголовка = Форма.ЦветТекстаНекорректногоКонтрагента;
	Иначе
		Форма.Элементы.ЮридическиеДанные.ЦветТекстаЗаголовка = Форма.ЦветГиперссылки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьПредставлениеПроверкиОКПО(Форма)
	
	Объект = Форма.Объект;
	ОписаниеОшибки = "";
	Если Не ПустаяСтрока(Объект.КодПоОКПО) Тогда
		РегламентированныеДанныеКлиентСервер.КодПоОКПОСоответствуетТребованиям(Объект.КодПоОКПО, ЭтоЮрЛицо(Объект.ВидКонтрагента), ОписаниеОшибки);
	КонецЕсли;
	Форма.ПредставлениеПроверкиОКПО = Новый ФорматированнаяСтрока(ОписаниеОшибки, , Форма.ЦветТекстаНекорректногоКонтрагента);
	
	Форма.Элементы.ОтступПроверкиДанных.Видимость = ЗначениеЗаполнено(ОписаниеОшибки);
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Форма.Элементы.ЮридическиеДанные.ЦветТекстаЗаголовка = Форма.ЦветТекстаНекорректногоКонтрагента;
	Иначе
		Форма.Элементы.ЮридическиеДанные.ЦветТекстаЗаголовка = Форма.ЦветГиперссылки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьПредставлениеПроверкиОГРН(Форма)
	
	Объект = Форма.Объект;
	ОписаниеОшибки = "";
	
	// Проверка ОГРН имеет смысл только для юридического лица зарегистрированного в России или для индивидуального предпринимателя
	Если Не ПустаяСтрока(Объект.РегистрационныйНомер)
		И ((ЭтоЮрЛицо(Объект.ВидКонтрагента) И Объект.СтранаРегистрации = ПредопределенноеЗначение("Справочник.СтраныМира.Россия"))
		Или (Объект.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель"))) Тогда
		
		РегламентированныеДанныеКлиентСервер.ОГРНСоответствуетТребованиям(Объект.РегистрационныйНомер, ЭтоЮрЛицо(Объект.ВидКонтрагента), ОписаниеОшибки);
	КонецЕсли;
	
	Форма.ПредставлениеПроверкиОГРН = Новый ФорматированнаяСтрока(ОписаниеОшибки, , Форма.ЦветТекстаНекорректногоКонтрагента);
	
	Форма.Элементы.ОтступПроверкиДанных.Видимость = ЗначениеЗаполнено(ОписаниеОшибки);
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Форма.Элементы.ЮридическиеДанные.ЦветТекстаЗаголовка = Форма.ЦветТекстаНекорректногоКонтрагента;
	Иначе
		Форма.Элементы.ЮридическиеДанные.ЦветТекстаЗаголовка = Форма.ЦветГиперссылки;
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьПредставлениеПроверкиДублей(Форма)
	
	Объект = Форма.Объект;
	ОписаниеОшибки = "";
	
	Если ИННиКППКорректны(Форма) И Не ПустаяСтрока(Объект.ИНН) Тогда
		
		
		МассивДублей = ПолучитьДублиКонтрагентаСервер(СокрЛП(Объект.ИНН), СокрЛП(Объект.КПП), Объект.Ссылка);
		
		КоличествоДублей = МассивДублей.Количество();
		
		Если КоличествоДублей > 0 Тогда
			
			СтруктураПараметровСообщенияОДублях = Новый Структура;
			СтруктураПараметровСообщенияОДублях.Вставить("ИННиКПП", ?(ЭтоЮрЛицо(Объект.ВидКонтрагента), НСтр("ru = 'ИНН и КПП'"), НСтр("ru = 'ИНН'")));
			
			Если КоличествоДублей = 1 Тогда
				СтруктураПараметровСообщенияОДублях.Вставить("КоличествоДублей", НСтр("ru = 'один'"));
				СтруктураПараметровСообщенияОДублях.Вставить("СклонениеКонтрагентов", НСтр("ru = 'контрагент'"));
			ИначеЕсли КоличествоДублей < 5 Тогда
				СтруктураПараметровСообщенияОДублях.Вставить("КоличествоДублей", КоличествоДублей);
				СтруктураПараметровСообщенияОДублях.Вставить("СклонениеКонтрагентов", НСтр("ru = 'контрагента'"));
			Иначе
				СтруктураПараметровСообщенияОДублях.Вставить("КоличествоДублей", КоличествоДублей);
				СтруктураПараметровСообщенияОДублях.Вставить("СклонениеКонтрагентов", НСтр("ru = 'контрагентов'"));
			КонецЕсли;
			
			ОписаниеОшибки = НСтр("ru = 'С таким [ИННиКПП] есть [КоличествоДублей] [СклонениеКонтрагентов]'");
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ОписаниеОшибки, СтруктураПараметровСообщенияОДублях);
			
		КонецЕсли;
	КонецЕсли;
	
	Форма.ПредставлениеПроверкиДублей = Новый ФорматированнаяСтрока(ОписаниеОшибки, , Форма.ЦветТекстаНекорректногоКонтрагента, , "ПоказатьДубли");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДублиКонтрагентаСервер(ИНН, КПП, ИсключаяСсылку)
	
	Возврат Справочники.Контрагенты.ПроверитьДублиСправочникаКонтрагентыПоИННКПП(ИНН, КПП, ИсключаяСсылку);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИННиКППКорректны(Форма)
	
	Объект = Форма.Объект;
	ЭтоЮрЛицо = ЭтоЮрЛицо(Объект.ВидКонтрагента);
	
	Если ЭтоЮрЛицо Тогда
		Результат = Объект.ИННВведенКорректно И (Объект.КППВведенКорректно Или ПустаяСтрока(Объект.КПП));
	Иначе
		Результат = Объект.ИННВведенКорректно;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоЮрЛицо(ВидКонтрагента)
	
	Возврат ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ЮридическоеЛицо")
		Или ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ГосударственныйОрган");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьРеквизитыПроверкиКонтрагентов(Форма)
	
	Объект = Форма.Объект;
	
	Форма.РеквизитыПроверкиКонтрагентов.ЭтоИностранныйКонтрагент =
		(Объект.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ЮридическоеЛицо")
		И Объект.СтранаРегистрации <> ПредопределенноеЗначение("Справочник.СтраныМира.Россия"));
	
	Форма.РеквизитыПроверкиКонтрагентов.ЭтоЮридическоеЛицо =
		(Объект.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ЮридическоеЛицо")
		Или Объект.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ГосударственныйОрган"));
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеЕГР

&НаКлиенте
Процедура ВыполнитьЗаполнениеРеквизитовПоИНН(знач ИНН)
	
	ОписаниеОшибкиЗаполнения = "";
	ЗаполнитьРеквизитыПоИНННаСервере(ИНН);
	УправлениеФормой();
	
	Если Не ПустаяСтрока(ОписаниеОшибкиЗаполнения) Тогда
		ОбработчикЗавершенияОбработкиОшибки = Новый ОписаниеОповещения(
		"ОбработатьОшибкуЗаполнитьРеквизитыПоИННЗавершение",
		ЭтотОбъект,
		ИНН);
		РаботаСКонтрагентамиКлиент.ОбработатьОшибку(ОписаниеОшибкиЗаполнения, ОбработчикЗавершенияОбработкиОшибки);
	Иначе
		Если ИспользоватьСпаркРиски Тогда
			// ИнтернетПоддержкаПользователей.СПАРКРиски
			ЭтотОбъект.ИндексыСПАРКРиски = Неопределено;
			ОбновитьОтображениеИндексыСПАРК();
			// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
		КонецЕсли;
		
		// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
		УстановитьРеквизитыПроверкиКонтрагентов(ЭтотОбъект);
		ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентаВСправочнике(ЭтотОбъект);
		// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыПоИНННаСервере(ИНН)
	
	Если СтрДлина(ИНН) = 12 Тогда
		ЭтоЮрЛицо = Ложь;
	Иначе
		ЭтоЮрЛицо = Истина;
	КонецЕсли;
	
	Если ЭтоЮрЛицо Тогда
		РеквизитыКонтрагента = РаботаСКонтрагентами.СведенияОЮридическомЛицеПоИНН(ИНН);
		
		Если ЗначениеЗаполнено(РеквизитыКонтрагента.ОписаниеОшибки) Тогда
			ОписаниеОшибкиЗаполнения = РеквизитыКонтрагента.ОписаниеОшибки;
			Возврат;
		КонецЕсли;
		
		Если РеквизитыКонтрагента.ЕГРЮЛ = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Объект.Наименование         = РеквизитыКонтрагента.ЕГРЮЛ.Наименование;
		Объект.НаименованиеПолное   = РеквизитыКонтрагента.ЕГРЮЛ.НаименованиеСокращенное;
		Объект.ИНН                  = РеквизитыКонтрагента.ИНН;
		Объект.КПП                  = РеквизитыКонтрагента.ЕГРЮЛ.КПП;
		Объект.РегистрационныйНомер = РеквизитыКонтрагента.ЕГРЮЛ.РегистрационныйНомер;
		
		ДанныеЗаполненияКИ = Новый Структура("Представление,Комментарий,Значение");
		
		Если РеквизитыКонтрагента.ЕГРЮЛ.ЮридическийАдрес <> Неопределено Тогда
			ДанныеЗаполненияКИ.Представление = РеквизитыКонтрагента.ЕГРЮЛ.ЮридическийАдрес.Представление;
			ДанныеЗаполненияКИ.Комментарий = РеквизитыКонтрагента.ЕГРЮЛ.ЮридическийАдрес.Комментарий;
			ДанныеЗаполненияКИ.Значение = РеквизитыКонтрагента.ЕГРЮЛ.ЮридическийАдрес.КонтактнаяИнформация;
			
			ВидКИ = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
			КонтактнаяИнформацияУНФ.ЗаполнитьЗначениеКонтактнойИнформации(ЭтотОбъект, ВидКИ, ДанныеЗаполненияКИ, Истина);
			
			ВидКИ = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
			КонтактнаяИнформацияУНФ.ЗаполнитьЗначениеКонтактнойИнформации(ЭтотОбъект, ВидКИ, ДанныеЗаполненияКИ, Ложь);
		КонецЕсли;
		
		Если РеквизитыКонтрагента.ЕГРЮЛ.Телефон <> Неопределено Тогда
			ДанныеЗаполненияКИ.Представление = РеквизитыКонтрагента.ЕГРЮЛ.Телефон.Представление;
			ДанныеЗаполненияКИ.Комментарий = РеквизитыКонтрагента.ЕГРЮЛ.Телефон.Комментарий;
			ДанныеЗаполненияКИ.Значение = РеквизитыКонтрагента.ЕГРЮЛ.Телефон.КонтактнаяИнформация;
			
			ВидКИ = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
			КонтактнаяИнформацияУНФ.ЗаполнитьЗначениеКонтактнойИнформации(ЭтотОбъект, ВидКИ, ДанныеЗаполненияКИ, Истина);
		КонецЕсли;
		
		Если ДанныеКонтактныхЛиц.Количество() = 0 Тогда
			ОсновнойКонтакт = ДанныеКонтактныхЛиц.Добавить();
		Иначе
			ОсновнойКонтакт = ДанныеКонтактныхЛиц[0];
		КонецЕсли;
		
		Если РеквизитыКонтрагента.ЕГРЮЛ.Руководители.Количество() <> 0 И ПустаяСтрока(ОсновнойКонтакт.Наименование) Тогда
			ДанныеРуководителяПоИНН = РеквизитыКонтрагента.ЕГРЮЛ.Руководители[0];
			ФИОРуководителя = Новый Массив;
			Если ЗначениеЗаполнено(ДанныеРуководителяПоИНН.Фамилия) Тогда
				ФИОРуководителя.Добавить(ДанныеРуководителяПоИНН.Фамилия);
			КонецЕсли;
			Если ЗначениеЗаполнено(ДанныеРуководителяПоИНН.Имя) Тогда
				ФИОРуководителя.Добавить(ДанныеРуководителяПоИНН.Имя);
			КонецЕсли;
			Если ЗначениеЗаполнено(ДанныеРуководителяПоИНН.Отчество) Тогда
				ФИОРуководителя.Добавить(ДанныеРуководителяПоИНН.Отчество);
			КонецЕсли;
			ОсновнойКонтакт.Наименование = СтрСоединить(ФИОРуководителя, " ");
			ОсновнойКонтакт.Должность = ДанныеРуководителяПоИНН.Должность;
			ОсновнойКонтакт.Изменен = Истина;
			ОсновнойКонтакт.ГруппаРазвернута = Истина;
		КонецЕсли;
		
		НаименованиеПолное = РеквизитыКонтрагента.ЕГРЮЛ.НаименованиеПолное;
		
	Иначе
		
		РеквизитыПредпринимателя = РаботаСКонтрагентами.РеквизитыПредпринимателяПоИНН(ИНН);
		
		Если ЗначениеЗаполнено(РеквизитыПредпринимателя.ОписаниеОшибки) Тогда
			ОписаниеОшибкиЗаполнения = РеквизитыПредпринимателя.ОписаниеОшибки;
			Возврат;
		КонецЕсли;
		
		Объект.Наименование			= РеквизитыПредпринимателя.Наименование;
		Объект.НаименованиеПолное	= РеквизитыПредпринимателя.НаименованиеСокращенное;
		Объект.ИНН					= РеквизитыПредпринимателя.ИНН;
		Объект.РегистрационныйНомер	= РеквизитыПредпринимателя.РегистрационныйНомер;
		
		Если РеквизитыПредпринимателя.СвидетельствоОРегистрации <> Неопределено Тогда
			Объект.СвидетельствоСерияНомер = "" + РеквизитыПредпринимателя.СвидетельствоОРегистрации.Серия + " " + РеквизитыПредпринимателя.СвидетельствоОРегистрации.Номер;
			Объект.СвидетельствоДатаВыдачи = РеквизитыПредпринимателя.СвидетельствоОРегистрации.Дата;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РеквизитыПредпринимателя.Фамилия) Тогда
			Объект.ФИО = РеквизитыПредпринимателя.Фамилия
				+ ?(ПустаяСтрока(РеквизитыПредпринимателя.Имя), "", " " + РеквизитыПредпринимателя.Имя)
				+ ?(ПустаяСтрока(РеквизитыПредпринимателя.Отчество), "", " " + РеквизитыПредпринимателя.Отчество);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РеквизитыПредпринимателя.Пол) Тогда
			Объект.Пол = ?(РеквизитыПредпринимателя.Пол = "1", Перечисления.ПолФизическогоЛица.Мужской, Перечисления.ПолФизическогоЛица.Женский);
		КонецЕсли;
		
		НаименованиеПолное = РеквизитыПредпринимателя.НаименованиеПолное;
		
	КонецЕсли;
	
	ЗаполнитьСписокВыбораНаименования(ЭтотОбъект);
	Если ЗначениеЗаполнено(НаименованиеПолное) Тогда
		
		Элементы.Наименование.СписокВыбора.Добавить(НаименованиеПолное);
		
		Элементы.НаименованиеПолное.СписокВыбора.Очистить();
		Элементы.НаименованиеПолное.СписокВыбора.Добавить(Объект.НаименованиеПолное);
		Элементы.НаименованиеПолное.СписокВыбора.Добавить(НаименованиеПолное);
		Элементы.НаименованиеПолное.КнопкаВыпадающегоСписка = Истина;
		
	КонецЕсли;
	
	Если СтрДлина(ИНН) = 10 Тогда
		Объект.ВидКонтрагента = Перечисления.ВидыКонтрагентов.ЮридическоеЛицо;
	ИначеЕсли СтрДлина(ИНН) = 12 Тогда
		
		Если ТипЗнч(ОрганизационноПравоваяФорма) = Тип("Структура")
			И ОрганизационноПравоваяФорма.Свойство("ПолнаяФорма")
			И ВРег(ОрганизационноПравоваяФорма.ПолнаяФорма) = ВРег("Индивидуальный предприниматель") Тогда
				
			Объект.ВидКонтрагента = Перечисления.ВидыКонтрагентов.ИндивидуальныйПредприниматель;
		Иначе
			Объект.ВидКонтрагента = Перечисления.ВидыКонтрагентов.ФизическоеЛицо;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьЭлементыКонтактныхЛиц();
	КонтактнаяИнформацияУНФКлиентСервер.ЗаполнитьСписокВыбораАдресов(ЭтотОбъект);
	ВыполнитьВсеПроверки(ЭтотОбъект);
	УстановитьЗаголовокЮридическихДанных(ЭтотОбъект);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КлючевыеРеквизитыЗаполнены(Форма)
	
	Результат = Ложь;
	Объект = Форма.Объект;
	
	Если ЗначениеЗаполнено(Объект.Наименование)
		Или Объект.КонтактнаяИнформация.Количество() > 0 Тогда
		
		Результат = Истина;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьОшибкуЗаполнитьРеквизитыПоИННЗавершение(Результат, СтрокаИНН) Экспорт
	
	Если Результат.ПовторитьДействие Тогда
		ВыполнитьЗаполнениеРеквизитовПоИНН(СтрокаИНН);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеРеквизитовКонтрагентВыбран(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Результат) 
		ИЛИ ТипЗнч(Результат) <> Тип("Строка") Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ИНН = Результат;
	ВыполнитьЗаполнениеРеквизитовПоИНН(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПерезаполнитьРеквизитыПоИННЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ВыполнитьЗаполнениеРеквизитовПоИНН(Объект.ИНН);
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти

#Область ЗаголовкиСвернутогоОтображенияГрупп

// Процедура устанавливает заголовок свернутого отображения для группы, по шаблону:
// <заголовок группы (как задан в конфигураторе)> : <динамический параметр 1>, <динамический параметр 2>
//
// Параметры:
//  Форма					 - Форма	 - текущая форма
//  НазваниеГруппы			 - Строка	 - имя группы формы, для которой устанавливается заголовок
//  ДинамическиеПараметры	 - Массив	 - массив частей заголовка.
//
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокСвернутогоОтображения(Форма, НазваниеГруппы, ДинамическиеПараметры)
	
	Заголовок = Новый Массив;
	
	Для Каждого Параметр Из ДинамическиеПараметры Цикл
		Заголовок.Добавить(Параметр);
	КонецЦикла;
	
	Если Заголовок.Количество() > 0 Тогда
		Форма.Элементы[НазваниеГруппы].Заголовок = СтрСоединить(Заголовок, " / ");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокЮридическихДанных(Форма)
	
	Объект = Форма.Объект;
	ДинамическиеПараметры = Новый Массив;
		
	Если Не ПустаяСтрока(Объект.ИНН) Тогда
		ДинамическиеПараметры.Добавить(СтрШаблон(НСтр("ru='ИНН: %1'"), Объект.ИНН));
	Иначе
		ДинамическиеПараметры.Добавить(НСтр("ru='<ИНН>'"));
	КонецЕсли;
	
	Если Объект.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ЮридическоеЛицо") 
		ИЛИ Объект.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ГосударственныйОрган") Тогда
		Если Не ПустаяСтрока(Объект.КПП) Тогда
			ДинамическиеПараметры.Добавить(СтрШаблон(НСтр("ru='КПП: %1'"), Объект.КПП));
		Иначе
			ДинамическиеПараметры.Добавить(НСтр("ru='<КПП>'"));
		КонецЕсли;
	Иначе
		Если Не ПустаяСтрока(Объект.ДокументУдостоверяющийЛичность) Тогда
			ДинамическиеПараметры.Добавить(СтрШаблон(НСтр("ru='Документ: %1'"), Объект.ДокументУдостоверяющийЛичность));
		Иначе
			ДинамическиеПараметры.Добавить(НСтр("ru='<Документ>'"));
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ГоловнойКонтрагент) Тогда
		ДинамическиеПараметры.Добавить(НСтр("ru='Есть головной'"));
	КонецЕсли;
	
	Если Форма.ЭтотКонтрагентЯвляетсяГоловным Тогда
		ДинамическиеПараметры.Добавить(НСтр("ru='Есть подразделения'"));
	КонецЕсли;
	
	УстановитьЗаголовокСвернутогоОтображения(Форма, "ЮридическиеДанные", ДинамическиеПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область ПанельДополнительнойИнформации

&НаСервере
Процедура ПрочитатьДанныеПанелиДопИнформации()
	
	Если УправлениеДоступомУНФ.ЕстьПрофильРабочееМестоКассира() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ПанельДополнительнойИнформации.Видимость = ЗначениеЗаполнено(Объект.Ссылка);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	КрупныйШрифт = Новый Шрифт(,11);
	МелкийШрифт  = Новый Шрифт(,8);
	
	Запрос = Новый Запрос;
	КонтрагентУИД = "Контрагент"+СтрЗаменить(Объект.Ссылка.УникальныйИдентификатор(),"-","");
	Запрос.УстановитьПараметр(КонтрагентУИД, Объект.Ссылка);
	
	// 1. Остаток взаиморасчетов
	
	Если ПросмотрВзаиморасчетов Тогда
				
		Элементы.ОстатокВзаиморасчетов.Заголовок = РаботаСКонтрагентамиУНФ.ЗаголовокНадписиВзаиморасчетов(Объект.Ссылка);
		
	КонецЕсли;
	
	// 2. Сумма продаж
	
	Если ПросмотрПродаж Тогда
		
		Запрос.Текст = "
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПродажиОбороты.СуммаОборот КАК Сумма
			|ИЗ
			|	РегистрНакопления.Продажи.Обороты(, , , Контрагент = &Контрагент) КАК ПродажиОбороты
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	Продажи.Документ КАК Документ,
			|	Продажи.Документ.Дата КАК Дата
			|ИЗ
			|	РегистрНакопления.Продажи КАК Продажи
			|ГДЕ
			|	Продажи.Контрагент = &Контрагент
			|
			|УПОРЯДОЧИТЬ ПО
			|	Продажи.Документ.Дата УБЫВ";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Контрагент", "&"+КонтрагентУИД);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		Выборка = МассивРезультатов[0].Выбрать();
		Если Выборка.Следующий() Тогда
			Сумма = Выборка.Сумма;
		Иначе
			Сумма = 0;
		КонецЕсли;
		
		КомпонентыФС = Новый Массив;
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Продажи на'") + " ", КрупныйШрифт));
		
		СуммаСтрокой = Формат(Сумма, "ЧДЦ=2; ЧРД=,; ЧРГ=' '; ЧН=0,00");
		ПозицияРазделителя = СтрНайти(СуммаСтрокой, ",");
		КомпонентыЧисла = Новый Массив;
		КомпонентыЧисла.Добавить(Новый ФорматированнаяСтрока(Лев(СуммаСтрокой, ПозицияРазделителя), КрупныйШрифт));
		КомпонентыЧисла.Добавить(Новый ФорматированнаяСтрока(Сред(СуммаСтрокой, ПозицияРазделителя+1), МелкийШрифт));
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(КомпонентыЧисла, , , , "Продажи"));
		
		КомпонентыФС.Добавить(" "
		+ УправлениеНебольшойФирмойПовтИсп.ПолучитьСимвольноеПредставлениеВалюты(
		УправлениеНебольшойФирмойПовтИсп.ПолучитьВалютуУчета()));
		
		Элементы.СуммаПродаж.Заголовок = Новый ФорматированнаяСтрока(КомпонентыФС, , ЦветаСтиля.ТекстВторостепеннойНадписи);
		
		// 3. Последняя продажа
		
		Выборка = МассивРезультатов[1].Выбрать();
		Если Выборка.Следующий() Тогда
			Дата = Выборка.Дата;
			СсылкаНаДокумент = ПолучитьНавигационнуюСсылку(Выборка.Документ);
		Иначе
			Дата = '00010101';
			СсылкаНаДокумент = "";
		КонецЕсли;
		
		КомпонентыФС = Новый Массив;
		КомпонентыФС.Добавить(НСтр("ru='Последняя продажа'") + " ");
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(Формат(Дата, "Л=ru_RU; ДЛФ=D; ДП=<нет>"), , , , СсылкаНаДокумент));
		
		Элементы.ПоследняяПродажа.Заголовок = Новый ФорматированнаяСтрока(КомпонентыФС, КрупныйШрифт, ЦветаСтиля.ТекстВторостепеннойНадписи);
		
	КонецЕсли;
	
	// 4. Последнее событие
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СобытиеУчастники.Ссылка КАК Событие,
	|	СобытиеУчастники.Ссылка.НачалоСобытия КАК Дата
	|ИЗ
	|	Документ.Событие.Участники КАК СобытиеУчастники
	|ГДЕ
	|	СобытиеУчастники.Контакт = &Контрагент
	|	И СобытиеУчастники.Ссылка.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	СобытиеУчастники.Ссылка.НачалоСобытия УБЫВ";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Контрагент", "&"+КонтрагентУИД);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Дата = Выборка.Дата;
		СсылкаНаДокумент = ПолучитьНавигационнуюСсылку(Выборка.Событие);
	Иначе
		Дата = '00010101';
		СсылкаНаДокумент = "";
	КонецЕсли;
	
	КомпонентыФС = Новый Массив;
	КомпонентыФС.Добавить(НСтр("ru='Последнее событие'") + " ");
	КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(Формат(Дата, "Л=ru_RU; ДЛФ=D; ДП=<нет>"), , , , СсылкаНаДокумент));
	
	Элементы.ПоследнееСобытие.Заголовок = Новый ФорматированнаяСтрока(КомпонентыФС, КрупныйШрифт, ЦветаСтиля.ТекстВторостепеннойНадписи);
	
КонецПроцедуры

#КонецОбласти

#Область КонтактнаяИнформацияКонтрагентаУНФ

&НаСервере
Процедура ДобавитьКонтактнуюИнформациюСервер(ДобавляемыйВид, УстановитьВыводВФормеВсегда = Ложь) Экспорт
	
	КонтактнаяИнформацияУНФ.ДобавитьКонтактнуюИнформацию(ЭтотОбъект, ДобавляемыйВид, УстановитьВыводВФормеВсегда);
	ДобавитьЭлементыДублей();
	
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_ДействиеКИНажатие(Элемент)
	
	КонтактнаяИнформацияУНФКлиент.ДействиеКИНажатие(ЭтотОбъект, Элемент);
	
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_ПредставлениеКИПриИзменении(Элемент)
	
	КонтактнаяИнформацияУНФКлиент.ПредставлениеКИПриИзменении(ЭтотОбъект, Элемент);
	
	ИндексКИ = Число(Сред(Элемент.Имя, СтрДлина("ПредставлениеКИ_")+1));
	ДанныеКИ = ЭтотОбъект.КонтактнаяИнформация[ИндексКИ];
	
	Если НЕ ЗначениеЗаполнено(ДанныеКИ.Представление) И ДублиКИ.Количество() <> 0 Тогда
		УдалитьНеактивныеДублиИЭлементы();
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеКИ.Представление) Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьКИНаДубли(ДанныеКИ.Представление, ИндексКИ, ДанныеКИ.Тип);
	
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_ПредставлениеКИНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	КонтактнаяИнформацияУНФКлиент.ПредставлениеКИНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_ПредставлениеКИОчистка(Элемент, СтандартнаяОбработка)
	
	КонтактнаяИнформацияУНФКлиент.ПредставлениеКИОчистка(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_КомментарийКИПриИзменении(Элемент)
	
	КонтактнаяИнформацияУНФКлиент.КомментарийКИПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияУНФВыполнитьКоманду(Команда)
	
	КонтактнаяИнформацияУНФКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)

	Если Истина Тогда

		КонтактнаяИнформацияУНФКлиент.АвтоПодбор(Текст, ДанныеВыбора, СтандартнаяОбработка);

	Иначе
		
		// вызов оставлен для проверки внедрения БСП
		УправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
			Ожидание, СтандартнаяОбработка);

	КонецЕсли;

КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Истина Тогда
		
		КонтактнаяИнформацияУНФКлиент.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, Элемент, СтандартнаяОбработка);
		
	Иначе
		
		// вызов оставлен для проверки внедрения БСП
		УправлениеКонтактнойИнформациейКлиент.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, Элемент.Имя, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_ПродолжитьОбновлениеКонтактнойИнформации(Результат, ДополнительныеПараметры) Экспорт
	ОбновитьКонтактнуюИнформациюПослеВыбораСервер();
КонецПроцедуры

#КонецОбласти

#Область КонтрольДублей

&НаСервере
Процедура УстановитьНастройкиКонтроляДублей()
	
	Элементы.ДублиНаименование.Видимость       = Ложь;
	Элементы.ДублиНаименованиеПолное.Видимость = Ложь;

	НастройкиКонтроляДублейКонтрагенты = Константы.НастройкиКонтроляДублейКонтрагенты.Получить().Получить();
	НастройкиКонтроляДублейКонтакты    = Константы.НастройкиКонтроляДублейКонтакты.Получить().Получить();
	
	Если НастройкиКонтроляДублейКонтрагенты <> Неопределено Тогда
		ПроверятьПредставлениеНаДубли = НастройкиКонтроляДублейКонтрагенты.Получить("ПроверятьНаименование");
		ПроверятьЮрНазваниеНаДубли    = НастройкиКонтроляДублейКонтрагенты.Получить("ПроверятьЮрНазвание");
		ПроверятьНомерТелефонаНаДубли = НастройкиКонтроляДублейКонтрагенты.Получить("ПроверятьТелефон");
		ПроверятьАдресЭПНаДубли       = НастройкиКонтроляДублейКонтрагенты.Получить("ПроверятьАдресЭП");
	КонецЕсли;

	Если НастройкиКонтроляДублейКонтакты <> Неопределено Тогда
		ПроверятьПредставлениеНаДублиКонтакт = НастройкиКонтроляДублейКонтакты.Получить("ПроверятьНаименование");
		ПроверятьНомерТелефонаНаДублиКонтакт = НастройкиКонтроляДублейКонтакты.Получить("ПроверятьТелефон");
		ПроверятьАдресЭПНаДублиКонтакт       = НастройкиКонтроляДублейКонтакты.Получить("ПроверятьАдресЭП");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНаДубли(Реквизит)
	
	Если Реквизит = "НаименованиеПолное" Тогда
		Если НЕ ЗначениеЗаполнено(Объект.НаименованиеПолное) Тогда
			Возврат;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ЭтотОбъект.ОрганизационноПравоваяФорма.НаименованиеБезОПФ) Тогда
			Возврат;
		КонецЕсли;
		НаименованиеПолноеДляПоискаДублей = НаименованиеДляПоискаДублей(ЭтотОбъект.ОрганизационноПравоваяФорма.НаименованиеБезОПФ);
	КонецЕсли;

	Если Реквизит = "Наименование" Тогда
		Если НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
			Возврат;
		КонецЕсли;
		НаименованиеДляПоискаДублей = НаименованиеДляПоискаДублей(Объект.Наименование);
	КонецЕсли;
	
	ЗапросКонтрагенты = Новый Запрос;
	ЗапросКонтрагенты.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Контрагенты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	НЕ Контрагенты.Ссылка = &Ссылка
	|	И (Контрагенты.Наименование ПОДОБНО &Наименование
	|			ИЛИ Контрагенты.НаименованиеПолное ПОДОБНО &Наименование)";
	
	
	ЗапросКонтрагенты.УстановитьПараметр("Наименование", ЭтотОбъект[Реквизит + "ДляПоискаДублей"]);
	ЗапросКонтрагенты.УстановитьПараметр("Ссылка", Объект.Ссылка);
	УстановитьПривилегированныйРежим(Истина);
	Результат = ЗапросКонтрагенты.Выполнить();
			
	Если Результат.Пустой() Тогда
		
		ЗапросКонтакты = Новый Запрос;
		ЗапросКонтакты.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Контакты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.КонтактныеЛица КАК Контакты
		|ГДЕ
		|	Контакты.Наименование ПОДОБНО &Наименование
		|	И НЕ Контакты.Ссылка В (&Контакты)";
		
		ЗапросКонтакты.УстановитьПараметр("Контакты", ДанныеКонтактныхЛиц.Выгрузить(,"КонтактноеЛицо").ВыгрузитьКолонку("КонтактноеЛицо"));
		ЗапросКонтакты.УстановитьПараметр("Наименование", ЭтотОбъект[Реквизит + "ДляПоискаДублей"]);
		Результат = ЗапросКонтакты.Выполнить();
		
		Если Результат.Пустой() Тогда
			
			ЗапросЛиды = Новый Запрос;
			ЗапросЛиды.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Лиды.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Лиды КАК Лиды
			|ГДЕ
			|	НЕ Лиды.СостояниеЛида = ЗНАЧЕНИЕ(Справочник.СостоянияЛидов.Завершен)
			|	И (Лиды.Наименование ПОДОБНО &Наименование
			|			ИЛИ Лиды.НаименованиеКомпании ПОДОБНО &Наименование)";
			
			ЗапросЛиды.УстановитьПараметр("Наименование", ЭтотОбъект[Реквизит + "ДляПоискаДублей"]);
			Результат = ЗапросЛиды.Выполнить();
			
		КонецЕсли;
		
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	Элементы["Дубли"+Реквизит].Видимость = НЕ Результат.Пустой();

КонецПроцедуры

&НаСервере
Процедура ПроверитьНаименованиеКонтактаНаДубли(ИндексКЛ)
	
	ПредставлениеКонтакта = ЭтотОбъект.ДанныеКонтактныхЛиц[ИндексКЛ].Наименование;
	
	Если НЕ ЗначениеЗаполнено(ПредставлениеКонтакта) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПроверятьПредставлениеНаДублиКонтакт Тогда
		Возврат;
	КонецЕсли;
	
	НаименованиеКонтактаДляПоискаДублей = НаименованиеДляПоискаДублей(ПредставлениеКонтакта);
	
	ЗапросКонтрагенты = Новый Запрос;
	ЗапросКонтрагенты.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Контрагенты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	НЕ Контрагенты.Ссылка = &Ссылка
	|	И (Контрагенты.Наименование ПОДОБНО &Наименование
	|			ИЛИ Контрагенты.НаименованиеПолное ПОДОБНО &Наименование)";
	
	ЗапросКонтрагенты.УстановитьПараметр("Наименование", НаименованиеКонтактаДляПоискаДублей);
	ЗапросКонтрагенты.УстановитьПараметр("Ссылка", Объект.Ссылка);
	УстановитьПривилегированныйРежим(Истина);
	Результат = ЗапросКонтрагенты.Выполнить();
			
	Если Результат.Пустой() Тогда
		
		ЗапросКонтакты = Новый Запрос;
		ЗапросКонтакты.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
		|	Контакты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.КонтактныеЛица КАК Контакты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвязиКонтрагентКонтакт.СрезПоследних(, ) КАК СвязиКонтрагентКонтакт
		|		ПО (СвязиКонтрагентКонтакт.Контакт = Контакты.Ссылка)
		|ГДЕ
		|	Контакты.Наименование ПОДОБНО &Наименование
		|	И НЕ СвязиКонтрагентКонтакт.Контрагент = &Ссылка
		|	И НЕ Контакты.Ссылка = &Контакт
		|	И СвязиКонтрагентКонтакт.СвязьНедействительна = ЛОЖЬ";
		
		ЗапросКонтакты.УстановитьПараметр("Ссылка", Объект.Ссылка);
		ЗапросКонтакты.УстановитьПараметр("Контакт", ДанныеКонтактныхЛиц[ИндексКЛ].КонтактноеЛицо);
		ЗапросКонтакты.УстановитьПараметр("Наименование", НаименованиеКонтактаДляПоискаДублей);
		Результат = ЗапросКонтакты.Выполнить();
		
		Если Результат.Пустой() Тогда
			
			ЗапросЛиды = Новый Запрос;
			ЗапросЛиды.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Лиды.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Лиды КАК Лиды
			|ГДЕ
			|	НЕ Лиды.СостояниеЛида = ЗНАЧЕНИЕ(Справочник.СостоянияЛидов.Завершен)
			|	И (Лиды.Наименование ПОДОБНО &Наименование
			|			ИЛИ Лиды.НаименованиеКомпании ПОДОБНО &Наименование)";
			
			ЗапросЛиды.УстановитьПараметр("Наименование", НаименованиеКонтактаДляПоискаДублей);
			Результат = ЗапросЛиды.Выполнить();
			
		КонецЕсли;
		
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	ПоказатьСкрытьНадписьОДубляхНаименованияКЛ(ИндексКЛ, НЕ Результат.Пустой());

КонецПроцедуры

&НаСервере
Процедура ПоказатьСкрытьНадписьОДубляхНаименованияКЛ(ИндексКЛ, ЕстьДубли)
	
	КЛ = ЭтотОбъект.ДанныеКонтактныхЛиц[ИндексКЛ];
	
	Если НЕ ЕстьДубли Тогда
		
		ЭлементКЛ = Элементы.Найти("СообщениеОДубляхКЛ_"+ Строка(ИндексКЛ));
		Если ЭлементКЛ <> Неопределено Тогда
			Элементы.Удалить(ЭлементКЛ);
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	СообщениеОДублях = Элементы.Добавить("СообщениеОДубляхКЛ_" + Строка(ИндексКЛ), Тип("ДекорацияФормы"), Элементы["Контакт_" + ИндексКЛ]);
	СообщениеОДублях.Заголовок = НСтр("ru = 'Найдены дубли по представлению'");
	СообщениеОДублях.Гиперссылка = Истина;
	СообщениеОДублях.УстановитьДействие("Нажатие", "Подключаемый_СообщениеОДубляхКЛНажатие");
	Элементы.Переместить(СообщениеОДублях, Элементы["Контакт_" + ИндексКЛ], Элементы["КонтактнаяИнформацияКонтакт_" + ИндексКЛ]);
	СообщениеОДублях.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьКИНаДубли(ПредставлениеКИ, ИндексКИ, ТипКИ, ИндексКЛ = Неопределено)
	
	Если НЕ ЗначениеЗаполнено(ПредставлениеКИ) Тогда
		Возврат;
	КонецЕсли;
	
	ТипКИАдресЭП = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
	ТипКИНомер   = Перечисления.ТипыКонтактнойИнформации.Телефон;
	
	Если ТипКИ <> ТипКИАдресЭП И ТипКИ <> ТипКИНомер Тогда
		Возврат;
	КонецЕсли;
	
	Если (ТипКИ = ТипКИАдресЭП И НЕ ПроверятьАдресЭПНаДубли И ИндексКЛ = Неопределено)
		ИЛИ (ТипКИ = ТипКИАдресЭП И НЕ ПроверятьАдресЭПНаДублиКонтакт И ИндексКЛ <> Неопределено) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипКИ = ТипКИНомер И НЕ ПроверятьНомерТелефонаНаДубли И ИндексКЛ = Неопределено
		ИЛИ (ТипКИ = ТипКИАдресЭП И НЕ ПроверятьНомерТелефонаНаДублиКонтакт И ИндексКЛ <> Неопределено) Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросКонтрагенты = Новый Запрос;
	ЗапросКонтрагенты.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Контрагенты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	НЕ Контрагенты.Ссылка = &Ссылка
	|	И Контрагенты.АдресЭПДляПоиска ПОДОБНО &ПредставлениеКИ";
	
	ПоисковоеВыражение = ПредставлениеКИ;
	
	Если ТипКИ = ТипКИНомер Тогда
		ПоисковоеВыражение = КонтактнаяИнформацияУНФ.ПреобразоватьНомерДляКонтактнойИнформации(ПредставлениеКИ);
		ПоисковоеВыражение = СтрЗаменить(ПоисковоеВыражение, "+", "");
		Если СтрНачинаетсяС(ПоисковоеВыражение, "7") Тогда
			ПоисковоеВыражение = Сред(ПоисковоеВыражение, 2, СтрДлина(ПоисковоеВыражение) - 1);
		КонецЕсли;

		ЗапросКонтрагенты.Текст = СтрЗаменить(ЗапросКонтрагенты.Текст, "АдресЭП", "НомерТелефона");
	КонецЕсли;
	
	ЗапросКонтрагенты.УстановитьПараметр("Ссылка", Объект.Ссылка);
	ЗапросКонтрагенты.УстановитьПараметр("ПредставлениеКИ", "%" + ПоисковоеВыражение + "%");
	УстановитьПривилегированныйРежим(Истина);
	Результат = ЗапросКонтрагенты.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		ЗапросКонтакты = Новый Запрос;
		ЗапросКонтакты.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Контакты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.КонтактныеЛица КАК Контакты
		|ГДЕ
		|	НЕ Контакты.Ссылка В (&МассивКонтактов)
		|	И Контакты.АдресЭПДляПоиска ПОДОБНО &ПредставлениеКИ";
		
		Если ТипКИ = ТипКИНомер Тогда
			ЗапросКонтакты.Текст = СтрЗаменить(ЗапросКонтакты.Текст, "АдресЭП", "НомерТелефона");
		КонецЕсли;
		
		ЗапросКонтакты.УстановитьПараметр("МассивКонтактов", ДанныеКонтактныхЛиц.Выгрузить(,"КонтактноеЛицо").ВыгрузитьКолонку("КонтактноеЛицо"));
		ЗапросКонтакты.УстановитьПараметр("ПредставлениеКИ", "%" + ПоисковоеВыражение + "%");
		
		Результат = ЗапросКонтакты.Выполнить();
		
		Если Результат.Пустой() Тогда
			
			ЗапросЛиды = Новый Запрос;
			ЗапросЛиды.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Лиды.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Лиды КАК Лиды
			|ГДЕ
			|	НЕ Лиды.СостояниеЛида = ЗНАЧЕНИЕ(Справочник.СостоянияЛидов.Завершен)
			|	И Лиды.АдресЭПДляПоиска ПОДОБНО &ПредставлениеКИ";
			
			Если ТипКИ = ТипКИНомер Тогда
				ЗапросЛиды.Текст = СтрЗаменить(ЗапросЛиды.Текст, "АдресЭП", "НомерТелефона");
			КонецЕсли;

			ЗапросЛиды.УстановитьПараметр("ПредставлениеКИ", "%" + ПоисковоеВыражение + "%");
			Результат = ЗапросЛиды.Выполнить();
			
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	ЕстьДубли = НЕ Результат.Пустой();
	ПоказатьСкрытьНадписьОДубляхКИ(ПредставлениеКИ, ТипКИ, ИндексКИ, ЕстьДубли, ИндексКЛ);
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьСкрытьНадписьОДубляхКИ(ПредставлениеКИ, ТипКИ, ИндексКИ, ЕстьДубли, ИндексКЛ = Неопределено)
	
	УдалитьНеактивныеДублиИЭлементы(ИндексКЛ);
	
	Дубли = ДублиКИ.НайтиСтроки(Новый Структура("Представление", ПредставлениеКИ));
	
	Если Дубли.Количество() <> 0 Тогда
		СообщениеОДублях = Элементы.Найти("СообщениеОДублях_" + Строка(Дубли[0].ИндексДекорации));
		СообщениеОДублях.Видимость = ЕстьДубли;
		Возврат;
	КонецЕсли;

	Если НЕ ЕстьДубли Тогда
		Возврат;
	КонецЕсли;
	
	ДублиКИ.Сортировать("ИндексДекорации УБЫВ");
	
	НовыйДубль = ДублиКИ.Добавить();
	НовыйДубль.ТипКИ           = ТипКИ;
	НовыйДубль.Представление   = ПредставлениеКИ;
	НовыйДубль.ИндексДекорации = ДублиКИ[0].ИндексДекорации + 1;
	НовыйДубль.ЭтоДубльКЛ      = ?(ИндексКЛ = Неопределено, Ложь, Истина);
	НовыйДубль.ИндексКЛ        = ?(ИндексКЛ = Неопределено, 0, ИндексКЛ);
	
	ЭлементыКИ = ?(ИндексКЛ = Неопределено, Элементы["ЗначенияКонтактнойИнформации"], Элементы["КонтактнаяИнформацияКонтакт_" + ИндексКЛ]);
	
	СообщениеОДублях = Элементы.Добавить("СообщениеОДублях_" + Строка(НовыйДубль.ИндексДекорации), Тип("ДекорацияФормы"), ЭлементыКИ);
	СообщениеОДублях.Заголовок = СообщенияОДубляхКИПоТипу(ТипКИ);
	СообщениеОДублях.Гиперссылка = Истина;
	СообщениеОДублях.УстановитьДействие("Нажатие", "Подключаемый_СообщениеОДубляхНажатие");
	
	Если ИндексКЛ = Неопределено Тогда
		ЭлементКИ = Элементы.Найти("КИ_"+Строка(ИндексКИ + 1));
	Иначе
		ЭлементКИ = Элементы.Найти("Контакт_"+ Строка(ИндексКЛ) +"_КИ_"+Строка(ИндексКИ + 1));
	КонецЕсли;
	
	Если ЭлементКИ = Неопределено Тогда
		Элементы.Переместить(СообщениеОДублях, ЭлементыКИ);
	Иначе
		Элементы.Переместить(СообщениеОДублях, ЭлементыКИ, ЭлементКИ);
	КонецЕсли;
		
	СообщениеОДублях.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Функция СообщенияОДубляхКИПоТипу(Тип)
	
	Если Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда
		Возврат НСтр("ru = 'Найдены дубли по e-mail'");
	КонецЕсли;
	
	Если Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
		Возврат НСтр("ru = 'Найдены дубли по номеру телефона'");
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УдалитьНеактивныеДублиИЭлементы(ИндексКЛ = Неопределено)
	
	Если ИндексКЛ = Неопределено Тогда
		ТаблицаКИ = ЭтотОбъект.КонтактнаяИнформация;
	Иначе
		ТаблицаКИ = ЭтотОбъект.ДанныеКонтактныхЛиц[ИндексКЛ].КонтактнаяИнформация;
	КонецЕсли;
	
	Если ДублиКИ.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийИндекс = ДублиКИ.Количество() - 1;
	
	Пока ТекущийИндекс >= 0 Цикл
		
		Дубль = ДублиКИ[ТекущийИндекс];
		ТекущийИндекс = ТекущийИндекс - 1;
		Если ИндексКЛ = Неопределено И Дубль.ЭтоДубльКЛ Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИндексКЛ = Неопределено И Дубль.ЭтоДубльКЛ Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИндексКЛ <> Неопределено И НЕ Дубль.ЭтоДубльКЛ Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаКИ = ТаблицаКИ.НайтиСтроки(Новый Структура("Представление", Дубль.Представление));
		Если СтрокаКИ.Количество() <> 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Элементы.Удалить(Элементы["СообщениеОДублях_"+ Строка(Дубль.ИндексДекорации)]);
		ДублиКИ.Удалить(ДублиКИ.Индекс(Дубль));
		
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ПроверитьКонтрагентаНаДублиКИ()
	
	Для Каждого КИ Из ЭтотОбъект.КонтактнаяИнформация Цикл
		ИндексКИ = ЭтотОбъект.КонтактнаяИнформация.Индекс(КИ);
		ПроверитьКИНаДубли(КИ.Представление, ИндексКИ, КИ.Тип);
	КонецЦикла
	
КонецПроцедуры

&НаСервере
Функция НаименованиеДляПоискаДублей(Наименование)
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Наименование))Тогда
		Возврат "";
	КонецЕсли;
	
	ПоисковоеВыражение = СтрЗаменить(Наименование, "-", "%");
	ПоисковоеВыражение = СтрЗаменить(ПоисковоеВыражение, " ", "%");
	ПоисковоеВыражение = СтрЗаменить(ПоисковоеВыражение, "(", "%");
	ПоисковоеВыражение = СтрЗаменить(ПоисковоеВыражение, ")", "%");
	ПоисковоеВыражение = СтрЗаменить(ПоисковоеВыражение, ".", "%");
	ПоисковоеВыражение = СтрЗаменить(ПоисковоеВыражение, """", "%");
	ПоисковоеВыражение = СтрЗаменить(ПоисковоеВыражение, """", "%");
	
	Возврат СокрЛП("%"+ПоисковоеВыражение+"%");
	
КонецФункции

&НаСервере
Процедура ОбработатьВыборДублей(ВыбранныйКонтрагент)
	
	ЗакрыватьПриВыборе = Ложь;
	ЗначениеВРеквизитФормы(ВыбранныйКонтрагент.ПолучитьОбъект(), "Объект");
	УдалитьЭлементыДублейКИ();
	ДублиКИ.Очистить();
	Прочитать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПредупреждениеОДублях()
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыПредупреждениеДублей",ЭтотОбъект);
	ТекстПредупреждения = ТекстПредупрежденияПоДублям();

	СписокКнопок = Новый СписокЗначений;
	СписокКнопок.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Проигнорировать и записать'"));
	СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена'"));
	
	ПоказатьВопрос(Оповещение, ТекстПредупреждения, СписокКнопок,,КодВозвратаДиалога.Отмена, "Контроль дублей");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗакрытиеФормыПредупреждениеДублей(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ПроигнорированоСообщениеОДублях = Истина;
	СозданКопированием = Ложь;
	СообщениеОДублированииИнформации = ТекстПредупрежденияПоДублям(Истина);
	
	УдалитьЭлементыДублейКИ();
	Элементы.ДублиНаименование.Видимость       = Ложь;
	Элементы.ДублиНаименованиеПолное.Видимость = Ложь;
	
	Записать();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементыДублейКИ()
	
	Для Каждого Дубль Из ДублиКИ Цикл
		Если Элементы.Найти("СообщениеОДублях_"+ Строка(Дубль.ИндексДекорации)) <> Неопределено Тогда
			Элементы.Удалить(Элементы["СообщениеОДублях_"+ Строка(Дубль.ИндексДекорации)]);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Контакт Из ДанныеКонтактныхЛиц Цикл
		
		ИндексКЛ = ДанныеКонтактныхЛиц.Индекс(Контакт);
		
		Если Элементы.Найти("СообщениеОДубляхКЛ_"+ Строка(ИндексКЛ)) <> Неопределено Тогда
			Элементы.Удалить(Элементы["СообщениеОДубляхКЛ_"+ Строка(ИндексКЛ)]);
		КонецЕсли;
		
	КонецЦикла;
	
	ДублиКИ.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьКонтрагентаНаДублиСервер()
	
	Если ПроверятьПредставлениеНаДубли  Тогда
		ПроверитьНаДубли("Наименование");
	КонецЕсли;
	
	Если ПроверятьЮрНазваниеНаДубли Тогда
		ПроверитьНаДубли("НаименованиеПолное");
	КонецЕсли;
		
	УдалитьЭлементыДублейКИ();
	ПроверитьКонтрагентаНаДублиКИ();
	
	Для Каждого Контакт Из ДанныеКонтактныхЛиц Цикл
		
		ИндексКЛ = ДанныеКонтактныхЛиц.Индекс(Контакт);
		ПроверитьНаименованиеКонтактаНаДубли(ИндексКЛ);
			
		Для Каждого КИ Из Контакт.КонтактнаяИнформация Цикл
			ИндексКИ = Контакт.КонтактнаяИнформация.Индекс(КИ);
			ПроверитьКИНаДубли(КИ.Представление, ИндексКИ, КИ.Тип, ИндексКЛ);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ТекстПредупрежденияПоДублям(СообщениеПриЗаписи = Ложь)
	
	МассивСтрок = Новый Массив;
	
	Если Элементы.ДублиНаименование.Видимость Тогда
		МассивСтрок.Добавить(НСтр("ru = 'представлению'"));
	КонецЕсли;
	
	Если Элементы.ДублиНаименованиеПолное.Видимость Тогда
		МассивСтрок.Добавить(НСтр("ru = 'юр. названию'"));
	КонецЕсли;
	
	Для Каждого Контакт Из ДанныеКонтактныхЛиц Цикл
		
		ИндексКЛ = ДанныеКонтактныхЛиц.Индекс(Контакт);
		
		Если Элементы.Найти("СообщениеОДубляхКЛ_"+Строка(ИндексКЛ)) <> Неопределено Тогда
			МассивСтрок.Добавить(НСтр("ru = 'имени контакта'"));
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Отбор = Новый Структура("ТипКИ", ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон"));
	ДублиПоНомеру = ДублиКИ.НайтиСтроки(Отбор);
	Если ДублиПоНомеру.Количество() > 0 Тогда
		МассивСтрок.Добавить(НСтр("ru = 'номеру телефона'"));
	КонецЕсли;
	
	Отбор = Новый Структура("ТипКИ", ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты"));
	ДублиПоАдресуЭП = ДублиКИ.НайтиСтроки(Отбор);
	Если ДублиПоАдресуЭП.Количество() > 0 Тогда
		МассивСтрок.Добавить(НСтр("ru = 'e-mail'"));
	КонецЕсли;
	
	Если СообщениеПриЗаписи Тогда
		Возврат НСтр("ru = 'Проигнорировано сообщение о дублировании информации по '") + СтрСоединить(МассивСтрок,", ");
	КонецЕсли;
	
	ТекстПредупреждения = НСтр("ru = 'Найдены дубли по '") + СтрСоединить(МассивСтрок,", ");
	Возврат ТекстПредупреждения;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьСообщениеОДублях()
	
	Если НЕ Элементы.ДублиНаименование.Видимость И НЕ Элементы.ДублиНаименованиеПолное.Видимость 
		И ДублиКИ.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Дублей не найдено'"));
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстПредупрежденияПоДублям());
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементыДублей()
	
	Для Каждого Дубль Из ДублиКИ Цикл
		
		Если Дубль.ЭтоДубльКЛ Тогда
			ТаблицаКИ = ЭтотОбъект.ДанныеКонтактныхЛиц[Дубль.ИндексКЛ].КонтактнаяИнформация;
		Иначе
			ТаблицаКИ = ЭтотОбъект.КонтактнаяИнформация;
		КонецЕсли;
		
		СтрокаКИ = ТаблицаКИ.НайтиСтроки(Новый Структура("Представление", Дубль.Представление));
		Если СтрокаКИ.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ИндексКИ = ТаблицаКИ.Индекс(СтрокаКИ[0]);
		ИндексКЛ = Дубль.ИндексКЛ;
		ТипКИ    = СтрокаКИ[0].Тип;
		
		ЭлементыКИ = ?(НЕ Дубль.ЭтоДубльКЛ, Элементы["ЗначенияКонтактнойИнформации"], Элементы["КонтактнаяИнформацияКонтакт_" + ИндексКЛ]);
	
		СообщениеОДублях = Элементы.Найти("СообщениеОДублях_" + Строка(Дубль.ИндексДекорации));
		
		Если СообщениеОДублях = Неопределено Тогда
			СообщениеОДублях = Элементы.Добавить("СообщениеОДублях_" + Строка(Дубль.ИндексДекорации), Тип("ДекорацияФормы"), ЭлементыКИ);
			СообщениеОДублях.Заголовок = СообщенияОДубляхКИПоТипу(ТипКИ);
			СообщениеОДублях.Гиперссылка = Истина;
			СообщениеОДублях.УстановитьДействие("Нажатие", "Подключаемый_СообщениеОДубляхНажатие");
		КонецЕсли;
		
		Если НЕ Дубль.ЭтоДубльКЛ Тогда
			ЭлементКИ = Элементы.Найти("КИ_"+Строка(ИндексКИ + 1));
		Иначе
			ЭлементКИ = Элементы.Найти("Контакт_"+ Строка(ИндексКЛ) +"_КИ_"+Строка(ИндексКИ + 1));
		КонецЕсли;
		
		Если ЭлементКИ = Неопределено Тогда
			Элементы.Переместить(СообщениеОДублях, ЭлементыКИ);
		Иначе
			Элементы.Переместить(СообщениеОДублях, ЭлементыКИ, ЭлементКИ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СообщениеОДубляхКЛНажатие(Элемент)
	
	ПоложениеИндекса = СтрНайти(Элемент.Имя, "_", НаправлениеПоиска.СКонца);
	ИндексДекорации  = Число(Сред(Элемент.Имя, ПоложениеИндекса+1));
	
	НаименованиеКЛ = ЭтотОбъект.ДанныеКонтактныхЛиц[ИндексДекорации].Наименование;
	
	ПараметрыДублей = Новый Структура;
	МассивКонтактов = Новый Массив;
	Для Каждого Контакт Из ЭтотОбъект.ДанныеКонтактныхЛиц Цикл
		МассивКонтактов.Добавить(Контакт.КонтактноеЛицо);
	КонецЦикла;
	
	ПараметрыДублей.Вставить("Наименование", НаименованиеДляПоискаДублей(НаименованиеКЛ));
	ПараметрыДублей.Вставить("ИсключаяКонтакты", МассивКонтактов);
	ПараметрыДублей.Вставить("СсылкаНаРедактируемыйОбъект", Объект.Ссылка);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыСпискаДублей", ЭтотОбъект);
	ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаВыбораДублей", ПараметрыДублей, ЭтотОбъект,,,,ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СообщениеОДубляхНажатие(Элемент)
	
	ПоложениеИндекса = СтрНайти(Элемент.Имя, "_", НаправлениеПоиска.СКонца);
	ИндексДекорации  = Число(Сред(Элемент.Имя, ПоложениеИндекса+1));
	
	Дубль = ДублиКИ.НайтиСтроки(Новый Структура("ИндексДекорации", ИндексДекорации));
	ТаблицаКИ = ?(НЕ Дубль[0].ЭтоДубльКЛ, ЭтотОбъект.КонтактнаяИнформация, ДанныеКонтактныхЛиц[Дубль[0].ИндексКЛ].КонтактнаяИнформация);
	СтрокаКИ = ТаблицаКИ.НайтиСтроки(Новый Структура("Представление", Дубль[0].Представление));
	
	Если СтрокаКИ.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИндексКИ = ТаблицаКИ.Индекс(СтрокаКИ[0]);
	ТипКИ    = ТаблицаКИ[ИндексКИ].Тип;
	ПредставлениеКИ = ТаблицаКИ[ИндексКИ].Представление;
	
	ПараметрыДублей = Новый Структура;
	МассивКонтактов = Новый Массив;
	Для Каждого Контакт Из ЭтотОбъект.ДанныеКонтактныхЛиц Цикл
		МассивКонтактов.Добавить(Контакт.КонтактноеЛицо);
	КонецЦикла;
	ПараметрыДублей.Вставить("ПредставлениеКИ", ПредставлениеКИ);
	ПараметрыДублей.Вставить("ТипКИ",           ТипКИ);
	ПараметрыДублей.Вставить("ИсключаяКонтакты", МассивКонтактов);
	ПараметрыДублей.Вставить("СсылкаНаРедактируемыйОбъект", Объект.Ссылка);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыСпискаДублей", ЭтотОбъект);
	ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаВыбораДублей", ПараметрыДублей, ЭтотОбъект,,,,ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗакрытиеФормыСпискаДублей(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено И РезультатЗакрытия.Свойство("ВыбранДубльКонтрагента") Тогда
		
		ОбработатьВыборДублей(РезультатЗакрытия.ВыбранныйКонтрагент);
		Элементы.ДублиНаименование.Видимость = Ложь;
		Элементы.ДублиНаименованиеПолное.Видимость = Ложь;
		КонтрагентЗамененНаДубль = Истина;
		Возврат;
		
	КонецЕсли;
	
	СформироватьПредставлениеПроверкиДублей(ЭтотОбъект);
	РаботаСКонтрагентамиКлиентСерверПереопределяемый.СформироватьПредставлениеПроверкиДанных(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область АвтоподборКонтактов

&НаСервере
Процедура ЗаполнитьНаименованиеИКонтактнуюИнформацию(Знач КлассификаторСсылка, Знач ИмяЭлемента = "")
	
	ДанныеКонтакта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
	КлассификаторСсылка,
	"Title, JSON");
	
	Если ДанныеКонтактныхЛиц.Количество() = 0 Тогда
		ДанныеКонтактныхЛиц.Добавить();
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяЭлемента) Тогда
		ИндексЭлемента = 0;
	Иначе
		ИндексЭлемента = Число(СтрЗаменить(ИмяЭлемента, "НаименованиеКонтакт_", ""));
	КонецЕсли;
	
	Если ИндексЭлемента = 0 Тогда
		Объект.Наименование = ДанныеКонтакта.Title;
	КонецЕсли;
	
	ДанныеКонтактныхЛиц[ИндексЭлемента].Наименование = ДанныеКонтакта.Title;
	ДанныеКонтактныхЛиц[ИндексЭлемента].Изменен = Истина;
	ДанныеКонтактныхЛиц[ИндексЭлемента].КонтактноеЛицо = Неопределено;
	
	ДанныеКонтактныхЛиц[ИндексЭлемента].КонтактнаяИнформация.Очистить();
	ЗаполнитьВсегдаВыводимыеВидыКИ(
	ДанныеКонтактныхЛиц[ИндексЭлемента].КонтактнаяИнформация,
	СвойстваВидовКонтактнойИнформацииКонтактныхЛиц);
	
	Справочники.КлассификаторКонтактов.ЗаполнитьКонтактнуюИнформациюИзJSON(
	ДанныеКонтактныхЛиц[ИндексЭлемента].КонтактнаяИнформация,
	ДанныеКонтакта.JSON,
	ТипЗнч(ДанныеКонтактныхЛиц[ИндексЭлемента].КонтактноеЛицо));
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Функция ОписаниеПолейДляАвтоподбора()
	
	Результат = Новый Соответствие;
	Индекс = 0;
	// @skip-warning
	Для Каждого Контакт Из ДанныеКонтактныхЛиц Цикл
		
		Если ДанныеКонтактныхЛиц.Индекс(Контакт) > 5 И НЕ ПоказыватьВсеКонтакты Тогда
			Прервать;
		КонецЕсли;
		
		ИмяПоля = "НаименованиеКонтакт_" + Индекс;
		Результат.Вставить(ИмяПоля, Новый Структура);
		
		Результат[ИмяПоля].Вставить("ПутьКДанным", "Объект.КонтактноеЛицо");
		Результат[ИмяПоля].Вставить("ТипЗначения", Тип("СправочникСсылка.КонтактныеЛица"));
		Результат[ИмяПоля].Вставить("ИмяФормыОбъекта", "Справочник.КонтактныеЛица.ФормаОбъекта");
		
		Индекс = Индекс + 1;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// @skip-warning
&НаКлиенте
Процедура Подключаемый_АвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	АвтоподборКонтактовКлиент.Подключаемый_АвтоПодбор(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, Параметры, Ожидание,
		СтандартнаяОбработка);
	
	ДобавитьПунктСозданиеКонтакта(ЭтотОбъект, ДанныеВыбора, Параметры, Текст,СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// ИнтернетПоддержкаПользователей.СПАРКРиски
// @skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьОтображениеИндексыСПАРК()
	ОбновитьОтображениеИндексыСПАРК();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеИндексыСПАРК()
	ПараметрыОтображения = Новый Структура("ВариантОтображения", "Многострочный");
	ВидКонтрагентаСПАРКРиски = СПАРКРискиКлиентСерверУНФ.ВидКонтрагентаСПАРКРиски(Объект.ВидКонтрагента);
	СПАРКРискиКлиент.ОтобразитьИндексыСПАРК(
		ЭтотОбъект.ИндексыСПАРКРиски,
		Объект,
		Объект.ИНН, // Искать по ИНН
		ВидКонтрагентаСПАРКРиски,
		ЭтотОбъект,
		ПараметрыОтображения,
		Истина);
КонецПроцедуры

&НаСервере
Процедура СПАРКРискиПриСозданииНаСервере()
	
	ИспользоватьСпаркРиски = ПолучитьФункциональнуюОпцию("ИспользоватьСервисСПАРКРиски");
	Элементы.ГруппаИндексыСПАРКРиски.Видимость = ИспользоватьСпаркРиски;
	
	Если ИспользоватьСпаркРиски Тогда
		
		// ИнтернетПоддержкаПользователей.СПАРКРиски
		ПараметрыПроцедуры = Новый Структура("ВариантОтображения", "Многострочный");
		ВидКонтрагентаСПАРКРиски = СПАРКРискиКлиентСерверУНФ.ВидКонтрагентаСПАРКРиски(Объект.ВидКонтрагента);
		СПАРКРиски.ПриСозданииНаСервере(
			ЭтотОбъект,
			Объект,
			Объект.Ссылка,
			ВидКонтрагентаСПАРКРиски,
			ПараметрыПроцедуры);
		// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
		
		// Команды1СПАРКРиски
		СПАРКРиски.ДобавитьПодключаемыеКомандыКонтрагента(ЭтотОбъект, Объект, Элементы.Подменю1СПАРКРиски);
		// Конец Команды1СПАРКРиски
		
	КонецЕсли;
	
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.СПАРКРиски

// Команды1СПАРКРиски
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду1СПАРКРиски(Команда)
	СПАРКРискиКлиент.ВыполнитьПодключаемуюКоманду(Команда, ЭтотОбъект, Объект);
КонецПроцедуры
// Конец Команды1СПАРКРиски

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура ПроверитьКонтрагента(Команда)
	УстановитьРеквизитыПроверкиКонтрагентов(ЭтотОбъект);
	ПроверкаКонтрагентовКлиент.ПроверитьКонтрагентаПоКнопке(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказатьПредложениеИспользоватьПроверкуКонтрагентов()
	ПроверкаКонтрагентовКлиент.ПредложитьВключитьПроверкуКонтрагентов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьРезультатПроверкиКонтрагентов()
	ПроверкаКонтрагентовКлиент.ОбработатьРезультатПроверкиКонтрагентовВСправочнике(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// ЭлектронноеВзаимодействие.ОбменСКонтрагентами

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()
	
	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКомандыЭДО()
	
	ОбменСКонтрагентамиКлиент.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры // Подключаемый_РедактироватьСоставСвойств()

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма, РеквизитФормыВЗначение("Объект"));
	
КонецПроцедуры // ОбновитьЭлементыДополнительныхРеквизитов()

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры // ОбновитьЗависимостиДополнительныхРеквизитов()

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры // Подключаемый_ПриИзмененииДополнительногоРеквизита()
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область Взаиморасчеты

&НаКлиенте
Процедура УстановитьЗаголовкиИВидимостьПриИзмененииФлагаРасчетовПоДоговорам()
	
	Если Объект.ВестиРасчетыПоДоговорам Тогда
		Элементы.ГруппаУсловияЗаполненияДокументовОплаты.Заголовок = НСтр("ru = 'Заполнение новых договоров'");
		Элементы.ГруппаУсловияЗаполненияДокументовОплаты.ОтображатьЗаголовок = Истина;
	Иначе
		Элементы.ГруппаУсловияЗаполненияДокументовОплаты.ОтображатьЗаголовок = Ложь;
	КонецЕсли;
	Элементы.ГруппаЗначенияДляДоговораПоУмолчанию.Видимость = Объект.ВестиРасчетыПоДоговорам;
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаРасчетовПоУмолчаниюПриИзменении(Элемент)
	
	Если Объект.ВалютаРасчетовПоУмолчанию = НациональнаяВалюта Тогда
		Объект.РасчетыВУсловныхЕдиницахПоУмолчанию = Ложь;
	КонецЕсли;
	УстановитьВидимостьФлагаВ_УЕ();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьФлагаВ_УЕ()
	
	Если Объект.ВалютаРасчетовПоУмолчанию = НациональнаяВалюта Тогда
		Элементы.РасчетыВУсловныхЕдиницахПоУмолчанию.Видимость = Ложь;
	Иначе
		Элементы.РасчетыВУсловныхЕдиницахПоУмолчанию.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КабинетКлиента

&НаСервере
Функция АдресQRКодаДляСкачивания()
	Возврат ПоместитьВоВременноеХранилище(БиблиотекаКартинок.QRКодКабинетКлиентаМЛК.ПолучитьДвоичныеДанные(), УникальныйИдентификатор);
КонецФункции

&НаСервере
Функция ПолучитьQRКод(КодКлиента = Неопределено)
	
	НастройкиПубликацииМЛК = Справочники.НастройкиПубликацииМЛК.ПолучитьНастройкиПубликацииМЛК();
	
	Если НастройкиПубликацииМЛК = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекущиеНастройкиКабинетКлиента = Справочники.НастройкиПубликацииМЛК.НастройкиИнтеграцииКабинетКлиента(НастройкиПубликацииМЛК);
	
	Если ТекущиеНастройкиКабинетКлиента = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	QRСтрока = "sbmcs" + ";" + ТекущиеНастройкиКабинетКлиента.АдресПриложения;
	Если КодКлиента <> Неопределено Тогда
		QRСтрока = QRСтрока + ";" + КодКлиента;
	КонецЕсли;
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(QRСтрока, 0, 190);
	QRКод = ПоместитьВоВременноеХранилище(ДанныеQRКода, УникальныйИдентификатор);
	
	Возврат QRКод
	
КонецФункции

&НаСервере
Функция ПолучитьСсылкуНаКонструктор()
	Возврат Справочники.НастройкиПубликацииМЛК.ПолучитьНастройкиПубликацииМЛК();
КонецФункции

&НаКлиенте
Процедура ДекорацияQRКодНаУстановкуНажатие(Элемент)
	НачатьПолучениеФайлаССервера(АдресQRКодаДляСкачивания(), НСтр("ru = 'QR-код для установки приложения.png'"));
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКодКлиентаQRКодНажатие(Элемент)
	НачатьПолучениеФайлаССервера(ПолучитьQRКод(Элементы.ДекорацияКодКлиента.Заголовок), СтрШаблон(НСтр("ru = 'QR-код клиента (%1).png'"),Объект.Наименование));
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКодКлиентаНажатие(Элемент)
	НастройкиПубликацииМЛККлиент.СкопироватьВБуферОбмена(Элементы.ДекорацияКодКлиента.Заголовок, НСтр("ru = 'Код клиента скопирован.'"));
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКодКомпанииQRКодНажатие(Элемент)
	НачатьПолучениеФайлаССервера(ПолучитьQRКод(), НСтр("ru = 'QR-код компании.png'"));
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКодКомпанииНажатие(Элемент)
	НастройкиПубликацииМЛККлиент.СкопироватьВБуферОбмена(Элементы.ДекорацияКодКомпании.Заголовок, НСтр("ru = 'Код компании скопирован.'"));
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияУстановитьИндивидуальныеЦеныНажатие(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Оповещение = Новый ОписаниеОповещения("ОбработатьВопросУстановитьИндивидуальныеЦены",ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные еще не записаны.'")
		+ Символы.ПС + НСтр("ru = 'Переход к установке индивидуальных цен возможен только после записи данных.'")
		+ Символы.ПС + НСтр("ru = 'Данные будут записаны'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Контрагент",Объект.Ссылка);
	ПараметрыФормы.Вставить("УстановитьИндивидуальныеЦены", Истина);
	ПараметрыФормы.Вставить("Ключ", ПолучитьСсылкуНаКонструктор());
	Оповещение = Новый ОписаниеОповещения("ОткрытьКонструкторЗавершение",ЭтотОбъект);
	ОткрытьФорму("Справочник.НастройкиПубликацииМЛК.Форма.КонструкторМобильногоПриложения", ПараметрыФормы,,,,,Оповещение);
	Оповестить("УстановитьИндивидуальныеЦены", Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура КабинетКлиентаОткрытьНастройкиНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Ключ", ПолучитьСсылкуНаКонструктор());
	Оповещение = Новый ОписаниеОповещения("ОткрытьКонструкторЗавершение",ЭтотОбъект);
	ОткрытьФорму("Справочник.НастройкиПубликацииМЛК.Форма.КонструкторМобильногоПриложения", ПараметрыФормы,,,,,Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВопросУстановитьИндивидуальныеЦены(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Ок Тогда
		
		Записать();
		
		Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Контрагент",Объект.Ссылка);
		ПараметрыФормы.Вставить("УстановитьИндивидуальныеЦены", Истина);
		ПараметрыФормы.Вставить("Ключ", ПолучитьСсылкуНаКонструктор());
		Оповещение = Новый ОписаниеОповещения("ОткрытьКонструкторЗавершение",ЭтотОбъект);
		ОткрытьФорму("Справочник.НастройкиПубликацииМЛК.Форма.КонструкторМобильногоПриложения", ПараметрыФормы,,,,,Оповещение);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонструкторЗавершение(Результат, ДополнительныеПараметры) Экспорт
	УстановитьОтображениеКабинетаКлиента();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКабинетКлиента(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Ключ", ПолучитьСсылкуНаКонструктор());
	Оповещение = Новый ОписаниеОповещения("ОткрытьКонструкторЗавершение",ЭтотОбъект);
	ОткрытьФорму("Справочник.НастройкиПубликацииМЛК.Форма.КонструкторМобильногоПриложения", ПараметрыФормы,,,,,Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаНаУстановкуНажатие(Элемент)
	НастройкиПубликацииМЛККлиент.СкопироватьВБуферОбмена("http://onelink.to/phnsu5", НСтр("ru = 'Ссылка на установку скопирована.'"));
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеКабинетаКлиента()
	
	Элементы.ГруппаКабинетКлиента.Видимость = Объект.Покупатель;
	Если НЕ Элементы.ГруппаКабинетКлиента.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоАдминистраторПрограммы = Пользователи.ЭтоПолноправныйПользователь(, Ложь, Ложь);
	Если Не ЭтоАдминистраторПрограммы Тогда
		Элементы.ГруппаКабинетКлиента.Видимость = Ложь;
		Элементы.КабинетКлиентаПодключен.Видимость = Ложь;
		Элементы.КабинетКлиентаНеПодключен.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	НастройкиПубликацииМЛК = Справочники.НастройкиПубликацииМЛК.ПолучитьНастройкиПубликацииМЛК();
	
	Если НастройкиПубликацииМЛК = Неопределено Тогда
		Элементы.КабинетКлиентаПодключитьПодключен.Заголовок = НСтр("ru = 'подключить'");
		Элементы.КабинетКлиентаПодключен.Видимость = Ложь;
		Элементы.КабинетКлиентаНеПодключен.Видимость = Истина;
		Возврат;
	КонецЕсли;
	
	НастройкиИнтеграцииКабинетКлиента = Справочники.НастройкиПубликацииМЛК.НастройкиИнтеграцииКабинетКлиента(НастройкиПубликацииМЛК);
	
	Если НастройкиИнтеграцииКабинетКлиента = Неопределено Тогда
		Элементы.КабинетКлиентаПодключитьПодключен.Заголовок = НСтр("ru = 'подключить'");
		Элементы.КабинетКлиентаПодключен.Видимость = Ложь;
		Элементы.КабинетКлиентаНеПодключен.Видимость = Истина;
		Возврат;
	КонецЕсли;
	
	КодКомпании = СтрЗаменить(НастройкиИнтеграцииКабинетКлиента.НомерОбласти, Символ(160), "");
	
	Если НастройкиИнтеграцииКабинетКлиента = Неопределено Тогда
		Элементы.КабинетКлиентаПодключитьПодключен.Заголовок = НСтр("ru = 'подключить'");
		Элементы.КабинетКлиентаПодключен.Видимость = Ложь;
		Элементы.КабинетКлиентаНеПодключен.Видимость = Истина;
		Возврат;
	КонецЕсли;
	
	Элементы.КабинетКлиентаПодключитьПодключен.Заголовок = НСтр("ru = 'подключен'");
	Элементы.КабинетКлиентаПодключен.Видимость = Истина;
	Элементы.КабинетКлиентаНеПодключен.Видимость = Ложь;
	
	РезультатЗапроса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкиПубликацииМЛК, "ИндивидуальныеЦены");
	
	Если ТипЗнч(РезультатЗапроса) = Тип("РезультатЗапроса") Тогда
		ИндивидуальныеЦены = РезультатЗапроса.Выгрузить();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Или ТипЗнч(ИндивидуальныеЦены) <> Тип("ТаблицаЗначений") Тогда
		Элементы.ИндивидуальныеЦеныЕсть.Видимость = Ложь;
		Элементы.ИндивидуальныеЦеныОтсутствуют.Видимость = Истина;
		Элементы.ДекорацияКодКомпании.Заголовок = КонструкторМобильногоПриложения.КодКлиентаКомпанииСМаской(КодКомпании);
		Возврат
	КонецЕсли;
	
	ИндивидуальныеЦеныНайденныеСтрокиМассив = ИндивидуальныеЦены.НайтиСтроки(Новый Структура("Контрагент", Объект.Ссылка));
	
	Если ИндивидуальныеЦеныНайденныеСтрокиМассив.Количество() = 0 Тогда
		Элементы.ИндивидуальныеЦеныЕсть.Видимость = Ложь;
		Элементы.ИндивидуальныеЦеныОтсутствуют.Видимость = Истина;
		Элементы.ДекорацияКодКомпании.Заголовок = КонструкторМобильногоПриложения.КодКлиентаКомпанииСМаской(КодКомпании);
	Иначе
		Элементы.ИндивидуальныеЦеныОтсутствуют.Видимость = Ложь;
		Элементы.ИндивидуальныеЦеныЕсть.Видимость = Истина;
		НайденнаяСтрока = ИндивидуальныеЦеныНайденныеСтрокиМассив[0];
		Элементы.ДекорацияКодКлиента.Заголовок = КонструкторМобильногоПриложения.КодКлиентаКомпанииСМаской(НайденнаяСтрока.КодКлиента);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьГруппыКабинетКлиента()
	
	Элементы.ГруппаКабинетКлиента.Видимость = Объект.Покупатель И (Элементы.КабинетКлиентаПодключен.Видимость Или Элементы.КабинетКлиентаНеПодключен.Видимость);
	
КонецПроцедуры

#КонецОбласти

