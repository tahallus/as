
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ТекстФормулы", ТекстФормулы);
	Значения = Неопределено;
	
	ДоставкаСервер.ЗаполнитьДеревоПараметровРасчета(ПараметрыРасчетов, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия="Запись_ПараметрыРасчетовДоставки" Тогда
		ОбновитьДеревоПараметровРасчета();
	КонецЕсли; 	
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПараметрыРасчетовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	СтруктураОткрытия = Новый Структура;
	Если Копирование Тогда
		ТекущаяСтрока = Элемент.ТекущиеДанные;
		Если ТекущаяСтрока=Неопределено ИЛИ НЕ ЗначениеЗаполнено(ТекущаяСтрока.Параметр) Тогда
			Возврат;
		КонецЕсли;
		СтруктураОткрытия.Вставить("ЗначениеКопирования", ТекущаяСтрока.Параметр);
	КонецЕсли; 
	ОткрытьФорму("Справочник.ПараметрыРасчетовДоставки.ФормаОбъекта", СтруктураОткрытия, Элемент, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыРасчетовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбновитьДеревоПараметровРасчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыРасчетовПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыРасчетовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущаяСтрока = Элемент.ТекущиеДанные;
	Если ТекущаяСтрока=Неопределено ИЛИ ПустаяСтрока(ТекущаяСтрока.Идентификатор) Тогда
		Возврат
	КонецЕсли;
	Если ТекущаяСтрока.ПолучитьЭлементы().Количество()>0 Тогда
		Возврат
	КонецЕсли; 
	ВставитьТекстВФормулу(ТекущаяСтрока.Идентификатор);

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьПоказатель(Команда)
	
	СтруктураОткрытия = Новый Структура;
	ТекущаяСтрока = Элементы.ПараметрыРасчетов.ТекущиеДанные;
	Если ТекущаяСтрока=Неопределено ИЛИ НЕ ЗначениеЗаполнено(ТекущаяСтрока.Параметр) Тогда
		Возврат;
	КонецЕсли;
	СтруктураОткрытия.Вставить("Ключ", ТекущаяСтрока.Параметр);
	ОткрытьФорму("Справочник.ПараметрыРасчетовДоставки.ФормаОбъекта", СтруктураОткрытия, Элементы.ПараметрыРасчетов, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ТекстФормулы", УдалитьЛишниеСимволы(ТекстФормулы));
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры
 
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВставитьТекстВФормулу(Показатель)
	
	ТекстФормулы = ТекстФормулы + " [" + СокрЛП(Показатель) + "] ";
			
КонецПроцедуры // ВставитьТекстВФормулу()	

&НаКлиентеНаСервереБезКонтекста
Функция УдалитьЛишниеСимволы(Текст)
	
	Результат = Текст;
	Результат = СтрЗаменить(Результат, Символы.ПС, "");
	Результат = СокрЛП(Результат);
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОбновитьДеревоПараметровРасчета()
	
	ДоставкаСервер.ЗаполнитьДеревоПараметровРасчета(ПараметрыРасчетов, Истина);
	
КонецПроцедуры
 
#КонецОбласти
 