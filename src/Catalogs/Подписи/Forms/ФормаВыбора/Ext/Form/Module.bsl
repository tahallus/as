
#Область Служебные

&НаСервереБезКонтекста
Процедура ОтменаАннулированияПраваПодписиНаСервере(ПодписьОтветственного)
	
	Если НЕ ЗначениеЗаполнено(ПодписьОтветственного) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Попытка
		
		ПодписьОтветственногоОбъект = ПодписьОтветственного.ПолучитьОбъект();
		
		ПодписьОтветственногоОбъект.Заблокировать();
		ПодписьОтветственногоОбъект.ПравоОтозвано = Ложь;
		ПодписьОтветственногоОбъект.Записать();
		
	Исключение
		
		ВызватьИсключение НСтр("ru ='Не удалось занять для записи право подписи, возможно элемент используется другим пользователем.'");
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область Форма

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ФормаТолькоДействующиеПраваПодписи.Пометка = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура ПравоПодписиОтозваноПоОшибке(Команда)
	
	ДанныеТекущейСтроки = Элементы.Список.ТекущиеДанные;
	Если НЕ ДанныеТекущейСтроки = Неопределено
		И ДанныеТекущейСтроки.ПравоОтозвано Тогда
		
		ДополнительныеПараметры = Новый Структура("ПодписьОтветственного", ДанныеТекущейСтроки.Ссылка);
		
		ТекстВопроса = НСтр("ru ='Отменить аннулирование права подписи для %1?'");
		ТекстВопроса = СтрШаблон(ТекстВопроса, Строка(ДанныеТекущейСтроки.ФизическоеЛицо));
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтменаАннулированияПраваПодписи", ЭтотОбъект, ДополнительныеПараметры);
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменаАннулированияПраваПодписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ОтменаАннулированияПраваПодписиНаСервере(ДополнительныеПараметры.ПодписьОтветственного);
		
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоДействующиеПраваПодписи(Команда)
	
	Элементы.ФормаТолькоДействующиеПраваПодписи.Пометка = НЕ Элементы.ФормаТолькоДействующиеПраваПодписи.Пометка;
	
	Отборы = Список.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	Для каждого ЭлементОтбора Из Отборы Цикл
		
		Если ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
			
			ЭлементОтбора.Использование = Элементы.ФормаТолькоДействующиеПраваПодписи.Пометка;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти