
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// ТехнологияСервиса.ВыгрузкаЗагрузкаДанных

// Возвращает реквизиты справочника, которые образуют естественный ключ для элементов справочника.
//
// Возвращаемое значение:
//  Массив из Строка - имена реквизитов, образующих естественный ключ.
//
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("Наименование");
	
	Возврат Результат;
	
КонецФункции

// Конец ТехнологияСервиса.ВыгрузкаЗагрузкаДанных

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	ПредставленияОснованийУвольненияОбработкаПолученияДанныхВыбора("ОснованияУвольнения", ДанныеВыбора, Параметры, СтандартнаяОбработка);
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	УчетСтраховыхВзносов.ПредставленияОснованийУвольненияОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка);
КонецПроцедуры

Процедура ПредставленияОснованийУвольненияОбработкаПолученияДанныхВыбора(ИмяОбъекта, ДанныеВыбора, Параметры, СтандартнаяОбработка) 
	
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Текст", "%" + Параметры.СтрокаПоиска + "%");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОснованияУвольнения.Ссылка,
	|	ВЫБОР
	|		КОГДА Представления.Использовать
	|			ТОГДА Представления.Наименование
	|		ИНАЧЕ ОснованияУвольнения.Наименование
	|	КОНЕЦ КАК Наименование
	|ПОМЕСТИТЬ ВТНаименования
	|ИЗ
	|	#ОснованияУвольнения КАК ОснованияУвольнения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредставленияОснованийУвольнения КАК Представления
	|		ПО ОснованияУвольнения.Ссылка = Представления.Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Наименования.Ссылка,
	|	Наименования.Наименование
	|ИЗ
	|	ВТНаименования КАК Наименования
	|ГДЕ
	|	Наименования.Наименование ПОДОБНО &Текст";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ОснованияУвольнения", "Справочник." + ИмяОбъекта);
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.Наименование);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ПервоначальноеЗаполнениеИОбновлениеИнформационнойБазы

// Процедура выполняет первоначальное заполнение статей ТК - оснований увольнения.
Процедура НачальноеЗаполнение() Экспорт
	
	ЗагрузитьКлассификатор();
	
КонецПроцедуры

Процедура ЗагрузитьКлассификатор() Экспорт
	
	КлассификаторТаблица = ТаблицаКлассификатора();
	Для Каждого СтрокаКлассификатора Из КлассификаторТаблица Цикл
		ЭлементСправочникаПоОписанию(СтрокаКлассификатора);
	КонецЦикла;
	
КонецПроцедуры

Функция ТаблицаКлассификатора()
	
	КлассификаторXML = Справочники.ОснованияУвольнения.ПолучитьМакет("ОснованияУвольненияПоТКРФ").ПолучитьТекст();
	Возврат ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
КонецФункции

Функция ЭлементСправочникаПоОписанию(СтрокаКлассификатора)
	
	Если ЗначениеЗаполнено(СтрокаКлассификатора.ID) Тогда 
		
		СправочникСсылка = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ОснованияУвольнения." + СтрокаКлассификатора.ID);
		Если СправочникСсылка = Неопределено Тогда
			СправочникОбъект = Справочники.ОснованияУвольнения.СоздатьЭлемент();
			СправочникОбъект.ИмяПредопределенныхДанных = СтрокаКлассификатора.ID;
		Иначе
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		КонецЕсли;
		СправочникОбъект.Наименование = СтрокаКлассификатора.Title;
		СправочникОбъект.ТекстОснования = СтрокаКлассификатора.Reason;
		СправочникОбъект.Статья = СтрокаКлассификатора.Article;
		СправочникОбъект.Часть = СтрокаКлассификатора.Part;
		СправочникОбъект.Пункт = СтрокаКлассификатора.Paragraph;
		СправочникОбъект.Подпункт = СтрокаКлассификатора.Subparagraph;
		СправочникОбъект.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
		СправочникОбъект.Записать();
		СправочникСсылка = СправочникОбъект.Ссылка;
		
	Иначе
		
		// Поиск существующего элемента
		СправочникСсылка = Справочники.ОснованияУвольнения.НайтиПоНаименованию(СтрокаКлассификатора.Title);
		Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			// Если элемент не был найден, производится поиск по альтернативным наименованиям
			Если Не ПустаяСтрока(СтрокаКлассификатора.TitleAlternative) Тогда
				СправочникСсылка = Справочники.ОснованияУвольнения.НайтиПоНаименованию(СтрокаКлассификатора.TitleAlternative);
			КонецЕсли;
			
			// Если элемент не был найден, производится поиск по наименованиям, содержащим неточности.
			Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
				
				НаименованиеСОшибкой = НаименованияСтатейСОшибкой().Получить(СтрокаКлассификатора.Title);
				Если НаименованиеСОшибкой <> Неопределено Тогда
					СправочникСсылка = Справочники.ОснованияУвольнения.НайтиПоНаименованию(НаименованиеСОшибкой);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
			СправочникОбъект = Справочники.ОснованияУвольнения.СоздатьЭлемент();
		Иначе
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		КонецЕсли;
		
		СправочникОбъект.Наименование = СтрокаКлассификатора.Title;
		СправочникОбъект.ТекстОснования = СтрокаКлассификатора.Reason;
		СправочникОбъект.Статья = СтрокаКлассификатора.Article;
		СправочникОбъект.Часть = СтрокаКлассификатора.Part;
		СправочникОбъект.Пункт = СтрокаКлассификатора.Paragraph;
		СправочникОбъект.Подпункт = СтрокаКлассификатора.Subparagraph;
		СправочникОбъект.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
		СправочникОбъект.Записать();
		СправочникСсылка = СправочникОбъект.Ссылка;
		
	КонецЕсли;
	
	Возврат СправочникСсылка;
		
КонецФункции

Функция НаименованияСтатейСОшибкой()
	
	НаименованияСтатей = Новый Соответствие;
	
	НаименованияСтатей.Вставить("п. 1 ч. 1 ст. 327.6", "п. 1 ч.1 ст. 327.6");
	НаименованияСтатей.Вставить("п. 2 ч. 1 ст. 327.6", "п. 2 ч.1 ст. 327.6");
	НаименованияСтатей.Вставить("п. 3 ч. 1 ст. 327.6", "п. 3 ч.1 ст. 327.6");
	НаименованияСтатей.Вставить("п. 4 ч. 1 ст. 327.6", "п. 4 ч.1 ст. 327.6");
	НаименованияСтатей.Вставить("п. 5 ч. 1 ст. 327.6", "п. 5 ч.1 ст. 327.6");
	НаименованияСтатей.Вставить("п. 6 ч. 1 ст. 327.6", "п. 6 ч.1 ст. 327.6");
	НаименованияСтатей.Вставить("п. 7 ч. 1 ст. 327.6", "п. 7 ч.1 ст. 327.6");
	НаименованияСтатей.Вставить("п. 8 ч. 1 ст. 327.6", "п. 8 ч.1 ст. 327.6");
	НаименованияСтатей.Вставить("п. 9 ч. 1 ст. 327.6", "п. 9 ч.1 ст. 327.6");
	НаименованияСтатей.Вставить("п. 10 ч. 1 ст. 327.6", "п. 10 ч.1 ст. 327.6");
	НаименованияСтатей.Вставить("п. 11 ч. 1 ст. 327.6", "п. 11 ч.1 ст. 327.6");
	
	Возврат НаименованияСтатей;
	
КонецФункции

#КонецОбласти
#КонецОбласти

#КонецЕсли