
#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Параметры.Отбор.Свойство("Владелец", Номенклатура);
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		ПроверкаИспользованияКомплектаций();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// КомандыПечати
	ПечатьДокументовУНФ.УстановитьОтображениеПодменюПечати(Элементы.ПодменюПечать);
	// Конец КомандыПечати
	
	//УНФ.ОтборыСписка
	РаботаСОтборами.ОпределитьПорядокПредопределенныхОтборов(ЭтотОбъект);
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		РаботаСОтборами.ВосстановитьНастройкиОтборов(ЭтотОбъект, Список, , , , "ДляНоменклатуры", Ложь);
	Иначе
		РаботаСОтборами.ВосстановитьНастройкиОтборов(ЭтотОбъект, Список, , , , , Ложь);
	КонецЕсли; 
	//Конец УНФ.ОтборыСписка
	
	// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов
	Если Элементы.Найти("СписокГрупповоеИзменениеОбъектов") <> Неопределено Тогда
		
		МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.Номенклатура);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокГрупповоеИзменениеОбъектов", "Видимость", МожноРедактировать);
		
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов
	
	ЗаполнитьХарактеристики();
	ОбновитьПараметрыОтображения();
	
	УправлениеФормой(ЭтотОбъект);
	УстановитьОтборНедействительная(ЭтотОбъект);
	
	// МобильныйКлиент
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		ЭтоМобильныйКлиент = Истина;
	КонецЕсли;
	// Конец МобильныйКлиент
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НЕ ЗавершениеРаботы Тогда
		//УНФ.ОтборыСписка
		СохранитьНастройкиОтборов();
		//Конец УНФ.ОтборыСписка
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "КопированиеКомплектаций" И ЗначениеЗаполнено(Номенклатура) И Параметр = Номенклатура Тогда
		
		Элементы.Список.Обновить();
		
	ИначеЕсли ИмяСобытия = "КомплектацииНоменклатурыЗаписана" Тогда
		
		СтрокаСписка = Элементы.Список.ТекущиеДанные;
		Если СтрокаСписка <> Неопределено И Параметр.Ссылка = СтрокаСписка.Ссылка Тогда
			ОтразитьВозможностьУстановкиКомплектацииКакОсновной(Параметр.Недействителен);
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если НЕ ЭтоМобильныйКлиент Тогда
		
		// СтандартныеПодсистемы.ПодключаемыеКоманды
		ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
		// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
	КонецЕсли;
	
	ОтразитьВозможностьУстановкиКомплектацииКакОсновной();
	
КонецПроцедуры

&НаКлиенте
Процедура РежимПриИзменении(Элемент)
	
	Если Режим = 0 Тогда
		ТекущаяХарактеристика = Неопределено;
		УстановитьОтбор(Список.Отбор, "Характеристика", ТекущаяХарактеристика);
	ИначеЕсли Режим = 2 Тогда
		ТекущаяХарактеристика = ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка");
		УстановитьОтбор(Список.Отбор, "Характеристика", ТекущаяХарактеристика);
	ИначеЕсли Режим = 1 И Элементы.Характеристики.ТекущиеДанные <> Неопределено Тогда
		ТекущаяХарактеристика = Элементы.Характеристики.ТекущиеДанные.Характеристика;
		УстановитьОтбор(Список.Отбор, "Характеристика", ТекущаяХарактеристика);
	Иначе
		ТекущаяХарактеристика = Неопределено;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ХарактеристикиПриАктивизацииСтроки(Элемент)

	Если Элемент.ТекущиеДанные=Неопределено ИЛИ Элемент.ТекущиеДанные.Характеристика=ТекущаяХарактеристика Тогда
		Возврат;
	КонецЕсли; 
	
	ТекущаяХарактеристика = Элемент.ТекущиеДанные.Характеристика;
	УстановитьОтбор(Список.Отбор, "Характеристика", ТекущаяХарактеристика);	
	
КонецПроцедуры
 
&НаКлиенте
Процедура ХарактеристикиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтрокаТабличнойЧасти = Характеристики.НайтиПоИдентификатору(Строка);
	
	МассивКомплектаций = Новый Массив;
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("СправочникСсылка.КомплектацииНоменклатуры") Тогда
		МассивКомплектаций.Добавить(ПараметрыПеретаскивания.Значение);
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		Для каждого Комплектация Из ПараметрыПеретаскивания.Значение Цикл
			Если ТипЗнч(Комплектация) = Тип("СправочникСсылка.КомплектацииНоменклатуры") Тогда
				МассивКомплектаций.Добавить(Комплектация);
			КонецЕсли; 
		КонецЦикла;
	Иначе
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	Если МассивКомплектаций.Количество() = 0 Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	ИзменитьХарактеристикуКомплектаций(МассивКомплектаций, СтрокаТабличнойЧасти.Характеристика);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВладелецОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("Владелец", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;

	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОтборКатегорияПродукцииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("Владелец.КатегорияНоменклатуры", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;

КонецПроцедуры

&НаКлиенте
Процедура ОтборМатериалОперацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("Состав.Номенклатура", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИспользоватьКакОсновную(Команда)
	
	СтрокаСписка = Элементы.Список.ТекущиеДанные;
	Если СтрокаСписка = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	ИспользоватьКакОсновнуюСервер(СтрокаСписка.Владелец, СтрокаСписка.Характеристика, СтрокаСписка.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ИспользоватьКакОсновнуюСервер(Номенклатура, Характеристика, Комплектация)
	
	Справочники.КомплектацииНоменклатуры.ИзменитьПризнакОсновнаяКомплектация(Номенклатура, Характеристика, Комплектация); 
	
	Элементы.Список.Обновить();
	Элементы.Список.ТекущаяСтрока = Комплектация;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьНедействительную(Команда)
	
	Элементы.ФормаПоказыватьНедействительную.Пометка = Не Элементы.ФормаПоказыватьНедействительную.Пометка;
	
	УстановитьОтборНедействительная(ЭтотОбъект)
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьОт(Команда)
	
	СтруктураОткрытия = Новый Структура;
	СтруктураОткрытия.Вставить("Номенклатура", Номенклатура);
	СтруктураОткрытия.Вставить("КопироватьКомплектации", Истина);
	СтруктураОткрытия.Вставить("КопироватьИзВыбранных", Истина);
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаКопированияСвязаннойИнформации", СтруктураОткрытия, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьДругим(Команда)
	
	СтруктураОткрытия = Новый Структура;
	СтруктураОткрытия.Вставить("Номенклатура", Номенклатура);
	СтруктураОткрытия.Вставить("КопироватьКомплектации", Истина);
	СтруктураОткрытия.Вставить("КопироватьИзВыбранных", Ложь);
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаКопированияСвязаннойИнформации", СтруктураОткрытия, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	НовоеУсловноеОформление = Список.УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Недействителен", Истина, ВидСравненияКомпоновкиДанных.Равно);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекстаТабличнойЧасти); 

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	ОткрытаИзНоменклатуры = ЗначениеЗаполнено(Форма.Номенклатура);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"ГруппаКопирование", "Видимость", ОткрытаИзНоменклатуры);
	
	ДанныеМеток = Форма.ДанныеМеток;
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяПоляОтбора", "Владелец");
	МенятьОсновнуюКомплектацию = (ДанныеМеток.НайтиСтроки(СтруктураОтбора).Количество() > 0) ИЛИ ОткрытаИзНоменклатуры;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"ФормаИспользоватьКакОсновную", "Видимость", МенятьОсновнуюКомплектацию);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"СписокКонтекстноеМенюИспользоватьКакОсновную", "Видимость", МенятьОсновнуюКомплектацию);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"Основная", "Видимость", МенятьОсновнуюКомплектацию);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"ГруппаОтборПродукция", "Видимость", НЕ ОткрытаИзНоменклатуры);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"Владелец", "Видимость", НЕ ОткрытаИзНоменклатуры);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"ГруппаОтборВладелец", "Видимость", НЕ ОткрытаИзНоменклатуры);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"ГруппаОтборКатегорияПродукции", "Видимость", НЕ ОткрытаИзНоменклатуры);

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"Характеристика", "Видимость", Форма.ИспользуютсяХарактеристики И Форма.Режим = 0);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"Режим", "Видимость", Форма.ИспользуютсяХарактеристики);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,  
		"ГруппаХарактеристики", "Видимость", Форма.ИспользуютсяХарактеристики И Форма.Режим = 1);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"Отступ", "Видимость", НЕ ОткрытаИзНоменклатуры ИЛИ НЕ Форма.ИспользуютсяХарактеристики ИЛИ Форма.Режим <> 1);
	
КонецПроцедуры

&НаСервере
Процедура ПроверкаИспользованияКомплектаций()
	
	ИспользоватьПодсистемуПроизводство = Константы.ФункциональнаяОпцияИспользоватьПодсистемуПроизводство.Получить();
	ИспользоватьПодсистемуРаботы = Константы.ФункциональнаяОпцияИспользоватьПодсистемуРаботы.Получить();
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "ТипНоменклатуры, ЭтоНабор");

	Если НЕ ЗначениеЗаполнено(Номенклатура)
		ИЛИ ЗначенияРеквизитов.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Запас Тогда
		
		АвтоЗаголовок = Ложь;
		Заголовок = НСтр("ru = 'Варианты комплектации хранятся только для запасов'");
		Элементы.Список.ТолькоПросмотр = Истина;
		Элементы.ФильтрыНастройкиИДопИнфо.Доступность = Ложь;
		Элементы.ФормаИспользоватьКакОсновную.Видимость = Ложь;
		Элементы.ГруппаКопирование.Доступность = Ложь;
		
	// Наборы
	ИначеЕсли ЗначенияРеквизитов.ЭтоНабор Тогда
		
		АвтоЗаголовок = Ложь;
		Заголовок = НСтр("ru = 'Варианты комплектации недоступны для наборов'");
		Элементы.Список.ТолькоПросмотр = Истина;
		Элементы.ФильтрыНастройкиИДопИнфо.Доступность = Ложь;
		Элементы.ФормаИспользоватьКакОсновную.Видимость = Ложь;
		Элементы.ГруппаКопирование.Доступность = Ложь;
	
	// Конец Наборы
	ИначеЕсли НЕ ПравоДоступа("Добавление", Метаданные.Справочники.КомплектацииНоменклатуры) Тогда
		
		Элементы.ГруппаКопирование.Доступность = Ложь;
		
	КонецЕсли;
		
КонецПроцедуры
 
&НаСервере
Процедура ОбновитьПараметрыОтображения()
	
	ИспользуютсяХарактеристики = (Характеристики.Количество() > 0);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьХарактеристики()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Ссылка КАК Характеристика,
	|	ХарактеристикиНоменклатуры.Наименование КАК Наименование
	|ПОМЕСТИТЬ ТабХарактеристики
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|ГДЕ
	|	(ХарактеристикиНоменклатуры.Владелец = &Номенклатура
	|			ИЛИ ХарактеристикиНоменклатуры.Владелец = ВЫРАЗИТЬ(&Номенклатура КАК Справочник.Номенклатура).КатегорияНоменклатуры)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Характеристики.Характеристика КАК Характеристика,
	|	ЛОЖЬ КАК Устарела,
	|	Характеристики.Наименование КАК Представление,
	|	0 КАК Порядок
	|ИЗ
	|	ТабХарактеристики КАК Характеристики
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КомплектацииНоменклатуры.Характеристика,
	|	ИСТИНА,
	|	КомплектацииНоменклатуры.Характеристика.Наименование,
	|	1
	|ИЗ
	|	Справочник.КомплектацииНоменклатуры КАК КомплектацииНоменклатуры
	|ГДЕ
	|	КомплектацииНоменклатуры.Характеристика <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И КомплектацииНоменклатуры.Владелец = &Номенклатура
	|	И НЕ КомплектацииНоменклатуры.Характеристика В
	|				(ВЫБРАТЬ
	|					ТабХарактеристики.Характеристика
	|				ИЗ
	|					ТабХарактеристики)
	|
	|СГРУППИРОВАТЬ ПО
	|	КомплектацииНоменклатуры.Характеристика,
	|	КомплектацииНоменклатуры.Характеристика.Наименование
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Представление";
	Характеристики.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Для каждого СтрХарактеристика Из Характеристики Цикл
		Если НЕ СтрХарактеристика.Устарела Тогда
			Продолжить;
		КонецЕсли; 
		СтрХарактеристика.Представление = СтрШаблон(НСтр("ru = '%1 (устарела)'"), СтрХарактеристика.Представление);
	КонецЦикла; 
	
	ТекущаяХарактеристика = Неопределено;
	УстановитьОтбор(Список.Отбор, "Характеристика", Неопределено);
	Если Характеристики.Количество() > 0 Тогда
		Элементы.Характеристики.ТекущаяСтрока = Характеристики[0].ПолучитьИдентификатор();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтбор(ГруппаЭлементовОтбора, ПутьКДаннымПоля, Значение)
	
	НайденныйЭлемент = Неопределено;
	Поле = Новый ПолеКомпоновкиДанных(ПутьКДаннымПоля);
	Для каждого ЭлементОтбора Из ГруппаЭлементовОтбора.Элементы Цикл
		Если ЭлементОтбора.ЛевоеЗначение = Поле Тогда
			НайденныйЭлемент = ЭлементОтбора;
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	Если НайденныйЭлемент = Неопределено И Значение = Неопределено Тогда
		Возврат;
	ИначеЕсли НайденныйЭлемент = Неопределено Тогда
		Отбор = ГруппаЭлементовОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение  = Поле;
	Иначе
		Отбор = НайденныйЭлемент;
	КонецЕсли; 
	Отбор.Использование = (Значение <> Неопределено);
	Если ТипЗнч(Значение) = Тип("СписокЗначений") Тогда
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	Иначе
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли; 
	Отбор.ПравоеЗначение = Значение;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьХарактеристикуКомплектаций(МассивКомплектаций, Характеристика)

	НачатьТранзакцию();
	Попытка
		Для каждого Комплектация Из МассивКомплектаций Цикл
			КомплектацияОбъект = Комплектация.ПолучитьОбъект();
			КомплектацияОбъект.Заблокировать();
			КомплектацияОбъект.Характеристика = Характеристика;
			КомплектацияОбъект.Записать();
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
	    ОтменитьТранзакцию();
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось изменить характеристику спецификаций.'"));
	КонецПопытки; 
	
	Элементы.Список.Обновить();
	
КонецПроцедуры 

&НаКлиенте
Процедура ОтразитьВозможностьУстановкиКомплектацииКакОсновной(Недействителен = Неопределено)
	
	Если Недействителен = Неопределено Тогда
		СтрокаСписка = Элементы.Список.ТекущиеДанные;
		Если СтрокаСписка = Неопределено 
			ИЛИ НЕ СтрокаСписка.Свойство("Недействителен") Тогда
			Недействителен = Ложь;
		Иначе
			Недействителен = СтрокаСписка.Недействителен;
		КонецЕсли; 
	КонецЕсли;
	ДоступностьКнопки = НЕ Недействителен И НЕ Элементы.Список.ТолькоПросмотр;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаИспользоватьКакОсновную", "Доступность", ДоступностьКнопки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборНедействительная(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.Список,
		"Недействителен",
		Ложь,
		,
		,
		Не Форма.Элементы.ФормаПоказыватьНедействительную.Пометка);
	
КонецПроцедуры

#КонецОбласти

#Область ЗамерыПроизводительности

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("СозданиеФормыКомплектация");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("ОткрытиеФормыКомплектация");
	
КонецПроцедуры

#КонецОбласти

#Область Отборы

&НаСервере
Процедура УстановитьМеткуИОтборСписка(ИмяПоляОтбораСписка, ГруппаРодительМетки, ВыбранноеЗначение, ПредставлениеЗначения="")
	
	Если ПредставлениеЗначения = "" Тогда
		ПредставлениеЗначения = Строка(ВыбранноеЗначение);
	КонецЕсли; 
	
	РаботаСОтборами.ПрикрепитьМеткуОтбора(ЭтотОбъект, ИмяПоляОтбораСписка, ГруппаРодительМетки, ВыбранноеЗначение, ПредставлениеЗначения);
	РаботаСОтборами.УстановитьОтборСписка(ЭтотОбъект, Список, ИмяПоляОтбораСписка);
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_МеткаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	МеткаИД = Сред(Элемент.Имя, СтрДлина("Метка_") + 1);
	УдалитьМеткуОтбора(МеткаИД);

КонецПроцедуры

&НаСервере
Процедура УдалитьМеткуОтбора(МеткаИД)
	
	СтрокаМеток = ДанныеМеток[Число(МеткаИД)];
	ИмяПоляОтбора = СтрокаМеток.ИмяПоляОтбора;
	РаботаСОтборами.УдалитьМеткуОтбораСервер(ЭтотОбъект, Список, МеткаИД);
	
	Если ИмяПоляОтбора = "Владелец" Тогда
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиОтборов()
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		РаботаСОтборами.СохранитьНастройкиОтборов(ЭтотОбъект, , , "ДляНоменклатуры", Ложь);
	Иначе
		РаботаСОтборами.СохранитьНастройкиОтборов(ЭтотОбъект, , , , Ложь);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьРазвернутьПанельОтборов(Элемент)
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфо.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость);
		
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОтборы(Команда)
	
	ИмяРеквизитаСписка = "Список";
	ИмяТЧДанныеМеток = "ДанныеМеток";
	ИмяТЧДанныеОтборов = "ДанныеОтборов";
	ИмяГруппыОтборов = "ГруппаОтборы";
	ИмяПредопределенныеОтборыПоУмолчанию = "ПредопределенныеОтборыПоУмолчанию";
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяРеквизитаСписка", ИмяРеквизитаСписка);
	ДополнительныеПараметры.Вставить("ИмяТЧДанныеМеток", ИмяТЧДанныеМеток);
	ДополнительныеПараметры.Вставить("ИмяТЧДанныеОтборов", ИмяТЧДанныеОтборов);
	ДополнительныеПараметры.Вставить("ИмяГруппыОтборов", ИмяГруппыОтборов);
	ДополнительныеПараметры.Вставить("ИмяПредопределенныеОтборыПоУмолчанию", ИмяПредопределенныеОтборыПоУмолчанию);
	
	РаботаСОтборамиКлиент.НастроитьОтборыНажатие(ЭтотОбъект, ПараметрыОткрытияФормыСНастройкамиОтборов(ДополнительныеПараметры), ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыОткрытияФормыСНастройкамиОтборов(ДополнительныеПараметры)
	
	Возврат РаботаСОтборами.ПараметрыДляОткрытияФормыСНастройкамиОтборов(ЭтотОбъект, ДополнительныеПараметры);
	
КонецФункции

&НаКлиенте
Процедура НастройкаОтборовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаОтборовЗавершениеНаСервере(Результат.АдресВыбранныеОтборы, Результат.АдресУдаленныеОтборы, ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура НастройкаОтборовЗавершениеНаСервере(АдресВыбранныеОтборы, АдресУдаленныеОтборы, ДополнительныеПараметры)
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ИмяРеквизитаСписка = "Список";
		ИмяТЧДанныеМеток = "ДанныеМеток";
		ИмяТЧДанныеОтборов = "ДанныеОтборов";
	Иначе
		ИмяРеквизитаСписка = ДополнительныеПараметры.ИмяРеквизитаСписка;
		ИмяТЧДанныеМеток = ДополнительныеПараметры.ИмяТЧДанныеМеток;
		ИмяТЧДанныеОтборов = ДополнительныеПараметры.ИмяТЧДанныеОтборов;
	КонецЕсли;
	
	РаботаСОтборами.НастройкаОтборовЗавершение(ЭтотОбъект, АдресВыбранныеОтборы, АдресУдаленныеОтборы, ДополнительныеПараметры);
	
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_ОтборПриИзменении(Элемент)
	
	Подключаемый_ОтборПриИзмененииНаСервере(Элемент.Имя, Элемент.Родитель.Имя)
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ОтборПриИзмененииНаСервере(ЭлементИмя, ЭлементРодительИмя)
	
	РаботаСОтборами.Подключаемый_ОтборПриИзменении(ЭтотОбъект, ЭлементИмя, ЭлементРодительИмя);
	
КонецПроцедуры

// @skip-warning
&НаКлиенте
Процедура Подключаемый_ОтборОчистка(Элемент)
	
	Подключаемый_ОтборОчисткаНаСервере(Элемент.Имя, Элемент.Родитель.Имя)
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ОтборОчисткаНаСервере(ЭлементИмя, ЭлементРодительИмя)
	
	РаботаСОтборами.Подключаемый_ОтборОчистка(ЭтотОбъект, ЭлементИмя);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти 


