#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	// Биллинг
	ДоговорОбслуживанияНаправлениеДеятельности = Неопределено;
	
	Если Не ПравоДоступа("Чтение", Метаданные.Справочники.СтатьиДвиженияДенежныхСредств) Тогда
		СтатьяДДСПоУмолчанию = Неопределено;
	КонецЕсли;
	
	ЧисловойНомер = 0;
	НомерДоговора = "";
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ВидОперацииКомиссия = Ложь;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("ОснованиеЗаполнения") Тогда
		Если ДанныеЗаполнения.Свойство("ВидОперацииКомиссия") И ДанныеЗаполнения.ВидОперацииКомиссия Тогда
			ВидОперацииКомиссия = Истина;
		КонецЕсли;
		ДанныеЗаполнения = ДанныеЗаполнения.ОснованиеЗаполнения;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Контрагенты") Тогда
		// Программное заполнение по методу Заполнить()
		
		СтандартнаяОбработка = Ложь;
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Покупатель,Поставщик,ПрочиеОтношения");
		
		ОрганизацияПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.АвторизованныйПользователь(), "ОсновнаяОрганизация");
		Если Не ЗначениеЗаполнено(ОрганизацияПоУмолчанию) Тогда
			ОрганизацияПоУмолчанию = Справочники.Организации.ОрганизацияПоУмолчанию();
		КонецЕсли;
		
		Если НЕ ДанныеЗаполнения.ВалютаРасчетовПоУмолчанию.Пустая() И ДанныеЗаполнения.ВестиРасчетыПоДоговорам Тогда
			ВалютаРасчетов		= ДанныеЗаполнения.ВалютаРасчетовПоУмолчанию;
			РасчетыВУсловныхЕдиницах = ДанныеЗаполнения.РасчетыВУсловныхЕдиницахПоУмолчанию;
		Иначе
			ВалютаРасчетов		= Константы.НациональнаяВалюта.Получить();
		КонецЕсли;
		
		Организация			= ОрганизацияПоУмолчанию;
		ВидЦен				= Справочники.ВидыЦен.ПолучитьОсновнойВидЦенПродажи();
		Владелец			= ДанныеЗаполнения;
		СрокОплатыПоставщику = Константы.СрокОплатыПоставщику.Получить();
		СрокОплатыПокупателя = Константы.СрокОплатыПокупателя.Получить();
		ВидДоговора			= ?(ВидОперацииКомиссия, Перечисления.ВидыДоговоров.СКомиссионером, Перечисления.ВидыДоговоров.СПокупателем);
		Если ЗначенияРеквизитов.Поставщик И Не ЗначенияРеквизитов.Покупатель Тогда
			ВидДоговора		= Перечисления.ВидыДоговоров.СПоставщиком;
		ИначеЕсли ЗначенияРеквизитов.ПрочиеОтношения И Не ЗначенияРеквизитов.Покупатель И Не ЗначенияРеквизитов.Поставщик Тогда
			ВидДоговора		= Перечисления.ВидыДоговоров.Прочее;
		КонецЕсли;
		
		СпособРазнесенияОплатыПоУмолчанию = ДанныеЗаполнения.СпособРазнесенияОплатыПоУмолчанию;
		СпособЗачетаПредоплатыПоУмолчанию = ДанныеЗаполнения.СпособЗачетаПредоплатыПоУмолчанию;
		
		// Заполним вид цен контрагента
		Если ПолучитьФункциональнуюОпцию("УчетЦенКонтрагентов") И ДанныеЗаполнения.Поставщик Тогда
			
			УстановитьПривилегированныйРежим(Истина);
			
			НовыйВидЦенКонтрагентов = Справочники.ВидыЦенКонтрагентов.ВидЦенКонтрагентаПоУмолчанию(ДанныеЗаполнения);
			Если НЕ ЗначениеЗаполнено(НовыйВидЦенКонтрагентов) Тогда 
				
				НовыйВидЦенКонтрагентов = Справочники.ВидыЦенКонтрагентов.НайтиЛюбойПервыйВидЦенКонтрагента(ДанныеЗаполнения);
				
				Если НЕ ЗначениеЗаполнено(НовыйВидЦенКонтрагентов) Тогда
					
					НовыйВидЦенКонтрагентов = Справочники.ВидыЦенКонтрагентов.СоздатьВидЦенКонтрагента(ДанныеЗаполнения, ВалютаРасчетов);
					
				КонецЕсли;
				
			КонецЕсли;
			
			УстановитьПривилегированныйРежим(Ложь);
			
			ВидЦенКонтрагента = НовыйВидЦенКонтрагентов;
			
		КонецЕсли;
		
		// Расчеты в валюте
		Если ДанныеЗаполнения.ВестиРасчетыПоДоговорам И НЕ ДанныеЗаполнения.ВалютаРасчетовПоУмолчанию.Пустая() Тогда
			ВалютаРасчетов		= ДанныеЗаполнения.ВалютаРасчетовПоУмолчанию;
			РасчетыВУсловныхЕдиницах = ДанныеЗаполнения.РасчетыВУсловныхЕдиницахПоУмолчанию;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("ОрганизацияВЭквайринговомТерминале") Тогда
			Если ЗначениеЗаполнено(ДанныеЗаполнения.ОрганизацияВЭквайринговомТерминале) Тогда
				Организация = ДанныеЗаполнения.ОрганизацияВЭквайринговомТерминале;
			КонецЕсли;
			ВидДоговора = Перечисления.ВидыДоговоров.Прочее;
		ИначеЕсли ДанныеЗаполнения.Свойство("Организация") Тогда
			Если ЗначениеЗаполнено(ДанныеЗаполнения.Организация) Тогда
				Организация = ДанныеЗаполнения.Организация;
			КонецЕсли;
		ИначеЕсли ДанныеЗаполнения.Свойство("КатегорияДоговора") Тогда
			Если ЗначениеЗаполнено(ДанныеЗаполнения.КатегорияДоговора) Тогда
				КатегорияДоговора = ДанныеЗаполнения.КатегорияДоговора;
			КонецЕсли;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Владелец") Тогда
			Если ТипЗнч(ДанныеЗаполнения.Владелец) = Тип("СправочникСсылка.Организации") Тогда
					ВалютаРасчетов		= Константы.ВалютаУчета.Получить();
					РасчетыВУсловныхЕдиницах = Ложь;
			Иначе
				Если ДанныеЗаполнения.Владелец.ВестиРасчетыПоДоговорам И НЕ ДанныеЗаполнения.Владелец.ВалютаРасчетовПоУмолчанию.Пустая() Тогда
					ВалютаРасчетов		= ДанныеЗаполнения.Владелец.ВалютаРасчетовПоУмолчанию;
					РасчетыВУсловныхЕдиницах = ДанныеЗаполнения.Владелец.РасчетыВУсловныхЕдиницахПоУмолчанию;
				КонецЕсли;
				
				СпособРазнесенияОплатыПоУмолчанию = ДанныеЗаполнения.Владелец.СпособРазнесенияОплатыПоУмолчанию;
				СпособЗачетаПредоплатыПоУмолчанию = ДанныеЗаполнения.Владелец.СпособЗачетаПредоплатыПоУмолчанию;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	АвтоматическиРегистрироватьЦены = ЗначениеЗаполнено(ВидЦенКонтрагента);
	
	// Автонумерация
	СтруктураНумерация = Нумерация.АктуальнаяНастройкаНумерации(ЭтотОбъект);
	
	Если СтруктураНумерация <> Неопределено 
		И Не СтруктураНумерация.РучнаяНумерация Тогда
		ПараметрыФормирования = Новый Структура;
		ПараметрыФормирования.Вставить("НастройкиНумерации", СтруктураНумерация.Ссылка);
		ПараметрыФормирования.Вставить("Контрагент", Владелец);
		ПараметрыФормирования.Вставить("ДатаДоговора", ?(ЗначениеЗаполнено(ДатаДоговора), ДатаДоговора, ТекущаяДатаСеанса()));
		
		Нумерация.СформироватьЧисловойНомерДокумента(ПараметрыФормирования, ЧисловойНомер);
		
		ПараметрыФормирования.Вставить("ЧисловойНомер", ЧисловойНомер);
		ПараметрыФормирования.Вставить("КатегорияДоговора", КатегорияДоговора);
		
		ОписанияОшибок = Новый СписокЗначений;
		Нумерация.СформироватьСтроковыйНомерДокумента(ПараметрыФормирования, НомерДоговора, ОписанияОшибок);
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Контрагенты") И 
		ПустаяСтрока(Наименование) Тогда
		Наименование = ШаблоныНаименований.НаименованиеДоговораПоУмолчанию(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Вид цен.
	Если ЗначениеЗаполнено(ВидСкидкиНаценки) Тогда
		ПроверяемыеРеквизиты.Добавить("ВидЦен");
	КонецЕсли;
	
	// Биллинг
	Если ЭтоДоговорОбслуживания Тогда
		ПроверяемыеРеквизиты.Добавить("ВидЦен");
		ПроверяемыеРеквизиты.Добавить("ДоговорОбслуживанияДатаНачала");
		ПроверяемыеРеквизиты.Добавить("ДоговорОбслуживанияТарифныйПлан");
		ПроверяемыеРеквизиты.Добавить("ДоговорОбслуживанияПериодичность");
	КонецЕсли;
	
	Если ВидДоговора = Перечисления.ВидыДоговоров.СКомитентом Тогда
		
		ЭтоБанковскийПлатежныйАгент = ПризнакАгента = Перечисления.ПризнакиАгента.БанковскийПлатежныйАгент 
								ИЛИ ПризнакАгента = Перечисления.ПризнакиАгента.БанковскийПлатежныйСубагент;
		
		ЭтоПлатежныйАгент = ПризнакАгента = Перечисления.ПризнакиАгента.ПлатежныйАгент
						ИЛИ ПризнакАгента = Перечисления.ПризнакиАгента.ПлатежныйСубагент;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьАгентскиеПлатежиИРазделениеВыручки") 
			ИЛИ (ПолучитьФункциональнуюОпцию("АгентскиеУслуги") И ПолучитьФункциональнуюОпцию("ПриемТоваровНаКомиссию")) Тогда
			ПроверяемыеРеквизиты.Добавить("ПризнакАгента");
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьАгентскиеПлатежиИРазделениеВыручки") И АвтоматическиВыделятьВознаграждениеВЧеке Тогда
			ПроверяемыеРеквизиты.Добавить("ПроцентКомиссионногоВознаграждения");
		КонецЕсли;
		
		Если АвтоматическиВыделятьВознаграждениеВЧеке Тогда
			ПроверяемыеРеквизиты.Добавить("УслугаКомиссионногоВознаграждения");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(УслугаКомиссионногоВознаграждения) И НЕ ЭтоПлатежныйАгент И НЕ ЭтоБанковскийПлатежныйАгент Тогда
			ПроверяемыеРеквизиты.Добавить("ДоговорУслугиКомиссионногоВознаграждения");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
		РегистрыСведений.ОсновныеДоговорыКонтрагента.ОчиститьЗаписиСДоговором(Ссылка);
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка <> &Ссылка
	|	И ДоговорыКонтрагентов.Владелец = &Владелец
	|	И НЕ ДоговорыКонтрагентов.Владелец.ВестиРасчетыПоДоговорам");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ТекстСообщения = НСтр("ru = 'Для контрагента не ведется учет по договорам.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , , Отказ);
	КонецЕсли;
	
	// Биллинг
	Если ЭтоДоговорОбслуживания И ЭтоНовый()
		ИЛИ ЭтоДоговорОбслуживания И НЕ ЭтоНовый() И НЕ Ссылка.ЭтоДоговорОбслуживания Тогда
		
		ИспользоватьНаправленияДеятельности = Константы.БиллингВестиУчетРасходовПоДоговорамОбслуживания.Получить();
		Если ИспользоватьНаправленияДеятельности Тогда
			ДоговорОбслуживанияНаправлениеДеятельности = Справочники.ДоговорыКонтрагентов.СоздатьНаправлениеДеятельностиДляДоговораОбслуживания(
				Владелец, Наименование);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ДополнительныеСвойства.Вставить("ПометкаУдаления", Ссылка.ПометкаУдаления);
	КонецЕсли;
	
	// Интеркампани
	ЭтоДоговорПередачиТоваровМеждуОрганизациями = (ЗначениеЗаполнено(Владелец) 
		И ТипЗнч(Владелец) = Тип("СправочникСсылка.Организации"));
		
	Если ВидДоговора = Перечисления.ВидыДоговоров.СКомитентом И НЕ ЗначениеЗаполнено(ПризнакАгента) Тогда
		ИспользуютсяАгентскиеУслуги = ПолучитьФункциональнуюОпцию("АгентскиеУслуги");
		ИспользуетсяКомиссионнаяТорговля = ПолучитьФункциональнуюОпцию("ПриемТоваровНаКомиссию");
		Если ИспользуютсяАгентскиеУслуги И НЕ ИспользуетсяКомиссионнаяТорговля Тогда
			ПризнакАгента = Перечисления.ПризнакиАгента.Агент;
		КонецЕсли;
		Если ИспользуетсяКомиссионнаяТорговля И НЕ ИспользуютсяАгентскиеУслуги Тогда
			ПризнакАгента = Перечисления.ПризнакиАгента.Комиссионер;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Биллинг
	Если ЭтоДоговорОбслуживания Тогда
		Если ЗначениеЗаполнено(ДоговорОбслуживанияНаправлениеДеятельности) Тогда
			// Если договор помечается (снимается пометка) на удаление, так же помечаем и связанное с ним направление деятельности.
			Если ДополнительныеСвойства.Свойство("ПометкаУдаления") Тогда
				Если ДополнительныеСвойства.ПометкаУдаления = Ложь И ПометкаУдаления = Истина
					ИЛИ ДополнительныеСвойства.ПометкаУдаления = Истина И ПометкаУдаления = Ложь Тогда
					
					НаправлениеДеятельностиОбъект = ДоговорОбслуживанияНаправлениеДеятельности.ПолучитьОбъект();
					НаправлениеДеятельностиОбъект.ПометкаУдаления = ПометкаУдаления;
					НаправлениеДеятельностиОбъект.Записать();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Эквайринг
	Если ЭтоДоговорЭквайринга ИЛИ ЭтоДоговорКредита Тогда
		Если КонтрольВзаиморасчетовЭквайринг Тогда
			Если НЕ ПолучитьФункциональнуюОпцию("РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям") Тогда
				УстановитьПривилегированныйРежим(Истина);
				Константы.ФункциональнаяОпцияРазноситьОплатуОтЭквайрераПоЭквайринговымОперациям.Установить(Истина);
				УстановитьПривилегированныйРежим(Ложь);
			КонецЕсли; 
		Иначе
			Если НЕ ЭквайринговыеОперацииСервер.ЕстьДоговорыЭквайрингаСРазнесениемОплат() И ПолучитьФункциональнуюОпцию("РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям") Тогда
				УстановитьПривилегированныйРежим(Истина);
				Константы.ФункциональнаяОпцияРазноситьОплатуОтЭквайрераПоЭквайринговымОперациям.Установить(Ложь);
				УстановитьПривилегированныйРежим(Ложь);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
	// Конец Эквайринг
	
	// Кредит
	Если ЭтоДоговорКредита И ПометкаУдаления Тогда
		Справочники.ЭквайринговыеТерминалы.ПометитьНаУдалениеТерминалыСДоговором(Ссылка);
	КонецЕсли;
	// Конец Кредит
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЧисловойНомер > 0 Тогда
		СтруктураНумерация = Нумерация.АктуальнаяНастройкаНумерации(ЭтотОбъект);
		Если СтруктураНумерация <> Неопределено Тогда
			СтруктураПараметров = НумерацияКлиентСервер.ПолучитьПараметрыНумерации(ЭтотОбъект);
			
			СтруктураПараметров.Вставить("НастройкиНумерации", СтруктураНумерация.Ссылка);
			Нумерация.ОсвободитьНомер(СтруктураПараметров);
		КонецЕсли;
	КонецЕсли;
	РегистрыСведений.ОсновныеДоговорыКонтрагента.ОчиститьЗаписиСДоговором(Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьНаименование() Экспорт
	
	Если Не ПустаяСтрока(НомерДоговора) Тогда
		Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '№ %1 %2 (%3)'"),
			СокрЛП(НомерДоговора),
			?(ЗначениеЗаполнено(ДатаДоговора), "от " + Формат(ДатаДоговора, "ДЛФ=D"), ""),
			Строка(ВалютаРасчетов));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли