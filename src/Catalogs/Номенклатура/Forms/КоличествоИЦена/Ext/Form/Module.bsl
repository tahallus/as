
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура УстановитьДоступность(Параметры)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Количество", "Доступность", ЗапрашиватьКоличество И Не ПодбиратьВесьОстаток);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПодбиратьВесьОстатокПереключатель", "Доступность", Параметры.ЗапрашиватьКоличество);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЕдиницаИзмерения", "Доступность", Параметры.ЗапрашиватьЦену);
	
	НациональнаяВалюта = Константы.НациональнаяВалюта.Получить();
	ВидимостьВалюты = ?(ЗначениеЗаполнено(Параметры.ВалютаПодбора) И Не НациональнаяВалюта = Параметры.ВалютаПодбора, Истина, Ложь);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияВалюта", "Видимость", ВидимостьВалюты);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияВалюта1", "Видимость", ВидимостьВалюты);
	
	Если УправлениеДоступомУНФ.РазрешеноРедактированиеЦенДокументов() И Параметры.ЗапрашиватьЦену Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Цена", "Доступность", Истина);
		
	КонецЕсли;
	
	// Если запрашиваем только цену, сразу позиционируем фокус на реквизите Цена
	Если НЕ Параметры.ЗапрашиватьКоличество
		И Параметры.ЗапрашиватьЦену Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.Цена;
	КонецЕсли;
	
	Валюта = Параметры.ВалютаПодбора;
	ИспользоватьХарактеристики = Номенклатура.ИспользоватьХарактеристики;
	ИспользоватьПартии = Номенклатура.ИспользоватьПартии;
	ПроверятьЗаполнениеПартий = Номенклатура.ПроверятьЗаполнениеПартий;
	ПроверятьЗаполнениеХарактеристики = Номенклатура.ПроверятьЗаполнениеХарактеристики;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Количество			= Параметры.Количество;
	ЕдиницаИзмерения	= Параметры.ЕдиницаИзмерения;
	Цена				= Параметры.Цена;
	Номенклатура 		= Параметры.Номенклатура;
	Характеристика		= Параметры.Характеристика;
	Партия				= Параметры.Партия;
	СтруктурнаяЕдинца	= Параметры.СтруктурнаяЕдиница;
	Ячейка				= Параметры.Ячейка;
	ТипНоменклатуры 	= Параметры.ТипНоменклатуры;
	ЭтоНабор = Параметры.ЭтоНабор;
	СтавкаНДС = Параметры.СтавкаНДС;
	ПодбиратьВесьОстаток = Параметры.ПодбиратьВесьОстаток;
	КоличествоОстаток = Параметры.КоличествоОстаток;
	
	ЗапрашиватьКоличество = Параметры.ЗапрашиватьКоличество;
	
	Элементы.ДекорацияВалюта.Заголовок = Параметры.ВалютаПодбора.СимвольноеПредставление;
	Элементы.ДекорацияВалюта1.Заголовок = Параметры.ВалютаПодбора.СимвольноеПредставление;
	
	УстановитьДоступность(Параметры);
		
	ЭтаФорма.Заголовок = Номенклатура.Наименование;
	
	Элементы.ПодбиратьВесьОстатокПереключатель.СписокВыбора[1].Представление = СтрШаблон(НСтр("ru = 'Свободно (%1)'"), Строка(Параметры.КоличествоОстаток));
	
	Если ПодбиратьВесьОстаток Тогда
		ПодбиратьВесьОстатокПереключатель = 1;
		Количество = КоличествоОстаток;
	КонецЕсли;
	
	Элементы.ДекорацияСумма.Заголовок = ПодборНоменклатурыВДокументахКлиентСервер.СуммаФорматированнойСтрокой(Количество * Цена);
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	СтруктураЗакрытия = Новый Структура;
	
	СтруктураЗакрытия.Вставить("Количество", Количество);
	СтруктураЗакрытия.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	СтруктураЗакрытия.Вставить("Цена", Цена);
	СтруктураЗакрытия.Вставить("Номенклатура", Номенклатура);
	СтруктураЗакрытия.Вставить("Характеристика", Характеристика);
	СтруктураЗакрытия.Вставить("Партия", Партия);
	СтруктураЗакрытия.Вставить("СтруктурнаяЕдиница", СтруктурнаяЕдинца);
	СтруктураЗакрытия.Вставить("Ячейка", Ячейка);
	СтруктураЗакрытия.Вставить("ТипНоменклатуры", ТипНоменклатуры);
	СтруктураЗакрытия.Вставить("ЭтоНабор", ЭтоНабор);
	СтруктураЗакрытия.Вставить("Валюта", Валюта);
	
	СтруктураЗакрытия.Вставить("ИспользоватьХарактеристики", ИспользоватьХарактеристики);
	СтруктураЗакрытия.Вставить("ИспользоватьПартии", ИспользоватьПартии);
	СтруктураЗакрытия.Вставить("ПроверятьЗаполнениеПартий", ПроверятьЗаполнениеПартий);
	СтруктураЗакрытия.Вставить("ПроверятьЗаполнениеХарактеристики", ПроверятьЗаполнениеХарактеристики);
	
	СтруктураЗакрытия.Вставить("Остаток", КоличествоОстаток);
	
	СтруктураЗакрытия.Вставить("СтавкаНДС", СтавкаНДС);
	
	СтруктураЗакрытия.Вставить("ПодбиратьВесьОстаток", ПодбиратьВесьОстаток);
	
	Закрыть(СтруктураЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	
	ОбновитьНадписьИтоговаяСумма();
	
КонецПроцедуры

&НаКлиенте
Процедура ЦенаПриИзменении(Элемент)
	
	ОбновитьНадписьИтоговаяСумма();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЕдиницаИзмерения = ВыбранноеЗначение 
		ИЛИ Цена = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийКоэффициент = 0;
	Если ТипЗнч(ЕдиницаИзмерения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
		ТекущийКоэффициент = 1;
	КонецЕсли;
	
	Коэффициент = 0;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
		Коэффициент = 1;
	КонецЕсли;
	
	Если ТекущийКоэффициент = 0 И Коэффициент = 0 Тогда
		
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(ЕдиницаИзмерения, ВыбранноеЗначение);
		
	ИначеЕсли ТекущийКоэффициент = 0 Тогда
		
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(ЕдиницаИзмерения);
		
	ИначеЕсли Коэффициент = 0 Тогда
		
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(,ВыбранноеЗначение);
		
	ИначеЕсли ТекущийКоэффициент = 1 И Коэффициент = 1 Тогда
		
		СтруктураДанные = Новый Структура("ТекущийКоэффициент, Коэффициент", 1, 1);
		
	КонецЕсли;
	
	Если СтруктураДанные.ТекущийКоэффициент <> 0 Тогда
		
		Цена = Цена * СтруктураДанные.Коэффициент / СтруктураДанные.ТекущийКоэффициент;
		
	КонецЕсли;
	
	ОбновитьНадписьИтоговаяСумма();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодбиратьВесьОстатокПереключательПриИзменении(Элемент)
	
	ПодбиратьВесьОстаток = ?(ПодбиратьВесьОстатокПереключатель = 1, Истина, Ложь);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Количество", "Доступность", ЗапрашиватьКоличество И Не ПодбиратьВесьОстаток);
	
	Если ПодбиратьВесьОстаток Тогда
		Количество = КоличествоОстаток;
	КонецЕсли;
	
	Элементы.ДекорацияСумма.Заголовок = ПодборНоменклатурыВДокументахКлиентСервер.СуммаФорматированнойСтрокой(Количество * Цена);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеОбработчики

&НаКлиенте
Процедура ОбновитьНадписьИтоговаяСумма()
	
	Элементы.ДекорацияСумма.Заголовок = 
	ПодборНоменклатурыВДокументахКлиентСервер.СуммаФорматированнойСтрокой(Количество * Цена);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеЕдиницаИзмеренияПриИзменении(ТекущаяЕдиницаИзмерения = Неопределено, ЕдиницаИзмерения = Неопределено)
	
	СтруктураДанные = Новый Структура();
	
	Если ТекущаяЕдиницаИзмерения = Неопределено Тогда
		СтруктураДанные.Вставить("ТекущийКоэффициент", 1);
	Иначе
		СтруктураДанные.Вставить("ТекущийКоэффициент", ТекущаяЕдиницаИзмерения.Коэффициент);
	КонецЕсли;
		
	Если ЕдиницаИзмерения = Неопределено Тогда
		СтруктураДанные.Вставить("Коэффициент", 1);
	Иначе
		СтруктураДанные.Вставить("Коэффициент", ЕдиницаИзмерения.Коэффициент);
	КонецЕсли;
	
	Возврат СтруктураДанные;
	
КонецФункции

#КонецОбласти