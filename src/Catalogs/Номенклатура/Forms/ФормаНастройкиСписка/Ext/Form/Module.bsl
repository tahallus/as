
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.СтруктураНастроек);
	
	Если Параметры.СтруктураНастроек.РежимВыбора Тогда
		Элементы.Корзину.Видимость = Ложь;
		Элементы.ГруппаЗапрашивать.Видимость = Ложь;
		Элементы.ДействиеПриПоискеПоШК.СписокВыбора[2].Представление = НСтр("ru = 'добавлять в документ'");
	КонецЕсли;
	
	Если Параметры.СтруктураНастроек.ЭтоПодбор Тогда
		
		Элементы.Корзину.Видимость = Ложь;
		ПоказыватьКорзину = Истина;
		
	КонецЕсли; 
	
	Если Параметры.СтруктураНастроек.Свойство("ИспользоватьМинимальныеЦены")
		И НЕ Параметры.СтруктураНастроек.ИспользоватьМинимальныеЦены Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПоказыватьМинимальныеЦены", "Видимость", Ложь);
		ПоказыватьМинимальныеЦены = Ложь;
		
	КонецЕсли;
	
	Элементы.ГруппаСопутствущиеТовары.Видимость = ПоказыватьСопутствующиеТовары;
	Элементы.Разделитель2.Видимость = Элементы.ГруппаЗапрашивать.Видимость;
	
	Элементы.ГруппаКакИскатьВертикальная.Видимость = ?(НастройкиПоиска = 1, Истина, Ложь);
	Элементы.ОтступГруппыПоиска.Видимость = ?(НастройкиПоиска = 1, Истина, Ложь);
	
	ИспользоватьПолнотекстовыйПоиск = Константы.ИспользоватьПолнотекстовыйПоиск.Получить()
									И ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить;
	
	Если Не ИспользоватьПолнотекстовыйПоиск Тогда
		НастройкиПоискаКакИскать = 1;
		Элементы.НастройкиПоискаКакИскать.Видимость = Ложь;
		Элементы.ОтступГруппыПоиска.Видимость = Ложь;
	КонецЕсли;
	
	Если НастройкиПоискаКакИскать = 0 Тогда
		Элементы.ПоискВДопРеквизитахИСведениях.Доступность = Ложь;
		ПоискВДопРеквизитахИСведениях = Истина;
	Иначе
		Элементы.ПоискВДопРеквизитахИСведениях.Доступность = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	СтруктураНастроек = Новый Структура();
	СтруктураНастроек.Вставить("ПоказыватьОстатки", ПоказыватьОстатки);
	СтруктураНастроек.Вставить("ПоказыватьЦены", ПоказыватьЦены);
	СтруктураНастроек.Вставить("ПоказыватьКартинку", ПоказыватьКартинку);
	СтруктураНастроек.Вставить("ПоказыватьКорзину", ПоказыватьКорзину);
	СтруктураНастроек.Вставить("ЗапрашиватьКоличество", ЗапрашиватьКоличество);
	СтруктураНастроек.Вставить("ЗапрашиватьЦену", ЗапрашиватьЦену);
	СтруктураНастроек.Вставить("БыстрыеТовары", БыстрыеТовары);
	СтруктураНастроек.Вставить("СравниватьНоменклатуруПоСвойствам", СравниватьНоменклатуруПоСвойствам);
	СтруктураНастроек.Вставить("ПоказыватьАналоги", ПоказыватьАналоги);
	СтруктураНастроек.Вставить("ПоказыватьНеПрямыеАналоги", ПоказыватьНеПрямыеАналоги);
	СтруктураНастроек.Вставить("ПоказыватьСопутствующиеТовары", ПоказыватьСопутствующиеТовары);
	СтруктураНастроек.Вставить("ВыводСопутствующихТоваров", ВыводСопутствующихТоваров);
	СтруктураНастроек.Вставить("ЦенаВСписке", ЦенаВСписке);
	СтруктураНастроек.Вставить("ДействиеПриПоискеПоШК", ДействиеПриПоискеПоШК);
	СтруктураНастроек.Вставить("ПоказыватьМинимальныеЦены", ПоказыватьМинимальныеЦены);
	
	// Поиск
	СтруктураНастроек.Вставить("ПоискВДопРеквизитахИСведениях", ПоискВДопРеквизитахИСведениях);
	СтруктураНастроек.Вставить("ПоискВХарактеристиках", ПоискВХарактеристиках);
	СтруктураНастроек.Вставить("ПоискВШтрихКодах", ПоискВШтрихКодах);
	СтруктураНастроек.Вставить("НастройкиПоискаКакИскать", НастройкиПоискаКакИскать);
	СтруктураНастроек.Вставить("НастройкиПоиска", НастройкиПоиска);
	СтруктураНастроек.Вставить("ПоискВДопСведениях", ПоискВДопСведениях);
	
	ЭтаФорма.Закрыть(СтруктураНастроек);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПоказыватьАналогиПриИзменении(Элемент)
	
	Если Не ПоказыватьАналоги Тогда ПоказыватьНеПрямыеАналоги = Ложь КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьСопутствующиеТоварыПриИзменении(Элемент)
	Элементы.ГруппаСопутствущиеТовары.Видимость = ПоказыватьСопутствующиеТовары;
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПоискаПриИзменении(Элемент)
	Элементы.ГруппаКакИскатьВертикальная.Видимость = ?(НастройкиПоиска = 1, Истина, Ложь);
	Элементы.ОтступГруппыПоиска.Видимость = ?(НастройкиПоиска = 1, Истина, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПоискаКакИскатьПриИзменении(Элемент)
	Если НастройкиПоискаКакИскать = 0 Тогда
		Элементы.ПоискВДопРеквизитахИСведениях.Доступность = Ложь;
		ПоискВДопРеквизитахИСведениях = Истина;
	Иначе
		Элементы.ПоискВДопРеквизитахИСведениях.Доступность = Истина;
		ПоискВДопРеквизитахИСведениях = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область БольшеВозможностей

&НаКлиенте
Процедура ПредложитьВозможностьНажатие(Элемент)
	
	ТекстДляПисьма = НСтр("ru = 'Опишите, каких настроек вам не хватает для списка номенклатуры:'");
	Тег = НСтр("ru = 'Список номенклатуры'");
	
	УправлениеНебольшойФирмойКлиент.ПредложитьВозможностьНажатие(Тег, ТекстДляПисьма);
	
КонецПроцедуры

#КонецОбласти
