
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ОграниченияТипов") Тогда
		ОграниченияТипов.ЗагрузитьЗначения(Параметры.ОграниченияТипов);
	КонецЕсли;
	Параметры.Свойство("Свойство", Свойство);
	Элементы.ДоступныеТипы.Видимость = (ОграниченияТипов.Количество() <> 1);
	Если НЕ Элементы.ДоступныеТипы.Видимость Тогда
		ТекущийТип = ОграниченияТипов[0].Значение;
		ОграничитьТип(ЭтотОбъект, ТекущийТип);
		КлючСохраненияПоложенияОкна = "Справочник.Спецификации.Форма.ДобавлениеФиксированногоЗначения.БезДерева";
	Иначе
		КлючСохраненияПоложенияОкна = "Справочник.Спецификации.Форма.ДобавлениеФиксированногоЗначения.СДеревом";
	КонецЕсли;
	
	// Предварительная сортировка
	СписокТипов = Новый СписокЗначений;
	СписокТипов.ЗагрузитьЗначения(Метаданные.ОпределяемыеТипы.ФиксированныеЗначенияФормул.Тип.Типы());
	Для каждого ЭлементСписка Из СписокТипов Цикл
		ЭлементСписка.Представление = Строка(ЭлементСписка.Значение);
	КонецЦикла; 
	СписокТипов.СортироватьПоПредставлению();
	// Заполнение дерева доступных типов
	Для каждого ЭлементСписка Из СписокТипов Цикл
		Тип = ЭлементСписка.Значение;
		Если ОграниченияТипов.Количество() > 0 И ОграниченияТипов.НайтиПоЗначению(Тип) = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		ИдентификаторРодителя = ОбщегоНазначения.ВидОбъектаПоТипу(Тип);
		СтрокаРодитель = ГруппаДоступныхТипов(ИдентификаторРодителя);
		НоваяСтрока = СтрокаРодитель.ПолучитьЭлементы().Добавить();
		ПолноеИмя = Метаданные.НайтиПоТипу(Тип).ПолноеИмя();
		ИмяОбъекта = СтрРазделить(ПолноеИмя, ".")[1];
		НоваяСтрока.Идентификатор = ИмяОбъекта;
		НоваяСтрока.Представление = Строка(Тип);
		НоваяСтрока.Тип = Тип;
	КонецЦикла;
	Если ДоступныеТипы.ПолучитьЭлементы().Количество() = 0 Тогда
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДоступныеТипыПриАктивизацииСтроки(Элемент)
	
	ТекущаяСтрока = Элементы.ДоступныеТипы.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда
		Тип = ТекущаяСтрока.Тип;
	Иначе
		Тип = Неопределено;
	КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(ТекущийТип) ИЛИ ВыбранныеЗначения.Количество() = 0 ИЛИ Тип = ТекущийТип Тогда
		ДоступныеТипыПриАктивизацииСтрокиЗавершение(КодВозвратаДиалога.ОК, Тип);
	Иначе
		Оповещение = Новый ОписаниеОповещения("ДоступныеТипыПриАктивизацииСтрокиЗавершение", ЭтотОбъект, Тип);
		ПоказатьВопрос(
			Оповещение, 
			НСтр("ru = 'При смене типа выбранные значения будут очищены. Продолжить?'"),
			РежимДиалогаВопрос.ОКОтмена);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеТипыПриАктивизацииСтрокиЗавершение(Ответ, Тип) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.ОК Тогда
		СтараяСтрока = СтрокаПоТипуРекурсивно(ТекущийТип, ДоступныеТипы.ПолучитьЭлементы());
		Элементы.ДоступныеТипы.ТекущаяСтрока = СтараяСтрока.ПолучитьИдентификатор();
		Возврат;
	КонецЕсли;
	Если ТекущийТип <> Тип Тогда
		ВыбранныеЗначения.Очистить();
	КонецЕсли; 
	ТекущийТип = Тип;
	ОграничитьТип(ЭтотОбъект, ТекущийТип);
	
КонецПроцедуры

&НаКлиенте
Функция СтрокаПоТипуРекурсивно(Тип, Строки)
	
	Для каждого Строка Из Строки Цикл
		Если Строка.Тип = Тип Тогда
			Возврат Строка;
		КонецЕсли; 
		Подстрока = СтрокаПоТипуРекурсивно(Тип, Строка.ПолучитьЭлементы());
		Если Подстрока <> Неопределено Тогда
			Возврат Подстрока;
		КонецЕсли; 
	КонецЦикла;
	Возврат Неопределено;
	
КонецФункции
 
#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	ОповеститьОВыборе(ВыбранныеЗначения.ВыгрузитьЗначения());	
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ГруппаДоступныхТипов(Идентификатор)
	
	НайденнаяСтрока = Неопределено;
	Для каждого СтрокаГруппы Из ДоступныеТипы.ПолучитьЭлементы() Цикл
		Если СтрокаГруппы.Идентификатор = Идентификатор Тогда
			НайденнаяСтрока = СтрокаГруппы;
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	Если НайденнаяСтрока = Неопределено Тогда
		НайденнаяСтрока = ДоступныеТипы.ПолучитьЭлементы().Добавить();
		НайденнаяСтрока.Идентификатор = Идентификатор;
		НайденнаяСтрока.Представление = Идентификатор;
	КонецЕсли; 
	Возврат НайденнаяСтрока;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОграничитьТип(Форма, Тип)
	
	Элементы = Форма.Элементы;
	Элементы.ВыбранныеЗначенияЗначение.ПараметрыВыбора = Новый ФиксированныйМассив(Новый Массив);
	Если ЗначениеЗаполнено(Тип) И ТипЗнч(Тип) = Тип("Тип") Тогда
		ОписаниеТипов = Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип));
		Элементы.ВыбранныеЗначенияЗначение.ОграничениеТипа = ОписаниеТипов;
		Форма.ВыбранныеЗначения.ТипЗначения = ОписаниеТипов;
		Элементы.ВыбранныеЗначения.ТолькоПросмотр = Ложь;
		Если Тип = Тип("СправочникСсылка.ЗначенияСвойствОбъектов") И ЗначениеЗаполнено(Форма.Свойство) Тогда
			ПараметрВыбора = Новый ПараметрВыбора("Отбор.Владелец", Форма.Свойство);
			Элементы.ВыбранныеЗначенияЗначение.ПараметрыВыбора = Новый ФиксированныйМассив(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрВыбора));
		КонецЕсли; 
	Иначе
		Элементы.ВыбранныеЗначенияЗначение.ОграничениеТипа = Новый ОписаниеТипов;
		Форма.ВыбранныеЗначения.ТипЗначения =  Новый ОписаниеТипов;
		Элементы.ВыбранныеЗначения.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
 

