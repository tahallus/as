#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ЕдиницаХранения") Тогда
		 ЕдиницаХранения = Параметры.ЕдиницаХранения;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЕдиницаХранения) Тогда
		
		Если Список.Параметры.Элементы.Найти("ЕдиницаХранения")<>Неопределено Тогда
			Список.Параметры.УстановитьЗначениеПараметра("ЕдиницаХранения", ЕдиницаХранения);
		КонецЕсли;
		
	Иначе
		
		ПараметрыСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
		ПараметрыСписка.ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СправочникНаборыЕдиницИзмеренияПереопределяемый.Ссылка КАК Ссылка,
		|	СправочникНаборыЕдиницИзмеренияПереопределяемый.ПометкаУдаления КАК ПометкаУдаления,
		|	СправочникНаборыЕдиницИзмеренияПереопределяемый.Наименование КАК Наименование,
		|	СправочникНаборыЕдиницИзмеренияПереопределяемый.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	СправочникНаборыЕдиницИзмеренияПереопределяемый.Предопределенный КАК Предопределенный,
		|	СправочникНаборыЕдиницИзмеренияПереопределяемый.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных,
		|	ЛОЖЬ КАК ЕдиницыРазличные
		|ИЗ
		|	Справочник.НаборыЕдиницИзмерения КАК СправочникНаборыЕдиницИзмеренияПереопределяемый";
		ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, ПараметрыСписка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат 
	КонецЕсли;
	
	Если ТекущиеДанные.ЕдиницыРазличные Тогда
		
		Режим = РежимДиалогаВопрос.ДаНет;
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОповещения = Новый Структура("ВыбраннаяСтрока, ЕдиницаИзмерения", ВыбраннаяСтрока, ТекущиеДанные.ЕдиницаИзмерения);
		Оповещение = Новый ОписаниеОповещения("ПослеВыбораНабора", ЭтотОбъект, ПараметрыОповещения);
		ПоказатьВопрос(Оповещение, НСтр("ru='Единица измерения выбранного набора не совпадает с единицей измерения владельца.
		|При выборе, единица измерения владельца, будет изменена на единицу измерения набора. Продолжить?
		|
		|Корректировка единиц измерений номенклатуры,
		|при наличии остатков или введенных ценах,
		|может привести к некорректным данным. 
		|Например, если числилось 10 кг запасов, 
		|то после смены единицы измерения 
		|на ""Паллета"", остаток будет равно 10 паллетам,
		|а прайс-лист продолжит указывать цены в килограммах.'"), Режим, 0);
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораНабора(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		СтруктураПараметров= Новый Структура("Ссылка, ЕдиницаИзмерения", ПараметрыОповещения.ВыбраннаяСтрока, ПараметрыОповещения.ЕдиницаИзмерения);
		ОповеститьОВыборе(СтруктураПараметров);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти