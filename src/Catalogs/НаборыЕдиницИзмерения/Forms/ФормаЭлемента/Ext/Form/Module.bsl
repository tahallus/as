
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УправлениеФормой();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	УправлениеФормой();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЕдиницаИзмеренияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) И Не ВыбранноеЗначение = Объект.ЕдиницаИзмерения Тогда
		
		Режим = РежимДиалогаВопрос.ДаНет;
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОповещения = Новый Структура("ВыбранноеЗначение", ВыбранноеЗначение);
		Оповещение = Новый ОписаниеОповещения("ПослеВыбораЕдиницыИзмерения", ЭтотОбъект, ПараметрыОповещения);
		ПоказатьВопрос(Оповещение, НСтр("ru='Корректировка единиц измерений номенклатуры,
		|при наличии остатков или введенных ценах,
		|может привести к некорректным данным. 
		|Например, если числилось 10 кг запасов, 
		|то после смены единицы измерения 
		|на ""Паллета"", остаток будет равно 10 паллетам,
		|а прайс-лист продолжит указывать цены в килограммах.
		|
		|Продолжить?'"), Режим, 0);
		
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораЕдиницыИзмерения(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.ЕдиницаИзмерения = ПараметрыОповещения.ВыбранноеЗначение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандФормы

&НаКлиенте
Процедура ИсторияИзменений(Команда)
	
	СтруктураОтбора = Новый Структура("ВладелецЕдиницыИзмерения", Объект.Ссылка);
	ПараметрыОтбора = Новый Структура("Отбор", СтруктураОтбора);
	ОткрытьФорму("РегистрСведений.ИсторияИзмененийЕдиницИзмерения.ФормаСписка", ПараметрыОтбора, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	
	ДоступностьСозданияЕдиницИзмерения = ЗначениеЗаполнено(Объект.Ссылка);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Список", "ТолькоПросмотр", Не ДоступностьСозданияЕдиницИзмерения);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияПредупреждение", "Видимость", Не ДоступностьСозданияЕдиницИзмерения);
	
	Если ДоступностьСозданияЕдиницИзмерения Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец", Объект.Ссылка, ВидСравненияКомпоновкиДанных.Равно);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец", "", ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
