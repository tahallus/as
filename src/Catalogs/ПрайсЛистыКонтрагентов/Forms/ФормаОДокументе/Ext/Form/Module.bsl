
#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем ИспользуетсяДатаФормирования;
	
	Параметры.Свойство("Автор",						Объект.Автор);
	Параметры.Свойство("ВариантыСортировки",		Объект.ВариантыСортировки);
	Параметры.Свойство("ВыводитьДатуФормирования",	ИспользуетсяДатаФормирования);
	Параметры.Свойство("ДатаФормирования",			Объект.ДатаФормирования);
	Параметры.Свойство("Индивидуальный",			Объект.Индивидуальный);
	Параметры.Свойство("Описание",					Объект.Описание);
	
	Если НЕ Параметры.Свойство("НаименованиеПрайсЛиста", Объект.Наименование) Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НаименованиеПрайсЛиста", "Видимость", Ложь);
		
	КонецЕсли;
	
	Если ИспользуетсяДатаФормирования <> Истина Тогда
		
		ВыводитьДатуФормирования = 0;
		Объект.ДатаФормирования = НачалоМесяца(ТекущаяДатаСеанса());
		
	ИначеЕсли ЗначениеЗаполнено(Объект.ДатаФормирования) Тогда
		
		ВыводитьДатуФормирования = 2;
		
	Иначе
		
		ВыводитьДатуФормирования = 1;
		Объект.ДатаФормирования = НачалоМесяца(ТекущаяДатаСеанса());
		
	КонецЕсли;
	
	Если Параметры.Свойство("ОтключитьРедактированиеСведений") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВыводитьДатуФормирования",		"ТолькоПросмотр",	Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВариантыСортировки",			"ТолькоПросмотр",	Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Описание",						"ТолькоПросмотр",	Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "УказатьКонтактыОтветственного",	"ТолькоПросмотр",	Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДатаФормирования",				"Доступность",		Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОК",							"Видимость",		Ложь);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "УказатьКонтактыОтветственного",	"ТолькоПросмотр",	НЕ Объект.Индивидуальный);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДатаФормирования",				"Доступность", 		(ВыводитьДатуФормирования = 2));
		
	КонецЕсли;
	
КонецПроцедуры //ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВыводитьДатуФормированияПриИзменении(Элемент)
	
	Если ВыводитьДатуФормирования <> 2 Тогда
		
		Объект.ДатаФормирования = НачалоМесяца(ОбщегоНазначенияКлиент.ДатаСеанса()); // Выключили опцию, сбросим значение на начало месяца..
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДатаФормирования", "Доступность", ВыводитьДатуФормирования = 2);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаФормированияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВводаДатыФормирования", ЭтотОбъект);
	ПоказатьВводДаты(ОписаниеОповещения, Объект.ДатаФормирования, НСтр("ru ='Дата формирования прайс-листа'"), ЧастиДаты.Дата);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаДатыФормирования(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		Объект.ДатаФормирования = Результат;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = Новый Структура;
	Результат.Вставить("ВариантыСортировки",		Объект.ВариантыСортировки);
	Результат.Вставить("НаименованиеПрайсЛиста",	Объект.Наименование);
	Результат.Вставить("Описание",					Объект.Описание);
	
	Если ВыводитьДатуФормирования = 0 Тогда
		
		Результат.Вставить("ВыводитьДатуФормирования",	Ложь);
		Результат.Вставить("ДатаФормирования",			Неопределено);
		
	ИначеЕсли ВыводитьДатуФормирования = 1 Тогда
		
		Результат.Вставить("ВыводитьДатуФормирования",	Истина);
		Результат.Вставить("ДатаФормирования",			Неопределено);
		
	Иначе
		
		Результат.Вставить("ВыводитьДатуФормирования",	Истина);
		Результат.Вставить("ДатаФормирования",			Объект.ДатаФормирования);
		
	КонецЕсли;
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

#КонецОбласти
