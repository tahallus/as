#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Организация") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Организация",
			Параметры.Организация, ВидСравненияКомпоновкиДанных.Равно, , ЗначениеЗаполнено(Параметры.Организация));
	КонецЕсли;
	
	Параметры.Свойство("Договор", Договор);
	Параметры.Свойство("ВидДоговора", ВидДоговора);
	
	УстановитьОтборПоВидуИДоговору();
	УстановитьУсловноеОформлениеПоТекущейНастройке();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборПоВидуИДоговору()
	
	ГруппаВидИлиДоговор= ОбщегоНазначенияКлиентСервер.НайтиЭлементОтбораПоПредставлению(
		Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы, "ГруппаВидИлиДоговор");
	
	Если ГруппаВидИлиДоговор = Неопределено Тогда
		ГруппаВидИлиДоговор = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
			Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы,
			"ГруппаВидИлиДоговор",
			ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИЛИ);
	КонецЕсли;
	СписокВидовДоговоров = Новый СписокЗначений;
	СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоров.ПустаяСсылка());
	СписокВидовДоговоров.Добавить(ВидДоговора);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаВидИлиДоговор,
		"ВидДоговора",
		ВидСравненияКомпоновкиДанных.ВСписке,
		СписокВидовДоговоров,
		,
		ЗначениеЗаполнено(ВидДоговора),
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
	
	СписокДоговоры = Новый СписокЗначений;
	СписокДоговоры.Добавить(Договор);
	СписокДоговоры.Добавить(Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаВидИлиДоговор,
		"Договор",
		ВидСравненияКомпоновкиДанных.ВСписке,
		СписокДоговоры,
		,
		ЗначениеЗаполнено(Договор),
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
КонецПроцедуры


&НаСервере
Процедура УстановитьУсловноеОформлениеПоТекущейНастройке()
	
	Если Параметры.Свойство("ТекущаяАвтонумерация") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТекущаяНастройка", Параметры.ТекущаяАвтонумерация);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТекущаяНастройка", Справочники.НастройкиНумерацииДоговоровКонтрагентов.ПустаяСсылка());
		Элементы.ДекорацияПодсказка.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	НовоеУсловноеОформление = Список.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы.Добавить();
	
	Оформление = НовоеУсловноеОформление.Оформление.Элементы.Найти("Шрифт");
	Оформление.Значение 		= Новый Шрифт(,, Истина);
	Оформление.Использование 	= Истина;
	
	Отбор = НовоеУсловноеОформление.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование 	= Истина;
	Отбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("ТекущаяНастройка");
	Отбор.ПравоеЗначение 	= Истина;
	
КонецПроцедуры

#КонецОбласти

