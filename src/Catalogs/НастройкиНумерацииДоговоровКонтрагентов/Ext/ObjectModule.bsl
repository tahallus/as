#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПометкаУдаления Тогда
		// Проверяем, есть ли уже аналогичные настройки
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	НастройкиНумерацииДоговоровКонтрагентов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НастройкиНумерацииДоговоровКонтрагентов КАК НастройкиНумерацииДоговоровКонтрагентов
		|ГДЕ
		|	НЕ НастройкиНумерацииДоговоровКонтрагентов.Ссылка = &Ссылка
		|	И НастройкиНумерацииДоговоровКонтрагентов.Организация = &Организация
		|	И НастройкиНумерацииДоговоровКонтрагентов.ВидДоговора = &ВидДоговора
		|	И НастройкиНумерацииДоговоровКонтрагентов.КатегорияДоговора = &КатегорияДоговора
		|	И НастройкиНумерацииДоговоровКонтрагентов.Договор = &Договор";
		
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ВидДоговора",  ВидДоговора);
		Запрос.УстановитьПараметр("КатегорияДоговора", КатегорияДоговора);
		Запрос.УстановитьПараметр("Договор",  Договор);
		Запрос.УстановитьПараметр("Ссылка",  Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ТекстСообщения = НСтр(
				"ru = 'В базе присутствует аналогичная настройка нумерации договоров.
				|Создание одинаковых настроек запрещено.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , , Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли