
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем ПолныйПутьОбъектЗагрузки;
	
	Если НЕ Параметры.Свойство("ОбъектЗагрузки", ПолныйПутьОбъектЗагрузки) Тогда
		
		ВызватьИсключение НСтр("ru ='Самостоятельное использование формы не предусмотрено.'");
		
	КонецЕсли;
	
	КэшЗначений = Новый Структура;
	КэшЗначений.Вставить("ВыбранныеПоля",		Неопределено);
	КэшЗначений.Вставить("ТекущийПользователь",	Пользователи.АвторизованныйПользователь());
	КэшЗначений.Вставить("ПолныйПутьОбъектЗагрузки", ПолныйПутьОбъектЗагрузки);
	
	ОписаниеОбъектаЗагрузки = ЗагрузкаДанныхИзВнешнегоИсточника.ОбъектЗагрузкиПоПолномуИмени(ПолныйПутьОбъектЗагрузки);
	КэшЗначений.Вставить("ОбъектЗагрузки",		ОписаниеОбъектаЗагрузки.ОбъектЗагрузки);
	КэшЗначений.Вставить("ИмяТабличнойЧасти",	ОписаниеОбъектаЗагрузки.ИмяТабличнойЧасти);
	
	Параметры.Свойство("ВыбранныеПоля", КэшЗначений.ВыбранныеПоля);
	
	УстановитьОтборы();
	
	Элементы.WildberriesПродажи.Видимость = (ПолныйПутьОбъектЗагрузки = "Документ.ОтчетКомиссионера.ТабличнаяЧасть.Запасы");
	Элементы.WildberriesВозвраты.Видимость = (ПолныйПутьОбъектЗагрузки = "Документ.ПриходнаяНакладная.ТабличнаяЧасть.Запасы");
	Элементы.WildberriesСписание.Видимость = (ПолныйПутьОбъектЗагрузки = "Документ.ОтчетКомиссионераОСписании.ТабличнаяЧасть.Запасы");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Создать(Команда)
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ВыбранныеПоля", КэшЗначений.ВыбранныеПоля);
	ДанныеЗаполнения.Вставить("Пользователь", КэшЗначений.ТекущийПользователь);
	ДанныеЗаполнения.Вставить("ПолныйПутьОбъектЗагрузки", КэшЗначений.ПолныйПутьОбъектЗагрузки);
	ДанныеЗаполнения.Вставить("ОбъектЗагрузки", КэшЗначений.ОбъектЗагрузки);
	ДанныеЗаполнения.Вставить("ИмяТабличнойЧасти", КэшЗначений.ИмяТабличнойЧасти);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", ДанныеЗаполнения);
	
	ОткрытьФорму("Справочник.СоответствиеПолейЗагрузкиДанныхИзВнешнегоИсточника.ФормаОбъекта", ПараметрыОткрытия, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборы()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Пользователь",	КэшЗначений.ТекущийПользователь,
		ВидСравненияКомпоновкиДанных.Равно, 
		НСтр("ru ='Пользователь'"), Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ОбъектЗагрузки",
		КэшЗначений.ОбъектЗагрузки,		ВидСравненияКомпоновкиДанных.Равно, 
		НСтр("ru ='Объект загрузки'"), Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ИмяТабличнойЧасти",
		КэшЗначений.ИмяТабличнойЧасти,		ВидСравненияКомпоновкиДанных.Равно, 
		НСтр("ru ='Имя табличной части'"), Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
КонецПроцедуры

&НаКлиенте
Процедура WildberriesПродажи(Команда)
	
	ОткрытьФорму("Справочник.СоответствиеПолейЗагрузкиДанныхИзВнешнегоИсточника.Форма.ФормаЭлемента", 
		Новый Структура("ЗначенияЗаполнения", ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.НастройкиЗагрузкиWildberriesОтчетОПродажах()),
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура WildberriesВозвраты(Команда)

	ОткрытьФорму("Справочник.СоответствиеПолейЗагрузкиДанныхИзВнешнегоИсточника.Форма.ФормаЭлемента", 
		Новый Структура("ЗначенияЗаполнения", ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.НастройкиЗагрузкиWildberriesВозвратОтКомиссионера()),
		ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура WildberriesСписание(Команда)
	
	ОткрытьФорму("Справочник.СоответствиеПолейЗагрузкиДанныхИзВнешнегоИсточника.Форма.ФормаЭлемента", 
		Новый Структура("ЗначенияЗаполнения", ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.НастройкиЗагрузкиWildberriesОтчетКомиссионераОСписании()),
		ЭтотОбъект);
		
КонецПроцедуры

#КонецОбласти
