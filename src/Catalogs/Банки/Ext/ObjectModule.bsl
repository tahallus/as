#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	Код = "";
	
КонецПроцедуры // ПриКопировании()

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Страна = Справочники.СтраныМира.Россия Тогда
	
		Если НЕ ЭтоГруппа Тогда
	
			Если ЗначениеЗаполнено(КоррСчет) И СтрДлина(СокрЛП(КоррСчет)) <> 20 Тогда
				ТекстСообщения = НСтр("ru = 'Корр.счет банка должен иметь 20 знаков.'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "КоррСчет", , Отказ);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(КоррСчет) И НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СокрЛП(КоррСчет)) Тогда
				ТекстСообщения = НСтр("ru = 'В составе Корр.счета банка должны быть только цифры.'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "КоррСчет", , Отказ);
			КонецЕсли;
			
			Если СтрДлина(СокрЛП(Код)) <> 9 Тогда
				ТекстСообщения = НСтр("ru = 'БИК банка должен иметь 9 знаков.'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Код", , Отказ);
			КонецЕсли;

			Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СокрЛП(Код)) Тогда
				ТекстСообщения = НСтр("ru = 'В составе БИК банка должны быть только цифры.'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Код", , Отказ);
			КонецЕсли;
		
		КонецЕсли;
		
	Иначе
		
		ЗаполнениеОбъектовУНФ.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "Код");
		
	КонецЕсли;
	
	ТекстСообщения = "";
	Если Не SWIFTКорректен(СокрЛП(СВИФТБИК), ТекстСообщения) Тогда
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "СВИФТБИК", , Отказ);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет корректность номера SWIFT банка
//
// Параметры:
//  СВИФТБИК          - Строка - номер SWIFT  проверяемого банка
//  ТекстСообщения - Строка - в параметр передается текст сообщения об ошибке, если проверка не пройдена.
// 
// Возвращаемое значение:
//  Булево - Результат проверки SWIFT, если Ложь, тогда SWIFT некорректный
//
Функция SWIFTКорректен(СВИФТБИК, ТекстСообщения)
	
	Если ПустаяСтрока(СВИФТБИК) Тогда
		Возврат Истина;
	КонецЕсли;
	
	SWIFTКорректен = Ложь;
	ТекстСообщения = "";
	Если Не БанковскиеПравилаКлиентСервер.ПроверитьДлинуSWIFT(СВИФТБИК) Тогда
		ТекстСообщения = ТекстСообщения + НСтр("ru = 'SWIFT должен состоять из 8 или 11 символов'");
	ИначеЕсли Не БанковскиеПравилаКлиентСервер.ПроверитьРазрешенныеСимволыSWIFT(СВИФТБИК) Тогда
		ТекстСообщения = ТекстСообщения + НСтр("ru = 'Разрешены только буквы от A до Z и цифры'");
	Иначе
		SWIFTКорректен = Истина;
	КонецЕсли;
	
	Возврат SWIFTКорректен;
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли