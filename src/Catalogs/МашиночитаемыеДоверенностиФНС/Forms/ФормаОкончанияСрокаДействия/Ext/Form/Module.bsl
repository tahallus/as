#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Параметры.СтруктураДанных;
	
	Если СтруктураДанных.Свойство("ТолькоПросмотрФормы") И СтруктураДанных.ТолькоПросмотрФормы Тогда
		Элементы.ДокументДатаОкончания.ТолькоПросмотр 				= Истина;
		Элементы.ОтсутствуетКонкретнаяДатаОкончания.ТолькоПросмотр 	= Истина;
		Элементы.ДокументСрокДействия.ТолькоПросмотр 				= Истина;
		Элементы.ФормаКнопкаСохранить.Доступность 					= Ложь;
	КонецЕсли;
	
	Если СтруктураДанных <> Неопределено Тогда
		ДокументДатаОкончания 	= СтруктураДанных.ДокументДатаОкончания;
		ДокументСрокДействия 	= СтруктураДанных.ДокументСрокДействия;
	КонецЕсли;
	
	ОтсутствуетКонкретнаяДатаОкончания = НЕ ЗначениеЗаполнено(ДокументДатаОкончания)
		И ЗначениеЗаполнено(ДокументСрокДействия);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтсутствуетКонкретнаяДатаОкончанияПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если НЕ СохранениеВозможно() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ДокументДатаОкончания", 	ДокументДатаОкончания);
	СтруктураДанных.Вставить("ДокументСрокДействия", 	ДокументСрокДействия);
	
	Закрыть(СтруктураДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Форма.Элементы.ГруппаДатаОкончания.Доступность = НЕ Форма.ОтсутствуетКонкретнаяДатаОкончания;
	Форма.Элементы.ГруппаСрокДействия.Доступность = Форма.ОтсутствуетКонкретнаяДатаОкончания;
	
	Если Форма.ОтсутствуетКонкретнаяДатаОкончания Тогда
		Форма.ДокументДатаОкончания = Неопределено;
	Иначе
		Форма.ДокументСрокДействия = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СохранениеВозможно()
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	Если НЕ ОтсутствуетКонкретнаяДатаОкончания И НЕ ЗначениеЗаполнено(ДокументДатаОкончания) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не задана дата окончания действия.'"),, "ДатаОкончания",,
			Отказ);
	КонецЕсли;
	
	Если ОтсутствуетКонкретнаяДатаОкончания И НЕ ЗначениеЗаполнено(ДокументСрокДействия) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не задан срок действия.'"),, "ДокументСрокДействия",,
			Отказ);
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

#КонецОбласти
