&НаКлиенте
Перем КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		Если НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			Если Параметры.Свойство("Организация") И ЗначениеЗаполнено(Параметры.Организация) Тогда
				Объект.Организация = Параметры.Организация;
				Объект.Доверитель = Параметры.Организация;
				ПодставитьРеквизитыДоверителя(ЭтаФорма);
				
			Иначе
				УчетПоВсемОрганизациям = РегламентированнаяОтчетность.ПолучитьПризнакУчетаПоВсемОрганизациям();
				Элементы.Доверитель.ТолькоПросмотр = НЕ УчетПоВсемОрганизациям;
				
				ОргПоУмолчанию = РегламентированнаяОтчетность.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
				
				КонтекстЭДОСервер = КонтекстЭДОСервер();
				
				Если ЗначениеЗаполнено(ОргПоУмолчанию)
					И ?(КонтекстЭДОСервер = Неопределено, Истина,
					КонтекстЭДОСервер.СписокДопустимыхОрганизацийВОбъектахОбмена().Найти(ОргПоУмолчанию) <> Неопределено)
					И ((ЗначениеЗаполнено(ОргПоУмолчанию) И НЕ УчетПоВсемОрганизациям)
					ИЛИ (НЕ ЗначениеЗаполнено(Объект.Организация) И УчетПоВсемОрганизациям
					И (ЗначениеЗаполнено(ОргПоУмолчанию)))) Тогда
					
					Объект.Организация = ОргПоУмолчанию;
					Объект.Доверитель = ОргПоУмолчанию;
					ПодставитьРеквизитыДоверителя(ЭтаФорма);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	КоличествоВидовПолномочийПредставителя =
		Справочники.МашиночитаемыеДоверенностиФНС.КоличествоВидовПолномочийПредставителя();
	
	УведомлениеОтправлено = УведомлениеОтправлено(Объект.Ссылка);
	
	Инициализация();
	
	УправлениеФормой(ЭтаФорма);
	ПрорисоватьСтатус(ЭтаФорма);
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганами.ОтметитьКакПрочтенное(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииПослеПолученияКонтекстаЭДО", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	СохранитьСтатусОтправкиУведомления();
	
	Оповестить("Запись_МашиночитаемыеДоверенностиФНС",, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ЗначениеЗаполнено(Объект.Доверитель) И ИмяСобытия = "Запись_Организации"
		И (Параметр = Объект.Доверитель ИЛИ Источник = Объект.Доверитель) Тогда
		
		ПодставитьРеквизитыДоверителя(ЭтаФорма);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ЛицоБезДовЮЛ) И (ИмяСобытия = "Запись_Организации"
		ИЛИ ИмяСобытия = "Запись_Контрагенты" ИЛИ ИмяСобытия = "Запись_Контрагента")
		И (Параметр = Объект.ЛицоБезДовЮЛ ИЛИ Источник = Объект.ЛицоБезДовЮЛ) Тогда
		
		ПодставитьРеквизитыЛицаБезДоверенностиЮЛ();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ЛицоБезДовФЛ) И (ИмяСобытия = "Запись_Контрагенты"
		ИЛИ ИмяСобытия = "Запись_Контрагента" ИЛИ ИмяСобытия = "Запись_ФизическиеЛица"
		ИЛИ ИмяСобытия = "Запись_ВладельцаИлиБухгалтера")
		И (Параметр = Объект.ЛицоБезДовФЛ ИЛИ Источник = Объект.ЛицоБезДовФЛ) Тогда
		
		ПодставитьРеквизитыЛицаБезДоверенностиФЛ();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Представитель) И (ИмяСобытия = "Запись_Организации"
		ИЛИ ИмяСобытия = "Запись_Контрагенты" ИЛИ ИмяСобытия = "Запись_Контрагента"
		ИЛИ ИмяСобытия = "Запись_ФизическиеЛица" ИЛИ ИмяСобытия = "Запись_ВладельцаИлиБухгалтера")
		И (Параметр = Объект.Представитель ИЛИ Источник = Объект.Представитель) Тогда
		
		ПодставитьРеквизитыПредставителя();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияРасширеннаяНастройкаСрокаДействияНажатие(Элемент)
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ДокументДатаОкончания", 	Объект.ДатаОкончания);
	СтруктураДанных.Вставить("ДокументСрокДействия", 	Объект.СрокДействия);
	СтруктураДанных.Вставить("ТолькоПросмотрФормы", 	УведомлениеОтправлено ИЛИ ПодписьЗагружена);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДекорацияРасширеннаяНастройкаСрокаДействияНажатиеПослеВвода",
		ЭтотОбъект);
	ОткрытьФорму(
		"Справочник.МашиночитаемыеДоверенностиФНС.Форма.ФормаОкончанияСрокаДействия",
		Новый Структура("СтруктураДанных", СтруктураДанных),
		ЭтаФорма,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоверительПриИзменении(Элемент)
	
	ПодставитьРеквизитыДоверителя(ЭтаФорма);
	
	Если НЕ ПодписьЗагружена И ТипЗнч(Объект.Доверитель) = Тип("Строка") Тогда
		Объект.ДоверительЮЛ_НаимОрг = Объект.Доверитель;
		Если Доверитель_ЮридическоеЛицо И НЕ Представитель_ЮридическоеЛицо И ПредставительЯвляетсяСотрудником Тогда
			Объект.ПредставительЮЛ_НаимОрг = Объект.Доверитель;
		КонецЕсли;
		ВвестиРеквизитыДоверителя();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоверительОчистка(Элемент, СтандартнаяОбработка)
	
	Если НЕ ПодписьЗагружена ИЛИ ТипОтправителя <> "1" Тогда
		Объект.Организация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
	КонецЕсли;
	
	Если ПодписьЗагружена Тогда
		Возврат;
	КонецЕсли;
	
	Доверитель_ЮридическоеЛицо 					= Истина;
	Объект.ДоверительЮЛ_ИностраннаяОрганизация 	= Ложь;
	Объект.ДоверительЮЛ_НаимОрг 				= "";
	Объект.ДоверительЮЛ_ИНН 					= "";
	Объект.ДоверительЮЛ_КПП 					= "";
	Объект.ДоверительЮЛ_ОГРН 					= "";
	Объект.ДоверительЮЛ_СтрРег 					= ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка");
	Объект.ДоверительЮЛ_НаимРегОрг 				= "";
	Объект.ДоверительЮЛ_РегНомер 				= "";
	Объект.ДоверительЮЛ_КодНПРег 				= "";
	Объект.ДоверительЮЛ_АдрРус 					= "";
	Объект.ДоверительЮЛ_АдрXML 					= "";
	Объект.ДоверительЮЛ_АдрЛат 					= "";
	
	Если Доверитель_ЮридическоеЛицо И НЕ Представитель_ЮридическоеЛицо И ПредставительЯвляетсяСотрудником Тогда
		Объект.ПредставительЮЛ_НаимОрг 	= "";
		Объект.ПредставительЮЛ_ИНН 		= "";
		Объект.ПредставительЮЛ_КПП 		= "";
		Объект.ПредставительЮЛ_ОГРН 	= "";
	КонецЕсли;
	
	СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук")));
	КоличествоСтрокФИО = СтрокиФИО.Количество();
	Для НомерСКонца = 1 По КоличествоСтрокФИО Цикл
		Объект.ФИО.Удалить(СтрокиФИО[КоличествоСтрокФИО - НомерСКонца]);
	КонецЦикла;
	СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ")));
	КоличествоСтрокФИО = СтрокиФИО.Количество();
	Для НомерСКонца = 1 По КоличествоСтрокФИО Цикл
		Объект.ФИО.Удалить(СтрокиФИО[КоличествоСтрокФИО - НомерСКонца]);
	КонецЦикла;
	
	СтрокиУдостоверений = Объект.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук")));
	КоличествоСтрокУдостоверений = СтрокиУдостоверений.Количество();
	Для НомерСКонца = 1 По КоличествоСтрокУдостоверений Цикл
		Объект.УдостоверенияЛичности.Удалить(СтрокиУдостоверений[КоличествоСтрокУдостоверений - НомерСКонца]);
	КонецЦикла;
	СтрокиУдостоверений = Объект.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ")));
	КоличествоСтрокУдостоверений = СтрокиУдостоверений.Количество();
	Для НомерСКонца = 1 По КоличествоСтрокУдостоверений Цикл
		Объект.УдостоверенияЛичности.Удалить(СтрокиУдостоверений[КоличествоСтрокУдостоверений - НомерСКонца]);
	КонецЦикла;
	
	Объект.ДоверительФЛ_ИНН 			= "";
	Объект.ДоверительФЛ_ОГРН 			= "";
	Объект.ДоверительФЛ_СНИЛС 			= "";
	Объект.ДоверительФЛ_Гражданство 	= ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка");
	Объект.ДоверительФЛ_Пол 			= ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.ПустаяСсылка");
	Объект.ДоверительФЛ_ДатаРождения 	= Неопределено;
	Объект.ДоверительФЛ_МестоРожд 		= "";
	
	Объект.ЛицоБезДовЮЛ 		= Неопределено;
	Объект.ЛицоБезДовЮЛ_НаимОрг = "";
	Объект.ЛицоБезДовЮЛ_ИНН 	= "";
	Объект.ЛицоБезДовЮЛ_КПП 	= "";
	Объект.ЛицоБезДовЮЛ_ОГРН 	= "";
	
	Объект.ЛицоБезДовФЛ 				= Неопределено;
	Объект.ЛицоБезДовФЛ_ИНН 			= "";
	Объект.ЛицоБезДовФЛ_СНИЛС 			= "";
	Объект.ЛицоБезДовФЛ_Гражданство 	= ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка");
	Объект.ЛицоБезДовФЛ_ДатаРождения 	= Неопределено;
	Объект.ЛицоБезДовФЛ_Должность 		= "";
	
	Объект.КодНалоговогоОрганаПредставления = "";
	Объект.НалоговыеОрганыДействия.Очистить();
	
	Модифицированность = Истина;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРеквизитыДоверителяНажатие(Элемент)
	
	ВвестиРеквизитыДоверителя();
	
КонецПроцедуры

&НаКлиенте
Процедура ЛицоБезДовЮЛПриИзменении(Элемент)
	
	ПодставитьРеквизитыЛицаБезДоверенностиЮЛ();
	
	Если НЕ ПодписьЗагружена И ТипЗнч(Объект.ЛицоБезДовЮЛ) = Тип("Строка") Тогда
		Объект.ЛицоБезДовЮЛ_НаимОрг = Объект.ЛицоБезДовЮЛ;
		ВвестиРеквизитыЛицаБезДоверенностиЮЛ();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛицоБезДовЮЛОчистка(Элемент, СтандартнаяОбработка)
	
	Объект.ЛицоБезДовЮЛ_НаимОрг = "";
	Объект.ЛицоБезДовЮЛ_ИНН 	= "";
	Объект.ЛицоБезДовЮЛ_КПП 	= "";
	Объект.ЛицоБезДовЮЛ_ОГРН 	= "";
	
	Модифицированность = Истина;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРеквизитыЛицаБезДоверенностиЮЛНажатие(Элемент)
	
	ВвестиРеквизитыЛицаБезДоверенностиЮЛ();
	
КонецПроцедуры

&НаКлиенте
Процедура ЛицоБезДовФЛПриИзменении(Элемент)
	
	ПодставитьРеквизитыЛицаБезДоверенностиФЛ();
	
	Если НЕ ПодписьЗагружена И ТипЗнч(Объект.ЛицоБезДовФЛ) = Тип("Строка") Тогда
		СтруктураФИО = ДокументооборотСКОВызовСервера.РазложитьФИО(Объект.ЛицоБезДовФЛ);
		
		СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук")));
		КоличествоСтрокФИО = СтрокиФИО.Количество();
		Если КоличествоСтрокФИО = 0 Тогда
			СтрокаФИО = Объект.ФИО.Добавить();
			СтрокаФИО.Владелец = ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук");
		Иначе
			СтрокаФИО = СтрокиФИО[0];
		КонецЕсли;
		СтрокаФИО.Фамилия 	= СокрЛП(СтруктураФИО.Фамилия);
		СтрокаФИО.Имя 		= СокрЛП(СтруктураФИО.Имя);
		СтрокаФИО.Отчество 	= СокрЛП(СтруктураФИО.Отчество);
		
		ВвестиРеквизитыЛицаБезДоверенностиФЛ();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛицоБезДовФЛОчистка(Элемент, СтандартнаяОбработка)
	
	СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук")));
	КоличествоСтрокФИО = СтрокиФИО.Количество();
	Для НомерСКонца = 1 По КоличествоСтрокФИО Цикл
		Объект.ФИО.Удалить(СтрокиФИО[КоличествоСтрокФИО - НомерСКонца]);
	КонецЦикла;
	
	Объект.ЛицоБезДовФЛ 				= Неопределено;
	Объект.ЛицоБезДовФЛ_ИНН 			= "";
	Объект.ЛицоБезДовФЛ_СНИЛС 			= "";
	Объект.ЛицоБезДовФЛ_Гражданство 	= ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка");
	Объект.ЛицоБезДовФЛ_ДатаРождения 	= Неопределено;
	Объект.ЛицоБезДовФЛ_Должность 		= "";
	
	Модифицированность = Истина;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРеквизитыЛицаБезДоверенностиФЛНажатие(Элемент)
	
	ВвестиРеквизитыЛицаБезДоверенностиФЛ();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставительПриИзменении(Элемент)
	
	ПодставитьРеквизитыПредставителя();
	
	Если НЕ ПодписьЗагружена И ТипЗнч(Объект.Представитель) = Тип("Строка") Тогда
		Объект.ПредставительЮЛ_НаимОрг = Объект.Представитель;
		ВвестиРеквизитыПредставителя();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставительОчистка(Элемент, СтандартнаяОбработка)
	
	Если ПодписьЗагружена И ТипОтправителя = "1" Тогда
		Объект.Организация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
	КонецЕсли;
	
	Если ПодписьЗагружена Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ПредставительЮЛ_НаимОрг 	= "";
	Объект.ПредставительЮЛ_ИНН 		= "";
	Объект.ПредставительЮЛ_КПП 		= "";
	Объект.ПредставительЮЛ_ОГРН 	= "";
	
	СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
	КоличествоСтрокФИО = СтрокиФИО.Количество();
	Для НомерСКонца = 1 По КоличествоСтрокФИО Цикл
		Объект.ФИО.Удалить(СтрокиФИО[КоличествоСтрокФИО - НомерСКонца]);
	КонецЦикла;
	
	ПредставительФЛ_ФИО = "";
	
	СтрокиУдостоверений = Объект.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
	КоличествоСтрокУдостоверений = СтрокиУдостоверений.Количество();
	Для НомерСКонца = 1 По КоличествоСтрокУдостоверений Цикл
		Объект.УдостоверенияЛичности.Удалить(СтрокиУдостоверений[КоличествоСтрокУдостоверений - НомерСКонца]);
	КонецЦикла;
	
	ПредставительФЛ_Удостоверение = "";
	
	Объект.ПредставительФЛ_ИНН = "";
	Объект.ПредставительФЛ_ОГРН = "";
	Объект.ПредставительФЛ_СНИЛС = "";
	Объект.ПредставительФЛ_Гражданство = ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка");
	Объект.ПредставительФЛ_ДатаРождения = Неопределено;
	
	Модифицированность = Истина;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРеквизитыПредставителяНажатие(Элемент)
	
	ВвестиРеквизитыПредставителя();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставительЯвляетсяСотрудникомПриИзменении(Элемент)
	
	Если Доверитель_ЮридическоеЛицо И НЕ Представитель_ЮридическоеЛицо И ПредставительЯвляетсяСотрудником Тогда
		Объект.ПредставительЮЛ_НаимОрг 	= Объект.ДоверительЮЛ_НаимОрг;
		Объект.ПредставительЮЛ_ИНН 		= Объект.ДоверительЮЛ_ИНН;
		Объект.ПредставительЮЛ_КПП 		= Объект.ДоверительЮЛ_КПП;
		Объект.ПредставительЮЛ_ОГРН 	= Объект.ДоверительЮЛ_ОГРН;
		
	Иначе
		Объект.ПредставительЮЛ_НаимОрг 	= "";
		Объект.ПредставительЮЛ_ИНН 		= "";
		Объект.ПредставительЮЛ_КПП 		= "";
		Объект.ПредставительЮЛ_ОГРН 	= "";
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставительФЛ_ФИОНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ФормаВводаФИО = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени("ФормаВводаФИО");
	
	СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
	
	ФормаВводаФИО.Фамилия 	= ?(СтрокиФИО.Количество() <> 0, СтрокиФИО[0].Фамилия, "");
	ФормаВводаФИО.Имя 		= ?(СтрокиФИО.Количество() <> 0, СтрокиФИО[0].Имя, "");
	ФормаВводаФИО.Отчество 	= ?(СтрокиФИО.Количество() <> 0, СтрокиФИО[0].Отчество, "");
	
	ФормаВводаФИО.ОписаниеОповещенияОЗакрытии =
		Новый ОписаниеОповещения("ПредставительФЛ_ФИОНачалоВыбораПослеВвода", ЭтотОбъект);
	ФормаВводаФИО.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ФормаВводаФИО.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставительФЛ_ФИООчистка(Элемент, СтандартнаяОбработка)
	
	СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
	
	КоличествоСтрокФИО = СтрокиФИО.Количество();
	Для НомерСКонца = 1 По КоличествоСтрокФИО Цикл
		Объект.ФИО.Удалить(СтрокиФИО[КоличествоСтрокФИО - НомерСКонца]);
	КонецЦикла;
	
	ПредставительФЛ_ФИО = "";
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставительФЛ_УдостоверениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтрокиУдостоверений = Объект.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ДокументВид",
		?(СтрокиУдостоверений.Количество() <> 0, СтрокиУдостоверений[0].ВидДок, ""));
	СтруктураДанных.Вставить("ДокументСерия",
		?(СтрокиУдостоверений.Количество() <> 0, СтрокиУдостоверений[0].СерДок, ""));
	СтруктураДанных.Вставить("ДокументНомер",
		?(СтрокиУдостоверений.Количество() <> 0, СтрокиУдостоверений[0].НомДок, ""));
	СтруктураДанных.Вставить("ДокументДатаВыдачи",
		?(СтрокиУдостоверений.Количество() <> 0, СтрокиУдостоверений[0].ДатаДок, ""));
	СтруктураДанных.Вставить("ДокументКемВыдан",
		?(СтрокиУдостоверений.Количество() <> 0, СтрокиУдостоверений[0].ВыдДок, ""));
	СтруктураДанных.Вставить("ДокументКодПодразделения",
		?(СтрокиУдостоверений.Количество() <> 0, СтрокиУдостоверений[0].КодВыдДок, ""));

	ОписаниеОповещения = Новый ОписаниеОповещения("ПредставительФЛ_УдостоверениеНачалоВыбораПослеВвода", ЭтотОбъект);
	ОткрытьФорму(
		"Справочник.МашиночитаемыеДоверенностиФНС.Форма.ФормаВводаУдостоверения",
		Новый Структура("СтруктураДанных", СтруктураДанных),
		ЭтаФорма,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставительФЛ_УдостоверениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтрокиУдостоверений = Объект.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
	
	КоличествоСтрокУдостоверений = СтрокиУдостоверений.Количество();
	Для НомерСКонца = 1 По КоличествоСтрокУдостоверений Цикл
		Объект.УдостоверенияЛичности.Удалить(СтрокиУдостоверений[КоличествоСтрокУдостоверений - НомерСКонца]);
	КонецЦикла;
	
	ПредставительФЛ_Удостоверение = "";
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОтправителяПриИзменении(Элемент)
	
	Если ТипОтправителя = "1" Тогда
		Объект.Организация = ?(ТипЗнч(Объект.Представитель) = Тип("СправочникСсылка.Организации"),
			Объект.Представитель, ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	Иначе
		Объект.Организация = ?(ТипЗнч(Объект.Доверитель) = Тип("СправочникСсылка.Организации"),
			Объект.Доверитель, ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодНалоговогоОрганаПредставленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ОчиститьСообщения();
		Если ПодписьЗагружена И ТипОтправителя = "1" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выберите представителя из справочника ""Организации""'"),,
				"Представитель");
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выберите доверителя из справочника ""Организации""'"),,
				"Доверитель");
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ЗначенияДляОтбора = Новый Массив;
	ЗначенияДляОтбора.Добавить(Новый Структура("КодНО, КПП",
		Объект.КодНалоговогоОрганаПредставления,
		?(ПодписьЗагружена И ТипОтправителя = "1", Объект.ПредставительЮЛ_КПП, Объект.ДоверительЮЛ_КПП)));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("ЗначенияДляОтбора", ЗначенияДляОтбора);
	
	ФормаВыбораНалоговогоОргана = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени(
		"ФормаВыбораНалоговогоОргана",
		ПараметрыФормы,
		ЭтаФорма);
	
	Если ФормаВыбораНалоговогоОргана.ТаблицаНО.Количество() <> 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("КодНалоговогоОрганаПредставленияНачалоВыбораПослеВвода", ЭтотОбъект);
		ФормаВыбораНалоговогоОргана.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаВыбораНалоговогоОргана.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ФормаВыбораНалоговогоОргана.Открыть();
		
	Иначе
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Код налогового органа необходимо задать в справочнике ""Организации""'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНалоговыеОрганыДействияНажатие(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ОчиститьСообщения();
		Если ПодписьЗагружена И ТипОтправителя = "1" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выберите представителя из справочника ""Организации""'"),,
				"Представитель");
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выберите доверителя из справочника ""Организации""'"),,
				"Доверитель");
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Организация", Объект.Организация);
	СтруктураДанных.Вставить("КодыНалоговыхОргановДействия", Новый Массив);
	Для каждого НалоговыйОрганДействия Из Объект.НалоговыеОрганыДействия Цикл
		СтруктураДанных.КодыНалоговыхОргановДействия.Добавить(НалоговыйОрганДействия.КодНалоговогоОргана);
	КонецЦикла;
	СтруктураДанных.Вставить("ТолькоПросмотрФормы", УведомлениеОтправлено ИЛИ ПодписьЗагружена);

	ОписаниеОповещения = Новый ОписаниеОповещения("ДекорацияНалоговыеОрганыДействияНажатиеПослеВвода", ЭтотОбъект);
	ОткрытьФорму(
		"Справочник.МашиночитаемыеДоверенностиФНС.Форма.ФормаВводаНалоговыхОрганов",
		Новый Структура("СтруктураДанных", СтруктураДанных),
		ЭтаФорма,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПолномочияПредставителяНажатие(Элемент)
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ПризнакПолныеПолномочия", ?(Объект.ПолномочияПредставителя.Количество() <> 0,
		Объект.ПолномочияПредставителя[0].ПризнакПолныеПолномочия, Истина));
	Для ВидПолномочия = 1 По КоличествоВидовПолномочийПредставителя Цикл
		ПредставлениеВида = Формат(ВидПолномочия, "ЧЦ=2; ЧВН=");
		СтруктураДанных.Вставить("Признак" + ПредставлениеВида, ?(Объект.ПолномочияПредставителя.Количество() <> 0,
			Объект.ПолномочияПредставителя[0]["Признак" + ПредставлениеВида], Ложь));
	КонецЦикла;
	СтруктураДанных.Вставить("ТолькоПросмотрФормы", УведомлениеОтправлено ИЛИ ПодписьЗагружена);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДекорацияПолномочияПредставителяНажатиеПослеВвода", ЭтотОбъект);
	ОткрытьФорму(
		"Справочник.МашиночитаемыеДоверенностиФНС.Форма.ФормаВводаПолномочий",
		Новый Структура("СтруктураДанных", СтруктураДанных),
		ЭтаФорма,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отправить(Команда)
	
	ОчиститьСообщения();
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Если ПодписьЗагружена И ТипОтправителя = "1" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выберите представителя из справочника ""Организации""'"),,
				"Представитель");
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выберите доверителя из справочника ""Организации""'"),,
				"Доверитель");
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПослеПодтвержденияПриПредупреждении", ЭтотОбъект);
	Если ЗначениеЗаполнено(Объект.ДатаОкончания)
		И Объект.ДатаОкончания < НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса()) Тогда
		
		ПоказатьВопрос(
			ОписаниеОповещения,
			НСтр("ru = 'Дата окончания действия доверенности меньше текущей, продолжить отправку?'"),
			РежимДиалогаВопрос.ДаНет,,
			КодВозвратаДиалога.Нет);
		
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ОчиститьСообщения();
	Если НЕ ПодписьЗагружена Тогда
		РезультатыПроверки = ДокументооборотСКОВызовСервера.ПроверитьМЧД(Объект);
		
		Если РезультатыПроверки.Количество() <> 0 Тогда
			Для каждого РезультатПроверки Из РезультатыПроверки Цикл
				ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатПроверки.ТекстОшибки,,
					РезультатПроверки.Поле);
			КонецЦикла;
			
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьПослеПодтвержденияВыгрузкиПодписи", ЭтотОбъект);
	Если ПодписьЗагружена Тогда
		ПоказатьВопрос(
			ОписаниеОповещения,
			НСтр("ru = 'Будут выгружены файлы машиночитаемой доверенности и подписи доверителя, продолжить?'"),
			РежимДиалогаВопрос.ДаНет,,
			КодВозвратаДиалога.Да);
		
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПослеПодтвержденияВыгрузкиПодписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если (Модифицированность ИЛИ Параметры.Ключ.Пустая())
		И НЕ Записать() ИЛИ НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОпределятьКодНалоговогоОрганаПолучателя = Истина;
	ФорматДоверенностиПоПриказуФНС 			= Истина;
	ПараметрыВозврата = Новый Структура;
	ПараметрыВозврата.Вставить("ВозвращатьАдресВоВременномХранилище", 	Истина);
	ПараметрыВозврата.Вставить("ВозвращатьИмяФайлаСРасширением", 		Истина);
	ПараметрыВозврата.Вставить("ВозвращатьПриложеннуюПодпись", 			Истина);
	РезультатВыгрузки = ДокументооборотСКОВызовСервера.ВыгрузитьМЧД(
		Объект.Ссылка,
		Новый УникальныйИдентификатор,,
		ОпределятьКодНалоговогоОрганаПолучателя,
		ФорматДоверенностиПоПриказуФНС,
		Ложь,
		ПараметрыВозврата);
	
	Если ЗначениеЗаполнено(РезультатВыгрузки) Тогда
		ОперацииСФайламиЭДКОКлиент.СохранитьФайлы(РезультатВыгрузки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	
	Если (Модифицированность ИЛИ Параметры.Ключ.Пустая())
		И НЕ Записать() ИЛИ НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьПослеЗагрузки", ЭтотОбъект);
	ОткрытьФорму(
		"Справочник.МашиночитаемыеДоверенностиФНС.Форма.ФормаЗагрузкиИзФайла",
		Новый Структура("СсылкаНаДоверенность", Объект.Ссылка),
		ЭтаФорма,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтаФорма);
	ПараметрыИзменения.Вставить("Организация", Объект.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС"));
	ПараметрыИзменения.Вставить("ТекстВопроса", НСтр("ru = 'Вы уверены, что уведомление уже зарегистрировано?'"));
	
	РегламентированнаяОтчетностьКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура Инициализация()
	
	Если ТипЗнч(Объект.Доверитель) <> Тип("Строка") Тогда
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("СправочникСсылка.Организации"));
		Элементы.Доверитель.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
	КонецЕсли;
	
	Если ТипЗнч(Объект.ЛицоБезДовЮЛ) <> Тип("Строка") Тогда
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
		МассивТипов.Добавить(Тип("СправочникСсылка.Организации"));
		Элементы.ЛицоБезДовЮЛ.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
	КонецЕсли;
	
	Если ТипЗнч(Объект.ЛицоБезДовФЛ) <> Тип("Строка") Тогда
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
		МассивТипов.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
		Элементы.ЛицоБезДовФЛ.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
	КонецЕсли;
	
	Если ТипЗнч(Объект.Представитель) <> Тип("Строка") Тогда
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
		МассивТипов.Добавить(Тип("СправочникСсылка.Организации"));
		МассивТипов.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
		Элементы.Представитель.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
	КонецЕсли;
	
	Доверитель_ЮридическоеЛицо = НЕ ЗначениеЗаполнено(Объект.Доверитель)
		ИЛИ ДокументооборотСКОВызовСервера.ЭтоЮридическоеЛицо(Объект.Доверитель);
	
	СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
		Перечисления.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ));
	ПредставительФЛ_ФИО = ?(СтрокиФИО.Количество() <> 0,
		ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеФИО(СтрокиФИО[0]), "");
	
	Представитель_ЮридическоеЛицо = ДокументооборотСКОВызовСервера.ЭтоЮридическоеЛицо(Объект.Представитель);
	
	ПредставительЯвляетсяСотрудником = НЕ ЗначениеЗаполнено(Объект.Представитель)
		ИЛИ (ЗначениеЗаполнено(Объект.Представитель) И НЕ Представитель_ЮридическоеЛицо
		И (ЗначениеЗаполнено(Объект.ПредставительЮЛ_НаимОрг) ИЛИ ЗначениеЗаполнено(Объект.ПредставительЮЛ_ИНН)
		ИЛИ ЗначениеЗаполнено(Объект.ПредставительЮЛ_КПП) ИЛИ ЗначениеЗаполнено(Объект.ПредставительЮЛ_ОГРН))
		И Объект.Доверитель <> Объект.Представитель
		И Объект.ДоверительЮЛ_НаимОрг = Объект.ПредставительЮЛ_НаимОрг И Объект.ДоверительЮЛ_ИНН = Объект.ПредставительЮЛ_ИНН
		И Объект.ДоверительЮЛ_КПП = Объект.ПредставительЮЛ_КПП И Объект.ДоверительЮЛ_ОГРН = Объект.ПредставительЮЛ_ОГРН);
	
	СтрокиУдостоверений = Объект.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец",
		Перечисления.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ));
	ПредставительФЛ_Удостоверение = ?(СтрокиУдостоверений.Количество() <> 0,
		ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеУдостоверение(СтрокиУдостоверений[0]), "");
	
	Если ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.ИмяФайлаВыгрузки) Тогда
		РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Ссылка, "ФайлВырузки, ЭлектроннаяПодпись");
		ПодписьЗагружена = (РеквизитыОбъекта.ФайлВырузки.Получить() <> Неопределено
			И РеквизитыОбъекта.ЭлектроннаяПодпись.Получить() <> Неопределено);
	Иначе
		ПодписьЗагружена = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ТипОтправителя = ?(Объект.Организация <> Объект.Доверитель И Объект.Организация = Объект.Представитель, "1", "0");
	Иначе
		ТипОтправителя = ?(ПодписьЗагружена, "1", "0");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПрорисоватьСтатус(Форма)
	
	ПараметрыПрорисовкиПанелиОтправки = ДокументооборотСКОВызовСервера.ПараметрыПрорисовкиПанелиОтправки(
		Форма.Объект.Ссылка,
		Форма.Объект.Организация,
		"ФНС");
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ПрименитьПараметрыПрорисовкиПанелиОтправки(
		Форма,
		ПараметрыПрорисовкиПанелиОтправки);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Форма.Элементы.Отправить.Видимость = НЕ Форма.УведомлениеОтправлено;
	Форма.Элементы.Записать.ТолькоВоВсехДействиях = Форма.УведомлениеОтправлено;
	Форма.Элементы.Загрузить.Видимость = НЕ Форма.УведомлениеОтправлено;
	
	Форма.Элементы.ДатаОкончания.Видимость = ЗначениеЗаполнено(Форма.Объект.ДатаОкончания)
		ИЛИ НЕ ЗначениеЗаполнено(Форма.Объект.СрокДействия);
	Форма.Элементы.СрокДействия.Видимость = НЕ ЗначениеЗаполнено(Форма.Объект.ДатаОкончания)
		И ЗначениеЗаполнено(Форма.Объект.СрокДействия);
	
	Форма.Элементы.ГруппаЛицоБезДоверенностиЮЛ.Видимость = Форма.Доверитель_ЮридическоеЛицо
		И НЕ Форма.Объект.ДоверительЮЛ_ИностраннаяОрганизация;
	Форма.Элементы.ГруппаЛицоБезДоверенностиФЛ.Видимость = Форма.Доверитель_ЮридическоеЛицо
		И НЕ Форма.Объект.ДоверительЮЛ_ИностраннаяОрганизация;
	
	Форма.Элементы.ДекорацияРеквизитыПредставителя.Видимость = Форма.Представитель_ЮридическоеЛицо;
	Форма.Элементы.ПредставительЯвляетсяСотрудником.Видимость = Форма.Доверитель_ЮридическоеЛицо
		И НЕ Форма.Представитель_ЮридическоеЛицо;
	
	Форма.Элементы.ПредставительФЛ_ОГРН.АвтоОтметкаНезаполненного = НЕ Форма.Представитель_ЮридическоеЛицо
		И НЕ Форма.ПредставительЯвляетсяСотрудником;
	Форма.Элементы.ПредставительФЛ_ОГРН.АвтоВыборНезаполненного = НЕ Форма.Представитель_ЮридическоеЛицо
		И НЕ Форма.ПредставительЯвляетсяСотрудником;
	
	КоличествоНалоговыхОргановДействия = Форма.Объект.НалоговыеОрганыДействия.Количество();
	Если КоличествоНалоговыхОргановДействия = 0 Тогда
		Форма.Элементы.ДекорацияНалоговыеОрганыДействия.Заголовок = НСтр("ru = 'Все'");
	ИначеЕсли КоличествоНалоговыхОргановДействия = 1 Тогда
		Форма.Элементы.ДекорацияНалоговыеОрганыДействия.Заголовок =
			Форма.Объект.НалоговыеОрганыДействия[0].КодНалоговогоОргана;
	Иначе
		Форма.Объект.НалоговыеОрганыДействия.Сортировать("КодНалоговогоОргана");
		Форма.Элементы.ДекорацияНалоговыеОрганыДействия.Заголовок =
			Форма.Объект.НалоговыеОрганыДействия[0].КодНалоговогоОргана
			+ ", " + Форма.Объект.НалоговыеОрганыДействия[1].КодНалоговогоОргана + ?(КоличествоНалоговыхОргановДействия > 2,
			" " + СтрШаблон(
				НСтр("ru = 'и еще %1'"),
				Формат(КоличествоНалоговыхОргановДействия - 2, "ЧДЦ=; ЧН=; ЧГ=")), "");
	КонецЕсли;
	
	Если Форма.Объект.ПолномочияПредставителя.Количество() = 0
		ИЛИ Форма.Объект.ПолномочияПредставителя[0].ПризнакПолныеПолномочия Тогда
		Форма.Элементы.ДекорацияПолномочияПредставителя.Заголовок = НСтр("ru = 'Все'");
	Иначе
		ПредставленияВидов = "";
		Для ВидПолномочия = 1 По Форма.КоличествоВидовПолномочийПредставителя Цикл
			ПредставлениеВида = Формат(ВидПолномочия, "ЧЦ=2; ЧВН=");
			Если Форма.Объект.ПолномочияПредставителя[0]["Признак" + ПредставлениеВида] Тогда
				ПредставленияВидов = ПредставленияВидов + ?(ЗначениеЗаполнено(ПредставленияВидов), ", ", "")
					+ ПредставлениеВида;
			КонецЕсли;
		КонецЦикла;
		Форма.Элементы.ДекорацияПолномочияПредставителя.Заголовок = ПредставленияВидов;
	КонецЕсли;
	
	Форма.Элементы.ГруппаТипОтправителя.Видимость 			= Форма.ПодписьЗагружена;
	Форма.Элементы.ДекорацияОтступТипОтправителя.Видимость 	= Форма.ПодписьЗагружена;
	
	ТолькоПросмотрФормы = Форма.УведомлениеОтправлено ИЛИ Форма.ПодписьЗагружена;
	
	Форма.Элементы.НомерДоверенности.ТолькоПросмотр 				= ТолькоПросмотрФормы;
	Форма.Элементы.ДатаВыдачи.ТолькоПросмотр 						= ТолькоПросмотрФормы;
	Форма.Элементы.ДатаОкончания.ТолькоПросмотр 					= ТолькоПросмотрФормы;
	Форма.Элементы.СрокДействия.ТолькоПросмотр 						= ТолькоПросмотрФормы;
	Форма.Элементы.Доверитель.ТолькоПросмотр 						= Форма.УведомлениеОтправлено;
	Форма.Элементы.ЛицоБезДовЮЛ.ТолькоПросмотр 						= ТолькоПросмотрФормы;
	Форма.Элементы.ЛицоБезДовФЛ.ТолькоПросмотр 						= ТолькоПросмотрФормы;
	Форма.Элементы.Представитель.ТолькоПросмотр 					= Форма.УведомлениеОтправлено;
	Форма.Элементы.ПредставительЯвляетсяСотрудником.ТолькоПросмотр 	= ТолькоПросмотрФормы;
	Форма.Элементы.ПредставительФЛ_ФИО.ТолькоПросмотр 				= ТолькоПросмотрФормы;
	Форма.Элементы.ПредставительФЛ_Удостоверение.ТолькоПросмотр 	= ТолькоПросмотрФормы;
	Форма.Элементы.ПредставительФЛ_ИНН.ТолькоПросмотр 				= ТолькоПросмотрФормы;
	Форма.Элементы.ПредставительФЛ_ОГРН.ТолькоПросмотр 				= ТолькоПросмотрФормы;
	Форма.Элементы.ПредставительФЛ_СНИЛС.ТолькоПросмотр 			= ТолькоПросмотрФормы;
	Форма.Элементы.ПредставительФЛ_Гражданство.ТолькоПросмотр 		= ТолькоПросмотрФормы;
	Форма.Элементы.ПредставительФЛ_ДатаРождения.ТолькоПросмотр 		= ТолькоПросмотрФормы;
	Форма.Элементы.КодНалоговогоОрганаПредставления.ТолькоПросмотр 	= ТолькоПросмотрФормы;
	Форма.Элементы.НомерРодительскойДоверенности.ТолькоПросмотр 	= ТолькоПросмотрФормы;
	Форма.Элементы.ТипОтправителя.ТолькоПросмотр 					= Форма.УведомлениеОтправлено;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПодставитьРеквизитыДоверителя(Форма)
	
	Если НЕ Форма.ПодписьЗагружена И ТипЗнч(Форма.Объект.Доверитель) = Тип("СправочникСсылка.Организации")
		И Форма.Объект.Доверитель <> ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка") Тогда
		
		Если НЕ Форма.ПодписьЗагружена ИЛИ Форма.ТипОтправителя <> "1" Тогда
			Форма.Объект.Организация = Форма.Объект.Доверитель;
		КонецЕсли;
		
		СвойстваОрганизации = ДокументооборотСКОВызовСервера.ПолучитьСвойстваОрганизации(Форма.Объект.Доверитель);
		
		Форма.Доверитель_ЮридическоеЛицо = СвойстваОрганизации.ЭтоЮридическоеЛицо;
		Форма.Объект.ДоверительЮЛ_ИностраннаяОрганизация = НЕ СвойстваОрганизации.ЭтоРоссийскаяОрганизация;
		
		Форма.Объект.ДоверительЮЛ_НаимОрг = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
			?(НЕ СвойстваОрганизации.ЭтоРоссийскаяОрганизация И ЗначениеЗаполнено(СвойстваОрганизации.НаимИОПол),
			СвойстваОрганизации.НаимИОПол, СвойстваОрганизации.НаимЮЛПол), "");
		Форма.Объект.ДоверительЮЛ_ИНН = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.ИННЮЛ, "");
		Форма.Объект.ДоверительЮЛ_КПП = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.КППЮЛ, "");
		Форма.Объект.ДоверительЮЛ_ОГРН = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо
			И СвойстваОрганизации.ЭтоРоссийскаяОрганизация, СвойстваОрганизации.ОГРН, "");
		Форма.Объект.ДоверительЮЛ_СтрРег = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо
			И НЕ СвойстваОрганизации.ЭтоРоссийскаяОрганизация, СвойстваОрганизации.СтранаРегистрацииИностраннойОрганизации,
			ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка"));
		Форма.Объект.ДоверительЮЛ_НаимРегОрг = "";
		Форма.Объект.ДоверительЮЛ_РегНомер = "";
		Форма.Объект.ДоверительЮЛ_КодНПРег = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо
			И НЕ СвойстваОрганизации.ЭтоРоссийскаяОрганизация, СвойстваОрганизации.КодИОСтрРег, "");
		
		Если Форма.Доверитель_ЮридическоеЛицо И НЕ Форма.Представитель_ЮридическоеЛицо
			И Форма.ПредставительЯвляетсяСотрудником Тогда
			
			Форма.Объект.ПредставительЮЛ_НаимОрг 	= Форма.Объект.ДоверительЮЛ_НаимОрг;
			Форма.Объект.ПредставительЮЛ_ИНН 		= Форма.Объект.ДоверительЮЛ_ИНН;
			Форма.Объект.ПредставительЮЛ_КПП 		= Форма.Объект.ДоверительЮЛ_КПП;
			Форма.Объект.ПредставительЮЛ_ОГРН 		= Форма.Объект.ДоверительЮЛ_ОГРН;
		КонецЕсли;
		
		Если СвойстваОрганизации.ЭтоЮридическоеЛицо Тогда
			Если СвойстваОрганизации.ЭтоРоссийскаяОрганизация Тогда
				АдресСтруктурой = Новый Структура("Строка, XML, JSON, Представление, Значение",
					СвойстваОрганизации.АдрЮР, СвойстваОрганизации.АдрЮР_XML, СвойстваОрганизации.АдрЮР_JSON,
					СвойстваОрганизации.АдрЮР, СвойстваОрганизации.АдрЮР_XML);
			Иначе
				АдресСтруктурой = Новый Структура("Строка, XML, JSON, Представление, Значение",
					СвойстваОрганизации.АдрИО, СвойстваОрганизации.АдрИО_XML, СвойстваОрганизации.АдрИО_JSON,
					СвойстваОрганизации.АдрИО, СвойстваОрганизации.АдрИО_XML);
			КонецЕсли;
			ДокументооборотСКОВызовСервера.ПреобразоватьАдресВФорматXML(АдресСтруктурой,
				ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации"));
			Форма.Объект.ДоверительЮЛ_АдрXML = АдресСтруктурой.XML;
			Если ЗначениеЗаполнено(АдресСтруктурой.XML) Тогда
				Форма.Объект.ДоверительЮЛ_АдрРус = ДокументооборотСКОВызовСервера.ПредставлениеКонтактнойИнформации(
					АдресСтруктурой.XML);
			Иначе
				Форма.Объект.ДоверительЮЛ_АдрРус = АдресСтруктурой.Представление;
			КонецЕсли;
		Иначе
			Форма.Объект.ДоверительЮЛ_АдрXML = "";
			Форма.Объект.ДоверительЮЛ_АдрРус = "";
		КонецЕсли;
		Форма.Объект.ДоверительЮЛ_АдрЛат = "";
		
		СубъектДоверенности = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук"),
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ"));
		СтрокиФИО = Форма.Объект.ФИО.НайтиСтроки(Новый Структура("Владелец", СубъектДоверенности));
		КоличествоСтрокФИО = СтрокиФИО.Количество();
		Если КоличествоСтрокФИО = 0 Тогда
			СтрокаФИО = Форма.Объект.ФИО.Добавить();
			СтрокаФИО.Владелец = СубъектДоверенности;
		Иначе
			СтрокаФИО = СтрокиФИО[0];
		КонецЕсли;
		СтрокаФИО.Фамилия = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
			СвойстваОрганизации.ФамилияРук, СвойстваОрганизации.ФамилияИП);
		СтрокаФИО.Имя = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
			СвойстваОрганизации.ИмяРук, СвойстваОрганизации.ИмяИП);
		СтрокаФИО.Отчество = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
			СвойстваОрганизации.ОтчествоРук, СвойстваОрганизации.ОтчествоИП);
		
		СтрокиУдостоверений = Форма.Объект.УдостоверенияЛичности.НайтиСтроки(
			Новый Структура("Владелец", СубъектДоверенности));
		КоличествоСтрокУдостоверений = СтрокиУдостоверений.Количество();
		Если КоличествоСтрокУдостоверений = 0 Тогда
			Если НЕ СвойстваОрганизации.ЭтоЮридическоеЛицо Тогда
				СтрокаУдостоверения = Форма.Объект.УдостоверенияЛичности.Добавить();
				СтрокаУдостоверения.Владелец = СубъектДоверенности;
			КонецЕсли;
		Иначе
			Если НЕ СвойстваОрганизации.ЭтоЮридическоеЛицо Тогда
				СтрокаУдостоверения = СтрокиУдостоверений[0];
			Иначе
				Для НомерСКонца = 1 По КоличествоСтрокУдостоверений Цикл
					Форма.Объект.УдостоверенияЛичности.Удалить(СтрокиУдостоверений[КоличествоСтрокУдостоверений - НомерСКонца]);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		Если НЕ СвойстваОрганизации.ЭтоЮридическоеЛицо Тогда
			СтрокаУдостоверения.ВидДок = СвойстваОрганизации.ВидУдостоверения;
			СтрокаУдостоверения.СерДок = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				СвойстваОрганизации.СерияУдЛичнРук, СвойстваОрганизации.СерияУдЛичн);
			СтрокаУдостоверения.НомДок = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				СвойстваОрганизации.НомерУдЛичнРук, СвойстваОрганизации.НомерУдЛичн);
			СтрокаУдостоверения.ДатаДок = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				СвойстваОрганизации.ДатаУдЛичнРук, СвойстваОрганизации.ДатаУдЛичн);
			СтрокаУдостоверения.ВыдДок = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				СвойстваОрганизации.ОрганВыданУдЛичнРук, СвойстваОрганизации.ОрганВыданУдЛичн);
			СтрокаУдостоверения.КодВыдДок = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				СвойстваОрганизации.КодПодрУдЛичнРук, СвойстваОрганизации.КодПодрУдЛичн);
		КонецЕсли;
		
		Форма.Объект.ДоверительФЛ_ИНН = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо
			И НЕ СвойстваОрганизации.ЭтоРоссийскаяОрганизация, СвойстваОрганизации.ИННРук,
			?(НЕ СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.ИННФЛ, ""));
		Форма.Объект.ДоверительФЛ_ОГРН = ?(НЕ СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.ОГРН, "");
		Форма.Объект.ДоверительФЛ_СНИЛС = ?(НЕ СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.СНИЛС, "");
		Форма.Объект.ДоверительФЛ_Гражданство = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо
			И НЕ СвойстваОрганизации.ЭтоРоссийскаяОрганизация ИЛИ НЕ СвойстваОрганизации.ЭтоЮридическоеЛицо,
			СвойстваОрганизации.Гражданство, ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка"));
		Форма.Объект.ДоверительФЛ_Пол = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо
			И НЕ СвойстваОрганизации.ЭтоРоссийскаяОрганизация, СвойстваОрганизации.ПолРук,
			ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.ПустаяСсылка"));
		Форма.Объект.ДоверительФЛ_ДатаРождения = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо
			И НЕ СвойстваОрганизации.ЭтоРоссийскаяОрганизация, СвойстваОрганизации.ДатаРождРук,
			?(НЕ СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.ДатаРожд, Неопределено));
		Форма.Объект.ДоверительФЛ_МестоРожд = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо
			И НЕ СвойстваОрганизации.ЭтоРоссийскаяОрганизация, СвойстваОрганизации.МестоРождения, "");
		
		Форма.Объект.ЛицоБезДовФЛ = СвойстваОрганизации.Руководитель;
		Форма.Объект.ЛицоБезДовФЛ_ИНН = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо
			И СвойстваОрганизации.ЭтоРоссийскаяОрганизация, СвойстваОрганизации.ИННРук, "");
		Форма.Объект.ЛицоБезДовФЛ_СНИЛС = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо
			И СвойстваОрганизации.ЭтоРоссийскаяОрганизация, СвойстваОрганизации.СНИЛС, "");
		Форма.Объект.ЛицоБезДовФЛ_Гражданство = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо
			И СвойстваОрганизации.ЭтоРоссийскаяОрганизация,
			СвойстваОрганизации.Гражданство, ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка"));
		Форма.Объект.ЛицоБезДовФЛ_ДатаРождения = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо
			И СвойстваОрганизации.ЭтоРоссийскаяОрганизация, СвойстваОрганизации.ДатаРождРук, Неопределено);
		Форма.Объект.ЛицоБезДовФЛ_Должность = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо
			И СвойстваОрганизации.ЭтоРоссийскаяОрганизация, СвойстваОрганизации.Должность, "");
		
		СтрокиФИО = Форма.Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
		КоличествоСтрокФИО = СтрокиФИО.Количество();
		СтрокиУдостоверений = Форма.Объект.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец",
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
		КоличествоСтрокУдостоверений = СтрокиУдостоверений.Количество();
		
		Форма.Объект.КодНалоговогоОрганаПредставления = СвойстваОрганизации.КодНО;
		Форма.Объект.НалоговыеОрганыДействия.Очистить();
		
		Форма.Модифицированность = Истина;
		
		УправлениеФормой(Форма);
		
	ИначеЕсли НЕ Форма.ПодписьЗагружена ИЛИ Форма.ТипОтправителя <> "1" Тогда
		Форма.Объект.Организация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиРеквизитыДоверителя()
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Доверитель_ЮридическоеЛицо", Доверитель_ЮридическоеЛицо);
	СтруктураДанных.Вставить("ДоверительЮЛ_РоссийскаяОрганизация", НЕ Объект.ДоверительЮЛ_ИностраннаяОрганизация);
	СтруктураДанных.Вставить("ДоверительЮЛ_НаимОрг", Объект.ДоверительЮЛ_НаимОрг);
	СтруктураДанных.Вставить("ДоверительЮЛ_ИНН", Объект.ДоверительЮЛ_ИНН);
	СтруктураДанных.Вставить("ДоверительЮЛ_КПП", Объект.ДоверительЮЛ_КПП);
	СтруктураДанных.Вставить("ДоверительЮЛ_ОГРН", Объект.ДоверительЮЛ_ОГРН);
	СтруктураДанных.Вставить("ДоверительЮЛ_СтрРег", Объект.ДоверительЮЛ_СтрРег);
	СтруктураДанных.Вставить("ДоверительЮЛ_НаимРегОрг", Объект.ДоверительЮЛ_НаимРегОрг);
	СтруктураДанных.Вставить("ДоверительЮЛ_РегНомер", Объект.ДоверительЮЛ_РегНомер);
	СтруктураДанных.Вставить("ДоверительЮЛ_КодНПРег", Объект.ДоверительЮЛ_КодНПРег);
	СтруктураДанных.Вставить("ДоверительЮЛ_Адр", ?(Доверитель_ЮридическоеЛицо
		И Объект.ДоверительЮЛ_ИностраннаяОрганизация И ЗначениеЗаполнено(Объект.ДоверительЮЛ_АдрЛат),
		Объект.ДоверительЮЛ_АдрЛат, Объект.ДоверительЮЛ_АдрРус));
	СтруктураДанных.Вставить("ДоверительЮЛ_АдрВЛатТранскрипции", Доверитель_ЮридическоеЛицо
		И Объект.ДоверительЮЛ_ИностраннаяОрганизация И ЗначениеЗаполнено(Объект.ДоверительЮЛ_АдрЛат));
	СтруктураДанных.Вставить("ДоверительЮЛ_АдрXML", Объект.ДоверительЮЛ_АдрXML);
	
	СубъектДоверенности = ?(Доверитель_ЮридическоеЛицо И Объект.ДоверительЮЛ_ИностраннаяОрганизация,
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук"),
		?(НЕ Доверитель_ЮридическоеЛицо,
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ"),
		Неопределено));
	СтрокиФИО = ?(СубъектДоверенности = Неопределено, Новый Массив,
		Объект.ФИО.НайтиСтроки(Новый Структура("Владелец", СубъектДоверенности)));
	СтруктураДанных.Вставить("ДоверительФЛ_Фамилия", ?(СтрокиФИО.Количество() <> 0, СтрокиФИО[0].Фамилия, ""));
	СтруктураДанных.Вставить("ДоверительФЛ_Имя", ?(СтрокиФИО.Количество() <> 0, СтрокиФИО[0].Имя, ""));
	СтруктураДанных.Вставить("ДоверительФЛ_Отчество", ?(СтрокиФИО.Количество() <> 0, СтрокиФИО[0].Отчество, ""));
	ФИО = ?(СтрокиФИО.Количество() <> 0, ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеФИО(СтрокиФИО[0]), "");
	СтруктураДанных.Вставить("ДоверительФЛ_ФИО", ФИО);
	
	СубъектДоверенности = ?(НЕ Доверитель_ЮридическоеЛицо,
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ"),
		Неопределено);
	СтрокиУдостоверений = ?(СубъектДоверенности = Неопределено, Новый Массив,
		Объект.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец", СубъектДоверенности)));
	СтруктураДанных.Вставить("ДоверительФЛ_ВидДок", ?(СтрокиУдостоверений.Количество() <> 0,
		СтрокиУдостоверений[0].ВидДок, ПредопределенноеЗначение("Справочник.ВидыДокументовФизическихЛиц.ПустаяСсылка")));
	СтруктураДанных.Вставить("ДоверительФЛ_СерДок", ?(СтрокиУдостоверений.Количество() <> 0,
		СтрокиУдостоверений[0].СерДок, ""));
	СтруктураДанных.Вставить("ДоверительФЛ_НомДок", ?(СтрокиУдостоверений.Количество() <> 0,
		СтрокиУдостоверений[0].НомДок, ""));
	СтруктураДанных.Вставить("ДоверительФЛ_ДатаДок", ?(СтрокиУдостоверений.Количество() <> 0,
		СтрокиУдостоверений[0].ДатаДок, Неопределено));
	СтруктураДанных.Вставить("ДоверительФЛ_ВыдДок", ?(СтрокиУдостоверений.Количество() <> 0,
		СтрокиУдостоверений[0].ВыдДок, ""));
	СтруктураДанных.Вставить("ДоверительФЛ_КодВыдДок", ?(СтрокиУдостоверений.Количество() <> 0,
		СтрокиУдостоверений[0].КодВыдДок, ""));
	ДоверительФЛ_Удостоверение = ?(СтрокиУдостоверений.Количество() <> 0,
		ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеУдостоверение(СтрокиУдостоверений[0]), "");
	СтруктураДанных.Вставить("ДоверительФЛ_Удостоверение", ДоверительФЛ_Удостоверение);
	
	СтруктураДанных.Вставить("ДоверительФЛ_ИНН", Объект.ДоверительФЛ_ИНН);
	СтруктураДанных.Вставить("ДоверительФЛ_ОГРН", Объект.ДоверительФЛ_ОГРН);
	СтруктураДанных.Вставить("ДоверительФЛ_СНИЛС", Объект.ДоверительФЛ_СНИЛС);
	СтруктураДанных.Вставить("ДоверительФЛ_Гражданство", Объект.ДоверительФЛ_Гражданство);
	СтруктураДанных.Вставить("ДоверительФЛ_Пол", Объект.ДоверительФЛ_Пол);
	СтруктураДанных.Вставить("ДоверительФЛ_ДатаРождения", Объект.ДоверительФЛ_ДатаРождения);
	СтруктураДанных.Вставить("ДоверительФЛ_МестоРожд", Объект.ДоверительФЛ_МестоРожд);
	
	СтруктураДанных.Вставить("ТолькоПросмотрФормы", УведомлениеОтправлено ИЛИ ПодписьЗагружена);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиРеквизитыДоверителяПослеВвода", ЭтотОбъект);
	ОткрытьФорму(
		"Справочник.МашиночитаемыеДоверенностиФНС.Форма.ФормаДоверителя",
		Новый Структура("СтруктураДанных", СтруктураДанных),
		ЭтаФорма,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиРеквизитыДоверителяПослеВвода(
		СтруктураДанных,
		ДополнительныеПараметры) Экспорт
	
	Если СтруктураДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураДанных.ДоверительЮЛ_НаимОрг) И НЕ ЗначениеЗаполнено(Объект.Доверитель) Тогда
		Элементы.Доверитель.ОграничениеТипа = Новый ОписаниеТипов;
		Объект.Доверитель = СтруктураДанных.ДоверительЮЛ_НаимОрг;
	КонецЕсли;
	
	Объект.ДоверительЮЛ_ИностраннаяОрганизация = Доверитель_ЮридическоеЛицо
		И НЕ СтруктураДанных.ДоверительЮЛ_РоссийскаяОрганизация;
	Объект.ДоверительЮЛ_НаимОрг = СтруктураДанных.ДоверительЮЛ_НаимОрг;
	Объект.ДоверительЮЛ_ИНН = ?(Доверитель_ЮридическоеЛицо, СтруктураДанных.ДоверительЮЛ_ИНН, "");
	Объект.ДоверительЮЛ_КПП = СтруктураДанных.ДоверительЮЛ_КПП;
	Объект.ДоверительЮЛ_ОГРН = СтруктураДанных.ДоверительЮЛ_ОГРН;
	Объект.ДоверительЮЛ_СтрРег = СтруктураДанных.ДоверительЮЛ_СтрРег;
	Объект.ДоверительЮЛ_НаимРегОрг = СтруктураДанных.ДоверительЮЛ_НаимРегОрг;
	Объект.ДоверительЮЛ_РегНомер = СтруктураДанных.ДоверительЮЛ_РегНомер;
	Объект.ДоверительЮЛ_КодНПРег = СтруктураДанных.ДоверительЮЛ_КодНПРег;
	Объект.ДоверительЮЛ_АдрЛат = ?(СтруктураДанных.ДоверительЮЛ_АдрВЛатТранскрипции
		И НЕ СтруктураДанных.ДоверительЮЛ_РоссийскаяОрганизация,
		СтруктураДанных.ДоверительЮЛ_Адр, "");
	Объект.ДоверительЮЛ_АдрРус = ?(СтруктураДанных.ДоверительЮЛ_АдрВЛатТранскрипции
		И НЕ СтруктураДанных.ДоверительЮЛ_РоссийскаяОрганизация,
		"", СтруктураДанных.ДоверительЮЛ_Адр);
	Объект.ДоверительЮЛ_АдрXML = СтруктураДанных.ДоверительЮЛ_АдрXML;
	
	Если Доверитель_ЮридическоеЛицо И НЕ Представитель_ЮридическоеЛицо И ПредставительЯвляетсяСотрудником Тогда
		Объект.ПредставительЮЛ_НаимОрг 	= Объект.ДоверительЮЛ_НаимОрг;
		Объект.ПредставительЮЛ_ИНН 		= Объект.ДоверительЮЛ_ИНН;
		Объект.ПредставительЮЛ_КПП 		= Объект.ДоверительЮЛ_КПП;
		Объект.ПредставительЮЛ_ОГРН 	= Объект.ДоверительЮЛ_ОГРН;
	КонецЕсли;
	
	Если Доверитель_ЮридическоеЛицо И НЕ СтруктураДанных.ДоверительЮЛ_РоссийскаяОрганизация
		ИЛИ НЕ Доверитель_ЮридическоеЛицо Тогда
		
		СубъектДоверенности = ?(Доверитель_ЮридическоеЛицо,
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук"),
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ"));
		СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец", СубъектДоверенности));
		Если СтрокиФИО.Количество() = 0 Тогда
			СтрокаФИО = Объект.ФИО.Добавить();
			СтрокаФИО.Владелец = СубъектДоверенности;
		Иначе
			СтрокаФИО = СтрокиФИО[0];
		КонецЕсли;
		СтрокаФИО.Фамилия 	= СтруктураДанных.ДоверительФЛ_Фамилия;
		СтрокаФИО.Имя 		= СтруктураДанных.ДоверительФЛ_Имя;
		СтрокаФИО.Отчество 	= СтруктураДанных.ДоверительФЛ_Отчество;
		
		Если НЕ Доверитель_ЮридическоеЛицо Тогда
			СтрокиУдостоверений = Объект.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец", СубъектДоверенности));
			Если СтрокиУдостоверений.Количество() = 0 Тогда
				СтрокаУдостоверения = Объект.УдостоверенияЛичности.Добавить();
				СтрокаУдостоверения.Владелец = СубъектДоверенности;
			Иначе
				СтрокаУдостоверения = СтрокиУдостоверений[0];
			КонецЕсли;
			СтрокаУдостоверения.ВидДок 		= СтруктураДанных.ДоверительФЛ_ВидДок;
			СтрокаУдостоверения.СерДок 		= СтруктураДанных.ДоверительФЛ_СерДок;
			СтрокаУдостоверения.НомДок 		= СтруктураДанных.ДоверительФЛ_НомДок;
			СтрокаУдостоверения.ДатаДок 	= СтруктураДанных.ДоверительФЛ_ДатаДок;
			СтрокаУдостоверения.ВыдДок 		= СтруктураДанных.ДоверительФЛ_ВыдДок;
			СтрокаУдостоверения.КодВыдДок 	= СтруктураДанных.ДоверительФЛ_КодВыдДок;
		КонецЕсли;
	КонецЕсли;
	
	Объект.ДоверительФЛ_ИНН 			= СтруктураДанных.ДоверительФЛ_ИНН;
	Объект.ДоверительФЛ_ОГРН 			= СтруктураДанных.ДоверительФЛ_ОГРН;
	Объект.ДоверительФЛ_СНИЛС 			= СтруктураДанных.ДоверительФЛ_СНИЛС;
	Объект.ДоверительФЛ_Гражданство 	= СтруктураДанных.ДоверительФЛ_Гражданство;
	Объект.ДоверительФЛ_Пол 			= СтруктураДанных.ДоверительФЛ_Пол;
	Объект.ДоверительФЛ_ДатаРождения 	= СтруктураДанных.ДоверительФЛ_ДатаРождения;
	Объект.ДоверительФЛ_МестоРожд 		= СтруктураДанных.ДоверительФЛ_МестоРожд;
	
	Модифицированность = Истина;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодставитьРеквизитыЛицаБезДоверенностиЮЛ()
	
	Если ТипЗнч(Объект.ЛицоБезДовЮЛ) = Тип("СправочникСсылка.Организации")
		И Объект.ЛицоБезДовЮЛ <> ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка") Тогда
		
		СвойстваОрганизации = ДокументооборотСКОВызовСервера.ПолучитьСвойстваОрганизации(Объект.ЛицоБезДовЮЛ);
		
		Объект.ЛицоБезДовЮЛ_НаимОрг = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
			?(НЕ СвойстваОрганизации.ЭтоРоссийскаяОрганизация И ЗначениеЗаполнено(СвойстваОрганизации.НаимИОПол),
			СвойстваОрганизации.НаимИОПол, СвойстваОрганизации.НаимЮЛПол), "");
		Объект.ЛицоБезДовЮЛ_ИНН = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.ИННЮЛ, "");
		Объект.ЛицоБезДовЮЛ_КПП = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.КППЮЛ, "");
		Объект.ЛицоБезДовЮЛ_ОГРН = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.ОГРН, "");
		
		СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук")));
		КоличествоСтрокФИО = СтрокиФИО.Количество();
		Если КоличествоСтрокФИО = 0 Тогда
			СтрокаФИО = Объект.ФИО.Добавить();
			СтрокаФИО.Владелец = ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук");
		Иначе
			СтрокаФИО = СтрокиФИО[0];
		КонецЕсли;
		СтрокаФИО.Фамилия = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
			СвойстваОрганизации.ФамилияРук, СвойстваОрганизации.ФамилияИП);
		СтрокаФИО.Имя = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
			СвойстваОрганизации.ИмяРук, СвойстваОрганизации.ИмяИП);
		СтрокаФИО.Отчество = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
			СвойстваОрганизации.ОтчествоРук, СвойстваОрганизации.ОтчествоИП);
		
		Объект.ЛицоБезДовФЛ = СвойстваОрганизации.Руководитель;
		Объект.ЛицоБезДовФЛ_ИНН = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
			СвойстваОрганизации.ИННРук, СвойстваОрганизации.ИННФЛ);
		Объект.ЛицоБезДовФЛ_СНИЛС = СвойстваОрганизации.СНИЛС;
		Объект.ЛицоБезДовФЛ_Гражданство = СвойстваОрганизации.Гражданство;
		Объект.ЛицоБезДовФЛ_ДатаРождения = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
			СвойстваОрганизации.ДатаРождРук, СвойстваОрганизации.ДатаРожд);
		Объект.ЛицоБезДовФЛ_Должность = СвойстваОрганизации.Должность;
		
		Модифицированность = Истина;
		
		УправлениеФормой(ЭтаФорма);
		
	ИначеЕсли ТипЗнч(Объект.ЛицоБезДовЮЛ) = Тип("СправочникСсылка.Контрагенты")
		И Объект.ЛицоБезДовЮЛ <> ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка") Тогда
		
		СвойстваКонтрагента = ДокументооборотСКОВызовСервера.ПолучитьСвойстваКонтрагента(Объект.ЛицоБезДовЮЛ);
		
		Объект.ЛицоБезДовЮЛ_НаимОрг = ?(СвойстваКонтрагента.ЭтоЮридическоеЛицо = Истина,
			СвойстваКонтрагента.НаименованиеПолное, "");
		Объект.ЛицоБезДовЮЛ_ИНН = ?(СвойстваКонтрагента.ЭтоЮридическоеЛицо = Истина, СвойстваКонтрагента.ИНН, "");
		Объект.ЛицоБезДовЮЛ_КПП = ?(СвойстваКонтрагента.ЭтоЮридическоеЛицо = Истина, СвойстваКонтрагента.КПП, "");
		Объект.ЛицоБезДовЮЛ_ОГРН = "";
		
		Если СвойстваКонтрагента.ЭтоЮридическоеЛицо = Ложь Тогда
			СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
				ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук")));
			КоличествоСтрокФИО = СтрокиФИО.Количество();
			Если КоличествоСтрокФИО = 0 Тогда
				СтрокаФИО = Объект.ФИО.Добавить();
				СтрокаФИО.Владелец = ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук");
			Иначе
				СтрокаФИО = СтрокиФИО[0];
			КонецЕсли;
			СтрокаФИО.Фамилия 	= СвойстваКонтрагента.Фамилия;
			СтрокаФИО.Имя 		= СвойстваКонтрагента.Имя;
			СтрокаФИО.Отчество 	= СвойстваКонтрагента.Отчество;
			
			Объект.ЛицоБезДовФЛ 				= Объект.ЛицоБезДовЮЛ;
			Объект.ЛицоБезДовЮЛ 				= ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка");
			Объект.ЛицоБезДовФЛ_ИНН 			= СвойстваКонтрагента.ИНН;
			Объект.ЛицоБезДовФЛ_СНИЛС 			= "";
			Объект.ЛицоБезДовФЛ_Гражданство 	= ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка");
			Объект.ЛицоБезДовФЛ_ДатаРождения 	= Неопределено;
			Объект.ЛицоБезДовФЛ_Должность 		= "";
			
			ТекущийЭлемент = Элементы.ЛицоБезДовФЛ;
		КонецЕсли;
		
		Модифицированность = Истина;
		
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиРеквизитыЛицаБезДоверенностиЮЛ()
	
	Если НЕ Доверитель_ЮридическоеЛицо ИЛИ Объект.ДоверительЮЛ_ИностраннаяОрганизация Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ЛицоБезДовЮЛ_НаимОрг", 	Объект.ЛицоБезДовЮЛ_НаимОрг);
	СтруктураДанных.Вставить("ЛицоБезДовЮЛ_ИНН", 		Объект.ЛицоБезДовЮЛ_ИНН);
	СтруктураДанных.Вставить("ЛицоБезДовЮЛ_КПП", 		Объект.ЛицоБезДовЮЛ_КПП);
	СтруктураДанных.Вставить("ЛицоБезДовЮЛ_ОГРН", 		Объект.ЛицоБезДовЮЛ_ОГРН);
	СтруктураДанных.Вставить("ТолькоПросмотрФормы", 	УведомлениеОтправлено ИЛИ ПодписьЗагружена);

	ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиРеквизитыЛицаБезДоверенностиЮЛПослеВвода",
		ЭтотОбъект);
	ОткрытьФорму(
		"Справочник.МашиночитаемыеДоверенностиФНС.Форма.ФормаЛицаБезДоверенностиЮЛ",
		Новый Структура("СтруктураДанных", СтруктураДанных),
		ЭтаФорма,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиРеквизитыЛицаБезДоверенностиЮЛПослеВвода(
		СтруктураДанных,
		ДополнительныеПараметры) Экспорт
	
	Если СтруктураДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураДанных.ЛицоБезДовЮЛ_НаимОрг) И НЕ ЗначениеЗаполнено(Объект.ЛицоБезДовЮЛ) Тогда
		Элементы.ЛицоБезДовЮЛ.ОграничениеТипа = Новый ОписаниеТипов;
		Объект.ЛицоБезДовЮЛ = СтруктураДанных.ЛицоБезДовЮЛ_НаимОрг;
	КонецЕсли;
	Объект.ЛицоБезДовЮЛ_НаимОрг = СтруктураДанных.ЛицоБезДовЮЛ_НаимОрг;
	Объект.ЛицоБезДовЮЛ_ИНН = СтруктураДанных.ЛицоБезДовЮЛ_ИНН;
	Объект.ЛицоБезДовЮЛ_КПП = СтруктураДанных.ЛицоБезДовЮЛ_КПП;
	Объект.ЛицоБезДовЮЛ_ОГРН = СтруктураДанных.ЛицоБезДовЮЛ_ОГРН;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодставитьРеквизитыЛицаБезДоверенностиФЛ()
	
	Если ТипЗнч(Объект.ЛицоБезДовФЛ) = Тип("СправочникСсылка.Контрагенты")
		И Объект.ЛицоБезДовФЛ <> ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка") Тогда
		
		СвойстваКонтрагента = ДокументооборотСКОВызовСервера.ПолучитьСвойстваКонтрагента(Объект.ЛицоБезДовФЛ);
		
		Если СвойстваКонтрагента.ЭтоЮридическоеЛицо = Истина Тогда
			Объект.ЛицоБезДовЮЛ 		= Объект.ЛицоБезДовФЛ;
			Объект.ЛицоБезДовФЛ 		= ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка");
			Объект.ЛицоБезДовЮЛ_НаимОрг = СвойстваКонтрагента.НаименованиеПолное;
			Объект.ЛицоБезДовЮЛ_ИНН 	= СвойстваКонтрагента.ИНН;
			Объект.ЛицоБезДовЮЛ_КПП 	= СвойстваКонтрагента.КПП;
			Объект.ЛицоБезДовЮЛ_ОГРН 	= "";
			
			ТекущийЭлемент = Элементы.ЛицоБезДовЮЛ;
		КонецЕсли;
		
		СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук")));
		КоличествоСтрокФИО = СтрокиФИО.Количество();
		Если КоличествоСтрокФИО = 0 Тогда
			Если СвойстваКонтрагента.ЭтоЮридическоеЛицо = Ложь Тогда
				СтрокаФИО = Объект.ФИО.Добавить();
				СтрокаФИО.Владелец = ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук");
			КонецЕсли;
		Иначе
			Если СвойстваКонтрагента.ЭтоЮридическоеЛицо = Ложь Тогда
				СтрокаФИО = СтрокиФИО[0];
			Иначе
				Для НомерСКонца = 1 По КоличествоСтрокФИО Цикл
					Объект.ФИО.Удалить(СтрокиФИО[КоличествоСтрокФИО - НомерСКонца]);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		Если СвойстваКонтрагента.ЭтоЮридическоеЛицо = Ложь Тогда
			СтрокаФИО.Фамилия 	= СвойстваКонтрагента.Фамилия;
			СтрокаФИО.Имя 		= СвойстваКонтрагента.Имя;
			СтрокаФИО.Отчество 	= СвойстваКонтрагента.Отчество;
		КонецЕсли;
		
		Объект.ЛицоБезДовФЛ_ИНН 			= ?(СвойстваКонтрагента.ЭтоЮридическоеЛицо = Ложь, СвойстваКонтрагента.ИНН, "");
		Объект.ЛицоБезДовФЛ_СНИЛС 			= "";
		Объект.ЛицоБезДовФЛ_Гражданство 	= ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка");
		Объект.ЛицоБезДовФЛ_ДатаРождения 	= Неопределено;
		Объект.ЛицоБезДовФЛ_Должность 		= "";
		
		Модифицированность = Истина;
		
		УправлениеФормой(ЭтаФорма);
		
	ИначеЕсли ТипЗнч(Объект.ЛицоБезДовФЛ) = Тип("СправочникСсылка.ФизическиеЛица")
		И Объект.ЛицоБезДовФЛ <> ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка") Тогда
		
		СвойстваФизическогоЛица = ДокументооборотСКОВызовСервера.ПолучитьСвойстваФизическогоЛица(Объект.ЛицоБезДовФЛ);
		
		СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук")));
		КоличествоСтрокФИО = СтрокиФИО.Количество();
		Если КоличествоСтрокФИО = 0 Тогда
			СтрокаФИО = Объект.ФИО.Добавить();
			СтрокаФИО.Владелец = ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук");
		Иначе
			СтрокаФИО = СтрокиФИО[0];
		КонецЕсли;
		СтрокаФИО.Фамилия 	= СвойстваФизическогоЛица.ФИО.Фамилия;
		СтрокаФИО.Имя 		= СвойстваФизическогоЛица.ФИО.Имя;
		СтрокаФИО.Отчество 	= СвойстваФизическогоЛица.ФИО.Отчество;
		
		Объект.ЛицоБезДовФЛ_ИНН 			= СвойстваФизическогоЛица.ИНН;
		Объект.ЛицоБезДовФЛ_СНИЛС 			= СвойстваФизическогоЛица.СНИЛС;
		Объект.ЛицоБезДовФЛ_Гражданство 	= СвойстваФизическогоЛица.Гражданство;
		Объект.ЛицоБезДовФЛ_ДатаРождения 	= СвойстваФизическогоЛица.ДатаРождения;
		Объект.ЛицоБезДовФЛ_Должность 		= СвойстваФизическогоЛица.Должность;
		
		Модифицированность = Истина;
		
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиРеквизитыЛицаБезДоверенностиФЛ()
	
	Если НЕ Доверитель_ЮридическоеЛицо ИЛИ Объект.ДоверительЮЛ_ИностраннаяОрганизация Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук")));
	СтруктураДанных.Вставить("ЛицоБезДовФЛ_Фамилия", ?(СтрокиФИО.Количество() <> 0, СтрокиФИО[0].Фамилия, ""));
	СтруктураДанных.Вставить("ЛицоБезДовФЛ_Имя", ?(СтрокиФИО.Количество() <> 0, СтрокиФИО[0].Имя, ""));
	СтруктураДанных.Вставить("ЛицоБезДовФЛ_Отчество", ?(СтрокиФИО.Количество() <> 0, СтрокиФИО[0].Отчество, ""));
	ФИО = ?(СтрокиФИО.Количество() <> 0, ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеФИО(СтрокиФИО[0]), "");
	СтруктураДанных.Вставить("ЛицоБезДовФЛ_ФИО", ФИО);
	
	СтруктураДанных.Вставить("ЛицоБезДовФЛ_ИНН", Объект.ЛицоБезДовФЛ_ИНН);
	СтруктураДанных.Вставить("ЛицоБезДовФЛ_СНИЛС", Объект.ЛицоБезДовФЛ_СНИЛС);
	СтруктураДанных.Вставить("ЛицоБезДовФЛ_Гражданство", Объект.ЛицоБезДовФЛ_Гражданство);
	СтруктураДанных.Вставить("ЛицоБезДовФЛ_ДатаРождения", Объект.ЛицоБезДовФЛ_ДатаРождения);
	СтруктураДанных.Вставить("ЛицоБезДовФЛ_Должность", Объект.ЛицоБезДовФЛ_Должность);
	
	СтруктураДанных.Вставить("ТолькоПросмотрФормы", УведомлениеОтправлено ИЛИ ПодписьЗагружена);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиРеквизитыЛицаБезДоверенностиФЛПослеВвода",
		ЭтотОбъект);
	ОткрытьФорму(
		"Справочник.МашиночитаемыеДоверенностиФНС.Форма.ФормаЛицаБезДоверенностиФЛ",
		Новый Структура("СтруктураДанных", СтруктураДанных),
		ЭтаФорма,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиРеквизитыЛицаБезДоверенностиФЛПослеВвода(
		СтруктураДанных,
		ДополнительныеПараметры) Экспорт
	
	Если СтруктураДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураДанных.ЛицоБезДовФЛ_Фамилия) ИЛИ ЗначениеЗаполнено(СтруктураДанных.ЛицоБезДовФЛ_Имя)
		ИЛИ ЗначениеЗаполнено(СтруктураДанных.ЛицоБезДовФЛ_Отчество) И НЕ ЗначениеЗаполнено(Объект.ЛицоБезДовФЛ) Тогда
		Элементы.ЛицоБезДовФЛ.ОграничениеТипа = Новый ОписаниеТипов;
		Объект.ЛицоБезДовФЛ = ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеФИО(
			Новый Структура("Фамилия, Имя, Отчество",
			СтруктураДанных.ЛицоБезДовФЛ_Фамилия, СтруктураДанных.ЛицоБезДовФЛ_Имя, СтруктураДанных.ЛицоБезДовФЛ_Отчество));
	КонецЕсли;
	
	СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук")));
	Если СтрокиФИО.Количество() = 0 Тогда
		СтрокаФИО = Объект.ФИО.Добавить();
		СтрокаФИО.Владелец = ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук");
	Иначе
		СтрокаФИО = СтрокиФИО[0];
	КонецЕсли;
	СтрокаФИО.Фамилия = СтруктураДанных.ЛицоБезДовФЛ_Фамилия;
	СтрокаФИО.Имя = СтруктураДанных.ЛицоБезДовФЛ_Имя;
	СтрокаФИО.Отчество = СтруктураДанных.ЛицоБезДовФЛ_Отчество;
	
	Объект.ЛицоБезДовФЛ_ИНН = СтруктураДанных.ЛицоБезДовФЛ_ИНН;
	Объект.ЛицоБезДовФЛ_СНИЛС = СтруктураДанных.ЛицоБезДовФЛ_СНИЛС;
	Объект.ЛицоБезДовФЛ_Гражданство = СтруктураДанных.ЛицоБезДовФЛ_Гражданство;
	Объект.ЛицоБезДовФЛ_ДатаРождения = СтруктураДанных.ЛицоБезДовФЛ_ДатаРождения;
	Объект.ЛицоБезДовФЛ_Должность = СтруктураДанных.ЛицоБезДовФЛ_Должность;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодставитьРеквизитыПредставителя()
	
	Если НЕ ПодписьЗагружена И ТипЗнч(Объект.Представитель) = Тип("СправочникСсылка.Организации")
		И Объект.Представитель <> ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка") Тогда
		
		СвойстваОрганизации = ДокументооборотСКОВызовСервера.ПолучитьСвойстваОрганизации(Объект.Представитель);
		
		Представитель_ЮридическоеЛицо = СвойстваОрганизации.ЭтоЮридическоеЛицо;
		Если Доверитель_ЮридическоеЛицо И НЕ Представитель_ЮридическоеЛицо И ПредставительЯвляетсяСотрудником Тогда
			Объект.ПредставительЮЛ_НаимОрг 	= Объект.ДоверительЮЛ_НаимОрг;
			Объект.ПредставительЮЛ_ИНН 		= Объект.ДоверительЮЛ_ИНН;
			Объект.ПредставительЮЛ_КПП 		= Объект.ДоверительЮЛ_КПП;
			Объект.ПредставительЮЛ_ОГРН 	= Объект.ДоверительЮЛ_ОГРН;
		Иначе
			Объект.ПредставительЮЛ_НаимОрг = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				?(НЕ СвойстваОрганизации.ЭтоРоссийскаяОрганизация И ЗначениеЗаполнено(СвойстваОрганизации.НаимИОПол),
				СвойстваОрганизации.НаимИОПол, СвойстваОрганизации.НаимЮЛПол), "");
			Объект.ПредставительЮЛ_ИНН = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.ИННЮЛ, "");
			Объект.ПредставительЮЛ_КПП = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.КППЮЛ, "");
			Объект.ПредставительЮЛ_ОГРН = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.ОГРН, "");
		КонецЕсли;
	
		СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
		КоличествоСтрокФИО = СтрокиФИО.Количество();
		СтрокиУдостоверений = Объект.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец",
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
		КоличествоСтрокУдостоверений = СтрокиУдостоверений.Количество();
		ФизическоеЛицоПредставителяЗаполнено = КоличествоСтрокФИО <> 0 ИЛИ КоличествоСтрокУдостоверений <> 0
			ИЛИ ЗначениеЗаполнено(Объект.ПредставительФЛ_ИНН) ИЛИ ЗначениеЗаполнено(Объект.ПредставительФЛ_ОГРН)
			ИЛИ ЗначениеЗаполнено(Объект.ПредставительФЛ_СНИЛС) ИЛИ ЗначениеЗаполнено(Объект.ПредставительФЛ_Гражданство)
			ИЛИ ЗначениеЗаполнено(Объект.ПредставительФЛ_ДатаРождения);
		Если ФизическоеЛицоПредставителяЗаполнено Тогда
			Если КоличествоСтрокФИО = 0 Тогда
				СтрокаФИО = Объект.ФИО.Добавить();
				СтрокаФИО.Владелец =
					ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ");
			Иначе
				СтрокаФИО = СтрокиФИО[0];
			КонецЕсли;
			СтрокаФИО.Фамилия = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				СвойстваОрганизации.ФамилияРук, СвойстваОрганизации.ФамилияИП);
			СтрокаФИО.Имя = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				СвойстваОрганизации.ИмяРук, СвойстваОрганизации.ИмяИП);
			СтрокаФИО.Отчество = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				СвойстваОрганизации.ОтчествоРук, СвойстваОрганизации.ОтчествоИП);
			
			ПредставительФЛ_ФИО = ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеФИО(СтрокаФИО);
			
			Если КоличествоСтрокУдостоверений = 0 Тогда
				СтрокаУдостоверения = Объект.УдостоверенияЛичности.Добавить();
				СтрокаУдостоверения.Владелец =
					ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ");
			Иначе
				СтрокаУдостоверения = СтрокиУдостоверений[0];
			КонецЕсли;
			СтрокаУдостоверения.ВидДок = СвойстваОрганизации.ВидУдостоверения;
			СтрокаУдостоверения.СерДок = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				СвойстваОрганизации.СерияУдЛичнРук, СвойстваОрганизации.СерияУдЛичн);
			СтрокаУдостоверения.НомДок = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				СвойстваОрганизации.НомерУдЛичнРук, СвойстваОрганизации.НомерУдЛичн);
			СтрокаУдостоверения.ДатаДок = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				СвойстваОрганизации.ДатаУдЛичнРук, СвойстваОрганизации.ДатаУдЛичн);
			СтрокаУдостоверения.ВыдДок = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				СвойстваОрганизации.ОрганВыданУдЛичнРук, СвойстваОрганизации.ОрганВыданУдЛичн);
			СтрокаУдостоверения.КодВыдДок = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				СвойстваОрганизации.КодПодрУдЛичнРук, СвойстваОрганизации.КодПодрУдЛичн);
			
			ПредставительФЛ_Удостоверение =
				ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеУдостоверение(СтрокаУдостоверения);
			
			Объект.ПредставительФЛ_ИНН = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.ИННРук,
				СвойстваОрганизации.ИННФЛ);
			Объект.ПредставительФЛ_ОГРН = ?(НЕ СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.ОГРН, "");
			Объект.ПредставительФЛ_СНИЛС = СвойстваОрганизации.СНИЛС;
			Объект.ПредставительФЛ_Гражданство = СвойстваОрганизации.Гражданство;
			Объект.ПредставительФЛ_ДатаРождения = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо,
				СвойстваОрганизации.ДатаРождРук, СвойстваОрганизации.ДатаРожд);
		КонецЕсли;
		
		Модифицированность = Истина;
		
		УправлениеФормой(ЭтаФорма);
		
	ИначеЕсли НЕ ПодписьЗагружена И ТипЗнч(Объект.Представитель) = Тип("СправочникСсылка.Контрагенты")
		И Объект.Представитель <> ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка") Тогда
		
		СвойстваКонтрагента = ДокументооборотСКОВызовСервера.ПолучитьСвойстваКонтрагента(Объект.Представитель);
		
		Если СвойстваКонтрагента.ЭтоЮридическоеЛицо = Истина Тогда
			Представитель_ЮридическоеЛицо = Истина;
			Объект.ПредставительЮЛ_НаимОрг = СвойстваКонтрагента.НаименованиеПолное;
			Объект.ПредставительЮЛ_ИНН = СвойстваКонтрагента.ИНН;
			Объект.ПредставительЮЛ_КПП = СвойстваКонтрагента.КПП;
			Объект.ПредставительЮЛ_ОГРН = "";
			
			Модифицированность = Истина;
			
			УправлениеФормой(ЭтаФорма);
			
		ИначеЕсли СвойстваКонтрагента.ЭтоЮридическоеЛицо = Ложь Тогда
			Представитель_ЮридическоеЛицо = Ложь;
			Если Доверитель_ЮридическоеЛицо И ПредставительЯвляетсяСотрудником Тогда
				Объект.ПредставительЮЛ_НаимОрг 	= Объект.ДоверительЮЛ_НаимОрг;
				Объект.ПредставительЮЛ_ИНН 		= Объект.ДоверительЮЛ_ИНН;
				Объект.ПредставительЮЛ_КПП 		= Объект.ДоверительЮЛ_КПП;
				Объект.ПредставительЮЛ_ОГРН 	= Объект.ДоверительЮЛ_ОГРН;
			Иначе
				Объект.ПредставительЮЛ_НаимОрг 	= "";
				Объект.ПредставительЮЛ_ИНН 		= "";
				Объект.ПредставительЮЛ_КПП 		= "";
				Объект.ПредставительЮЛ_ОГРН 	= "";
			КонецЕсли;
			
			СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
				ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
			КоличествоСтрокФИО = СтрокиФИО.Количество();
			Если КоличествоСтрокФИО = 0 Тогда
				СтрокаФИО = Объект.ФИО.Добавить();
				СтрокаФИО.Владелец =
					ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ");
			Иначе
				СтрокаФИО = СтрокиФИО[0];
			КонецЕсли;
			СтрокаФИО.Фамилия 	= СвойстваКонтрагента.Фамилия;
			СтрокаФИО.Имя 		= СвойстваКонтрагента.Имя;
			СтрокаФИО.Отчество 	= СвойстваКонтрагента.Отчество;
			
			ПредставительФЛ_ФИО = ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеФИО(СтрокаФИО);
			
			СтрокиУдостоверений = Объект.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец",
				ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
			КоличествоСтрокУдостоверений = СтрокиУдостоверений.Количество();
			Для НомерСКонца = 1 По КоличествоСтрокУдостоверений Цикл
				Объект.УдостоверенияЛичности.Удалить(СтрокиУдостоверений[КоличествоСтрокУдостоверений - НомерСКонца]);
			КонецЦикла;
			
			ПредставительФЛ_Удостоверение = "";
			
			Объект.ПредставительФЛ_ИНН = СвойстваКонтрагента.ИНН;
			Объект.ПредставительФЛ_ОГРН = "";
			Объект.ПредставительФЛ_СНИЛС = "";
			Объект.ПредставительФЛ_Гражданство = ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка");
			Объект.ПредставительФЛ_ДатаРождения = Неопределено;
			
			Модифицированность = Истина;
			
			УправлениеФормой(ЭтаФорма);
			
		КонецЕсли;
		
	ИначеЕсли НЕ ПодписьЗагружена И ТипЗнч(Объект.Представитель) = Тип("СправочникСсылка.ФизическиеЛица")
		И Объект.Представитель <> ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка") Тогда
		
		СвойстваФизическогоЛица = ДокументооборотСКОВызовСервера.ПолучитьСвойстваФизическогоЛица(Объект.Представитель);
		
		Представитель_ЮридическоеЛицо 	= Ложь;
		Если Доверитель_ЮридическоеЛицо И ПредставительЯвляетсяСотрудником Тогда
			Объект.ПредставительЮЛ_НаимОрг 	= Объект.ДоверительЮЛ_НаимОрг;
			Объект.ПредставительЮЛ_ИНН 		= Объект.ДоверительЮЛ_ИНН;
			Объект.ПредставительЮЛ_КПП 		= Объект.ДоверительЮЛ_КПП;
			Объект.ПредставительЮЛ_ОГРН 	= Объект.ДоверительЮЛ_ОГРН;
		Иначе
			Объект.ПредставительЮЛ_НаимОрг 	= "";
			Объект.ПредставительЮЛ_ИНН 		= "";
			Объект.ПредставительЮЛ_КПП 		= "";
			Объект.ПредставительЮЛ_ОГРН 	= "";
		КонецЕсли;
		
		СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
		КоличествоСтрокФИО = СтрокиФИО.Количество();
		Если КоличествоСтрокФИО = 0 Тогда
			СтрокаФИО = Объект.ФИО.Добавить();
			СтрокаФИО.Владелец = ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ");
		Иначе
			СтрокаФИО = СтрокиФИО[0];
		КонецЕсли;
		СтрокаФИО.Фамилия 	= СвойстваФизическогоЛица.ФИО.Фамилия;
		СтрокаФИО.Имя 		= СвойстваФизическогоЛица.ФИО.Имя;
		СтрокаФИО.Отчество 	= СвойстваФизическогоЛица.ФИО.Отчество;
		
		ПредставительФЛ_ФИО = ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеФИО(СтрокаФИО);
		
		СтрокиУдостоверений = Объект.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец",
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
		КоличествоСтрокУдостоверений = СтрокиУдостоверений.Количество();
		Если КоличествоСтрокУдостоверений = 0 Тогда
			СтрокаУдостоверения = Объект.УдостоверенияЛичности.Добавить();
			СтрокаУдостоверения.Владелец =
				ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ");
		Иначе
			СтрокаУдостоверения = СтрокиУдостоверений[0];
		КонецЕсли;
		СтрокаУдостоверения.ВидДок 		= СвойстваФизическогоЛица.ВидДокумента;
		СтрокаУдостоверения.СерДок 		= СвойстваФизическогоЛица.Серия;
		СтрокаУдостоверения.НомДок 		= СвойстваФизическогоЛица.Номер;
		СтрокаУдостоверения.ДатаДок 	= СвойстваФизическогоЛица.ДатаВыдачи;
		СтрокаУдостоверения.ВыдДок 		= СвойстваФизическогоЛица.КемВыдан;
		СтрокаУдостоверения.КодВыдДок 	= СвойстваФизическогоЛица.КодПодразделения;
		
		ПредставительФЛ_Удостоверение =
			ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеУдостоверение(СтрокаУдостоверения);
		
		Объект.ПредставительФЛ_ИНН 			= СвойстваФизическогоЛица.ИНН;
		Объект.ПредставительФЛ_ОГРН 		= "";
		Объект.ПредставительФЛ_СНИЛС 		= СвойстваФизическогоЛица.СНИЛС;
		Объект.ПредставительФЛ_Гражданство 	= СвойстваФизическогоЛица.Гражданство;
		Объект.ПредставительФЛ_ДатаРождения = СвойстваФизическогоЛица.ДатаРождения;
		
		Модифицированность = Истина;
		
		УправлениеФормой(ЭтаФорма);
		
	ИначеЕсли ПодписьЗагружена И ТипОтправителя = "1" Тогда
		Объект.Организация = ?(ТипЗнч(Объект.Представитель) = Тип("СправочникСсылка.Организации"),
			Объект.Представитель, ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиРеквизитыПредставителя()
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ПредставительЮЛ_НаимОрг", Объект.ПредставительЮЛ_НаимОрг);
	СтруктураДанных.Вставить("ПредставительЮЛ_ИНН", 	Объект.ПредставительЮЛ_ИНН);
	СтруктураДанных.Вставить("ПредставительЮЛ_КПП", 	Объект.ПредставительЮЛ_КПП);
	СтруктураДанных.Вставить("ПредставительЮЛ_ОГРН", 	Объект.ПредставительЮЛ_ОГРН);
	СтруктураДанных.Вставить("ТолькоПросмотрФормы", 	УведомлениеОтправлено ИЛИ ПодписьЗагружена);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиРеквизитыПредставителяПослеВвода",
		ЭтотОбъект);
	ОткрытьФорму(
		"Справочник.МашиночитаемыеДоверенностиФНС.Форма.ФормаПредставителя",
		Новый Структура("СтруктураДанных", СтруктураДанных),
		ЭтаФорма,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиРеквизитыПредставителяПослеВвода(
		СтруктураДанных,
		ДополнительныеПараметры) Экспорт
	
	Если СтруктураДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураДанных.ПредставительЮЛ_НаимОрг) И НЕ ЗначениеЗаполнено(Объект.Представитель) Тогда
		Представитель_ЮридическоеЛицо = Истина;
		Элементы.Представитель.ОграничениеТипа = Новый ОписаниеТипов;
		Объект.Представитель = СтруктураДанных.ПредставительЮЛ_НаимОрг;
	КонецЕсли;
	
	Объект.ПредставительЮЛ_НаимОрг 	= СтруктураДанных.ПредставительЮЛ_НаимОрг;
	Объект.ПредставительЮЛ_ИНН 		= СтруктураДанных.ПредставительЮЛ_ИНН;
	Объект.ПредставительЮЛ_КПП 		= СтруктураДанных.ПредставительЮЛ_КПП;
	Объект.ПредставительЮЛ_ОГРН 	= СтруктураДанных.ПредставительЮЛ_ОГРН;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииПослеПолученияКонтекстаЭДО(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРасширеннаяНастройкаСрокаДействияНажатиеПослеВвода(
		СтруктураДанных,
		ДополнительныеПараметры) Экспорт
	
	Если СтруктураДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ДатаОкончания = СтруктураДанных.ДокументДатаОкончания;
	Объект.СрокДействия = СтруктураДанных.ДокументСрокДействия;
	
	Модифицированность = Истина;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставительФЛ_ФИОНачалоВыбораПослеВвода(РезультатРедактирования, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(РезультатРедактирования) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиФИО = Объект.ФИО.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
	
	КоличествоСтрокФИО = СтрокиФИО.Количество();
	Если КоличествоСтрокФИО = 0 Тогда
		СтрокаФИО = Объект.ФИО.Добавить();
		СтрокаФИО.Владелец = ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ");
	Иначе
		СтрокаФИО = СтрокиФИО[0];
	КонецЕсли;
	СтрокаФИО.Фамилия 	= РезультатРедактирования.Фамилия;
	СтрокаФИО.Имя 		= РезультатРедактирования.Имя;
	СтрокаФИО.Отчество 	= РезультатРедактирования.Отчество;
	
	ПредставительФЛ_ФИО = ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеФИО(СтрокаФИО);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставительФЛ_УдостоверениеНачалоВыбораПослеВвода(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(РезультатВыбора) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиУдостоверений = Объект.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
	
	КоличествоСтрокУдостоверений = СтрокиУдостоверений.Количество();
	Если КоличествоСтрокУдостоверений = 0 Тогда
		СтрокаУдостоверения = Объект.УдостоверенияЛичности.Добавить();
		СтрокаУдостоверения.Владелец =
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ");
	Иначе
		СтрокаУдостоверения = СтрокиУдостоверений[0];
	КонецЕсли;
	СтрокаУдостоверения.ВидДок 		= РезультатВыбора.ДокументВид;
	СтрокаУдостоверения.СерДок 		= РезультатВыбора.ДокументСерия;
	СтрокаУдостоверения.НомДок 		= РезультатВыбора.ДокументНомер;
	СтрокаУдостоверения.ДатаДок 	= РезультатВыбора.ДокументДатаВыдачи;
	СтрокаУдостоверения.ВыдДок 		= РезультатВыбора.ДокументКемВыдан;
	СтрокаУдостоверения.КодВыдДок 	= РезультатВыбора.ДокументКодПодразделения;
	
	ПредставительФЛ_Удостоверение =
		ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеУдостоверение(СтрокаУдостоверения);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КодНалоговогоОрганаПредставленияНачалоВыбораПослеВвода(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.КодНалоговогоОрганаПредставления = РезультатВыбора.КодНО;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНалоговыеОрганыДействияНажатиеПослеВвода(
		СтруктураДанных,
		ДополнительныеПараметры) Экспорт
	
	Если СтруктураДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.НалоговыеОрганыДействия.Очистить();
	Для каждого КодНалоговогоОрганаДействия Из СтруктураДанных.КодыНалоговыхОргановДействия Цикл
		НалоговыйОрганДействия = Объект.НалоговыеОрганыДействия.Добавить();
		НалоговыйОрганДействия.КодНалоговогоОргана = КодНалоговогоОрганаДействия;
	КонецЦикла;
	
	Модифицированность = Истина;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПолномочияПредставителяНажатиеПослеВвода(
		СтруктураДанных,
		ДополнительныеПараметры) Экспорт
	
	Если СтруктураДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ПолномочияПредставителя.Количество() = 0 Тогда
		ПолномочиеПредставителя = Объект.ПолномочияПредставителя.Добавить();
	Иначе
		ПолномочиеПредставителя = Объект.ПолномочияПредставителя[0];
	КонецЕсли;
	
	ПолномочиеПредставителя.ПризнакПолныеПолномочия = СтруктураДанных.ПризнакПолныеПолномочия;
	Для ВидПолномочия = 1 По КоличествоВидовПолномочийПредставителя Цикл
		ПредставлениеВида = Формат(ВидПолномочия, "ЧЦ=2; ЧВН=");
		ПолномочиеПредставителя["Признак" + ПредставлениеВида] = СтруктураДанных["Признак" + ПредставлениеВида];
	КонецЦикла;
	
	Модифицированность = Истина;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПослеПодтвержденияПриПредупреждении(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПодписьЗагружена Тогда
		РезультатыПроверки = ДокументооборотСКОВызовСервера.ПроверитьМЧД(Объект);
		
		Если РезультатыПроверки.Количество() <> 0 Тогда
			Для каждого РезультатПроверки Из РезультатыПроверки Цикл
				ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатПроверки.ТекстОшибки,,
					РезультатПроверки.Поле);
			КонецЦикла;
			
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если (Модифицированность ИЛИ Параметры.Ключ.Пустая())
		И НЕ Записать() ИЛИ НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПослеОтправки", ЭтотОбъект);
	КонтекстЭДОКлиент.ОтправкаРегламентированногоОтчетаВФНС(Объект.Ссылка, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПослеОтправки(Результат, ДополнительныеПараметры) Экспорт
	
	УведомлениеОтправлено = ПослеОтправкиНаСервере();
	
	// Перерисовка статуса отправки в форме Отчетность
	ПараметрыОповещения = Новый Структура(); 
	ПараметрыОповещения.Вставить("Ссылка", Объект.Ссылка);
	ПараметрыОповещения.Вставить("Организация", Объект.Организация);
	Оповестить("Завершение отправки", ПараметрыОповещения, Объект.Ссылка);
	
	Если Открыта() И УведомлениеОтправлено Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПослеЗагрузки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		Прочитать();
		Инициализация();
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПослеОтправкиНаСервере()
	
	УведомлениеОтправлено = УведомлениеОтправлено(Объект.Ссылка);
	УправлениеФормой(ЭтаФорма);
	ПрорисоватьСтатус(ЭтаФорма);
	
	Возврат УведомлениеОтправлено;
	
КонецФункции

&НаСервереБезКонтекста
Функция КонтекстЭДОСервер()
	
	Возврат ДокументооборотСКО.ПолучитьОбработкуЭДО();
	
КонецФункции

&НаСервереБезКонтекста
Функция УведомлениеОтправлено(Ссылка)
	
	СтатусОтправки = КонтекстЭДОСервер().ПолучитьСтатусОтправкиОбъекта(Ссылка);
	
	УведомлениеОтправлено =
		ЗначениеЗаполнено(СтатусОтправки) И СтатусОтправки <> Перечисления.СтатусыОтправки.ВКонверте;
	
	Возврат УведомлениеОтправлено;
	
КонецФункции

&НаСервере
Процедура СохранитьСтатусОтправкиУведомления()
	
	РегламентированнаяОтчетность.СохранитьСтатусОтправкиУведомления(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти
