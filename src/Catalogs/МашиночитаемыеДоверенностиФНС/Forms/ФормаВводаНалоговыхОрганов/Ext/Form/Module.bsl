
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Параметры.СтруктураДанных;
	
	Если СтруктураДанных.Свойство("ТолькоПросмотрФормы") И СтруктураДанных.ТолькоПросмотрФормы Тогда
		Элементы.ТипНалоговыхОргановДействия.ТолькоПросмотр = Истина;
		Элементы.НалоговыеОрганыДействия.ТолькоПросмотр 	= Истина;
		Элементы.ФормаКнопкаСохранить.Доступность 			= Ложь;
	КонецЕсли;
	
	ТипНалоговыхОргановДействия = ?(СтруктураДанных.КодыНалоговыхОргановДействия.Количество() > 0, "1", "0");
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	РегистрацииВНалоговомОргане.Код КАК Код,
		|	РегистрацииВНалоговомОргане.НаименованиеИФНС КАК НаименованиеИФНС
		|ИЗ
		|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
		|ГДЕ
		|	(РегистрацииВНалоговомОргане.Владелец = &Организация
		|	ИЛИ РегистрацииВНалоговомОргане.Владелец = &ГоловнаяОрганизация)
		|	И РегистрацииВНалоговомОргане.ПометкаУдаления = ЛОЖЬ
		|УПОРЯДОЧИТЬ ПО
		|	РегистрацииВНалоговомОргане.Код");
	Запрос.УстановитьПараметр("Организация", СтруктураДанных.Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",
		РегламентированнаяОтчетность.ГоловнаяОрганизация(СтруктураДанных.Организация));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НалоговыйОрганДействия = НалоговыеОрганыДействия.Добавить();
		НалоговыйОрганДействия.Пометка = СтруктураДанных.КодыНалоговыхОргановДействия.Найти(Выборка.Код) <> Неопределено;
		НалоговыйОрганДействия.Код = Выборка.Код;
		НалоговыйОрганДействия.Наименование = Выборка.НаименованиеИФНС;
	КонецЦикла;
	
	Для каждого КодНалоговогоОрганаДействия Из СтруктураДанных.КодыНалоговыхОргановДействия Цикл
		ПараметрыОтбора = Новый Структура("Код", КодНалоговогоОрганаДействия);
		Если НалоговыеОрганыДействия.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
			НалоговыйОрганДействия = НалоговыеОрганыДействия.Добавить();
			НалоговыйОрганДействия.Пометка = Истина;
			НалоговыйОрганДействия.Код = КодНалоговогоОрганаДействия;
			НалоговыйОрганДействия.Наименование = "";
		КонецЕсли;
	КонецЦикла;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипНалоговыхОргановДействияПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить(Команда)
	
	Элементы.НалоговыеОрганыДействия.ДобавитьСтроку();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого Стр Из НалоговыеОрганыДействия Цикл
		Стр.Пометка = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого Стр Из НалоговыеОрганыДействия Цикл
		Стр.Пометка = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если НЕ СохранениеВозможно() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("КодыНалоговыхОргановДействия", Новый Массив);
	
	Если ТипНалоговыхОргановДействия = "1" Тогда
		Для каждого Стр Из НалоговыеОрганыДействия Цикл
			Если Стр.Пометка Тогда
				СтруктураДанных.КодыНалоговыхОргановДействия.Добавить(Стр.Код);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Закрыть(СтруктураДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Форма.Элементы.НалоговыеОрганыДействия.Доступность = (Форма.ТипНалоговыхОргановДействия = "1");
	
КонецПроцедуры

&НаКлиенте
Функция СохранениеВозможно()
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	Если ТипНалоговыхОргановДействия = "1" Тогда
		ЕстьНалоговыеОрганыДействия = Ложь;
		Для каждого Стр Из НалоговыеОрганыДействия Цикл
			Если Стр.Пометка Тогда
				ЕстьНалоговыеОрганыДействия = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НЕ ЕстьНалоговыеОрганыДействия Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Не выбраны налоговые органы, для которых действует доверенность.'"),,
				"НалоговыеОрганыДействия",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

#КонецОбласти
