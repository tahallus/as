
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокПолучателей();
	
	УстановитьПодсказку();
	
	Если ТипЗнч(Параметры.Ссылка) <> Тип("СправочникСсылка.НастройкиПубликацииМЛК") Тогда
		Элементы.ОтправитьНеподтвержденнымПользователям.Доступность = Ложь;
		Элементы.СписокПолучателей.Доступность = Ложь;
		Элементы.КомандаОК.Заголовок = НСтр("ru='Закрыть'");
	Иначе
		Элементы.ГруппаПодсказкаОРедактированииСпискаПолучателей.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ОтправитьНеподтвержденнымПользователям", ОтправитьНеподтвержденнымПользователям);
	
	ВыбранныеПолучатели = Новый Массив;
	ИдентификаторыПолучателей = Новый Массив;
	
	Для каждого Реквизит Из СписокПолучателей Цикл
		
		Если Реквизит.Пометка Тогда
			ВыбранныеПолучатели.Добавить(Реквизит.Получатель);
			ИдентификаторыПолучателей.Добавить(Строка(Реквизит.Получатель.УникальныйИдентификатор()));
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураВозврата.Вставить("ВыбранныеПолучатели", ВыбранныеПолучатели);
	СтруктураВозврата.Вставить("ИдентификаторыПолучателей", ИдентификаторыПолучателей);
	
	АдресСпискаПолучателей = ПоместитьВоВременноеХранилище(СтруктураВозврата, Новый УникальныйИдентификатор);
	
	Закрыть(Новый Структура("АдресСпискаПолучателей, ПодсказкаБылаЗакрыта", АдресСпискаПолучателей, ПодсказкаБылаЗакрыта));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для каждого Строка Из СписокПолучателей Цикл
	
		Строка.Пометка = Истина;
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для каждого Строка Из СписокПолучателей Цикл
	
		Строка.Пометка = Ложь;
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказкаАссистентаПонятно(Команда)
	
	Элементы.ГруппаСтраницыПодсказкаАссистента.Видимость = Ложь;
	ПодсказкаБылаЗакрыта = Истина;
	Элементы.ОтправитьНеподтвержденнымПользователям.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПодсказку()
	
	Если ТипЗнч(Параметры.Ссылка) = Тип("СправочникСсылка.НастройкиПубликацииМЛК") Тогда
		
		СтруктураВСтроке = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ссылка, "ПодсказкиАссистента");
		
		Если СтруктураВСтроке <> "" Тогда
			МассивСПодсказками = КонструкторМобильногоПриложения.ЧтениеJSONВСтруктуру(СтруктураВСтроке);
			Если МассивСПодсказками.Найти("НеподтвержденныеПользователи") <> Неопределено Тогда
				Элементы.ГруппаСтраницыПодсказкаАссистента.Видимость = Ложь;
				Элементы.ОтправитьНеподтвержденнымПользователям.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Элементы.ГруппаСтраницыПодсказкаАссистента.Видимость = Ложь;
		Элементы.ОтправитьНеподтвержденнымПользователям.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	КонецЕсли;
	

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПолучателей()
	
	МассивВыбранныеПолучатели = Новый массив;
	Если Параметры.АдресСпискаПолучателей <> "" Тогда
		
		СтруктураПолучатели = ПолучитьИзВременногоХранилища(Параметры.АдресСпискаПолучателей);
		
		Если ТипЗнч(СтруктураПолучатели) = Тип("Структура") Тогда
			ОтправитьНеподтвержденнымПользователям = СтруктураПолучатели.ОтправитьНеподтвержденнымПользователям;
			МассивВыбранныеПолучатели = СтруктураПолучатели.ВыбранныеПолучатели;
		КонецЕсли;
		
	КонецЕсли;
	
	СписокКонтрагентов = ПолучитьСписокКонтрагентов();
	
	Для каждого Контрагент Из СписокКонтрагентов Цикл
		
		НовСтр = СписокПолучателей.Добавить();
		НовСтр.Получатель = Контрагент;
		
		Если МассивВыбранныеПолучатели.Найти(НовСтр.Получатель) = Неопределено Тогда
			НовСтр.Пометка = Ложь;
		Иначе
			НовСтр.Пометка = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокКонтрагентов()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказПокупателя.Контрагент КАК Контрагент
		|ИЗ
		|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
		|ГДЕ
		|	ЗаказПокупателя.СозданИзКабинетаКлиента
		|	И ЗаказПокупателя.ДатаИзменения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказПокупателя.Контрагент";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Контрагент");

КонецФункции // ()

#КонецОбласти