#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		
		Возврат;
		
	КонецЕсли;
	
	НомерГрузовойДекларации = СокрЛП(Код);
	Если ПустаяСтрока(НомерГрузовойДекларации) Тогда
		
		ВызватьИсключение НСтр("ru ='Запись декларации с пустым номером не допускается'");
		
	КонецЕсли;
	
	РазделительДекларации = "/";
	
	МассивДопустимыхРазделителей = Новый Массив;
	МассивДопустимыхРазделителей.Добавить("-");
	МассивДопустимыхРазделителей.Добавить("\");
	МассивДопустимыхРазделителей.Добавить("|");
	МассивДопустимыхРазделителей.Добавить("~");
	
	Для каждого ВторичныйРазделитель Из МассивДопустимыхРазделителей Цикл
		
		Код = СтрЗаменить(Код, ВторичныйРазделитель, РазделительДекларации);
		
	КонецЦикла;
	
	ДанныеНомераГТД = Справочники.НомераГТД.ДанныеНомераГТД(Код, РазделительДекларации);
	
	Ошибки = Справочники.НомераГТД.ПроверитьКорректностьЗаполненияНомераГТД(ДанныеНомераГТД);
	Если Ошибки <> Неопределено Тогда
		
		Если ДопускаетсяЗаписьСОшибкой Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
			
		Иначе
			
			ТекстИсключения = "";
			Для каждого Ошибка Из Ошибки.СписокОшибок Цикл
				
				ТекстИсключения = ТекстИсключения + ?(ПустаяСтрока(ТекстИсключения), "", Символы.ПС) + Ошибка.ТекстДляОднойОшибки;
				
			КонецЦикла;
			
			ВызватьИсключение ТекстИсключения;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеНомераГТД);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли