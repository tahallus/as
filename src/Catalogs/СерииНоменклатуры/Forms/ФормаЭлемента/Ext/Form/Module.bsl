
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = УправлениеСвойствамиУНФ.ЗаполнитьДополнительныеПараметры(Объект, "СтраницаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Элементы.ГруппаЗаполнить.Видимость	= Ложь;
		Элементы.ГруппаПродан.Видимость	= Истина;
	Иначе
		Элементы.ГруппаЗаполнить.Видимость = Истина;
		Элементы.ГруппаПродан.Видимость	= Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("СрокГодности") Тогда
		Элементы.ДатаПроизводства.Видимость = Параметры.ДатаПроизводства;
		Элементы.ГоденДо.Видимость = Параметры.СрокГодности;
	Иначе
		Элементы.ДатаПроизводства.Видимость = Объект.Владелец.ИспользоватьДатуПроизводстваСерии;
		Элементы.ГоденДо.Видимость = Объект.Владелец.ИспользоватьСрокГодностиСерии;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Владелец.ТочностьУказанияСрокаГодностиСерии) Тогда
		Если Объект.Владелец.ТочностьУказанияСрокаГодностиСерии = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоДней Тогда
			Элементы.ДатаПроизводства.ФорматРедактирования = "ДФ=дд.ММ.гггг";
			Элементы.ГоденДо.ФорматРедактирования = "ДФ=дд.ММ.гггг";
		Иначе
			Элементы.ДатаПроизводства.ФорматРедактирования = "ДФ='дд.ММ.гггг ЧЧ:00'";
			Элементы.ГоденДо.ФорматРедактирования = "ДФ='дд.ММ.гггг ЧЧ:00'";
		КонецЕсли;
	Иначе
		Элементы.ДатаПроизводства.ФорматРедактирования = "ДФ=дд.ММ.гггг";
		Элементы.ГоденДо.ФорматРедактирования = "ДФ=дд.ММ.гггг";
	КонецЕсли;
	
	Если Объект.Продан Тогда
		ДанныеГарантии = Справочники.СерииНоменклатуры.СрокДействияГарантии(Объект.Ссылка, ТекущаяДата());
		Если ДанныеГарантии.Количество()>0 Тогда
			ПродажаИнфо = ?(ДанныеГарантии.Гарантия, 
				Строка(ДанныеГарантии.ДокументПродажи)+", гарантийный срок до "+ДанныеГарантии.СрокДействияГарантии,
				Строка(ДанныеГарантии.ДокументПродажи));
			ДокументПродажи = ДанныеГарантии.ДокументПродажи;
		КонецЕсли;
	КонецЕсли;
	
	ВидМаркировки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "ВидМаркировки");
	Элементы.ГруппаRFIDГИСМ.Видимость = ЗначениеЗаполнено(ВидМаркировки) И ВидМаркировки <> Перечисления.ВидыМаркировки.НеМаркируется;
	
	ЕдиницаИзмеренияСрокаГодности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "ЕдиницаИзмеренияСрокаГодности");
	СрокГодности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "СрокГодности");
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры // ОбработкаПроверкиЗаполненияНаСервере()

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры // ОбработкаОповещения()

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик механизма "Свойства".
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры // ПередЗаписьюНаСервере()

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДатаПроизводстваПриИзменении(Элемент)
	
	Если Элементы.ГоденДо.Видимость И СрокГодности > 0 Тогда
		Объект.ГоденДо = СерииНоменклатурыУНФКлиентСервер.ГоденДоПоДатеПроизводства(Объект.ДатаПроизводства, СрокГодности, ЕдиницаИзмеренияСрокаГодности)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГоденДоПриИзменении(Элемент)
	
	Если Элементы.ГоденДо.Видимость И СрокГодности > 0 Тогда
		Объект.ДатаПроизводства = СерииНоменклатурыУНФКлиентСервер.ДатаПроизводстваПоГоденДо(Объект.ГоденДо, СрокГодности, ЕдиницаИзмеренияСрокаГодности)
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьДополнительныйРеквизит(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущийНаборСвойств", ПредопределенноеЗначение("Справочник.НаборыДополнительныхРеквизитовИСведений.Справочник_СерииНоменклатуры"));
	ПараметрыФормы.Вставить("ЭтоДополнительноеСведение", Ложь);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта", ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры // Подключаемый_РедактироватьСоставСвойств()

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма, РеквизитФормыВЗначение("Объект"));
	
КонецПроцедуры // ОбновитьЭлементыДополнительныхРеквизитов()

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ДобавитьСерияНаСервере()
	
	Объект.Наименование = СерииНоменклатурыУНФ.ДобавитьСерия(Объект.Владелец, ШаблонСерииНоменклатуры).НовыйНомер;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСерия(Команда)
	
	ДобавитьСерияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродажаИнфоНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСФормойДокументаКлиент.ОткрытьФормуДокументаПоСсылке(ДокументПродажи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если СокрЛП(Объект.Наименование)="" Тогда
	    Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Номер серии не заполнен!'");
		Сообщение.Сообщить();
	КонецЕсли; 
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#КонецОбласти