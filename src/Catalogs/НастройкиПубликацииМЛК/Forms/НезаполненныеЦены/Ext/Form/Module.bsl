
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокВыбранныхВидовЦен.ЗагрузитьЗначения(Параметры.СписокВыбранныхВидовЦен);
	СписокВыбранныхТоваров.ЗагрузитьЗначения(Параметры.СписокВыбранныхТоваров);
	ЗаполнитьТаблицу();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура НезаполненныеЦеныВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущийЭлемент.Имя = "НезаполненныеЦеныНоменклатура" Тогда
		ПараметрыНоменклатуры = Новый Структура("Ключ", Элемент.ТекущиеДанные.Номенклатура);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытиеФормыНоменклатурыЗавершение", ЭтотОбъект);
		ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЭлемента", ПараметрыНоменклатуры,,,,,ОписаниеОповещения);
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицу()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ втНоменклатура
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка В(&СписокВыбранныхТоваров)
		|	И НЕ Номенклатура.ЭтоГруппа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидЦен.Ссылка КАК ВидЦен
		|ПОМЕСТИТЬ втВыбранныеВидыЦен
		|ИЗ
		|	Справочник.ВидыЦен КАК ВидЦен
		|ГДЕ
		|	ВидЦен.Ссылка В(&СписокВыбранныхВидовЦен)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втНоменклатура.Ссылка КАК Ссылка,
		|	втВыбранныеВидыЦен.ВидЦен КАК ВидЦен
		|ПОМЕСТИТЬ втНоменклатураВидыЦен
		|ИЗ
		|	втНоменклатура КАК втНоменклатура,
		|	втВыбранныеВидыЦен КАК втВыбранныеВидыЦен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втНоменклатураВидыЦен.Ссылка КАК Ссылка,
		|	втНоменклатураВидыЦен.ВидЦен КАК ВидЦен,
		|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
		|ПОМЕСТИТЬ втВсеЦены
		|ИЗ
		|	втНоменклатураВидыЦен КАК втНоменклатураВидыЦен
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних КАК ЦеныНоменклатурыСрезПоследних
		|		ПО втНоменклатураВидыЦен.Ссылка = ЦеныНоменклатурыСрезПоследних.Номенклатура
		|			И втНоменклатураВидыЦен.ВидЦен = ЦеныНоменклатурыСрезПоследних.ВидЦен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втВсеЦены.Ссылка КАК Номенклатура,
		|	втВсеЦены.ВидЦен КАК ВидЦен
		|ИЗ
		|	втВсеЦены КАК втВсеЦены
		|ГДЕ
		|	втВсеЦены.Цена ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("СписокВыбранныхВидовЦен", СписокВыбранныхВидовЦен);
	Запрос.УстановитьПараметр("СписокВыбранныхТоваров", СписокВыбранныхТоваров);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	НезаполненныеЦены.Очистить();
	ТекущаяНоменклатура = Неопределено;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ТекущаяНоменклатура <> ВыборкаДетальныеЗаписи.Номенклатура Тогда
			ТекущаяНоменклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			НоваяСтрока = НезаполненныеЦены.Добавить();
			НоваяСтрока.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			НоваяСтрока.ОтсутствующиеЦены = Строка(ВыборкаДетальныеЗаписи.ВидЦен);
		Иначе
			НоваяСтрока.ОтсутствующиеЦены = НоваяСтрока.ОтсутствующиеЦены + "; " + Строка(ВыборкаДетальныеЗаписи.ВидЦен);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытиеФормыНоменклатурыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьТаблицу();
	
КонецПроцедуры

#КонецОбласти

