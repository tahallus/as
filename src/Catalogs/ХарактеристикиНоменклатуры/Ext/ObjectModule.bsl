#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ЭтоНовыйОбъект;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("НоменклатураСсылка") Тогда
			
			Владелец = ДанныеЗаполнения.НоменклатураСсылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	// Требуется заполнять служебные свойства в режиме ОбменДанными.Загрузка
	Если ОбменДанными.Загрузка И ДополнительныеСвойства.Свойство("ОтключитьМеханизмРегистрацииОбъектов") Тогда
		Возврат;
	КонецЕсли;
	
	КатегорииНоменклатурыСервер.ПроверкаЗаполненияСвойствПередЗаписью(ЭтотОбъект, Отказ);
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоНовыйОбъект = ЭтоНовый();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// Требуется заполнять служебные свойства в режиме ОбменДанными.Загрузка
	Если ОбменДанными.Загрузка И ДополнительныеСвойства.Свойство("ОтключитьМеханизмРегистрацииОбъектов") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		Категория = Владелец.КатегорияНоменклатуры;
	ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.КатегорииНоменклатуры") Тогда
		Категория = Владелец;
	КонецЕсли;
	КатегорииНоменклатурыСервер.ПроверкаЗаполненияСвойствПриЗаписи(ЭтотОбъект, Категория, Отказ);
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовыйОбъект И Владелец.ПроверятьЗаполнениеХарактеристики Тогда
		ИспользоватьКакОсновнуюПервуюХарактеристику(Ссылка, Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Производит запись в регистр сведений значение характеристики по умолчанию для номенклатуры
//
Процедура ИспользоватьКакОсновную(НоменклатураДляЗаписи, Характеристика, Отказ)
	
	НаборЗаписей = РегистрыСведений.ЗначенияНоменклатурыПоУмолчанию.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Номенклатура.Установить(НоменклатураДляЗаписи);
	НаборЗаписей.Прочитать();
	
	Если НЕ НаборЗаписей.Количество()
		Тогда
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Номенклатура = НоменклатураДляЗаписи;
		НоваяЗапись.Характеристика = Характеристика;     
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ТекстСообщения = НСтр("ru = 'При записи Характеристики произошла ошибка!
			|Дополнительное описание: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			
			Отказ = Истина;
		КонецПопытки;  	
	КонецЕсли;
	
КонецПроцедуры

// Производит запись в регистр сведений значение характеристики по умолчанию для номенклатуры если это первая характеристика
//
Процедура ИспользоватьКакОсновнуюПервуюХарактеристику(СсылкаНаНовуюХарактеристику, Отказ)
	
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Номенклатура") И Владелец.ПроверятьЗаполнениеХарактеристики 
		Тогда
		Если КоличествоХарактеристик(Владелец, Владелец.КатегорияНоменклатуры) = 1 И НоменклатураВДокументахСервер.ЗначенияНоменклатурыПоУмолчанию(Владелец) = Неопределено
			Тогда
			ИспользоватьКакОсновную(Владелец,  СсылкаНаНовуюХарактеристику, Отказ);
		КонецЕсли;
	ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.КатегорииНоменклатуры") И Владелец.ПроверятьЗаполнениеХарактеристики
		Тогда
		Если КоличествоХарактеристик(Владелец) = 1
			Тогда
			ИспользоватьКакОсновную(Владелец, СсылкаНаНовуюХарактеристику, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает количество характеристик по владельцу 
//
Функция КоличествоХарактеристик(ВладелецХарактеристики, КатегорияНоменклатуры = Неопределено)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Владелец", ВладелецХарактеристики);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ХарактеристикиНоменклатуры.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|ГДЕ
	|	ХарактеристикиНоменклатуры.Владелец = &Владелец";
	
	Результат = Запрос.Выполнить().Выбрать();
	
	КоличествоПоВладельцу = Результат.Количество();
	
	КоличествоПоКатегории = 0;
	
	Если Не КатегорияНоменклатуры = Неопределено
		Тогда	
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Владелец", КатегорияНоменклатуры);
		
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ХарактеристикиНоменклатуры.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
		|ГДЕ
		|	ХарактеристикиНоменклатуры.Владелец = &Владелец";
		
		Результат = Запрос.Выполнить().Выбрать();
		
		КоличествоПоКатегории = Результат.Количество();
	КонецЕсли;
	
	Возврат КоличествоПоВладельцу + КоличествоПоКатегории;
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли