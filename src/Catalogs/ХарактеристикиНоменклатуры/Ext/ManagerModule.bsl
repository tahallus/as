#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Владелец") И ТипЗнч(Параметры.Отбор.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		// Если задана связь параметров выбора по значению номенклатуры,
		// то дополним параметры выбора отбором по владельцу - номенклатурной группе.
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Отбор.Владелец, "КатегорияНоменклатуры, ТипНоменклатуры, ИспользоватьХарактеристики, ЭтоНабор");
		
		Номенклатура 		  = Параметры.Отбор.Владелец;
		КатегорияНоменклатуры = ЗначенияРеквизитов.КатегорияНоменклатуры;
		
		ТекстСообщения = "";
		Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнена номенклатура!'");
		ИначеЕсли Параметры.Свойство("ЭтоПриходныйДокумент") И ЗначенияРеквизитов.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда
			ТекстСообщения = НСтр("ru = 'Для услуг сторонних контрагентов не ведется учет по характеристикам!'");
		ИначеЕсли НЕ ЗначенияРеквизитов.ИспользоватьХарактеристики Тогда
			ТекстСообщения = НСтр("ru = 'Для номенклатуры не ведется учет по характеристикам!'");
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ТекстСообщения) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			СтандартнаяОбработка = Ложь;
			Возврат;
		КонецЕсли;
		
		// Наборы
		Если НЕ Параметры.Свойство("ЭтоПриходныйДокумент") И ЗначенияРеквизитов.ЭтоНабор Тогда
			СтандартнаяОбработка = Ложь;
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
			Запрос.УстановитьПараметр("КатегорияНоменклатуры", КатегорияНоменклатуры);
			Если Параметры.Свойство("СтрокаПоиска") И НЕ ПустаяСтрока(Параметры.СтрокаПоиска) Тогда
				Запрос.УстановитьПараметр("Текст", "%"+Параметры.СтрокаПоиска+"%");
			Иначе
				Запрос.УстановитьПараметр("Текст", "");
			КонецЕсли; 
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ХарактеристикиНоменклатуры.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
			|ГДЕ
			|	(ХарактеристикиНоменклатуры.Владелец = &Номенклатура
			|			ИЛИ ХарактеристикиНоменклатуры.Владелец = &КатегорияНоменклатуры)
			|	И (&Текст = """"
			|			ИЛИ ХарактеристикиНоменклатуры.Наименование ПОДОБНО &Текст)
			|	И НЕ ХарактеристикиНоменклатуры.ПометкаУдаления
			|	И НЕ ХарактеристикиНоменклатуры.Недействителен
			|
			|УПОРЯДОЧИТЬ ПО
			|	ХарактеристикиНоменклатуры.Наименование";
			Выборка = Запрос.Выполнить().Выбрать();
			ДанныеВыбора = Новый СписокЗначений;
			ТекстБезХарактеристики = НСтр("ru = '<Без характеристики>'");
			Если НЕ Параметры.Свойство("СтрокаПоиска") ИЛИ ПустаяСтрока(Параметры.СтрокаПоиска) ИЛИ СтрНайти(ТекстБезХарактеристики, Параметры.СтрокаПоиска)>0 Тогда
				ДанныеВыбора.Добавить(Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка(), ТекстБезХарактеристики);
			КонецЕсли; 
			Пока Выборка.Следующий() Цикл
				ДанныеВыбора.Добавить(Выборка.Ссылка);
			КонецЦикла; 
			Возврат;
		КонецЕсли; 
		// Конец Наборы
		
		МассивОтбора = Новый Массив;
		МассивОтбора.Добавить(Номенклатура);
		МассивОтбора.Добавить(КатегорияНоменклатуры);
		
		Параметры.Отбор.Вставить("Владелец", МассивОтбора);
		
	КонецЕсли;
	
	Если Не Параметры.Отбор.Свойство("Недействителен") Тогда
		Параметры.Отбор.Вставить("Недействителен", Ложь);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПолученияДанныхВыбора()

#КонецОбласти

#Область ИнтерфейсПечати
// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеОбработчики

// Функция возвращает список имен «ключевых» реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	
	Возврат Результат;
	
КонецФункции // ПолучитьБлокируемыеРеквизитыОбъекта()

#КонецОбласти

#Область ОбработчикиПодсистем

#Область ЗагрузкаДанныхИзВнешнегоИсточника

Процедура ПриОпределенииОбразцовЗагрузкиДанных(НастройкиЗагрузкиДанных, УникальныйИдентификатор) Экспорт
	
	Образец_xlsx = ПолучитьМакет("ОбразецЗагрузкиДанных_xlsx");
	ОбразецЗагрузкиДанных_xlsx = ПоместитьВоВременноеХранилище(Образец_xlsx, УникальныйИдентификатор);
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_xlsx", ОбразецЗагрузкиДанных_xlsx);
	
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_mxl", "ОбразецЗагрузкиДанных_mxl");
	
	Образец_csv = ПолучитьМакет("ОбразецЗагрузкиДанных_csv");
	ОбразецЗагрузкиДанных_csv = ПоместитьВоВременноеХранилище(Образец_csv, УникальныйИдентификатор);
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_csv", ОбразецЗагрузкиДанных_csv);
	
КонецПроцедуры

Процедура ПоляЗагрузкиДанныхИзВнешнегоИсточника(ТаблицаПолейЗагрузки, НастройкиЗагрузкиДанных) Экспорт
	
	//
	// Для группы полей действует правило: хотя бы одно поле в группе должно быть выбрано в колонках
	//
	
	ОписаниеТиповСтрока11 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(11));
	ОписаниеТиповСтрока25 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(25));
	ОписаниеТиповСтрока50 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50));
	ОписаниеТиповСтрока150 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(150));
	ОписаниеТиповСтрока0000 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(0));
	
	Если НастройкиЗагрузкиДанных.ФиксированныйШаблон Тогда
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "УИД", НСтр("ru = 'УИД'"), ОписаниеТиповСтрока50, ОписаниеТиповСтрока50);
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "УИДВладелец", НСтр("ru = 'УИД Владельца'"), ОписаниеТиповСтрока50, ОписаниеТиповСтрока50);
	КонецЕсли; 
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ХарактеристикаНаименование", НСтр("ru = 'Характеристика (наименование)'"), ОписаниеТиповСтрока150, ОписаниеТиповКолонка, "Характеристика", 1, Истина, Истина);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ХарактеристикаНаименованиеДляПечати", НСтр("ru = 'Характеристика (наименование для печати)'"), ОписаниеТиповСтрока0000, ОписаниеТиповКолонка, "Характеристика", 2, , Истина);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Артикул", НСтр("ru = 'Характеристика (артикул)'"), ОписаниеТиповСтрока25, ОписаниеТиповКолонка, "Характеристика",,Ложь);
	
	// ДополнительныеРеквизиты
	ЗагрузкаДанныхИзВнешнегоИсточника.ПодготовитьСоответствиеПоДополнительнымРеквизитам(НастройкиЗагрузкиДанных, Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_ХарактеристикиНоменклатуры);
	Если НастройкиЗагрузкиДанных.ОписаниеДополнительныхРеквизитов.Количество() > 0 Тогда
		
		ИмяПоля = ЗагрузкаДанныхИзВнешнегоИсточника.ИмяПоляДобавленияДополнительныхРеквизитов();
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, ИмяПоля, НСтр("ru = 'Дополнительные реквизиты'"), ОписаниеТиповСтрока150, ОписаниеТиповСтрока11, , , , , , Истина, Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_ХарактеристикиНоменклатуры);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьЗагружаемыеДанныеИзВнешнегоИсточника(ПараметрыСопоставления, АдресРезультата = Неопределено) Экспорт
	
	ТаблицаСопоставленияДанных	= ПараметрыСопоставления.ТаблицаСопоставленияДанных;
	РазмерТаблицыДанных			= ТаблицаСопоставленияДанных.Количество();
	НастройкиЗагрузкиДанных		= ПараметрыСопоставления.НастройкиЗагрузкиДанных;
	
	// ТаблицаСопоставленияДанных - Тип ДанныеФормыКоллекция
	Для каждого СтрокаТаблицы Из ТаблицаСопоставленияДанных Цикл
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "Владелец") Тогда
			
			ВладелецХарактеристик = ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ЗаполнитьПоУИД(СтрокаТаблицы, "Номенклатура", "УИДВладелец_ВходящиеДанные");
			
		Иначе
		    ВладелецХарактеристик = НастройкиЗагрузкиДанных.ОбщееЗначение;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВладелецХарактеристик) Тогда
			
			// Характеристика по Наименованию, НаименованиеДляПечати
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьХарактеристикуПоНаименованиюАртикулу(СтрокаТаблицы, ВладелецХарактеристик);
			
			// Дополнительные реквизиты
			Если НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты.Количество() > 0 Тогда
				
				ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьДополнительныеРеквизиты(СтрокаТаблицы, НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ПроверитьКорректностьДанныхВСтрокеТаблицы(СтрокаТаблицы);
		
		ЗагрузкаДанныхИзВнешнегоИсточника.ПрогрессСопоставленияДанных(ТаблицаСопоставленияДанных.Индекс(СтрокаТаблицы), РазмерТаблицыДанных);
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(ТаблицаСопоставленияДанных, АдресРезультата);
	
КонецПроцедуры

Процедура ПроверитьКорректностьДанныхВСтрокеТаблицы(СтрокаТаблицы, ПолноеИмяОбъектаЗаполнения = "") Экспорт
	
	СтрокаТаблицы._СтрокаСопоставлена = ЗначениеЗаполнено(СтрокаТаблицы.Характеристика);
	
	ИмяСлужебногоПоля = ЗагрузкаДанныхИзВнешнегоИсточника.ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна();
	
	СтрокаТаблицы[ИмяСлужебногоПоля] = СтрокаТаблицы._СтрокаСопоставлена
		ИЛИ (НЕ СтрокаТаблицы._СтрокаСопоставлена 
				И НЕ ПустаяСтрока(СтрокаТаблицы.ХарактеристикаНаименование));
	
КонецПроцедуры

Процедура ОбработатьПодготовленныеДанные(РезультатЗагрузки) Экспорт
	
	Для каждого СтрокаТаблицы Из РезультатЗагрузки.ТаблицаСопоставленияДанных Цикл
		
		ЗагрузкаВПриложениеВозможна = СтрокаТаблицы[ЗагрузкаДанныхИзВнешнегоИсточника.ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна()];
		
		СогласованноеСостояниеСтроки = (СтрокаТаблицы._СтрокаСопоставлена И РезультатЗагрузки.НастройкиЗагрузкиДанных.ОбновлятьСуществующие)
			ИЛИ (НЕ СтрокаТаблицы._СтрокаСопоставлена И РезультатЗагрузки.НастройкиЗагрузкиДанных.СоздаватьЕслиНеСопоставлено);
		
		Если ЗагрузкаВПриложениеВозможна И СогласованноеСостояниеСтроки Тогда
			
			Если СтрокаТаблицы._СтрокаСопоставлена Тогда
				
				ЭлементСправочника = СтрокаТаблицы.Характеристика.ПолучитьОбъект();
				ЭлементСправочника.Заблокировать();
			Иначе
				
				ЭлементСправочника = Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();
				
				Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "УИД") И ЗначениеЗаполнено(СтрокаТаблицы.УИД) Тогда
					НовыйУИД = Новый УникальныйИдентификатор(СтрокаТаблицы.УИД);
					ЭлементСправочника.УстановитьСсылкуНового(Справочники.ХарактеристикиНоменклатуры.ПолучитьСсылку(НовыйУИД));
				КонецЕсли; 
				
				Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "Владелец") Тогда
				    ЭлементСправочника.Владелец = СтрокаТаблицы.Владелец;
				Иначе	
					ЭлементСправочника.Владелец = РезультатЗагрузки.НастройкиЗагрузкиДанных.ОбщееЗначение;
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТаблицы.ХарактеристикаНаименование) Тогда
				ЭлементСправочника.Наименование = СтрокаТаблицы.ХарактеристикаНаименование;
			КонецЕсли; 
			
			НаименованиеДляПечати = ?(ПустаяСтрока(СтрокаТаблицы.ХарактеристикаНаименованиеДляПечати), СтрокаТаблицы.ХарактеристикаНаименование, СтрокаТаблицы.ХарактеристикаНаименованиеДляПечати);
			Если ЗначениеЗаполнено(НаименованиеДляПечати) Тогда
				ЭлементСправочника.НаименованиеДляПечати = НаименованиеДляПечати;
			КонецЕсли; 
			
			Если ЗначениеЗаполнено(СтрокаТаблицы.Артикул) Тогда
				ЭлементСправочника.Артикул = СтрокаТаблицы.Артикул;
			КонецЕсли; 
			
			Если РезультатЗагрузки.НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты.Количество() > 0 Тогда
				
				ЗагрузкаДанныхИзВнешнегоИсточника.ОбработатьВыбранныеДополнительныеРеквизиты(ЭлементСправочника, СтрокаТаблицы._СтрокаСопоставлена, СтрокаТаблицы, РезультатЗагрузки.НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты);
				
			КонецЕсли;
			
			ЭлементСправочника.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли