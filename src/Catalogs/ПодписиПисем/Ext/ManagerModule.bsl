
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получаем актуальную подпись по ключу "Пользователь" "Учетная запись"
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты
//  ПодписьПисем - СправочникСсылка.ПодписиПисем
// 
// Возвращаемое значение:
//  Структура - Учетная запись и основные настройки.
Функция ПолучитьПодпись(УчетнаяЗапись, ПодписьПисем = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПодписиПисем.Ссылка КАК Подпись,
	|	1 КАК Сортировка,
	|	ПодписиПисем.ВключатьПодписьДляНовыхСообщений КАК ВключатьПодписьДляНовыхСообщений,
	|	ПодписиПисем.ВключатьПодписьПриОтветеИлиПересылке КАК ВключатьПодписьПриОтветеИлиПересылке,
	|	ПодписиПисем.ПодписьПриОтветеФорматированныйДокумент КАК ПодписьПриОтветеФорматированныйДокумент,
	|	ПодписиПисем.ПодписьДляНовыхФорматированныйДокумент КАК ПодписьДляНовыхФорматированныйДокумент,
	|	ПодписиПисем.УчетнаяЗапись КАК УчетнаяЗапись,
	|	ПодписиПисем.ВладелецПодписи КАК ВладелецПодписи
	|ИЗ
	|	Справочник.ПодписиПисем КАК ПодписиПисем
	|ГДЕ
	|	ПодписиПисем.Ссылка = &ПодписьПисем
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОсновныеПодписиПисем.Подпись,
	|	2,
	|	ПодписиПисем.ВключатьПодписьДляНовыхСообщений,
	|	ПодписиПисем.ВключатьПодписьПриОтветеИлиПересылке,
	|	ПодписиПисем.ПодписьПриОтветеФорматированныйДокумент,
	|	ПодписиПисем.ПодписьДляНовыхФорматированныйДокумент,
	|	ПодписиПисем.УчетнаяЗапись,
	|	ПодписиПисем.ВладелецПодписи
	|ИЗ
	|	РегистрСведений.ОсновныеПодписиПисем КАК ОсновныеПодписиПисем
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПодписиПисем КАК ПодписиПисем
	|		ПО ОсновныеПодписиПисем.Подпись = ПодписиПисем.Ссылка
	|ГДЕ
	|	ОсновныеПодписиПисем.Пользователь = &Пользователь
	|	И ОсновныеПодписиПисем.УчетнаяЗапись = &УчетнаяЗапись
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОсновныеПодписиПисем.Подпись,
	|	3,
	|	ПодписиПисем.ВключатьПодписьДляНовыхСообщений,
	|	ПодписиПисем.ВключатьПодписьПриОтветеИлиПересылке,
	|	ПодписиПисем.ПодписьПриОтветеФорматированныйДокумент,
	|	ПодписиПисем.ПодписьДляНовыхФорматированныйДокумент,
	|	ПодписиПисем.УчетнаяЗапись,
	|	ПодписиПисем.ВладелецПодписи
	|ИЗ
	|	РегистрСведений.ОсновныеПодписиПисем КАК ОсновныеПодписиПисем
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПодписиПисем КАК ПодписиПисем
	|		ПО ОсновныеПодписиПисем.Подпись = ПодписиПисем.Ссылка
	|ГДЕ
	|	ОсновныеПодписиПисем.Пользователь = &Пользователь
	|	И ОсновныеПодписиПисем.УчетнаяЗапись = ЗНАЧЕНИЕ(Справочник.УчетныеЗаписиЭлектроннойПочты.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сортировка";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	Запрос.УстановитьПараметр("ПодписьПисем", ПодписьПисем);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	Результат = Новый Структура;
	Результат.Вставить("ВключатьПодписьДляНовыхСообщений" ,
	ВыборкаДетальныеЗаписи.ВключатьПодписьДляНовыхСообщений);
	
	Результат.Вставить("ВключатьПодписьПриОтветеИлиПересылке" ,
	ВыборкаДетальныеЗаписи.ВключатьПодписьПриОтветеИлиПересылке);
	
	Результат.Вставить("ПодписьПриОтветеФорматированныйДокумент" ,
	ВыборкаДетальныеЗаписи.ПодписьПриОтветеФорматированныйДокумент);
	
	Результат.Вставить("ПодписьДляНовыхФорматированныйДокумент" ,
	ВыборкаДетальныеЗаписи.ПодписьДляНовыхФорматированныйДокумент);
	
	Результат.Вставить("УчетнаяЗапись" , ВыборкаДетальныеЗаписи.УчетнаяЗапись);
	Результат.Вставить("ВладелецПодписи" , ВыборкаДетальныеЗаписи.ВладелецПодписи);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
