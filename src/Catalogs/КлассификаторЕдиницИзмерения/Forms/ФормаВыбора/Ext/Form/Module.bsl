
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВыборЕдиницыИзмеренияИзКлассификатора" Тогда
		
		Элементы.Список.Обновить();
		Если ЗначениеЗаполнено(Параметр) Тогда
			
			Элементы.Список.ТекущаяСтрока = Параметр;
			ЭтаФорма.ТекущийЭлемент = Элементы.Список;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаОповещения()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Процедура - обработчик события ПередНачаломДобавления табличного поля Список
//
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	ТекстВопроса = НСтр("ru = 'Имеется возможность добавить единицу измерения из классификатора. Добавить из классификатора?
		|(Да - добавить из классификатора; Нет - добавить самостоятельно; Отмена - отменить действие)'");
		
	ПоказатьВопрос(Новый ОписаниеОповещения("СписокПередНачаломДобавленияЗавершение", ЭтотОбъект),
		ТекстВопроса,
		РежимДиалогаВопрос.ДаНетОтмена
	);
	Отказ = Истина;
	Возврат;
	
КонецПроцедуры // СписокПередНачаломДобавления()

&НаКлиенте
Процедура СписокПередНачаломДобавленияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ДобавитьЕдиницуИзмеренияИзКлассификатора();
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		
		ОткрытьФорму("Справочник.КлассификаторЕдиницИзмерения.Форма.ФормаЭлемента");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Обработчик команды КомандаПодборИзКлассификатора.
//
Процедура ПодобратьИзКлассификатора(Команда)
	
	ДобавитьЕдиницуИзмеренияИзКлассификатора();
	
КонецПроцедуры // ПодобратьИзКлассификатора()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Процедура открывает форму выбора единиц измерения по классификатору
//
//
Процедура ДобавитьЕдиницуИзмеренияИзКлассификатора()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьПодборИзКлассификатора", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.КлассификаторЕдиницИзмерения.Форма.КлассификаторЕдиницИзмерения", , ЭтаФорма, , , , ОписаниеОповещения);
	
КонецПроцедуры // ДобавитьЕдиницуИзмеренияИзКлассификатора()

&НаКлиенте
Процедура ОбработатьПодборИзКлассификатора(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Свойство("СообщениеОбОшибке") И ЗначениеЗаполнено(Результат.СообщениеОбОшибке) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.СообщениеОбОшибке);
		Возврат;
	КонецЕсли;
	
	Если Результат.Свойство("НовыеЭлементы") И ЗначениеЗаполнено(Результат.НовыеЭлементы) Тогда
		ОповеститьПользователя(Результат.НовыеЭлементы);
	КонецЕсли;
	
	ОповеститьОбИзменении(Тип("СправочникСсылка.КлассификаторЕдиницИзмерения"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьПользователя(НовыеЭлементы)
	
	Если НовыеЭлементы.Количество() = 1 Тогда
		
		ПоказатьОповещениеПользователя(НСтр("ru='Добавление:'"), ПолучитьНавигационнуюСсылку(НовыеЭлементы[0]), Строка(НовыеЭлементы[0]), БиблиотекаКартинок.Информация32);
		
	Иначе
		
		ПоказатьОповещениеПользователя(СтрШаблон(НСтр("ru='Добавление (%1):'", НовыеЭлементы.Количество())),, НСтр("ru='Единицы измерения'"), БиблиотекаКартинок.Информация32);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
