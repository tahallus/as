
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВыборЕдиницыИзмеренияИзКлассификатора" Тогда
		
		Элементы.Список.Обновить();
		Если ЗначениеЗаполнено(Параметр) Тогда
			
			Элементы.Список.ТекущаяСтрока = Параметр;
			ЭтаФорма.ТекущийЭлемент = Элементы.Список;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СписокПередНачаломДобавленияЗавершение", ЭтотОбъект);
	
	СписокКнопок = Новый СписокЗначений;
	СписокКнопок.Добавить("ДобавитьИзКлассификатора", НСтр("ru = 'Добавить из классификатора'"));
	СписокКнопок.Добавить("Вручную", НСтр("ru = 'Вручную'"));
	СписокКнопок.Добавить(КодВозвратаДиалога.Отмена);
	
	ПоказатьВопрос(
	ОписаниеОповещения,
	НСтр("ru = 'Имеется возможность добавить единицу измерения из классификатора.'"),
	СписокКнопок);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавленияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = "ДобавитьИзКлассификатора" Тогда
		
		ДобавитьЕдиницуИзмеренияИзКлассификатора();
		
	ИначеЕсли Результат = "Вручную" Тогда
		
		ОткрытьФорму("Справочник.КлассификаторЕдиницИзмерения.Форма.ФормаЭлемента",, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Элементы.Список.ТекущаяСтрока = НовыйОбъект;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодобратьИзКлассификатора(Команда)
	
	ДобавитьЕдиницуИзмеренияИзКлассификатора();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавитьЕдиницуИзмеренияИзКлассификатора()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьПодборИзКлассификатора", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.КлассификаторЕдиницИзмерения.Форма.КлассификаторЕдиницИзмерения", , ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПодборИзКлассификатора(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Свойство("СообщениеОбОшибке") И ЗначениеЗаполнено(Результат.СообщениеОбОшибке) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.СообщениеОбОшибке);
		Возврат;
	КонецЕсли;
	
	Если Результат.Свойство("НовыеЭлементы") И ЗначениеЗаполнено(Результат.НовыеЭлементы) Тогда
		ОповеститьПользователя(Результат.НовыеЭлементы);
	КонецЕсли;
	
	ОповеститьОбИзменении(Тип("СправочникСсылка.КлассификаторЕдиницИзмерения"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьПользователя(НовыеЭлементы)
	
	Если НовыеЭлементы.Количество() = 1 Тогда
		
		ПоказатьОповещениеПользователя(
		НСтр("ru='Добавление:'"), 
		ПолучитьНавигационнуюСсылку(НовыеЭлементы[0]), 
		Строка(НовыеЭлементы[0]),
		БиблиотекаКартинок.Информация32);
		
	Иначе
		
		ПоказатьОповещениеПользователя(
		СтрШаблон(НСтр("ru='Добавление (%1):'", НовыеЭлементы.Количество())),,
		НСтр("ru='Единицы измерения'"),
		БиблиотекаКартинок.Информация32);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти