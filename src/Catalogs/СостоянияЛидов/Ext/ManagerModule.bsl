#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеВыбора = Новый СписокЗначений;
	Запрос = Новый Запрос;
	КоличествоЭлементовБыстрогоВыбора = 15;
	
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 15
		|	СостоянияЛидов.Ссылка КАК Состояние,
		|	СостоянияЛидов.Наименование КАК Наименование,
		|	СостоянияЛидов.Цвет КАК Цвет,
		|	ВЫБОР
		|		КОГДА СостоянияЛидов.Ссылка = ЗНАЧЕНИЕ(Справочник.СостоянияЛидов.Завершен)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЗавершенПоследним
		|ИЗ
		|	Справочник.СостоянияЛидов КАК СостоянияЛидов
		|ГДЕ
		|	СостоянияЛидов.ПометкаУдаления = ЛОЖЬ
		|	И СостоянияЛидов.Ссылка <> ЗНАЧЕНИЕ(Справочник.СостоянияЛидов.Завершен)
		|
		|УПОРЯДОЧИТЬ ПО
		|	СостоянияЛидов.РеквизитДопУпорядочивания,
		|	ЗавершенПоследним";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "15", КоличествоЭлементовБыстрогоВыбора);
	Выборка = Запрос.Выполнить().Выбрать();
	НомерПоПорядку = 0;
	ЖирныйШрифт = Новый Шрифт(ШрифтыСтиля.КрупныйШрифтТекста, , Истина);
	
	Пока Выборка.Следующий() Цикл
		Цвет = Выборка.Цвет.Получить();
		Если Цвет = Неопределено Тогда
			Цвет = ЦветаСтиля.ЦветТекстаПоля;
		КонецЕсли;
		НомерПоПорядку = НомерПоПорядку + 1;
		КомпонентыФС = Новый Массив;
		КомпонентыФС.Добавить(Строка(НомерПоПорядку) + ". ");
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(
			Выборка.Наименование,Неопределено,
			Цвет
		));
		ДанныеВыбора.Добавить(Выборка.Состояние, Новый ФорматированнаяСтрока(КомпонентыФС),,);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ЗаполнитьПоставляемыеСостоянияЛидов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СостоянияЛидов.Ссылка
		|ИЗ
		|	Справочник.СостоянияЛидов КАК СостоянияЛидов
		|ГДЕ
		|	СостоянияЛидов.Предопределенный = ЛОЖЬ";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		// 1. Состояние "Не обработан"
		Состояние = Справочники.СостоянияЛидов.СоздатьЭлемент();
		Состояние.Наименование	= НСтр("ru='Не обработан'");
		Состояние.РеквизитДопУпорядочивания = 1;
		Состояние.Цвет = Новый ХранилищеЗначения(Новый Цвет(189, 105, 57));
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Состояние);
		
		// 1. Состояние "В работе"
		Состояние = Справочники.СостоянияЛидов.СоздатьЭлемент();
		Состояние.Наименование	= НСтр("ru='В работе'");
		Состояние.РеквизитДопУпорядочивания = 2;
		Состояние.Цвет = Новый ХранилищеЗначения(Новый Цвет(82, 163, 163));
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Состояние);
		
		Состояние = Справочники.СостоянияЛидов.Завершен.ПолучитьОбъект();
		Состояние.РеквизитДопУпорядочивания = 99999;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Состояние);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось заполнить справочник ""Состояния лидов"" по умолчанию по причине:
				|%1'"), 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
		);
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.СостоянияЛидов, , ТекстСообщения);
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЦветЭлементовСостояния(ЦветТекста) Экспорт
	ЦветаЭлементовСостояния = Новый Структура;
	Если ЦветТекста = Новый Цвет(189,105,57) Тогда
		// Бледно-красный
		ЦветаЭлементовСостояния.Вставить("ЦветЭлемента", Новый Цвет(243, 224, 213));
		ЦветаЭлементовСостояния.Вставить("ЦветПанели", Новый Цвет(230, 192, 170));
	ИначеЕсли ЦветТекста = Новый Цвет(153,135,35) Тогда
		// Бледно-жёлтый
		ЦветаЭлементовСостояния.Вставить("ЦветЭлемента", Новый Цвет(250, 247, 191));
		ЦветаЭлементовСостояния.Вставить("ЦветПанели", Новый Цвет(224, 209, 119));
	ИначеЕсли ЦветТекста = Новый Цвет(88,163,88) Тогда
		// Бледно-зелёный
		ЦветаЭлементовСостояния.Вставить("ЦветЭлемента", Новый Цвет(216, 240, 216));
		ЦветаЭлементовСостояния.Вставить("ЦветПанели", Новый Цвет(184, 216, 184));
	ИначеЕсли ЦветТекста = Новый Цвет(82,163,163) Тогда
		//Бирюзовый
		ЦветаЭлементовСостояния.Вставить("ЦветЭлемента", Новый Цвет(214, 241, 241));
		ЦветаЭлементовСостояния.Вставить("ЦветПанели", Новый Цвет(182, 218, 218));
	ИначеЕсли ЦветТекста = Новый Цвет(132,61,184) Тогда
		// Фиолетовый
		ЦветаЭлементовСостояния.Вставить("ЦветЭлемента", Новый Цвет(229, 212, 241));
		ЦветаЭлементовСостояния.Вставить("ЦветПанели", Новый Цвет(203, 170, 226));
	ИначеЕсли ЦветТекста = Новый Цвет(189,32,82) Тогда
		//Розовый
		ЦветаЭлементовСостояния.Вставить("ЦветЭлемента", Новый Цвет(253, 202, 213));
		ЦветаЭлементовСостояния.Вставить("ЦветПанели", Новый Цвет(235, 136, 168));
	ИначеЕсли ЦветТекста = Новый Цвет(107,93,1) Тогда
		// Ярко-желтый
		ЦветаЭлементовСостояния.Вставить("ЦветЭлемента", Новый Цвет(235, 203, 4));
		ЦветаЭлементовСостояния.Вставить("ЦветПанели", Новый Цвет(206, 178, 3));
	ИначеЕсли ЦветТекста = Новый Цвет(132,156,54) Тогда
		//Зеленоватый
		ЦветаЭлементовСостояния.Вставить("ЦветЭлемента", Новый Цвет(233, 242, 204));
		ЦветаЭлементовСостояния.Вставить("ЦветПанели", Новый Цвет(200, 217, 145));
	ИначеЕсли ЦветТекста = Новый Цвет(66,113,179) Тогда
		// Бледно-синий
		ЦветаЭлементовСостояния.Вставить("ЦветЭлемента", Новый Цвет(214, 225, 240));
		ЦветаЭлементовСостояния.Вставить("ЦветПанели", Новый Цвет(173, 194, 224));
	ИначеЕсли ЦветТекста = Новый Цвет(122,122,122) Тогда
		//Серый
		ЦветаЭлементовСостояния.Вставить("ЦветЭлемента", Новый Цвет(227, 227, 227));
		ЦветаЭлементовСостояния.Вставить("ЦветПанели", Новый Цвет(200, 200, 200));
	Иначе
		ЦветаЭлементовСостояния.Вставить("ЦветЭлемента", Новый Цвет(249, 249, 249));
		ЦветаЭлементовСостояния.Вставить("ЦветПанели", Новый Цвет(227, 227, 227));
	КонецЕсли;
	Возврат ЦветаЭлементовСостояния;
КонецФункции

#КонецОбласти

#КонецЕсли
