
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.РежимВыбора Тогда
		Элементы.Список.РежимВыбора = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Текст = НСтр("ru = 'Предусмотрена возможность подобрать должность из классификатора.
                  |Подобрать?'");
	Оповещение = Новый ОписаниеОповещения("СписокПередНачаломДобавленияЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавленияЗавершение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПодобратьИзКлассификатора();
	Иначе 
		ОткрытьФорму("Справочник.СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспечения.ФормаОбъекта");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПодобратьИзКлассификатора(Команда)
	
	ПодобратьИзКлассификатора();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодобратьИзКлассификатора()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("Name", "НаименованиеПрофессии");
	
	ОткрытьФормуПодбораИзКлассификатора(
		"СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспечения",
		"Справочник.СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспечения.КлассификаторПрофессийДолжностейЛьготногоПенсионногоОбеспечения",
		НСтр("ru='Списки производств, работ, профессий, должностей и показателей, дающих право на льготное пенсионное обеспечение'"),
		ЭтаФорма,
		СоответствиеПолей);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПодбораИзКлассификатора(ИмяСправочника, ПолныйПутьКМакету, Заголовок, Форма, СоответствиеПолей = Неопределено, ОповещениеЗавершения = Неопределено, ЗаполнятьДополнительныеСведения = Ложь)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ИмяСправочника", ИмяСправочника);
	СтруктураПараметров.Вставить("ПолныйПутьКМакету", ПолныйПутьКМакету);
	СтруктураПараметров.Вставить("Заголовок", Заголовок);
	СтруктураПараметров.Вставить("ЗаполнятьДополнительныеСведения", ЗаполнятьДополнительныеСведения);
		
	Если СоответствиеПолей <> Неопределено Тогда
		СтруктураПараметров.Вставить("СоответствиеПолей", СоответствиеПолей);
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("Форма, ОповещениеЗавершения", Форма, ОповещениеЗавершения);
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуПодбораИзКлассификатораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Справочник.СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспечения.Форма.ЗаполнениеСправочниковИзКлассификаторов", СтруктураПараметров, Форма, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Продолжение процедуры ОткрытьФормуПодбораИзКлассификатора.
//
&НаКлиенте
Процедура ОткрытьФормуПодбораИзКлассификатораЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	ДополнительныеПараметры.Форма.Элементы.Список.Обновить();
	
	Если ДополнительныеПараметры.ОповещениеЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения, Результат);
	КонецЕсли;
		
КонецПроцедуры


#КонецОбласти
