
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СсылкаНаУзелОбмена = Параметры.СсылкаНаУзелОбмена;
	МассивИспользуемыхВМагазине = Параметры.МассивИспользуемыхВМагазине;
	
	ОписаниеУзлаОбмена = ПланыОбмена.ИнтеграцияСМагазинамиСоцСетей.ПолучитьОписаниеУзлаОбмена(СсылкаНаУзелОбмена);
	
	ДеревоКатегорийТоваров.ПолучитьЭлементы().Очистить();
	
	КатегорииТоваров = ПланыОбмена.ИнтеграцияСМагазинамиСоцСетей.ПолучитьКатегорийТоваров(ОписаниеУзлаОбмена);
	Для Каждого СтрРодитель Из КатегорииТоваров Цикл
		
		НовРодитель = ДеревоКатегорийТоваров.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(НовРодитель, СтрРодитель, "Наименование,Идентификатор");
		НовРодитель.ЭтоГруппа = Истина;
		
		ЕстьИспользование = Ложь;
		
		Для Каждого СтрЭлемент Из СтрРодитель.Строки Цикл
			
			НовСтрока = НовРодитель.ПолучитьЭлементы().Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, СтрЭлемент, "Наименование,Идентификатор");
			НовСтрока.Используется = (МассивИспользуемыхВМагазине.Найти(НовСтрока.Идентификатор) <> Неопределено); 
			
			ЕстьИспользование = Макс(ЕстьИспользование, НовСтрока.Используется);
			
		КонецЦикла;
		
		НовРодитель.Используется = ЕстьИспользование;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийРеквизитовЭлементовФормы 

&НаКлиенте
Процедура ДеревоКатегорийТоваровПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКатегорийТоваровПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНаФорме(Команда)
	
	МассивИспользуемыхВМагазине = Новый Массив;
	Для Каждого СтрРодитель Из ДеревоКатегорийТоваров.ПолучитьЭлементы() Цикл
		Для Каждого СтрЭлемент Из СтрРодитель.ПолучитьЭлементы() Цикл
			
			Если Не СтрЭлемент.Используется Тогда
				Продолжить;
			КонецЕсли;
			
			Описание = Новый Структура;
			Описание.Вставить("Идентификатор" , СтрЭлемент.Идентификатор);
			Описание.Вставить("Наименование" , СтрРодитель.Наименование + "\" + СтрЭлемент.Наименование);
			МассивИспользуемыхВМагазине.Добавить(Описание);
			
		КонецЦикла;
	КонецЦикла;
	
	Закрыть(МассивИспользуемыхВМагазине);
	
КонецПроцедуры

#КонецОбласти
