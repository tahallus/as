
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформлениеФормы();
	
	УзелОбменаЭтаИБ = ПроверитьУзелОбменаЭтаИБСервер();
	
	Если УзелОбменаЭтаИБ Тогда
		Возврат;
	КонецЕсли;
	
	РазделениеВключено = РаботаВМоделиСервиса.РазделениеВключено();
	ВыгружатьКартинкиДоИзменения = Объект.ВыгружатьКартинки;
	
	ВыполнитьДействияПриСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Задание = ОбменССайтомРегламентныеЗадания.НайтиЗадание(Объект.ИдентификаторРегламентногоЗадания);
	
	Если Не Задание = Неопределено Тогда
		
		Если ТипЗнч(Задание.Расписание) = Тип("РасписаниеРегламентногоЗадания") Тогда
			РасписаниеРегламентногоЗаданияОбмена = Задание.Расписание;
		ИначеЕсли ТипЗнч(Задание.Расписание) = Тип("ХранилищеЗначения") Тогда
			РасписаниеРегламентногоЗаданияОбмена = Задание.Расписание.Получить();
		Иначе
			РасписаниеРегламентногоЗаданияОбмена = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Задание = ОбменССайтомРегламентныеЗадания.НайтиЗадание(Объект.ИдентификаторРегламентногоЗаданияПечатьЧеков);
	
	Если Не Задание = Неопределено Тогда
		
		Если ТипЗнч(Задание.Расписание) = Тип("РасписаниеРегламентногоЗадания") Тогда
			РасписаниеРегламентногоЗаданияФормированиеЧеков = Задание.Расписание;
		ИначеЕсли ТипЗнч(Задание.Расписание) = Тип("ХранилищеЗначения") Тогда
			РасписаниеРегламентногоЗаданияФормированиеЧеков = Задание.Расписание.Получить();
		Иначе
			РасписаниеРегламентногоЗаданияФормированиеЧеков = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьКассыККМСоответствийОплат(Объект.СоответствиеВидовОплат);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКассыККМСоответствийОплат(СоответствиеВидовОплат=Неопределено)
	
	//Для каждого стр Из Объект.СоответствиеВидовОплат Цикл
	//	Если ЗначениеЗаполнено(стр.Терминал) Тогда
	//		стр.КассаККМ = стр.Терминал.Касса;
	//	КонецЕсли;
	//КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Обмен с сайтом по расписанию
	Если ТекущийОбъект.ИспользоватьРегламентныеЗадания
		И (РасписаниеРегламентногоЗаданияОбмена = Неопределено
		ИЛИ (РазделениеВключено И Не РасписаниеРегламентногоЗаданияОбмена.ПериодПовтораВТечениеДня > 0)) Тогда
		
		ТекущийОбъект.ИспользоватьРегламентныеЗадания = Ложь;
	КонецЕсли;
	
	Задание = ОбменССайтомРегламентныеЗадания.НайтиЗадание(ТекущийОбъект.ИдентификаторРегламентногоЗадания);
	Если ТекущийОбъект.ИспользоватьРегламентныеЗадания Тогда
		
		Если ТипЗнч(РасписаниеРегламентногоЗаданияФормированиеЧеков) <> Тип("РасписаниеРегламентногоЗадания") Тогда
			РасписаниеРегламентногоЗаданияФормированиеЧеков = ПолучитьРасписаниеРегламентногоЗаданияФормированиеЧековПоУмолчанию();
		КонецЕсли;
		
		Если Задание = Неопределено Тогда
			ИдентификаторЗадания = ОбменССайтомРегламентныеЗадания.СоздатьНовоеЗадание(ТекущийОбъект.Код, ТекущийОбъект.Наименование, РасписаниеРегламентногоЗаданияОбмена);
			ТекущийОбъект.ИдентификаторРегламентногоЗадания = ИдентификаторЗадания;
		Иначе
			ОбменССайтомРегламентныеЗадания.УстановитьПараметрыЗадания(Задание, Истина, ТекущийОбъект.Код, ТекущийОбъект.Наименование, РасписаниеРегламентногоЗаданияОбмена);
		КонецЕсли;
		
	Иначе
		
		Если Задание <> Неопределено Тогда
			ОбменССайтомРегламентныеЗадания.УдалитьЗадание(Задание);
		КонецЕсли;
		ТекущийОбъект.ИдентификаторРегламентногоЗадания = Неопределено;
		
	КонецЕсли;
	
	Задание = ОбменССайтомРегламентныеЗадания.НайтиЗадание(ТекущийОбъект.ИдентификаторРегламентногоЗаданияПечатьЧеков);
	Если ТекущийОбъект.ОнлайнОплаты Тогда
		
		Если Задание = Неопределено Тогда
			ИдентификаторЗадания = ОбменССайтомРегламентныеЗадания.СоздатьНовоеЗадание(ТекущийОбъект.Код, ТекущийОбъект.Наименование+"-онлайн чеки", РасписаниеРегламентногоЗаданияФормированиеЧеков);
			ТекущийОбъект.ИдентификаторРегламентногоЗаданияПечатьЧеков = ИдентификаторЗадания;
		Иначе
			ОбменССайтомРегламентныеЗадания.УстановитьПараметрыЗадания(Задание, Истина, ТекущийОбъект.Код, ТекущийОбъект.Наименование+"-онлайн чеки", РасписаниеРегламентногоЗаданияФормированиеЧеков);
		КонецЕсли;
		
	Иначе
		
		Если Задание <> Неопределено Тогда
			ОбменССайтомРегламентныеЗадания.УдалитьЗадание(Задание);
		КонецЕсли;
		ТекущийОбъект.ИдентификаторРегламентногоЗаданияПечатьЧеков = Неопределено;
		
	КонецЕсли;
	
	// Сохранение видов цен.
	ВидыЦен = ТекущийОбъект.ВидыЦен;
	ВидыЦен.Очистить();
	
	Для каждого ЭлементСЗ Из СписокВидовЦен Цикл 
		
		НоваяСтрока = ВидыЦен.Добавить();
		НоваяСтрока.ВидЦен = ЭлементСЗ.Значение;
		
	КонецЦикла;
	
	// Таблицы каталогов и услуг.
	ТаблицаКаталоговСохранить(ТекущийОбъект);
	
	ТекущийОбъект.ВыполнятьПолнуюВыгрузкуПринудительно = ТекущийОбъект.ЭтоНовый();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбменССайтом.ОбновитьПараметрыСеанса();
	
	Если ВыгружатьКартинкиДоИзменения <> ТекущийОбъект.ВыгружатьКартинки Тогда
		ОбновитьПовторноИспользуемыеЗначения();
		ВыгружатьКартинкиДоИзменения = ТекущийОбъект.ВыгружатьКартинки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если УзелОбменаЭтаИБ Тогда
		
		ОчиститьСообщения();
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		НСтр("ru = 'Узел соответствует этой информационной базе и не может использоваться в обмене с сайтом. Используйте другой узел обмена или создайте новый.'"));
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	Элементы.НастроитьРасписаниеОбмена.Заголовок = УстановитьНадписьРасписания(РасписаниеРегламентногоЗаданияОбмена, "ИнтервалОбменаССайтом");
	Элементы.НастроитьРасписаниеЗагрузкиОплат.Заголовок = УстановитьНадписьРасписания(РасписаниеРегламентногоЗаданияФормированиеЧеков, "ИнтервалЗагрузкиОплатССайта");
	
	УстановитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Объект.ОбменТоварами И ТаблицаКаталогов.Количество() = 0 Тогда
		
		Отказ = Истина;
		
		Сообщение = НСтр("ru = 'Таблица каталогов не заполнена!'");
		Поле = "ТаблицаКаталогов";
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(Сообщение, Объект.Ссылка, Поле);
		
	КонецЕсли;
	
	Если Объект.ОбменТоварами И СписокВидовЦен.Количество() = 0 Тогда
		
		Отказ = Истина;
		
		Сообщение = НСтр("ru = 'Вид цен для товаров не выбран!'");
		Поле = "ВыбратьВидыЦенНоменклатуры";
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(Сообщение, Объект.Ссылка, Поле);
		
	КонецЕсли;
	
	Если Объект.ОбменЗаписьНаУслуги И НЕ ЗначениеЗаполнено(Объект.ВидЦенУслуг) Тогда
		
		Отказ = Истина;
		
		Сообщение = НСтр("ru = 'Вид цен для услуг не выбран!'");
		Поле = "ВыбратьВидЦенУслуг";
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(Сообщение, Объект.Ссылка, Поле);
		
	КонецЕсли;
	
	Если НЕ Объект.ВыгружатьКартинки И Объект.ОбязательноеНаличиеФотографий Тогда
		Объект.ОбязательноеНаличиеФотографий = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбщегоНазначения

&НаСервере
Процедура ВыполнитьДействияПриСозданииНаСервере()
	
	ТекОбъект = РеквизитФормыВЗначение("Объект",Тип("ПланОбменаОбъект.ОбменУправлениеНебольшойФирмойСайт"));
	
	Если ПолучитьФункциональнуюОпцию("РаботаВЛокальномРежиме") Тогда
		Элементы.ГруппаВыгружатьНаСайтГоризонтально.Видимость = Истина;
	Иначе
		Элементы.ГруппаВыгружатьНаСайтГоризонтально.Видимость = Ложь;
		Если Не Объект.ВыгружатьНаСайт Тогда
			Объект.ВыгружатьНаСайт = Истина;
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Объект.Ссылка.Пустая() Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		ПарольИзХранилища = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Объект.Ссылка, "Пароль");
		УстановитьПривилегированныйРежим(Ложь);
		Пароль = ?(ЗначениеЗаполнено(ПарольИзХранилища), ЭтотОбъект.УникальныйИдентификатор, "");
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.ДатаНачалаВыгрузкиЗаказов = НачалоМесяца(ТекущаяДата());
		Объект.ВидЗаказа = Справочники.ВидыЗаказовПокупателей.Основной;
		Объект.СостояниеЗаказа = ПолучитьСостояниеЗаказаПокупателя(Объект.ВидЗаказа);
		Объект.СпособУстановкиДатыОтгрузкиЗаказаКоличествоДней = 1;
	КонецЕсли;
	
	СпособУстановкиДатыОтгрузкиЗаказаПриИзмененииНаСервере();
	
	ЗаполнитьГиперссылкиЦенСервер();
	
	ЗаполнитьТаблицуКаталоговСервер();
	НастройкиЗаписьНаУслуги.Параметры.УстановитьЗначениеПараметра("УзелОбмена", Объект.Ссылка);
	
	УстановитьПараметрыТаблицыКаталоговСервер();
	
	УстановитьТипЗначенийСпискаГруппТаблицыКаталоговСервер();
	
	Если ЗначениеЗаполнено(Объект.НастройкиПоискаКонтрагентов) Тогда
		НастройкиПоискаКонтрагентовПредставление = ПоискКонтрагентовПредставление(ОбменССайтом.ЧтениеJSONВСтруктуру(Объект.НастройкиПоискаКонтрагентов));
	КонецЕсли;
	
	НастройкиВыгрузкиРеквизитовПредставление = НастройкиВыгрузкиРеквизитовПредставление(Объект.НастройкиВыгрузкиРеквизитов,Объект.НастройкиВыгрузкиДопРеквизитовНоменклатуры);
	НастройкиВыгрузкиДопРеквизитовЗаказовПредставление = НастройкиВыгрузкиДопРеквизитовЗаказовПредставление(Объект.НастройкиВыгрузкиДопРеквизитовЗаказов);
	
	Элементы.НастройкиВыгрузкиОстатковПоставщиковНаСайт.Видимость = ПолучитьФункциональнуюОпцию("УчетНоменклатурыПоставщиков");
	Элементы.СтраницаПереходНаСайт.Видимость = ПолучитьФункциональнуюОпцию("СайтСоздан");
	
	ЗаполнитьСписокВыбораСоответствиеПолейНоменклатуры();
	
	Если Объект.ВыгружатьКартинки Тогда
		Элементы.ОбязательноеНаличиеФотографий.ТолькоПросмотр = Ложь;
	Иначе
		Элементы.ОбязательноеНаличиеФотографий.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораСоответствиеПолейНоменклатуры()
	
	СписокВыбораСоответствиеПолейНоменклатуры = Элементы.СоответствиеПолейНоменклатурыНаименованиеПоля1С.СписокВыбора;
	СписокВыбораСоответствиеПолейНоменклатуры.Добавить("Наименование", НСтр("ru = 'Наименование'"));
	СписокВыбораСоответствиеПолейНоменклатуры.Добавить("НаименованиеПолное", НСтр("ru = 'Наименование для печати'"));
	СписокВыбораСоответствиеПолейНоменклатуры.Добавить("Комментарий", НСтр("ru = 'Описание'"));
	СписокВыбораСоответствиеПолейНоменклатуры.Добавить("Штрихкод", НСтр("ru = 'Штрихкод'"));
	СписокВыбораСоответствиеПолейНоменклатуры.Добавить("Артикул", НСтр("ru = 'Артикул'"));
	СписокВыбораСоответствиеПолейНоменклатуры.Добавить("Код", НСтр("ru = 'Код'"));
	СписокВыбораСоответствиеПолейНоменклатуры.Добавить("СтранаПроисхождения", НСтр("ru = 'Страна происхождения'"));
	СписокВыбораСоответствиеПолейНоменклатуры.Добавить("Вес", НСтр("ru = 'Вес'"));
	
	ТаблицаСвойств = УправлениеСвойствамиСлужебный.СписокСвойствДляВидаОбъектов("Справочник_Номенклатура", "ДополнительныеРеквизиты");
	Для каждого ДопРеквизит Из ТаблицаСвойств Цикл
		СписокВыбораСоответствиеПолейНоменклатуры.Добавить(ДопРеквизит.Свойство.Имя, ДопРеквизит.Свойство);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьИДоступностьЭлементов()
	
	Элементы.СтраницаВыгрузкаТоваров.Видимость = Объект.ОбменТоварами;
	Элементы.СтраницаОбменЗаказами.Видимость = Объект.ОбменЗаказами;
	Элементы.СтраницаПечатьОнлайнЧеков.Видимость = Объект.ОбменЗаказами И Объект.ОнлайнОплаты;
	
	УстановитьДоступностьПолейОбменЗаказами();	
	
	Если Не РазделениеВключено Тогда
		Элементы.ГруппаАвтообменСтраницы.ТекущаяСтраница = Элементы.ГруппаАвтообменСтраница1;
		Элементы.НастроитьРасписаниеОбмена.Доступность = Объект.ИспользоватьРегламентныеЗадания;
	Иначе
		Элементы.ГруппаАвтообменСтраницы.ТекущаяСтраница = Элементы.ГруппаАвтообменСтраница2;
		Элементы.ИнтервалОбменаССайтом.Доступность = Объект.ИспользоватьРегламентныеЗадания;
	КонецЕсли;
	
	Если Объект.СпособИдентификацииКонтрагентов = ПредопределенноеЗначение("Перечисление.СпособыИдентификацииКонтрагентов.ПредопределенноеЗначение") Тогда
		Элементы.КонтрагентДляПодстановкиВЗаказы.Видимость = Истина;
		Элементы.ГруппаДляНовыхКонтрагентов.Доступность = Ложь;
	Иначе
		Элементы.КонтрагентДляПодстановкиВЗаказы.Видимость = Ложь;
		Элементы.ГруппаДляНовыхКонтрагентов.Доступность = Истина;
	КонецЕсли;
	
	Если Объект.ВыгружатьНаСайт Тогда
		Элементы.СтраницыТипОбмена.ТекущаяСтраница = Элементы.СтраницаВыгрузкаНаСайт;
		ПереключательНазначениеОбмена = 0;
	Иначе
		Элементы.СтраницыТипОбмена.ТекущаяСтраница = Элементы.СтраницаВыгрузкаВКаталог;
		ПереключательНазначениеОбмена = 1;
	КонецЕсли;
	
	Если Объект.ПротоколОбменаCMS = ПредопределенноеЗначение("Перечисление.ПротоколыОбменаCMS.UMI") Тогда
		Элементы.ВыгружатьОстаткиПоСкладам.Видимость = Ложь;
	Иначе
		Элементы.ВыгружатьОстаткиПоСкладам.Видимость = Истина;
	КонецЕсли;
	
	Если Объект.ПротоколОбменаCMS = ПредопределенноеЗначение("Перечисление.ПротоколыОбменаCMS.Битрикс") Тогда
		Элементы.СоответствиеВидовОплатЗагрузитьВидыОплатССайта.Видимость = Истина;
		Элементы.СоответствиеСтатусовЗаказовЗагрузитьСтатусыЗаказовССайта.Видимость = Истина;
		Элементы.СоответствиеСтатусовЗаказовЗагрузитьСлужбыДоставкиССайта.Видимость = Истина;
	ИначеЕсли Объект.ПротоколОбменаCMS = ПредопределенноеЗначение("Перечисление.ПротоколыОбменаCMS.UMI") Тогда
		Элементы.СоответствиеВидовОплатЗагрузитьВидыОплатССайта.Видимость = Истина;
		Элементы.СоответствиеСтатусовЗаказовЗагрузитьСтатусыЗаказовССайта.Видимость = Истина;
		Элементы.СоответствиеСтатусовЗаказовЗагрузитьСлужбыДоставкиССайта.Видимость = Ложь;
	Иначе
		Элементы.СоответствиеВидовОплатЗагрузитьВидыОплатССайта.Видимость = Ложь;
		Элементы.СоответствиеСтатусовЗаказовЗагрузитьСтатусыЗаказовССайта.Видимость = Ложь;
		Элементы.СоответствиеСтатусовЗаказовЗагрузитьСлужбыДоставкиССайта.Видимость = Ложь;
	КонецЕсли;
	
	Если Объект.РежимВыгрузкиЦен=1 Тогда
		Элементы.ГруппаНастройки.Доступность = Ложь;
	Иначе
		Элементы.ГруппаНастройки.Доступность = Истина;
	КонецЕсли;
	
	Если Объект.ВыгружатьИзменения Тогда
		ЭтотОбъект.ПереключательВыгрузкаИзменений = 1;
	Иначе
		ЭтотОбъект.ПереключательВыгрузкаИзменений = 0;
	КонецЕсли;
	Элементы.ГруппаВыгружатьИзменения.Доступность = Объект.ИспользоватьРегламентныеЗадания;
	
	Элементы.ПодчиненныеДокументы.Видимость = (Объект.ПротоколОбменаCMS = ПредопределенноеЗначение("Перечисление.ПротоколыОбменаCMS.Битрикс"));
	Элементы.ОбменЗаписьНаУслуги.Видимость = (Объект.ПротоколОбменаCMS = ПредопределенноеЗначение("Перечисление.ПротоколыОбменаCMS.UMI"));
	
	УстановитьВидимостьСтраницФормы();
	
	ВидимостьСпособИдентификацииКонтрагентов();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьГиперссылкиЦенСервер()
	
	СписокВидовЦенСтрока = "";
	Для каждого СтрокаВидовЦен Из Объект.ВидыЦен Цикл 
		
		НовыйЭлемент = СписокВидовЦен.Добавить();
		НовыйЭлемент.Значение = СтрокаВидовЦен.ВидЦен;
		
		СписокВидовЦенСтрока = СписокВидовЦенСтрока + ?(СписокВидовЦенСтрока = "", "", "; ")
			+ СтрокаВидовЦен.ВидЦен.Наименование;
		
	КонецЦикла;
	Элементы.ВыбратьВидыЦенНоменклатуры.Заголовок = СтрШаблон(НСтр("ru = 'Вид цен: %1'"), СписокВидовЦенСтрока);
	
	Если ЗначениеЗаполнено(Объект.ВидЦенУслуг) Тогда
		Элементы.ВыбратьВидЦенУслуг.Заголовок = СтрШаблон(НСтр("ru = 'Вид цен: %1'"), Объект.ВидЦенУслуг);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьУзелОбменаЭтаИБСервер()
	
	ЭтотУзел = ПланыОбмена.ОбменУправлениеНебольшойФирмойСайт.ЭтотУзел();
	Возврат Объект.Ссылка = ЭтотУзел;
	
КонецФункции

&НаКлиенте
Процедура УстановитьДоступностьПолейОбменЗаказами()
	
	Элементы.ФайлЗагрузки.Доступность = Объект.ОбменЗаказами;
	Элементы.ОнлайнОплаты.Доступность = Объект.ОбменЗаказами;
	Элементы.ПодчиненныеДокументы.Доступность = Объект.ОбменЗаказами;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииОбменЗаказами()
	
	Если Объект.ОбменЗаказами=Ложь Тогда
		Объект.ОнлайнОплаты = Ложь;
		Объект.ПодчиненныеДокументы = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьСтраницФормы();
	УстановитьДоступностьПолейОбменЗаказами();
	
	Если НЕ ЗначениеЗаполнено(Объект.ФайлЗагрузки) И ЗначениеЗаполнено(Объект.КаталогВыгрузки) И ПереключательНазначениеОбмена=1 И Объект.ОбменЗаказами Тогда
		Объект.ФайлЗагрузки = Объект.КаталогВыгрузки + "\Orders.xml";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСпособИдентификацииКонтрагентов()
	
	ВидимостьСпособИдентификацииКонтрагентов();	
	
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьСпособИдентификацииКонтрагентов()
	
	СпособПредопределенноеЗначение = (Объект.СпособИдентификацииКонтрагентов = ПредопределенноеЗначение("Перечисление.СпособыИдентификацииКонтрагентов.ПредопределенноеЗначение"));
	
	Если Объект.НастройкиПоискаКонтрагентов = "" Тогда
		Объект.НастройкиПоискаКонтрагентов = ОбменССайтомСлужебныйВызовСервера.ЗаписьJSONВСтруктуру(ОбменССайтомСлужебныйВызовСервера.ПоляПоискаКонтрагентовПоУмолчанию(Истина));
		НастройкиПоискаКонтрагентовПредставление = ПоискКонтрагентовПредставление(ОбменССайтомСлужебныйВызовСервера.ЧтениеJSONВСтруктуру(Объект.НастройкиПоискаКонтрагентов));
	КонецЕсли;
	
	Элементы.КонтрагентДляПодстановкиВЗаказы.Видимость = СпособПредопределенноеЗначение;
	Элементы.ГруппаДляНовыхКонтрагентов.Видимость = НЕ СпособПредопределенноеЗначение;
	Элементы.НастройкиПоискаКонтрагентов.Видимость = НЕ СпособПредопределенноеЗначение;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПереключательНазначениеОбмена()
	
	Объект.ВыгружатьНаСайт = ПереключательНазначениеОбмена = 0;
	УстановитьВидимостьСтраницТипаОбмена();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
// Функция возвращает ПериодПовтораВТечениеДня в секундах
Функция ПолучитьПериодПовтораВТечениеДня()
	
	ЗначенияВыбора = СоответствиеЗначенийВыбораККоличествуСекунд();
	
	ПериодПовтораВТечениеДня = ЗначенияВыбора.Получить(ИнтервалОбменаССайтом);
	Возврат ?(ПериодПовтораВТечениеДня = Неопределено, 1800, ПериодПовтораВТечениеДня);
	
КонецФункции //ПолучитьПериодПовтораВТечениеДня()

&НаКлиенте
Функция СоответствиеЗначенийВыбораККоличествуСекунд()
	
	СоответствиеНадписей = Новый Соответствие;
	СоответствиеНадписей.Вставить("Один раз в 5 минут", 300);
	СоответствиеНадписей.Вставить("Один раз в 15 минут", 900);
	СоответствиеНадписей.Вставить("Один раз в 30 минут", 1800);
	СоответствиеНадписей.Вставить("Один раз в час", 3600);
	СоответствиеНадписей.Вставить("Один раз в 3 часа", 10800);
	СоответствиеНадписей.Вставить("Один раз в 6 часов", 21600);
	СоответствиеНадписей.Вставить("Один раз в 12 часов", 43200);
	
	Возврат СоответствиеНадписей;
	
КонецФункции //СоответствиеЗначенийВыбораККоличествуСекунд()


// Заполняет значения расписания регламентного задания.
&НаКлиенте
Процедура УстановитьРасписаниеРегламентногоЗаданияОбмена()
	
	Месяцы = Новый Массив;
	Месяцы.Добавить(1);
	Месяцы.Добавить(2);
	Месяцы.Добавить(3);
	Месяцы.Добавить(4);
	Месяцы.Добавить(5);
	Месяцы.Добавить(6);
	Месяцы.Добавить(7);
	Месяцы.Добавить(8);
	Месяцы.Добавить(9);
	Месяцы.Добавить(10);
	Месяцы.Добавить(11);
	Месяцы.Добавить(12);
	
	ДниНедели = Новый Массив;
	ДниНедели.Добавить(1);
	ДниНедели.Добавить(2);
	ДниНедели.Добавить(3);
	ДниНедели.Добавить(4);
	ДниНедели.Добавить(5);
	ДниНедели.Добавить(6);
	ДниНедели.Добавить(7);
	
	ПериодПовтораВТечениеДня = ПолучитьПериодПовтораВТечениеДня();
	
	Если ПериодПовтораВТечениеДня > 0 Тогда
		
		Расписание = Новый РасписаниеРегламентногоЗадания;
		Расписание.Месяцы					= Месяцы;
		Расписание.ДниНедели				= ДниНедели;
		Расписание.ПериодПовтораВТечениеДня = ПериодПовтораВТечениеДня; // секунды
		Расписание.ПериодПовтораДней		= 1; // каждый день
		
		РасписаниеРегламентногоЗаданияОбмена = Расписание;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Заполняет значения расписания регламентного задания.
Процедура УстановитьРасписаниеРегламентногоЗаданияФормированиеЧеков()
	
	Месяцы = Новый Массив;
	Месяцы.Добавить(1);
	Месяцы.Добавить(2);
	Месяцы.Добавить(3);
	Месяцы.Добавить(4);
	Месяцы.Добавить(5);
	Месяцы.Добавить(6);
	Месяцы.Добавить(7);
	Месяцы.Добавить(8);
	Месяцы.Добавить(9);
	Месяцы.Добавить(10);
	Месяцы.Добавить(11);
	Месяцы.Добавить(12);
	
	ДниНедели = Новый Массив;
	ДниНедели.Добавить(1);
	ДниНедели.Добавить(2);
	ДниНедели.Добавить(3);
	ДниНедели.Добавить(4);
	ДниНедели.Добавить(5);
	ДниНедели.Добавить(6);
	ДниНедели.Добавить(7);
	
	СоответствиеНадписей = Новый Соответствие;
	СоответствиеНадписей.Вставить("Один раз в 5 минут", 300);
	СоответствиеНадписей.Вставить("Один раз в 15 минут", 900);
	СоответствиеНадписей.Вставить("Один раз в 30 минут", 1800);
	СоответствиеНадписей.Вставить("Один раз в час", 3600);
	СоответствиеНадписей.Вставить("Один раз в 3 часа", 10800);
	СоответствиеНадписей.Вставить("Один раз в 6 часов", 21600);
	СоответствиеНадписей.Вставить("Один раз в 12 часов", 43200);
	
	ПериодПовтораВТечениеДня = СоответствиеНадписей.Получить(ИнтервалЗагрузкиОплатССайта);
	Если ПериодПовтораВТечениеДня = Неопределено Тогда
		Если РазделениеВключено Тогда
			ПериодПовтораВТечениеДня = 300; //каждые 5 мин для фреша
		Иначе
			ПериодПовтораВТечениеДня = 30; //каждые 30 сек
		КонецЕсли;
	КонецЕсли;
	
	Если ПериодПовтораВТечениеДня > 0 Тогда
		
		Расписание = Новый РасписаниеРегламентногоЗадания;
		Расписание.Месяцы					= Месяцы;
		Расписание.ДниНедели				= ДниНедели;
		Расписание.ПериодПовтораВТечениеДня = ПериодПовтораВТечениеДня; // секунды
		Расписание.ПериодПовтораДней		= 1; // каждый день
		
		РасписаниеРегламентногоЗаданияФормированиеЧеков = Расписание;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьРасписаниеРегламентногоЗаданияФормированиеЧековПоУмолчанию()
	
	Месяцы = Новый Массив;
	Месяцы.Добавить(1);
	Месяцы.Добавить(2);
	Месяцы.Добавить(3);
	Месяцы.Добавить(4);
	Месяцы.Добавить(5);
	Месяцы.Добавить(6);
	Месяцы.Добавить(7);
	Месяцы.Добавить(8);
	Месяцы.Добавить(9);
	Месяцы.Добавить(10);
	Месяцы.Добавить(11);
	Месяцы.Добавить(12);
	
	ДниНедели = Новый Массив;
	ДниНедели.Добавить(1);
	ДниНедели.Добавить(2);
	ДниНедели.Добавить(3);
	ДниНедели.Добавить(4);
	ДниНедели.Добавить(5);
	ДниНедели.Добавить(6);
	ДниНедели.Добавить(7);
	
	ПериодПовтораВТечениеДня = 300; //каждые 5 мин для фреша
	
	Расписание = Новый РасписаниеРегламентногоЗадания;
	Расписание.Месяцы					= Месяцы;
	Расписание.ДниНедели				= ДниНедели;
	Расписание.ПериодПовтораВТечениеДня = ПериодПовтораВТечениеДня; // секунды
	Расписание.ПериодПовтораДней		= 1; // каждый день
	
	Возврат Расписание;
	
КонецФункции

&НаКлиенте
Функция УстановитьНадписьРасписания(Расписание, СтрокаРасписания)
	
	Если Не РазделениеВключено Тогда
		
		Если Расписание = Неопределено Тогда
			ТекстЗаголовка = НСтр("ru='Настроить расписание обмена'");
		Иначе
			ТекстЗаголовка = Расписание;
		КонецЕсли;
		
		Возврат ТекстЗаголовка;
		
	Иначе
		
		Если Расписание = Неопределено Тогда
			
			Если СтрокаРасписания = "ИнтервалЗагрузкиОплатССайта" Тогда
				Если РазделениеВключено Тогда
					ЭтотОбъект[СтрокаРасписания] = НСтр("ru = 'Один раз в 5 - 15 минут'"); 
				Иначе
					ЭтотОбъект[СтрокаРасписания] = НСтр("ru = 'Один раз в 5 минут'"); 
				КонецЕсли;
			Иначе
				ЭтотОбъект[СтрокаРасписания] = НСтр("ru = 'Один раз в 30 минут'"); 
			КонецЕсли;
			
		Иначе
			
			ЗначениеПериода = Расписание.ПериодПовтораВТечениеДня;
			Если ЗначениеПериода = 0 Тогда
				
				ЭтотОбъект[СтрокаРасписания] = НСтр("ru = 'Один раз в 30 минут'");
				
			ИначеЕсли ЗначениеПериода <= 300 Тогда
				
				Если РазделениеВключено Тогда
					ЭтотОбъект[СтрокаРасписания] = НСтр("ru = 'Один раз в 5 - 15 минут'"); 
				Иначе
					ЭтотОбъект[СтрокаРасписания] = НСтр("ru = 'Один раз в 5 минут'"); 
				КонецЕсли;
			ИначеЕсли ЗначениеПериода <= 900 Тогда
				
				Если РазделениеВключено Тогда
					ЭтотОбъект[СтрокаРасписания] = НСтр("ru = 'Один раз в 15 - 30 минут'"); 
				Иначе
					ЭтотОбъект[СтрокаРасписания] = НСтр("ru = 'Один раз в 15 минут'"); 
				КонецЕсли;
				
			ИначеЕсли ЗначениеПериода <= 1800 Тогда
				
				ЭтотОбъект[СтрокаРасписания] = НСтр("ru = 'Один раз в 30 минут'");
				
			ИначеЕсли ЗначениеПериода <= 3600 Тогда
				
				ЭтотОбъект[СтрокаРасписания] = НСтр("ru = 'Один раз в час'");
				
			ИначеЕсли ЗначениеПериода <= 10800 Тогда
				
				ЭтотОбъект[СтрокаРасписания] = НСтр("ru = 'Один раз в 3 часа'");
				
			ИначеЕсли ЗначениеПериода <= 21600 Тогда
				
				ЭтотОбъект[СтрокаРасписания] = НСтр("ru = 'Один раз в 6 часов'");
				
			ИначеЕсли ЗначениеПериода <= 43200 Тогда
				
				ЭтотОбъект[СтрокаРасписания] = НСтр("ru = 'Один раз в 12 часов'");
				
			КонецЕсли;
			
			Возврат ЭтотОбъект[СтрокаРасписания];
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииИспользоватьРегламентныеЗадания()
	
	УстановитьДоступностьРасписанияОбмена();
	
	Если Объект.ИспользоватьРегламентныеЗадания Тогда
		
		Если Не РазделениеВключено Тогда
			ВыполнитьНастройкуРасписания(РасписаниеРегламентногоЗаданияОбмена, Элементы.НастроитьРасписаниеОбмена, "ИнтервалОбменаССайтом");
		Иначе
			УстановитьРасписаниеРегламентногоЗаданияОбмена();
		КонецЕсли;
		
		Элементы.НастроитьРасписаниеОбмена.Заголовок = УстановитьНадписьРасписания(РасписаниеРегламентногоЗаданияОбмена, "ИнтервалОбменаССайтом");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьРасписанияОбмена()
	
	Если Не РазделениеВключено Тогда
		Элементы.НастроитьРасписаниеОбмена.Доступность = Объект.ИспользоватьРегламентныеЗадания;
	Иначе
		Элементы.ИнтервалОбменаССайтом.Доступность = Объект.ИспользоватьРегламентныеЗадания;
	КонецЕсли;
	
	Элементы.ГруппаВыгружатьИзменения.Доступность = Объект.ИспользоватьРегламентныеЗадания;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательВыгрузкаИзмененийПриИзменении(Элемент)
	
	ПриИзмененииПереключательВыгрузкаИзменений()
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПереключательВыгрузкаИзменений()
	
	Объект.ВыгружатьИзменения = (ПереключательВыгрузкаИзменений = 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьНастройкуРасписания(РасписаниеЗадания, Элемент, СтрокаРасписания)
	
	Если РасписаниеЗадания = Неопределено Тогда
		РасписаниеЗадания = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеЗадания);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьНастройкуРасписанияЗавершение", ЭтотОбъект, Новый Структура("ЭлементФормы, СтрокаРасписания, Расписание", Элемент, СтрокаРасписания, РасписаниеЗадания));
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьНастройкуРасписанияЗавершение(Расписание, ДополнительныеПараметры) Экспорт
	
	Если Расписание=Неопределено Тогда
		РасписаниеРегламентногоЗаданияФормированиеЧеков = ДополнительныеПараметры.Расписание;
	Иначе
		ЭтотОбъект[ДополнительныеПараметры.СтрокаРасписания] = Расписание;
		ДополнительныеПараметры.ЭлементФормы.Заголовок = УстановитьНадписьРасписания(Расписание, ЭтотОбъект[ДополнительныеПараметры.СтрокаРасписания]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВидыЦенНоменклатуры(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СписокВидовЦен", СписокВидовЦен);
	
	Если Объект.ПротоколОбменаCMS = ПредопределенноеЗначение("Перечисление.ПротоколыОбменаCMS.UMI") Тогда
		ОткрытьФорму("Справочник.ВидыЦен.ФормаВыбора",ПараметрыФормы,ЭтаФорма,,,, Новый ОписаниеОповещения("ВыбратьВидыЦенНоменклатурыЗавершение", ЭтотОбъект));
	Иначе	
		ОткрытьФорму("Обработка.ПомощникСозданияОбменаДаннымиССайтом.Форма.ФормаВыбораВидовЦен",ПараметрыФормы,ЭтаФорма,,,, Новый ОписаниеОповещения("ВыбратьВидыЦенНоменклатурыЗавершение", ЭтотОбъект));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВидыЦенУслуг(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СписокВидовЦен", СписокВидовЦен);
	
	ОткрытьФорму("Справочник.ВидыЦен.ФормаВыбора",ПараметрыФормы,ЭтаФорма,,,, Новый ОписаниеОповещения("ВыбратьВидыЦенУслугЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВидыЦенНоменклатурыЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено И ТипЗнч(РезультатВыбора) = Тип("СписокЗначений") Тогда
		
		СписокВидовЦен = РезультатВыбора;
		
		СписокВидовЦенСтрока = "";
		Для каждого ЭлементСЗ Из СписокВидовЦен Цикл
			
			СписокВидовЦенСтрока = СписокВидовЦенСтрока + ?(СписокВидовЦенСтрока = "","","; ") + ЭлементСЗ.Представление;
			
		КонецЦикла;
		Элементы.ВыбратьВидыЦенНоменклатуры.Заголовок = СтрШаблон(НСтр("ru = 'Виды цен: %1'"), СписокВидовЦенСтрока);
		
	ИначеЕсли РезультатВыбора <> Неопределено И ТипЗнч(РезультатВыбора) = Тип("СправочникСсылка.ВидыЦен") Тогда
		СписокВидовЦен.Очистить();
		СписокВидовЦен.Добавить(РезультатВыбора, РезультатВыбора);
		Элементы.ВыбратьВидыЦенНоменклатуры.Заголовок = СтрШаблон(НСтр("ru = 'Вид цен: %1'"), РезультатВыбора);
	КонецЕсли;
	
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВидыЦенУслугЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено И ТипЗнч(РезультатВыбора) = Тип("СправочникСсылка.ВидыЦен") Тогда
		Объект.ВидЦенУслуг = РезультатВыбора;
		Элементы.ВыбратьВидЦенУслуг.Заголовок = СтрШаблон(НСтр("ru = 'Вид цен: %1'"), РезультатВыбора);
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьДублированиеСтатусов(ИмяКолонки)
	
	СтатусЗаказаНаСайте = Элементы.СоответствиеСтатусовЗаказов.ТекущиеДанные.СтатусЗаказаНаСайте;
	СостояниеЗаказаПокупателя = Элементы.СоответствиеСтатусовЗаказов.ТекущиеДанные.СостояниеЗаказаПокупателя;
	
	Если НЕ ПустаяСтрока(СтатусЗаказаНаСайте) Тогда
		Найдено = Объект.СоответствиеСтатусовЗаказов.НайтиСтроки(Новый Структура("СтатусЗаказаНаСайте", СтатусЗаказаНаСайте));
		Если Найдено.Количество() > 1 Тогда
			ИмяКолонки = "СтатусЗаказаНаСайте";
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СостояниеЗаказаПокупателя) Тогда
		Найдено = Объект.СоответствиеСтатусовЗаказов.НайтиСтроки(Новый Структура("СостояниеЗаказаПокупателя", СостояниеЗаказаПокупателя));
		Если Найдено.Количество() > 1 Тогда
			ИмяКолонки = "СтатусЗаказаКлиента";
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область УправлениеВнешнимВидомФормы

&НаКлиенте
Процедура УстановитьВидимостьСтраницТипаОбмена()
	
	Если Объект.ВыгружатьНаСайт Тогда
		Элементы.СтраницыТипОбмена.ТекущаяСтраница = Элементы.СтраницаВыгрузкаНаСайт;
	Иначе
		Элементы.СтраницыТипОбмена.ТекущаяСтраница = Элементы.СтраницаВыгрузкаВКаталог;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьСтраницФормы()
	
	Элементы.СтраницаВыгрузкаТоваров.Видимость = Объект.ОбменТоварами;
	Элементы.СтраницаОбменЗаказами.Видимость = Объект.ОбменЗаказами;
	Элементы.СтраницаЗаписьНаУслуги.Видимость = Объект.ОбменЗаписьНаУслуги;
	Элементы.СтраницаПечатьОнлайнЧеков.Видимость = Объект.ОбменЗаказами И (Объект.ОнлайнОплаты ИЛИ Объект.ПодчиненныеДокументы);
	
	Если Не РазделениеВключено Тогда
		Элементы.ГруппаАвтоПечатьЧековСтраницы.ТекущаяСтраница = Элементы.ГруппаАвтоПечатьЧековСтраница1;
		Элементы.НастроитьРасписаниеЗагрузкиОплат.Доступность = Объект.ОнлайнОплаты;
	Иначе
		Элементы.ГруппаАвтоПечатьЧековСтраницы.ТекущаяСтраница = Элементы.ГруппаАвтоПечатьЧековСтраница2;
		Элементы.ИнтервалЗагрузкиОплатССайта.Доступность = Объект.ОнлайнОплаты;
	КонецЕсли;
	
	Элементы.ВключитьОбмен.Видимость = Объект.ОнлайнОплаты;
	
	Если НЕ ЗначениеЗаполнено(Объект.ИдентификаторКаталогаУслуг) Тогда
		
		Объект.ИдентификаторКаталогаУслуг = Новый УникальныйИдентификатор;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеФормы()
	
	УсловноеОформление.Элементы.Очистить();
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.СоответствиеСтатусовЗаказов.РезервироватьТовар", Ложь);
	РаботаСФормой.ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.СоответствиеСтатусовЗаказовСкладРезерв.Имя);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Доступность", Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ДействияКомандныхПанелейФормы

// Процедура - обработчик команды КомандаПроверитьСоединение.
&НаКлиенте
Процедура КомандаПроверитьСоединение(Команда)
	
	Если Модифицированность Тогда
		
		Ответ = Неопределено;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("КомандаПроверитьСоединениеЗавершение", ЭтотОбъект), 
		НСтр("ru = 'Настройка обмена изменена и не записана. Записать?'"),
		РежимДиалогаВопрос.ДаНет);
		Возврат;
		
	КонецЕсли;
	
	КомандаПроверитьСоединениеФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПроверитьСоединениеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	
	Если НЕ Ответ = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Записать() Тогда
		Возврат;
	КонецЕсли;
	
	КомандаПроверитьСоединениеФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПроверитьСоединениеФрагмент()
	
	Перем ТекстПредупреждения;
	
	ТекстПредупреждения = "";
	НастройкиПодключения = Новый Структура;
	НастройкиПодключения.Вставить("ИмяПользователя", Объект.ИмяПользователя);
	НастройкиПодключения.Вставить("АдресСайта", Объект.АдресСайта);
	НастройкиПодключения.Вставить("ТипСайта", Объект.ПротоколОбменаCMS);
	
	ПроверитьПодключение(НастройкиПодключения, ТекстПредупреждения);
	
	ПоказатьПредупреждение(,ТекстПредупреждения);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьПодключение(НастройкиПодключения, ТекстПредупреждения)
	
	Если Объект.Ссылка.Пустая() ИЛИ ПарольИзменен Тогда
		ПарольИзХранилища = Пароль;
	Иначе
		УстановитьПривилегированныйРежим(Истина);
		ПарольИзХранилища = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Объект.Ссылка, "Пароль");
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	НастройкиПодключения.Вставить("Пароль", ПарольИзХранилища);
	
	ОбменССайтом.ВыполнитьТестовоеПодключениеКСайту(НастройкиПодключения, ТекстПредупреждения);
	
КонецПроцедуры

// Процедура - обработчик команды НастроитьРасписаниеОбмена.
//
&НаКлиенте
Процедура НастроитьРасписаниеОбмена(Команда)
	
	НастроитьРасписание(РасписаниеРегламентногоЗаданияОбмена, Элементы.НастроитьРасписаниеОбмена, "РасписаниеРегламентногоЗаданияОбмена");
	
КонецПроцедуры

// Процедура - обработчик команды НастроитьРасписаниеОбмена.
//
&НаКлиенте
Процедура НастроитьРасписаниеЗагрузкиОплат(Команда)
	
	НастроитьРасписание(РасписаниеРегламентногоЗаданияФормированиеЧеков, Элементы.НастроитьРасписаниеЗагрузкиОплат, "РасписаниеРегламентногоЗаданияФормированиеЧеков");
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРасписание(Расписание, Элемент, СтрокаРасписания)
	
	ВыполнитьНастройкуРасписания(Расписание, Элемент, СтрокаРасписания);
	Элемент.Заголовок = УстановитьНадписьРасписания(Расписание, СтрокаРасписания);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоследнийФайлЗаказовССайта(Команда)
	
	Текст = Новый ТекстовыйДокумент;
	Текст.УстановитьТекст(ПрочитатьДанныеНаСервере("ДанныеПоследнегоОбмена"));
	Текст.Показать(НСтр("ru = 'Текст заказов'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПоследнийНепустойФайлЗаказовССайта(Команда)
	
	Текст = Новый ТекстовыйДокумент;
	Текст.УстановитьТекст(ПрочитатьДанныеНаСервере("ДанныеПоследнегоНепустогоОбмена"));
	Текст.Показать(НСтр("ru = 'Текст заказов'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьЗаказИзФайла(Команда)
	
	ОткрытьФорму("ПланОбмена.ОбменУправлениеНебольшойФирмойСайт.Форма.ТекстЗаказа", Новый Структура("УзелОбмена", Объект.Ссылка));
	
КонецПроцедуры

&НаКлиенте
Процедура ПоследнийНепустойТекстОшибки(Команда)
	
	Текст = Новый ТекстовыйДокумент;
	Текст.УстановитьТекст(ПрочитатьТекстОшибкиНаСервере());
	Текст.Показать(НСтр("ru = 'Текст заказов'"));
	
КонецПроцедуры

&НаСервере
Функция ПрочитатьТекстОшибкиНаСервере()
	
	СостояниеОбмена = РегистрыСведений.СостоянияОбменовССайтами.СоздатьМенеджерЗаписи();
	СостояниеОбмена.УзелИнформационнойБазы = Объект.Ссылка;
	СостояниеОбмена.ДействиеПриОбмене = Перечисления.ДействияПриОбмене.ЗагрузкаДанных;
	СостояниеОбмена.Прочитать();
	
	Возврат СостояниеОбмена.ПоследнийТекстОшибки;
	
КонецФункции

&НаСервере
Функция ПрочитатьДанныеНаСервере(ИмяПоля)
	
	СостояниеОбмена = РегистрыСведений.СостоянияОбменовССайтами.СоздатьМенеджерЗаписи();
	СостояниеОбмена.УзелИнформационнойБазы = Объект.Ссылка;
	СостояниеОбмена.ДействиеПриОбмене = Перечисления.ДействияПриОбмене.ЗагрузкаДанных;
	СостояниеОбмена.Прочитать();
	
	Возврат СостояниеОбмена[ИмяПоля].Получить();
	
КонецФункции

&НаСервере
Функция ФормаНастройкаВыгрузкиВыгрузитьВсеРеквизитыНаСервере(ДобавитьПароль = Ложь)
	
	ОбъектСтруктура = Новый Структура;
	
	ТекОбъект = РеквизитФормыВЗначение("Объект",Тип("ПланОбменаОбъект.ОбменУправлениеНебольшойФирмойСайт"));
	
	ОбъектСтруктура.Вставить("Наименование" , ТекОбъект.Наименование);
	ОбъектСтруктура.Вставить("Код" , ТекОбъект.Код);
	
	Если ДобавитьПароль Тогда
		УстановитьПривилегированныйРежим(Истина);
		ПарольИзХранилища = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(ТекОбъект.Ссылка, "Пароль");
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	ОбъектСтруктура.Вставить("ПарольИзХранилища" , ПарольИзХранилища);
	
	Для Каждого СтрРеквизит Из Метаданные.ПланыОбмена.ОбменУправлениеНебольшойФирмойСайт.Реквизиты Цикл
		ОбъектСтруктура.Вставить(СтрРеквизит.Имя , ТекОбъект[СтрРеквизит.Имя]);
	КонецЦикла;
	
	ОписаниеТаблиц = Новый Структура;
	Для Каждого СтрРеквизит Из Метаданные.ПланыОбмена.ОбменУправлениеНебольшойФирмойСайт.ТабличныеЧасти Цикл
		ОписаниеТаблиц.Вставить(СтрРеквизит.Имя , ТекОбъект[СтрРеквизит.Имя].Выгрузить());
	КонецЦикла;
	
	ОбъектСтруктура.Вставить("ОписаниеТаблиц" , ОписаниеТаблиц);
	ОбъектСтруктураСтрокой = ЗначениеВСтрокуВнутр(ОбъектСтруктура);
	
	Возврат ОбъектСтруктураСтрокой;
	
КонецФункции

&НаКлиенте
Процедура ФормаНастройкаВыгрузкиВыгрузитьВсеРеквизиты(Команда)
	ФормаНастройкаВыгрузкиВыгрузить(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ФормаНастройкаВыгрузкиВыгрузить(ДобавитьПароль)
	
	ОбъектСтруктураСтрокой = ФормаНастройкаВыгрузкиВыгрузитьВсеРеквизитыНаСервере(ДобавитьПароль);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ФормаНастройкаВыгрузкиВыгрузитьЗавершение",ЭтаФорма,ОбъектСтруктураСтрокой);
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.ПолноеИмяФайла = "" + Объект.Наименование + ".txt";
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаНастройкаВыгрузкиВыгрузитьЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено Тогда
		ЗаписьТекста = Новый ЗаписьТекста(ВыбранныеФайлы[0]);
		ЗаписьТекста.Записать(ДополнительныеПараметры);
		ЗаписьТекста.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаНастройкаВыгрузкиВыгрузитьБезПароля(Команда)
	ФормаНастройкаВыгрузкиВыгрузить(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ФормаНастройкаЗагрузитьДанные(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ФормаНастройкаЗагрузитьДанныеЗавершение",ЭтаФорма);
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаНастройкаЗагрузитьДанныеЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено Тогда
		
		ЧтениеТекста = Новый ЧтениеТекста(ВыбранныеФайлы[0],КодировкаТекста.UTF8);
		ОбъектСтруктураСтрокой = ЧтениеТекста.Прочитать();
		ЧтениеТекста.Закрыть();
		
		ФормаНастройкаЗагрузитьДанныеНаСервере(ОбъектСтруктураСтрокой);
		
		ЭтотОбъект.Прочитать();
		
		ВыполнитьДействияПриСозданииНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ФормаНастройкаЗагрузитьДанныеНаСервере(ОбъектСтруктураСтрокой)
	
	ТаблицаКаталогов.Очистить();
	
	ОбъектСтруктура = ЗначениеИзСтрокиВнутр(ОбъектСтруктураСтрокой);
	ТекОбъект = РеквизитФормыВЗначение("Объект",Тип("ПланОбменаОбъект.ОбменУправлениеНебольшойФирмойСайт"));
	
	ЗаполнитьЗначенияСвойств(ТекОбъект , ОбъектСтруктура,,"Код");
	
	Пароль = "";
	ПарольИзменен = Истина;
	
	Если ОбъектСтруктура.Свойство("ПарольИзХранилища") Тогда
		Пароль = ОбъектСтруктура.ПарольИзХранилища;
	КонецЕсли;
	
	Для Каждого СтрТаблица Из ОбъектСтруктура.ОписаниеТаблиц Цикл
		ТекОбъект[СтрТаблица.Ключ].Загрузить(СтрТаблица.Значение);
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(ТекОбъект.Код) Тогда
		ТекОбъект.УстановитьНовыйКод();
	КонецЕсли;
	
	ТекОбъект.Записать();
	
	ЗначениеВРеквизитФормы(ТекОбъект,"Объект");
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийРеквизитовФормы

&НаКлиенте
Процедура СоответствиеСтатусовЗаказовПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКолонки = "";
	Если НЕ ПроверитьДублированиеСтатусов(ИмяКолонки) Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		НСтр("ru = 'Такой статус уже указан в другой строке таблицы!'"),
		Объект.Ссылка,
		ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
		"Объект.СоответствиеСтатусовЗаказов", Объект.СоответствиеСтатусовЗаказов.Индекс(Элементы.СоответствиеСтатусовЗаказов.ТекущиеДанные) + 1, ИмяКолонки));
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события НачалоВыбора поля ввода КаталогВыгрузки.
//
&НаКлиенте
Процедура КаталогВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НачатьПодключениеРасширенияРаботыСФайлами(Новый ОписаниеОповещения("КаталогВыгрузкиНачалоВыбораЗавершение1", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогВыгрузкиНачалоВыбораЗавершение1(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Не Подключено Тогда
		ОтветОповещения = Новый ОписаниеОповещения("ВыборФайлаЗавершить", ЭтотОбъект);
		ЗадатьВопросУстановкиРасширения(ОтветОповещения);
		
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	
	Диалог.Заголовок = НСтр("ru = 'Укажите каталог обмена'");
	Диалог.Каталог = Объект.КаталогВыгрузки;
	
	Диалог.Показать(Новый ОписаниеОповещения("КаталогВыгрузкиНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("Диалог", Диалог)));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаЗавершить(Результат) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьДиалогВыбораФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДиалогВыбораФайла()
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	
	Диалог.Заголовок = НСтр("ru = 'Укажите каталог обмена'");
	Диалог.Каталог = Объект.КаталогВыгрузки;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораКаталогаВыгрузки", ЭтотОбъект);
	
	Диалог.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКаталогаВыгрузки(ВыбранныеФайлы, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено Тогда
		Объект.КаталогВыгрузки = ВыбранныеФайлы[0];
		Модифицированность = Истина; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьВопросУстановкиРасширения(ОповещениеВопроса)
	
	ТекстВопроса= НСтр("ru = 'Для данной операции необходимо установить расширение работы с файлами.
	|Установить расширение работы с файлами ?'");
	
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеВопроса", ОповещениеВопроса);
	
	ОтветОповещения = Новый ОписаниеОповещения("ВопросУстановитьРасширениеЗавершить", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(ОтветОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросУстановитьРасширениеЗавершить(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		УстановитьРасширениеОповещение = ДополнительныеПараметры.ОповещениеВопроса;
		
		НачатьУстановкуРасширенияРаботыСФайлами(УстановитьРасширениеОповещение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогВыгрузкиНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Диалог = ДополнительныеПараметры.Диалог;
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		Объект.КаталогВыгрузки = Диалог.Каталог;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события Открытие поля ввода КаталогВыгрузки.
//
&НаКлиенте
Процедура КаталогВыгрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПолноеИмяФайла = Объект.КаталогВыгрузки;
	
	Если ПустаяСтрока(ПолноеИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("НачатьЗапускПриложенияЗавершить", ЭтотОбъект);
	
	НачатьПодключениеРасширенияРаботыСФайлами(Оповещение);
	
КонецПроцедуры

// Процедура - обработчик события НачалоВыбора поля ввода ФайлЗагрузки.
//
&НаКлиенте
Процедура ФайлЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НачатьПодключениеРасширенияРаботыСФайлами(Новый ОписаниеОповещения("ФайлЗагрузкиНачалоВыбораЗавершение1", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлЗагрузкиНачалоВыбораЗавершение1(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Не Подключено Тогда
		
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Для данной операции необходимо установить расширение работы с файлами!'"));
		Возврат;
		
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	Диалог.Заголовок = НСтр("ru = 'Выберите xml-файл с заказами'");
	Диалог.ПолноеИмяФайла = Объект.ФайлЗагрузки;
	Диалог.Фильтр = НСтр("ru = 'Документ XML'") + " (*.xml)|*.xml";
	
	Диалог.Показать(Новый ОписаниеОповещения("ФайлЗагрузкиНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("Диалог", Диалог)));
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлЗагрузкиНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Диалог = ДополнительныеПараметры.Диалог;
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		
		Объект.ФайлЗагрузки = Диалог.ПолноеИмяФайла;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события Открытие поля ввода ФайлЗагрузки.
//
&НаКлиенте
Процедура ФайлЗагрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПолноеИмяФайла = Объект.ФайлЗагрузки;
	Если ПустаяСтрока(ПолноеИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ФайлЗагрузкиОткрытиеЗавершить", ЭтотОбъект);
	НачатьПодключениеРасширенияРаботыСФайлами(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлЗагрузкиОткрытиеЗавершить(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Не Подключено Тогда
		
		ОповещениеВопроса = Новый ОписаниеОповещения("ФайлЗагрузкиВопросЗавершить", ЭтотОбъект);
		ЗадатьВопросУстановкиРасширения(ОповещениеВопроса);
		
	Иначе
		
		ОткрытьФайлОбменаЗаказами();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлЗагрузкиВопросЗавершить(Результат) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФайлОбменаЗаказами();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлОбменаЗаказами()
	
	ФайловаяСистемаКлиент.ЗапуститьПрограмму("explorer.exe /select, " + Объект.ФайлЗагрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗапускПриложенияЗавершить(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Не Подключено Тогда
		
		ОтветОповещения = Новый ОписаниеОповещения("ОткрытьКаталогЗавершить", ЭтотОбъект);
		
		ЗадатьВопросУстановкиРасширения(ОтветОповещения);
		
	Иначе
		
		ОткрытьКаталогОбмена();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКаталогОбмена()
	
	ФайловаяСистемаКлиент.ЗапуститьПрограмму(Объект.КаталогВыгрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПустойОбработчик(Результат, ДополнительныеПараметры) Экспорт
	
	// Действий не требуется.
	Возврат;
	
КонецПроцедуры

// Процедура - обработчик события НачалоВыбора поля ввода Комментарий.
//
&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Комментарий");
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля переключателя ПереключательНазначениеОбмена.
//
&НаКлиенте
Процедура ПереключательНазначениеОбменаПриИзменении(Элемент)
	
	ПриИзмененииПереключательНазначениеОбмена();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля флажка ИспользоватьРегламентныеЗадания.
//
&НаКлиенте
Процедура ИспользоватьРегламентныеЗаданияПриИзменении(Элемент)
	
	ПриИзмененииИспользоватьРегламентныеЗадания();
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалОбменаССайтомПриИзменении(Элемент)
	
	УстановитьРасписаниеРегламентногоЗаданияОбмена();
	Элементы.НастроитьРасписаниеОбмена.Заголовок = УстановитьНадписьРасписания(РасписаниеРегламентногоЗаданияОбмена, "ИнтервалОбменаССайтом");
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалЗагрузкиОплатССайтаПриИзменении(Элемент)
	
	УстановитьРасписаниеРегламентногоЗаданияФормированиеЧеков();
	Элементы.НастроитьРасписаниеОбмена.Заголовок = УстановитьНадписьРасписания(РасписаниеРегламентногоЗаданияФормированиеЧеков, "ИнтервалОбменаССайтом");
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля флажка ОбменТоварами.
//
&НаКлиенте
Процедура ОбменТоварамиПриИзменении(Элемент)
	
	УстановитьВидимостьСтраницФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбменЗаписьНаУслугиПриИзменении(Элемент)
	
	УстановитьВидимостьСтраницФормы();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля флажка ОбменЗаказами.
//
&НаКлиенте
Процедура ОбменЗаказамиПриИзменении(Элемент)
	
	ПриИзмененииОбменЗаказами();
	
КонецПроцедуры

&НаКлиенте
Процедура ПечататьОнлайнЧекиПриИзменении(Элемент)
	
	ПоказатьСтраницуОнлайнОплаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьОнлайнОплаты()
	
	УстановитьВидимостьСтраницФормы();
	
	Если Объект.ОнлайнОплаты Тогда
		Если РазделениеВключено ИЛИ РасписаниеРегламентногоЗаданияФормированиеЧеков = Неопределено Тогда
			УстановитьРасписаниеРегламентногоЗаданияФормированиеЧеков();	
		Иначе
			ВыполнитьНастройкуРасписания(РасписаниеРегламентногоЗаданияФормированиеЧеков, Элементы.НастроитьРасписаниеЗагрузкиОплат, "ИнтервалЗагрузкиОплатССайта");
		КонецЕсли;
		Элементы.НастроитьРасписаниеЗагрузкиОплат.Заголовок = УстановитьНадписьРасписания(РасписаниеРегламентногоЗаданияФормированиеЧеков, "ИнтервалЗагрузкиОплатССайта");
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СпособИдентификацииКонтрагентов.
//
&НаКлиенте
Процедура СпособИдентификацииКонтрагентовПриИзменении(Элемент)
	
	ПриИзмененииСпособИдентификацииКонтрагентов();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЗаказаПриИзменении(Элемент)
	
	Объект.СостояниеЗаказа = ПолучитьСостояниеЗаказаПокупателя(Объект.ВидЗаказа);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЗаказаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Ожидание = 0 Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ПолучитьДанныеВыбора(Тип("СправочникСсылка.ВидыЗаказовПокупателей"), ПараметрыПолученияДанных);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеЗаказаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	АвтоПодборСостоянияЗаказа(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СоответствиеСтатусовЗаказовСтатусЗаказаКлиентаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	АвтоПодборСостоянияЗаказа(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура АвтоПодборСостоянияЗаказа(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыПолученияДанных.Вставить("ВидЗаказа", Объект.ВидЗаказа);
	ДанныеВыбораСостояния = ПолучитьДанныеВыбора(Тип("СправочникСсылка.СостоянияЗаказовПокупателей"), ПараметрыПолученияДанных);
	
	ДанныеВыбора = ДанныеВыбораСостояния;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСостояниеЗаказаПокупателя(ВидЗаказа)
	
	Возврат ЗаполнениеОбъектовУНФ.ПолучитьСостояниеЗаказаПокупателя(ВидЗаказа);
	
КонецФункции

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	ПарольИзменен = Истина;
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПарольИзменен Тогда
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ТекущийОбъект.Ссылка, Пароль);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстФайловОбмена(Команда)
	
	УзелОбмена = Объект.Ссылка;
	
	Если УзелОбмена = ОбменССайтомПовтИсп.ПолучитьЭтотУзелПланаОбмена("ОбменУправлениеНебольшойФирмойСайт") Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		НСтр("ru = 'Узел соответствует этой информационной базе и не может использоваться в обмене с сайтом. Используйте другой узел обмена или создайте новый.'"));
		Возврат;
	КонецЕсли;
	
	#Если НЕ МобильныйКлиент Тогда
		
		СоответствиеПакетов = ОбменССайтомСлужебныйВызовСервера.ТекстФайлаОбмена(УзелОбмена, Истина);
		
		Для каждого Пакет Из СоответствиеПакетов Цикл
			Текст = Новый ТекстовыйДокумент;
			Текст.УстановитьТекст(Пакет.Значение);
			Текст.Показать(Пакет.Ключ);
		КонецЦикла;
		
	#КонецЕсли 
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстФайловПолногоОбмена(Команда)
	
	УзелОбмена = Объект.Ссылка;
	
	Если УзелОбмена = ОбменССайтомПовтИсп.ПолучитьЭтотУзелПланаОбмена("ОбменУправлениеНебольшойФирмойСайт") Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		НСтр("ru = 'Узел соответствует этой информационной базе и не может использоваться в обмене с сайтом. Используйте другой узел обмена или создайте новый.'"));
		Возврат;
	КонецЕсли;
	
	СоответствиеПакетов = ОбменССайтомСлужебныйВызовСервера.ТекстФайлаОбмена(УзелОбмена, Ложь);
	
	Для каждого Пакет Из СоответствиеПакетов Цикл
		Текст = Новый ТекстовыйДокумент;
		Текст.УстановитьТекст(Пакет.Значение);
		Текст.Показать(Пакет.Ключ);
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПротоколОбменаCMSПриИзменении(Элемент)
	
	Если НЕ Объект.ПротоколОбменаCMS = ПредопределенноеЗначение("Перечисление.ПротоколыОбменаCMS.Битрикс") Тогда	
		Объект.ПодчиненныеДокументы = Ложь;
		Объект.ОнлайнОплаты = Ложь;
	КонецЕсли;
	
	Если НЕ Объект.ПротоколОбменаCMS = ПредопределенноеЗначение("Перечисление.ПротоколыОбменаCMS.UMI") Тогда
		Объект.ОбменЗаписьНаУслуги = Ложь;
	КонецЕсли; 
	
	УстановитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура РежимВыгрузкиЦенПриИзменении(Элемент)
	
	УстановитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Функция ПроверитьУстановкуКонстант(СтруктураНастроек)
	
	СтруктураНастроек.Вставить("УчетРозничныхПродаж", ПолучитьФункциональнуюОпцию("УчетРозничныхПродаж"));
	СтруктураНастроек.Вставить("ИспользоватьЗаказыВРозничнойТорговле", ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыВРозничнойТорговле"));
	СтруктураНастроек.Вставить("ИспользоватьПодключаемоеОборудование", ПолучитьФункциональнуюОпцию("ИспользоватьПодключаемоеОборудование"));
	
	Если СтруктураНастроек.УчетРозничныхПродаж 
		И СтруктураНастроек.ИспользоватьЗаказыВРозничнойТорговле 
		И СтруктураНастроек.ИспользоватьПодключаемоеОборудование Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура УстановитьНастройкиОнлайнОплат(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УстановитьКонстантыДляОнлайнОплат(ДополнительныеПараметры);
		Объект.ОнлайнОплаты = Истина;
		ВключитьОнлайнОплаты();
	Иначе
		Объект.ОнлайнОплаты = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьКонстантыДляОнлайнОплат(ДополнительныеПараметры)
	
	Если НЕ ДополнительныеПараметры.УчетРозничныхПродаж Тогда
		Константы.ФункциональнаяОпцияУчетРозничныхПродаж.Установить(Истина);
	КонецЕсли;
	
	Если НЕ ДополнительныеПараметры.ИспользоватьЗаказыВРозничнойТорговле Тогда
		Константы.ФункциональнаяОпцияИспользоватьЗаказыВРозничнойТорговле.Установить(Истина);
		Константы.ФункциональнаяОпцияИспользоватьЗаказыВРозничнойТорговлеДляНастроек.Установить(Истина);
	КонецЕсли;
	
	Если НЕ ДополнительныеПараметры.ИспользоватьПодключаемоеОборудование Тогда
		Константы.ФункциональнаяОпцияИспользоватьПодключаемоеОборудование.Установить(Истина);
	КонецЕсли;
	
	Если НЕ Константы.ФункциональнаяОпцияИспользоватьОплатуКартами.Получить() Тогда
		Константы.ФункциональнаяОпцияИспользоватьОплатуКартами.Установить(Истина);
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ЗагрузитьВидыОплатССайта(Команда)
	
	ЗагрузитьВидыОплатССайтаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСтатусыЗаказовССайта(Команда)
	
	ЗагрузитьСтатусыЗаказовССайтаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьВидыОплатССайтаНаСервере()
	
	ОписаниеОшибки = "";
	НастройкиСайта = ЗагрузитьНастройкиСайта(ОписаниеОшибки);
	
	Если НЕ ЗначениеЗаполнено(НастройкиСайта) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось загрузить виды оплат систем с сайта!'")
		+Символы.ПС + ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	Если НастройкиСайта.Свойство("ПлатежныеСистемы") Тогда
		Объект.СоответствиеВидовОплат.Очистить();
		Для каждого стр Из НастройкиСайта.ПлатежныеСистемы Цикл
			
			НовСтр = Объект.СоответствиеВидовОплат.Добавить();
			НовСтр.ИдСпособаОплатыНаСайте = стр.Ид;
			НовСтр.СпособОплатыНаСайте = стр.Название;
			Если стр.Свойство("ТипОплаты") Тогда
				Если стр.ТипОплаты = "Безналичная оплата" Тогда
					НовСтр.ТипОплатыНаСайте = Перечисления.ТипыОплатНаСайте.Безналичные;
				ИначеЕсли стр.ТипОплаты = "Эквайринговая оплата" Тогда
					НовСтр.ТипОплатыНаСайте = Перечисления.ТипыОплатНаСайте.Эквайринг;
				ИначеЕсли стр.ТипОплаты = "Наличная оплата" Тогда
					НовСтр.ТипОплатыНаСайте = Перечисления.ТипыОплатНаСайте.Наличные;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ЗаполнитьСуществующиеВидыОплат();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСтатусыЗаказовССайтаНаСервере()
	
	ОписаниеОшибки = "";
	НастройкиСайта = ЗагрузитьНастройкиСайта(ОписаниеОшибки);
	
	Если НЕ ЗначениеЗаполнено(НастройкиСайта) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось загрузить список платежных систем с сайта!'")
		+Символы.ПС + ОписаниеОшибки);
		Возврат;
	КонецЕсли; 
	
	Если НастройкиСайта.Свойство("Статусы") Тогда
		Объект.СоответствиеСтатусовЗаказов.Очистить();
		Для каждого стр Из НастройкиСайта.Статусы Цикл
			
			НовСтр = Объект.СоответствиеСтатусовЗаказов.Добавить();
			НовСтр.ИДСтатусаЗаказаНаСайте = стр.Ид;
			НовСтр.СтатусЗаказаНаСайте = стр.Название;
			
		КонецЦикла;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

Функция ПрочитатьЭлементы(ЧтениеXML, ИмяКорневогоУзла)
	
	МассивЗначений = Новый Массив;
	
	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.Имя = ИмяКорневогоУзла И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			Прервать;
		КонецЕсли;
		
		СтруктураЗначений = Новый Структура;
		Пока ЧтениеXML.Прочитать() Цикл
			
			Если ЧтениеXML.Имя = "Элемент" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
				Прервать;
			КонецЕсли;
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				ИмяЭлемента = ЧтениеXML.Имя;
				ЧтениеXML.Прочитать();
				ЗначениеЭлемента = ЧтениеXML.Значение;
				
				СтруктураЗначений.Вставить(ИмяЭлемента, ЗначениеЭлемента);
			КонецЕсли;
			
		КонецЦикла;
		
		МассивЗначений.Добавить(СтруктураЗначений);
	КонецЦикла;
	
	Возврат МассивЗначений;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСуществующиеВидыОплат()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭквайринговыеТерминалы.Ссылка КАК АналитикаОплаты,
	|	ЭквайринговыеТерминалы.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ЭквайринговыеТерминалы КАК ЭквайринговыеТерминалы
	|ГДЕ
	|	ЭквайринговыеТерминалы.Наименование В(&спНаименование)
	|	И НЕ ЭквайринговыеТерминалы.ПометкаУдаления
	|	И ЭквайринговыеТерминалы.Касса.ТипКассы = &ТипКассы";
	
	Запрос.УстановитьПараметр("ТипКассы", Перечисления.ТипыКассККМ.ФискальныйРегистратор);
	Запрос.УстановитьПараметр("спНаименование", Объект.СоответствиеВидовОплат.Выгрузить().ВыгрузитьКолонку("СпособОплатыНаСайте"));
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОтборНаименование = Новый Структура("СпособОплатыНаСайте", Выборка.Наименование);
		НайденныеСтроки = Объект.СоответствиеВидовОплат.НайтиСтроки(ОтборНаименование);
		Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			НайденнаяСтрока.АналитикаОплаты = Выборка.Терминал;
			
			Модифицированность = Истина;
		КонецЦикла;
	КонецЦикла;
	
	ОбновитьКассыККМСоответствийОплат(Объект.СоответствиеВидовОплат);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьВидыОплатВ1СНаСервере()
	
	НачатьТранзакцию();
	
	// Остальные создаем
	Для каждого стр Из Объект.СоответствиеВидовОплат Цикл
		
		Если ЗначениеЗаполнено(стр.АналитикаОплаты) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СокрЛП(стр.СпособОплатыНаСайте)) Тогда
			Продолжить;
		КонецЕсли;
		
		ОбТерминал = Справочники.ЭквайринговыеТерминалы.СоздатьЭлемент();
		ОбТерминал.Наименование = стр.СпособОплатыНаСайте;
		
		ОсновнаяКасса = Неопределено;
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КассыККМ.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.КассыККМ КАК КассыККМ
		|ГДЕ
		|	НЕ КассыККМ.ПометкаУдаления
		|	И КассыККМ.ТипКассы = ЗНАЧЕНИЕ(Перечисление.ТипыКассККМ.ФискальныйРегистратор)
		|	И КассыККМ.ТипОборудования = ЗНАЧЕНИЕ(Перечисление.ТипыПодключаемогоОборудования.ККТ)
		|
		|УПОРЯДОЧИТЬ ПО
		|	КассыККМ.ПодключаемоеОборудование УБЫВ";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество()=1 Тогда
			Выборка.Следующий();
			ОсновнаяКасса = Выборка.Ссылка;
		КонецЕсли;
		ОбТерминал.Касса = ОсновнаяКасса;
		
		ОбТерминал.Организация = ?(ЗначениеЗаполнено(Объект.ОрганизацияДляПодстановкиВЗаказы), Объект.ОрганизацияДляПодстановкиВЗаказы, Справочники.Организации.ОрганизацияПоУмолчанию());
		ОбТерминал.СчетУчета = ПланыСчетов.Управленческий.ПереводыВПути;
		ОбТерминал.СчетЗатрат = ПланыСчетов.Управленческий.ПрочиеРасходы;
		
		ОбТерминал.БанковскийСчетЭквайринг = ОбТерминал.Организация.БанковскийСчетПоУмолчанию;
		ОбТерминал.Подразделение = Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение;
		ОбТерминал.НаправлениеДеятельности = Справочники.НаправленияДеятельности.ОсновноеНаправление;
		ОбТерминал.ИспользоватьБезПодключенияОборудования = Истина;
		
		Эквайрер = Справочники.Контрагенты.НайтиПоНаименованию(ОбТерминал.Наименование);
		Если НЕ ЗначениеЗаполнено(Эквайрер) Тогда
			ОбЭквайрер = Справочники.Контрагенты.СоздатьЭлемент();
			ОбЭквайрер.Наименование = ОбТерминал.Наименование;
			ОбЭквайрер.ВидКонтрагента = Перечисления.ВидыКонтрагентов.ЮридическоеЛицо;
			ОбЭквайрер.ПрочиеОтношения = Истина;
			
			ОбЭквайрер.ВестиРасчетыПоДоговорам = Истина;
			ОбЭквайрер.ВестиРасчетыПоДокументам = Истина;
			ОбЭквайрер.ВестиРасчетыПоЗаказам = Истина;
			ОбЭквайрер.ВестиУчетОплатыПоСчетам = Истина;
			
			ОбЭквайрер.СчетУчетаРасчетовСПокупателем = ПланыСчетов.Управленческий.РасчетыСПокупателями;
			ОбЭквайрер.СчетУчетаАвансовПокупателя = ПланыСчетов.Управленческий.РасчетыПоАвансамПолученным;
			ОбЭквайрер.СчетУчетаРасчетовСПоставщиком = ПланыСчетов.Управленческий.РасчетыСПоставщиками;
			ОбЭквайрер.СчетУчетаАвансовПоставщику = ПланыСчетов.Управленческий.РасчетыПоАвансамВыданным;
			
			ОсновнойОтветственный = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
			Пользователи.ТекущийПользователь(),
			"ОсновнойОтветственный");
			ОбЭквайрер.Ответственный = ОсновнойОтветственный;
			ОбЭквайрер.СтранаРегистрации = Справочники.СтраныМира.Россия;
			ОбЭквайрер.Родитель = Объект.ГруппаДляНовыхКонтрагентов;
			
			Попытка
				ОбЭквайрер.Записать();
			Исключение
			КонецПопытки;
			
			СписокВидовДоговора = Новый СписокЗначений;
			СписокВидовДоговора.Добавить(Перечисления.ВидыДоговоров.Прочее);
			ДоговорПоУмолчанию = Справочники.ДоговорыКонтрагентов.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Эквайрер, ОбТерминал.Организация, СписокВидовДоговора);
			Справочники.ДоговорыКонтрагентов.ЗаписатьДоговорПоУмолчанию(ДоговорПоУмолчанию);
			
			КонтактноеЛицо = Справочники.КонтактныеЛица.СоздатьЭлемент();
			КонтактноеЛицо.Наименование = ОбЭквайрер.Наименование;
			КонтактноеЛицо.Ответственный = ОсновнойОтветственный;
			Попытка
				КонтактноеЛицо.Записать();
			Исключение
			КонецПопытки;
			ОбЭквайрер.КонтактноеЛицо = КонтактноеЛицо.Ссылка;
			
			Попытка
				ОбЭквайрер.Записать();
				Эквайрер = ОбЭквайрер.Ссылка;
			Исключение
			КонецПопытки;
			
		КонецЕсли; 
		ОбТерминал.Эквайрер = Эквайрер;
		ОбТерминал.Договор = Справочники.ДоговорыКонтрагентов.ДоговорПоУмолчанию(Эквайрер.Ссылка);
		
		НовСтр = ОбТерминал.ВидыПлатежныхКарт.Добавить();
		НовСтр.ВидПлатежнойКарты = стр.СпособОплатыНаСайте;
		
		Попытка
			ОбТерминал.Записать();
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
		КонецПопытки;
		
		стр.АналитикаОплаты = ОбТерминал.Ссылка;
		Модифицированность = Истина;
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВидыОплатВ1С(Команда)
	
	Если Объект.СоответствиеВидовОплат.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСуществующиеВидыОплат();
	
	ЧислоНезаполненных = 0;
	Для каждого стр Из Объект.СоответствиеВидовОплат Цикл
		Если НЕ ЗначениеЗаполнено(стр.АналитикаОплаты) И ЗначениеЗаполнено(СокрЛП(стр.СпособОплатыНаСайте)) Тогда
			ЧислоНезаполненных = ЧислоНезаполненных+1;
		КонецЕсли;
	КонецЦикла;
	
	Если ЧислоНезаполненных>0 Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("СоздатьВидыОплатЗавершение", ЭтотОбъект), 
		НСтр("ru = 'Будут созданы и заполнены "+ЧислоНезаполненных+" видов оплат. Продолжить?'"),
		РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВидыОплатЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		СоздатьВидыОплатВ1СНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоответствиеВидовОплатТерминалПриИзменении(Элемент)
	
	ОбновитьКассыККМСоответствийОплат();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьКассыККМСоответствийОплат();
	
КонецПроцедуры

&НаСервере
Процедура СпособУстановкиДатыОтгрузкиЗаказаПриИзмененииНаСервере()
	
	Если Объект.СпособУстановкиДатыОтгрузкиЗаказа = Перечисления.СпособыУстановкиДатыОтгрузкиЗаказа.КоличествоДней Тогда
		Элементы.СпособУстановкиДатыОтгрузкиЗаказа.АвтоМаксимальнаяШирина = Ложь;
		Элементы.СпособУстановкиДатыОтгрузкиЗаказа.МаксимальнаяШирина = 31;
		Элементы.СпособУстановкиДатыОтгрузкиЗаказаКоличествоДней.Видимость = Истина;
	Иначе
		Элементы.СпособУстановкиДатыОтгрузкиЗаказа.АвтоМаксимальнаяШирина = Истина;
		Элементы.СпособУстановкиДатыОтгрузкиЗаказа.МаксимальнаяШирина = 0;
		Элементы.СпособУстановкиДатыОтгрузкиЗаказаКоличествоДней.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособУстановкиДатыОтгрузкиЗаказаПриИзменении(Элемент)
	СпособУстановкиДатыОтгрузкиЗаказаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоответствиеВидовОплатТерминалНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекСтрока = Элементы.СоответствиеВидовОплат.ТекущиеДанные;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПоискаКонтрагентовНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ШаблонНастройкиПоискаКонтрагентов = ОбменССайтомСлужебныйВызовСервера.ПоляПоискаКонтрагентов();
	НастройкиПоиска = ОбменССайтомСлужебныйВызовСервера.ЧтениеJSONВСтруктуру(Объект.НастройкиПоискаКонтрагентов);
	
	Для каждого стр Из ШаблонНастройкиПоискаКонтрагентов Цикл
		
		ИмяПоляПоиска = стр.Получить("Значение");
		СохраненныеНастройки = НастройкиПоиска.Получить(ИмяПоляПоиска);
		Если СохраненныеНастройки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для каждого сохраненноеЗначение Из СохраненныеНастройки Цикл
			
			Если стр.Получить(сохраненноеЗначение.Ключ) <> Неопределено Тогда
				
				стр.Вставить(сохраненноеЗначение.Ключ, сохраненноеЗначение.Значение);
				
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	Если НастройкиПоиска.Получить("ВариантПоиска")<>Неопределено Тогда
		ВариантПоиска = НастройкиПоиска.Получить("ВариантПоиска");
	Иначе
		ВариантПоиска = 0;
	КонецЕсли;
	
	СоответствиеНастроек = Новый Структура("НастройкиПоиска", ШаблонНастройкиПоискаКонтрагентов);
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("НастройкиПоискаСоответствие", СоответствиеНастроек);
	ПараметрыОткрытия.Вставить("ВариантПоиска", ВариантПоиска);
	ПараметрыОткрытия.Вставить("ЗаголовокФормы", НСтр("ru = 'Настройки сопоставления контрагентов при загрузке с сайта'"));
	ПараметрыОткрытия.Вставить("ЗаголовокПоляПоиска", НСтр("ru = 'Данные сайта для поиска покупателя в 1С'"));
	ОписаниеОповещения = Новый ОписаниеОповещения("НастройкиПоискаПоКонтактнойИнформацииЗакрытие", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.НастройкиПоискаПоКонтактнойИнформации", ПараметрыОткрытия, ЭтотОбъект
	,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПоискаПоКонтактнойИнформацииЗакрытие(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия<>Неопределено И ТипЗнч(РезультатЗакрытия) = Тип("Соответствие") Тогда
		
		НастройкиПоискаКонтрагентовПредставление = ПоискКонтрагентовПредставление(РезультатЗакрытия);
		Объект.НастройкиПоискаКонтрагентов = ОбменССайтомСлужебныйВызовСервера.ЗаписьJSONВСтруктуру(РезультатЗакрытия);
		ЭтотОбъект.Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПоискКонтрагентовПредставление(СоответствиеНастройки)
	
	СтрокаПредставление = "";
	СписокНастроекВПорядкеПриоритета = Новый СписокЗначений;
	Для каждого элНастройки Из СоответствиеНастройки Цикл
		
		Если элНастройки.Ключ = "ВариантПоиска" ИЛИ элНастройки.Ключ = "СовпадениеВсехПолей" Тогда
			Продолжить;
		КонецЕсли;
		
		Если элНастройки.Значение.Получить("КонтрагентыПометка") = Истина
			ИЛИ элНастройки.Значение.Получить("КонтактныеЛицаПометка") = Истина
			Тогда
			
			СписокНастроекВПорядкеПриоритета.Добавить(элНастройки.Значение.Получить("Порядок"), элНастройки.Значение.Получить("Значение"));
			
		КонецЕсли;
		
	КонецЦикла;
	
	СписокНастроекВПорядкеПриоритета.СортироватьПоЗначению();
	
	Для каждого СтрокаНастроек Из СписокНастроекВПорядкеПриоритета Цикл
		СтрокаПредставление = СтрокаПредставление + СтрокаНастроек + ", ";
	КонецЦикла;
	
	Если СтрокаПредставление="" Тогда
		СтрокаПредставление = "<Настройка не заполнена>";
	Иначе
		СтрокаПредставление = Лев(СтрокаПредставление, СтрДлина(СтрокаПредставление)-2);
	КонецЕсли;
	
	Возврат СтрокаПредставление;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НастройкиВыгрузкиРеквизитовПредставление(НастройкиВыгрузкиРеквизитов, НастройкиВыгрузкиДопРеквизитовНоменклатуры)
	
	СтрокаПредставление = "<Реквизиты не выбраны>";
	
	Если НастройкиВыгрузкиРеквизитов = "" И НастройкиВыгрузкиДопРеквизитовНоменклатуры = "" Тогда
		Возврат СтрокаПредставление;
	КонецЕсли;
	
	СтрокаПредставление = "";
	
	Если НастройкиВыгрузкиРеквизитов <> "" Тогда
		СтруктураНастройки = ОбменССайтомСлужебныйВызовСервера.ЧтениеJSONВСтруктуру(НастройкиВыгрузкиРеквизитов, Ложь);
		Для каждого СтрокаНастроек Из СтруктураНастройки Цикл
			СтрокаПредставление = СтрокаПредставление + СтрокаНастроек.Ключ + ", ";
		КонецЦикла;
		СтрокаПредставление = Лев(СтрокаПредставление, СтрДлина(СтрокаПредставление)-2);
	КонецЕсли;
	
	Если НастройкиВыгрузкиДопРеквизитовНоменклатуры <> "" Тогда
		
		СтрокаПредставление = СтрокаПредставление + ?(СтрокаПредставление = "","",", ");
		
		СтруктураНастройки = ОбменССайтомСлужебныйВызовСервера.ЧтениеJSONВСтруктуру(НастройкиВыгрузкиДопРеквизитовНоменклатуры, Ложь);
		Для каждого СтрокаНастроек Из СтруктураНастройки Цикл
			СтрокаПредставление = СтрокаПредставление + СтрокаНастроек.Ключ + ", ";
		КонецЦикла;
		СтрокаПредставление = Лев(СтрокаПредставление, СтрДлина(СтрокаПредставление)-2);
	КонецЕсли;
	
	Возврат СтрокаПредставление;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НастройкиВыгрузкиДопРеквизитовЗаказовПредставление(НастройкиВыгрузкиДопРеквизитовЗаказов)
	
	СтрокаПредставление = "";
	Если НастройкиВыгрузкиДопРеквизитовЗаказов = "" Тогда
		СтрокаПредставление = "<Реквизиты не выбраны>";
	Иначе
		СтруктураНастройки = ОбменССайтомСлужебныйВызовСервера.ЧтениеJSONВСтруктуру(НастройкиВыгрузкиДопРеквизитовЗаказов, Ложь);
		Для каждого СтрокаНастроек Из СтруктураНастройки Цикл
			СтрокаПредставление = СтрокаПредставление + СтрокаНастроек.Ключ + ", ";
		КонецЦикла;
		СтрокаПредставление = Лев(СтрокаПредставление, СтрДлина(СтрокаПредставление)-2);
	КонецЕсли;
	
	Возврат СтрокаПредставление;
	
КонецФункции

&НаКлиенте
Процедура НастройкиВыгрузкиРеквизитовПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьРеквизитыЗавершение", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("НастройкиВыгрузкиРеквизитов" , Объект.НастройкиВыгрузкиРеквизитов);
	ПараметрыФормы.Вставить("НастройкиВыгрузкиДопРеквизитов" , Объект.НастройкиВыгрузкиДопРеквизитовНоменклатуры);
	ПараметрыФормы.Вставить("НастройкиВыгрузкиДопРеквизитовНоменклатурыИспользовать" , Объект.НастройкиВыгрузкиДопРеквизитовНоменклатурыИспользовать);
	
	ОткрытьФорму("ПланОбмена.ОбменУправлениеНебольшойФирмойСайт.Форма.ФормаВыбораРеквизитов", ПараметрыФормы, ЭтотОбъект,,,,ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиВыгрузкиДопРеквизитовЗаказовПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьДопРеквизитыЗаказовЗавершение", ЭтотОбъект);
	ОткрытьФорму("ПланОбмена.ОбменУправлениеНебольшойФирмойСайт.Форма.ФормаВыбораРеквизитов", 
	Новый Структура("НастройкиВыгрузкиДопРеквизитовЗаказов, ЗаказПокупателя", Объект.НастройкиВыгрузкиДопРеквизитовЗаказов, Объект.Ссылка), 
	ЭтотОбъект,,,,ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьРеквизитыЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйЭлемент.НастройкиВыгрузкиРеквизитов.Количество()=0 Тогда
		Объект.НастройкиВыгрузкиРеквизитов = "";
	Иначе
		Объект.НастройкиВыгрузкиРеквизитов = ОбменССайтомСлужебныйВызовСервера.ЗаписьJSONВСтруктуру(ВыбранныйЭлемент.НастройкиВыгрузкиРеквизитов);
	КонецЕсли;
	
	Если ВыбранныйЭлемент.НастройкиВыгрузкиДопРеквизитов.Количество()=0 Тогда
		Объект.НастройкиВыгрузкиДопРеквизитовНоменклатуры = "";
	Иначе
		Объект.НастройкиВыгрузкиДопРеквизитовНоменклатуры = ОбменССайтомСлужебныйВызовСервера.ЗаписьJSONВСтруктуру(ВыбранныйЭлемент.НастройкиВыгрузкиДопРеквизитов);
	КонецЕсли;
	
	Объект.НастройкиВыгрузкиДопРеквизитовНоменклатурыИспользовать = Истина;
	
	НастройкиВыгрузкиРеквизитовПредставление = НастройкиВыгрузкиРеквизитовПредставление(Объект.НастройкиВыгрузкиРеквизитов,Объект.НастройкиВыгрузкиДопРеквизитовНоменклатуры);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДопРеквизитыЗаказовЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйЭлемент.НастройкиВыгрузкиДопРеквизитов.Количество()=0 Тогда
		Объект.НастройкиВыгрузкиДопРеквизитовЗаказов = "";
	Иначе
		Объект.НастройкиВыгрузкиДопРеквизитовЗаказов = ОбменССайтомСлужебныйВызовСервера.ЗаписьJSONВСтруктуру(ВыбранныйЭлемент.НастройкиВыгрузкиДопРеквизитов);
	КонецЕсли;
	
	НастройкиВыгрузкиДопРеквизитовЗаказовПредставление = НастройкиВыгрузкиДопРеквизитовЗаказовПредставление(Объект.НастройкиВыгрузкиДопРеквизитовЗаказов);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьДублированиеДоставки(ИмяКолонки)
	
	СоответствиеСлужбаДоставкиНаСайте = Элементы.СоответствиеСлужбДоставки.ТекущиеДанные.СлужбаДоставкиНаСайте;
	СоответствиеСлужбаДоставки1С = Элементы.СоответствиеСлужбДоставки.ТекущиеДанные.СлужбаДоставки;
	
	Если НЕ ПустаяСтрока(СоответствиеСлужбаДоставкиНаСайте) Тогда
		Найдено = Объект.СоответствиеСлужбДоставки.НайтиСтроки(Новый Структура("СлужбаДоставкиНаСайте", СоответствиеСлужбаДоставкиНаСайте));
		Если Найдено.Количество() > 1 Тогда
			ИмяКолонки = "СлужбаДоставкиНаСайте";
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура СоответствиеСлужбыДоставкиПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКолонки = "";
	Если НЕ ПроверитьДублированиеДоставки(ИмяКолонки) Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		НСтр("ru = 'Такая служба доставки уже указана в другой строке таблицы!'"),
		Объект.Ссылка,
		ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
		"Объект.СоответствиеСлужбДоставки", Объект.СоответствиеСлужбДоставки.Индекс(Элементы.СоответствиеСлужбДоставки.ТекущиеДанные) + 1, ИмяКолонки));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьОтгрузкиПриИзменении(Элемент)
	
	ПоказатьСтраницуОнлайнОплаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСтраницуОнлайнОплаты()
	
	Если Объект.ОнлайнОплаты Тогда
		СтруктураНастроек = Новый Структура;
		Если ПроверитьУстановкуКонстант(СтруктураНастроек) Тогда
			ВключитьОнлайнОплаты();
		Иначе
			ТекстВопроса = НСтр("ru = 'Для использования онлайн оплат нужно включить настройки: '");
			ТекстВопроса = ТекстВопроса + ?(СтруктураНастроек.УчетРозничныхПродаж, "", Символы.ПС+НСтр("ru = 'Продажи - Учет розничных продаж"));
			ТекстВопроса = ТекстВопроса + ?(СтруктураНастроек.ИспользоватьЗаказыВРозничнойТорговле, "", Символы.ПС+НСтр("ru = 'Продажи - Использовать заказы в розничной торговле'"));
			ТекстВопроса = ТекстВопроса + ?(СтруктураНастроек.ИспользоватьПодключаемоеОборудование, "", Символы.ПС+НСтр("ru = 'Администрирование - Использовать подключаемое оборудование'"));
			
			ТекстВопроса = ТекстВопроса + Символы.ПС+НСтр("ru = 'Включить настройки сейчас?'");
			ПоказатьВопрос(Новый ОписаниеОповещения("УстановитьНастройкиОнлайнОплат", ЭтотОбъект, СтруктураНастроек), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
	Иначе
		УстановитьВидимостьСтраницФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьНастройкиСайта(ОписаниеОшибки)
	
	НастройкиСайта = Новый Структура;	
	Если Объект.ПротоколОбменаCMS = ПредопределенноеЗначение("Перечисление.ПротоколыОбменаCMS.Битрикс") Тогда
		СтрокаXML = ОбменССайтом.ЗагрузитьНастройкиБитрикс(Объект.Ссылка, ОписаниеОшибки);
	ИначеЕсли Объект.ПротоколОбменаCMS = ПредопределенноеЗначение("Перечисление.ПротоколыОбменаCMS.UMI") Тогда
		СтрокаXML = ОбменССайтом.ЗагрузитьНастройкиUMI(Объект.Ссылка, ОписаниеОшибки);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Если СтрокаXML = "" Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось подключиться к сайту!'"));
		Возврат Неопределено;
	КонецЕсли; 
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(СтрокаXML);
	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.Имя = "Статусы" ИЛИ ЧтениеXML.Имя = "Cтатусы" Тогда
			// Битрикс возвращает имя узла с английской буквой "С" в слове "Статусы"
			МассивСтатусы = ПрочитатьЭлементы(ЧтениеXML, ЧтениеXML.Имя);
			НастройкиСайта.Вставить("Статусы", МассивСтатусы);
		ИначеЕсли ЧтениеXML.Имя = "ПлатежныеСистемы" Тогда
			МассивВидыОплат = ПрочитатьЭлементы(ЧтениеXML, "ПлатежныеСистемы");
			НастройкиСайта.Вставить("ПлатежныеСистемы", МассивВидыОплат);
		ИначеЕсли ЧтениеXML.Имя = "СлужбыДоставки" Тогда
			МассивСлужбыДоставки = ПрочитатьЭлементы(ЧтениеXML, "СлужбыДоставки");
			НастройкиСайта.Вставить("СлужбыДоставки", МассивСлужбыДоставки);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НастройкиСайта;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьСлужбыДоставкиССайтаНаСервере()
	
	ОписаниеОшибки = "";
	НастройкиСайта = ЗагрузитьНастройкиСайта(ОписаниеОшибки);
	
	Если НЕ ЗначениеЗаполнено(НастройкиСайта) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось загрузить список служб доставки с сайта!'")
		+Символы.ПС + ОписаниеОшибки);
		Возврат;
	КонецЕсли; 
	
	Если НастройкиСайта.Свойство("СлужбыДоставки") Тогда
		Объект.СоответствиеСлужбДоставки.Очистить();
		Для каждого стр Из НастройкиСайта.СлужбыДоставки Цикл
			
			НовСтр = Объект.СоответствиеСлужбДоставки.Добавить();
			НовСтр.ИдСлужбыДоставкиНаСайте = стр.Ид;
			НовСтр.СлужбаДоставкиНаСайте = стр.Название;
			
		КонецЦикла;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСлужбыДоставкиССайта(Команда)
	ЗагрузитьСлужбыДоставкиССайтаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтветыНаЧастоЗадаваемыеВопросы(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Заголовок", "FAQ");
	ПараметрыОткрытия.Вставить("КлючПодсказки", "ОбменССайтом_FAQ");
	ОткрытьФорму("Обработка.МенеджерПодсказок.Форма", ПараметрыОткрытия);	
	
КонецПроцедуры

// Процедура - обработчик команды КакОпубликоватьВебСервис.
//
&НаКлиенте
Процедура КакОпубликоватьВебСервис(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Заголовок", "Публикация на web-сервере");
	ПараметрыОткрытия.Вставить("КлючПодсказки", "ПомощникНастроекОбменаССайтом_ОбменЧерезВебСервис");
	ОткрытьФорму("Обработка.МенеджерПодсказок.Форма", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПробиватьЧекиПоОнлайнОплатамПриИзменении(Элемент)
	
	Если Объект.ПробиватьЧекиПоОнлайнОплатам И НЕ ПробиватьЧекиПоОнлайнОплатамПриИзмененииНаСервере() Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		СтрШаблон(НСтр("ru = 'Нет подключенного ККТ с флагом «Автоматическая фискализация» для организации %1'"),
		Объект.ОрганизацияДляПодстановкиВЗаказы),
		Объект.ОрганизацияДляПодстановкиВЗаказы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПробиватьЧекиПоОнлайнОплатамПриИзмененииНаСервере()
	
	Если НЕ ПодключаемоеОборудованиеУНФ.ЕстьККТСАвтоматическойФискализацией(Объект.ОрганизацияДляПодстановкиВЗаказы) Тогда
		
		Объект.ПробиватьЧекиПоОнлайнОплатам = Ложь;
		Возврат Ложь;
		
	КонецЕсли; 
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ТаблицаКаталогов

// Процедура - обработчик события ПередОкончаниемРедактирования таблицы ТаблицаКаталогов.
//
&НаКлиенте
Процедура ТаблицаКаталоговПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Отказ = НЕ ПроверитьУникальностьИдентификатора();
	
КонецПроцедуры

// Процедура - обработчик события ПриОкончанииРедактирования таблицы ТаблицаКаталогов.
//
&НаКлиенте
Процедура ТаблицаКаталоговПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные = НеОпределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьВыбранныеГруппыСерверБезКонтекста(Элемент.ТекущиеДанные.Группы, НадписьВсеЭлементыСписка, Элементы.ТаблицаКаталоговГруппыКатегории.ВыборГруппИЭлементов);
	
	Если ПустаяСтрока(Элемент.ТекущиеДанные.ИдентификаторКаталога) Тогда
		Элемент.ТекущиеДанные.ИдентификаторКаталога = Строка(Новый УникальныйИдентификатор);
	КонецЕсли;
	
	Если ПустаяСтрока(Элемент.ТекущиеДанные.Каталог) Тогда
		Элемент.ТекущиеДанные.Каталог = НСтр("ru = 'Каталог товаров'") + " " + ВРег(СокрЛП(Лев(Элемент.ТекущиеДанные.ИдентификаторКаталога, 8)));
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбработатьВыбранныеГруппыСерверБезКонтекста(СписокГрупп, НадписьВсеЭлементыСписка, ВыборГруппИЭлементов)
	
	ГруппыВыбраны = Ложь;
	
	Если ВыборГруппИЭлементов = ГруппыИЭлементы.Группы Тогда
		// Удаляем не группы номенклатуры.
		МассивУдалить = Новый Массив;
		Для Каждого ЭлементСЗ Из СписокГрупп Цикл
			
			ТекГруппа = ЭлементСЗ.Значение;
			Если ЗначениеЗаполнено(ТекГруппа) И НЕ ТекГруппа.ЭтоГруппа Тогда
				МассивУдалить.Добавить(ЭлементСЗ);
				Продолжить;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТекГруппа) Тогда
				ЭлементСЗ.Представление = Строка(ЭлементСЗ.Значение);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ТекГруппа) И НЕ ЗначениеЗаполнено(ЭлементСЗ.Представление) Тогда
				ЭлементСЗ.Представление = ОбменССайтом.НадписьНетГруппы();
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого ЭлементМУ Из МассивУдалить Цикл
			СписокГрупп.Удалить(ЭлементМУ);
		КонецЦикла;
	КонецЕсли;
	
	// Удаляем дубли и подчиненные элементы.
	МассивУдалить = Новый Массив;
	Для Каждого ЭлементСЗ Из СписокГрупп Цикл
		
		Если НЕ МассивУдалить.Найти(ЭлементСЗ) = НеОпределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ТекГруппа = ЭлементСЗ.Значение;
		
		Для Каждого ЭлементСпискаВложенный Из СписокГрупп Цикл
			
			Если НЕ МассивУдалить.Найти(ЭлементСпискаВложенный) = НеОпределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ТекГруппа)
				ИЛИ НЕ ЗначениеЗаполнено(ЭлементСпискаВложенный.Значение) Тогда
				// Пропускаем элемент "Все" и "Нет группы"
				Продолжить;
			КонецЕсли;
			
			Если НЕ ЭлементСпискаВложенный = ЭлементСЗ
				И ЭлементСпискаВложенный.Значение = ТекГруппа Тогда
				
				МассивУдалить.Добавить(ЭлементСпискаВложенный);
				
			Иначе
				
				Если ЭлементСпискаВложенный.Значение.ПринадлежитЭлементу(ТекГруппа) Тогда
					
					МассивУдалить.Добавить(ЭлементСпискаВложенный);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ЭлементМУ Из МассивУдалить Цикл
		
		СписокГрупп.Удалить(ЭлементМУ);
		
	КонецЦикла;
	
	Для Каждого ЭлементСЗ Из СписокГрупп Цикл
		
		Если ЗначениеЗаполнено(ЭлементСЗ.Значение) ИЛИ ЗначениеЗаполнено(ЭлементСЗ.Представление) Тогда
			
			ГруппыВыбраны = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ГруппыВыбраны Тогда
		
		СписокГрупп.Очистить();
		СписокГрупп.Добавить(Неопределено, НадписьВсеЭлементыСписка);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПослеУдаления таблицы ТаблицаКаталогов.
//
&НаКлиенте
Процедура ТаблицаКаталоговПослеУдаления(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

// Процедура - обработчик события ПриНачалеРедактирования таблицы ТаблицаКаталогов.
//
&НаКлиенте
Процедура ТаблицаКаталоговПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Копирование Тогда
		
		Элемент.ТекущиеДанные.ИдентификаторКаталога = "";
		
	КонецЕсли;
	
	Если (Элемент.ТекущиеДанные.Группы.Количество() = 1
		И НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.Группы[0].Значение) 
		И Элемент.ТекущиеДанные.Группы[0].Представление = НадписьВсеЭлементыСписка)
		ИЛИ Элемент.ТекущиеДанные.Группы.Количество() = 0 Тогда
		
		НовыйСписокГрупп = Новый СписокЗначений;
		
		Если Объект.ОтборГруппыКатегорииНоменклатуры = ПредопределенноеЗначение("Перечисление.ВидыОтборовНоменклатуры.ГруппыНоменклатуры") Тогда
			ТипЗначений = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
			Элементы.ТаблицаКаталоговГруппыКатегории.ВыборГруппИЭлементов = ГруппыИЭлементы.Группы;
		Иначе
			ТипЗначений = Новый ОписаниеТипов("СправочникСсылка.КатегорииНоменклатуры");
			Элементы.ТаблицаКаталоговГруппыКатегории.ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;
		КонецЕсли;
		
		НовыйСписокГрупп.ТипЗначения = ТипЗначений;
		НовыйСписокГрупп.Добавить(Неопределено, НадписьВсеЭлементыСписка);
		Элемент.ТекущиеДанные.Группы = НовыйСписокГрупп;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаСтруктурыКаталогаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	Элементы.ТаблицаКаталогов.ТекущиеДанные.РучнаяНастройкаКаталога = Результат.ДетализацияОтбора;
	Элементы.ТаблицаКаталогов.ТекущиеДанные.ТекущаяСтрокаИдентификатор = Результат.ТекущаяСтрокаИдентификатор;
	Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекСтруктурыКаталога = Результат.АдресВХранилище;
	
КонецПроцедуры // ОткрытьФормуОтбораКаталога()

Процедура ТаблицаКаталоговСохранить(ТекущийОбъект)
	
	ТаблицаКаталоговТЗ = ДанныеФормыВЗначение(ТаблицаКаталогов, Тип("ТаблицаЗначений"));
	ТаблицаКаталоговТЗ.Колонки.Добавить("ХранилищеНастроекКомпоновки", Новый ОписаниеТипов("ХранилищеЗначения"));
	
	Для Каждого СтрокаТаблицыКаталогов Из ТаблицаКаталоговТЗ Цикл
		
		Если ЭтоАдресВременногоХранилища(СтрокаТаблицыКаталогов.АдресНастроекКомпоновки) Тогда
			НастройкиКомпоновки = ПолучитьИзВременногоХранилища(СтрокаТаблицыКаталогов.АдресНастроекКомпоновки);
			СтрокаТаблицыКаталогов.ХранилищеНастроекКомпоновки = Новый ХранилищеЗначения(НастройкиКомпоновки);
		КонецЕсли;
		
		ОбновитьНастройкиСтруктурыКаталога(СтрокаТаблицыКаталогов);
	КонецЦикла;
	
	ТаблицаКаталоговТЗ.Колонки.Удалить("АдресНастроекКомпоновки");
	ТаблицаКаталоговТЗ.Колонки.Удалить("АдресНастроекСтруктурыКаталога");
	
	ТекущийОбъект.СохраненнаяТаблицаКаталогов = Новый ХранилищеЗначения(ТаблицаКаталоговТЗ);
	
	ОчиститьУдаленныеНастройкиСтруктурыКаталога();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипЗначенийСпискаГруппТаблицыКаталоговСервер()
	
	Если Объект.ОтборГруппыКатегорииНоменклатуры = Перечисления.ВидыОтборовНоменклатуры.ГруппыНоменклатуры Тогда
		ТипЗначений = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	Иначе
		ТипЗначений = Новый ОписаниеТипов("СправочникСсылка.КатегорииНоменклатуры");
	КонецЕсли;	
	
	Для Каждого СтрокаТаблицыКаталогов Из ТаблицаКаталогов Цикл
		СтрокаТаблицыКаталогов.Группы.ТипЗначения = ТипЗначений;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыТаблицыКаталоговСервер()
	
	ЗаголовокТаблицы = НСтр("ru = 'Таблица каталогов (соответствие групп номенклатуры каталогам на сайте)'");
	Если Объект.ОтборГруппыКатегорииНоменклатуры = Перечисления.ВидыОтборовНоменклатуры.ГруппыНоменклатуры Тогда
		ЗаголовокКолонки = НСтр("ru = 'Группы номенклатуры'");
		ВыборГруппИЭлементов = ГруппыИЭлементы.Группы;
	Иначе
		ЗаголовокКолонки = НСтр("ru = 'Категории номенклатуры'");
		ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;
	КонецЕсли;
	
	Элементы.ГруппаТаблицаКаталогов.Заголовок = ЗаголовокТаблицы;
	Элементы.ТаблицаКаталоговГруппыКатегории.Заголовок = ЗаголовокКолонки;
	Элементы.ТаблицаКаталоговГруппыКатегории.ВыборГруппИЭлементов = ВыборГруппИЭлементов;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуКаталоговСервер()
	
	НадписьВсеЭлементыСписка = ОбменССайтом.НадписьВсеГруппы();
	
	СохраненнаяТаблицаКаталогов = РеквизитФормыВЗначение("Объект").СохраненнаяТаблицаКаталогов.Получить();
	
	Если НЕ ТипЗнч(СохраненнаяТаблицаКаталогов) = Тип("ТаблицаЗначений") Тогда
		
		СоздатьКаталогПоУмолчаниюСервер();
		
	Иначе
		
		Для Каждого СтрокаСохраненнойТаблицыКаталогов Из СохраненнаяТаблицаКаталогов Цикл
			
			НоваяСтрока = ТаблицаКаталогов.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСохраненнойТаблицыКаталогов);
			
			ХранилищеНастроекКомпоновки = СтрокаСохраненнойТаблицыКаталогов.ХранилищеНастроекКомпоновки.Получить();
			НоваяСтрока.АдресНастроекКомпоновки = ПоместитьВоВременноеХранилище(ХранилищеНастроекКомпоновки, УникальныйИдентификатор);
			
		КонецЦикла;
		
		Если ТаблицаКаталогов.Количество() = 0 Тогда
			
			СоздатьКаталогПоУмолчаниюСервер();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьКаталогПоУмолчаниюСервер()
	
	НоваяСтрока = ТаблицаКаталогов.Добавить();
	НоваяСтрока.Каталог = НСтр("ru = 'Основной каталог товаров'");
	НоваяСтрока.Группы.Добавить(Неопределено, НадписьВсеЭлементыСписка);
	НоваяСтрока.ИдентификаторКаталога = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКаталоговГруппыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Группы = Элементы.ТаблицаКаталогов.ТекущиеДанные.Группы;
	
	Если Группы.Количество() = 1 Тогда
		
		Если НЕ ЗначениеЗаполнено(Группы[0].Значение) 
			И Объект.ОтборГруппыКатегорииНоменклатуры = ПредопределенноеЗначение("Перечисление.ВидыОтборовНоменклатуры.КатегорииНоменклатуры") Тогда
			
			Группы.Очистить();
		ИначеЕсли НЕ ЗначениеЗаполнено(Группы[0].Значение) И Группы[0].Представление = НадписьВсеЭлементыСписка Тогда
			Группы.Очистить();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОтбораКаталога(АдресНастроекКомпоновки)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресНастроекКомпоновки", АдресНастроекКомпоновки);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуОтбораКаталогаЗавершение", ЭтотОбъект);
	
	РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("ПланОбмена.ОбменУправлениеНебольшойФирмойСайт.Форма.ФормаНастройкиОтбора", ПараметрыФормы, ЭтаФорма,,,,ОписаниеОповещения, РежимОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОтбораКаталогаЗавершение(НастройкиКомпоновки, ДополнительныеПараметры) Экспорт
	
	Если НастройкиКомпоновки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	АдресНастроекКомпоновки = ПоместитьВоВременноеХранилище(НастройкиКомпоновки, УникальныйИдентификатор);
	
	Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекКомпоновки = АдресНастроекКомпоновки;
	
КонецПроцедуры // ОткрытьФормуОтбораКаталога()

&НаКлиенте
Функция ПроверитьУникальностьИдентификатора()
	
	ИдентификаторКаталога = Элементы.ТаблицаКаталогов.ТекущиеДанные.ИдентификаторКаталога;
	Найдено = ТаблицаКаталогов.НайтиСтроки(Новый Структура("ИдентификаторКаталога", ИдентификаторКаталога));
	ИдентификаторыУникальны = Найдено.Количество() = 1;
	
	Если НЕ ИдентификаторыУникальны Тогда
		
		ОчиститьСообщения();
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		НСтр("ru = 'Идентификатор каталога должен быть уникальным!'"),
		Объект.Ссылка,
		ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
		"ТаблицаКаталогов", ТаблицаКаталогов.Индекс(Элементы.ТаблицаКаталогов.ТекущиеДанные) + 1, "ИдентификаторКаталога"));
		
		Элементы.ТаблицаКаталогов.ТекущиеДанные.ИдентификаторКаталога = Новый УникальныйИдентификатор;
		
	КонецЕсли;
	
	Возврат ИдентификаторыУникальны;
	
КонецФункции

// Процедура - обработчик команды НастроитьОтбор.
//
&НаКлиенте
Процедура НастроитьОтбор(Команда)
	
	Если Элементы.ТаблицаКаталогов.ТекущиеДанные = НеОпределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуОтбораКаталога(Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекКомпоновки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборГруппыКатегорииНоменклатурыПриИзменении(Элемент)
	
	Если ТаблицаКаталогов.Количество()>0 Тогда
		ТаблицаКаталогов.Очистить();
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("УстановитьПараметрыТаблицыКаталоговКлиент", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыТаблицыКаталоговКлиент()
	
	ЗаголовокТаблицы = НСтр("ru = 'Таблица каталогов (соответствие групп номенклатуры каталогам на сайте)'");
	Если Объект.ОтборГруппыКатегорииНоменклатуры = ПредопределенноеЗначение("Перечисление.ВидыОтборовНоменклатуры.ГруппыНоменклатуры") Тогда
		ЗаголовокКолонки = НСтр("ru = 'Группы номенклатуры'");
		ВыборГруппИЭлементов = ГруппыИЭлементы.Группы;
	Иначе
		ЗаголовокКолонки = НСтр("ru = 'Категории номенклатуры'");
		ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;
	КонецЕсли;
	
	Элементы.ГруппаТаблицаКаталогов.Заголовок = ЗаголовокТаблицы;
	Элементы.ТаблицаКаталоговГруппыКатегории.Заголовок = ЗаголовокКолонки;
	Элементы.ТаблицаКаталоговГруппыКатегории.ВыборГруппИЭлементов = ВыборГруппИЭлементов;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиУслугВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекСтрока = Элемент.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуРедактирования(ТекСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактирования(ТекСтрока = Неопределено)
	
	Если ТекСтрока = Неопределено Тогда
		ПараметрыФормы = Новый Структура("ИсточникЗаписи, УзелОбмена", 
		ПредопределенноеЗначение("Перечисление.ЗаписьНаУслугиИсточник.Сайт"),
		Объект.Ссылка);
	ИначеЕсли ТекСтрока.РодительскаяГруппировкаСтроки = Неопределено Тогда
		ПараметрыФормы = Новый Структура("Услуга, ИсточникЗаписи, УзелОбмена", 
		ТекСтрока.Услуга, 
		ПредопределенноеЗначение("Перечисление.ЗаписьНаУслугиИсточник.Сайт"), 
		Объект.Ссылка);
	Иначе
		ПараметрыФормы = Новый Структура("Услуга, ИсточникЗаписи, УзелОбмена", 
		ТекСтрока.РодительскаяГруппировкаСтроки.Ключ, 
		ПредопределенноеЗначение("Перечисление.ЗаписьНаУслугиИсточник.Сайт"), 
		Объект.Ссылка);
	КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("ФормаРедактированияОбработкаВыбора", ЭтотОбъект);	
	ОткрытьФорму("РегистрСведений.НастройкиЗаписьНаУслуги.Форма.ФормаРедактирования", ПараметрыФормы,,,,,ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаРедактированияОбработкаВыбора(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.НастройкиЗаписьНаУслуги.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗаписьНаУслугиПередНачаломИзменения(Элемент, Отказ)
	
	ТекСтрока = Элемент.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	ОткрытьФормуРедактирования(ТекСтрока);	
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НастройкиЗаписьНаУслугиПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Для каждого стр Из Строки Цикл
		стр.Значение.Данные.УслугаРесурс = стр.Ключ.Ресурс;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗаписьНаУслугиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ОткрытьФормуРедактирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКаталоговПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	ПоказатьВопрос(Новый ОписаниеОповещения("УдалитьКаталогЗавершение", ЭтотОбъект), 
	НСтр("ru = 'Настройки структуры каталога и идентификатор каталога будут удалены. Продолжить?'"),
	РежимДиалогаВопрос.ОКОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьКаталогЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Модифицированность = Истина;
		ТаблицаКаталогов.Удалить(Элементы.ТаблицаКаталогов.ТекущиеДанные);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьНастройкиСтруктурыКаталога(СтрокаТаблицыКаталогов)
	
	Если НЕ ЗначениеЗаполнено(СтрокаТаблицыКаталогов.АдресНастроекСтруктурыКаталога) Тогда
		// не было изменений
		Возврат;
	КонецЕсли;
	
	НЗ = РегистрыСведений.ОбменССайтомСтруктураКаталога.СоздатьНаборЗаписей();
	НЗ.Отбор.УзелОбмена.Установить(Объект.Ссылка);
	НЗ.Отбор.ИдентификаторКаталога.Установить(СтрокаТаблицыКаталогов.ИдентификаторКаталога);
	
	СохраненныеДанные = ПолучитьИзВременногоХранилища(СтрокаТаблицыКаталогов.АдресНастроекСтруктурыКаталога);
	СохраненныеДанные.ЗаполнитьЗначения(СтрокаТаблицыКаталогов.ИдентификаторКаталога, "ИдентификаторКаталога");
	СохраненныеДанные.Свернуть("УзелОбмена, ИдентификаторКаталога, Номенклатура, ГруппаКаталога, РодительИдентификатор, 
	|Идентификатор, РодительГруппа, РодительКатегория, ЭтоГруппа");
	
	НЗ.Загрузить(СохраненныеДанные);
	НЗ.Записать();
	
КонецПроцедуры

Процедура ОчиститьУдаленныеНастройкиСтруктурыКаталога()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбменССайтомСтруктураКаталога.ИдентификаторКаталога КАК ИдентификаторКаталога
	|ИЗ
	|	РегистрСведений.ОбменССайтомСтруктураКаталога КАК ОбменССайтомСтруктураКаталога
	|ГДЕ
	|	ОбменССайтомСтруктураКаталога.УзелОбмена = &УзелОбмена";
	Запрос.УстановитьПараметр("УзелОбмена", Объект.Ссылка);
	
	СохраненныеИдентификаторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИдентификаторКаталога");
	Для каждого Идентификатор Из СохраненныеИдентификаторы Цикл
		
		Если ТаблицаКаталогов.НайтиСтроки(Новый Структура("ИдентификаторКаталога", Идентификатор)).Количество() = 0 Тогда
			
			НЗ = РегистрыСведений.ОбменССайтомСтруктураКаталога.СоздатьНаборЗаписей();
			НЗ.Отбор.УзелОбмена.Установить(Объект.Ссылка);
			НЗ.Отбор.ИдентификаторКаталога.Установить(Идентификатор);
			НЗ.Записать(Истина);
			
		КонецЕсли; 
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКаталоговИдентификаторКаталогаИзменениеТекстаРедактирования(ИдентификаторыКаталога)
	
	ТекДанные = Элементы.ТаблицаКаталогов.ТекущиеДанные;
	Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекСтруктурыКаталога = ПрочитатьНастройкиКаталогаВоВременноеХранилище(ТекДанные.ИдентификаторКаталога);
	
КонецПроцедуры

&НаСервере
Функция ПрочитатьНастройкиКаталогаВоВременноеХранилище(ИдентификаторКаталога)
	
	НЗ = РегистрыСведений.ОбменССайтомСтруктураКаталога.СоздатьНаборЗаписей();
	НЗ.Отбор.УзелОбмена.Установить(Объект.Ссылка);
	НЗ.Отбор.ИдентификаторКаталога.Установить(ИдентификаторКаталога);
	НЗ.Прочитать();
	
	Возврат ПоместитьВоВременноеХранилище(НЗ.Выгрузить(), ЭтаФорма.УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура УдалитьУслугуНаСервере(Услуга)
	
	НЗ = РегистрыСведений.НастройкиЗаписьНаУслуги.СоздатьНаборЗаписей();
	НЗ.Отбор.ИсточникЗаписи.Установить(Перечисления.ЗаписьНаУслугиИсточник.Сайт);
	НЗ.Отбор.УзелОбмена.Установить(Объект.Ссылка);
	НЗ.Отбор.Услуга.Установить(Услуга);
	НЗ.Записать(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередУдалениемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		УдалитьУслугуНаСервере(ДополнительныеПараметры);
		Элементы.НастройкиЗаписьНаУслуги.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьУслугу(Команда)
	
	ТекДанные = Элементы.НастройкиЗаписьНаУслуги.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекДанные, "Услуга") Тогда
		ТекУслуга = ТекДанные.Услуга;
	Иначе
		ТекУслуга = ТекДанные.РодительскаяГруппировкаСтроки.Ключ;
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПередУдалениемЗавершение", ЭтотОбъект, ТекУслуга);
	ПоказатьВопрос(ОповещениеОЗакрытии, НСтр("ru = 'Удалить настройки для услуги "+ ТекУслуга +"?'"), РежимДиалогаВопрос.ОКОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКаталоговВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущийЭлемент.Имя = "ТаблицаКаталоговСтруктураКаталога" Тогда
		
		ТекДанные = Элемент.ТекущиеДанные;
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Ключ", Объект.Ссылка);
		СтруктураПараметров.Вставить("ОтборГруппыКатегорииНоменклатуры", Объект.ОтборГруппыКатегорииНоменклатуры);
		СтруктураПараметров.Вставить("ТаблицаКаталоговГруппы", ТекДанные.Группы);
		СтруктураПараметров.Вставить("ИдентификаторКаталога", ТекДанные.ИдентификаторКаталога);
		СтруктураПараметров.Вставить("АдресНастроекСтруктурыКаталога", ТекДанные.АдресНастроекСтруктурыКаталога);
		СтруктураПараметров.Вставить("РучнаяНастройкаКаталога", ТекДанные.РучнаяНастройкаКаталога);
		СтруктураПараметров.Вставить("ТекущаяСтрокаИдентификатор", ТекДанные.ТекущаяСтрокаИдентификатор);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаСтруктурыКаталогаЗавершение", ЭтотОбъект);
		
		ОткрытьФорму("ПланОбмена.ОбменУправлениеНебольшойФирмойСайт.Форма.НастройкаСтруктурыКаталога", СтруктураПараметров, ЭтотОбъект,,,,ОписаниеОповещения);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСайт(Команда)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Объект.ПереходАдресСайта, ЭтотОбъект,
	"Объект.ПереходАдресСайта", "Введите адрес перехода на сайт");
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьАдминзону(Команда)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Объект.ПереходURLАдминЗоны, ЭтотОбъект,
	"Объект.ПереходURLАдминЗоны", "Введите адрес перехода в админзону сайта");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьКартинкиПриИзменении(Элемент)
	
	Если Объект.ВыгружатьКартинки Тогда
		Элементы.ОбязательноеНаличиеФотографий.ТолькоПросмотр = Ложь;
	Иначе
		Объект.ОбязательноеНаличиеФотографий = Ложь;
		Элементы.ОбязательноеНаличиеФотографий.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормыПереопределяемая

&НаКлиенте
Процедура ПереходАдресСайтаНажатие(Элемент, СтандартнаяОбработка)
	
	URLСайта = Объект.ПереходАдресСайта;
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(URLСайта) Тогда
		Если НЕ Лев(URLСайта, 4) = "http" Тогда
			URLСайта = "https://" + URLСайта;
		КонецЕсли;
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(URLСайта);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходURLАдминЗоныНажатие(Элемент, СтандартнаяОбработка)
	
	URLСайта = Объект.ПереходURLАдминЗоны;
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(URLСайта) Тогда
		Если НЕ Лев(URLСайта, 4) = "http" Тогда
			URLСайта = "https://" + URLСайта;
		КонецЕсли;
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(URLСайта);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСоответствиеПолейНоменклатурыПоУмолчаниюНаСервере()
	
	Объект.СоответствиеПолейНоменклатуры.Очистить();
	
	НовСтр = Объект.СоответствиеПолейНоменклатуры.Добавить();
	НовСтр.ПолеНоменклатуры = "Наименование";
	НовСтр.НаименованиеПоля1С = "Наименование";
	НовСтр.НаименованиеПоляXML = "Наименование";
	
	НовСтр = Объект.СоответствиеПолейНоменклатуры.Добавить();
	НовСтр.ПолеНоменклатуры = "Комментарий";
	НовСтр.НаименованиеПоля1С = "Описание";
	НовСтр.НаименованиеПоляXML = "Описание";
	
	НовСтр = Объект.СоответствиеПолейНоменклатуры.Добавить();
	НовСтр.ПолеНоменклатуры = "Штрихкод";
	НовСтр.НаименованиеПоля1С = "Штрихкод";
	НовСтр.НаименованиеПоляXML = "Штрихкод";
	
	НовСтр = Объект.СоответствиеПолейНоменклатуры.Добавить();
	НовСтр.ПолеНоменклатуры = "Артикул";
	НовСтр.НаименованиеПоля1С = "Артикул";
	НовСтр.НаименованиеПоляXML = "Артикул";
	
	НовСтр = Объект.СоответствиеПолейНоменклатуры.Добавить();
	НовСтр.ПолеНоменклатуры = "Код";
	НовСтр.НаименованиеПоля1С = "Код";
	НовСтр.НаименованиеПоляXML = "Код";
	
	НовСтр = Объект.СоответствиеПолейНоменклатуры.Добавить();
	НовСтр.ПолеНоменклатуры = "СтранаПроисхождения";
	НовСтр.НаименованиеПоля1С = "Страна происхождения";
	НовСтр.НаименованиеПоляXML = "Страна";
	
	НовСтр = Объект.СоответствиеПолейНоменклатуры.Добавить();
	НовСтр.ПолеНоменклатуры = "Вес";
	НовСтр.НаименованиеПоля1С = "Вес";
	НовСтр.НаименованиеПоляXML = "Вес";
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСоответствиеПолейНоменклатурыПоУмолчанию(Команда)
	ЗаполнитьСоответствиеПолейНоменклатурыПоУмолчаниюНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоответствиеПолейНоменклатурыНаименованиеПоля1СОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбранноеЗначениеСтрока = Элемент.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	
	ТекСтрока = Элементы.СоответствиеПолейНоменклатуры.ТекущиеДанные;
	ТекСтрока.ПолеНоменклатуры = ВыбранноеЗначениеСтрока.Значение;
	ТекСтрока.НаименованиеПоля1С = ВыбранноеЗначениеСтрока.Представление;
	
	Элементы.СоответствиеПолейНоменклатуры.ЗакончитьРедактированиеСтроки(Ложь);
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти
