#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьПараметрыДинамическогоСписка();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	Если ТипЗнч(ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ПометкаУдаления
		И ТекущиеДанные.СтатусОбмена > 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'При следующем сеансе обмена в мобильном приложении будут удалены все данные.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоставОтправляемыхДанных(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	Если ТипЗнч(ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		ТекстПредупреждения = НСтр("ru = 'Действие недоступно для строки группировки списка!'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	Иначе
		ОбменДаннымиКлиент.ОткрытьСоставОтправляемыхДанных(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПараметрыДинамическогоСписка()
	
	ТекстЗапроса = ТекстЗапросаДинамическогоСпискаНастроек();
	
	Если ОбщегоНазначения.РазделениеВключено()
		И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РегистрСведений.СостоянияОбменовДанными",
			"РегистрСведений.СостоянияОбменовДаннымиОбластейДанных");
	КонецЕсли;
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ОсновнаяТаблица = "ПланОбмена.ОбменСМобильнымиПриложениями";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
	ГруппировкаПоВидуНастройкиОбмена = Список.Группировка.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ГруппировкаПоВидуНастройкиОбмена.Поле = Новый ПолеКомпоновкиДанных("МобильноеПриложение");
	ГруппировкаПоВидуНастройкиОбмена.Использование = Истина;
КонецПроцедуры

Функция ТекстЗапросаДинамическогоСпискаНастроек()
	
	Возврат "ВЫБРАТЬ
	|	СостоянияОбменовДанными.УзелИнформационнойБазы КАК УзелИнформационнойБазы,
	|	МАКСИМУМ(СостоянияОбменовДанными.ДатаОкончания) КАК ДатаОкончания
	|ПОМЕСТИТЬ СостоянияОбменовДаннымиМаксДата
	|ИЗ
	|	РегистрСведений.СостоянияОбменовДанными КАК СостоянияОбменовДанными
	|
	|СГРУППИРОВАТЬ ПО
	|	СостоянияОбменовДанными.УзелИнформационнойБазы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СостоянияОбменовДанными.УзелИнформационнойБазы КАК УзелИнформационнойБазы,
	|	СостоянияОбменовДанными.РезультатВыполненияОбмена КАК РезультатВыполненияОбмена,
	|	СостоянияОбменовДанными.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ РезультатыВыполненияОбмена
	|ИЗ
	|	РегистрСведений.СостоянияОбменовДанными КАК СостоянияОбменовДанными
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СостоянияОбменовДаннымиМаксДата КАК СостоянияОбменовДаннымиМаксДата
	|		ПО (СостоянияОбменовДаннымиМаксДата.УзелИнформационнойБазы = СостоянияОбменовДанными.УзелИнформационнойБазы)
	|			И (СостоянияОбменовДаннымиМаксДата.ДатаОкончания = СостоянияОбменовДанными.ДатаОкончания)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбменСМобильнымиПриложениями.Ссылка КАК Ссылка,
	|	ОбменСМобильнымиПриложениями.ПометкаУдаления КАК ПометкаУдаления,
	|	ОбменСМобильнымиПриложениями.МобильноеПриложение КАК МобильноеПриложение,
	|	ОбменСМобильнымиПриложениями.ГруппаПользователей КАК ГруппаПользователей,
	|	ОбменСМобильнымиПриложениями.Пользователь КАК Пользователь,
	|	ОбменСМобильнымиПриложениями.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА СостоянияОбменовДанными.РезультатВыполненияОбмена ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СостоянияОбменовДанными.РезультатВыполненияОбмена = ЗНАЧЕНИЕ(Перечисление.РезультатыВыполненияОбмена.Выполнено)
	|					ТОГДА 1
	|				ИНАЧЕ 2
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатусОбмена,
	|	СостоянияОбменовДанными.ДатаОкончания КАК ДатаОкончания,
	|	ОбменСМобильнымиПриложениями.ПрефиксДляДанныхМобильногоУстройства КАК Префикс
	|ИЗ
	|	ПланОбмена.ОбменСМобильнымиПриложениями КАК ОбменСМобильнымиПриложениями
	|		ЛЕВОЕ СОЕДИНЕНИЕ РезультатыВыполненияОбмена КАК СостоянияОбменовДанными
	|		ПО ОбменСМобильнымиПриложениями.Ссылка = СостоянияОбменовДанными.УзелИнформационнойБазы
	|ГДЕ
	|	НЕ ОбменСМобильнымиПриложениями.ЭтотУзел";
	
КонецФункции

#КонецОбласти
