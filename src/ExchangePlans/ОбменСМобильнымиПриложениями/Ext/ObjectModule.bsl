#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЭтотУзел И Не ЗначениеЗаполнено(Пользователь)
		И Не ЗначениеЗаполнено(ГруппаПользователей) Тогда
		ПроверяемыеРеквизиты.Добавить("Пользователь");
	КонецЕсли;
	МассивНепроверяемыхРеквизитов = Новый Массив;
	ДобавитьНепроверяемыеРеквизитыВМассив(МассивНепроверяемыхРеквизитов);
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если МобильноеПриложение.ВариантНастройки = "Заказы" Тогда
			
		Если Не ЗначениеЗаполнено(Наименование) Тогда
			Если ЭтотУзел Тогда
				Наименование = НСтр("ru = 'Центральный'");
			Иначе
				ТекстНаименование = НСтр("ru = '%Пользователь% (""1С:Заказы"")'");
				ТекстНаименование = СтрЗаменить(ТекстНаименование, "%Пользователь%", СокрЛП(Пользователь));
				Наименование = ТекстНаименование;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Код) Тогда
			Если ЭтотУзел Тогда
				Код = "001";
			Иначе
				НовыйКод = СокрЛП(Пользователь.УникальныйИдентификатор());
				ПроверитьНовыйКод(НовыйКод, ЭтотОбъект.Ссылка, Отказ);
				Если Отказ Тогда
					Сообщение = НСтр("ru = 'Для пользователя ""%Пользователь%"" уже существует настройка,
					|которая еще не была задействована для синхронизации данных с мобильным приложением ""Заказы"".
					|Используйте ее для настройки обмена данными.'");
					Сообщение = СтрЗаменить(Сообщение, "%Пользователь%", Пользователь);
					Поле = "Пользователь";
					ОбщегоНазначения.СообщитьПользователю(Сообщение, ЭтотОбъект, Поле,, Отказ);
					Возврат;
				Иначе
					Код = НовыйКод;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		Если ЗначениеЗаполнено(ГруппаПользователей)
			И ГруппаПользователей.Состав.Количество() = 0 Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В группе пользователей ""%1"" отсутствуют участники'"), ГруппаПользователей);
			Поле = "ГруппаПользователей";
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Код) Тогда
			Код = СокрЛП(Новый УникальныйИдентификатор);
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЭтотУзел Тогда
		
		ПрефиксИспользуется = УправлениеМобильнымиПриложениями.ПроверитьПрефиксДляДанныхМобильногоУстройства(
			Ссылка, ПрефиксДляДанныхМобильногоУстройства);
		
		Если ПрефиксИспользуется Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Префикс ""%1"" уже используется'"), ПрефиксДляДанныхМобильногоУстройства);
			Поле = "ПрефиксДляДанныхМобильногоУстройства";
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПланыОбмена.УдалитьРегистрациюИзменений(Ссылка);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Код = "";
	Наименование = "";
	ПрефиксДляДанныхМобильногоУстройства = "";
	Если МобильноеПриложение.ВариантНастройки = "Заказы" Тогда
		ИдентификаторПодписчикаДоставляемыхУведомлений = Неопределено;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьНепроверяемыеРеквизитыВМассив(МассивНепроверяемыхРеквизитов)
	
	МассивНепроверяемыхРеквизитов.Добавить("Код");
	
	Если Ссылка = ПланыОбмена.ОбменСМобильнымиПриложениями.ЭтотУзел() Тогда

		МассивНепроверяемыхРеквизитов.Добавить("ПрефиксДляДанныхМобильногоУстройства");
		МассивНепроверяемыхРеквизитов.Добавить("МобильноеПриложение");
		Если МобильноеПриложение.ВариантНастройки = "Заказы" Тогда
			МассивНепроверяемыхРеквизитов.Добавить("Пользователь");
		КонецЕсли;
	КонецЕсли;
	
	Если МобильноеПриложение.ВариантНастройки = "Заказы" Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Наименование");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНовыйКод(НовыйКод, СсылкаНаОбъект, Отказ)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ОбменСМобильнымиПриложениями.Ссылка
	|ИЗ
	|	ПланОбмена.ОбменСМобильнымиПриложениями КАК ОбменСМобильнымиПриложениями
	|ГДЕ
	|	НЕ ОбменСМобильнымиПриложениями.Ссылка = &СсылкаНаОбъект
	|	И ОбменСМобильнымиПриложениями.Код = &Код");
	
	Запрос.УстановитьПараметр("СсылкаНаОбъект", СсылкаНаОбъект);
	Запрос.УстановитьПараметр("Код", НовыйКод);
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли

