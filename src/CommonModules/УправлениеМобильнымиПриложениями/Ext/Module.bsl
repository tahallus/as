#Область ПрограммныйИнтерфейс

// Возвращает Истина, если в информационной базе используется
// обмен с мобильными приложениями.
//
Функция ИспользоватьОбменСМобильнымиПриложениями() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьОбменСМобильнымиПриложениями");
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область КодыПодключения

// Заполняет таблицу кодов подключения пользователей настройки.
//
// Параметры:
//   КодыПодключения - ДанныеФормыКоллекция - Таблица с колонками:
//     * Пользователь - СправочникСсылка.Пользователи - пользователь настройки обмена
//     * КодПодключения - Картинка - QR-код подключения пользователя
//     * ПисьмоОтправлено - Булево - признак успешной или не успешной отправки кода на email
//   УзелОбмена - ПланОбменаСсылка - См. УправлениеМобильнымиПриложениямиКлиентСервер.ИмяПланаОбмена
//   МассивПользователей - Массив из СправочникСсылка.Пользователи
//   ТабличныйДокумент - ТабличныйДокумент - для вывода кодов подключения на печать
//
Процедура ЗаполнитьКодыПодключения(
		КодыПодключения,
		УзелОбмена,
		МассивПользователей,
		ТабличныйДокумент = Неопределено) Экспорт
	
	КодыПодключения.Очистить();
	
	Макет = Обработки.УправлениеМобильнымиПриложениями.ПолучитьМакет("QRКодПодключения");
	Область = Макет.ПолучитьОбласть("Строка|Колонка");
	ОбластьГраница = Макет.ПолучитьОбласть("Граница|Колонка");
	Рисунок = Область.Рисунки.ШтрихКод;
	
	Эталон = Справочники.ПодключаемоеОборудование.ПолучитьМакет("МакетДляОпределенияКоэффициентовЕдиницИзмерения");
	КоличествоМиллиметровВПикселеВысота = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	КоличествоМиллиметровВПикселеШирина = Эталон.Рисунки.Квадрат100Пикселей.Ширина / 100;
	ШиринаШтрихкода = Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселеШирина);
	ВысотаШтрихкода = Окр(Рисунок.Высота / КоличествоМиллиметровВПикселеВысота);
	
	АдресПубликации = Константы.АдресПубликацииИнформационнойБазыВИнтернете.Получить();
	Если ТипЗнч(УзелОбмена) = Тип("Структура") Тогда
		Если УзелОбмена.Свойство("АдресПубликации") Тогда
			АдресПубликации = УзелОбмена.АдресПубликации;
		КонецЕсли;
	КонецЕсли;
		
	КодУзла = УзелОбмена.Код;
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		МодульРаботаВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервиса");
		СеансЗапущенБезРазделителей = МодульРаботаВМоделиСервиса.СеансЗапущенБезРазделителей();
		Если НЕ СеансЗапущенБезРазделителей Тогда
			ТекущаяОбласть = МодульРаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
			Попытка
				МодульПрограммныйИнтерфейсСервиса = ОбщегоНазначения.ОбщийМодуль("ПрограммныйИнтерфейсСервиса");
				СвойстваПриложения = МодульПрограммныйИнтерфейсСервиса.СвойстваПриложения(ТекущаяОбласть);
				АдресПубликации = СвойстваПриложения.АдресПриложения;
			Исключение
				АдресПубликации = "";
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	ЭтоГрупповаяНастройка = МассивПользователей.Количество() > 1;
	Для Каждого УчастникНастройки Из МассивПользователей Цикл
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
			УчастникНастройки.ИдентификаторПользователяИБ);
		
		Если ПользовательИБ = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПользовательНастройки = ПользовательИБ.Имя;
		ДвоичныеДанные = ПолучитьДвоичныеДанныеИзСтроки(ПользовательНастройки);
		ПользовательНастройки = Base64Строка(ДвоичныеДанные);

		СтрокаДляКодирования = АдресПубликации + ";"
			+ КодУзла + ";"
			+ ПользовательНастройки + ";";
			
		Область.Параметры.Пользователь = УчастникНастройки.Наименование;
		Область.Параметры.НастройкаОбмена = УзелОбмена.Наименование;
		ДанныеQRКода = ПолучитьШтрихкод(ШиринаШтрихкода, ВысотаШтрихкода, СтрокаДляКодирования);
		Рисунок.Картинка = ДанныеQRКода.Картинка;
		
		Область.Параметры.СтрокаПодключения = СтрокаДляКодирования;
		
		Если ТабличныйДокумент <> Неопределено Тогда
			Если Не ТабличныйДокумент.ПроверитьВывод(Область) Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ТабличныйДокумент.Вывести(Область);
			Если ЭтоГрупповаяНастройка Тогда
				ТабличныйДокумент.Вывести(ОбластьГраница);
			КонецЕсли;
		КонецЕсли;
		
		Строка = КодыПодключения.Добавить();
		Строка.Пользователь = УчастникНастройки;
		Строка.КодПодключения = ДанныеQRКода.Картинка;
	КонецЦикла;
	
КонецПроцедуры

// Отправляет пользователям настройки обмена электронные письма с кодами подключения
// к приложению.
// Возвращает Истина в случае успешного завершения отправки.
// Возвращает Ложь, если нет доступной учетной записи для отправки электронных писем.
//
// Параметры:
//   КодыПодключения - ДанныеФормыКоллекция - Таблица с колонками:
//     * Пользователь - СправочникСсылка.Пользователи - пользователь настройки обмена
//     * КодПодключения - Картинка - QR-код подключения пользователя
//     * ПисьмоОтправлено - Булево - признак успешной или не успешной отправки кода на email
//   УзелОбмена - ПланОбменаСсылка - См. УправлениеМобильнымиПриложениямиКлиентСервер.ИмяПланаОбмена
//
Функция ОтправитьКодыПодключения(КодыПодключения, УзелОбмена) Экспорт
	
	// Выбираем первую доступную запись электронной почты
	ДоступныеУчетныеЗаписи = РаботаСПочтовымиСообщениями.ДоступныеУчетныеЗаписи(Истина);
	Если ДоступныеУчетныеЗаписи.Количество() = 0 Тогда
		ТекстСообщения =
			НСтр("ru = 'Не обнаружены доступные учетные записи электронной почты, обратитесь к администратору системы.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;
	
	УчетнаяЗапись = ДоступныеУчетныеЗаписи[0].Ссылка;
	
	ТемаПисьма = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Подключение к приложению %1'"), УзелОбмена.МобильноеПриложение);
		
	ТелоПисьма =
		НСтр("ru = 'Откройте приложение и отсканируйте QR-код.
		|
		|Пользователь: %1
		|Настройка: %2
		|
		|Данное письмо сформировано автоматически и не требует ответа.'");
		
	ВидКонтактнойИнформации = УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("EmailПользователя");
	
	Для Каждого Строка Из КодыПодключения Цикл
		АдресЭлектроннойПочты = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(
			Строка.Пользователь,ВидКонтактнойИнформации);
			
		Если ПустаяСтрока(АдресЭлектроннойПочты) Тогда
			Продолжить;
		КонецЕсли;
	
		ПараметрыПисьма = Новый Структура;
		
		ПараметрыПисьма.Вставить("Тема", ТемаПисьма);
		ПараметрыПисьма.Вставить("Кому", АдресЭлектроннойПочты);
		Тело = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТелоПисьма,
			Строка.Пользователь, УзелОбмена.Наименование);
		ПараметрыПисьма.Вставить("Тело", Тело);
		
		АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(Строка.КодПодключения.ПолучитьДвоичныеДанные());

		Вложения = Новый Массив;
		ОписаниеВложения = Новый Структура;
		ОписаниеВложения.Вставить("Представление", "QR-код.jpg");
		ОписаниеВложения.Вставить("АдресВоВременномХранилище", АдресВоВременномХранилище);
		Вложения.Добавить(ОписаниеВложения);
		ПараметрыПисьма.Вставить("Вложения", Вложения);
		
		РаботаСПочтовымиСообщениями.ОтправитьПочтовоеСообщение(УчетнаяЗапись, ПараметрыПисьма);
		Строка.ПисьмоОтправлено = Истина;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область Конструкторы

// Конструктор структуры настроек приложения.
//
Функция НастройкаПриложения() Экспорт
	
	НастройкаПриложения = Новый Структура;
	НастройкаПриложения.Вставить("ЭтоОтбор", Ложь);
	НастройкаПриложения.Вставить("ОбязательноеЗаполнение", Ложь);
	НастройкаПриложения.Вставить("ИмяФункциональнойОпции", "");
	
	Возврат НастройкаПриложения;
	
КонецФункции

// Конструктор структуры параметров предопределенного приложения.
//
Функция ПараметрыПриложения() Экспорт
	
	ПараметрыПриложения = Новый Структура();
	ПараметрыПриложения.Вставить("Наименование");
	ПараметрыПриложения.Вставить("ВариантНастройки");
	ПараметрыПриложения.Вставить("ВерсияПриложения");
	ПараметрыПриложения.Вставить("КраткаяИнформация");
	ПараметрыПриложения.Вставить("СайтПриложения");
	ПараметрыПриложения.Вставить("Разработчик");
	ПараметрыПриложения.Вставить("СайтРазработчика");
	ПараметрыПриложения.Вставить("ИдентификаторПриложенияGoogle", "");
	ПараметрыПриложения.Вставить("ИдентификаторПриложенияApple", "");
	ПараметрыПриложения.Вставить("ИдентификаторПриложенияWindows", "");
	
	Возврат ПараметрыПриложения;
	
КонецФункции

#КонецОбласти

#Область МобильноеПриложение

// Возвращает версию формата обмена мобильного приложения
//
// Параметры:
//  МобильноеПриложение - СправочникСсылка.МобильныеПриложения - мобильное приложение узла обмена
//
// Возвращаемое значение
//   ВерсияФорматаОбмена - Строка - текущая версия формата обмена мобильного приложения
//
Функция ВерсияФорматаОбмена(МобильноеПриложение) Экспорт
	
	ВерсияФорматаОбмена = "";
	УправлениеМобильнымиПриложениямиПереопределяемый.ПриОпределенииВерсииФорматаОбмена(
		ВерсияФорматаОбмена, МобильноеПриложение);
	
	Возврат ВерсияФорматаОбмена;
КонецФункции

// Возвращает описание приложения.
//
// Параметры:
//  МобильноеПриложение - СправочникСсылка.МобильныеПриложения - мобильное приложение
//
// Возвращаемое значение:
//   Строка - URL-адрес приложения, текст HTML документа
//
Функция ПолучитьОписаниеПриложения(МобильноеПриложение) Экспорт
	
	ОписаниеПриложения = "";
	СтандартнаяОбработка = Истина;
	
	УправлениеМобильнымиПриложениямиПереопределяемый.ПриОпределенииОписанияПриложения(
		МобильноеПриложение, ОписаниеПриложения, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка Тогда
		ОписаниеПриложения = МобильноеПриложение.СайтПриложения;
	КонецЕсли;
	
	Возврат ОписаниеПриложения;
	
КонецФункции

// Возвращает картинку с QR-кодом для скачивания приложения.
//
// Параметры:
//   МобильноеПриложение - СправочникСсылка.МобильныеПриложения - мобильное приложение
//   УникальныйИдентификатор - УникальныйИдентификатор - идентификатор формы, в которой требуется отобразить QR-код
//
// Возвращаемое значение:
//   Строка - адрес QR-кода приложения во временном хранилище
//
Функция ПолучитьСсылкуНаСкачивание(МобильноеПриложение, УникальныйИдентификатор) Экспорт
	
	АдресСсылкиGooglePlay = "";
	УправлениеМобильнымиПриложениямиПереопределяемый.ПриПолученииСсылкиНаСкачивание(
		МобильноеПриложение, УникальныйИдентификатор, АдресСсылкиGooglePlay);
	
	Возврат АдресСсылкиGooglePlay;
	
КонецФункции

// Возвращает адрес временного хранилища иконки мобильного приложения.
//
// Параметры:
//   МобильноеПриложение - СправочникСсылка.МобильныеПриложения - мобильное приложение
//   УникальныйИдентификатор - УникальныйИдентификатор - идентификатор формы, в которой требуется отобразить превью
//
// Возвращаемое значение:
//   Строка - адрес иконки во временном хранилище
//
Функция ПревьюПриложения(МобильноеПриложение, УникальныйИдентификатор) Экспорт
	
	ДвоичныеДанныеИконки = Неопределено;
	УправлениеМобильнымиПриложениямиПереопределяемый.ПриПолученииИконкиПриложения(
		МобильноеПриложение, ДвоичныеДанныеИконки);
	
	Превью = "";
	Если ДвоичныеДанныеИконки <> Неопределено Тогда
		Превью = ПоместитьВоВременноеХранилище(ДвоичныеДанныеИконки, УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат Превью;

КонецФункции

// Возвращает результат проверки варианта настройки мобильного приложения.
//
// Параметры:
//  МобильноеПриложение - ПланОбменаСсылка - См. УправлениеМобильнымиПриложениямиКлиентСервер.ИмяПланаОбмена
//  ВариантНастройки - Строка - проверяемый вариант настройки
//
// Возвращаемое значение:
//  Булево - истина, когда вариант настройки уже используется.
//
Функция ПроверитьВариантНастройкиМобильногоПриложения(МобильноеПриложение, ВариантНастройки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	МобильныеПриложения.ВариантНастройки КАК ВариантНастройки
	|ИЗ
	|	Справочник.МобильныеПриложения КАК МобильныеПриложения
	|ГДЕ
	|	НЕ МобильныеПриложения.Ссылка = &Ссылка
	|	И МобильныеПриложения.ВариантНастройки = &ВариантНастройки");
	
	Запрос.УстановитьПараметр("Ссылка", МобильноеПриложение);
	Запрос.УстановитьПараметр("ВариантНастройки", ВариантНастройки);
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
КонецФункции

// Возвращает список поддерживаемых мобильных приложений
//
// Возвращаемое значение:
//   СписокПриложений - Массив - содержит массив мобильных приложений
//
Функция СписокМобильныхПриложений() Экспорт
	
	СписокПриложений = Новый Массив;
	СтандартнаяОбработка = Истина;
	
	УправлениеМобильнымиПриложениямиПереопределяемый.ПриОпределенииСпискаМобильныхПриложений(
		СписокПриложений, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка Тогда
		СписокПриложений.Очистить();
		ВыборкаМобильныеПриложения = Справочники.МобильныеПриложения.Выбрать();
		Пока ВыборкаМобильныеПриложения.Следующий() Цикл
			Если ВыборкаМобильныеПриложения.ПометкаУдаления Тогда
				Продолжить;
			КонецЕсли;
			СписокПриложений.Добавить(ВыборкаМобильныеПриложения.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
	Возврат СписокПриложений;
КонецФункции

#КонецОбласти

#Область ПланОбмена

#Область ДанныеУзлаОбмена

// Возвращает новый префикс для данных мобильного устройства.
//
// Возвращаемое значение:
//  НовыйПрефиксДляДанныхМобильногоУстройства - Строка - новый префикс
//
Функция НовыйПрефиксДляДанныхМобильногоУстройства() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НовыйПрефиксДляДанныхМобильногоУстройства = "АА";
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ОбменСМобильнымиПриложениями.ПрефиксДляДанныхМобильногоУстройства КАК СТРОКА(2)) КАК ПрефиксДляДанныхМобильногоУстройства
	|ИЗ
	|	ПланОбмена." + ИмяПланаОбмена() + " КАК ОбменСМобильнымиПриложениями
	|ГДЕ
	|	НЕ ОбменСМобильнымиПриложениями.ПрефиксДляДанныхМобильногоУстройства = """"
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПрефиксДляДанныхМобильногоУстройства УБЫВ");
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат НовыйПрефиксДляДанныхМобильногоУстройства;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Выборка.Следующий();
	
	ТекущийПрефикс = Выборка.ПрефиксДляДанныхМобильногоУстройства;
	
	ПервыйСимвол = Макс(1040, КодСимвола(ТекущийПрефикс, 1));
	ВторойСимвол = Макс(1040, КодСимвола(ТекущийПрефикс, 2));
	
	Если ВторойСимвол < 1071 Тогда
		ВторойСимвол = ВторойСимвол + 1;
	Иначе
		ВторойСимвол = 1040;
		ПервыйСимвол = ПервыйСимвол + 1;
		Если ПервыйСимвол > 1071 Тогда
			// Нумерация закончилась.
			ПервыйСимвол = 1071;
			ВторойСимвол = 1071;
		КонецЕсли;
	КонецЕсли;
	
	НовыйПрефиксДляДанныхМобильногоУстройства = Символ(ПервыйСимвол)+Символ(ВторойСимвол);
	
	Возврат НовыйПрефиксДляДанныхМобильногоУстройства;
КонецФункции

// Возвращает результат проверки префикса для данных мобильного устройства.
//
// Параметры:
//  УзелОбмена - ПланОбменаСсылка - См. УправлениеМобильнымиПриложениямиКлиентСервер.ИмяПланаОбмена
//  Префикс - Строка - проверяемый префикс для данных мобильного устройства.
//
// Возвращаемое значение:
//  Булево - истина, когда префикс уже используется на другом узле.
//
Функция ПроверитьПрефиксДляДанныхМобильногоУстройства(УзелОбмена, Префикс) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ОбменСМобильнымиПриложениями.ПрефиксДляДанныхМобильногоУстройства КАК СТРОКА(2)) КАК ПрефиксДляДанныхМобильногоУстройства
	|ИЗ
	|	ПланОбмена." + ИмяПланаОбмена() + " КАК ОбменСМобильнымиПриложениями
	|ГДЕ
	|	НЕ ОбменСМобильнымиПриложениями.Ссылка = &Ссылка
	|	И (ВЫРАЗИТЬ(ОбменСМобильнымиПриложениями.ПрефиксДляДанныхМобильногоУстройства КАК СТРОКА(2))) = &Префикс");
	
	Запрос.УстановитьПараметр("Ссылка", УзелОбмена);
	Запрос.УстановитьПараметр("Префикс", Префикс);
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
КонецФункции

// Завершает настройку узла обмена.
//
// Параметры:
//   УзелОбмена - ПланОбменаСсылка - См. УправлениеМобильнымиПриложениямиКлиентСервер.ИмяПланаОбмена
//
Процедура ПослеЗаписиНастройки(УзелОбмена) Экспорт
	
	Если УзелОбмена.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		//При необходимости обновляем правила регистрации для плана обмена
		ИмяПланаОбмена = УправлениеМобильнымиПриложениямиКлиентСервер.ИмяПланаОбмена();
		ИмяПараметра = "СтандартныеПодсистемы.ОбменДанными.ПравилаРегистрации."
			+ ИмяПланаОбмена;
		ПравилаРегистрацииОбновлены = СтандартныеПодсистемыСервер.ПараметрРаботыПрограммы(ИмяПараметра);
		Если ПравилаРегистрацииОбновлены = Неопределено Тогда
			ОбменДаннымиСервер.ПроверитьИспользованиеОбменаДанными(Истина);
			ОбменДаннымиСервер.ВыполнитьОбновлениеПравилДляОбменаДанными();
		КонецЕсли;
	КонецЕсли;

	РегистрыСведений.ОбщиеНастройкиУзловИнформационныхБаз.УстановитьПризнакНастройкаЗавершена(УзелОбмена);
	
	СтандартнаяОбработка = Истина;
	УправлениеМобильнымиПриложениямиПереопределяемый.ПослеЗаписиНастройки(УзелОбмена, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиОбмена = УзелОбмена.НастройкиОбмена.Получить();
	Если НастройкиОбмена = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементОтбора из НастройкиОбмена.Отбор.Элементы Цикл
		ПравоеЗначение = ЭлементОтбора.ПравоеЗначение;
		Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
			Если ПравоеЗначение.Количество() > 0 Тогда
				ТипЗначения = ТипЗнч(ПравоеЗначение[0].Значение);
			КонецЕсли;
		Иначе
			ТипЗначения = ТипЗнч(ПравоеЗначение);
		КонецЕсли;
		
		ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗначения);
		Если ОбъектМетаданных = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
//		Если Метаданные.Справочники.Содержит(ОбъектМетаданных) Тогда
		Если УзелОбмена.Метаданные().Состав.Содержит(ОбъектМетаданных) Тогда
			Если ЗначениеЗаполнено(ПравоеЗначение) И ЭлементОтбора.Использование Тогда
				Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
					Для Каждого ЭлементСписка из ПравоеЗначение Цикл
						ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, ЭлементСписка.Значение);
					КонецЦикла;
				Иначе
					ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, ПравоеЗначение);
				КонецЕсли;
			Иначе
				ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, ОбъектМетаданных);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ФормаУзлаОбмена

// Заполняет настройки компоновщика по схеме компоновки настроек мобильного приложения.
//
// Параметры:
//  КомпоновщикНастроек - КомпоновщикНастроек - компоновщик настроек данных, в котором необходимо заполнить настройки
//  МобильноеПриложение - СправочникСсылка.МобильныеПриложения - мобильное приложение узла обмена
//  НастройкиКомпоновки - НастройкиКомпоновкиДанных - настройки для заполнения
//
Процедура ЗаполнитьНастройкиКомпоновщика(КомпоновщикНастроек,
		МобильноеПриложение, НастройкиКомпоновки = Неопределено) Экспорт
	
	СхемаКомпоновки = Неопределено;
	УправлениеМобильнымиПриложениямиПереопределяемый.ПриПолученииСхемыКомпоновкиНастроек(
		МобильноеПриложение, СхемаКомпоновки);
	
	Если СхемаКомпоновки = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если НастройкиКомпоновки = Неопределено Тогда
		НастройкиКомпоновки = СхемаКомпоновки.НастройкиПоУмолчанию;
	КонецЕсли;
	URLСхемы = ПоместитьВоВременноеХранилище(СхемаКомпоновки, Новый УникальныйИдентификатор());
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы));
	КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиКомпоновки);
	КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
КонецПроцедуры

// Добавляет на форму узла обмена реквизиты и элементы, соответствующие настройкам мобильного приложения.
// Заполняет отборы из компоновщика настроек в реквизиты формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма узла обмена
//  ГруппаФормы - ГруппаФормы - группа, в которой будут располагаться настройки приложения
//  КомпоновщикНастроек - КомпоновщикНастроек - компоновщик настроек данных для заполнения
//  МобильноеПриложение - СправочникСсылка.МобильныеПриложения - мобильное приложение узла обмена
//
Процедура ОтрисоватьФормуУзла(Форма, ГруппаФормы, КомпоновщикНастроек, МобильноеПриложение) Экспорт
	
	НастройкиПриложения = НастройкиПриложения(МобильноеПриложение);
	ДобавитьНастройкиПриложенияНаФорму(Форма, ГруппаФормы, КомпоновщикНастроек, НастройкиПриложения);
	УстановитьОтборыКомпоновщикаВРеквизитыФормы(Форма, КомпоновщикНастроек, НастройкиПриложения);
	ОпределитьСписокВыбораНаименования(Форма, МобильноеПриложение, НастройкиПриложения);
	
КонецПроцедуры

// Обработчик изменения полей формы узла обмена, которые содержат обязательные настройки приложения.
//
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма узла обмена
//  ИмяНастройки - Строка - имя элемента формы, который был изменен и является обязательным
//  МобильноеПриложение - СправочникСсылка.МобильныеПриложения - мобильное приложение узла обмена
//  КомпоновщикНастроек - КомпоновщикНастроек - компоновщик настроек данных с настройками приложения
//
Процедура ОбязательнаяНастройкаПриИзменении(Форма, ИмяНастройки, МобильноеПриложение, КомпоновщикНастроек) Экспорт
	
	НастройкиПриложения = НастройкиПриложения(МобильноеПриложение);
	ОпределитьСписокВыбораНаименования(Форма, МобильноеПриложение, НастройкиПриложения);
	УправлениеМобильнымиПриложениямиПереопределяемый.ПриИзмененииОбязательнойНастройки(ИмяНастройки, КомпоновщикНастроек);
	
КонецПроцедуры

// Заполняет отбор компоновщика настроек реквизитами формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма узла обмена
//  КомпоновщикНастроек - КомпоновщикНастроек - компоновщик настроек данных для заполнения
//  МобильноеПриложение - СправочникСсылка.МобильныеПриложения - мобильное приложение узла обмена
//
Процедура УстановитьРеквизитыФормыВОтборыКомпоновщика(Форма, КомпоновщикНастроек, МобильноеПриложение) Экспорт
	
	НастройкиПриложения = НастройкиПриложения(МобильноеПриложение);
	Для Каждого ТекущаяНастройка Из НастройкиПриложения Цикл
		ИмяНастройки = ТекущаяНастройка.Ключ;
		ЗначениеНастройки = ТекущаяНастройка.Значение;
		Если Не ЗначениеНастройки.ЭтоОтбор Тогда
			Если НастройкаИспользуется(ЗначениеНастройки) Тогда
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(КомпоновщикНастроек.Настройки.Отбор,
					ИмяНастройки, Форма[ИмяНастройки], ВидСравненияКомпоновкиДанных.Равно,,
					ЗначениеЗаполнено(Форма[ИмяНастройки]));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область РегистрацияИзменений

// Процедура-обработчик события "ПередЗаписью" документов для механизма регистрации объектов на узлах.
//
// Параметры:
//  Источник       - ДокументОбъект - источник события.
//  Отказ          - Булево - флаг отказа от выполнения обработчика.
//  РежимЗаписи     - РежимЗаписиДокумента - режим записи документа.
//  РежимПроведения - РежимПроведенияДокумента - режим проведения документа.
// 
Процедура ОбменСМобильнымиПриложениямиПередЗаписьюДокумента(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	ОбменДаннымиСобытия.МеханизмРегистрацииОбъектовПередЗаписьюДокумента(
		ИмяПланаОбмена(), Источник, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область КодыПодключения

Функция ПолучитьШтрихкод(ШиринаШтрихкода, ВысотаШтрихкода, Штрихкод)
	
	ПараметрыШтрихкода = Новый Структура;
	ПараметрыШтрихкода.Вставить("Ширина"            , ШиринаШтрихкода);
	ПараметрыШтрихкода.Вставить("Высота"            , ВысотаШтрихкода);
	ПараметрыШтрихкода.Вставить("ТипКода"           , 16);
	ПараметрыШтрихкода.Вставить("ОтображатьТекст"   , Истина);
	ПараметрыШтрихкода.Вставить("РазмерШрифта"      , 12);
	ПараметрыШтрихкода.Вставить("УголПоворота"      , 0);
	ПараметрыШтрихкода.Вставить("Штрихкод"          , Штрихкод);
	ПараметрыШтрихкода.Вставить("ПрозрачныйФон"     , Ложь);
	ПараметрыШтрихкода.Вставить("Масштабировать"    , Ложь);
	Возврат ГенерацияШтрихкодаВызовСервера.ИзображениеШтрихкода(ПараметрыШтрихкода);
	
КонецФункции

#КонецОбласти

#Область ПланОбмена

Функция ИмяПланаОбмена()
	
	Возврат УправлениеМобильнымиПриложениямиКлиентСервер.ИмяПланаОбмена();
	
КонецФункции

#Область ФормаУзлаОбмена

Процедура ДобавитьНастройкиПриложенияНаФорму(Форма, ГруппаФормы, КомпоновщикНастроек, НастройкиПриложения)
	
	НастройкиКомпоновки = КомпоновщикНастроек.Настройки;
	
	МассивУдаляемыхРеквизитов = Новый Массив;
	Для Каждого Элемент Из ГруппаФормы.ПодчиненныеЭлементы Цикл
		МассивУдаляемыхРеквизитов.Добавить(Элемент.ПутьКДанным);
	КонецЦикла;
	Форма.ИзменитьРеквизиты(, МассивУдаляемыхРеквизитов);
	
	Для Каждого УдаленныйРеквизит Из МассивУдаляемыхРеквизитов Цикл
		ЭлементКУдалению = Форма.Элементы.Найти(УдаленныйРеквизит);
		Если ЭлементКУдалению <> Неопределено Тогда
			Форма.Элементы.Удалить(ЭлементКУдалению);
		КонецЕсли;
	КонецЦикла;
	
	МассивДобавляемыхРеквизитов = Новый Массив;
	МассивОбязательныхНастроек = Новый Массив;
	Для Каждого ТекущаяНастройка из НастройкиПриложения Цикл
		ИмяНастройки = ТекущаяНастройка.Ключ;
		ЗначениеНастройки = ТекущаяНастройка.Значение;
		Если ЗначениеНастройки.ЭтоОтбор Тогда
			Продолжить;
		ИначеЕсли Не НастройкаИспользуется(ЗначениеНастройки) Тогда
			Продолжить;
		КонецЕсли;
		ПолеКомпоновки = НастройкиКомпоновки.ДоступныеПоляОтбора.НайтиПоле(
			Новый ПолеКомпоновкиДанных(ИмяНастройки));
		Если ПолеКомпоновки <> Неопределено Тогда
			НовыйРеквизит = Новый РеквизитФормы(
			ИмяНастройки, ПолеКомпоновки.Тип,, ПолеКомпоновки.Заголовок);
			Если ЗначениеНастройки.ОбязательноеЗаполнение Тогда
				МассивДобавляемыхРеквизитов.Вставить(0, НовыйРеквизит);
				МассивОбязательныхНастроек.Добавить(ИмяНастройки);
			Иначе
				МассивДобавляемыхРеквизитов.Добавить(НовыйРеквизит);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Форма.ИзменитьРеквизиты(МассивДобавляемыхРеквизитов);
	
	Для Каждого ДобавленныйРеквизит из МассивДобавляемыхРеквизитов Цикл
		НовыйЭлемент = ДобавленныйРеквизит.Имя;
		Если Форма.Элементы.Найти(НовыйЭлемент) = Неопределено Тогда
			ЭлементФормы = Форма.Элементы.Добавить(НовыйЭлемент, Тип("ПолеФормы"), ГруппаФормы);
			ЭлементФормы.ПутьКДанным = НовыйЭлемент;
			Если ТипЗнч(Форма[НовыйЭлемент]) = Тип("Булево") Тогда
				ЭлементФормы.Вид = ВидПоляФормы.ПолеФлажка;
				ЭлементФормы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
			Иначе
				ЭлементФормы.Вид = ВидПоляФормы.ПолеВвода;
				Если МассивОбязательныхНастроек.Найти(НовыйЭлемент) <> Неопределено Тогда
					ЭлементФормы.АвтоОтметкаНезаполненного = Истина;
					ЭлементФормы.УстановитьДействие("ПриИзменении", "Подключаемый_ОбязательнаяНастройкаПриИзменении");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ЗначениеЭлементаОтбораИзПользовательскихНастроек(КомпоновщикНастроек, ИмяЭлемента)
	
	Если ТипЗнч(КомпоновщикНастроек) <> Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЭлементыОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(КомпоновщикНастроек.Настройки.Отбор,
		ИмяЭлемента);
	Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		ИдентификаторПользовательскойНастройки = ЭлементОтбора.ИдентификаторПользовательскойНастройки;
		КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить(ИмяЭлемента,
			ИдентификаторПользовательскойНастройки);
		ЭлементПользовательскихНастроек = 
			КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ЭлементОтбора.ИдентификаторПользовательскойНастройки);
		Если ЭлементПользовательскихНастроек <> Неопределено 
			И ЭлементПользовательскихНастроек.Использование Тогда
				Возврат ЭлементПользовательскихНастроек.ПравоеЗначение;
		Иначе
			Если ЭлементПользовательскихНастроек = Неопределено
				И ЭлементОтбора.Использование Тогда
					Возврат ЭлементОтбора.ПравоеЗначение;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции

Функция НастройкиПриложения(МобильноеПриложение)
	
	НастройкиПриложения = Новый Соответствие;
	УправлениеМобильнымиПриложениямиПереопределяемый.ПриОпределенииНастроекПриложения(
		МобильноеПриложение, НастройкиПриложения);
		
	Возврат НастройкиПриложения;
	
КонецФункции

Процедура ОпределитьСписокВыбораНаименования(Форма, МобильноеПриложение, НастройкиПриложения)

	Наименование = Форма.Элементы.Наименование;
	Наименование.СписокВыбора.Очистить();

	Если Не ЗначениеЗаполнено(МобильноеПриложение) Тогда
		Наименование.КнопкаВыпадающегоСписка = Ложь;
		Возврат;
	КонецЕсли;
	
	УправлениеМобильнымиПриложениямиПереопределяемый.ПриОпределенииСпискаВыбораНаименования(
		Форма, Наименование, МобильноеПриложение, НастройкиПриложения);
		
	Наименование.КнопкаВыпадающегоСписка = Наименование.СписокВыбора.Количество() > 0;

КонецПроцедуры

Процедура УстановитьОтборыКомпоновщикаВРеквизитыФормы(Форма, КомпоновщикНастроек, НастройкиПриложения)
	
	Для каждого ТекущаяНастройка Из НастройкиПриложения Цикл
		ИмяРеквизита = ТекущаяНастройка.Ключ;
		ЗначениеНастройки = ТекущаяНастройка.Значение;
		Если НЕ НастройкаИспользуется(ЗначениеНастройки) Тогда
			УдалитьЭлементОтбораИзВсехНастроекОтчета(КомпоновщикНастроек, ИмяРеквизита);
			Продолжить;
		КонецЕсли;
		Если НЕ ЗначениеНастройки.ЭтоОтбор Тогда
			Форма[ИмяРеквизита] = ЗначениеЭлементаОтбораИзПользовательскихНастроек(КомпоновщикНастроек, ИмяРеквизита);
			УдалитьЭлементОтбораИзВсехНастроекОтчета(КомпоновщикНастроек, ИмяРеквизита);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция НастройкаИспользуется(ЗначениеНастройки)
	
	ИспользованиеНастройки = Истина;
	Если НЕ ПустаяСтрока(ЗначениеНастройки.ИмяФункциональнойОпции) Тогда
		Если НЕ ПолучитьФункциональнуюОпцию(ЗначениеНастройки.ИмяФункциональнойОпции) Тогда
			ИспользованиеНастройки = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИспользованиеНастройки;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область МетодыИВыраженияСистемыКомпоновкиДанных

// Удаляет отбор из настроек и пользовательских настроек отчета.
//
// Параметры:
//  КомпоновщикНастроек - КомпоновщикНастроек - компоновщик настроек из которых будет удален отбор.
//  ИмяЭлемента - Строка - имя элемента, который будет удален.
//
Процедура УдалитьЭлементОтбораИзВсехНастроекОтчета(КомпоновщикНастроек, ИмяЭлемента) Экспорт
	
	Если ТипЗнч(КомпоновщикНастроек) <> Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Возврат
	КонецЕсли;
	
	ЭлементыОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(КомпоновщикНастроек.Настройки.Отбор, ИмяЭлемента);
	Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		ИдентификаторПользовательскойНастройки = ЭлементОтбора.ИдентификаторПользовательскойНастройки;
		КомпоновщикНастроек.Настройки.Отбор.Элементы.Удалить(ЭлементОтбора);
		
		ЭлементПользовательскихНастроек = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ЭлементОтбора.ИдентификаторПользовательскойНастройки);
		Если ЭлементПользовательскихНастроек <> Неопределено Тогда
			КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Удалить(ИдентификаторПользовательскойНастройки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти





