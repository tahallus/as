#Область ПрограммныйИнтерфейс

// Отправляет событие с дополнительными данными в фоне.
//
Процедура ОтправитьСобытиеСДопДаннымиВФоне(ИмяСобытия, ДопДанные) Экспорт
	
	Параметры = Новый Массив();
	Параметры.Добавить(ИмяСобытия);
	Параметры.Добавить(ДопДанные);
	
	ФоновыеЗадания.Выполнить(
		"СборСтатистикиУНФ.ОтправитьСобытиеСДопДанными",
		Параметры,
		Новый УникальныйИдентификатор,
		"ОтправкаСобытияСДопДанными"
	);
	
КонецПроцедуры // ОтправитьСобытиеСДопДаннымиВФоне()

// Отправляет событие с дополнительными данными.
//
Процедура ОтправитьСобытиеСДопДанными(ИмяСобытия, ДопДанные, СообщениеОбОшибке = Неопределено) Экспорт
	
	Попытка
		HTTPСоединение = Новый HTTPСоединение(
		"unf-stat1c-gpt-msk.1c.ru",
		80,
		,
		,
		,
		5
		);
		
		ПутьНаСервере = 
		"/unf-stat1c/hs/analytics/sendstats/"
		+ ИдентификаторПриложения() + "/"
		+ Константы.ИдентификаторИнформационнойБазы.Получить() + "/"
		+ ИмяСобытия;
		
		HTTPЗапрос = Новый HTTPЗапрос(ПутьНаСервере);
		HTTPЗапрос.УстановитьТелоИзСтроки(ДопДанные);
		Результат = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
		КодСостояния = Результат.КодСостояния;
		Если Результат.КодСостояния <> 200 Тогда
			СообщениеОбОшибке = Результат.ПолучитьТелоКакСтроку();
		КонецЕсли;
	Исключение
		Инфо = ИнформацияОбОшибке();
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(Инфо);
	КонецПопытки;
	
КонецПроцедуры // ОтправитьСобытиеСДопДанными()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИдентификаторПриложения()
	
	Возврат "9a94779e-fe04-11e8-429b-0050569f4ccd";
	
КонецФункции // ИдентификаторПриложения()

#КонецОбласти
