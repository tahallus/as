////////////////////////////////////////////////////////////////////////////////
//
// Подсистема "Развитие бизнеса"
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает выборку показателей по переданному виду отчета.
//
// Параметры:
//  ВидОтчета - Перечисление.ВидыФинансовыхОтчетов
//
Функция ВыборкаДереваПоказателей(ВидОтчета) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДеревоПоказателей();
	Запрос.УстановитьПараметр("ВидОтчета", ВидОтчета);
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Возвращает выборку сумм документа бюджет для подстановки в дерево показателей. 
// Данные могут быть получены как по ссылке на документ, так и из временных таблиц бюджета
//
// Параметры:
//  ВидОтчета - Перечисление.ВидыФинансовыхОтчетов
//  ДокументБюджет - ДокументОбъект.Бюджет - документ, данные которого необходимо получить
//  БюджетМодифицирован - Булево - Если Истина, значит документ еще не записан и данные будут получены из временных таблиц
//
Функция ВыборкаСуммПоказателейБюджета(ВидОтчета, ДокументБюджет, БюджетМодифицирован) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаБюджетаПоВидуОтчета(ВидОтчета, НЕ БюджетМодифицирован);
	
	Если БюджетМодифицирован Тогда
		
		Если ВидОтчета = Перечисления.ВидыФинансовыхОтчетов.ДоходыРасходы Тогда
			Запрос.УстановитьПараметр("Доходы", 		  ДокументБюджет.Доходы.Выгрузить());
			Запрос.УстановитьПараметр("Расходы", 		  ДокументБюджет.Расходы.Выгрузить());
			Запрос.УстановитьПараметр("ПрямыеЗатраты", 	  ДокументБюджет.ПрямыеЗатраты.Выгрузить());
			Запрос.УстановитьПараметр("КосвенныеЗатраты", ДокументБюджет.КосвенныеЗатраты.Выгрузить());
		ИначеЕсли ВидОтчета = Перечисления.ВидыФинансовыхОтчетов.ДенежныйПоток Тогда
			Запрос.УстановитьПараметр("Поступления", 	  ДокументБюджет.Поступления.Выгрузить());
			Запрос.УстановитьПараметр("Выбытия", 		  ДокументБюджет.Выбытия.Выгрузить());
		Иначе // Баланс
			Запрос.УстановитьПараметр("Остатки", 		  ДокументБюджет.Остатки.Выгрузить());
			Запрос.УстановитьПараметр("ПрямыеЗатраты", 	  ДокументБюджет.ПрямыеЗатраты.Выгрузить());
			Запрос.УстановитьПараметр("КосвенныеЗатраты", ДокументБюджет.КосвенныеЗатраты.Выгрузить());
			Запрос.УстановитьПараметр("Поступления", 	  ДокументБюджет.Поступления.Выгрузить());
			Запрос.УстановитьПараметр("Выбытия", 		  ДокументБюджет.Выбытия.Выгрузить());
			Запрос.УстановитьПараметр("Доходы", 		  ДокументБюджет.Доходы.Выгрузить());
			Запрос.УстановитьПараметр("Расходы", 		  ДокументБюджет.Расходы.Выгрузить());
			Запрос.УстановитьПараметр("Операции", 		  ДокументБюджет.Операции.Выгрузить());
			Запрос.УстановитьПараметр("Доходы", 		  ДокументБюджет.Доходы.Выгрузить());
			Запрос.УстановитьПараметр("Доходы", 		  ДокументБюджет.Доходы.Выгрузить());
			Запрос.УстановитьПараметр("Доходы", 		  ДокументБюджет.Доходы.Выгрузить());
			Запрос.УстановитьПараметр("Доходы", 		  ДокументБюджет.Доходы.Выгрузить());
			Запрос.УстановитьПараметр("Доходы", 		  ДокументБюджет.Доходы.Выгрузить());
			Запрос.УстановитьПараметр("ДатаНачала", 	  ДокументБюджет.ДатаНачала);
		КонецЕсли;
		
	Иначе
		Запрос.УстановитьПараметр("Ссылка", ДокументБюджет.Ссылка);
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Возвращает текст запроса сумм по показателям для отчета АнализБизнеса
//
// Параметры:
//  ВидОтчета - Перечисление.ВидыФинансовыхОтчетов
//  ПланФакт - Перечисление.ПланФакт
//  Периодичность - Перечисление.Периодичность
//  ПоВсемПоказателям - Булево
//
Функция ТекстЗапросаДляОтчета(ВидОтчета, ПланФакт, Периодичность, ПоВсемПоказателям = Истина) Экспорт
	
	Если ВидОтчета = Перечисления.ВидыФинансовыхОтчетов.ДоходыРасходы Тогда
		
		Если ПланФакт = Перечисления.ПланФакт.ПланФакт Тогда
			ТекстЗапроса = ТекстЗапросаДоходыРасходыПланФакт();
		Иначе
			ТекстЗапроса = ТекстЗапросаДоходыРасходы(ПланФакт);
		КонецЕсли;
		
	ИначеЕсли ВидОтчета = Перечисления.ВидыФинансовыхОтчетов.ДенежныйПоток Тогда
		
		Если ПланФакт = Перечисления.ПланФакт.ПланФакт Тогда
			ТекстЗапроса = ТекстЗапросаДенежныйПотокПланФакт();
		Иначе
			ТекстЗапроса = ТекстЗапросаДенежныйПоток();
		КонецЕсли;
		
	Иначе // Баланс
		
		Если ПланФакт = Перечисления.ПланФакт.ПланФакт Тогда
			ТекстЗапроса = ТекстЗапросаБалансПланФакт();
		Иначе
			ТекстЗапроса = ТекстЗапросаБаланс();
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьВыбраннуюПериодичностьВЗапросе(ТекстЗапроса, ВидОтчета, Периодичность);
	
	Если ПоВсемПоказателям Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И ПоказателиБизнеса.Ссылка В(&Показатели)", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса расшифровки значения показателя АнализаБизнеса
//
// Параметры:
//     ВидОтчета - Перечисление.ВидыФинансовыхОтчетов
//     СценарийПланирования - СправочникСсылка.СценарииПланирования
// Возвращаемое значение:
//     Строка - Текст запроса для получения расшифровки показателя.
//
Функция ПолучитьТекстЗапросаРасшифровкаПоказателя(ВидОтчета, СценарийПланирования) Экспорт
	
	Если ВидОтчета = Перечисления.ВидыФинансовыхОтчетов.ДоходыРасходы Тогда
		
		Если СценарийПланирования = Справочники.СценарииПланирования.Фактический Тогда
			
			ТекстЗапроса = 
				"ВЫБРАТЬ
				|	ПоказателиБизнеса.Ссылка КАК Показатель,
				|	ПоказателиБизнеса.Родитель КАК Родитель,
				|	ПоказателиБизнеса.Порядок КАК Порядок,
				|	ПоказателиБизнеса.ТипПоказателя КАК ТипПоказателя,
				|	ПоказателиБизнеса.ИсточникДанных КАК ИсточникДанных,
				|	ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
				|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
				|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Доходы)
				|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеДоходы)
				|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.СебестоимостьПродаж) КАК ЭтоДоходыРасходы,
				|	ПоказателиБизнеса.СпособПолученияДанныхИсточника КАК СпособПолученияДанныхИсточника,
				|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя
				|ПОМЕСТИТЬ ПоказателиБизнеса
				|ИЗ
				|	Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
				|ГДЕ
				|	ПоказателиБизнеса.ВидОтчета = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхОтчетов.ДоходыРасходы)
				|	И (ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
				|			ИЛИ ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход))
				|	И ПоказателиБизнеса.Ссылка В(&МассивПоказателей)
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	НАЧАЛОПЕРИОДА(ЗакрытиеМесяца.Дата, МЕСЯЦ) КАК Период,
				|	ЗакрытиеМесяца.Организация КАК Организация,
				|	ИСТИНА КАК МесяцЗакрыт
				|ПОМЕСТИТЬ Закрытия
				|ИЗ
				|	Документ.ЗакрытиеМесяца КАК ЗакрытиеМесяца
				|ГДЕ
				|	ЗакрытиеМесяца.Проведен
				|	И ЗакрытиеМесяца.РасчетФинансовогоРезультата
				|	И ЗакрытиеМесяца.Дата МЕЖДУ &НачалоПериода И &КонецПериода
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	ДоходыИРасходыОбороты.Период КАК Период,
				|	ДоходыИРасходыОбороты.Регистратор КАК Регистратор,
				|	ДоходыИРасходыОбороты.СодержаниеПроводки КАК Содержание,
				|	ПоказателиБизнеса.Показатель КАК Показатель,
				|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
				|	ДоходыИРасходыОбороты.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
				|	ДоходыИРасходыОбороты.Организация КАК Организация,
				|	ДоходыИРасходыОбороты.Проект КАК Проект,
				|	ВЫБОР
				|		КОГДА ЕСТЬNULL(Закрытия.МесяцЗакрыт, ЛОЖЬ)
				|			ТОГДА 0
				|		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
				|			ТОГДА ДоходыИРасходыОбороты.СуммаДоходов
				|		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход)
				|			ТОГДА ДоходыИРасходыОбороты.СуммаРасходов
				|		ИНАЧЕ 0
				|	КОНЕЦ КАК Сумма
				|ПОМЕСТИТЬ ДоходыРасходыПредварительно
				|ИЗ
				|	ПоказателиБизнеса КАК ПоказателиБизнеса
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДоходыИРасходы КАК ДоходыИРасходыОбороты
				|			ЛЕВОЕ СОЕДИНЕНИЕ Закрытия КАК Закрытия
				|			ПО ДоходыИРасходыОбороты.Организация = Закрытия.Организация
				|				И ДоходыИРасходыОбороты.Период = Закрытия.Период
				|		ПО ПоказателиБизнеса.ИсточникДанных = ДоходыИРасходыОбороты.СчетУчета
				|			И (ПоказателиБизнеса.ЭтоДоходыРасходы)
				|ГДЕ
				|	ДоходыИРасходыОбороты.Период МЕЖДУ &НачалоПериода И &КонецПериода
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	ДоходыИРасходыОбороты.Период,
				|	ДоходыИРасходыОбороты.Регистратор,
				|	ДоходыИРасходыОбороты.СодержаниеПроводки,
				|	ПоказателиБизнеса.Показатель,
				|	ПоказателиБизнеса.ИдентификаторПоказателя,
				|	ДоходыИРасходыОбороты.СтруктурнаяЕдиница,
				|	ДоходыИРасходыОбороты.Организация,
				|	ДоходыИРасходыОбороты.Проект,
				|	ВЫБОР
				|		КОГДА НЕ ЕСТЬNULL(Закрытия.МесяцЗакрыт, ЛОЖЬ)
				|			ТОГДА 0
				|		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
				|			ТОГДА ДоходыИРасходыОбороты.СуммаДоходов
				|		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход)
				|			ТОГДА ДоходыИРасходыОбороты.СуммаРасходов
				|		ИНАЧЕ 0
				|	КОНЕЦ
				|ИЗ
				|	ПоказателиБизнеса КАК ПоказателиБизнеса
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ФинансовыйРезультат КАК ДоходыИРасходыОбороты
				|			ЛЕВОЕ СОЕДИНЕНИЕ Закрытия КАК Закрытия
				|			ПО ДоходыИРасходыОбороты.Организация = Закрытия.Организация
				|				И ДоходыИРасходыОбороты.Период = Закрытия.Период
				|		ПО ПоказателиБизнеса.ИсточникДанных = ДоходыИРасходыОбороты.СчетУчета
				|			И (ПоказателиБизнеса.ЭтоДоходыРасходы)
				|ГДЕ
				|	ДоходыИРасходыОбороты.Период МЕЖДУ &НачалоПериода И &КонецПериода
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	УправленческийОбороты.Период,
				|	УправленческийОбороты.Регистратор,
				|	УправленческийОбороты.Содержание,
				|	ПоказателиБизнеса.Показатель,
				|	ПоказателиБизнеса.ИдентификаторПоказателя,
				|	УправленческийОбороты.Организация,
				|	ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка),
				|	ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка),
				|	УправленческийОбороты.Сумма
				|ИЗ
				|	ПоказателиБизнеса КАК ПоказателиБизнеса
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий КАК УправленческийОбороты
				|		ПО (ПоказателиБизнеса.ИсточникДанных = УправленческийОбороты.СчетДт
				|					И ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтОборот)
				|				ИЛИ ПоказателиБизнеса.ИсточникДанных = УправленческийОбороты.СчетКт
				|					И ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.КтОборот))
				|			И (НЕ ПоказателиБизнеса.ЭтоДоходыРасходы)
				|ГДЕ
				|	УправленческийОбороты.СценарийПланирования = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический)
				|	И УправленческийОбороты.Период МЕЖДУ &НачалоПериода И &КонецПериода
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ДоходыИРасходыРасшифровка.Регистратор КАК Документ,
				|	ДоходыИРасходыРасшифровка.Период КАК Период,
				|	ДоходыИРасходыРасшифровка.Сумма КАК Сумма,
				|	ДоходыИРасходыРасшифровка.Содержание КАК Содержание
				|ИЗ
				|	ДоходыРасходыПредварительно КАК ДоходыИРасходыРасшифровка
				|ГДЕ
				|	ДоходыИРасходыРасшифровка.Сумма <> 0
				|
				|УПОРЯДОЧИТЬ ПО
				|	Период";
		Иначе
			
			ТекстЗапроса = 
				"ВЫБРАТЬ
				|	ПоказателиБизнеса.Ссылка КАК Показатель,
				|	ПоказателиБизнеса.Родитель КАК Родитель,
				|	ПоказателиБизнеса.Порядок КАК Порядок,
				|	ПоказателиБизнеса.ТипПоказателя КАК ТипПоказателя,
				|	ПоказателиБизнеса.ИсточникДанных КАК ИсточникДанных,
				|	ПоказателиБизнеса.СпособПолученияДанныхИсточника КАК СпособПолученияДанныхИсточника,
				|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
				|	ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
				|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
				|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Доходы)
				|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеДоходы)
				|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.СебестоимостьПродаж) КАК ЭтоДоходыРасходы
				|ПОМЕСТИТЬ ПоказателиБизнеса
				|ИЗ
				|	Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
				|ГДЕ
				|	ПоказателиБизнеса.ВидОтчета = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхОтчетов.ДоходыРасходы)
				|	И (ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
				|			ИЛИ ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход))
				|	И ПоказателиБизнеса.Ссылка В(&МассивПоказателей)
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	ДоходыИРасходыОбороты.Период КАК Период,
				|	ДоходыИРасходыОбороты.Регистратор КАК Регистратор,
				|	ДоходыИРасходыОбороты.СодержаниеПроводки КАК Содержание,
				|	ПоказателиБизнеса.Показатель КАК Показатель,
				|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
				|	ДоходыИРасходыОбороты.Организация КАК Организация,
				|	ДоходыИРасходыОбороты.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
				|	ДоходыИРасходыОбороты.Проект КАК Проект,
				|	ВЫБОР
				|		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
				|			ТОГДА ДоходыИРасходыОбороты.СуммаДоходов
				|		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход)
				|			ТОГДА ДоходыИРасходыОбороты.СуммаРасходов
				|		ИНАЧЕ 0
				|	КОНЕЦ КАК Сумма
				|ПОМЕСТИТЬ ДоходыРасходыПредварительно
				|ИЗ
				|	ПоказателиБизнеса КАК ПоказателиБизнеса
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДоходыИРасходыПрогноз КАК ДоходыИРасходыОбороты
				|		ПО ПоказателиБизнеса.ИсточникДанных = ДоходыИРасходыОбороты.СчетУчета
				|			И (ПоказателиБизнеса.ЭтоДоходыРасходы)
				|ГДЕ
				|	ДоходыИРасходыОбороты.СценарийПланирования = &СценарийПланирования
				|	И ДоходыИРасходыОбороты.Период МЕЖДУ &НачалоПериода И &КонецПериода
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	УправленческийОбороты.Период,
				|	УправленческийОбороты.Регистратор,
				|	УправленческийОбороты.Содержание,
				|	ПоказателиБизнеса.Показатель,
				|	ПоказателиБизнеса.ИдентификаторПоказателя,
				|	УправленческийОбороты.Организация,
				|	ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка),
				|	ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка),
				|	УправленческийОбороты.Сумма
				|ИЗ
				|	ПоказателиБизнеса КАК ПоказателиБизнеса
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий КАК УправленческийОбороты
				|		ПО (ПоказателиБизнеса.ИсточникДанных = УправленческийОбороты.СчетДт
				|					И ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтОборот)
				|				ИЛИ ПоказателиБизнеса.ИсточникДанных = УправленческийОбороты.СчетКт
				|					И ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.КтОборот))
				|			И (НЕ ПоказателиБизнеса.ЭтоДоходыРасходы)
				|ГДЕ
				|	УправленческийОбороты.СценарийПланирования = &СценарийПланирования
				|	И УправленческийОбороты.Период МЕЖДУ &НачалоПериода И &КонецПериода
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ДоходыИРасходыРасшифровка.Регистратор КАК Документ,
				|	ДоходыИРасходыРасшифровка.Период КАК Период,
				|	ДоходыИРасходыРасшифровка.Сумма КАК Сумма,
				|	ДоходыИРасходыРасшифровка.Содержание КАК Содержание
				|ИЗ
				|	ДоходыРасходыПредварительно КАК ДоходыИРасходыРасшифровка
				|ГДЕ
				|	ДоходыИРасходыРасшифровка.Сумма <> 0
				|
				|УПОРЯДОЧИТЬ ПО
				|	Период";
				
		КонецЕсли;
		
	ИначеЕсли ВидОтчета = Перечисления.ВидыФинансовыхОтчетов.ДенежныйПоток Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	ДвиженияДенежныхСредств.Период КАК Период,
		               |	ДвиженияДенежныхСредств.Регистратор КАК Документ,
		               |	ДвиженияДенежныхСредств.Проект КАК Проект,
		               |	ДвиженияДенежныхСредств.Подразделение КАК Подразделение,
		               |	ДвиженияДенежныхСредств.Аналитика КАК Аналитика,
		               |	ДвиженияДенежныхСредств.ХозяйственнаяОперация КАК Содержание,
		               |	ВЫБОР
		               |		КОГДА ДвиженияДенежныхСредств.СуммаРасход <> 0
		               |			ТОГДА ДвиженияДенежныхСредств.СуммаРасход
		               |		ИНАЧЕ ДвиженияДенежныхСредств.СуммаПриход
		               |	КОНЕЦ КАК Сумма
		               |ИЗ
		               |	РегистрНакопления.ДвиженияДенежныхСредств КАК ДвиженияДенежныхСредств
		               |ГДЕ
		               |	ДвиженияДенежныхСредств.Период МЕЖДУ &НачалоПериода И &КонецПериода
		               |	И ДвиженияДенежныхСредств.СценарийПланирования = &СценарийПланирования
		               |	И (ДвиженияДенежныхСредств.СуммаРасход <> 0
		               |				И ДвиженияДенежныхСредств.Статья В
		               |					(ВЫБРАТЬ
		               |						ПоказателиБизнеса.ИсточникДанных КАК ИсточникДанных
		               |					ИЗ
		               |						Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
		               |					ГДЕ
		               |						ПоказателиБизнеса.Ссылка В (&МассивПоказателей)
		               |						И ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.КтОборот))
		               |			ИЛИ ДвиженияДенежныхСредств.СуммаПриход <> 0
		               |				И ДвиженияДенежныхСредств.Статья В
		               |					(ВЫБРАТЬ
		               |						ПоказателиБизнеса.ИсточникДанных КАК ИсточникДанных
		               |					ИЗ
		               |						Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
		               |					ГДЕ
		               |						ПоказателиБизнеса.Ссылка В (&МассивПоказателей)
		               |						И ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтОборот)))";
	
	Иначе // Баланс
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	Управленческий.Регистратор КАК Документ,
		               |	Управленческий.Период КАК Период,
		               |	Управленческий.Сумма КАК Сумма,
		               |	Управленческий.Содержание КАК Содержание
		               |ИЗ
		               |	РегистрБухгалтерии.Управленческий КАК Управленческий
		               |ГДЕ
		               |	Управленческий.СценарийПланирования = &СценарийПланирования
		               |	И Управленческий.Период МЕЖДУ &НачалоПериода И &КонецПериода
		               |	И (Управленческий.СчетКт В
		               |				(ВЫБРАТЬ
		               |					ПоказателиБизнеса.ИсточникДанных КАК ИсточникДанных
		               |				ИЗ
		               |					Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
		               |				ГДЕ
		               |					ПоказателиБизнеса.Ссылка В (&МассивПоказателей))
		               |			ИЛИ Управленческий.СчетДт В
		               |				(ВЫБРАТЬ
		               |					ПоказателиБизнеса.ИсточникДанных КАК ИсточникДанных
		               |				ИЗ
		               |					Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
		               |				ГДЕ
		               |					ПоказателиБизнеса.Ссылка В (&МассивПоказателей)))";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаДеревоПоказателей()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ПоказателиБизнеса.Ссылка КАК Показатель,
	               |	ПоказателиБизнеса.Наименование КАК Наименование,
	               |	ПоказателиБизнеса.ПредставлениеПоказателя КАК Представление,
	               |	ПоказателиБизнеса.Родитель КАК Родитель,
	               |	ПоказателиБизнеса.Порядок КАК Порядок,
	               |	ПоказателиБизнеса.ИсточникДанных КАК ИсточникДанных,
	               |	ВЫБОР
	               |		КОГДА ПоказателиБизнеса.ИсточникДанных ССЫЛКА ПланСчетов.Управленческий
	               |			ТОГДА ПоказателиБизнеса.ИсточникДанных.ТипСчета
	               |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	               |	КОНЕЦ КАК ТипСчета,
	               |	ПоказателиБизнеса.ТипПоказателя КАК ТипПоказателя,
	               |	ПоказателиБизнеса.СпособПолученияДанныхИсточника КАК СпособПолученияДанныхИсточника,
	               |	ПоказателиБизнеса.ПредставлениеФормулы КАК ПредставлениеФормулы,
	               |	ПоказателиБизнеса.СтрокаФормулы КАК СтрокаФормулы,
	               |	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
	               |	ПоказателиБизнеса.Описание КАК ОписаниеПоказателя,
	               |	ПоказателиБизнеса.ЭтоПроцент КАК ЭтоПроцент,
	               |	ВЫБОР
	               |		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Группа)
	               |			ТОГДА 0
	               |		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход)
	               |				ИЛИ ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
	               |			ТОГДА 1
	               |		ИНАЧЕ 2
	               |	КОНЕЦ КАК ПорядокПоТипу,
	               |	ВЫБОР
	               |		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
	               |			ТОГДА 0
	               |		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход)
	               |			ТОГДА 1
	               |		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Группа)
	               |			ТОГДА 2
	               |		ИНАЧЕ 3
	               |	КОНЕЦ КАК НомерКартинкиСтроки
	               |ИЗ
	               |	Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
	               |ГДЕ
	               |	НЕ ПоказателиБизнеса.ПометкаУдаления
	               |	И ПоказателиБизнеса.ВидОтчета = &ВидОтчета
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ПорядокПоТипу,
	               |	ПоказателиБизнеса.Ссылка ИЕРАРХИЯ,
	               |	Порядок";
	
	Возврат ТекстЗапроса;
	
КонецФункции
 
Функция ТекстЗапросаДоходыРасходыПоБюджету(ПоСсылке)

	ТекстЗапроса = ТекстЗапросаДанныеТаблицДоходыРасходы(ПоСсылке);
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	ТекстЗапроса = ТекстЗапроса + 
					"ВЫБРАТЬ
					|	ПоказателиБизнеса.Ссылка КАК Показатель,
					|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
					|	ДоходыИРасходы.ДатаПланирования КАК ДатаПланирования,
					|	СУММА(ДоходыИРасходы.Сумма) КАК Сумма
					|ИЗ
					|	ДоходыИРасходы КАК ДоходыИРасходы
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
					|		ПО (ПоказателиБизнеса.ИсточникДанных = ДоходыИРасходы.ИсточникДанных)
					|ГДЕ
					|	ПоказателиБизнеса.ВидОтчета = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхОтчетов.ДоходыРасходы)
					|	И НЕ ПоказателиБизнеса.ПометкаУдаления
					|
					|СГРУППИРОВАТЬ ПО
					|	ПоказателиБизнеса.Ссылка,
					|	ПоказателиБизнеса.ИдентификаторПоказателя,
					|	ДоходыИРасходы.ДатаПланирования
					|
					|УПОРЯДОЧИТЬ ПО
					|	ПоказателиБизнеса.Родитель,
					|	ПоказателиБизнеса.Порядок,
					|	Показатель";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДенежныйПотокПоБюджету(ПоСсылке)
	
	ТекстЗапроса = ТекстЗапросаДанныеТаблицДенежныйПоток(ПоСсылке);
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	ТекстЗапроса = ТекстЗапроса +
					"ВЫБРАТЬ
					|	ПоказателиБизнеса.Ссылка КАК Показатель,
					|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
					|	ПоступленияВыбытия.ДатаПланирования КАК ДатаПланирования,
					|	СУММА(ПоступленияВыбытия.Сумма) КАК Сумма
					|ИЗ
					|	ПоступленияВыбытия КАК ПоступленияВыбытия
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
					|		ПО (ПоказателиБизнеса.ИсточникДанных = ПоступленияВыбытия.ИсточникДанных)
					|			И (ПоказателиБизнеса.ТипПоказателя = ПоступленияВыбытия.ТипПоказателя)
					|ГДЕ
					|	ПоказателиБизнеса.ВидОтчета = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхОтчетов.ДенежныйПоток)
					|	И НЕ ПоказателиБизнеса.ПометкаУдаления
					|
					|СГРУППИРОВАТЬ ПО
					|	ПоказателиБизнеса.Ссылка,
					|	ПоказателиБизнеса.ИдентификаторПоказателя,
					|	ПоступленияВыбытия.ДатаПланирования
					|
					|УПОРЯДОЧИТЬ ПО
					|	ПоказателиБизнеса.Родитель,
					|	ПоказателиБизнеса.Порядок,
					|	Показатель";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаБалансПоБюджету(ПоСсылке)
	
	ТекстЗапроса = ТекстЗапросаДанныеТаблицБаланс(ПоСсылке);
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	ТекстЗапроса = ТекстЗапроса + 
				"ВЫБРАТЬ
				|	ПоказателиБизнеса.Ссылка КАК Показатель,
				|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
				|	Баланс.ДатаПланирования КАК ДатаПланирования,
				|	СУММА(Баланс.Сумма) КАК Сумма
				|ИЗ
				|	Баланс КАК Баланс
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
				|		ПО (ПоказателиБизнеса.ИсточникДанных = Баланс.Счет
				|				ИЛИ ПоказателиБизнеса.ИсточникДанных = Баланс.КорСчет)
				|ГДЕ
				|	ПоказателиБизнеса.ВидОтчета = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхОтчетов.Баланс)
				|	И НЕ ПоказателиБизнеса.ПометкаУдаления
				|
				|СГРУППИРОВАТЬ ПО
				|	ПоказателиБизнеса.Ссылка,
				|	ПоказателиБизнеса.ИдентификаторПоказателя,
				|	Баланс.ДатаПланирования
				|
				|УПОРЯДОЧИТЬ ПО
				|	ПоказателиБизнеса.Родитель,
				|	ПоказателиБизнеса.Порядок,
				|	Показатель";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаБюджетаПоВидуОтчета(ВидОтчета, ПоСсылке = Истина)
	
	Если ВидОтчета = Перечисления.ВидыФинансовыхОтчетов.ДоходыРасходы Тогда
		ТекстЗапроса = ТекстЗапросаДоходыРасходыПоБюджету(ПоСсылке);
	ИначеЕсли ВидОтчета = Перечисления.ВидыФинансовыхОтчетов.ДенежныйПоток Тогда
		ТекстЗапроса = ТекстЗапросаДенежныйПотокПоБюджету(ПоСсылке);
	Иначе // Баланс
		ТекстЗапроса = ТекстЗапросаБалансПоБюджету(ПоСсылке);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеТаблицДоходыРасходы(ПоСсылке)
	
	Если ПоСсылке Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	БюджетДоходы.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетДоходы.Счет КАК ИсточникДанных,
		               |	БюджетДоходы.Сумма КАК Сумма
		               |ПОМЕСТИТЬ ДоходыИРасходы
		               |ИЗ
		               |	Документ.Бюджет.Доходы КАК БюджетДоходы
		               |ГДЕ
		               |	БюджетДоходы.Ссылка = &Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетРасходы.ДатаПланирования,
		               |	БюджетРасходы.Счет,
		               |	БюджетРасходы.Сумма
		               |ИЗ
		               |	Документ.Бюджет.Расходы КАК БюджетРасходы
		               |ГДЕ
		               |	БюджетРасходы.Ссылка = &Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ПрямыеЗатраты.ДатаПланирования,
		               |	ПрямыеЗатраты.Счет,
		               |	ПрямыеЗатраты.Сумма
		               |ИЗ
		               |	Документ.Бюджет.ПрямыеЗатраты КАК ПрямыеЗатраты
		               |ГДЕ
		               |	ПрямыеЗатраты.Ссылка = &Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	КосвенныеЗатраты.ДатаПланирования,
		               |	КосвенныеЗатраты.Счет,
		               |	КосвенныеЗатраты.Сумма
		               |ИЗ
		               |	Документ.Бюджет.КосвенныеЗатраты КАК КосвенныеЗатраты
		               |ГДЕ
		               |	КосвенныеЗатраты.Ссылка = &Ссылка";
		
	Иначе // По таблицам значений
		ТекстЗапроса = "ВЫБРАТЬ
		               |	БюджетДоходы.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетДоходы.Счет КАК Счет,
		               |	БюджетДоходы.Сумма КАК Сумма
		               |ПОМЕСТИТЬ Доходы
		               |ИЗ
		               |	&Доходы КАК БюджетДоходы
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	БюджетРасходы.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетРасходы.Счет КАК Счет,
		               |	БюджетРасходы.Сумма КАК Сумма
		               |ПОМЕСТИТЬ Расходы
		               |ИЗ
		               |	&Расходы КАК БюджетРасходы
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ПрямыеЗатраты.ДатаПланирования КАК ДатаПланирования,
		               |	ПрямыеЗатраты.Счет КАК Счет,
		               |	ПрямыеЗатраты.Сумма КАК Сумма
		               |ПОМЕСТИТЬ ПрямыеЗатраты
		               |ИЗ
		               |	&ПрямыеЗатраты КАК ПрямыеЗатраты
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	КосвенныеЗатраты.ДатаПланирования КАК ДатаПланирования,
		               |	КосвенныеЗатраты.Счет КАК Счет,
		               |	КосвенныеЗатраты.Сумма КАК Сумма
		               |ПОМЕСТИТЬ КосвенныеЗатраты
		               |ИЗ
		               |	&КосвенныеЗатраты КАК КосвенныеЗатраты
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	БюджетДоходы.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетДоходы.Счет КАК ИсточникДанных,
		               |	БюджетДоходы.Сумма КАК Сумма
		               |ПОМЕСТИТЬ ДоходыИРасходы
		               |ИЗ
		               |	Доходы КАК БюджетДоходы
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетРасходы.ДатаПланирования,
		               |	БюджетРасходы.Счет,
		               |	БюджетРасходы.Сумма
		               |ИЗ
		               |	Расходы КАК БюджетРасходы
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ПрямыеЗатраты.ДатаПланирования,
		               |	ПрямыеЗатраты.Счет,
		               |	ПрямыеЗатраты.Сумма
		               |ИЗ
		               |	ПрямыеЗатраты КАК ПрямыеЗатраты
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	КосвенныеЗатраты.ДатаПланирования,
		               |	КосвенныеЗатраты.Счет,
		               |	КосвенныеЗатраты.Сумма
		               |ИЗ
		               |	КосвенныеЗатраты КАК КосвенныеЗатраты";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеТаблицДенежныйПоток(ПоСсылке)
	
	Если ПоСсылке Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	БюджетПоступления.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетПоступления.Статья КАК ИсточникДанных,
		               |	ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход) КАК ТипПоказателя,
		               |	БюджетПоступления.Сумма КАК Сумма
		               |ПОМЕСТИТЬ ПоступленияВыбытия
		               |ИЗ
		               |	Документ.Бюджет.Поступления КАК БюджетПоступления
		               |ГДЕ
		               |	БюджетПоступления.Ссылка = &Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетВыбытия.ДатаПланирования,
		               |	БюджетВыбытия.Статья,
		               |	ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход),
		               |	БюджетВыбытия.Сумма
		               |ИЗ
		               |	Документ.Бюджет.Выбытия КАК БюджетВыбытия
		               |ГДЕ
		               |	БюджетВыбытия.Ссылка = &Ссылка";
		
	Иначе // По таблицам значений
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	БюджетПоступления.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетПоступления.Статья КАК ИсточникДанных,
		               |	БюджетПоступления.Сумма КАК Сумма
		               |ПОМЕСТИТЬ Поступления
		               |ИЗ
		               |	&Поступления КАК БюджетПоступления
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	БюджетВыбытия.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетВыбытия.Статья КАК ИсточникДанных,
		               |	БюджетВыбытия.Сумма КАК Сумма
		               |ПОМЕСТИТЬ Выбытия
		               |ИЗ
		               |	&Выбытия КАК БюджетВыбытия
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	БюджетПоступления.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетПоступления.ИсточникДанных КАК ИсточникДанных,
		               |	ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход) КАК ТипПоказателя,
		               |	БюджетПоступления.Сумма КАК Сумма
		               |ПОМЕСТИТЬ ПоступленияВыбытия
		               |ИЗ
		               |	Поступления КАК БюджетПоступления
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетВыбытия.ДатаПланирования,
		               |	БюджетВыбытия.ИсточникДанных,
		               |	ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход),
		               |	БюджетВыбытия.Сумма
		               |ИЗ
		               |	Выбытия КАК БюджетВыбытия";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеТаблицБаланс(ПоСсылке)
	
	Если ПоСсылке Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	БюджетОстатки.Ссылка.ДатаНачала КАК ДатаПланирования,
		               |	БюджетОстатки.Счет КАК Счет,
		               |	БюджетОстатки.Счет КАК КорСчет,
		               |	БюджетОстатки.Сумма КАК Сумма
		               |ПОМЕСТИТЬ Баланс
		               |ИЗ
		               |	Документ.Бюджет.Остатки КАК БюджетОстатки
		               |ГДЕ
		               |	БюджетОстатки.Ссылка = &Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетПрямыеЗатраты.ДатаПланирования,
		               |	БюджетПрямыеЗатраты.Счет,
		               |	БюджетПрямыеЗатраты.КоррСчет,
		               |	БюджетПрямыеЗатраты.Сумма
		               |ИЗ
		               |	Документ.Бюджет.ПрямыеЗатраты КАК БюджетПрямыеЗатраты
		               |ГДЕ
		               |	БюджетПрямыеЗатраты.Ссылка = &Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетКосвенныеЗатраты.ДатаПланирования,
		               |	БюджетКосвенныеЗатраты.Счет,
		               |	БюджетКосвенныеЗатраты.КоррСчет,
		               |	БюджетКосвенныеЗатраты.Сумма
		               |ИЗ
		               |	Документ.Бюджет.КосвенныеЗатраты КАК БюджетКосвенныеЗатраты
		               |ГДЕ
		               |	БюджетКосвенныеЗатраты.Ссылка = &Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетПоступления.ДатаПланирования,
		               |	БюджетПоступления.Счет,
		               |	БюджетПоступления.КоррСчет,
		               |	БюджетПоступления.Сумма
		               |ИЗ
		               |	Документ.Бюджет.Поступления КАК БюджетПоступления
		               |ГДЕ
		               |	БюджетПоступления.Ссылка = &Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетВыбытия.ДатаПланирования,
		               |	БюджетВыбытия.Счет,
		               |	БюджетВыбытия.КоррСчет,
		               |	БюджетВыбытия.Сумма
		               |ИЗ
		               |	Документ.Бюджет.Выбытия КАК БюджетВыбытия
		               |ГДЕ
		               |	БюджетВыбытия.Ссылка = &Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетДоходы.ДатаПланирования,
		               |	БюджетДоходы.Счет,
		               |	БюджетДоходы.КоррСчет,
		               |	БюджетДоходы.Сумма
		               |ИЗ
		               |	Документ.Бюджет.Доходы КАК БюджетДоходы
		               |ГДЕ
		               |	БюджетДоходы.Ссылка = &Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетРасходы.ДатаПланирования,
		               |	БюджетРасходы.Счет,
		               |	БюджетРасходы.КоррСчет,
		               |	БюджетРасходы.Сумма
		               |ИЗ
		               |	Документ.Бюджет.Расходы КАК БюджетРасходы
		               |ГДЕ
		               |	БюджетРасходы.Ссылка = &Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетОперации.ДатаПланирования,
		               |	БюджетОперации.СчетДт,
		               |	БюджетОперации.СчетКт,
		               |	БюджетОперации.Сумма
		               |ИЗ
		               |	Документ.Бюджет.Операции КАК БюджетОперации
		               |ГДЕ
		               |	БюджетОперации.Ссылка = &Ссылка";
		
	Иначе // По таблицам значений
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	&ДатаНачала КАК ДатаПланирования,
		               |	БюджетОстатки.Счет КАК Счет,
		               |	БюджетОстатки.Счет КАК КорСчет,
		               |	БюджетОстатки.Сумма КАК Сумма
		               |ПОМЕСТИТЬ Остатки
		               |ИЗ
		               |	&Остатки КАК БюджетОстатки
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	БюджетПрямыеЗатраты.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетПрямыеЗатраты.Счет КАК Счет,
		               |	БюджетПрямыеЗатраты.КоррСчет КАК КорСчет,
		               |	БюджетПрямыеЗатраты.Сумма КАК Сумма
		               |ПОМЕСТИТЬ ПрямыеЗатраты
		               |ИЗ
		               |	&ПрямыеЗатраты КАК БюджетПрямыеЗатраты
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	БюджетКосвенныеЗатраты.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетКосвенныеЗатраты.Счет КАК Счет,
		               |	БюджетКосвенныеЗатраты.КоррСчет КАК КорСчет,
		               |	БюджетКосвенныеЗатраты.Сумма КАК Сумма
		               |ПОМЕСТИТЬ КосвенныеЗатраты
		               |ИЗ
		               |	&КосвенныеЗатраты КАК БюджетКосвенныеЗатраты
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	БюджетПоступления.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетПоступления.Счет КАК Счет,
		               |	БюджетПоступления.КоррСчет КАК КорСчет,
		               |	БюджетПоступления.Сумма КАК Сумма
		               |ПОМЕСТИТЬ Поступления
		               |ИЗ
		               |	&Поступления КАК БюджетПоступления
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	БюджетВыбытия.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетВыбытия.Счет КАК Счет,
		               |	БюджетВыбытия.КоррСчет КАК КорСчет,
		               |	БюджетВыбытия.Сумма КАК Сумма
		               |ПОМЕСТИТЬ Выбытия
		               |ИЗ
		               |	&Выбытия КАК БюджетВыбытия
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	БюджетДоходы.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетДоходы.Счет КАК Счет,
		               |	БюджетДоходы.КоррСчет КАК КорСчет,
		               |	БюджетДоходы.Сумма КАК Сумма
		               |ПОМЕСТИТЬ Доходы
		               |ИЗ
		               |	&Доходы КАК БюджетДоходы
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	БюджетРасходы.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетРасходы.Счет КАК Счет,
		               |	БюджетРасходы.КоррСчет КАК КорСчет,
		               |	БюджетРасходы.Сумма КАК Сумма
		               |ПОМЕСТИТЬ Расходы
		               |ИЗ
		               |	&Расходы КАК БюджетРасходы
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	БюджетОперации.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетОперации.СчетДт КАК Счет,
		               |	БюджетОперации.СчетКт КАК КорСчет,
		               |	БюджетОперации.Сумма КАК Сумма
		               |ПОМЕСТИТЬ Операции
		               |ИЗ
		               |	&Операции КАК БюджетОперации
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	БюджетОстатки.ДатаПланирования КАК ДатаПланирования,
		               |	БюджетОстатки.Счет КАК Счет,
		               |	БюджетОстатки.КорСчет КАК КорСчет,
		               |	БюджетОстатки.Сумма КАК Сумма
		               |ПОМЕСТИТЬ Баланс
		               |ИЗ
		               |	Остатки КАК БюджетОстатки
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетПрямыеЗатраты.ДатаПланирования,
		               |	БюджетПрямыеЗатраты.Счет,
		               |	БюджетПрямыеЗатраты.КорСчет,
		               |	БюджетПрямыеЗатраты.Сумма
		               |ИЗ
		               |	ПрямыеЗатраты КАК БюджетПрямыеЗатраты
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетКосвенныеЗатраты.ДатаПланирования,
		               |	БюджетКосвенныеЗатраты.Счет,
		               |	БюджетКосвенныеЗатраты.КорСчет,
		               |	БюджетКосвенныеЗатраты.Сумма
		               |ИЗ
		               |	КосвенныеЗатраты КАК БюджетКосвенныеЗатраты
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетПоступления.ДатаПланирования,
		               |	БюджетПоступления.Счет,
		               |	БюджетПоступления.КорСчет,
		               |	БюджетПоступления.Сумма
		               |ИЗ
		               |	Поступления КАК БюджетПоступления
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетВыбытия.ДатаПланирования,
		               |	БюджетВыбытия.Счет,
		               |	БюджетВыбытия.КорСчет,
		               |	БюджетВыбытия.Сумма
		               |ИЗ
		               |	Выбытия КАК БюджетВыбытия
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетДоходы.ДатаПланирования,
		               |	БюджетДоходы.Счет,
		               |	БюджетДоходы.КорСчет,
		               |	БюджетДоходы.Сумма
		               |ИЗ
		               |	Доходы КАК БюджетДоходы
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетРасходы.ДатаПланирования,
		               |	БюджетРасходы.Счет,
		               |	БюджетРасходы.КорСчет,
		               |	БюджетРасходы.Сумма
		               |ИЗ
		               |	Расходы КАК БюджетРасходы
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	БюджетОперации.ДатаПланирования,
		               |	БюджетОперации.Счет,
		               |	БюджетОперации.КорСчет,
		               |	БюджетОперации.Сумма
		               |ИЗ
		               |	Операции КАК БюджетОперации";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДоходыРасходы(ПланФакт)
	
	Если ПланФакт = Перечисления.ПланФакт.План Тогда
		ТекстЗапроса = ТекстЗапросаДоходыРасходыПланПредварительно();
		ИмяТаблицы = "ДоходыРасходыПредварительноПлан"; // Не локализуется
	Иначе
		ТекстЗапроса = ТекстЗапросаДоходыРасходыФактПредварительно();
		ИмяТаблицы = "ДоходыРасходыПредварительноФакт"; // Не локализуется
	КонецЕсли;
	
	ТекстЗапросаИтоговаяТаблица = 
			"ВЫБРАТЬ
			|	НАЧАЛОПЕРИОДА(ДоходыИРасходыОбороты.Период, МЕСЯЦ) КАК Период,
			|	ДоходыИРасходыОбороты.Показатель КАК Показатель,
			|	ДоходыИРасходыОбороты.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
			|	СУММА(ДоходыИРасходыОбороты.Сумма) КАК Сумма
			|ИЗ
			|	ДоходыРасходыПредварительно КАК ДоходыИРасходыОбороты
			|ГДЕ
			|	ДоходыИРасходыОбороты.Период <> ДАТАВРЕМЯ(1, 1, 1)
			|
			|СГРУППИРОВАТЬ ПО
			|	НАЧАЛОПЕРИОДА(ДоходыИРасходыОбороты.Период, МЕСЯЦ),
			|	ДоходыИРасходыОбороты.Показатель,
			|	ДоходыИРасходыОбороты.ИдентификаторПоказателя
			|
			|ИМЕЮЩИЕ
			|	СУММА(ДоходыИРасходыОбороты.Сумма) <> 0
			|
			|УПОРЯДОЧИТЬ ПО
			|	Период";
	
	ТекстЗапросаИтоговаяТаблица = СтрЗаменить(ТекстЗапросаИтоговаяТаблица, "ДоходыРасходыПредварительно", ИмяТаблицы);
	
	ТекстЗапроса = ТекстЗапроса 
				 + ОбщегоНазначения.РазделительПакетаЗапросов()
				 + ТекстЗапросаИтоговаяТаблица;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДоходыРасходыПланФакт()
	
	ТекстЗапросаПлан = ТекстЗапросаДоходыРасходыПланПредварительно();
	ТекстЗапросаФакт = ТекстЗапросаДоходыРасходыФактПредварительно();
	
	ТекстЗапроса = 
		ТекстЗапросаПлан + ОбщегоНазначения.РазделительПакетаЗапросов() 
		+ ТекстЗапросаФакт + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ
		|	ДоходыИРасходыОбороты.Период КАК Период,
		|	ДоходыИРасходыОбороты.Показатель КАК Показатель,
		|	ДоходыИРасходыОбороты.Организация КАК Организация,
		|	ДоходыИРасходыОбороты.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ДоходыИРасходыОбороты.Проект КАК Проект,
		|	ДоходыИРасходыОбороты.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
		|	ДоходыИРасходыОбороты.Сумма КАК СуммаПлан,
		|	0 КАК СуммаФакт
		|ПОМЕСТИТЬ ДоходыРасходыПланФакт
		|ИЗ
		|	ДоходыРасходыПредварительноПлан КАК ДоходыИРасходыОбороты
		|ГДЕ
		|	ДоходыИРасходыОбороты.Период <> ДАТАВРЕМЯ(1, 1, 1)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДоходыИРасходыОбороты.Период,
		|	ДоходыИРасходыОбороты.Показатель,
		|	ДоходыИРасходыОбороты.Организация,
		|	ДоходыИРасходыОбороты.СтруктурнаяЕдиница,
		|	ДоходыИРасходыОбороты.Проект,
		|	ДоходыИРасходыОбороты.ИдентификаторПоказателя,
		|	0,
		|	ДоходыИРасходыОбороты.Сумма
		|ИЗ
		|	ДоходыРасходыПредварительноФакт КАК ДоходыИРасходыОбороты
		|ГДЕ
		|	ДоходыИРасходыОбороты.Период <> ДАТАВРЕМЯ(1, 1, 1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ДоходыИРасходыОбороты.Период, МЕСЯЦ) КАК Период,
		|	ДоходыИРасходыОбороты.Показатель КАК Показатель,
		|	ДоходыИРасходыОбороты.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
		|	СУММА(ДоходыИРасходыОбороты.СуммаПлан) КАК СуммаПлан,
		|	СУММА(ДоходыИРасходыОбороты.СуммаФакт) КАК СуммаФакт
		|ИЗ
		|	ДоходыРасходыПланФакт КАК ДоходыИРасходыОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	НАЧАЛОПЕРИОДА(ДоходыИРасходыОбороты.Период, МЕСЯЦ),
		|	ДоходыИРасходыОбороты.Показатель,
		|	ДоходыИРасходыОбороты.ИдентификаторПоказателя
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДенежныйПоток()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПоказателиБизнеса.Ссылка КАК Показатель,
		|	ПоказателиБизнеса.Родитель КАК Родитель,
		|	ПоказателиБизнеса.Порядок КАК Порядок,
		|	ПоказателиБизнеса.ТипПоказателя КАК ТипПоказателя,
		|	ПоказателиБизнеса.ИсточникДанных КАК ИсточникДанных,
		|	ПоказателиБизнеса.СпособПолученияДанныхИсточника КАК СпособПолученияДанныхИсточника,
		|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя
		|ПОМЕСТИТЬ ПоказателиБизнеса
		|ИЗ
		|	Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
		|ГДЕ
		|	ПоказателиБизнеса.ВидОтчета = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхОтчетов.ДенежныйПоток)
		|	И (ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
		|			ИЛИ ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДвиженияДенежныхСредствОбороты.Период КАК Период,
		|	ПоказателиБизнеса.Показатель КАК Показатель,
		|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
		|	СУММА(ВЫБОР
		|			КОГДА ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтОборот)
		|				ТОГДА ДвиженияДенежныхСредствОбороты.СуммаПриходОборот
		|			КОГДА ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.КтОборот)
		|				ТОГДА ДвиженияДенежныхСредствОбороты.СуммаРасходОборот
		|			КОГДА ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтМинусКтОборот)
		|				ТОГДА ДвиженияДенежныхСредствОбороты.СуммаПриходОборот - ДвиженияДенежныхСредствОбороты.СуммаРасходОборот
		|			ИНАЧЕ 0
		|		КОНЕЦ * ВЫБОР
		|			КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
		|				ТОГДА 1
		|			ИНАЧЕ -1
		|		КОНЕЦ) КАК Сумма
		|ИЗ
		|	ПоказателиБизнеса КАК ПоказателиБизнеса
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияДенежныхСредств.Обороты(&НачалоПериода, &КонецПериода, МЕСЯЦ, СценарийПланирования = &СценарийПланирования) КАК ДвиженияДенежныхСредствОбороты
		|		ПО ПоказателиБизнеса.ИсточникДанных = ДвиженияДенежныхСредствОбороты.Статья
		|ГДЕ
		|	ДвиженияДенежныхСредствОбороты.Период <> ДАТАВРЕМЯ(1, 1, 1)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДвиженияДенежныхСредствОбороты.Период,
		|	ПоказателиБизнеса.Показатель,
		|	ПоказателиБизнеса.ИдентификаторПоказателя
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДенежныйПотокПланФакт()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПоказателиБизнеса.Ссылка КАК Показатель,
	|	ПоказателиБизнеса.ТипПоказателя КАК ТипПоказателя,
	|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
	|	ПоказателиБизнеса.ИсточникДанных КАК ИсточникДанных,
	|	ПоказателиБизнеса.СпособПолученияДанныхИсточника КАК СпособПолученияДанныхИсточника,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования
	|ПОМЕСТИТЬ ПоказателиПоСценариям
	|ИЗ
	|	Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
	|ГДЕ
	|	ПоказателиБизнеса.ВидОтчета = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхОтчетов.ДенежныйПоток)
	|	И (ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
	|			ИЛИ ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход))
	|	И ПоказателиБизнеса.Ссылка В(&Показатели)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоказателиБизнеса.Ссылка,
	|	ПоказателиБизнеса.ТипПоказателя,
	|	ПоказателиБизнеса.ИдентификаторПоказателя,
	|	ПоказателиБизнеса.ИсточникДанных,
	|	ПоказателиБизнеса.СпособПолученияДанныхИсточника,
	|	&СценарийПланирования
	|ИЗ
	|	Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
	|ГДЕ
	|	ПоказателиБизнеса.ВидОтчета = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхОтчетов.ДенежныйПоток)
	|	И (ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
	|			ИЛИ ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияДенежныхСредствОбороты.Период КАК Период,
	|	ПоказателиПоСценариям.Показатель КАК Показатель,
	|	ПоказателиПоСценариям.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
	|	СУММА(ВЫБОР
	|			КОГДА ДвиженияДенежныхСредствОбороты.СценарийПланирования = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический)
	|				ТОГДА 0
	|			КОГДА ПоказателиПоСценариям.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтОборот)
	|				ТОГДА ДвиженияДенежныхСредствОбороты.СуммаПриходОборот
	|			КОГДА ПоказателиПоСценариям.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.КтОборот)
	|				ТОГДА ДвиженияДенежныхСредствОбороты.СуммаРасходОборот
	|			КОГДА ПоказателиПоСценариям.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтМинусКтОборот)
	|				ТОГДА ДвиженияДенежныхСредствОбороты.СуммаПриходОборот - ДвиженияДенежныхСредствОбороты.СуммаРасходОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ * ВЫБОР
	|			КОГДА ПоказателиПоСценариям.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ) КАК СуммаПлан,
	|	СУММА(ВЫБОР
	|			КОГДА ДвиженияДенежныхСредствОбороты.СценарийПланирования <> ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический)
	|				ТОГДА 0
	|			КОГДА ПоказателиПоСценариям.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтОборот)
	|				ТОГДА ДвиженияДенежныхСредствОбороты.СуммаПриходОборот
	|			КОГДА ПоказателиПоСценариям.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.КтОборот)
	|				ТОГДА ДвиженияДенежныхСредствОбороты.СуммаРасходОборот
	|			КОГДА ПоказателиПоСценариям.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтМинусКтОборот)
	|				ТОГДА ДвиженияДенежныхСредствОбороты.СуммаПриходОборот - ДвиженияДенежныхСредствОбороты.СуммаРасходОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ * ВЫБОР
	|			КОГДА ПоказателиПоСценариям.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ) КАК СуммаФакт
	|ИЗ
	|	ПоказателиПоСценариям КАК ПоказателиПоСценариям
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияДенежныхСредств.Обороты(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				МЕСЯЦ,
	|				(СценарийПланирования = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический)
	|					ИЛИ СценарийПланирования = &СценарийПланирования) ) КАК ДвиженияДенежныхСредствОбороты
	|		ПО ПоказателиПоСценариям.ИсточникДанных = ДвиженияДенежныхСредствОбороты.Статья
	|			И ПоказателиПоСценариям.СценарийПланирования = ДвиженияДенежныхСредствОбороты.СценарийПланирования
	|ГДЕ
	|	ДвиженияДенежныхСредствОбороты.Период <> ДАТАВРЕМЯ(1, 1, 1)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияДенежныхСредствОбороты.Период,
	|	ПоказателиПоСценариям.Показатель,
	|	ПоказателиПоСценариям.ИдентификаторПоказателя
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаБаланс()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПоказателиБизнеса.Ссылка КАК Показатель,
		|	ПоказателиБизнеса.Родитель КАК Родитель,
		|	ПоказателиБизнеса.Порядок КАК Порядок,
		|	ПоказателиБизнеса.ТипПоказателя КАК ТипПоказателя,
		|	ПоказателиБизнеса.ИсточникДанных КАК ИсточникДанных,
		|	ПоказателиБизнеса.СпособПолученияДанныхИсточника КАК СпособПолученияДанныхИсточника,
		|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя
		|ПОМЕСТИТЬ ПоказателиБизнеса
		|ИЗ
		|	Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
		|ГДЕ
		|	ПоказателиБизнеса.ВидОтчета = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхОтчетов.Баланс)
		|	И (ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
		|			ИЛИ ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УправленческийОбороты.Период КАК Период,
		|	ПоказателиБизнеса.Показатель КАК Показатель,
		|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
		|	СУММА(ВЫБОР
		|			КОГДА ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтСальдо)
		|				ТОГДА УправленческийОбороты.СуммаКонечныйОстатокДт
		|			КОГДА ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.КтСальдо)
		|				ТОГДА УправленческийОбороты.СуммаКонечныйОстатокКт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Сумма,
		|	СУММА(ВЫБОР
		|			КОГДА ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтСальдо)
		|				ТОГДА УправленческийОбороты.СуммаНачальныйОстатокДт
		|			КОГДА ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.КтСальдо)
		|				ТОГДА УправленческийОбороты.СуммаНачальныйОстатокКт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаНачальныйОстаток
		|ИЗ
		|	ПоказателиБизнеса КАК ПоказателиБизнеса
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.ОстаткиИОбороты(
		|				&НачалоПериода,
		|				&КонецПериода,
		|				МЕСЯЦ,
		|				ДвиженияИГраницыПериода,
		|				,
		|				СценарийПланирования = &СценарийПланирования) КАК УправленческийОбороты
		|		ПО ПоказателиБизнеса.ИсточникДанных = УправленческийОбороты.Счет
		|ГДЕ
		|	УправленческийОбороты.Период <> ДАТАВРЕМЯ(1, 1, 1)
		|
		|СГРУППИРОВАТЬ ПО
		|	УправленческийОбороты.Период,
		|	ПоказателиБизнеса.Показатель,
		|	ПоказателиБизнеса.ИдентификаторПоказателя
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаБалансПланФакт()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПоказателиБизнеса.Ссылка КАК Показатель,
		|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
		|	ПоказателиБизнеса.ИсточникДанных КАК ИсточникДанных,
		|	ПоказателиБизнеса.СпособПолученияДанныхИсточника КАК СпособПолученияДанныхИсточника,
		|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования
		|ПОМЕСТИТЬ ПоказателиПоСценариям
		|ИЗ
		|	Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
		|ГДЕ
		|	ПоказателиБизнеса.ВидОтчета = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхОтчетов.Баланс)
		|	И (ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
		|			ИЛИ ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход))
		|	И ПоказателиБизнеса.Ссылка В(&Показатели)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоказателиБизнеса.Ссылка,
		|	ПоказателиБизнеса.ИдентификаторПоказателя,
		|	ПоказателиБизнеса.ИсточникДанных,
		|	ПоказателиБизнеса.СпособПолученияДанныхИсточника,
		|	&СценарийПланирования
		|ИЗ
		|	Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
		|ГДЕ
		|	ПоказателиБизнеса.ВидОтчета = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхОтчетов.Баланс)
		|	И (ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
		|			ИЛИ ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход))
		|	И ПоказателиБизнеса.Ссылка В(&Показатели)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УправленческийОбороты.Период КАК Период,
		|	ПоказателиПоСценариям.Показатель КАК Показатель,
		|	ПоказателиПоСценариям.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
		|	СУММА(ВЫБОР
		|			КОГДА УправленческийОбороты.СценарийПланирования = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический)
		|				ТОГДА 0
		|			КОГДА ПоказателиПоСценариям.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтСальдо)
		|				ТОГДА УправленческийОбороты.СуммаКонечныйОстатокДт
		|			КОГДА ПоказателиПоСценариям.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.КтСальдо)
		|				ТОГДА УправленческийОбороты.СуммаКонечныйОстатокКт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаПлан,
		|	СУММА(ВЫБОР
		|			КОГДА УправленческийОбороты.СценарийПланирования <> ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический)
		|				ТОГДА 0
		|			КОГДА ПоказателиПоСценариям.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтСальдо)
		|				ТОГДА УправленческийОбороты.СуммаКонечныйОстатокДт
		|			КОГДА ПоказателиПоСценариям.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.КтСальдо)
		|				ТОГДА УправленческийОбороты.СуммаКонечныйОстатокКт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаФакт,
		|	СУММА(ВЫБОР
		|			КОГДА УправленческийОбороты.СценарийПланирования = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический)
		|				ТОГДА 0
		|			КОГДА ПоказателиПоСценариям.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтСальдо)
		|				ТОГДА УправленческийОбороты.СуммаНачальныйОстатокДт
		|			КОГДА ПоказателиПоСценариям.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.КтСальдо)
		|				ТОГДА УправленческийОбороты.СуммаНачальныйОстатокКт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаНачальныйОстатокПлан,
		|	СУММА(ВЫБОР
		|			КОГДА УправленческийОбороты.СценарийПланирования <> ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический)
		|				ТОГДА 0
		|			КОГДА ПоказателиПоСценариям.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтСальдо)
		|				ТОГДА УправленческийОбороты.СуммаНачальныйОстатокДт
		|			КОГДА ПоказателиПоСценариям.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.КтСальдо)
		|				ТОГДА УправленческийОбороты.СуммаНачальныйОстатокКт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаНачальныйОстатокФакт
		|ИЗ
		|	ПоказателиПоСценариям КАК ПоказателиПоСценариям
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.ОстаткиИОбороты(
		|				&НачалоПериода,
		|				&КонецПериода,
		|				МЕСЯЦ,
		|				ДвиженияИГраницыПериода,
		|				,
		|				(СценарийПланирования = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический)
		|					ИЛИ СценарийПланирования = &СценарийПланирования)) КАК УправленческийОбороты
		|		ПО ПоказателиПоСценариям.ИсточникДанных = УправленческийОбороты.Счет
		|			И ПоказателиПоСценариям.СценарийПланирования = УправленческийОбороты.СценарийПланирования
		|ГДЕ
		|	УправленческийОбороты.Период <> ДАТАВРЕМЯ(1, 1, 1)
		|
		|СГРУППИРОВАТЬ ПО
		|	УправленческийОбороты.Период,
		|	ПоказателиПоСценариям.Показатель,
		|	ПоказателиПоСценариям.ИдентификаторПоказателя
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДоходыРасходыФактПредварительно()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПоказателиБизнеса.Ссылка КАК Показатель,
		|	ПоказателиБизнеса.Родитель КАК Родитель,
		|	ПоказателиБизнеса.Порядок КАК Порядок,
		|	ПоказателиБизнеса.ТипПоказателя КАК ТипПоказателя,
		|	ПоказателиБизнеса.ИсточникДанных КАК ИсточникДанных,
		|	ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
		|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
		|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Доходы)
		|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеДоходы)
		|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.СебестоимостьПродаж) КАК ЭтоДоходыРасходы,
		|	ПоказателиБизнеса.СпособПолученияДанныхИсточника КАК СпособПолученияДанныхИсточника,
		|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя
		|ПОМЕСТИТЬ ПоказателиБизнесаФакт
		|ИЗ
		|	Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
		|ГДЕ
		|	ПоказателиБизнеса.ВидОтчета = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхОтчетов.ДоходыРасходы)
		|	И (ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
		|			ИЛИ ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход))
		|	И ПоказателиБизнеса.Ссылка В(&Показатели)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ЗакрытиеМесяца.Дата, МЕСЯЦ) КАК Период,
		|	ЗакрытиеМесяца.Организация КАК Организация,
		|	ИСТИНА КАК МесяцЗакрыт
		|ПОМЕСТИТЬ Закрытия
		|ИЗ
		|	Документ.ЗакрытиеМесяца КАК ЗакрытиеМесяца
		|ГДЕ
		|	ЗакрытиеМесяца.Проведен
		|	И ЗакрытиеМесяца.РасчетФинансовогоРезультата
		|	И ЗакрытиеМесяца.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДоходыИРасходыОбороты.Период КАК Период,
		|	ПоказателиБизнеса.Показатель КАК Показатель,
		|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
		|	ДоходыИРасходыОбороты.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ДоходыИРасходыОбороты.Организация КАК Организация,
		|	ДоходыИРасходыОбороты.Проект КАК Проект,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(Закрытия.МесяцЗакрыт, ЛОЖЬ)
		|			ТОГДА 0
		|		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
		|			ТОГДА ДоходыИРасходыОбороты.СуммаДоходовОборот
		|		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход)
		|			ТОГДА ДоходыИРасходыОбороты.СуммаРасходовОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Сумма
		|ПОМЕСТИТЬ ДоходыРасходыПредварительноФакт
		|ИЗ
		|	ПоказателиБизнесаФакт КАК ПоказателиБизнеса
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДоходыИРасходы.Обороты(&НачалоПериода, &КонецПериода, МЕСЯЦ, ) КАК ДоходыИРасходыОбороты
		|			ЛЕВОЕ СОЕДИНЕНИЕ Закрытия КАК Закрытия
		|			ПО ДоходыИРасходыОбороты.Организация = Закрытия.Организация
		|				И ДоходыИРасходыОбороты.Период = Закрытия.Период
		|		ПО ПоказателиБизнеса.ИсточникДанных = ДоходыИРасходыОбороты.СчетУчета
		|			И (ПоказателиБизнеса.ЭтоДоходыРасходы)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДоходыИРасходыОбороты.Период,
		|	ПоказателиБизнеса.Показатель,
		|	ПоказателиБизнеса.ИдентификаторПоказателя,
		|	ДоходыИРасходыОбороты.СтруктурнаяЕдиница,
		|	ДоходыИРасходыОбороты.Организация,
		|	ДоходыИРасходыОбороты.Проект,
		|	ВЫБОР
		|		КОГДА НЕ ЕСТЬNULL(Закрытия.МесяцЗакрыт, ЛОЖЬ)
		|			ТОГДА 0
		|		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
		|			ТОГДА ДоходыИРасходыОбороты.СуммаДоходовОборот
		|		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход)
		|			ТОГДА ДоходыИРасходыОбороты.СуммаРасходовОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|ИЗ
		|	ПоказателиБизнесаФакт КАК ПоказателиБизнеса
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ФинансовыйРезультат.Обороты(&НачалоПериода, &КонецПериода, МЕСЯЦ, ) КАК ДоходыИРасходыОбороты
		|			ЛЕВОЕ СОЕДИНЕНИЕ Закрытия КАК Закрытия
		|			ПО ДоходыИРасходыОбороты.Организация = Закрытия.Организация
		|				И ДоходыИРасходыОбороты.Период = Закрытия.Период
		|		ПО ПоказателиБизнеса.ИсточникДанных = ДоходыИРасходыОбороты.СчетУчета
		|			И (ПоказателиБизнеса.ЭтоДоходыРасходы)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УправленческийОбороты.Период,
		|	ПоказателиБизнеса.Показатель,
		|	ПоказателиБизнеса.ИдентификаторПоказателя,
		|	УправленческийОбороты.Организация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка),
		|	ВЫБОР
		|		КОГДА ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтОборот)
		|			ТОГДА УправленческийОбороты.СуммаОборотДт
		|		КОГДА ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.КтОборот)
		|			ТОГДА УправленческийОбороты.СуммаОборотКт
		|		КОГДА ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтМинусКтОборот)
		|			ТОГДА УправленческийОбороты.СуммаОборотДт - УправленческийОбороты.СуммаОборотКт
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|ИЗ
		|	ПоказателиБизнесаФакт КАК ПоказателиБизнеса
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Обороты(&НачалоПериода, &КонецПериода, МЕСЯЦ, , СценарийПланирования = ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический), ) КАК УправленческийОбороты
		|		ПО ПоказателиБизнеса.ИсточникДанных = УправленческийОбороты.Счет
		|			И (НЕ ПоказателиБизнеса.ЭтоДоходыРасходы)
		|ГДЕ
		|	УправленческийОбороты.Период <> ДАТАВРЕМЯ(1, 1, 1)";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаДоходыРасходыПланПредварительно()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПоказателиБизнеса.Ссылка КАК Показатель,
		|	ПоказателиБизнеса.Родитель КАК Родитель,
		|	ПоказателиБизнеса.Порядок КАК Порядок,
		|	ПоказателиБизнеса.ТипПоказателя КАК ТипПоказателя,
		|	ПоказателиБизнеса.ИсточникДанных КАК ИсточникДанных,
		|	ПоказателиБизнеса.СпособПолученияДанныхИсточника КАК СпособПолученияДанныхИсточника,
		|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
		|	ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
		|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
		|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Доходы)
		|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеДоходы)
		|		ИЛИ ПоказателиБизнеса.ИсточникДанных.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.СебестоимостьПродаж) КАК ЭтоДоходыРасходы
		|ПОМЕСТИТЬ ПоказателиБизнесаПлан
		|ИЗ
		|	Справочник.ПоказателиБизнеса КАК ПоказателиБизнеса
		|ГДЕ
		|	ПоказателиБизнеса.ВидОтчета = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхОтчетов.ДоходыРасходы)
		|	И (ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
		|			ИЛИ ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход))
		|	И ПоказателиБизнеса.Ссылка В(&Показатели)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДоходыИРасходыОбороты.Период КАК Период,
		|	ПоказателиБизнеса.Показатель КАК Показатель,
		|	ПоказателиБизнеса.ИдентификаторПоказателя КАК ИдентификаторПоказателя,
		|	ДоходыИРасходыОбороты.Организация КАК Организация,
		|	ДоходыИРасходыОбороты.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ДоходыИРасходыОбороты.Проект КАК Проект,
		|	ВЫБОР
		|		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Доход)
		|			ТОГДА ДоходыИРасходыОбороты.СуммаДоходовОборот
		|		КОГДА ПоказателиБизнеса.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейБизнеса.Расход)
		|			ТОГДА ДоходыИРасходыОбороты.СуммаРасходовОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Сумма
		|ПОМЕСТИТЬ ДоходыРасходыПредварительноПлан
		|ИЗ
		|	ПоказателиБизнесаПлан КАК ПоказателиБизнеса
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДоходыИРасходыПрогноз.Обороты(&НачалоПериода, &КонецПериода, МЕСЯЦ, СценарийПланирования = &СценарийПланирования) КАК ДоходыИРасходыОбороты
		|		ПО ПоказателиБизнеса.ИсточникДанных = ДоходыИРасходыОбороты.СчетУчета
		|			И (ПоказателиБизнеса.ЭтоДоходыРасходы)
		|ГДЕ
		|	ДоходыИРасходыОбороты.Период <> ДАТАВРЕМЯ(1, 1, 1)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УправленческийОбороты.Период,
		|	ПоказателиБизнеса.Показатель,
		|	ПоказателиБизнеса.ИдентификаторПоказателя,
		|	УправленческийОбороты.Организация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка),
		|	ВЫБОР
		|		КОГДА ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтОборот)
		|			ТОГДА УправленческийОбороты.СуммаОборотДт
		|		КОГДА ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.КтОборот)
		|			ТОГДА УправленческийОбороты.СуммаОборотКт
		|		КОГДА ПоказателиБизнеса.СпособПолученияДанныхИсточника = ЗНАЧЕНИЕ(Перечисление.СпособыПолученияДанныхИсточника.ДтМинусКтОборот)
		|			ТОГДА УправленческийОбороты.СуммаОборотДт - УправленческийОбороты.СуммаОборотКт
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|ИЗ
		|	ПоказателиБизнесаПлан КАК ПоказателиБизнеса
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Обороты(&НачалоПериода, &КонецПериода, МЕСЯЦ, , СценарийПланирования = &СценарийПланирования, ) КАК УправленческийОбороты
		|		ПО ПоказателиБизнеса.ИсточникДанных = УправленческийОбороты.Счет
		|			И (НЕ ПоказателиБизнеса.ЭтоДоходыРасходы)
		|ГДЕ
		|	УправленческийОбороты.Период <> ДАТАВРЕМЯ(1, 1, 1)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьВыбраннуюПериодичностьВЗапросе(ТекстЗапроса, ВидОтчета, Периодичность)
	
	Если Периодичность = Перечисления.Периодичность.Квартал Тогда
		ПериодичностьСтрокой = "КВАРТАЛ"; // Не локализуется
	ИначеЕсли Периодичность = Перечисления.Периодичность.Полугодие Тогда
		ПериодичностьСтрокой = "ПОЛУГОДИЕ"; // Не локализуется
	ИначеЕсли Периодичность = Перечисления.Периодичность.Год Тогда
		ПериодичностьСтрокой = "ГОД"; // Не локализуется
	Иначе
		ПериодичностьСтрокой = "МЕСЯЦ"; // Не локализуется
	КонецЕсли;
	
	Если ВидОтчета = Перечисления.ВидыФинансовыхОтчетов.ДоходыРасходы Тогда
		
		ИсходноеВыражение = "НАЧАЛОПЕРИОДА(ДоходыИРасходыОбороты.Период, МЕСЯЦ)"; // Не локализуется
		НовоеВыражение = СтрЗаменить(ИсходноеВыражение, "МЕСЯЦ", ПериодичностьСтрокой);
		
		НомерОператора = 0;
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
		ПакетЗапроса = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1];
		
		ПоляСхемыЗапроса = ПакетЗапроса.Операторы[НомерОператора].ВыбираемыеПоля;
		ВыражениеПериода = ПоляСхемыЗапроса.Найти(ИсходноеВыражение);
		ПоляСхемыЗапроса.Установить(ПоляСхемыЗапроса.Индекс(ВыражениеПериода), Новый ВыражениеСхемыЗапроса(НовоеВыражение));
		
		ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
		
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "МЕСЯЦ", ПериодичностьСтрокой); 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
