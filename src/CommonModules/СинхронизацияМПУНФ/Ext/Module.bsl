
#Область ЗапускФоновогоЗаданияДляСервера

Процедура ЗапуститьУдалениеНомеровВФоновомЗадании(СтруктураЗапроса) Экспорт
	
	РегистрыСведений.СформированныеСообщенияСИзмененнымиНомерамиМПУНФ.УдалитьСообщение(СтруктураЗапроса.НомерСообщения, СтруктураЗапроса.КодКлиентскогоУзла);
	
КонецПроцедуры

Процедура СоздатьСообщениеСПодтвержденнымиНомерами(КодУзла) Экспорт
	
	НачатьТранзакцию();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НомераСправочниковИДокументовДляИзмененияНаКлиентскомУзлеМП.Ссылка КАК Ссылка,
	|	НомераСправочниковИДокументовДляИзмененияНаКлиентскомУзлеМП.Номер КАК Номер
	|ИЗ
	|	РегистрСведений.НомераСправочниковИДокументовДляИзмененияНаКлиентскомУзлеМП КАК НомераСправочниковИДокументовДляИзмененияНаКлиентскомУзлеМП
	|ГДЕ
	|	НомераСправочниковИДокументовДляИзмененияНаКлиентскомУзлеМП.КодУзла = &КодУзла";
	
	Запрос.УстановитьПараметр("КодУзла", КодУзла);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	МассивСЗаписями = Новый Массив;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Структура = Новый Структура("Ссылка, Номер", ВыборкаДетальныеЗаписи.Ссылка, ВыборкаДетальныеЗаписи.Номер);
		МассивСЗаписями.Добавить(Структура);
		РегистрыСведений.НомераСправочниковИДокументовДляИзмененияНаКлиентскомУзлеМП.УдалитьЗапись(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	
	Если МассивСЗаписями.Количество() > 0 Тогда
		
		НомерСообщенияОбмена = РегистрыСведений.СформированныеСообщенияСИзмененнымиНомерамиМПУНФ.ПолучитьНомерСообщения();
		
		НаборЗаписей = РегистрыСведений.СформированныеСообщенияСИзмененнымиНомерамиМПУНФ.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Номер.Установить(НомерСообщенияОбмена);
		НаборЗаписей.Отбор.КодУзла.Установить(КодУзла);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Номер = НомерСообщенияОбмена;
		НоваяЗапись.КодУзла = КодУзла;
		НоваяЗапись.Сообщение = Новый ХранилищеЗначения(МассивСЗаписями, Новый СжатиеДанных(9));
		
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать(Истина);
		
	КонецЕсли;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

#КонецОбласти

#Область ТекущаяВерсияПриложения

Функция ПолучитьТекущуюВерсиюПриложения() Экспорт
	
	Возврат "2.0.8.1"
	
КонецФункции

#КонецОбласти
