
#Область ПрограммныйИнтерфейс

// Проверка на возможность отключения настройки КонтролироватьОстаткиПоНомерамГТД.
//
// Возвращаемое значение:
// 	Строка - текст сообщения если изменение настройки невозможно.
//
Функция ОтказИзменитьНастройкуКонтролироватьОстаткиПоНомерамГТД() Экспорт
	
	ТекстОшибки = "";
	
	Если Не МожноВключитьКонтролироватьОстаткиПоНомерамГТД() Тогда
		ТекстОшибки = НСтр("ru='Нельзя изменить значение опции Контроль остатков в разрезе ГТД, потому что в программе зафиксированы отрицательные остатки по ГТД.'");
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Страны ЕАЭС поддерживающие структурированный ввод.
// 
// Возвращаемое значение:
//  Соответствие - Список стран.
//
Функция ТаблицаСтранТаможенногоСоюза(ТолькоСЗаполненнойСсылкой = Истина, ВключаяРоссию = Истина) Экспорт
	
	// По состоянию на 01.01.2017г.:
	//
	// АРМЕНИЯ, код ISO 051
	// БЕЛАРУСЬ, код ISO 112
	// КАЗАХСТАН, код ISO 398
	// КИРГИЗИЯ, код ISO 417
	// РОССИЯ, код ISO 643
	
	СтраныТаможенногоСоюза = Новый ТаблицаЗначений;
	СтраныТаможенногоСоюза.Колонки.Добавить("Код", Новый ОписаниеТипов(Метаданные.Справочники.СтраныМира.СтандартныеРеквизиты.Код.Тип));
	СтраныТаможенногоСоюза.Колонки.Добавить("Наименование", Новый ОписаниеТипов(Метаданные.Справочники.СтраныМира.СтандартныеРеквизиты.Наименование.Тип));
	
	НоваяСтрока = СтраныТаможенногоСоюза.Добавить();
	НоваяСтрока.Наименование = "АРМЕНИЯ";
	НоваяСтрока.Код = "051";
	
	НоваяСтрока = СтраныТаможенногоСоюза.Добавить();
	НоваяСтрока.Наименование = "БЕЛАРУСЬ";
	НоваяСтрока.Код = "112";
	
	НоваяСтрока = СтраныТаможенногоСоюза.Добавить();
	НоваяСтрока.Наименование = "КАЗАХСТАН";
	НоваяСтрока.Код = "398";
	
	НоваяСтрока = СтраныТаможенногоСоюза.Добавить();
	НоваяСтрока.Наименование = "КИРГИЗИЯ";
	НоваяСтрока.Код = "417";
	
	Если ВключаяРоссию Тогда
		
		НоваяСтрока = СтраныТаможенногоСоюза.Добавить();
		НоваяСтрока.Наименование = "РОССИЯ";
		НоваяСтрока.Код = "643";
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтраныТаможенногоСоюза", СтраныТаможенногоСоюза);
	Запрос.Текст = 
	"ВЫБРАТЬ 
	|	СтраныТаможенногоСоюза.Код КАК Код
	|	,СтраныТаможенногоСоюза.Наименование КАК Наименование
	|ПОМЕСТИТЬ КодыСтранТаможенногоСоюза
	|ИЗ &СтраныТаможенногоСоюза КАК СтраныТаможенногоСоюза
	|Индексировать ПО Код
	|
	|;Выбрать 
	|	КодыСтранТаможенногоСоюза.Код КАК Код
	|	,КодыСтранТаможенногоСоюза.Наименование КАК Наименование
	|	,ЕстьNULL(СтраныМира.Ссылка, Неопределено) КАК Ссылка
	|ИЗ КодыСтранТаможенногоСоюза КАК КодыСтранТаможенногоСоюза
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
	|		ПО КодыСтранТаможенногоСоюза.Код = СтраныМира.Код";
	
	Если ТолькоСЗаполненнойСсылкой Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Левое Соединение", "Соединение");
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ВыделитьДатуИзНомераГТД(КодГТД) Экспорт
	
	ПолученнаяДата = Дата(1, 1, 1);
	
	ПозицияПервогоРазделителя	= СтрНайти(КодГТД, "/");
	ДатаГТД						= Прав(КодГТД, СтрДлина(КодГТД) - ПозицияПервогоРазделителя);
	ПозицияВторогоРазделителя	= СтрНайти(ДатаГТД, "/");
	ДатаГТД						= Лев(ДатаГТД, ПозицияВторогоРазделителя - 1);
	
	Если СтрДлина(ДатаГТД) = 6 Тогда
		
		ДеньДаты	= Лев(ДатаГТД, 2);
		МесяцДаты	= Сред(ДатаГТД, 3, 2);
		ГодДаты		= Сред(ДатаГТД, 5, 2);
		
		Попытка
			
			ГодДаты			= ?(Число(ГодДаты) >= 30, "19" + ГодДаты, "20" + ГодДаты);
			ПолученнаяДата	= Дата(ГодДаты, МесяцДаты, ДеньДаты);
			
		Исключение
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат ПолученнаяДата;
	
КонецФункции

Процедура ПодготовитьТаблицуЗапасовИзТаблицыФормы(ПараметрыПодбора, ТаблицаФормы) Экспорт
	
	ТаблицаЗапасы = Новый ТаблицаЗначений;
	
	ТаблицаЗапасы.Колонки.Добавить("ИдентификаторСтроки",	Новый ОписаниеТипов("Число"));
	ТаблицаЗапасы.Колонки.Добавить("Номенклатура", 			Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаЗапасы.Колонки.Добавить("Характеристика",		Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаЗапасы.Колонки.Добавить("Партия",				Новый ОписаниеТипов("СправочникСсылка.ПартииНоменклатуры"));
	
	Для каждого СтрокаТаблицыФормы Из ТаблицаФормы Цикл
		
		Если ПараметрыПодбора.Свойство("ПроверятьПометку")
			И НЕ СтрокаТаблицыФормы.Пометка Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если СтрокаТаблицыФормы.СтранаПроисхождения = Справочники.СтраныМира.Россия Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если ПараметрыПодбора.Свойство("КлючСвязи")
			И СтрокаТаблицыФормы.КлючСвязи <> ПараметрыПодбора.КлючСвязи Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		НоваяСтрока = ТаблицаЗапасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыФормы);
		
		НоваяСтрока.ИдентификаторСтроки = СтрокаТаблицыФормы.ПолучитьИдентификатор();
		
	КонецЦикла;
	
	ПараметрыПодбора.ТаблицаЗапасы = ТаблицаЗапасы;
	
КонецПроцедуры

Процедура ПодобратьНомераГТДПоПредыдущимПоступлениям(ПараметрыПодбора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВременнаяТаблицаЗапасы",	ПараметрыПодбора.ТаблицаЗапасы);
	Если ПолучитьФункциональнуюОпцию("ПередачаТоваровМеждуОрганизациями") 
		И ПараметрыПодбора.Свойство("ПоддержкаИнтеркампани")
		И ПараметрыПодбора.ПоддержкаИнтеркампани = Истина Тогда
		ОтборОрганизации = РегистрыСведений.НастройкаПередачиТоваровМеждуОрганизациями.ПолучитьСписокОрганизацийДляОстатков(
			Константы.УчетПоКомпании.Компания(ПараметрыПодбора.Организация));
	Иначе
		ОтборОрганизации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
			Константы.УчетПоКомпании.Компания(ПараметрыПодбора.Организация));
	КонецЕсли;
	Запрос.УстановитьПараметр("Организации", ОтборОрганизации);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаЗапасы.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия
	|ПОМЕСТИТЬ ТаблицаЗапасы
	|ИЗ
	|	&ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыВРазрезеГТДОбороты.Регистратор.Контрагент КАК Контрагент,
	|	ТаблицаЗапасы.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ЗапасыВРазрезеГТДОбороты.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЗапасыВРазрезеГТДОбороты.НомерГТД КАК НомерГТД,
	|	ЗапасыВРазрезеГТДОбороты.НомерГТД.Код КАК КодГТД,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаГТД
	|ИЗ
	|	ТаблицаЗапасы КАК ТаблицаЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыВРазрезеГТД.Обороты(, , Запись, Организация В (&Организации)) КАК ЗапасыВРазрезеГТДОбороты
	|		ПО ТаблицаЗапасы.Номенклатура = ЗапасыВРазрезеГТДОбороты.Номенклатура
	|			И ТаблицаЗапасы.Характеристика = ЗапасыВРазрезеГТДОбороты.Характеристика
	|			И ТаблицаЗапасы.Партия = ЗапасыВРазрезеГТДОбороты.Партия
	|			И (&УсловиеКонтрагентаЗапасыСобственные)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗапасыПринятыеВРазрезеГТДОбороты.Регистратор.Контрагент,
	|	ТаблицаЗапасы.ИдентификаторСтроки,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.Партия,
	|	ЗапасыПринятыеВРазрезеГТДОбороты.СтранаПроисхождения,
	|	ЗапасыПринятыеВРазрезеГТДОбороты.НомерГТД,
	|	ЗапасыПринятыеВРазрезеГТДОбороты.НомерГТД.Код,
	|	ДАТАВРЕМЯ(1, 1, 1)
	|ИЗ
	|	ТаблицаЗапасы КАК ТаблицаЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыПринятыеВРазрезеГТД.Обороты(, , Запись, Организация В (&Организации)) КАК ЗапасыПринятыеВРазрезеГТДОбороты
	|		ПО ТаблицаЗапасы.Номенклатура = ЗапасыПринятыеВРазрезеГТДОбороты.Номенклатура
	|			И ТаблицаЗапасы.Характеристика = ЗапасыПринятыеВРазрезеГТДОбороты.Характеристика
	|			И ТаблицаЗапасы.Партия = ЗапасыПринятыеВРазрезеГТДОбороты.Партия
	|			И (&УсловиеКонтрагентаЗапасыПринятые)";
	
	Если ПараметрыПодбора.Свойство("Контрагент") Тогда 
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеКонтрагентаЗапасыСобственные", "(ЗапасыВРазрезеГТДОбороты.Регистратор.Контрагент = &Контрагент)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеКонтрагентаЗапасыПринятые", "(ЗапасыПринятыеВРазрезеГТДОбороты.Регистратор.Контрагент = &Контрагент)");
		Запрос.УстановитьПараметр("Контрагент", ПараметрыПодбора.Контрагент);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеКонтрагентаЗапасыСобственные", "Истина");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеКонтрагентаЗапасыПринятые", "Истина");
		
	КонецЕсли;
	
	ОборотыЗапасовВРазрезеГТД = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТаблицы Из ОборотыЗапасовВРазрезеГТД Цикл
		
		СтрокаТаблицы.ДатаГТД = ВыделитьДатуИзНомераГТД(СтрокаТаблицы.КодГТД);
		
	КонецЦикла;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаЗапасы.Контрагент КАК Контрагент
	|	,ТаблицаЗапасы.ИдентификаторСтроки
	|	,ТаблицаЗапасы.Номенклатура
	|	,ТаблицаЗапасы.Характеристика
	|	,ТаблицаЗапасы.Партия
	|	,ТаблицаЗапасы.СтранаПроисхождения
	|	,ТаблицаЗапасы.НомерГТД
	|	,ТаблицаЗапасы.ДатаГТД КАК ДатаГТД
	|ПОМЕСТИТЬ ТаблицаЗапасыСЗаполненнымиДатами
	|ИЗ
	|	&ОборотыЗапасовВРазрезеГТД КАК ТаблицаЗапасы;
	|
	|//:::::::::::::::::::::::::::::::::::::::::::::::::::::::
	|//::::::: Выделим ГТД с максимально свежей датой ::::::::
	|//:::::::::::::::::::::::::::::::::::::::::::::::::::::::
	|ВЫБРАТЬ 
	|	ТаблицаЗапасыСЗаполненнымиДатами.Контрагент
	|	,ТаблицаЗапасыСЗаполненнымиДатами.ИдентификаторСтроки
	|	,ТаблицаЗапасыСЗаполненнымиДатами.Номенклатура
	|	,ТаблицаЗапасыСЗаполненнымиДатами.Характеристика
	|	,ТаблицаЗапасыСЗаполненнымиДатами.Партия
	|	,ТаблицаЗапасыСЗаполненнымиДатами.СтранаПроисхождения
	|	,Максимум(ТаблицаЗапасыСЗаполненнымиДатами.НомерГТД)
	|	,Максимум(ТаблицаЗапасыСЗаполненнымиДатами.ДатаГТД)
	|ИЗ ТаблицаЗапасыСЗаполненнымиДатами КАК ТаблицаЗапасыСЗаполненнымиДатами
	|ГДЕ
	|	НЕ ТаблицаЗапасыСЗаполненнымиДатами.НомерГТД ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыСЗаполненнымиДатами.Контрагент
	|	,ТаблицаЗапасыСЗаполненнымиДатами.ИдентификаторСтроки
	|	,ТаблицаЗапасыСЗаполненнымиДатами.Номенклатура
	|	,ТаблицаЗапасыСЗаполненнымиДатами.Характеристика
	|	,ТаблицаЗапасыСЗаполненнымиДатами.Партия
	|	,ТаблицаЗапасыСЗаполненнымиДатами.СтранаПроисхождения";
	
	Запрос.УстановитьПараметр("ОборотыЗапасовВРазрезеГТД", ОборотыЗапасовВРазрезеГТД);
	ПараметрыПодбора.ТаблицаЗапасы = Запрос.Выполнить().Выгрузить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура СформироватьОстаткиНомеровГТД(ПараметрыПодбора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия
	|ПОМЕСТИТЬ ТаблицаЗапасы
	|ИЗ
	|	&ТаблицаЗапасы КАК ТаблицаЗапасы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыВРазрезеГТДОстатки.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЗапасыВРазрезеГТДОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ЗапасыВРазрезеГТДОстатки.НомерГТД КАК НомерГТД,
	|	ЗапасыВРазрезеГТДОстатки.НомерГТД.Код КАК КодГТД,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаГТД
	|ПОМЕСТИТЬ ОстаткиСУчетомТекущегоДокумента
	|ИЗ
	|	ТаблицаЗапасы КАК ТаблицаЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыВРазрезеГТД.Остатки(, Организация В (&Организации)) КАК ЗапасыВРазрезеГТДОстатки
	|		ПО ТаблицаЗапасы.Номенклатура = ЗапасыВРазрезеГТДОстатки.Номенклатура
	|			И ТаблицаЗапасы.Характеристика = ЗапасыВРазрезеГТДОстатки.Характеристика
	|			И ТаблицаЗапасы.Партия = ЗапасыВРазрезеГТДОстатки.Партия
	|ГДЕ
	|	ЗапасыВРазрезеГТДОстатки.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗапасыПринятыеВРазрезеГТДОстатки.СтранаПроисхождения,
	|	ЗапасыПринятыеВРазрезеГТДОстатки.КоличествоОстаток,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.Партия,
	|	ЗапасыПринятыеВРазрезеГТДОстатки.НомерГТД,
	|	ЗапасыПринятыеВРазрезеГТДОстатки.НомерГТД.Код,
	|	ДАТАВРЕМЯ(1, 1, 1)
	|ИЗ
	|	ТаблицаЗапасы КАК ТаблицаЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыПринятыеВРазрезеГТД.Остатки(, Организация В (&Организации)) КАК ЗапасыПринятыеВРазрезеГТДОстатки
	|		ПО ТаблицаЗапасы.Номенклатура = ЗапасыПринятыеВРазрезеГТДОстатки.Номенклатура
	|			И ТаблицаЗапасы.Характеристика = ЗапасыПринятыеВРазрезеГТДОстатки.Характеристика
	|			И ТаблицаЗапасы.Партия = ЗапасыПринятыеВРазрезеГТДОстатки.Партия
	|ГДЕ
	|	ЗапасыПринятыеВРазрезеГТДОстатки.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущийДокумент.СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ТекущийДокумент.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ЕСТЬNULL(ТекущийДокумент.Количество, 0)
	|		ИНАЧЕ -ЕСТЬNULL(ТекущийДокумент.Количество, 0)
	|	КОНЕЦ,
	|	ТекущийДокумент.Номенклатура,
	|	ТекущийДокумент.Характеристика,
	|	ТекущийДокумент.Партия,
	|	ТекущийДокумент.НомерГТД,
	|	ТекущийДокумент.НомерГТД.Код,
	|	ДАТАВРЕМЯ(1, 1, 1)
	|ИЗ
	|	РегистрНакопления.ЗапасыВРазрезеГТД КАК ТекущийДокумент
	|ГДЕ
	|	ТекущийДокумент.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиЗапасовПоРегиструГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(ОстаткиЗапасовПоРегиструГТД.КоличествоОстаток) КАК КоличествоОстаток,
	|	ОстаткиЗапасовПоРегиструГТД.Номенклатура КАК Номенклатура,
	|	ОстаткиЗапасовПоРегиструГТД.Характеристика КАК Характеристика,
	|	ОстаткиЗапасовПоРегиструГТД.Партия КАК Партия,
	|	ОстаткиЗапасовПоРегиструГТД.НомерГТД КАК НомерГТД,
	|	ОстаткиЗапасовПоРегиструГТД.НомерГТД.Код КАК КодГТД,
	|	ОстаткиЗапасовПоРегиструГТД.ДатаГТД КАК ДатаГТД
	|ИЗ
	|	ОстаткиСУчетомТекущегоДокумента КАК ОстаткиЗапасовПоРегиструГТД
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиЗапасовПоРегиструГТД.СтранаПроисхождения,
	|	ОстаткиЗапасовПоРегиструГТД.Номенклатура,
	|	ОстаткиЗапасовПоРегиструГТД.Характеристика,
	|	ОстаткиЗапасовПоРегиструГТД.Партия,
	|	ОстаткиЗапасовПоРегиструГТД.НомерГТД,
	|	ОстаткиЗапасовПоРегиструГТД.НомерГТД.Код,
	|	ОстаткиЗапасовПоРегиструГТД.ДатаГТД");
	
	Запрос.УстановитьПараметр("Период", ПараметрыПодбора.Дата);
	Запрос.УстановитьПараметр("Ссылка", ПараметрыПодбора.Ссылка);
	Запрос.УстановитьПараметр("ТаблицаЗапасы", ПараметрыПодбора.ТаблицаЗапасы);
	Если ПолучитьФункциональнуюОпцию("ПередачаТоваровМеждуОрганизациями") 
		И ПараметрыПодбора.Свойство("ПоддержкаИнтеркампани")
		И ПараметрыПодбора.ПоддержкаИнтеркампани = Истина Тогда
		ОтборОрганизации = РегистрыСведений.НастройкаПередачиТоваровМеждуОрганизациями.ПолучитьСписокОрганизацийДляОстатков(
			Константы.УчетПоКомпании.Компания(ПараметрыПодбора.Организация));
	Иначе
		ОтборОрганизации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
			Константы.УчетПоКомпании.Компания(ПараметрыПодбора.Организация));
	КонецЕсли; 
	Запрос.УстановитьПараметр("Организации", ОтборОрганизации);
	ОстаткиПоГТД = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаТаблицы Из ОстаткиПоГТД Цикл
		
		СтрокаТаблицы.ДатаГТД = ВыделитьДатуИзНомераГТД(СтрокаТаблицы.КодГТД);
		
	КонецЦикла; 
	
	ОстаткиПоГТД.Сортировать("Номенклатура, Характеристика, Партия, ДатаГТД");
	
	ПараметрыПодбора.ОстаткиПоГТД = ОстаткиПоГТД;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура СформироватьОстаткиПереданныеНомеровГТД(ПараметрыПодбора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия
	|ПОМЕСТИТЬ ТаблицаЗапасы
	|ИЗ
	|	&ТаблицаЗапасы КАК ТаблицаЗапасы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыПереданныеВРазрезеГТДОстатки.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЗапасыПереданныеВРазрезеГТДОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ЗапасыПереданныеВРазрезеГТДОстатки.НомерГТД КАК НомерГТД,
	|	ЗапасыПереданныеВРазрезеГТДОстатки.НомерГТД.Код КАК КодГТД,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаГТД
	|ПОМЕСТИТЬ ОстаткиБезУчетаТекущегоДокумента
	|ИЗ
	|	ТаблицаЗапасы КАК ТаблицаЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыПереданныеВРазрезеГТД.Остатки(, Организация = &Организация) КАК ЗапасыПереданныеВРазрезеГТДОстатки
	|		ПО ТаблицаЗапасы.Номенклатура = ЗапасыПереданныеВРазрезеГТДОстатки.Номенклатура
	|			И ТаблицаЗапасы.Характеристика = ЗапасыПереданныеВРазрезеГТДОстатки.Характеристика
	|			И ТаблицаЗапасы.Партия = ЗапасыПереданныеВРазрезеГТДОстатки.Партия
	|ГДЕ
	|	ЗапасыПереданныеВРазрезеГТДОстатки.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущийДокумент.СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ТекущийДокумент.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЕСТЬNULL(ТекущийДокумент.Количество, 0)
	|		ИНАЧЕ -ЕСТЬNULL(ТекущийДокумент.Количество, 0)
	|	КОНЕЦ,
	|	ТекущийДокумент.Номенклатура,
	|	ТекущийДокумент.Характеристика,
	|	ТекущийДокумент.Партия,
	|	ТекущийДокумент.НомерГТД,
	|	ТекущийДокумент.НомерГТД.Код,
	|	ДАТАВРЕМЯ(1, 1, 1)
	|ИЗ
	|	РегистрНакопления.ЗапасыПереданныеВРазрезеГТД КАК ТекущийДокумент
	|ГДЕ
	|	ТекущийДокумент.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиЗапасовПереданныхПоРегиструГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(ОстаткиЗапасовПереданныхПоРегиструГТД.КоличествоОстаток) КАК КоличествоОстаток,
	|	ОстаткиЗапасовПереданныхПоРегиструГТД.Номенклатура КАК Номенклатура,
	|	ОстаткиЗапасовПереданныхПоРегиструГТД.Характеристика КАК Характеристика,
	|	ОстаткиЗапасовПереданныхПоРегиструГТД.Партия КАК Партия,
	|	ОстаткиЗапасовПереданныхПоРегиструГТД.НомерГТД КАК НомерГТД,
	|	ОстаткиЗапасовПереданныхПоРегиструГТД.НомерГТД.Код КАК КодГТД,
	|	ОстаткиЗапасовПереданныхПоРегиструГТД.ДатаГТД КАК ДатаГТД
	|ИЗ
	|	ОстаткиБезУчетаТекущегоДокумента КАК ОстаткиЗапасовПереданныхПоРегиструГТД
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиЗапасовПереданныхПоРегиструГТД.СтранаПроисхождения,
	|	ОстаткиЗапасовПереданныхПоРегиструГТД.Номенклатура,
	|	ОстаткиЗапасовПереданныхПоРегиструГТД.Характеристика,
	|	ОстаткиЗапасовПереданныхПоРегиструГТД.Партия,
	|	ОстаткиЗапасовПереданныхПоРегиструГТД.НомерГТД,
	|	ОстаткиЗапасовПереданныхПоРегиструГТД.НомерГТД.Код,
	|	ОстаткиЗапасовПереданныхПоРегиструГТД.ДатаГТД");
	
	Запрос.УстановитьПараметр("Период", ПараметрыПодбора.Дата);
	Запрос.УстановитьПараметр("Ссылка", ПараметрыПодбора.Ссылка);
	Запрос.УстановитьПараметр("ТаблицаЗапасы", ПараметрыПодбора.ТаблицаЗапасы);
	Запрос.УстановитьПараметр("Организация", Константы.УчетПоКомпании.Компания(ПараметрыПодбора.Организация));
	ОстаткиПоГТД = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаТаблицы Из ОстаткиПоГТД Цикл
		
		СтрокаТаблицы.ДатаГТД = ВыделитьДатуИзНомераГТД(СтрокаТаблицы.КодГТД);
		
	КонецЦикла; 
	
	ОстаткиПоГТД.Сортировать("Номенклатура, Характеристика, Партия, ДатаГТД");
	
	ПараметрыПодбора.ОстаткиПоГТД = ОстаткиПоГТД;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура СформироватьОстаткиПринятыеНомеровГТД(ПараметрыПодбора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия
	|ПОМЕСТИТЬ ТаблицаЗапасы
	|ИЗ
	|	&ТаблицаЗапасы КАК ТаблицаЗапасы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыПринятыеВРазрезеГТДОстатки.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЗапасыПринятыеВРазрезеГТДОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ЗапасыПринятыеВРазрезеГТДОстатки.НомерГТД КАК НомерГТД,
	|	ЗапасыПринятыеВРазрезеГТДОстатки.НомерГТД.Код КАК КодГТД,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаГТД
	|ПОМЕСТИТЬ ОстаткиБезУчетаТекущегоДокумента
	|ИЗ
	|	ТаблицаЗапасы КАК ТаблицаЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыПринятыеВРазрезеГТД.Остатки(, Организация = &Организация) КАК ЗапасыПринятыеВРазрезеГТДОстатки
	|		ПО ТаблицаЗапасы.Номенклатура = ЗапасыПринятыеВРазрезеГТДОстатки.Номенклатура
	|			И ТаблицаЗапасы.Характеристика = ЗапасыПринятыеВРазрезеГТДОстатки.Характеристика
	|			И ТаблицаЗапасы.Партия = ЗапасыПринятыеВРазрезеГТДОстатки.Партия
	|ГДЕ
	|	ЗапасыПринятыеВРазрезеГТДОстатки.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущийДокумент.СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ТекущийДокумент.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЕСТЬNULL(ТекущийДокумент.Количество, 0)
	|		ИНАЧЕ -ЕСТЬNULL(ТекущийДокумент.Количество, 0)
	|	КОНЕЦ,
	|	ТекущийДокумент.Номенклатура,
	|	ТекущийДокумент.Характеристика,
	|	ТекущийДокумент.Партия,
	|	ТекущийДокумент.НомерГТД,
	|	ТекущийДокумент.НомерГТД.Код,
	|	ДАТАВРЕМЯ(1, 1, 1)
	|ИЗ
	|	РегистрНакопления.ЗапасыПринятыеВРазрезеГТД КАК ТекущийДокумент
	|ГДЕ
	|	ТекущийДокумент.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиЗапасовПоРегиструГТД.СтранаПроисхождения,
	|	СУММА(ОстаткиЗапасовПоРегиструГТД.КоличествоОстаток) КАК КоличествоОстаток,
	|	ОстаткиЗапасовПоРегиструГТД.Номенклатура,
	|	ОстаткиЗапасовПоРегиструГТД.Характеристика,
	|	ОстаткиЗапасовПоРегиструГТД.Партия,
	|	ОстаткиЗапасовПоРегиструГТД.НомерГТД,
	|	ОстаткиЗапасовПоРегиструГТД.НомерГТД.Код КАК КодГТД,
	|	ОстаткиЗапасовПоРегиструГТД.ДатаГТД КАК ДатаГТД
	|ИЗ
	|	ОстаткиБезУчетаТекущегоДокумента КАК ОстаткиЗапасовПоРегиструГТД
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиЗапасовПоРегиструГТД.СтранаПроисхождения,
	|	ОстаткиЗапасовПоРегиструГТД.Номенклатура,
	|	ОстаткиЗапасовПоРегиструГТД.Характеристика,
	|	ОстаткиЗапасовПоРегиструГТД.Партия,
	|	ОстаткиЗапасовПоРегиструГТД.НомерГТД,
	|	ОстаткиЗапасовПоРегиструГТД.НомерГТД.Код,
	|	ОстаткиЗапасовПоРегиструГТД.ДатаГТД");
	
	Запрос.УстановитьПараметр("Период", ПараметрыПодбора.Дата);
	Запрос.УстановитьПараметр("Ссылка", ПараметрыПодбора.Ссылка);
	Запрос.УстановитьПараметр("ТаблицаЗапасы", ПараметрыПодбора.ТаблицаЗапасы);
	Запрос.УстановитьПараметр("Организация", Константы.УчетПоКомпании.Компания(ПараметрыПодбора.Организация));
	ОстаткиПоГТД = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаТаблицы Из ОстаткиПоГТД Цикл
		
		СтрокаТаблицы.ДатаГТД = ВыделитьДатуИзНомераГТД(СтрокаТаблицы.КодГТД);
		
	КонецЦикла; 
	
	ОстаткиПоГТД.Сортировать("Номенклатура, Характеристика, Партия, ДатаГТД");
	
	ПараметрыПодбора.ОстаткиПоГТД = ОстаткиПоГТД;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ПеренестиНомераГТДВТаблицуФормы(ТаблицаФормы, ПараметрыПодбора) Экспорт
	
	Для каждого СтрокаЗапасов Из ПараметрыПодбора.ТаблицаЗапасы Цикл
		
		СтрокаТаблицыФормы = ТаблицаФормы.НайтиПоИдентификатору(СтрокаЗапасов.ИдентификаторСтроки);
		
		СтрокаТаблицыФормы.СтранаПроисхождения = СтрокаЗапасов.СтранаПроисхождения;
		СтрокаТаблицыФормы.НомерГТД = СтрокаЗапасов.НомерГТД;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПеренестиОстаткиНомеровГТДВТаблицуФормы(ТаблицаФормы, ПараметрыПодбора) Экспорт
	
	БылиТоварыСГТД = Ложь;
	ГТДНеХватило = Ложь;
	МожетПонадобитьсяВторойПеренос = Ложь;
	
	Если ТаблицаФормы.Количество() < 1 Тогда
		
		ПараметрыПодбора.ИндексТекущейСтроки = -1;
		Возврат МожетПонадобитьсяВторойПеренос;
		
	КонецЕсли;
	
	ТаблицаЗапасы = ТаблицаФормы.Выгрузить();
	ТаблицаЗапасы.Очистить();
	
	КоличественныеПоля = Новый Массив;
	КоличественныеПоля.Добавить("Резерв");
	
	СтруктураОтбора = Новый Структура("Номенклатура, Характеристика, Партия");
	
	ЕстьКолонкаЕдиницаИзмерения = (ТаблицаЗапасы.Колонки.Найти("ЕдиницаИзмерения") = Неопределено);
	
	КлючСвязиАвтоматическихСкидок	= Неопределено;
	НовыйИндексТекущейСтроки		= -1;
	Для каждого СтрокаТЧ Из ТаблицаФормы Цикл
		
		Если ПараметрыПодбора.Свойство("ПропуститьВозвраты")
			И СтрокаТЧ.Количество < 0 Тогда
			
			ЗаполнитьЗначенияСвойств(ТаблицаЗапасы.Добавить(), СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		// В ОРП возможен такой сценарий:
		// Продали товар с ГТД, вернули этот товар и опять продали этот же товар.
		// Если это произошло в рамках одной смены, то потребуется 2 раза вызвать текущую функцию.
		// 1-й раз - распределяем остатки ГТД по проданным товарам.
		// Промежуточная обработка - распределяем проданные ГТД, которые вычислили при первом вызове, на возвраты (в другой процедуре).
		// 2-й раз - распределяем ГТД возвратов на те товары, для которых не хватило ГТД при первом проходе.
		Если ПараметрыПодбора.Свойство("ВторойПеренос")
			И ПараметрыПодбора.ВторойПеренос
			И (СтрокаТЧ.Количество < 0 Или Не СтрокаТЧ.НомерГТД.Пустая()) Тогда
			
			ЗаполнитьЗначенияСвойств(ТаблицаЗапасы.Добавить(), СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		Если ПараметрыПодбора.Свойство("ПроверятьПометку")
			И НЕ СтрокаТЧ.Пометка Тогда
			
			ЗаполнитьЗначенияСвойств(ТаблицаЗапасы.Добавить(), СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		Если ПараметрыПодбора.Свойство("КлючСвязи")
			И НЕ СтрокаТЧ.КлючСвязи = ПараметрыПодбора.КлючСвязи Тогда
			
			ЗаполнитьЗначенияСвойств(ТаблицаЗапасы.Добавить(), СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		Если ПараметрыПодбора.Свойство("ЕстьКлючСвязиАвтоматическихСкидок") Тогда
			
			КлючСвязиАвтоматическихСкидок = СтрокаТЧ.КлючСвязи;
			
		КонецЕсли;
		
		ЭтоТекущаяСтрока = (ТаблицаФормы.Индекс(СтрокаТЧ) = ПараметрыПодбора.ИндексТекущейСтроки);
		
		СтруктураИтогов = Новый Структура;
		Для каждого ИмяПоля Из ПараметрыПодбора.ИменаПолей Цикл
			
			СтруктураИтогов.Вставить(ИмяПоля, СтрокаТЧ[ИмяПоля]);
			
		КонецЦикла;
		
		СтруктураОтбора.Номенклатура	= СтрокаТЧ.Номенклатура;
		СтруктураОтбора.Характеристика	= СтрокаТЧ.Характеристика;
		СтруктураОтбора.Партия			= СтрокаТЧ.Партия;
		
		МассивСтрокГТД		= ПараметрыПодбора.ОстаткиПоГТД.НайтиСтроки(СтруктураОтбора);
		КоличествоОстаток	= СтрокаТЧ.Количество;
		
		КоэффициентПользовательскойЕдиницыИзмерения = 1;
		Если ЕстьКолонкаЕдиницаИзмерения
			И ТипЗнч(СтрокаТЧ.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
			
			КоэффициентПользовательскойЕдиницыИзмерения = СтрокаТЧ.ЕдиницаИзмерения.Коэффициент;
			
		КонецЕсли;
		
		Для каждого СтрокаМассива Из МассивСтрокГТД Цикл
			
			БылиТоварыСГТД = Истина;
			
			НоваяСтрока = ТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
			
			Если ПараметрыПодбора.Свойство("ЕстьКлючСвязиАвтоматическихСкидок") Тогда
				
				НоваяСтрока.КлючСвязи = КлючСвязиАвтоматическихСкидок;
				Если КлючСвязиАвтоматическихСкидок <> Неопределено Тогда
					
					КлючСвязиАвтоматическихСкидок = Неопределено;
					
				КонецЕсли;
				
			КонецЕсли;
			
			НоваяСтрока.НомерГТД			= СтрокаМассива.НомерГТД;
			НоваяСтрока.СтранаПроисхождения	= СтрокаМассива.СтранаПроисхождения;
			
			Если (КоличествоОстаток * КоэффициентПользовательскойЕдиницыИзмерения) <= СтрокаМассива.КоличествоОстаток Тогда
				
				НоваяСтрока.Количество			= КоличествоОстаток;
				СтрокаМассива.КоличествоОстаток	= СтрокаМассива.КоличествоОстаток - (КоличествоОстаток * КоэффициентПользовательскойЕдиницыИзмерения);
				КоличествоОстаток				= 0;
				
				Если СтрокаМассива.КоличествоОстаток = 0 Тогда
					
					ПараметрыПодбора.ОстаткиПоГТД.Удалить(СтрокаМассива);
					
				КонецЕсли;
				
				Для каждого ИмяПоля Из ПараметрыПодбора.ИменаПолей Цикл
					
					НоваяСтрока[ИмяПоля] = СтруктураИтогов[ИмяПоля]
					
				КонецЦикла;
				
				Прервать;
				
			Иначе
				
				КоличествоСУчетомКоэффициента = ОКР(СтрокаМассива.КоличествоОстаток / КоэффициентПользовательскойЕдиницыИзмерения, 3);
				
				НоваяСтрока.Количество	= КоличествоСУчетомКоэффициента;
				
				Если ПараметрыПодбора.ИменаПолей.Количество() > 0 Тогда
					
					МножительПоКоэффициенту = (КоличествоСУчетомКоэффициента/СтрокаТЧ.Количество);
					Для каждого ИмяПоля Из ПараметрыПодбора.ИменаПолей Цикл
						
						Если КоличественныеПоля.Найти(ИмяПоля) = Неопределено Тогда
							
							НоваяСтрока[ИмяПоля] = Окр(НоваяСтрока[ИмяПоля] * МножительПоКоэффициенту, 3);
							
						Иначе
							
							НоваяСтрока[ИмяПоля] = МИН(НоваяСтрока.Количество, НоваяСтрока[ИмяПоля]);
							
						КонецЕсли;
						
						СтруктураИтогов[ИмяПоля] = СтруктураИтогов[ИмяПоля] - НоваяСтрока[ИмяПоля];
						
					КонецЦикла;
					
				КонецЕсли;
				
				КоличествоОстаток		= КоличествоОстаток - КоличествоСУчетомКоэффициента;
				ПараметрыПодбора.ОстаткиПоГТД.Удалить(СтрокаМассива);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если КоличествоОстаток > 0 Тогда
			
			ГТДНеХватило = Истина;
			
			НоваяСтрока				= ТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
			
			НоваяСтрока.Количество			= КоличествоОстаток;
			НоваяСтрока.НомерГТД			= Справочники.НомераГТД.ПустаяСсылка();
			НоваяСтрока.СтранаПроисхождения = СтрокаТЧ.СтранаПроисхождения;
			
			Для каждого ИмяПоля Из ПараметрыПодбора.ИменаПолей Цикл
				
				НоваяСтрока[ИмяПоля] = СтруктураИтогов[ИмяПоля];
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если ЭтоТекущаяСтрока Тогда
			
			НовыйИндексТекущейСтроки = ТаблицаЗапасы.Индекс(НоваяСтрока);
			ЭтоТекущаяСтрока = Ложь;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаФормы.Загрузить(ТаблицаЗапасы);
	
	Если НовыйИндексТекущейСтроки <> -1 Тогда
		
		ПараметрыПодбора.ИндексТекущейСтроки = НовыйИндексТекущейСтроки;
		
	КонецЕсли;
	
	МожетПонадобитьсяВторойПеренос = БылиТоварыСГТД И ГТДНеХватило;
	Возврат МожетПонадобитьсяВторойПеренос;
	
КонецФункции

Процедура ОчиститьНомераГТДИСтраныПроисхождения(ТаблицаФормы, КлючСвязи = -1, ПроверятьПометку = Истина) Экспорт
	
	Для каждого СтрокаТаблицы Из ТаблицаФормы Цикл
		
		Если ПроверятьПометку
			И НЕ СтрокаТаблицы.Пометка Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если КлючСвязи >= 0
			И СтрокаТаблицы.КлючСвязи <> КлючСвязи Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если СтрокаТаблицы.Свойство("СтранаПроисхождения") Тогда
			
			СтрокаТаблицы.СтранаПроисхождения = Неопределено;
			
		КонецЕсли;
		
		Если СтрокаТаблицы.Свойство("НомерГТД") Тогда
			
			СтрокаТаблицы.НомерГТД = Неопределено;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСписокДействий(Действия, ЭтоДокументРасхода = Истина, ИмяТЧ = "") Экспорт
	
	Если ПолучитьФункциональнуюОпцию("УчетГТД") Тогда
		
		Если ЭтоДокументРасхода Тогда
			
			Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ЗаполнитьПоФактическимОстаткамНомераГТД);
			
		КонецЕсли;
		
		Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ПодобратьНомераГТД);
		Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ОчиститьНомераГТДИСтраныПроисхождения);
		
	КонецЕсли;
	
КонецПроцедуры

Функция НеобходимоОтразитьСчетФактуройЗапасыВРазрезеГТД(СчетФактураСсылка) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1 ЗапасыГТД.Регистратор ИЗ РегистрНакопления.ЗапасыВРазрезеГТД КАК ЗапасыГТД ГДЕ ЗапасыГТД.Регистратор В(&МассивСсылок)");
	Запрос.УстановитьПараметр("МассивСсылок", СчетФактураСсылка.ДокументыОснования.Выгрузить(,"ДокументОснование"));
	РезультатЗапроса = Запрос.Выполнить();
	
	НеобходимоОтразить = Константы.ФункциональнаяОпцияУчетГТД.Получить() И (НачалоДня(Константы.ДатаОбновленияНаРелиз_1_6_6.Получить()) < СчетФактураСсылка.Дата);
	
	Возврат НеобходимоОтразить И РезультатЗапроса.Пустой();
	
КонецФункции

Функция МожноВключитьКонтролироватьОстаткиПоНомерамГТД() Экспорт
	
	Запрос			= Новый Запрос;
	Запрос.Текст	= "ВЫБРАТЬ ПЕРВЫЕ 1 СпрНомераГТД.НомерГТД ИЗ РегистрНакопления.ЗапасыВРазрезеГТД.Остатки КАК СпрНомераГТД ГДЕ СпрНомераГТД.КоличествоОстаток < 0";
	Выборка			= Запрос.Выполнить().Выбрать();
	
	Возврат НЕ Выборка.Следующий();
	
КонецФункции

Функция ИменаТЧИПолейДляТаблицыЗапасы_БазовыеПоляСПрослеживаемостью(пИменаТЧИПолей = Неопределено, пИмяТЧ = "Запасы") Экспорт
	
	ИменаТЧСтруктура = Новый Структура;
	ИменаТЧСтруктура.Вставить("ИмяПоляПроверки", "Объект."+пИмяТЧ+".СтранаПроисхождения");
	ИменаТЧСтруктура.Вставить("ИмяПоляПрослеживаемость", "Объект."+пИмяТЧ+".ПрослеживаемыйТовар");
	ИменаТЧСтруктура.Вставить("ИмяПоляОформления", пИмяТЧ + "РНПТ");
	ИменаТЧСтруктура.Вставить("ИмяПоляНомерГТД", пИмяТЧ + "НомерГТД");
	ИменаТЧСтруктура.Вставить("ИмяГруппыРНПТ", пИмяТЧ + "ГруппаРНПТ");
	ИменаТЧСтруктура.Вставить("ИмяПоляРНПТ", "Объект."+пИмяТЧ+".РНПТ");
	
	Если пИменаТЧИПолей = Неопределено Тогда
		ИменаТЧИПолей = Новый Массив;
	Иначе
		ИменаТЧИПолей = пИменаТЧИПолей;
	КонецЕсли;
	ИменаТЧИПолей.Добавить(ИменаТЧСтруктура);
	
	Возврат ИменаТЧИПолей;
	
КонецФункции

Процедура ПриСозданииНаСервере(ЭтаФорма, ИменаТЧ, КэшЗначений) Экспорт
	
	Если ТипЗнч(КэшЗначений) <> Тип("Структура") Тогда
		
		КэшЗначений = Новый Структура;
		
	КонецЕсли;
	
	КэшЗначений.Вставить("УчетГТД", ПолучитьФункциональнуюОпцию("УчетГТД"));
	КэшЗначений.Вставить("Россия", Справочники.СтраныМира.Россия);
	
	Для каждого СтруктураПараметров Из ИменаТЧ Цикл
		
		ИмяПоляПроверки = СтруктураПараметров.ИмяПоляПроверки;
		ИмяПоляОформления = СтруктураПараметров.ИмяПоляОформления;
		
		//(1)
		ДоступенНомерГТД = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
		ДоступенНомерГТД.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Ложь);
		
		ГруппаОтбора = ДоступенНомерГТД.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, ИмяПоляПроверки, ВидСравненияКомпоновкиДанных.Заполнено);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, ИмяПоляПроверки, ВидСравненияКомпоновкиДанных.НеРавно, Справочники.СтраныМира.Россия);
		
		Если СтруктураПараметров.Свойство("ДополнительноеПолеПроверки") Тогда
			
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, СтруктураПараметров.ДополнительноеПолеПроверки, ВидСравненияКомпоновкиДанных.Равно, Ложь);
			
		КонецЕсли;
		
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ДоступенНомерГТД.Поля, ИмяПоляОформления);
		
		//(2)
		НедоступенНомерГТД = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
		НедоступенНомерГТД.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		
		ГруппаОтбора = НедоступенНомерГТД.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, ИмяПоляПроверки, ВидСравненияКомпоновкиДанных.НеЗаполнено);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, ИмяПоляПроверки, ВидСравненияКомпоновкиДанных.Равно, Справочники.СтраныМира.Россия);
		
		Если СтруктураПараметров.Свойство("ДополнительноеПолеПроверки") Тогда
			
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, СтруктураПараметров.ДополнительноеПолеПроверки, ВидСравненияКомпоновкиДанных.Равно, Истина);
			
		КонецЕсли;
		
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(НедоступенНомерГТД.Поля, ИмяПоляОформления);
		
	КонецЦикла;
	
КонецПроцедуры

#Область Прослеживаемость

Процедура ПриСозданииНаСервереДляДокументаСПрослеживаемостью(ЭтаФорма, ИменаТЧ, КэшЗначений) Экспорт
	
	Если ТипЗнч(КэшЗначений) <> Тип("Структура") Тогда
		КэшЗначений = Новый Структура;
	КонецЕсли;
	Если Не КэшЗначений.Свойство("ВестиУчетПрослеживаемыхТоваров") Тогда
		ПрослеживаемостьФормыУНФ.ПриСозданииФормы(ЭтаФорма, КэшЗначений);
	КонецЕсли;
	
	КэшЗначений.Вставить("УчетГТД", ПолучитьФункциональнуюОпцию("УчетГТД"));
	КэшЗначений.Вставить("Россия", Справочники.СтраныМира.Россия);
	
	Для каждого СтруктураПараметров Из ИменаТЧ Цикл
		Если КэшЗначений.ВестиУчетПрослеживаемыхТоваров
			Или Не КэшЗначений.УчетГТД Тогда
			Если СтруктураПараметров.Свойство("ИмяПоляНомерГТД") Тогда
				ЭтаФорма.Элементы[СтруктураПараметров.ИмяПоляНомерГТД].Видимость = Ложь;
			КонецЕсли;
		КонецЕсли;
		Если Не КэшЗначений.ВестиУчетПрослеживаемыхТоваров
			Или Не КэшЗначений.УчетГТД Тогда
			Если СтруктураПараметров.Свойство("ИмяГруппыРНПТ") Тогда
				ЭтаФорма.Элементы[СтруктураПараметров.ИмяГруппыРНПТ].Видимость = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если КэшЗначений.Свойство("РеализацияВЕАЭС") Тогда
		
		// КодТНВЭД
		// Видимость
		НовоеУсловноеОформление = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
		
		ГруппаИли = РаботаСФормой.ДобавитьГруппуЭлементовОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "ГруппаИЛИ");
		
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(ГруппаИли,
			"РеализацияВЕАЭС", Ложь, ВидСравненияКомпоновкиДанных.Равно);
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(
			ГруппаИли,
			"Объект.НалогообложениеНДС",
			ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.НеОблагаетсяНДС"),
			ВидСравненияКомпоновкиДанных.Равно);
			
		РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ЗапасыКодТНВЭД");
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Видимость", Ложь);
		
		// Отметка незаполненного
		НовоеУсловноеОформление = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
		
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор,
			"РеализацияВЕАЭС", Истина, ВидСравненияКомпоновкиДанных.Равно);
		
		ГруппаИли = РаботаСФормой.ДобавитьГруппуЭлементовОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "ГруппаИЛИ");
		
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(
			ГруппаИли,
			"Объект.Запасы.СтавкаНДС",
			Справочники.СтавкиНДС.СтавкаНДС(ПредопределенноеЗначение("Перечисление.ВидыСтавокНДС.Нулевая"), ЭтаФорма.Объект.Дата),
			ВидСравненияКомпоновкиДанных.НеРавно);
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(
			ГруппаИли,
			"Объект.Запасы.ТипНоменклатурыЗапас",
			Ложь,
			ВидСравненияКомпоновкиДанных.Равно);
			
		РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "ЗапасыКодТНВЭД");
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Ложь);
		// Конец КодТНВЭД
		
	КонецЕсли;
	
	Если Не КэшЗначений.ВестиУчетПрослеживаемыхТоваров Тогда
		Если СтруктураПараметров.Свойство("ИмяПоляНомерГТД") Тогда
			Для каждого СтруктураПараметров Из ИменаТЧ Цикл
				СтруктураПараметров.ИмяПоляОформления = СтруктураПараметров.ИмяПоляНомерГТД;
			КонецЦикла;
			ГрузовыеТаможенныеДекларацииСервер.ПриСозданииНаСервере(ЭтаФорма, ИменаТЧ, КэшЗначений);
		КонецЕсли;
		Возврат;
	КонецЕсли;

КонецПроцедуры

Процедура ПриСозданииНаСервереДляДокументаСАвтоподборомРНПТ(ЭтаФорма, ИменаТЧИПолей, КэшЗначений) Экспорт
	// Процедура ожидает распространения на все документы с автоматическим подбором РНПТ.
	Возврат;
	
	Если ТипЗнч(КэшЗначений) <> Тип("Структура") Тогда
		КэшЗначений = Новый Структура;
	КонецЕсли;
	Если Не КэшЗначений.Свойство("ВестиУчетПрослеживаемыхТоваров") Тогда
		ВестиУчетПрослеживаемыхТоваров = ПрослеживаемостьУНФ.ВедетсяУчетПрослеживаемыхТоваров(ЭтаФорма.Объект.Дата)
	Иначе
		ВестиУчетПрослеживаемыхТоваров = КэшЗначений.ВестиУчетПрослеживаемыхТоваров
	КонецЕсли;
	
	Если Не ВестиУчетПрослеживаемыхТоваров Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтруктураПараметров Из ИменаТЧИПолей Цикл
		
		НовоеУсловноеОформление = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
		
		ГруппаИ = РаботаСФормой.ДобавитьГруппуЭлементовОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "ГруппаИ");
		
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(
			ГруппаИ,
			СтруктураПараметров.ИмяПоляПрослеживаемость,
			Истина,
			ВидСравненияКомпоновкиДанных.Равно);
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(
			ГруппаИ,
			СтруктураПараметров.ИмяПоляРНПТ,
			,
			ВидСравненияКомпоновкиДанных.НеЗаполнено);
			
		РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, СтруктураПараметров.ИмяПоляОформления);
		
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Текст", "<Авто>");
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", ЦветаСтиля.ТекстВторостепеннойНадписи);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

Процедура ПриОбработкеПроверкиЗаполнения(Отказ, ОбъектПроверки, ИменаТабличныхЧастей = Неопределено) Экспорт
	Перем Ошибки;
	
	Если НЕ ПолучитьФункциональнуюОпцию("УчетГТД") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ИменаТабличныхЧастей = Неопределено Тогда
		
		ИменаТабличныхЧастей = Новый Массив;
		ИменаТабличныхЧастей.Добавить("Запасы");
		
	КонецЕсли;
	
	СтраныЕАЭС = ТаблицаСтранТаможенногоСоюза().ВыгрузитьКолонку("Ссылка");
	ВестиУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров");
	
	ИндексРоссия = СтраныЕАЭС.Найти(Справочники.СтраныМира.Россия);
	Если ИндексРоссия <> Неопределено Тогда
		
		СтраныЕАЭС.Удалить(ИндексРоссия);
		
	КонецЕсли;
	
	Для каждого ИмяТЧ Из ИменаТабличныхЧастей Цикл
		
		Для каждого СтрокаТаблицыДокумента Из ОбъектПроверки[ИмяТЧ] Цикл
			
			Если СтраныЕАЭС.Найти(СтрокаТаблицыДокумента.СтранаПроисхождения) <> Неопределено Тогда
				
				// Страны ЕАЭС могут содержать номер ГТД, но это не обязательная мера.
				Продолжить;
				
			КонецЕсли;
			
			ПрослеживаемыйТовар = ВестиУчетПрослеживаемыхТоваров 
				И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицыДокумента, "ПрослеживаемыйТовар") 
				И СтрокаТаблицыДокумента.ПрослеживаемыйТовар; 
			
			СтранаПроисхожденияТребуетНомерГТД = ЗначениеЗаполнено(СтрокаТаблицыДокумента.СтранаПроисхождения)
				И СтрокаТаблицыДокумента.СтранаПроисхождения <> Справочники.СтраныМира.Россия;
			
			Если ЗначениеЗаполнено(СтрокаТаблицыДокумента.НомерГТД)
				И НЕ СтранаПроисхожденияТребуетНомерГТД Тогда
				
				ТекстОшибки = СтрШаблон(НСтр("ru = 'В строке [%1] табличной части %2 не верно указана страна происхождения'"), СокрЛП(СтрокаТаблицыДокумента.НомерСтроки), ИмяТЧ);
				ПолеОшибки = "Объект." + ИмяТЧ + "[%1].СтранаПроисхождения";
				
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, ПолеОшибки, ТекстОшибки, "", ОбъектПроверки[ИмяТЧ].Индекс(СтрокаТаблицыДокумента));
				
			КонецЕсли;
			
			Если Константы.ТребоватьЗаполнениеГТДИмпортногоТовара.Получить() = Истина
				И НЕ ЗначениеЗаполнено(СтрокаТаблицыДокумента.НомерГТД)
				И СтранаПроисхожденияТребуетНомерГТД
				И НЕ ПрослеживаемыйТовар Тогда
				
				ТекстОшибки = СтрШаблон(НСтр("ru = 'В строке [%1] табличной части %2 не указан номер ГТД'"), СокрЛП(СтрокаТаблицыДокумента.НомерСтроки), ИмяТЧ);
				Если ВестиУчетПрослеживаемыхТоваров Тогда
					ПолеОшибки = "Объект." + ИмяТЧ + "[%1].РНПТ";
				Иначе
					ПолеОшибки = "Объект." + ИмяТЧ + "[%1].НомерГТД";
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, ПолеОшибки, ТекстОшибки, "", ОбъектПроверки[ИмяТЧ].Индекс(СтрокаТаблицыДокумента));
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если НЕ Ошибки = Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура АвтоматическийПодборВРеализацииНомераГТД(СтруктураДанные) Экспорт
	
	Если Константы.АвтоПодборНомеровГТД.Получить() <> Перечисления.ДаНет.Да Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтруктураДанные.Номенклатура) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураДанные.СтранаПроисхождения)
		И СтруктураДанные.СтранаПроисхождения = Справочники.СтраныМира.Россия Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Номенклатура = СтруктураДанные.Номенклатура;
	Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	Партия = Справочники.ПартииНоменклатуры.ПустаяСсылка();
	
	Если Номенклатура.ИспользоватьХарактеристики = Истина
		И СтруктураДанные.Свойство("Характеристика")
		И ЗначениеЗаполнено(СтруктураДанные.Характеристика) Тогда
		
		Характеристика = СтруктураДанные.Характеристика;
		
	КонецЕсли;
	
	Если Номенклатура.ИспользоватьПартии = Истина
		И СтруктураДанные.Свойство("Партия")
		И ЗначениеЗаполнено(СтруктураДанные.Партия) Тогда
		
		Партия = СтруктураДанные.Партия;
		
	КонецЕсли;
	
	ИндексТаблицы = ?(Константы.КонтролироватьОстаткиПоНомерамГТД.Получить() = Истина, 1, 0);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",	Константы.УчетПоКомпании.Компания(СтруктураДанные.Организация));
	Запрос.УстановитьПараметр("Номенклатура",	Номенклатура);
	Запрос.УстановитьПараметр("Характеристика",	Характеристика);
	Запрос.УстановитьПараметр("Партия",			Партия);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗапасыВРазрезеГТДОбороты.Номенклатура КАК Номенклатура
	|	,ЗапасыВРазрезеГТДОбороты.Характеристика КАК Характеристика
	|	,ЗапасыВРазрезеГТДОбороты.Партия КАК Партия
	|	,ЗапасыВРазрезеГТДОбороты.СтранаПроисхождения КАК СтранаПроисхождения
	|	,ЗапасыВРазрезеГТДОбороты.НомерГТД КАК НомерГТД
	|	,ЗапасыВРазрезеГТДОбороты.НомерГТД.Код КАК КодГТД
	|	,ДатаВремя(1, 1, 1) КАК ДатаГТД
	|ИЗ РегистрНакопления.ЗапасыВРазрезеГТД.Обороты( , , Запись,
	|		Организация = &Организация
	|			И Номенклатура = &Номенклатура И Характеристика = &Характеристика И Партия = &Партия)
	|	КАК ЗапасыВРазрезеГТДОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыВРазрезеГТДОбороты.Номенклатура
	|	,ЗапасыВРазрезеГТДОбороты.Характеристика
	|	,ЗапасыВРазрезеГТДОбороты.Партия
	|	,ЗапасыВРазрезеГТДОбороты.СтранаПроисхождения
	|	,ЗапасыВРазрезеГТДОбороты.НомерГТД
	|
	|;Выбрать
	|	ЗапасыВРазрезеГТДОстатки.Номенклатура КАК Номенклатура
	|	,ЗапасыВРазрезеГТДОстатки.Характеристика КАК Характеристика
	|	,ЗапасыВРазрезеГТДОстатки.Партия КАК Партия
	|	,ЗапасыВРазрезеГТДОстатки.СтранаПроисхождения КАК СтранаПроисхождения
	|	,ЗапасыВРазрезеГТДОстатки.НомерГТД КАК НомерГТД
	|	,ЗапасыВРазрезеГТДОстатки.НомерГТД.Код КАК КодГТД
	|	,ДатаВремя(1, 1, 1) КАК ДатаГТД
	|	,Сумма(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ИЗ РегистрНакопления.ЗапасыВРазрезеГТД.Остатки(, Организация = &Организация
	|			И Номенклатура = &Номенклатура И Характеристика = &Характеристика И Партия = &Партия)
	|	КАК ЗапасыВРазрезеГТДОстатки
	|ГДЕ
	|	ЗапасыВРазрезеГТДОстатки.КоличествоОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыВРазрезеГТДОстатки.Номенклатура
	|	,ЗапасыВРазрезеГТДОстатки.Характеристика
	|	,ЗапасыВРазрезеГТДОстатки.Партия
	|	,ЗапасыВРазрезеГТДОстатки.СтранаПроисхождения
	|	,ЗапасыВРазрезеГТДОстатки.НомерГТД";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаНомеровГТД = РезультатЗапроса[ИндексТаблицы].Выгрузить();
	Для каждого СтрокаТаблицы Из ТаблицаНомеровГТД Цикл
		
		СтрокаТаблицы.ДатаГТД = ВыделитьДатуИзНомераГТД(СтрокаТаблицы.КодГТД);
		
	КонецЦикла;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНомеровГТД.Номенклатура
	|	,ТаблицаНомеровГТД.Характеристика
	|	,ТаблицаНомеровГТД.Партия
	|	,ТаблицаНомеровГТД.СтранаПроисхождения
	|	,ТаблицаНомеровГТД.НомерГТД
	|	,ТаблицаНомеровГТД.ДатаГТД КАК ДатаГТД
	|ПОМЕСТИТЬ ТаблицаНомеровГТДСЗаполненнымиДатами
	|ИЗ &ТаблицаНомеровГТД КАК ТаблицаНомеровГТД
	|
	|//:::::::::::::::::::::::::::::::::::::::::::::::::::::::
	|//::::::: Выделим ГТД с максимально свежей датой ::::::::
	|//:::::::::::::::::::::::::::::::::::::::::::::::::::::::
	|;Выбрать ПЕРВЫЕ 1
	|	ТаблицаНомеровГТДСЗаполненнымиДатами.Номенклатура
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.Характеристика
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.Партия
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.ДатаГТД КАК ДатаГТД
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.СтранаПроисхождения КАК СтранаПроисхождения
	|	,Максимум(ТаблицаНомеровГТДСЗаполненнымиДатами.НомерГТД) КАК НомерГТД
	|ИЗ ТаблицаНомеровГТДСЗаполненнымиДатами КАК ТаблицаНомеровГТДСЗаполненнымиДатами
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНомеровГТДСЗаполненнымиДатами.Номенклатура
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.Характеристика
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.Партия
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.ДатаГТД
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.СтранаПроисхождения 
	|УПОРЯДОЧИТЬ ПО 
	|	ТаблицаНомеровГТДСЗаполненнымиДатами.ДатаГТД УБЫВ";
	
	Запрос.УстановитьПараметр("ТаблицаНомеровГТД", ТаблицаНомеровГТД);
	
	ВыборкаНомеровГТД = Запрос.Выполнить().Выбрать();
	Если ВыборкаНомеровГТД.Следующий() Тогда
		
		СтруктураДанные.СтранаПроисхождения = ВыборкаНомеровГТД.СтранаПроисхождения;
		СтруктураДанные.НомерГТД = ВыборкаНомеровГТД.НомерГТД;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура АвтоматическийПодборВКомиссииНомераГТДПереданного(СтруктураДанные) Экспорт
	
	Если Константы.АвтоПодборНомеровГТД.Получить() <> Перечисления.ДаНет.Да Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтруктураДанные.Номенклатура) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураДанные.СтранаПроисхождения)
		И СтруктураДанные.СтранаПроисхождения = Справочники.СтраныМира.Россия Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Номенклатура = СтруктураДанные.Номенклатура;
	Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	Партия = Справочники.ПартииНоменклатуры.ПустаяСсылка();
	
	Если Номенклатура.ИспользоватьХарактеристики = Истина
		И СтруктураДанные.Свойство("Характеристика")
		И ЗначениеЗаполнено(СтруктураДанные.Характеристика) Тогда
		
		Характеристика = СтруктураДанные.Характеристика;
		
	КонецЕсли;
	
	Если Номенклатура.ИспользоватьПартии = Истина
		И СтруктураДанные.Свойство("Партия")
		И ЗначениеЗаполнено(СтруктураДанные.Партия) Тогда
		
		Партия = СтруктураДанные.Партия;
		
	КонецЕсли;
	
	ИндексТаблицы = ?(Константы.КонтролироватьОстаткиПоНомерамГТД.Получить() = Истина, 1, 0);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",	Константы.УчетПоКомпании.Компания(СтруктураДанные.Организация));
	Запрос.УстановитьПараметр("Номенклатура",	Номенклатура);
	Запрос.УстановитьПараметр("Характеристика",	Характеристика);
	Запрос.УстановитьПараметр("Партия",			Партия);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗапасыПереданныеВРазрезеГТД.Номенклатура КАК Номенклатура
	|	,ЗапасыПереданныеВРазрезеГТД.Характеристика КАК Характеристика
	|	,ЗапасыПереданныеВРазрезеГТД.Партия КАК Партия
	|	,ЗапасыПереданныеВРазрезеГТД.СтранаПроисхождения КАК СтранаПроисхождения
	|	,ЗапасыПереданныеВРазрезеГТД.НомерГТД КАК НомерГТД
	|	,ЗапасыПереданныеВРазрезеГТД.НомерГТД.Код КАК КодГТД
	|	,ДатаВремя(1, 1, 1) КАК ДатаГТД
	|ИЗ РегистрНакопления.ЗапасыПереданныеВРазрезеГТД.Обороты( , , Запись,
	|		Организация = &Организация
	|			И Номенклатура = &Номенклатура И Характеристика = &Характеристика И Партия = &Партия)
	|	КАК ЗапасыПереданныеВРазрезеГТД
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыПереданныеВРазрезеГТД.Номенклатура
	|	,ЗапасыПереданныеВРазрезеГТД.Характеристика
	|	,ЗапасыПереданныеВРазрезеГТД.Партия
	|	,ЗапасыПереданныеВРазрезеГТД.СтранаПроисхождения
	|	,ЗапасыПереданныеВРазрезеГТД.НомерГТД
	|
	|;Выбрать
	|	ЗапасыПереданныеВРазрезеГТДОстатки.Номенклатура КАК Номенклатура
	|	,ЗапасыПереданныеВРазрезеГТДОстатки.Характеристика КАК Характеристика
	|	,ЗапасыПереданныеВРазрезеГТДОстатки.Партия КАК Партия
	|	,ЗапасыПереданныеВРазрезеГТДОстатки.СтранаПроисхождения КАК СтранаПроисхождения
	|	,ЗапасыПереданныеВРазрезеГТДОстатки.НомерГТД КАК НомерГТД
	|	,ЗапасыПереданныеВРазрезеГТДОстатки.НомерГТД.Код КАК КодГТД
	|	,ДатаВремя(1, 1, 1) КАК ДатаГТД
	|	,Сумма(ЗапасыПереданныеВРазрезеГТДОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ИЗ РегистрНакопления.ЗапасыПереданныеВРазрезеГТД.Остатки(, Организация = &Организация
	|			И Номенклатура = &Номенклатура И Характеристика = &Характеристика И Партия = &Партия)
	|	КАК ЗапасыПереданныеВРазрезеГТДОстатки
	|ГДЕ
	|	ЗапасыПереданныеВРазрезеГТДОстатки.КоличествоОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыПереданныеВРазрезеГТДОстатки.Номенклатура
	|	,ЗапасыПереданныеВРазрезеГТДОстатки.Характеристика
	|	,ЗапасыПереданныеВРазрезеГТДОстатки.Партия
	|	,ЗапасыПереданныеВРазрезеГТДОстатки.СтранаПроисхождения
	|	,ЗапасыПереданныеВРазрезеГТДОстатки.НомерГТД";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаНомеровГТД = РезультатЗапроса[ИндексТаблицы].Выгрузить();
	Для каждого СтрокаТаблицы Из ТаблицаНомеровГТД Цикл
		
		СтрокаТаблицы.ДатаГТД = ВыделитьДатуИзНомераГТД(СтрокаТаблицы.КодГТД);
		
	КонецЦикла;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНомеровГТД.Номенклатура
	|	,ТаблицаНомеровГТД.Характеристика
	|	,ТаблицаНомеровГТД.Партия
	|	,ТаблицаНомеровГТД.СтранаПроисхождения
	|	,ТаблицаНомеровГТД.НомерГТД
	|	,ТаблицаНомеровГТД.ДатаГТД КАК ДатаГТД
	|ПОМЕСТИТЬ ТаблицаНомеровГТДСЗаполненнымиДатами
	|ИЗ &ТаблицаНомеровГТД КАК ТаблицаНомеровГТД
	|
	|//:::::::::::::::::::::::::::::::::::::::::::::::::::::::
	|//::::::: Выделим ГТД с максимально свежей датой ::::::::
	|//:::::::::::::::::::::::::::::::::::::::::::::::::::::::
	|;Выбрать ПЕРВЫЕ 1
	|	ТаблицаНомеровГТДСЗаполненнымиДатами.Номенклатура
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.Характеристика
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.Партия
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.ДатаГТД КАК ДатаГТД
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.СтранаПроисхождения КАК СтранаПроисхождения
	|	,Максимум(ТаблицаНомеровГТДСЗаполненнымиДатами.НомерГТД) КАК НомерГТД
	|ИЗ ТаблицаНомеровГТДСЗаполненнымиДатами КАК ТаблицаНомеровГТДСЗаполненнымиДатами
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНомеровГТДСЗаполненнымиДатами.Номенклатура
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.Характеристика
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.Партия
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.ДатаГТД
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.СтранаПроисхождения 
	|УПОРЯДОЧИТЬ ПО 
	|	ТаблицаНомеровГТДСЗаполненнымиДатами.ДатаГТД УБЫВ";
	
	Запрос.УстановитьПараметр("ТаблицаНомеровГТД", ТаблицаНомеровГТД);
	
	ВыборкаНомеровГТД = Запрос.Выполнить().Выбрать();
	Если ВыборкаНомеровГТД.Следующий() Тогда
		
		СтруктураДанные.СтранаПроисхождения = ВыборкаНомеровГТД.СтранаПроисхождения;
		СтруктураДанные.НомерГТД = ВыборкаНомеровГТД.НомерГТД;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура АвтоматическийПодборВКомиссииНомераГТДПринятого(СтруктураДанные) Экспорт
	
	Если Константы.АвтоПодборНомеровГТД.Получить() <> Перечисления.ДаНет.Да Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтруктураДанные.Номенклатура) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураДанные.СтранаПроисхождения)
		И СтруктураДанные.СтранаПроисхождения = Справочники.СтраныМира.Россия Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Номенклатура = СтруктураДанные.Номенклатура;
	Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	Партия = Справочники.ПартииНоменклатуры.ПустаяСсылка();
	
	Если Номенклатура.ИспользоватьХарактеристики = Истина
		И СтруктураДанные.Свойство("Характеристика")
		И ЗначениеЗаполнено(СтруктураДанные.Характеристика) Тогда
		
		Характеристика = СтруктураДанные.Характеристика;
		
	КонецЕсли;
	
	Если Номенклатура.ИспользоватьПартии = Истина
		И СтруктураДанные.Свойство("Партия")
		И ЗначениеЗаполнено(СтруктураДанные.Партия) Тогда
		
		Партия = СтруктураДанные.Партия;
		
	КонецЕсли;
	
	ИндексТаблицы = ?(Константы.КонтролироватьОстаткиПоНомерамГТД.Получить() = Истина, 1, 0);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",	Константы.УчетПоКомпании.Компания(СтруктураДанные.Организация));
	Запрос.УстановитьПараметр("Номенклатура",	Номенклатура);
	Запрос.УстановитьПараметр("Характеристика",	Характеристика);
	Запрос.УстановитьПараметр("Партия",			Партия);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗапасыПринятыеВРазрезеГТД.Номенклатура КАК Номенклатура
	|	,ЗапасыПринятыеВРазрезеГТД.Характеристика КАК Характеристика
	|	,ЗапасыПринятыеВРазрезеГТД.Партия КАК Партия
	|	,ЗапасыПринятыеВРазрезеГТД.СтранаПроисхождения КАК СтранаПроисхождения
	|	,ЗапасыПринятыеВРазрезеГТД.НомерГТД КАК НомерГТД
	|	,ЗапасыПринятыеВРазрезеГТД.НомерГТД.Код КАК КодГТД
	|	,ДатаВремя(1, 1, 1) КАК ДатаГТД
	|ИЗ РегистрНакопления.ЗапасыПринятыеВРазрезеГТД.Обороты( , , Запись,
	|		Организация = &Организация
	|			И Номенклатура = &Номенклатура И Характеристика = &Характеристика И Партия = &Партия)
	|	КАК ЗапасыПринятыеВРазрезеГТД
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыПринятыеВРазрезеГТД.Номенклатура
	|	,ЗапасыПринятыеВРазрезеГТД.Характеристика
	|	,ЗапасыПринятыеВРазрезеГТД.Партия
	|	,ЗапасыПринятыеВРазрезеГТД.СтранаПроисхождения
	|	,ЗапасыПринятыеВРазрезеГТД.НомерГТД
	|
	|;Выбрать
	|	ЗапасыПринятыеВРазрезеГТДОстатки.Номенклатура КАК Номенклатура
	|	,ЗапасыПринятыеВРазрезеГТДОстатки.Характеристика КАК Характеристика
	|	,ЗапасыПринятыеВРазрезеГТДОстатки.Партия КАК Партия
	|	,ЗапасыПринятыеВРазрезеГТДОстатки.СтранаПроисхождения КАК СтранаПроисхождения
	|	,ЗапасыПринятыеВРазрезеГТДОстатки.НомерГТД КАК НомерГТД
	|	,ЗапасыПринятыеВРазрезеГТДОстатки.НомерГТД.Код КАК КодГТД
	|	,ДатаВремя(1, 1, 1) КАК ДатаГТД
	|	,Сумма(ЗапасыПринятыеВРазрезеГТДОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ИЗ РегистрНакопления.ЗапасыПринятыеВРазрезеГТД.Остатки(, Организация = &Организация
	|			И Номенклатура = &Номенклатура И Характеристика = &Характеристика И Партия = &Партия)
	|	КАК ЗапасыПринятыеВРазрезеГТДОстатки
	|ГДЕ
	|	ЗапасыПринятыеВРазрезеГТДОстатки.КоличествоОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыПринятыеВРазрезеГТДОстатки.Номенклатура
	|	,ЗапасыПринятыеВРазрезеГТДОстатки.Характеристика
	|	,ЗапасыПринятыеВРазрезеГТДОстатки.Партия
	|	,ЗапасыПринятыеВРазрезеГТДОстатки.СтранаПроисхождения
	|	,ЗапасыПринятыеВРазрезеГТДОстатки.НомерГТД";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаНомеровГТД = РезультатЗапроса[ИндексТаблицы].Выгрузить();
	Для каждого СтрокаТаблицы Из ТаблицаНомеровГТД Цикл
		
		СтрокаТаблицы.ДатаГТД = ВыделитьДатуИзНомераГТД(СтрокаТаблицы.КодГТД);
		
	КонецЦикла;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаНомеровГТД.Номенклатура
	|	,ТаблицаНомеровГТД.Характеристика
	|	,ТаблицаНомеровГТД.Партия
	|	,ТаблицаНомеровГТД.СтранаПроисхождения
	|	,ТаблицаНомеровГТД.НомерГТД
	|	,ТаблицаНомеровГТД.ДатаГТД КАК ДатаГТД
	|ПОМЕСТИТЬ ТаблицаНомеровГТДСЗаполненнымиДатами
	|ИЗ &ТаблицаНомеровГТД КАК ТаблицаНомеровГТД
	|
	|//:::::::::::::::::::::::::::::::::::::::::::::::::::::::
	|//::::::: Выделим ГТД с максимально свежей датой ::::::::
	|//:::::::::::::::::::::::::::::::::::::::::::::::::::::::
	|;Выбрать ПЕРВЫЕ 1
	|	ТаблицаНомеровГТДСЗаполненнымиДатами.Номенклатура
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.Характеристика
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.Партия
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.ДатаГТД КАК ДатаГТД
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.СтранаПроисхождения КАК СтранаПроисхождения
	|	,Максимум(ТаблицаНомеровГТДСЗаполненнымиДатами.НомерГТД) КАК НомерГТД
	|ИЗ ТаблицаНомеровГТДСЗаполненнымиДатами КАК ТаблицаНомеровГТДСЗаполненнымиДатами
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНомеровГТДСЗаполненнымиДатами.Номенклатура
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.Характеристика
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.Партия
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.ДатаГТД
	|	,ТаблицаНомеровГТДСЗаполненнымиДатами.СтранаПроисхождения 
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНомеровГТДСЗаполненнымиДатами.ДатаГТД УБЫВ";
	
	Запрос.УстановитьПараметр("ТаблицаНомеровГТД", ТаблицаНомеровГТД);
	
	ВыборкаНомеровГТД = Запрос.Выполнить().Выбрать();
	Если ВыборкаНомеровГТД.Следующий() Тогда
		
		СтруктураДанные.СтранаПроисхождения = ВыборкаНомеровГТД.СтранаПроисхождения;
		СтруктураДанные.НомерГТД = ВыборкаНомеровГТД.НомерГТД;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти