
#Область ПрограммныйИнтерфейс

// Функция - Возвращает массивы отборов номенклатуры и характеристик
//
// Параметры:
//  ТекстПоиска	 - Строка	 - Искомый текст
// 
// Возвращаемое значение:
//  Структура - Данные отборов. Обязательные свойства:
//		Номенклатура - Массив
//		ХарактеристикиНоменклатуры - Массив
//
Функция МассивыОтборовНоменклатурыИХарактеристик(ТекстПоиска) Экспорт
	
	СтруктураМассивов = Новый Структура;
	
	СтруктураМассивов.Вставить("Номенклатура", СформироватьМассивОтбораПоНоменклатуре(ТекстПоиска));
	СтруктураМассивов.Вставить("ХарактеристикиНоменклатуры", СформироватьМассивОтбораПоХарактеристикам(ТекстПоиска));
	
	Возврат СтруктураМассивов;
	
КонецФункции

// Функция - Определяет вид цен счет-фактуры
//
// Параметры:
//  Контрагент			 - СправочникСсылка.Контрагенты				 - Контрагент
//  ДоговорКонтрагента	 - СправочникСсылка.ДоговорыКонтрагентов	 - Договор контрагента
//  Входящий			 - Булево									 - Признак входящей счет-фактуры
// 
// Возвращаемое значение:
//  СправочникСсылка.ВидыЦен - Используемый вид цен
//
Функция ВидЦенСчетФактуры(Контрагент, ДоговорКонтрагента, Входящий = Истина) Экспорт
	
	ВидЦен = Неопределено;
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		
		ВидЦен = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, ?(Входящий, "ВидЦенКонтрагента", "ВидЦен"));
		
	ИначеЕсли ЗначениеЗаполнено(Контрагент) Тогда
		
		ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ДоговорПоУмолчанию(Контрагент);
		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			
			ВидЦен = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, ?(Входящий, "ВидЦенКонтрагента", "ВидЦен"));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВидЦен
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьМассивОтбораПоНоменклатуре(ТекстПоиска)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ СпрНоменклатура.Ссылка КАК НоменклатураСсылка
	|ИЗ Справочник.Номенклатура КАК СпрНоменклатура
	|ГДЕ СпрНоменклатура.Наименование ПОДОБНО &ТекстПоиска
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ СпрНоменклатура.Ссылка КАК НоменклатураСсылка
	|ИЗ Справочник.Номенклатура КАК СпрНоменклатура
	|ГДЕ СпрНоменклатура.НаименованиеПолное ПОДОБНО &ТекстПоиска
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ СпрНоменклатура.Ссылка КАК НоменклатураСсылка
	|ИЗ Справочник.Номенклатура КАК СпрНоменклатура
	|ГДЕ СпрНоменклатура.Артикул ПОДОБНО &ТекстПоиска
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ СпрНоменклатура.Ссылка КАК НоменклатураСсылка
	|ИЗ Справочник.Номенклатура КАК СпрНоменклатура
	|ГДЕ СпрНоменклатура.Комментарий ПОДОБНО &ТекстПоиска
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ СпрНоменклатура.Ссылка КАК НоменклатураСсылка
	|ИЗ Справочник.Номенклатура КАК СпрНоменклатура
	|ГДЕ СпрНоменклатура.ЦеноваяГруппа.Наименование ПОДОБНО &ТекстПоиска";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТекстПоиска", "%" + ТекстПоиска + "%");
	ТаблицаНоменклатуры = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаНоменклатуры.ВыгрузитьКолонку("НоменклатураСсылка");
	
КонецФункции

Функция СформироватьМассивОтбораПоХарактеристикам(ТекстПоиска)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ СпрХарактеристики.Ссылка КАК ХарактеристикаСсылка
	|ИЗ Справочник.ХарактеристикиНоменклатуры КАК СпрХарактеристики
	|ГДЕ СпрХарактеристики.Наименование ПОДОБНО &ТекстПоиска";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТекстПоиска", "%" + ТекстПоиска + "%");
	ТаблицаХарактеристик = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаХарактеристик.ВыгрузитьКолонку("ХарактеристикаСсылка");
	
КонецФункции

#КонецОбласти 


 

