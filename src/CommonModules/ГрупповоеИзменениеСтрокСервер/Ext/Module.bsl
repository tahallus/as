////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции, предназначенные для группового изменения строк в табличной
//  части и управления оформлением панели редактирования.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Вызывается при создании формы на сервере; заполняет список выбора поля ввода Действие панели редактирования таблицы.
//
// Параметры:
//  НаборЭлементов - Структура - набор необходимых реквизитов и элементов формы, входящих в панель редактирования.
//    * ДокументСсылка  - ДокументСсылка - Ссылка на документ, в котором происходит редактирование ТЧ.
//    * ИмяТЧ  - Строка - Имя ТЧ, которая будет редактироваться.
//    * ДействиеЭлемент - ПолеФормы - Элемент формы, содержащий выбранное действие для группового изменения строк.
//  Действие       - ПеречислениеСсылка - Выбранное действие для группового изменения строк.
//
Процедура ПриСозданииНаСервере(НаборЭлементов, Действие) Экспорт
	
	НаборЭлементов.ПанельРедактирования.Видимость = Ложь;
	НаборЭлементов.КолонкаПометка.Видимость = Ложь;
	НаборЭлементов.КолонкаПометка.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	Если НаборЭлементов.Свойство("КолонкаНомерСтроки") Тогда
		НаборЭлементов.КолонкаНомерСтроки.Видимость = Истина;
	КонецЕсли; 
	
	КлючОбъекта = ТипЗнч(НаборЭлементов.ДокументСсылка);
	КлючНастроек = "ГрупповоеИзменениеСтрок_" + НаборЭлементов.ИмяТЧ;
	Действие = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта, КлючНастроек);
	
КонецПроцедуры

// Управляет видимостью командной панели
Процедура ПоказатьСкрытьПанельРедактирования(Форма, НаборЭлементов, СостояниеПерехода, ИзменяетДанные = Неопределено) Экспорт
	
	ВидимостьПанели = НЕ НаборЭлементов.ПанельРедактирования.Видимость;
	НаборЭлементов.ПанельРедактирования.Видимость = ВидимостьПанели;
	НаборЭлементов.КнопкаИзменитьСтроки.Пометка = ВидимостьПанели;
	
	СостояниеПерехода = ?(ВидимостьПанели, 1, 0);
	НастроитьОформлениеПанелиРедактирования(Форма, НаборЭлементов, СостояниеПерехода, Неопределено, ИзменяетДанные);
	
	ГрупповоеИзменениеСтрокКлиентСервер.УстановитьПредставлениеДействия(НаборЭлементов, Ложь);
	
КонецПроцедуры

// Записывает в пользовательские настройки последнее выбранное действие для изменения таблицы.
// Вызывается при закрытии формы.
//
// Параметры:
//  НаборЭлементов - Структура - набор необходимых реквизитов и элементов формы, входящих в панель редактирования.
//    * ДокументСсылка  - ДокументСсылка - Ссылка на документ, в котором происходит редактирование ТЧ.
//    * ИмяТЧ - Строка - Имя ТЧ, которая будет редактироваться.
//    * ДействиеЭлемент - ПолеФормы - Элемент формы, содержащий выбранное действие для группового изменения строк.
//  Действие       - ПеречислениеСсылка - Выбранное действие для группового изменения строк.
//
Процедура СохранитьНастройки(НаборЭлементов) Экспорт
	
	КлючОбъекта = ТипЗнч(НаборЭлементов.ДокументСсылка);
	КлючНастроек = "ГрупповоеИзменениеСтрок_" + НаборЭлементов.ИмяТЧ;
	ХранилищеОбщихНастроек.Сохранить(КлючОбъекта, КлючНастроек, НаборЭлементов.Действие);
	
КонецПроцедуры

// Получает строковое представление действия.
//
// Параметры:
//  ДействиеПеречисление - ПеречислениеСсылка.ДействияГрупповогоИзмененияСтрок - Действие, описание которого необходимо получить.
// Возвращаемое значение:
//  Строка - Представление действия.
Функция ПредставлениеДействия(ДействиеПеречисление) Экспорт
	
	ДействиеПредставление = Строка(ДействиеПеречисление);
	Возврат Лев(ДействиеПредставление, СтрНайти(ДействиеПредставление, "@") - 1);
	
КонецФункции

Процедура НастроитьИсходнуюПанельРедактирования(Значение, НаборЭлементов) Экспорт
	
	Значение = "";
	НаборЭлементов.ЗначениеЭлемент.Заголовок = НСтр("ru = 'Значение'");
	ТипСтрока = Новый Массив;
	ТипСтрока.Добавить(Тип("Строка"));
	НаборЭлементов.ЗначениеЭлемент.ОграничениеТипа = Новый ОписаниеТипов(ТипСтрока);
	
КонецПроцедуры

// Управляет оформлением панели редактирования таблицы.
//
// Параметры:
//  ЭтаФорма         - ФормаКлиентскогоПриложения - Форма, в которой происходит групповое изменение строк ТЧ.
//  НаборЭлементов   - Структура - набор необходимых реквизитов и элементов формы, входящих в панель редактирования.
//    * ПанельРедактирования - ГруппаФормы - Группа формы, непосредственно являющаяся панелью редактирования ТЧ.
//    * ГруппаДействие - ГруппаФормы - Группа формы, содержащая поле выбора действия для группового изменения строк.
//    * ГруппаЗначение - ГруппаФормы - Группа формы, содержащая поле ввода значения для группового изменения строк.
//    * ГруппаВыполнить - ГруппаФормы - Группа формы, содержащая кнопку формы, запускающую обработку ТЧ.
//    * КнопкаВыполнитьДействие - КнопкаФормы - Кнопка формы, запускающая обработку ТЧ.
//    * КолонкаПометка - ПолеФормы - Пометка строки.
//    * КолонкаНомерСтроки - ПолеФормы - Номер строки.
//    * Действие - ПеречислениеСсылка.ДействияГрупповогоИзмененияСтрок - Выбранное действие для группового изменения строк.
//    * ЗначениеЭлемент - ПолеФормы - Поле ввода значения для группового изменения строк.
//    * ОбъектИзменений - Строка - Название элемента формы, входящего в состав ТЧ, над которым будут производиться изменения.
//    * КолонкаОбъектИзменений - ПолеФормы - Элемент формы, входящего в состав ТЧ, над которым будут производиться изменения.
//  Состояние        - Число - Состояние формы, к которому происходит переход.
//    * 0 - Панель скрыта.
//    * 1 - Выбор действия.
//    * 2 - Выбор значения.
//    * 3 - Применение изменений.
//    * 4 - Представление изменений.
//  Значение         - ПроизвольноеЗначение - Значение, которое используется для изменения значений колонки ТЧ при
//                                            групповом изменении строк.
//  ИзменяетЗначение - Булево - Определяет, нужно ли загрузить исходное состояние редактируемой ТЧ
//                              при скрытии панели редактирования.
//
Процедура НастроитьОформлениеПанелиРедактирования(ЭтаФорма, НаборЭлементов, Состояние, Значение, ИзменяетЗначение = Неопределено) Экспорт
	
	Если Состояние = 2 И НаборЭлементов.Действие = Перечисления.ДействияГрупповогоИзмененияСтрок.УдалитьСтроки Тогда
		Состояние = 3;
	КонецЕсли;
	
	Если Состояние = 0 Тогда
		
		// 0. Панель скрыта
		Если ЗначениеЗаполнено(ИзменяетЗначение) И ИзменяетЗначение Тогда
			ЭтаФорма.Модифицированность = Истина;
		КонецЕсли;
		
		НаборЭлементов.ПанельРедактирования.Видимость = Ложь;
		НаборЭлементов.КолонкаПометка.Видимость = Ложь;
		НаборЭлементов.КолонкаПометка.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
		Если НаборЭлементов.Свойство("КолонкаНомерСтроки") Тогда
			НаборЭлементов.КолонкаНомерСтроки.Видимость = Истина;
			НаборЭлементов.КолонкаНомерСтроки.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
		КонецЕсли; 
		
	ИначеЕсли Состояние = 1 Тогда
		// 1. Выбор действия
		
		НаборЭлементов.КолонкаПометка.Видимость = Истина;
		НаборЭлементов.КолонкаПометка.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
		Если НаборЭлементов.Свойство("КолонкаНомерСтроки") Тогда
			НаборЭлементов.КолонкаНомерСтроки.Видимость = Ложь;
			НаборЭлементов.КолонкаНомерСтроки.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
		КонецЕсли; 
		НаборЭлементов.ПанельРедактирования.Видимость = Истина;
		
		Значение = НаборЭлементов.ЗначениеЭлемент.ОграничениеТипа.ПривестиЗначение("");
		
	ИначеЕсли Состояние = 2 Тогда
		// 2. Выбор значения
		
		Если НаборЭлементов.Действие <> Перечисления.ДействияГрупповогоИзмененияСтрок.УдалитьСтроки Тогда
			НаборЭлементов.ЗначениеЭлемент.Видимость = Истина;
		КонецЕсли;
		
		Значение = НаборЭлементов.ЗначениеЭлемент.ОграничениеТипа.ПривестиЗначение("");
		
		Если НаборЭлементов.ЗначениеЭлемент.Видимость Тогда
			ЭтаФорма.ТекущийЭлемент = НаборЭлементов.ЗначениеЭлемент;
		Иначе
			ЭтаФорма.ТекущийЭлемент = НаборЭлементов.КнопкаВыполнитьДействие;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеРезервнымиКопиями(Форма, Таблица, РезервнаяКопияТаблицыАдрес, СостояниеПерехода, ИзменяетДанные) Экспорт
	
	ИзменяетДанные = ?(ИзменяетДанные = Неопределено, Ложь, ИзменяетДанные);
	Если СостояниеПерехода = 1 Тогда
		СделатьРезервнуюКопиюТаблицы(Форма, Таблица, РезервнаяКопияТаблицыАдрес);
	ИначеЕсли СостояниеПерехода = 0 И НЕ ИзменяетДанные Тогда
		ВосстановитьТаблицуИзРезервнойКопии(Таблица, РезервнаяКопияТаблицыАдрес);
	КонецЕсли;
	
	Если СостояниеПерехода = 1 Тогда
		Если Таблица.Количество()>0 И Таблица[0].Свойство("Пометка") Тогда
			// При открытии устанавливаем пометки
			Для каждого стр Из Таблица Цикл
				стр.Пометка = Истина;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

// Помещает таблицу во временное хранилище.
//
// Параметры:
//  ЭтаФорма - ФормаКлиентскогоПриложения - Форма, в которой происходит групповое изменение строк ТЧ.
//  Таблица  - ДанныеФормыКоллекция - ТЧ, которую требуется поместить во временное хранилище.
//  РезервнаяКопияТаблицыАдрес - Строка - Вернет адрес таблицы во временном хранилище.
//
Процедура СделатьРезервнуюКопиюТаблицы(ЭтаФорма, Таблица, РезервнаяКопияТаблицыАдрес) Экспорт
	
	РезервнаяКопияТаблицыАдрес = ПоместитьВоВременноеХранилище(Таблица.Выгрузить(), ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

// Полностью заменяет текущую таблицу таблицей из временного хранилища.
//
// Параметры:
//  Таблица  - ДанныеФормыКоллекция - ТЧ, которую требуется заменить таблицей во временном хранилище.
//  РезервнаяКопияТаблицыАдрес - Строка - Адрес таблицы во временном хранилище.
//
Процедура ВосстановитьТаблицуИзРезервнойКопии(Таблица, РезервнаяКопияТаблицыАдрес) Экспорт
	
	Таблица.Загрузить(ПолучитьИзВременногоХранилища(РезервнаяКопияТаблицыАдрес));
	
КонецПроцедуры

// Производит групповое изменение строк ТЧ.
//
// Параметры:
//  ЭтаФорма       - ФормаКлиентскогоПриложения - Форма, в которой происходит групповое изменение строк ТЧ.
//  Таблица        - ДанныеФормыКоллекция - ТЧ, над которой производится групповое изменение строк.
//  Действие       - ПеречислениеСсылка.ДействияГрупповогоИзмененияСтрок - Выбранное действие для группового изменения строк.
//  ОбъектДействия - Строка - Имя реквизита ТЧ объекта, над которым производятся изменения.
//  Значение       - ПроизвольноеЗначение - Значение, которое используется для изменения значений колонки ТЧ при
//                                          групповом изменении строк.
//
Процедура ОбработатьТаблицу(ЭтаФорма, Таблица, Действие, ОбъектДействия, Значение, ИмяЭлементаНоменклатура, ПараметрыОтбора = Неопределено) Экспорт
	
	ПриводитьКМинимальнымЦенам = ЦенообразованиеСерверПовтИсп.МинимальныеЦеныИспользуются();
	
	Если Действие <> Перечисления.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокумента
		И Действие <> Перечисления.ДействияГрупповогоИзмененияСтрок.УстановитьПроцентСкидкиНаценки
		И Действие <> Перечисления.ДействияГрупповогоИзмененияСтрок.УстановитьСкидкуСуммой
		И Действие <> Перечисления.ДействияГрупповогоИзмененияСтрок.РаспределитьПоКоличествуВЗаказе
		И Таблица.НайтиСтроки(Новый Структура("Пометка", Истина)).Количество() = 0 Тогда
		
		ТекстСообщения = НСтр("ru='Не выбрана ни одна строка.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ИзменитьЦеныНаПроцент") Тогда
		
		Для каждого Строка Из Таблица Цикл
			
			Если НЕ Строка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Если Строка.Свойство("Цена") Тогда
				
				Строка.Цена = Строка.Цена * (100 + Значение) / 100;
				
				Если ПриводитьКМинимальнымЦенам 
					И Строка.Свойство("МинимальнаяЦена") 
					И Строка.Цена < Строка.МинимальнаяЦена Тогда
					
					Строка.Цена = Строка.МинимальнаяЦена;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЦены") Тогда
		
		Для каждого Строка Из Таблица Цикл
			
			Если НЕ Строка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Если Строка.Свойство("Цена") Тогда
				Строка.Цена = Значение;
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЦеныПоВиду") Тогда
		
		Для каждого Строка Из Таблица Цикл
			
			Если НЕ Строка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			СтруктураДанные = Новый Структура();
			
			СтруктураДанные.Вставить("ДатаОбработки",	ЭтаФорма.Объект.Дата);
			СтруктураДанные.Вставить("Номенклатура",	Строка.Номенклатура);
			СтруктураДанные.Вставить("Характеристика",	Строка.Характеристика);
			СтруктураДанные.Вставить("ВалютаДокумента", ЭтаФорма.Объект.ВалютаДокумента);
			СтруктураДанные.Вставить("ВидЦен", 			Значение);
			Если Строка.Свойство("ЕдиницаИзмерения")
				И ТипЗнч(Строка.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
				СтруктураДанные.Вставить("Коэффициент", Строка.ЕдиницаИзмерения.Коэффициент);
			Иначе
				СтруктураДанные.Вставить("Коэффициент", 1);
			КонецЕсли;
			
			Если Строка.Свойство("Цена") Тогда
				
				ЦенообразованиеКлиентСервер.ДополнитьСтруктуруДанныхНоменклатурыСтруктурнойЕдиницей(ЭтаФорма.Объект, Строка, СтруктураДанные);
				Строка.Цена = ЦенообразованиеСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураДанные);
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ОкруглитьЦены") Тогда
		
		Для каждого Строка Из Таблица Цикл
			
			Если НЕ Строка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Если Строка.Свойство("Цена") Тогда
				Строка.Цена = ЦенообразованиеСервер.ОкруглитьЦену(Строка.Цена, Значение, Ложь);
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ОкруглитьСуммы") Тогда
		
		Для каждого Строка Из Таблица Цикл
			
			Если НЕ Строка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Если Строка.Свойство("Сумма") Тогда
				Строка.Сумма = ЦенообразованиеСервер.ОкруглитьЦену(Строка.Сумма, Значение, Ложь);
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьПроцентСкидкиНаценки") Тогда
		
		Для каждого Строка Из Таблица Цикл
			
			Если ПараметрыОтбора <> Неопределено Тогда
				ЭтоПодходящаяСтрока = ОбщегоНазначенияУНФКлиентСервер.ОбъектСоответствуетПараметрамОтбора(Строка, ПараметрыОтбора);
				Если НЕ ЭтоПодходящаяСтрока Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если Строка.Свойство("ПроцентСкидкиНаценки") Тогда
				Строка.ПроцентСкидкиНаценки = Значение;
			КонецЕсли;
			Если Строка.Свойство("Пометка") Тогда
				Строка.Пометка = Истина;
			КонецЕсли;

		КонецЦикла;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьСкидкуСуммой") Тогда
		
		РаспределитьСуммуНаСкидки(Таблица, "Сумма", Значение, ПараметрыОтбора);
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьПоКоличествуВЗаказе") Тогда
		
		РаспределитьПоКоличествуВЗаказе(Таблица);
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоКоличеству") Тогда
		
		Если Значение <> Неопределено Тогда
			РаспределитьСуммуПоСуммеПропорциональноКолонке(Таблица, "Количество", Значение);
		КонецЕсли;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоСуммам") Тогда
		
		Если Значение <> Неопределено Тогда
			РаспределитьСуммуПоСуммеПропорциональноКолонке(Таблица, "Сумма", Значение);
		КонецЕсли;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоСуммеПередачи") Тогда
		
		Если Значение <> Неопределено Тогда
			РаспределитьСуммуПоКолонке(Таблица, "СуммаПередачи", Значение);
		КонецЕсли;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоСуммеВознаграждения") Тогда
		
		Если Значение <> Неопределено Тогда
			РаспределитьСуммуПоСуммеВознаграждения(Таблица, "СуммаВознаграждения", Значение);
		КонецЕсли;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьСтавкуНДС") Тогда
		
		Для Каждого Строка Из Таблица Цикл
			
			Если НЕ Строка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Если Строка.Свойство("СтавкаНДС") Тогда
				Строка.СтавкаНДС = Значение;
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокумента") Тогда
		
		// Оставим пометки только у добавленных из документа.
		Для Каждого Строка Из Таблица Цикл
			Строка.Пометка = Ложь;
		КонецЦикла;
		
		Если ТипЗнч(Значение) = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
			ИмяТЧИсточник = "РаботыИУслуги";
		ИначеЕсли ТипЗнч(Значение) = Тип("ДокументСсылка.ЗаказНаПроизводство") Тогда
			ИмяТЧИсточник = "Продукция";
		ИначеЕсли ТипЗнч(Значение) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			ИмяТЧИсточник = "Запасы";
		ИначеЕсли ТипЗнч(Значение) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			ИмяТЧИсточник = "Запасы";
		ИначеЕсли ТипЗнч(Значение) = Тип("ДокументСсылка.ИнвентаризацияЗапасов") Тогда
			ИмяТЧИсточник = "Запасы";
		ИначеЕсли ТипЗнч(Значение) = Тип("ДокументСсылка.ОприходованиеЗапасов") Тогда
			ИмяТЧИсточник = "Запасы";
		ИначеЕсли ТипЗнч(Значение) = Тип("ДокументСсылка.ПеремещениеЗапасов") Тогда
			ИмяТЧИсточник = "Запасы";
		ИначеЕсли ТипЗнч(Значение) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
			ИмяТЧИсточник = "Запасы";
		ИначеЕсли ТипЗнч(Значение) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
			ИмяТЧИсточник = "Запасы";
		ИначеЕсли ТипЗнч(Значение) = Тип("ДокументСсылка.СборкаЗапасов") Тогда
			ИмяТЧИсточник = "Продукция";
		ИначеЕсли ТипЗнч(Значение) = Тип("ДокументСсылка.СписаниеЗапасов") Тогда
			ИмяТЧИсточник = "Запасы";
		ИначеЕсли ТипЗнч(Значение) = Тип("ДокументСсылка.СчетНаОплату") Тогда
			ИмяТЧИсточник = "Запасы";
		ИначеЕсли ТипЗнч(Значение) = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда
			ИмяТЧИсточник = "Запасы";
		КонецЕсли;
		
		ДопустимыеТипыНоменклатуры = Новый Массив;
		
		ЭлементНоменклатура = ЭтаФорма.Элементы[ИмяЭлементаНоменклатура];
		ПараметрыВыбораНоменклатуры = ЭлементНоменклатура.ПараметрыВыбора;
		Для Каждого ПараметрВыбора Из ПараметрыВыбораНоменклатуры Цикл
			
			Если ПараметрВыбора.Имя <> "Отбор.ТипНоменклатуры" Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(ПараметрВыбора.Значение) = Тип("ФиксированныйМассив")
				ИЛИ ТипЗнч(ПараметрВыбора.Значение) = Тип("ФиксированныйМассив") Тогда
				
				Для каждого ЗначениеОтбора Из ПараметрВыбора.Значение Цикл
					ДопустимыеТипыНоменклатуры.Добавить(ЗначениеОтбора);
				КонецЦикла;
			Иначе
				ДопустимыеТипыНоменклатуры.Добавить(ПараметрВыбора.Значение);
			КонецЕсли;
			
		КонецЦикла;
		
		ЗначенияНовойСтроки = Новый Структура("Номенклатура,Количество,Характеристика,Партия,Цена,СтавкаНДС,СтранаПроисхождения,НомерГТД,СерийныеНомера");
		
		Для Каждого Строка Из Значение[ИмяТЧИсточник] Цикл
			
			Если ТипЗнч(Строка.Номенклатура) <> Тип("СправочникСсылка.Номенклатура") Тогда
				Продолжить;
			КонецЕсли;
			
			Если ДопустимыеТипыНоменклатуры.Найти(Строка.Номенклатура.ТипНоменклатуры) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ЗначенияНовойСтроки.Номенклатура = Неопределено;
			ЗначенияНовойСтроки.Количество = Неопределено;
			
			ЗаполнитьЗначенияСвойств(ЗначенияНовойСтроки, Строка);
			НоваяСтрока = Таблица.Добавить();
			НоваяСтрока.Пометка = Истина;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗначенияНовойСтроки);
			
		КонецЦикла;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УдалитьСтроки") Тогда
		
		ПомеченныеСтроки = Таблица.НайтиСтроки(Новый Структура("Пометка", Истина));
		
		Для Каждого Строка Из ПомеченныеСтроки Цикл
			
			Таблица.Удалить(Строка);
			
		КонецЦикла;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЗаказПокупателя")
		ИЛИ Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьДатуОтгрузки")
		ИЛИ Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьДатуПоступления")
		ИЛИ Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьПокупателя")
		ИЛИ Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьПоставщика")
		ИЛИ Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьИзготовителя")
		ИЛИ Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьДатуРеализации")
		ИЛИ Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьДатуПланирования")
		ИЛИ Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЗаказПоставщикуЗаказПокупателя")
		ИЛИ Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьИсходноеМесто")
		ИЛИ Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьНовоеМесто") Тогда
		
		Для Каждого Строка Из Таблица Цикл
			
			Если НЕ Строка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Строка[ОбъектДействия] = Значение;
			
		КонецЦикла;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьСклад") Тогда
		
		Для Каждого Строка Из Таблица Цикл
			
			Если НЕ Строка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Строка[ОбъектДействия] = Значение;
			
			Если Не Строка.Свойство("Ячейка") ИЛИ Не ЗначениеЗаполнено(Строка.Ячейка) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Ячейка, "Владелец") <> Значение Тогда
				Строка.Ячейка = Справочники.Ячейки.ПустаяСсылка();
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЯчейку") Тогда
		
		Для Каждого Строка Из Таблица Цикл
			
			Если НЕ Строка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Если Строка.СтруктурнаяЕдиница <> Значение.Владелец Тогда
				Продолжить;
			КонецЕсли;
			
			Строка.Ячейка = Значение;
			
		КонецЦикла;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ЗаполнитьСерииНоменклатуры") Тогда
		
		Если ИмяЭлементаНоменклатура = "ПродукцияНоменклатура" Тогда
			СерииНоменклатурыУНФ.ЗаполнитьСерииНоменклатурыВНаличии(ЭтаФорма.Объект, "Продукция");
		Иначе
			СерииНоменклатурыУНФ.ЗаполнитьСерииНоменклатурыВНаличии(ЭтаФорма.Объект);
		КонецЕсли; 
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЭтап") Тогда
		
		Для Каждого Строка Из Таблица Цикл
			
			Если НЕ Строка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Если Строка.Свойство("Этап") Тогда
				Строка.Этап = Значение;
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ОкруглитьКоличествоКратноЕдиницеИзмерения") Тогда
		
		Для Каждого Строка Из Таблица Цикл
			
			Если НЕ Строка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Строка.ЕдиницаИзмерения) 
				Или ТипЗнч(Строка.ЕдиницаИзмерения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
				Продолжить
			КонецЕсли;
			
			Коэффициент = Строка.ЕдиницаИзмерения.Коэффициент;
			
			Если Коэффициент = 1 Или Коэффициент = 0 Тогда
				Продолжить
			КонецЕсли;
			
			Если Строка.Количество <= Коэффициент Тогда
				Строка.Количество = Коэффициент;
				Продолжить;
			КонецЕсли;
			
			РезультатДеления = Строка.Количество/Коэффициент;
			
			Если РезультатДеления - Цел(РезультатДеления) = 0 Тогда
				Продолжить
			КонецЕсли;
			
			Строка.Количество = Окр(РезультатДеления)*Коэффициент;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура РаспределитьПоКоличествуВЗаказе(Таблица)
	
	Для Каждого ТекСтрока Из Таблица Цикл
		Если ТекСтрока.РасхождениеСЗаказом <= 0 Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = Таблица.Вставить(Таблица.Индекс(ТекСтрока) + 1);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		НоваяСтрока.Заказ = Неопределено;
		НоваяСтрока.Количество = ТекСтрока.РасхождениеСЗаказом;
		НоваяСтрока.КоличествоСправочно = НоваяСтрока.Количество;
		НоваяСтрока.КоличествоВЗаказе = 0;
		НоваяСтрока.РасхождениеСЗаказом = 0;
		НоваяСтрока.РасхождениеСЗаказомПредставление = "";
		НоваяСтрока.ЦенаВЗаказе = 0;
		НоваяСтрока.ЦенаВЗаказеПредставление = "";
		ТекСтрока.Количество = ТекСтрока.Количество - ТекСтрока.РасхождениеСЗаказом;
		ТекСтрока.РасхождениеСЗаказом = 0;
		ТекСтрока.РасхождениеСЗаказомПредставление = "0";
	КонецЦикла;
	
КонецПроцедуры

// Распределяет сумму по колонке Сумма, пропорционально значениям в колонке ИмяКолонки таблицы.
Процедура РаспределитьСуммуПоСуммеПропорциональноКолонке(Таблица, ИмяКолонки, СуммаРаспределения)
	
	Если НЕ ЗначениеЗаполнено(СуммаРаспределения) Тогда
		Возврат;
	КонецЕсли;
	
	// Посчитаем общую помеченных позиций
	ОбщаяСумма = 0;
	Для каждого СтрокаТабличнойЧасти Из Таблица Цикл
		Если НЕ СтрокаТабличнойЧасти.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		ОбщаяСумма = ОбщаяСумма + СтрокаТабличнойЧасти[ИмяКолонки];
	КонецЦикла;
	
	Если ОбщаяСумма = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Теперь распределяем
	СтрокаМаксимальнойСуммы = Неопределено; // На эту строку будем относить остаток после распределения (ошибки округления)
	МаксимальнаяСумма       = 0; // Значение максимальной суммы.
	НепогашеннаяСумма       = СуммаРаспределения;
	
	Для каждого СтрокаТабличнойЧасти Из Таблица Цикл
		Если НЕ СтрокаТабличнойЧасти.Пометка Тогда
			Продолжить;
		КонецЕсли;
			
		ТекущаяСумма = СтрокаТабличнойЧасти.Сумма;
		Дельта       = СуммаРаспределения * СтрокаТабличнойЧасти[ИмяКолонки] / ОбщаяСумма;
		
		// Если Дельта по модулю оказалась больше, чем осталось погасить
		Если Дельта < 0 Тогда
			Дельта = Макс(НепогашеннаяСумма, Дельта)
		Иначе
			Дельта = Мин(НепогашеннаяСумма, Дельта)
		КонецЕсли;
		
		// Проверим текущую сумму на максимум.
		Если ТекущаяСумма > МаксимальнаяСумма Тогда
			МаксимальнаяСумма       = ТекущаяСумма;
			СтрокаМаксимальнойСуммы = СтрокаТабличнойЧасти;
		КонецЕсли;
		
		// Увеличиваем значение
		СтрокаТабличнойЧасти.Сумма = ТекущаяСумма + Дельта;
		
		// Остаток нераспределенной суммы надо уменьшать на дельту реального изменения
		НепогашеннаяСумма = НепогашеннаяСумма - (СтрокаТабличнойЧасти.Сумма - ТекущаяСумма);
		
	КонецЦикла;
	
	// Если что-то осталось, кидаем на строку с максимальной суммой.
	Если НепогашеннаяСумма > 0 И СтрокаМаксимальнойСуммы <> Неопределено Тогда
		СтрокаМаксимальнойСуммы.Сумма = СтрокаМаксимальнойСуммы.Сумма + НепогашеннаяСумма;
	КонецЕсли;
	
КонецПроцедуры

// Распределяет сумму по колонке Сумма, пропорционально значениям в колонке ИмяКолонки таблицы.
Процедура РаспределитьСуммуПоСуммеВознаграждения(Таблица, ИмяКолонки, СуммаРаспределения)
	
	Если НЕ ЗначениеЗаполнено(СуммаРаспределения) Тогда
		Возврат;
	КонецЕсли;
	
	// Посчитаем общую помеченных позиций
	ОбщаяСумма = 0;
	Для каждого СтрокаТабличнойЧасти Из Таблица Цикл
		Если НЕ СтрокаТабличнойЧасти.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		ОбщаяСумма = ОбщаяСумма + СтрокаТабличнойЧасти.Сумма;
	КонецЦикла;
	
	Если ОбщаяСумма = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Теперь распределяем
	СтрокаМаксимальнойСуммы = Неопределено; // На эту строку будем относить остаток после распределения (ошибки округления)
	МаксимальнаяСумма       = 0; // Значение максимальной суммы.
	НепогашеннаяСумма       = СуммаРаспределения;
	
	Для каждого СтрокаТабличнойЧасти Из Таблица Цикл
		Если НЕ СтрокаТабличнойЧасти.Пометка Тогда
			Продолжить;
		КонецЕсли;
			
		ТекущаяСумма = СтрокаТабличнойЧасти.СуммаВознаграждения;
		Дельта       = СуммаРаспределения * СтрокаТабличнойЧасти.Сумма / ОбщаяСумма;
		
		// Если Дельта по модулю оказалась больше, чем осталось погасить
		Если Дельта < 0 Тогда
			Дельта = Макс(НепогашеннаяСумма, Дельта)
		Иначе
			Дельта = Мин(НепогашеннаяСумма, Дельта)
		КонецЕсли;
		
		// Проверим текущую сумму на максимум.
		Если ТекущаяСумма > МаксимальнаяСумма Тогда
			МаксимальнаяСумма       = ТекущаяСумма;
			СтрокаМаксимальнойСуммы = СтрокаТабличнойЧасти;
		КонецЕсли;
		
		// Увеличиваем значение
		СтрокаТабличнойЧасти.СуммаВознаграждения = ТекущаяСумма + Дельта;
		
		// Остаток нераспределенной суммы надо уменьшать на дельту реального изменения
		НепогашеннаяСумма = НепогашеннаяСумма - (СтрокаТабличнойЧасти.СуммаВознаграждения - ТекущаяСумма);
		
	КонецЦикла;
	
	// Если что-то осталось, кидаем на строку с максимальной суммой.
	Если НепогашеннаяСумма > 0 И СтрокаМаксимальнойСуммы <> Неопределено Тогда
		СтрокаМаксимальнойСуммы.Сумма = СтрокаМаксимальнойСуммы.Сумма + НепогашеннаяСумма;
	КонецЕсли;
	
КонецПроцедуры

// Распределяет сумму по указанной в ИмяКолонки колонке таблицы, пропорционально значениям в этой же колонке
Процедура РаспределитьСуммуПоКолонке(Таблица, ИмяКолонки, СуммаРаспределения)
	
	Если НЕ ЗначениеЗаполнено(СуммаРаспределения) Тогда
		Возврат;
	КонецЕсли;
	
	// Посчитаем общую помеченных позиций
	ОбщаяСумма = 0;
	Для каждого СтрокаТабличнойЧасти Из Таблица Цикл
		Если НЕ СтрокаТабличнойЧасти.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		ОбщаяСумма = ОбщаяСумма + СтрокаТабличнойЧасти[ИмяКолонки];
	КонецЦикла;
	
	Если ОбщаяСумма = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Теперь распределяем
	СтрокаМаксимальнойСуммы = Неопределено; // На эту строку будем относить остаток после распределения (ошибки округления)
	МаксимальнаяСумма       = 0; // Значение максимальной суммы.
	НепогашеннаяСумма       = СуммаРаспределения;
	
	Для каждого СтрокаТабличнойЧасти Из Таблица Цикл
		Если НЕ СтрокаТабличнойЧасти.Пометка Тогда
			Продолжить;
		КонецЕсли;
			
		ТекущаяСумма = СтрокаТабличнойЧасти[ИмяКолонки];
		Дельта       = СуммаРаспределения * СтрокаТабличнойЧасти[ИмяКолонки] / ОбщаяСумма;
		
		// Если Дельта по модулю оказалась больше, чем осталось погасить
		Если Дельта < 0 Тогда
			Дельта = Макс(НепогашеннаяСумма, Дельта)
		Иначе
			Дельта = Мин(НепогашеннаяСумма, Дельта)
		КонецЕсли;
		
		// Проверим текущую сумму на максимум.
		Если ТекущаяСумма > МаксимальнаяСумма Тогда
			МаксимальнаяСумма       = ТекущаяСумма;
			СтрокаМаксимальнойСуммы = СтрокаТабличнойЧасти;
		КонецЕсли;
		
		// Увеличиваем значение
		СтрокаТабличнойЧасти[ИмяКолонки] = ТекущаяСумма + Дельта;
		
		// Остаток нераспределенной суммы надо уменьшать на дельту реального изменения
		НепогашеннаяСумма = НепогашеннаяСумма - (СтрокаТабличнойЧасти[ИмяКолонки] - ТекущаяСумма);
		
	КонецЦикла;
	
	// Если что-то осталось, кидаем на строку с максимальной суммой.
	Если НепогашеннаяСумма > 0 И СтрокаМаксимальнойСуммы <> Неопределено Тогда
		СтрокаМаксимальнойСуммы[ИмяКолонки] = СтрокаМаксимальнойСуммы[ИмяКолонки] + НепогашеннаяСумма;
	КонецЕсли;
	
КонецПроцедуры

// Распределяет сумму по колонке таблицы.
Процедура РаспределитьСуммуНаСкидки(Таблица, ИмяКолонки, СуммаРаспределения, ПараметрыОтбора = Неопределено)
	
	ПриводитьКМинимальным = ЦенообразованиеСерверПовтИсп.МинимальныеЦеныИспользуются()
							И Таблица.Количество()>0
							И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Таблица[0], "МинимальнаяЦена");
							
	ЭтоЗаказПокупателяИлиРасходная = ТипЗнч(Таблица) = Тип("ДокументТабличнаяЧасть.ЗаказПокупателя.Запасы")
								ИЛИ ТипЗнч(Таблица) = Тип("ДокументТабличнаяЧасть.РасходнаяНакладная.Запасы");

	// Посчитаем общую сумму помеченных позиций
	ОбщаяСумма = 0;
	Для каждого СтрокаТабличнойЧасти Из Таблица Цикл
		
		Если ПараметрыОтбора <> Неопределено Тогда
			Если НЕ ОбщегоНазначенияУНФКлиентСервер.ОбъектСоответствуетПараметрамОтбора(СтрокаТабличнойЧасти, ПараметрыОтбора) Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Цена = СтрокаТабличнойЧасти.Цена;		
		
		Если ЭтоЗаказПокупателяИлиРасходная Тогда	
			СуммаПоСтроке = Цена * СтрокаТабличнойЧасти.Количество;
		Иначе
			СуммаПоСтроке = Цена * СтрокаТабличнойЧасти.Количество 
			* ?(СтрокаТабличнойЧасти.Свойство("Кратность"), СтрокаТабличнойЧасти.Кратность, 1) 
			* ?(СтрокаТабличнойЧасти.Свойство("Коэффициент"), СтрокаТабличнойЧасти.Коэффициент, 1);
		КонецЕсли;
		
		Если ПриводитьКМинимальным И СтрокаТабличнойЧасти.МинимальнаяЦена>0 Тогда
			
			Если ЭтоЗаказПокупателяИлиРасходная Тогда
				МинимальнаяСуммаПоСтроке = СтрокаТабличнойЧасти.МинимальнаяЦена * СтрокаТабличнойЧасти.Количество;
			Иначе
				МинимальнаяСуммаПоСтроке = СтрокаТабличнойЧасти.МинимальнаяЦена * СтрокаТабличнойЧасти.Количество
				* ?(СтрокаТабличнойЧасти.Свойство("Кратность"), СтрокаТабличнойЧасти.Кратность, 1) 
				* ?(СтрокаТабличнойЧасти.Свойство("Коэффициент"), СтрокаТабличнойЧасти.Коэффициент, 1);
			КонецЕсли;
			
			// Исключим сумму по этой строке из формулы расчета процента
			Если СуммаПоСтроке <= МинимальнаяСуммаПоСтроке Тогда         					
				СуммаПоСтроке = 0; 			
			КонецЕсли;
			
		КонецЕсли;               				
		
		ОбщаяСумма = ОбщаяСумма + СуммаПоСтроке;			
				
	КонецЦикла;
	
	Если ОбщаяСумма = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПроцентСкидки = СуммаРаспределения / ОбщаяСумма * 100;
	
	ОбщаяСуммаСкидки = 0;
	ПоследняяСтрока = Неопределено;
	Для каждого СтрокаТабличнойЧасти Из Таблица Цикл
		
		Если ПараметрыОтбора <> Неопределено Тогда
			Если НЕ ОбщегоНазначенияУНФКлиентСервер.ОбъектСоответствуетПараметрамОтбора(СтрокаТабличнойЧасти, ПараметрыОтбора) Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если ПриводитьКМинимальным И СтрокаТабличнойЧасти.МинимальнаяЦена > 0 Тогда
			
			МинимальнаяСуммаПоСтроке = СтрокаТабличнойЧасти.МинимальнаяЦена * СтрокаТабличнойЧасти.Количество
				* ?(СтрокаТабличнойЧасти.Свойство("Кратность"), СтрокаТабличнойЧасти.Кратность, 1) 
				* ?(СтрокаТабличнойЧасти.Свойство("Коэффициент"), СтрокаТабличнойЧасти.Коэффициент, 1);
				
			СуммаСтрокиБезСкидок = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество 
				* ?(СтрокаТабличнойЧасти.Свойство("Кратность"), СтрокаТабличнойЧасти.Кратность, 1) 
				* ?(СтрокаТабличнойЧасти.Свойство("Коэффициент"), СтрокаТабличнойЧасти.Коэффициент, 1);
				
			Если СуммаСтрокиБезСкидок <= МинимальнаяСуммаПоСтроке Тогда
					
				ПроцентСкидки = 0;
				СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = 0;
				
			Иначе
				
				МаксимальнаяСуммаСкидки = СуммаСтрокиБезСкидок - МинимальнаяСуммаПоСтроке;
				СуммаСкидкиНаценки = СуммаСтрокиБезСкидок * ПроцентСкидки / 100;
			
				Если СуммаСкидкиНаценки > МаксимальнаяСуммаСкидки Тогда
					
					ПроцентСкидки =  МаксимальнаяСуммаСкидки / СуммаСтрокиБезСкидок * 100;
					
				КонецЕсли;				
				
			КонецЕсли;
		
		КонецЕсли;
		
		СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = ПроцентСкидки;	
		
		Если ЭтоЗаказПокупателяИлиРасходная Тогда
			СуммаСтрокиБезСкидок = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество;
			СуммаСкидкиНаценки = СуммаСтрокиБезСкидок * ПроцентСкидки / 100;
			СтрокаТабличнойЧасти.СуммаСкидкиНаценки = СуммаСкидкиНаценки;
		Иначе
			СуммаСтрокиБезСкидок = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество 
				* ?(СтрокаТабличнойЧасти.Свойство("Кратность"), СтрокаТабличнойЧасти.Кратность, 1) 
				* ?(СтрокаТабличнойЧасти.Свойство("Коэффициент"), СтрокаТабличнойЧасти.Коэффициент, 1);
			СуммаСкидкиНаценки = СуммаСтрокиБезСкидок * ПроцентСкидки / 100;
			Если СтрокаТабличнойЧасти.Свойство("СуммаСкидкиНаценки") Тогда
				СтрокаТабличнойЧасти.СуммаСкидкиНаценки = СуммаСкидкиНаценки;
			КонецЕсли;
			СтрокаТабличнойЧасти.Пометка = Истина;
		КонецЕсли;
		СтрокаТабличнойЧасти.Сумма = СуммаСтрокиБезСкидок - СуммаСкидкиНаценки;
		
		ОбщаяСуммаСкидки = ОбщаяСуммаСкидки + СуммаСкидкиНаценки;
		ПоследняяСтрока = СтрокаТабличнойЧасти;
		ПроцентСкидки = СуммаРаспределения / ОбщаяСумма * 100;
		
	КонецЦикла;
	
	ИтогСуммаСкидкиНаценки = 0;
	Для каждого СтрокаТабличнойЧасти Из Таблица Цикл
		Если ПараметрыОтбора <> Неопределено Тогда
			Если Не ОбщегоНазначенияУНФКлиентСервер.ОбъектСоответствуетПараметрамОтбора(СтрокаТабличнойЧасти,
				ПараметрыОтбора) Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ИтогСуммаСкидкиНаценки = ИтогСуммаСкидкиНаценки + СтрокаТабличнойЧасти.СуммаСкидкиНаценки;
	КонецЦикла;
	
	НепогашеннаяСумма = СуммаРаспределения - ИтогСуммаСкидкиНаценки;
	Если ПоследняяСтрока <> Неопределено И НепогашеннаяСумма <> 0 Тогда
		
		Если ЭтоЗаказПокупателяИлиРасходная Тогда
			
			СуммаСтрокиБезСкидки = ПоследняяСтрока.Цена * ПоследняяСтрока.Количество;
			
			ПроцентСкидкиНаценки = (ПоследняяСтрока.Цена * ПоследняяСтрока.Количество 
				* ПоследняяСтрока.ПроцентСкидкиНаценки/100 
				+ НепогашеннаяСумма) / СуммаСтрокиБезСкидки * 100;
				
		Иначе
				
			СуммаСтрокиБезСкидки = ПоследняяСтрока.Цена * ПоследняяСтрока.Количество 
				* ?(ПоследняяСтрока.Свойство("Кратность"), ПоследняяСтрока.Кратность, 1) 
				* ?(ПоследняяСтрока.Свойство("Коэффициент"), ПоследняяСтрока.Коэффициент, 1);
				
			ПроцентСкидкиНаценки = (ПоследняяСтрока.Цена * ПоследняяСтрока.Количество 
				* ?(ПоследняяСтрока.Свойство("Кратность"), ПоследняяСтрока.Кратность, 1) 
				* ?(ПоследняяСтрока.Свойство("Коэффициент"), ПоследняяСтрока.Коэффициент, 1) 
				* ПоследняяСтрока.ПроцентСкидкиНаценки/100 
				+ НепогашеннаяСумма) / СуммаСтрокиБезСкидки * 100;
				
		КонецЕсли;
			
		Если ПриводитьКМинимальным И ПоследняяСтрока.МинимальнаяЦена > 0 Тогда
			
			МинимальнаяСуммаПоСтроке = ПоследняяСтрока.МинимальнаяЦена * ПоследняяСтрока.Количество
				* ?(ПоследняяСтрока.Свойство("Кратность"), ПоследняяСтрока.Кратность, 1) 
				* ?(ПоследняяСтрока.Свойство("Коэффициент"), ПоследняяСтрока.Коэффициент, 1);
			
			Если ПоследняяСтрока.Сумма <= МинимальнаяСуммаПоСтроке Тогда   				
				НепогашеннаяСумма = 0;
			КонецЕсли;
			
		Иначе
			
			ПоследняяСтрока.ПроцентСкидкиНаценки = ПроцентСкидкиНаценки;
			
		КонецЕсли;
		
		ПоследняяСтрока.СуммаСкидкиНаценки = ПоследняяСтрока.СуммаСкидкиНаценки + НепогашеннаяСумма;
		ПоследняяСтрока.Сумма = СуммаСтрокиБезСкидок - ПоследняяСтрока.СуммаСкидкиНаценки;
		ПроцентСкидки = СуммаРаспределения / ОбщаяСумма * 100;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвязиПараметровВыбора(НаборЭлементов) Экспорт
	
	Если НаборЭлементов.КолонкаОбъектИзменений <> Неопределено Тогда
		НаборЭлементов.ЗначениеЭлемент.СвязиПараметровВыбора = НаборЭлементов.КолонкаОбъектИзменений.СвязиПараметровВыбора;
	Иначе
		НаборЭлементов.ЗначениеЭлемент.СвязиПараметровВыбора = Новый ФиксированныйМассив(Новый Массив);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
