
#Область ПрограммныйИнтерфейс

// Формирует таблицу движений по учету резервов товаров организаций
// Таблицы значений сохраняет в свойствах структуры "СтруктураДополнительныеСвойства".
//
// Параметры:
//  ДокументСсылка					 - 	ДокументСсылка - Ссылка на проводимый документ
//  СтруктураДополнительныеСвойства	 - 	Структура - Структура данных для проведения 
//
Процедура СформироватьТаблицаРезервыТоваровОрганизаций(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ПередачаТоваровМеждуОрганизациями") Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРезервыТоваровОрганизаций", Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли; 
	
	ОрганизацииИнтеркампани = РегистрыСведений.НастройкаПередачиТоваровМеждуОрганизациями.ПолучитьСписокОрганизацийДляОстатков(
		СтруктураДополнительныеСвойства.ДляПроведения.Организация);
		
	Если ОрганизацииИнтеркампани.Количество() < 2 Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРезервыТоваровОрганизаций", Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("РезервированиеЗапасов", ПолучитьФункциональнуюОпцию("РезервированиеЗапасов"));
	Запрос.УстановитьПараметр("УчетГТД", ПолучитьФункциональнуюОпцию("УчетГТД"));
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментКонтроля);
	Запрос.УстановитьПараметр("МассивОрганизаций", ОрганизацииИнтеркампани);
	Запрос.УстановитьПараметр("ДатаДокумента", СтруктураДополнительныеСвойства.ДляПроведения.Дата);
	Запрос.Текст = ТекстЗапросаРезервыТоваровОрганизаций();
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаОстатковСклад = РезультатЗапроса[РезультатЗапроса.Количество() - 5].Выгрузить();
	ТаблицаОстатковСклад.Индексы.Добавить(СтрШаблон("%1, %2", ПоляСкладскойУчет(), "ОстатокБезПередачи"));
	ТаблицаОстатковЗапасов = РезультатЗапроса[РезультатЗапроса.Количество() - 4].Выгрузить();
	ТаблицаОстатковЗапасов.Индексы.Добавить(СтрШаблон("%1, %2", ПоляЗапасы(), "ОстатокБезПередачи"));
	ТаблицаОстатковЗапасов.Индексы.Добавить(СтрШаблон("%1, %2", ПоляЗапасы(), "Организация"));
	ТаблицаОстатковГТД = РезультатЗапроса[РезультатЗапроса.Количество() - 3].Выгрузить();
	ТаблицаОстатковГТД.Индексы.Добавить(СтрШаблон("%1, %2", ПоляУчетГТД(), "ОстатокБезПередачи"));
	ТаблицаОстатковГТД.Индексы.Добавить(СтрШаблон("%1, %2", ПоляУчетГТД(), "Организация"));
	ТаблицаРезервы = РезультатЗапроса[РезультатЗапроса.Количество() - 2].Выгрузить();
	ТаблицаРезервы.Индексы.Добавить(СтрШаблон("%1, %2", ПоляРезервы(), "ОстатокБезПередачи"));
	ТаблицыТоваров = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить();
	ТаблицаРезервыТоваровОрганизаций = ТаблицыТоваров.СкопироватьКолонки();
	
	Ошибки = Новый Массив;
	КонтролироватьОстатки = Константы.КонтролироватьОстаткиПриПроведении.Получить();
	Для Каждого СтрокаТовара Из ТаблицыТоваров Цикл
		
		СтруктураОтбора = Новый Структура(ПоляЗапасы());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("ОстатокБезПередачи", Истина);
		СтрокиЗапасы = ТаблицаОстатковЗапасов.НайтиСтроки(СтруктураОтбора);
		
		СтруктураОтбора = Новый Структура(ПоляСкладскойУчет());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("ОстатокБезПередачи", Истина);
		СтрокиСклад = ТаблицаОстатковСклад.НайтиСтроки(СтруктураОтбора);
		
		СтруктураОтбора = Новый Структура(ПоляУчетГТД());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("ОстатокБезПередачи", Истина);
		СтрокиГТД = ТаблицаОстатковГТД.НайтиСтроки(СтруктураОтбора);
		
		СтруктураОтбора = Новый Структура(ПоляРезервы());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("ОстатокБезПередачи", Истина);
		СтрокиРезервы = ТаблицаРезервы.НайтиСтроки(СтруктураОтбора);
		
		КоличествоТовараНеХватило = КоличествоРезерва(СтрокаТовара, СтрокиЗапасы, СтрокиСклад, СтрокиГТД, СтрокиРезервы);
		Если КоличествоТовараНеХватило <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Первая итерация: сначала обрабатываются организации, от которых запланирована или выполнена передача товаров
		// Организации отсортированы в порядке уменьшения резерва, который ранее выполнялся документом
		СтруктураОтбора = Новый Структура(ПоляРезервы());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("ОстатокБезПередачи", Ложь);
		СтрокиРезервы = ТаблицаРезервы.НайтиСтроки(СтруктураОтбора);
		Для каждого НайденнаяСтрока Из СтрокиРезервы Цикл
			
			ДоступныйРезерв = -Мин(НайденнаяСтрока.ДвиженияДокумента, НайденнаяСтрока.Остаток);
			Если ДоступныйРезерв <=0 Тогда
				Продолжить;
			КонецЕсли; 
			
			СтруктураОтбора = Новый Структура(ПоляСкладскойУчет());
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
			СтруктураОтбора.Вставить("Организация", НайденнаяСтрока.Организация);
			СтрокиСклад = ТаблицаОстатковСклад.НайтиСтроки(СтруктураОтбора);
			
			СтруктураОтбора = Новый Структура(ПоляЗапасы());
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
			СтруктураОтбора.Вставить("Организация", НайденнаяСтрока.Организация);
			СтрокиЗапасы = ТаблицаОстатковЗапасов.НайтиСтроки(СтруктураОтбора);
			
			СтруктураОтбора = Новый Структура(ПоляУчетГТД());
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
			СтруктураОтбора.Вставить("Организация", НайденнаяСтрока.Организация);
			СтрокиГТД = ТаблицаОстатковГТД.НайтиСтроки(СтруктураОтбора);
			
			Остаток = МинимальныйОстатокПоУчетам(СтрокаТовара, ДоступныйРезерв, СтрокиЗапасы, СтрокиГТД, СтрокиСклад);
			Если Остаток <= 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			ПерваяСтрока = ТаблицаРезервыТоваровОрганизаций.Добавить();
			ЗаполнитьЗначенияСвойств(ПерваяСтрока, СтрокаТовара);
			ВтораяСтрока = ТаблицаРезервыТоваровОрганизаций.Добавить();
			ЗаполнитьЗначенияСвойств(ВтораяСтрока, ПерваяСтрока);
			ПерваяСтрока.КорОрганизация = НайденнаяСтрока.Организация;
			ВтораяСтрока.Организация = НайденнаяСтрока.Организация;
			ВтораяСтрока.ВидДвижения = ВидДвиженияНакопления.Расход;
			КПередаче = Мин(КоличествоТовараНеХватило, Остаток);
			ПерваяСтрока.Количество = КПередаче;
			ВтораяСтрока.Количество = ПерваяСтрока.Количество;
			НайденнаяСтрока.Остаток = НайденнаяСтрока.Остаток + КПередаче;
			НайденнаяСтрока.ДвиженияДокумента = НайденнаяСтрока.ДвиженияДокумента + КПередаче;
			ЗачестьПоОстаткам(СтрокиСклад, КПередаче);
			Если ПолучитьФункциональнуюОпцию("РезервированиеЗапасов") Тогда
				ЗачестьПоОстаткам(СтрокиЗапасы, КПередаче);
			КонецЕсли;
			Если ПолучитьФункциональнуюОпцию("УчетГТД") И ЗначениеЗаполнено(СтрокаТовара.НомерГТД) Тогда
				ЗачестьПоОстаткам(СтрокиГТД, КПередаче);
			КонецЕсли; 
			КоличествоТовараНеХватило = КоличествоТовараНеХватило - КПередаче;
			Если КоличествоТовараНеХватило <= 0 Тогда
				Прервать;
			КонецЕсли; 
			
		КонецЦикла; 
		
		Если КоличествоТовараНеХватило <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Вторая итерация: прочие организации, на которых есть остатки
		СтруктураОтбора = Новый Структура(ПоляСкладскойУчет());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("ОстатокБезПередачи", Ложь);
		СтрокиСклад = ТаблицаОстатковСклад.НайтиСтроки(СтруктураОтбора);
		Для Каждого НайденнаяСтрока Из СтрокиСклад Цикл
			
			СтруктураОтбора = Новый Структура(ПоляЗапасы());
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
			СтруктураОтбора.Вставить("Организация", НайденнаяСтрока.Организация);
			СтрокиЗапасы = ТаблицаОстатковЗапасов.НайтиСтроки(СтруктураОтбора);
			
			СтруктураОтбора = Новый Структура(ПоляУчетГТД());
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
			СтруктураОтбора.Вставить("Организация", НайденнаяСтрока.Организация);
			СтрокиГТД = ТаблицаОстатковГТД.НайтиСтроки(СтруктураОтбора);
			
			Остаток = МинимальныйОстатокПоУчетам(СтрокаТовара, НайденнаяСтрока.Остаток, СтрокиЗапасы, СтрокиГТД);
			Если Остаток <= 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			ПерваяСтрока = ТаблицаРезервыТоваровОрганизаций.Добавить();
			ЗаполнитьЗначенияСвойств(ПерваяСтрока, СтрокаТовара);
			ВтораяСтрока = ТаблицаРезервыТоваровОрганизаций.Добавить();
			ЗаполнитьЗначенияСвойств(ВтораяСтрока, ПерваяСтрока);
			ПерваяСтрока.КорОрганизация = НайденнаяСтрока.Организация;
			ВтораяСтрока.Организация = НайденнаяСтрока.Организация;
			ВтораяСтрока.ВидДвижения = ВидДвиженияНакопления.Расход;
			КПередаче = Мин(КоличествоТовараНеХватило, Остаток);
			ПерваяСтрока.Количество = КПередаче;
			ВтораяСтрока.Количество = ПерваяСтрока.Количество;
			НайденнаяСтрока.Остаток = НайденнаяСтрока.Остаток - КПередаче;
			Если ПолучитьФункциональнуюОпцию("РезервированиеЗапасов") Тогда
				ЗачестьПоОстаткам(СтрокиЗапасы, КПередаче);
			КонецЕсли;
			Если ПолучитьФункциональнуюОпцию("УчетГТД") И ЗначениеЗаполнено(СтрокаТовара.НомерГТД) Тогда
				ЗачестьПоОстаткам(СтрокиГТД, КПередаче);
			КонецЕсли; 
			КоличествоТовараНеХватило = КоличествоТовараНеХватило - КПередаче;
			Если КоличествоТовараНеХватило <= 0 Тогда
				Прервать;
			КонецЕсли; 
			
		КонецЦикла;
		
		Если КоличествоТовараНеХватило > 0 И КонтролироватьОстатки Тогда
			// Не удалось выполнить резерв товаров организаций
			ОшибкаПодбораОстатков(СтрокаТовара, КоличествоТовараНеХватило, Ошибки);
		КонецЕсли; 
		
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРезервыТоваровОрганизаций", ТаблицаРезервыТоваровОрганизаций);
	Для каждого ТекстОшибки Из Ошибки Цикл
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ДокументСсылка);
	КонецЦикла; 
	СтруктураДополнительныеСвойства.Вставить("ЕстьОшибкиПередачиТоваров", Ошибки.Количество() > 0);
	
КонецПроцедуры

// Функция - Договор передачи между организациями по умолчанию для денежного документа
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации	 - Организация документа
//  ВидОперации	 - ПеречислениеСсылка			 - Вид операции документа
// 
// Возвращаемое значение:
//  СправочникСсылка.ДоговорыКонтрагентов - Договор передачи по умолчанию
//
Функция ДоговорПередачиПоУмолчанию(Организация, ВидОперации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	(ДоговорыКонтрагентов.Владелец = &Контрагент
	|			ИЛИ ДоговорыКонтрагентов.Организация = &Организация)
	|	И ДоговорыКонтрагентов.ЭтоДоговорПередачиТоваровМеждуОрганизациями
	|	И НЕ ДоговорыКонтрагентов.ПометкаУдаления";
	
	Если ВидОперации = Перечисления.ВидыОперацийРасходИзКассы.НашейОрганизации
		ИЛИ ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.НашейОрганизации Тогда
		Запрос.УстановитьПараметр("Организация", Справочники.Организации.ПустаяСсылка());
		Запрос.УстановитьПараметр("Контрагент", Организация);
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтНашейОрганизации
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтНашейОрганизации Тогда
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	Иначе
		Возврат Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Договор;
	Иначе
		Возврат Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли; 
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаРезервыТоваровОрганизаций()
	
	Возврат
	"ВЫБРАТЬ
	|	ОстаткиИРезервы.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ОстаткиИРезервы.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОстатокБезПередачи,
	|	ОстаткиИРезервы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ОстаткиИРезервы.Номенклатура КАК Номенклатура,
	|	ОстаткиИРезервы.Характеристика КАК Характеристика,
	|	ОстаткиИРезервы.Партия КАК Партия,
	|	ОстаткиИРезервы.Ячейка КАК Ячейка,
	|	СУММА(ОстаткиИРезервы.КоличествоОстаток) + СУММА(ОстаткиИРезервы.КоличествоПоДокументу) + СУММА(ОстаткиИРезервы.КоличествоРезерв) + СУММА(ОстаткиИРезервы.РезервПоДокументу) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыНаСкладахОстатки.Организация КАК Организация,
	|		ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗапасыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыНаСкладахОстатки.Характеристика КАК Характеристика,
	|		ЗапасыНаСкладахОстатки.Партия КАК Партия,
	|		ЗапасыНаСкладахОстатки.Ячейка КАК Ячейка,
	|		ЗапасыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|		0 КАК КоличествоРезерв,
	|		0 КАК КоличествоПоДокументу,
	|		0 КАК РезервПоДокументу
	|	ИЗ
	|		РегистрНакопления.ЗапасыНаСкладах.Остатки(
	|				&МоментКонтроля,
	|				Организация В (&МассивОрганизаций)
	|					И (СтруктурнаяЕдиница, Ячейка, Номенклатура, Характеристика, Партия) В
	|						(ВЫБРАТЬ
	|							ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|							ВЫБОР
	|								КОГДА ЗапасыДляРезервовТоваровОрганизаций.Ячейка = НЕОПРЕДЕЛЕНО
	|									ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|								ИНАЧЕ ЗапасыДляРезервовТоваровОрганизаций.Ячейка
	|							КОНЕЦ,
	|							ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|							ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|							ЗапасыДляРезервовТоваровОрганизаций.Партия
	|						ИЗ
	|							ЗапасыДляРезервовТоваровОрганизаций)) КАК ЗапасыНаСкладахОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗапасыНаСкладах.Организация,
	|		ЗапасыНаСкладах.СтруктурнаяЕдиница,
	|		ЗапасыНаСкладах.Номенклатура,
	|		ЗапасыНаСкладах.Характеристика,
	|		ЗапасыНаСкладах.Партия,
	|		ЗапасыНаСкладах.Ячейка,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА ЗапасыНаСкладах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЗапасыНаСкладах.Количество
	|			ИНАЧЕ -ЗапасыНаСкладах.Количество
	|		КОНЕЦ,
	|		0
	|	ИЗ
	|		РегистрНакопления.ЗапасыНаСкладах КАК ЗапасыНаСкладах
	|	ГДЕ
	|		ЗапасыНаСкладах.Регистратор = &Ссылка
	|		И ЗапасыНаСкладах.Организация В(&МассивОрганизаций)
	|		И (ЗапасыНаСкладах.СтруктурнаяЕдиница, ЗапасыНаСкладах.Ячейка, ЗапасыНаСкладах.Номенклатура, ЗапасыНаСкладах.Характеристика, ЗапасыНаСкладах.Партия) В
	|				(ВЫБРАТЬ
	|					ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|					ВЫБОР
	|						КОГДА ЗапасыДляРезервовТоваровОрганизаций.Ячейка = НЕОПРЕДЕЛЕНО
	|							ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|						ИНАЧЕ ЗапасыДляРезервовТоваровОрганизаций.Ячейка
	|					КОНЕЦ,
	|					ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|					ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|					ЗапасыДляРезервовТоваровОрганизаций.Партия
	|				ИЗ
	|					ЗапасыДляРезервовТоваровОрганизаций)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизацийОстатки.Организация,
	|		РезервыТоваровОрганизацийОстатки.СтруктурнаяЕдиница,
	|		РезервыТоваровОрганизацийОстатки.Номенклатура,
	|		РезервыТоваровОрганизацийОстатки.Характеристика,
	|		РезервыТоваровОрганизацийОстатки.Партия,
	|		РезервыТоваровОрганизацийОстатки.Ячейка,
	|		0,
	|		РезервыТоваровОрганизацийОстатки.КоличествоОстаток,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
	|				&МоментКонтроля,
	|				Организация В (&МассивОрганизаций)
	|					И (СтруктурнаяЕдиница, Ячейка, Номенклатура, Характеристика, Партия) В
	|						(ВЫБРАТЬ
	|							ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|							ВЫБОР
	|								КОГДА ЗапасыДляРезервовТоваровОрганизаций.Ячейка = НЕОПРЕДЕЛЕНО
	|									ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|								ИНАЧЕ ЗапасыДляРезервовТоваровОрганизаций.Ячейка
	|							КОНЕЦ,
	|							ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|							ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|							ЗапасыДляРезервовТоваровОрганизаций.Партия
	|						ИЗ
	|							ЗапасыДляРезервовТоваровОрганизаций)) КАК РезервыТоваровОрганизацийОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизаций.Организация,
	|		РезервыТоваровОрганизаций.СтруктурнаяЕдиница,
	|		РезервыТоваровОрганизаций.Номенклатура,
	|		РезервыТоваровОрганизаций.Характеристика,
	|		РезервыТоваровОрганизаций.Партия,
	|		РезервыТоваровОрганизаций.Ячейка,
	|		0,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -РезервыТоваровОрганизаций.Количество
	|			ИНАЧЕ РезервыТоваровОрганизаций.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|	ГДЕ
	|		РезервыТоваровОрганизаций.Регистратор = &Ссылка
	|		И РезервыТоваровОрганизаций.Организация В(&МассивОрганизаций)
	|		И (РезервыТоваровОрганизаций.СтруктурнаяЕдиница, РезервыТоваровОрганизаций.Ячейка, РезервыТоваровОрганизаций.Номенклатура, РезервыТоваровОрганизаций.Характеристика, РезервыТоваровОрганизаций.Партия) В
	|				(ВЫБРАТЬ
	|					ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|					ВЫБОР
	|						КОГДА ЗапасыДляРезервовТоваровОрганизаций.Ячейка = НЕОПРЕДЕЛЕНО
	|							ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|						ИНАЧЕ ЗапасыДляРезервовТоваровОрганизаций.Ячейка
	|					КОНЕЦ,
	|					ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|					ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|					ЗапасыДляРезервовТоваровОрганизаций.Партия
	|				ИЗ
	|					ЗапасыДляРезервовТоваровОрганизаций)) КАК ОстаткиИРезервы
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИРезервы.Ячейка,
	|	ОстаткиИРезервы.Партия,
	|	ОстаткиИРезервы.Номенклатура,
	|	ОстаткиИРезервы.Характеристика,
	|	ОстаткиИРезервы.Организация,
	|	ОстаткиИРезервы.СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ОстаткиИРезервы.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиИРезервы.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ОстаткиИРезервы.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОстатокБезПередачи,
	|	ОстаткиИРезервы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ОстаткиИРезервы.Номенклатура КАК Номенклатура,
	|	ОстаткиИРезервы.Характеристика КАК Характеристика,
	|	ОстаткиИРезервы.Партия КАК Партия,
	|	СУММА(ОстаткиИРезервы.КоличествоОстаток) + СУММА(ОстаткиИРезервы.КоличествоПоДокументу) + СУММА(ОстаткиИРезервы.КоличествоРезерв) + СУММА(ОстаткиИРезервы.РезервПоДокументу) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыОстатки.Организация КАК Организация,
	|		ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыОстатки.Характеристика КАК Характеристика,
	|		ЗапасыОстатки.Партия КАК Партия,
	|		ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|		0 КАК КоличествоРезерв,
	|		0 КАК КоличествоПоДокументу,
	|		0 КАК РезервПоДокументу
	|	ИЗ
	|		РегистрНакопления.Запасы.Остатки(
	|				&МоментКонтроля,
	|				Организация В (&МассивОрганизаций)
	|					И (СтруктурнаяЕдиница, Номенклатура, Характеристика, Партия) В
	|						(ВЫБРАТЬ
	|							ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|							ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|							ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|							ЗапасыДляРезервовТоваровОрганизаций.Партия
	|						ИЗ
	|							ЗапасыДляРезервовТоваровОрганизаций)
	|					И ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)) КАК ЗапасыОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Запасы.Организация,
	|		Запасы.СтруктурнаяЕдиница,
	|		Запасы.Номенклатура,
	|		Запасы.Характеристика,
	|		Запасы.Партия,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА Запасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА Запасы.Количество
	|			ИНАЧЕ -Запасы.Количество
	|		КОНЕЦ,
	|		0
	|	ИЗ
	|		РегистрНакопления.Запасы КАК Запасы
	|	ГДЕ
	|		Запасы.Регистратор = &Ссылка
	|		И Запасы.Организация В(&МассивОрганизаций)
	|		И (Запасы.СтруктурнаяЕдиница, Запасы.Номенклатура, Запасы.Характеристика, Запасы.Партия) В
	|				(ВЫБРАТЬ
	|					ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|					ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|					ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|					ЗапасыДляРезервовТоваровОрганизаций.Партия
	|				ИЗ
	|					ЗапасыДляРезервовТоваровОрганизаций)
	|		И Запасы.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизацийОстатки.Организация,
	|		РезервыТоваровОрганизацийОстатки.СтруктурнаяЕдиница,
	|		РезервыТоваровОрганизацийОстатки.Номенклатура,
	|		РезервыТоваровОрганизацийОстатки.Характеристика,
	|		РезервыТоваровОрганизацийОстатки.Партия,
	|		0,
	|		РезервыТоваровОрганизацийОстатки.КоличествоОстаток,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
	|				&МоментКонтроля,
	|				Организация В (&МассивОрганизаций)
	|					И (СтруктурнаяЕдиница, Номенклатура, Характеристика, Партия) В
	|						(ВЫБРАТЬ
	|							ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|							ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|							ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|							ЗапасыДляРезервовТоваровОрганизаций.Партия
	|						ИЗ
	|							ЗапасыДляРезервовТоваровОрганизаций)) КАК РезервыТоваровОрганизацийОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизаций.Организация,
	|		РезервыТоваровОрганизаций.СтруктурнаяЕдиница,
	|		РезервыТоваровОрганизаций.Номенклатура,
	|		РезервыТоваровОрганизаций.Характеристика,
	|		РезервыТоваровОрганизаций.Партия,
	|		0,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -РезервыТоваровОрганизаций.Количество
	|			ИНАЧЕ РезервыТоваровОрганизаций.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|	ГДЕ
	|		РезервыТоваровОрганизаций.Регистратор = &Ссылка
	|		И РезервыТоваровОрганизаций.Организация В(&МассивОрганизаций)
	|		И (РезервыТоваровОрганизаций.СтруктурнаяЕдиница, РезервыТоваровОрганизаций.Номенклатура, РезервыТоваровОрганизаций.Характеристика, РезервыТоваровОрганизаций.Партия) В
	|				(ВЫБРАТЬ
	|					ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|					ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|					ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|					ЗапасыДляРезервовТоваровОрганизаций.Партия
	|				ИЗ
	|					ЗапасыДляРезервовТоваровОрганизаций)) КАК ОстаткиИРезервы
	|ГДЕ
	|	&РезервированиеЗапасов
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИРезервы.Партия,
	|	ОстаткиИРезервы.Номенклатура,
	|	ОстаткиИРезервы.Характеристика,
	|	ОстаткиИРезервы.Организация,
	|	ОстаткиИРезервы.СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ОстаткиИРезервы.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиИРезервы.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ОстаткиИРезервы.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОстатокБезПередачи,
	|	ОстаткиИРезервы.НомерГТД КАК НомерГТД,
	|	ОстаткиИРезервы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ОстаткиИРезервы.Номенклатура КАК Номенклатура,
	|	ОстаткиИРезервы.Характеристика КАК Характеристика,
	|	ОстаткиИРезервы.Партия КАК Партия,
	|	СУММА(ОстаткиИРезервы.КоличествоОстаток) + СУММА(ОстаткиИРезервы.КоличествоПоДокументу) + СУММА(ОстаткиИРезервы.КоличествоРезерв) + СУММА(ОстаткиИРезервы.РезервПоДокументу) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыВРазрезеГТДОстатки.Организация КАК Организация,
	|		ЗапасыВРазрезеГТДОстатки.НомерГТД КАК НомерГТД,
	|		ЗапасыВРазрезеГТДОстатки.СтранаПроисхождения КАК СтранаПроисхождения,
	|		ЗапасыВРазрезеГТДОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыВРазрезеГТДОстатки.Характеристика КАК Характеристика,
	|		ЗапасыВРазрезеГТДОстатки.Партия КАК Партия,
	|		ЗапасыВРазрезеГТДОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|		0 КАК КоличествоРезерв,
	|		0 КАК КоличествоПоДокументу,
	|		0 КАК РезервПоДокументу
	|	ИЗ
	|		РегистрНакопления.ЗапасыВРазрезеГТД.Остатки(
	|				&МоментКонтроля,
	|				Организация В (&МассивОрганизаций)
	|					И НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|					И (НомерГТД, СтранаПроисхождения, Номенклатура, Характеристика, Партия) В
	|						(ВЫБРАТЬ
	|							ЗапасыДляРезервовТоваровОрганизаций.НомерГТД,
	|							ЗапасыДляРезервовТоваровОрганизаций.СтранаПроисхождения,
	|							ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|							ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|							ЗапасыДляРезервовТоваровОрганизаций.Партия
	|						ИЗ
	|							ЗапасыДляРезервовТоваровОрганизаций)) КАК ЗапасыВРазрезеГТДОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗапасыВРазрезеГТД.Организация,
	|		ЗапасыВРазрезеГТД.НомерГТД,
	|		ЗапасыВРазрезеГТД.СтранаПроисхождения,
	|		ЗапасыВРазрезеГТД.Номенклатура,
	|		ЗапасыВРазрезеГТД.Характеристика,
	|		ЗапасыВРазрезеГТД.Партия,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА ЗапасыВРазрезеГТД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЗапасыВРазрезеГТД.Количество
	|			ИНАЧЕ -ЗапасыВРазрезеГТД.Количество
	|		КОНЕЦ,
	|		0
	|	ИЗ
	|		РегистрНакопления.ЗапасыВРазрезеГТД КАК ЗапасыВРазрезеГТД
	|	ГДЕ
	|		ЗапасыВРазрезеГТД.Регистратор = &Ссылка
	|		И ЗапасыВРазрезеГТД.Организация В(&МассивОрганизаций)
	|		И ЗапасыВРазрезеГТД.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|		И (ЗапасыВРазрезеГТД.НомерГТД, ЗапасыВРазрезеГТД.СтранаПроисхождения, ЗапасыВРазрезеГТД.Номенклатура, ЗапасыВРазрезеГТД.Характеристика, ЗапасыВРазрезеГТД.Партия) В
	|				(ВЫБРАТЬ
	|					ЗапасыДляРезервовТоваровОрганизаций.НомерГТД,
	|					ЗапасыДляРезервовТоваровОрганизаций.СтранаПроисхождения,
	|					ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|					ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|					ЗапасыДляРезервовТоваровОрганизаций.Партия
	|				ИЗ
	|					ЗапасыДляРезервовТоваровОрганизаций)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизацийОстатки.Организация,
	|		РезервыТоваровОрганизацийОстатки.НомерГТД,
	|		РезервыТоваровОрганизацийОстатки.СтранаПроисхождения,
	|		РезервыТоваровОрганизацийОстатки.Номенклатура,
	|		РезервыТоваровОрганизацийОстатки.Характеристика,
	|		РезервыТоваровОрганизацийОстатки.Партия,
	|		0,
	|		РезервыТоваровОрганизацийОстатки.КоличествоОстаток,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
	|				&МоментКонтроля,
	|				Организация В (&МассивОрганизаций)
	|					И НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|					И (НомерГТД, СтранаПроисхождения, Номенклатура, Характеристика, Партия) В
	|						(ВЫБРАТЬ
	|							ЗапасыДляРезервовТоваровОрганизаций.НомерГТД,
	|							ЗапасыДляРезервовТоваровОрганизаций.СтранаПроисхождения,
	|							ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|							ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|							ЗапасыДляРезервовТоваровОрганизаций.Партия
	|						ИЗ
	|							ЗапасыДляРезервовТоваровОрганизаций)) КАК РезервыТоваровОрганизацийОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизаций.Организация,
	|		РезервыТоваровОрганизаций.НомерГТД,
	|		РезервыТоваровОрганизаций.СтранаПроисхождения,
	|		РезервыТоваровОрганизаций.Номенклатура,
	|		РезервыТоваровОрганизаций.Характеристика,
	|		РезервыТоваровОрганизаций.Партия,
	|		0,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -РезервыТоваровОрганизаций.Количество
	|			ИНАЧЕ РезервыТоваровОрганизаций.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|	ГДЕ
	|		РезервыТоваровОрганизаций.Регистратор = &Ссылка
	|		И РезервыТоваровОрганизаций.Организация В(&МассивОрганизаций)
	|		И РезервыТоваровОрганизаций.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|		И (РезервыТоваровОрганизаций.НомерГТД, РезервыТоваровОрганизаций.СтранаПроисхождения, РезервыТоваровОрганизаций.Номенклатура, РезервыТоваровОрганизаций.Характеристика, РезервыТоваровОрганизаций.Партия) В
	|				(ВЫБРАТЬ
	|					ЗапасыДляРезервовТоваровОрганизаций.НомерГТД,
	|					ЗапасыДляРезервовТоваровОрганизаций.СтранаПроисхождения,
	|					ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|					ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|					ЗапасыДляРезервовТоваровОрганизаций.Партия
	|				ИЗ
	|					ЗапасыДляРезервовТоваровОрганизаций)) КАК ОстаткиИРезервы
	|ГДЕ
	|	&УчетГТД
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИРезервы.Партия,
	|	ОстаткиИРезервы.Номенклатура,
	|	ОстаткиИРезервы.Характеристика,
	|	ОстаткиИРезервы.Организация,
	|	ОстаткиИРезервы.НомерГТД,
	|	ОстаткиИРезервы.СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ОстаткиИРезервы.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезервыТоваровОрганизаций.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА РезервыТоваровОрганизаций.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОстатокБезПередачи,
	|	РезервыТоваровОрганизаций.Номенклатура КАК Номенклатура,
	|	РезервыТоваровОрганизаций.Характеристика КАК Характеристика,
	|	РезервыТоваровОрганизаций.Партия КАК Партия,
	|	РезервыТоваровОрганизаций.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	РезервыТоваровОрганизаций.Ячейка КАК Ячейка,
	|	РезервыТоваровОрганизаций.НомерГТД КАК НомерГТД,
	|	РезервыТоваровОрганизаций.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(ВЫБОР
	|			КОГДА РезервыТоваровОрганизаций.Регистратор = &Ссылка
	|				ТОГДА 0
	|			КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -РезервыТоваровОрганизаций.Количество
	|			ИНАЧЕ РезервыТоваровОрганизаций.Количество
	|		КОНЕЦ) КАК Остаток,
	|	СУММА(ВЫБОР
	|			КОГДА РезервыТоваровОрганизаций.Регистратор <> &Ссылка
	|				ТОГДА 0
	|			КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РезервыТоваровОрганизаций.Количество
	|			ИНАЧЕ -РезервыТоваровОрганизаций.Количество
	|		КОНЕЦ) КАК ДвиженияДокумента
	|ИЗ
	|	РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|ГДЕ
	|	РезервыТоваровОрганизаций.Период МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаДокумента, ДЕНЬ) И КОНЕЦПЕРИОДА(&ДатаДокумента, ДЕНЬ)
	|	И (РезервыТоваровОрганизаций.СтруктурнаяЕдиница, РезервыТоваровОрганизаций.Ячейка, РезервыТоваровОрганизаций.НомерГТД, РезервыТоваровОрганизаций.СтранаПроисхождения, РезервыТоваровОрганизаций.Номенклатура, РезервыТоваровОрганизаций.Характеристика, РезервыТоваровОрганизаций.Партия) В
	|			(ВЫБРАТЬ
	|				ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|				ВЫБОР
	|					КОГДА ЗапасыДляРезервовТоваровОрганизаций.Ячейка = НЕОПРЕДЕЛЕНО
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|					ИНАЧЕ ЗапасыДляРезервовТоваровОрганизаций.Ячейка
	|				КОНЕЦ,
	|				ЗапасыДляРезервовТоваровОрганизаций.НомерГТД,
	|				ЗапасыДляРезервовТоваровОрганизаций.СтранаПроисхождения,
	|				ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|				ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|				ЗапасыДляРезервовТоваровОрганизаций.Партия
	|			ИЗ
	|				ЗапасыДляРезервовТоваровОрганизаций)
	|	И (РезервыТоваровОрганизаций.Организация = &Организация
	|			ИЛИ РезервыТоваровОрганизаций.КорОрганизация = &Организация)
	|
	|СГРУППИРОВАТЬ ПО
	|	РезервыТоваровОрганизаций.Организация,
	|	ВЫБОР
	|		КОГДА РезервыТоваровОрганизаций.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	РезервыТоваровОрганизаций.Номенклатура,
	|	РезервыТоваровОрганизаций.Характеристика,
	|	РезервыТоваровОрганизаций.Партия,
	|	РезервыТоваровОрганизаций.СтруктурнаяЕдиница,
	|	РезервыТоваровОрганизаций.Ячейка,
	|	РезервыТоваровОрганизаций.НомерГТД,
	|	РезервыТоваровОрганизаций.СтранаПроисхождения
	|
	|УПОРЯДОЧИТЬ ПО
	|	РезервыТоваровОрганизаций.Номенклатура,
	|	РезервыТоваровОрганизаций.Характеристика,
	|	РезервыТоваровОрганизаций.Партия,
	|	РезервыТоваровОрганизаций.СтруктурнаяЕдиница,
	|	РезервыТоваровОрганизаций.Ячейка,
	|	РезервыТоваровОрганизаций.НомерГТД,
	|	РезервыТоваровОрганизаций.СтранаПроисхождения,
	|	ОстатокБезПередачи,
	|	РезервыТоваровОрганизаций.Организация,
	|	ДвиженияДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыНаСкладах.НомерСтроки КАК НомерСтроки,
	|	ЗапасыНаСкладах.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ЗапасыНаСкладах.Организация КАК Организация,
	|	ЗапасыНаСкладах.Организация КАК КорОрганизация,
	|	ЗапасыНаСкладах.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗапасыНаСкладах.Номенклатура КАК Номенклатура,
	|	ЗапасыНаСкладах.Характеристика КАК Характеристика,
	|	ЗапасыНаСкладах.Партия КАК Партия,
	|	ВЫБОР
	|		КОГДА ЗапасыНаСкладах.Ячейка = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|		ИНАЧЕ ЗапасыНаСкладах.Ячейка
	|	КОНЕЦ КАК Ячейка,
	|	ЗапасыНаСкладах.НомерГТД КАК НомерГТД,
	|	ЗапасыНаСкладах.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЗапасыНаСкладах.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ЗапасыНаСкладах.Количество КАК Количество
	|ИЗ
	|	ЗапасыДляРезервовТоваровОрганизаций КАК ЗапасыНаСкладах";
	
КонецФункции

Функция КоличествоРезерва(СтрокаТовара, СтрокиЗапасы, СтрокиСклад, СтрокиГТД, СтрокиРезервы)
	
	УчетРезервов = ПолучитьФункциональнуюОпцию("РезервированиеЗапасов");
	УчетГТД = ПолучитьФункциональнуюОпцию("УчетГТД") И ЗначениеЗаполнено(СтрокаТовара.НомерГТД);
	
	// Определение остатков разным учетам
	ОстатокЗапасы = 0;
	Для каждого СтрокаТаблицы Из СтрокиЗапасы Цикл
		ОстатокЗапасы = ОстатокЗапасы + СтрокаТаблицы.Остаток;
	КонецЦикла;  
	ОстатокСклад = 0;
	Для каждого СтрокаТаблицы Из СтрокиСклад Цикл
		ОстатокСклад = ОстатокСклад + СтрокаТаблицы.Остаток;
	КонецЦикла;  
	ОстатокГТД = 0;
	Если УчетГТД Тогда
		Для каждого СтрокаТаблицы Из СтрокиГТД Цикл
			ОстатокГТД = ОстатокГТД + СтрокаТаблицы.Остаток;
		КонецЦикла;
	КонецЕсли;
	ОстатокРезерв = 0;
	
	// Проверки перепроведения документа, выполнявшего резерв
	// Обеспеченный передачей товаров резерв отменять нельзя
	Для каждого СтрокаТаблицы Из СтрокиРезервы Цикл
		ОстатокРезерв = ОстатокРезерв + Мин(СтрокаТаблицы.Остаток, СтрокаТаблицы.ДвиженияДокумента);
	КонецЦикла;  
	Если ОстатокРезерв < 0 Тогда
		ОстатокРезерв = 0;
	КонецЕсли;
	ОстатокРезерв = Мин(ОстатокРезерв, СтрокаТовара.Количество);
	
	// Расчет количества, отгружаемого из остатков
	Остаток = ОстатокСклад;
	Если УчетРезервов Тогда
		Остаток = Мин(Остаток, ОстатокЗапасы);
	КонецЕсли;
	Если УчетГТД Тогда
		Остаток = Мин(Остаток, ОстатокГТД);
	КонецЕсли;
	КоличествоТовараНеХватило = СтрокаТовара.Количество - Остаток;
	Если ОстатокРезерв > 0 И ОстатокРезерв > КоличествоТовараНеХватило Тогда
		КоличествоТовараНеХватило = ОстатокРезерв;
		Зачтено = СтрокаТовара.Количество - ОстатокРезерв;
	ИначеЕсли КоличествоТовараНеХватило <= 0 Тогда
		Зачтено = СтрокаТовара.Количество;
	Иначе
		Зачтено = Остаток;
	КонецЕсли;
	
	// Уменьшение остатков на зачтенное количество
	Если Зачтено > 0 Тогда
		ЗачестьПоОстаткам(СтрокиСклад, Зачтено);
		Если УчетРезервов Тогда
			ЗачестьПоОстаткам(СтрокиЗапасы, Зачтено);
		КонецЕсли;
		Если УчетГТД Тогда
			ЗачестьПоОстаткам(СтрокиГТД, Зачтено);
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат Мин(КоличествоТовараНеХватило, СтрокаТовара.Количество);
		
КонецФункции

Процедура ЗачестьПоОстаткам(СтрокиОстатков, Зачтено)
	
	ОстатокЗачета = Зачтено;
	Для каждого СтрокаТаблицы Из СтрокиОстатков Цикл
		ПоПозиции = Мин(СтрокаТаблицы.Остаток, ОстатокЗачета);
		СтрокаТаблицы.Остаток = СтрокаТаблицы.Остаток - ПоПозиции;
		ОстатокЗачета = ОстатокЗачета - ПоПозиции;
		Если ОстатокЗачета <= 0 Тогда
			Прервать;
		КонецЕсли; 
	КонецЦикла;  
	
КонецПроцедуры

Функция МинимальныйОстатокПоУчетам(СтрокаТовара, ОстатокСкладРезерв, СтрокиЗапасы, СтрокиГТД, СтрокиСклад = Неопределено)
	
	ОстатокЗапасы = 0;
	Для каждого СтрокаТаблицы Из СтрокиЗапасы Цикл
		ОстатокЗапасы = ОстатокЗапасы + СтрокаТаблицы.Остаток;
	КонецЦикла;  
	ОстатокГТД = 0;
	Для каждого СтрокаТаблицы Из СтрокиГТД Цикл
		ОстатокГТД = ОстатокГТД + СтрокаТаблицы.Остаток;
	КонецЦикла;
	ОстатокСклад = 0;
	Если СтрокиСклад = Неопределено Тогда
		ОстатокСклад = ОстатокСкладРезерв;
	Иначе
		Для каждого СтрокаТаблицы Из СтрокиСклад Цикл
			ОстатокСклад = ОстатокСклад + СтрокаТаблицы.Остаток;
		КонецЦикла;  
	КонецЕсли; 
	Возврат Мин(ОстатокСкладРезерв, ОстатокСклад, 
		?(ПолучитьФункциональнуюОпцию("РезервированиеЗапасов"), ОстатокЗапасы, ОстатокСкладРезерв), 
		?(ПолучитьФункциональнуюОпцию("УчетГТД") И ЗначениеЗаполнено(СтрокаТовара.НомерГТД), ОстатокГТД, ОстатокСкладРезерв));
	
КонецФункции
 
// Сообщает об ошибках подбора остатков по регистру РезервыТоваровОрганизаций.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка - проводимый документ,
//  СтрокаТовара - СтрокаТабличнойЧасти - строка с данными номенклатуры,
//  Нехватка - Число - количество, для которого недостаточно остатка
Процедура ОшибкаПодбораОстатков(СтрокаТовара, Нехватка, Ошибки)
	
	Если Ошибки.Количество() = 0 Тогда
		ТекстЗаголовкаСообщения = НСтр("ru = 'Ошибка:
	    	|Не хватает остатка для передачи товаров организаций'");
		Ошибки.Добавить(ТекстЗаголовкаСообщения);
	КонецЕсли; 
	
	ШаблонСообщения = НСтр("ru = 'Номенклатура: %НоменклатураХарактеристикаПартия%,
		|недостаточно %КоличествоИРезерв% %ЕдиницаИзмерения%'");
	
	ПредставлениеНоменклатуры = Справочники.Номенклатура.Представление(СтрокаТовара.Номенклатура,
		СтрокаТовара.Характеристика, СтрокаТовара.Партия);
	ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%НоменклатураХарактеристикаПартия%", ПредставлениеНоменклатуры);
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КоличествоИРезерв%", Нехватка);
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ЕдиницаИзмерения%", 
		СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТовара.Номенклатура, "ЕдиницаИзмерения")));
		
	Ошибки.Добавить(ТекстСообщения);
		
КонецПроцедуры

#Область ФиксированныеСтроки

Функция ПоляРезервы()
	Возврат "Номенклатура, Характеристика, Партия, СтруктурнаяЕдиница, Ячейка, НомерГТД, СтранаПроисхождения";		
КонецФункции
 
Функция ПоляСкладскойУчет()
	Возврат "Номенклатура, Характеристика, Партия, СтруктурнаяЕдиница, Ячейка";		
КонецФункции
 
Функция ПоляЗапасы()
	Возврат "Номенклатура, Характеристика, Партия, СтруктурнаяЕдиница";		
КонецФункции
 
Функция ПоляУчетГТД()
	Возврат "Номенклатура, Характеристика, Партия, НомерГТД, СтранаПроисхождения";		
КонецФункции

#КонецОбласти 

#КонецОбласти 


 