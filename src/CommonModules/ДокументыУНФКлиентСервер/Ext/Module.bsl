
#Область СлужебныйПрограммныйИнтерфейс

Процедура ПолучитьПредставлениеПолейДокумента(Источник, Поля, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Ссылка");
	Поля.Добавить("Дата");
	Поля.Добавить("Номер");
	Поля.Добавить("Проведен");
	Поля.Добавить("ПометкаУдаления");
	Поля.Добавить("НомерИсправления");
	Поля.Добавить("НомерВходящегоДокумента");
	Поля.Добавить("ВидОперации");
	
КонецПроцедуры

Процедура ПолучитьПредставлениеПолейДокументаТолькоЗапись(Источник, Поля, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Ссылка");
	Поля.Добавить("Дата");
	Поля.Добавить("Номер");
	Поля.Добавить("ПометкаУдаления");
	
КонецПроцедуры

Процедура ПолучитьПредставлениеДокумента(Источник, Данные, Представление, СтандартнаяОбработка) Экспорт

	Если Данные.Номер = Null Или Не ЗначениеЗаполнено(Данные.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	Представление = ПредставлениеДокумента(Данные, Источник.ПустаяСсылка());
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

Процедура ПолучитьПредставлениеДокументаСВходящимНомером(Источник, Данные, Представление, СтандартнаяОбработка) Экспорт
	
	Если Данные.Номер = Null Или Не ЗначениеЗаполнено(Данные.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	Представление = ПредставлениеДокумента(Данные, Источник.ПустаяСсылка(), Истина);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеДокумента(Данные, ПустаяСсылкаОбъекта = Неопределено, СВходящимНомером = Ложь)

	КомпонентыПредставления = Новый Массив;

	ПредставленияОбъектовМетаданных = ДокументыУНФКлиентСерверПовтИсп.ПредставленияОбъектовМетаданных();
	КомпонентыПредставления.Добавить(ПредставленияОбъектовМетаданных[ПустаяСсылкаОбъекта]);

	Если Данные.Свойство("Номер") Тогда
		КомпонентыПредставления.Добавить(ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Данные.Номер, Истина,
			Истина));
	КонецЕсли;
	
	Если СВходящимНомером Тогда
		ПредставлениеВходящегоНомера = ПредставлениеВходящегоНомера(Данные);
		Если ЗначениеЗаполнено(ПредставлениеВходящегоНомера) Тогда
			КомпонентыПредставления.Добавить(ПредставлениеВходящегоНомера);
		КонецЕсли;
	КонецЕсли;
	
	КомпонентыПредставления.Добавить(НСтр("ru = 'от'"));
	КомпонентыПредставления.Добавить(Формат(Данные.Дата, "ДЛФ=D"));
	Если Данные.ПометкаУдаления Тогда
		КомпонентыПредставления.Добавить(НСтр("ru='(удален)'"));
	КонецЕсли;
	Если Данные.Свойство("Проведен") И Не Данные.Проведен Тогда
		КомпонентыПредставления.Добавить(НСтр("ru='(не проведен)'"));
	КонецЕсли;

	Результат = СтрСоединить(КомпонентыПредставления, " ");
	Возврат Результат;

КонецФункции

Функция ПредставлениеВходящегоНомера(Данные)
	
	Если ТипЗнч(Данные.Ссылка) = Тип("ДокументСсылка.КорректировкаРеализации") 
		И Данные.ВидОперации = ПредопределенноеЗначение(
			"Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки")
		И ЗначениеЗаполнено(Данные.НомерИсправления) Тогда
		Возврат СтрШаблон(НСтр("ru='(Исправление № %1)'"), Данные.НомерИсправления);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Данные.НомерВходящегоДокумента) Тогда
		Возврат СтрШаблон(НСтр("ru='(вх. %1)'"), Данные.НомерВходящегоДокумента);
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

#КонецОбласти
