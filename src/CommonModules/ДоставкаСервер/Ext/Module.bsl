////////////////////////////////////////////////////////////////////////////////
// ДоставкаСервер: процедуры и функции расчета данных доставки.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Функция определяет вводился ли раннее элемент расчета
//
// Параметры:
//  ЗначениеИдентификатора	 - 	Строка - значение реквизита Идентификатор элемента справочника 
//								ПараметрыРасчетовДоставки
// 
// Возвращаемое значение:
//   Булево - Признак существования параметра расчета
//
Функция ПараметрРасчетаСуществует(ЗначениеИдентификатора) Экспорт
	
	Если ПустаяСтрока(ЗначениеИдентификатора)Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат НЕ Справочники.ПараметрыРасчетовДоставки.НайтиПоРеквизиту("Идентификатор", ЗначениеИдентификатора).Пустая();
	
КонецФункции // ПараметрРасчетаСуществует()

// Функция обновляет параметры расчета
//
// Параметры:
//  ПараметрыРасчетов	 - 	ТаблицаЗначений, ТабличнаяЧасть - таблица, в которую будут добавлены используемые 
//															параметры.
//							*Параметр - СправочникСсылка.ПараметрыРасчетовДоставки - параметр, 
//										значение которого содержится в таблице
//  ЗначенияФормулы		 - 	ТаблицаЗначений, ТабличнаяЧасть - таблица фиксированных значений, используемых в формуле
//							* Идентификатор - Строка - Идентификатор значения (как он отображается в формуле)
//							* Значение -      Произвольный - Само значение
//  ФормулаСтоимости	 - 	Строка - текст формулы расчета стоимости
//  ФормулаСебестоимости - 	Строка - текст формулы расчета себестоимости
//  Отказ				 - 	Булево - Признак ошибки разбора формул
//
Процедура ОбновитьПараметрыРасчета(ПараметрыРасчетов, ЗначенияФормулы, ФормулаСтоимости, 
	ФормулаСебестоимости, Отказ) Экспорт
	
	Ошибки = Неопределено;
	МассивПараметров = Новый Массив;
	
	ОшибкаВФормуле = Ложь;
	ДобавитьПараметрыВСтруктуру(ФормулаСтоимости, МассивПараметров, ОшибкаВФормуле);
	Если ОшибкаВФормуле Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
		Ошибки, 
		"Объект.ФормулаСтоимости", 
		НСтр("ru = 'Ошибка составления формулы расчета стоимости'"),
		"Объект.ФормулаСтоимости");
		Отказ = Истина;
	КонецЕсли;
	ОшибкаВФормуле = Ложь;
	ДобавитьПараметрыВСтруктуру(ФормулаСебестоимости, МассивПараметров, ОшибкаВФормуле);
	Если ОшибкаВФормуле Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
		Ошибки, 
		"Объект.ФормулаСебестоимости", 
		НСтр("ru = 'Ошибка составления формулы расчета себестоимости'"), 
		"Объект.ФормулаСебестоимости");
		Отказ = Истина;  
	КонецЕсли;
	Если Отказ Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		Возврат;
	КонецЕсли; 
	
	ТаблицаПараметров = Новый ТаблицаЗначений;
	ТаблицаПараметров.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(100)));
	Для каждого Параметр Из МассивПараметров Цикл
		Если Найти(Параметр, ".")>0 Тогда
			Продолжить;
		КонецЕсли; 
		ТаблицаПараметров.Добавить().Имя = Параметр;
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаПараметров", ТаблицаПараметров);
	Запрос.УстановитьПараметр("ЗначенияФормулы", ЗначенияФормулы.Выгрузить());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПараметров.Имя
	|ПОМЕСТИТЬ ТаблицаПараметров
	|ИЗ
	|	&ТаблицаПараметров КАК ТаблицаПараметров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗначенияФормулы.Идентификатор
	|ПОМЕСТИТЬ ЗначенияФормулы
	|ИЗ
	|	&ЗначенияФормулы КАК ЗначенияФормулы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыРасчетовДоставки.Ссылка КАК Параметр,
	|	ПараметрыРасчетовДоставки.Идентификатор
	|ИЗ
	|	Справочник.ПараметрыРасчетовДоставки КАК ПараметрыРасчетовДоставки
	|ГДЕ
	|	ПараметрыРасчетовДоставки.Идентификатор В
	|			(ВЫБРАТЬ
	|				ТаблицаПараметров.Имя
	|			ИЗ
	|				ТаблицаПараметров КАК ТаблицаПараметров)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПараметров.Имя
	|ИЗ
	|	ТаблицаПараметров КАК ТаблицаПараметров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПараметрыРасчетовДоставки КАК ПараметрыРасчетовДоставки
	|		ПО ТаблицаПараметров.Имя = ПараметрыРасчетовДоставки.Идентификатор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗначенияФормулы КАК ЗначенияФормулы
	|		ПО ТаблицаПараметров.Имя = ЗначенияФормулы.Идентификатор
	|ГДЕ
	|	ПараметрыРасчетовДоставки.Ссылка ЕСТЬ NULL 
	|	И ЗначенияФормулы.Идентификатор ЕСТЬ NULL ";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Если НЕ РезультатЗапроса.Получить(3).Пустой() Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
		Ошибки, 
		"Объект", 
		НСтр("ru = 'В формулах используются несуществующие параметры'"),
		"Объект");
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		Возврат;
	КонецЕсли;
	
	ВыборкаПараметров = РезультатЗапроса.Получить(2).Выбрать();
	Пока ВыборкаПараметров.Следующий() Цикл
		ПараметрыРасчетов.Добавить().Параметр = ВыборкаПараметров.Параметр;	
	КонецЦикла; 

КонецПроцедуры

// Функция выполняет расчет стоимости доставки по формуле.
// 
// Параметры:
// 	Контекст - ДанныеФормыСтруктура - данные заказа покупателя
// 	СтруктураПараметров - Структура - структура значений параметров, устанавливаемых вручную
// Возвращаемое значение:
// 	Число - рассчитанная стоимость доставки
//
Функция СтоимостьДоставки(Контекст, СтруктураПараметров) Экспорт
	
	СлужбаДоставки = Контекст.СлужбаДоставки;
	Если НЕ ЗначениеЗаполнено(СлужбаДоставки) Тогда
		Возврат 0;
	КонецЕсли; 
	
	СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СлужбаДоставки, "ВариантУчета, ФормулаСтоимости, ФормулаСебестоимости");
	
	Если СтруктураРеквизитов.ВариантУчета=Перечисления.ВариантыУчетаДоставки.БесплатнаяДоставка Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Службой ""%1"" доставка выполняется бесплатно'"), СлужбаДоставки);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, СлужбаДоставки, "ВариантУчета");
		Возврат 0;
	ИначеЕсли СтруктураРеквизитов.ВариантУчета=Перечисления.ВариантыУчетаДоставки.ВозмещениеСтоимости Тогда
		Формула = СтруктураРеквизитов.ФормулаСебестоимости;
		Если ПустаяСтрока(Формула) Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не задана формула расчета себестоимости доставки для службы ""%1""'",), СлужбаДоставки);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, СлужбаДоставки, "ФормулаСебестоимости");
			Возврат 0;
		КонецЕсли; 
	ИначеЕсли СтруктураРеквизитов.ВариантУчета=Перечисления.ВариантыУчетаДоставки.ДоставкаСОплатой Тогда
		Формула = СтруктураРеквизитов.ФормулаСтоимости;
		Если ПустаяСтрока(Формула) Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не задана формула расчета стоимости доставки для службы ""%1""'",), СлужбаДоставки);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, СлужбаДоставки, "ФормулаСтоимости");
			Возврат 0;
		КонецЕсли; 
	Иначе
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не задан вариант учета для службы ""%1""'"), СлужбаДоставки);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, СлужбаДоставки, "ВариантУчета");
		Возврат 0;
	КонецЕсли; 
	
	Стоимость = РассчитатьПоФормуле(Контекст, Формула, СтруктураПараметров);
	
	Возврат Стоимость;
	
КонецФункции

// Функция выполняет расчет себестоимости доставки по формуле.
//
// Параметры:
//  Контекст - ДанныеФормыСтруктура - данные заказа покупателя,
//  СтруктураПараметров - Структура - структура значений параметров, устанавливаемых вручную.
//
// Возвращаемое значение:
// 	Число - рассчитанная себестоимость доставки.
Функция СебестоимостьДоставки(Контекст, СтруктураПараметров) Экспорт
	
	СлужбаДоставки = Контекст.СлужбаДоставки;
	Если НЕ ЗначениеЗаполнено(СлужбаДоставки) Тогда
		Возврат 0;
	КонецЕсли; 
	
	СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СлужбаДоставки, "ВариантУчета, ФормулаСебестоимости");
	
	Если НЕ ЗначениеЗаполнено(СтруктураРеквизитов.ВариантУчета) Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не задан вариант учета для службы ""%1""'"), СлужбаДоставки);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, СлужбаДоставки, "ВариантУчета");
		Возврат 0;
	ИначеЕсли ПустаяСтрока(СтруктураРеквизитов.ФормулаСебестоимости) Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не задана формула расчета себестоимости доставки для службы ""%1""'",), СлужбаДоставки);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, СлужбаДоставки, "ФормулаСебестоимости");
		Возврат 0;
	КонецЕсли; 
	
	Себестоимость = РассчитатьПоФормуле(Контекст, СтруктураРеквизитов.ФормулаСебестоимости, СтруктураПараметров);
	
	Возврат Себестоимость;
	
КонецФункции

// Функция показывает используется ли доставка
//
// Параметры:
//  СпособДоставки	 - 	ПеречислениеСсылка.СпособыДоставки  - способ доставки
// 
// Возвращаемое значение:
//   Булево - признак использования доставки
//
Функция ИспользуетсяДоставка(СпособДоставки) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СпособДоставки) ИЛИ СпособДоставки=Перечисления.СпособыДоставки.Самовывоз Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли; 	
	
КонецФункции

#Область ИнтерфейсПечати 

// Функция возвращает признак необходимости вывода данных о доставке на печать.
// 
// Параметры:
// Шапка - ВыборкаДанныхЗапроса, Структура - Структура данных шапки печатаемого документа
//		Обязательные свойства:
//		СпособДоставки - ПеречислениеСсылка.СпособыДоставки - способ доставки заказа
//		НоменклатураДоставки - СправочникСсылка.Номенклатура - услуга, использующаяся для отражения доставки в учете
// 
// Возвращаемое значение:
//   Булево - признак возможности печати доставки
//
Функция ВозможностьПечатиДоставки(Шапка) Экспорт
	
	Если НЕ ИспользуетсяДоставка(Шапка.СпособДоставки) Тогда
		Возврат Ложь;
	КонецЕсли; 	
	Если Не ЗначениеЗаполнено(Шапка.НоменклатураДоставки) Тогда
		ОбщегоНазначения.СообщитьПользователю(
		НСтр("ru = 'Не заполнена услуга доставки. Печать невозможна'"),
		Шапка.Ссылка,
		"НоменклатураДоставки",
		"Объект");
		Возврат Неопределено;
	КонецЕсли;
	Возврат Истина;
	
КонецФункции

// Добавляет строку доставки в таблицу для печати доставки.
//
// Параметры:
//  ДанныеДокументов	 - ТаблицаЗначений - данные таблиц документа, в которые следует добавить строку доставки.
//  ИмяТабличнойЧасти	 - Строка - имя табличной части, в которую следует добавить строку доставки.
//
Процедура ДобавитьСтрокуДоставкиУниверсальныеДанные(ДанныеДокументов, ИмяТабличнойЧасти = "ТаблицаЗапасы") Экспорт
	
	ВариантПечатиДоставки = Константы.ВариантПечатиДоставки.Получить();
	
	ПоляНаименование = "ПредставлениеНоменклатуры, Запас, НаименованиеНоменклатуры, Содержание";
	ПоляКод = "ЗапасКод, ТоварКод, Код, КодПродукции";
	ПоляЕдиница = "ЕдиницаИзмерения";
	ПоляКодЕдиницы = "ЕдиницаИзмеренияПоОКЕИ_Код, ЕдиницаИзмеренияКод";
	ПоляНаименованиеЕдиницы = "ЕдиницаИзмеренияПоОКЕИ_Наименование, НаименованиеЕдиницыИзмерения, БазоваяЕдиницаНаименование";
	ПоляКоэффициент = "КоэффициентЕдиницыИзмерения, ЕдиницаИзмеренияКоэффициент";
	
	Для Каждого ТекСтрока Из ДанныеДокументов Цикл
		
		Если Не ЗначениеЗаполнено(ТекСтрока.НоменклатураДоставки) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекСтрока[ИмяТабличнойЧасти].Колонки.Найти("НомерВариантаКП")<>Неопределено Тогда
			ТаблицаВариантов = ТекСтрока[ИмяТабличнойЧасти].Скопировать(, "НомерВариантаКП");
			ТаблицаВариантов.Свернуть("НомерВариантаКП");
			МассивВариантовКП = ТаблицаВариантов.ВыгрузитьКолонку("НомерВариантаКП");
		Иначе
			МассивВариантовКП = Новый Массив;
			МассивВариантовКП.Добавить(0);
		КонецЕсли;
		
		ДатаОтгрузки = '0001-01-01';
		Если ТекСтрока[ИмяТабличнойЧасти].Колонки.Найти("ДатаОтгрузки")<>Неопределено Тогда
			Для каждого СтрокаЗапасы Из ТекСтрока[ИмяТабличнойЧасти] Цикл
				ДатаОтгрузки = Макс(ДатаОтгрузки, СтрокаЗапасы.ДатаОтгрузки);
			КонецЦикла; 
		КонецЕсли; 
		
		Для каждого НомерВариантаКП Из МассивВариантовКП Цикл
			
			Если ВариантПечатиДоставки = Перечисления.ВариантыПечатиДоставки.НеВыводитьБесплатнуюДоставку 
				И ТекСтрока.СтоимостьДоставки = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрокаДоставка = ТекСтрока[ИмяТабличнойЧасти].Добавить();
			ПривестиЗначенияКолонокСтроки(НоваяСтрокаДоставка);
			
			ДанныеСтроки = Новый Структура;
			ДанныеСтроки.Вставить("НомерВариантаКП", НомерВариантаКП);
			ДанныеСтроки.Вставить("НомерСтроки", ТекСтрока[ИмяТабличнойЧасти].Количество());
			
			// Наименование
			Для каждого ИмяПоля Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПоляНаименование) Цикл
				ДанныеСтроки.Вставить(ИмяПоля, ТекСтрока.ПредставлениеНоменклатурыДоставки);
			КонецЦикла; 
			
			// Код, артикул
			Если ДанныеДокументов.Колонки.Найти("КодДоставки")<>Неопределено Тогда
				Для каждого ИмяПоля Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПоляКод) Цикл
					ДанныеСтроки.Вставить(ИмяПоля, ТекСтрока.КодДоставки);
				КонецЦикла; 
			КонецЕсли; 
			Если ДанныеДокументов.Колонки.Найти("АртикулДоставки")<>Неопределено Тогда
				ДанныеСтроки.Вставить("Артикул", ТекСтрока.АртикулДоставки);
			КонецЕсли; 
			
			// Тип номенклатуры
			ДанныеСтроки.Вставить("ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Услуга);
			
			// Ставка НДС
			Если ДанныеДокументов.Колонки.Найти("СтавкаНДСДоставки") <> Неопределено Тогда
				ДанныеСтроки.Вставить("СтавкаНДС", ТекСтрока.СтавкаНДСДоставки);
			КонецЕсли;
			
			// Единица измерения
			Если ДанныеДокументов.Колонки.Найти("ЕдиницаИзмеренияДоставки")<>Неопределено Тогда
				Единица = ТекСтрока.ЕдиницаИзмеренияДоставки;
			Иначе
				Единица = Справочники.КлассификаторЕдиницИзмерения.шт;
			КонецЕсли;
			Для каждого ИмяПоля Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПоляЕдиница) Цикл
				ДанныеСтроки.Вставить(ИмяПоля, Единица);
			КонецЦикла; 
			НаименованиеЕдиницы = Строка(Единица);
			Для каждого ИмяПоля Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПоляНаименованиеЕдиницы) Цикл
				ДанныеСтроки.Вставить(ИмяПоля, НаименованиеЕдиницы);
			КонецЦикла; 
			Если ДанныеДокументов.Колонки.Найти("КодЕдиницыИзмеренияДоставки")<>Неопределено Тогда
				КодЕдиницы = ТекСтрока.КодЕдиницыИзмеренияДоставки;
			Иначе
				КодЕдиницы = ?(ТипЗнч(Единица)=Тип("СправочникСсылка.КлассификаторЕдиницИзмерения"), ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Единица, "Код"), "");
			КонецЕсли; 
			Для каждого ИмяПоля Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПоляКодЕдиницы) Цикл
				ДанныеСтроки.Вставить(ИмяПоля, КодЕдиницы);
			КонецЦикла; 
			Для каждого ИмяПоля Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПоляКоэффициент) Цикл
				ДанныеСтроки.Вставить(ИмяПоля, 1);
			КонецЦикла; 
			
			ДанныеСтроки.Вставить("Количество", 1);
			ДанныеСтроки.Вставить("КоличествоМест", 0);
			ДанныеСтроки.Вставить("Цена", ТекСтрока.СтоимостьДоставки);
			ДанныеСтроки.Вставить("Сумма", ТекСтрока.СтоимостьДоставки);
			ДанныеСтроки.Вставить("СуммаНДС", ТекСтрока.СуммаНДСДоставки);
			ДанныеСтроки.Вставить("Всего", ТекСтрока.СтоимостьДоставки);
			
			ПараметрыРасчета = Новый Структура;
			ПараметрыРасчета.Вставить("СуммаВключаетНДС", ТекСтрока.СуммаВключаетНДС);
			
			ТабличныеЧастиУНФКлиентСервер.РассчитатьСуммуНДСИВсего(ДанныеСтроки, ПараметрыРасчета);
			
			ДанныеСтроки.Вставить("Скидка", 0);
			ДанныеСтроки.Вставить("Вес", 0);
			ДанныеСтроки.Вставить("МассаБрутто", 0);
			ДанныеСтроки.Вставить("Объем", 0);
			ДанныеСтроки.Вставить("ЭтоРазделитель", Ложь);
			ДанныеСтроки.Вставить("ДатаОтгрузки", ДатаОтгрузки);
			
			// Наборы
			ДанныеСтроки.Вставить("НеобходимоВыделитьКакСоставНабора", Ложь);
			ДанныеСтроки.Вставить("ЭтоНабор", Ложь);
			// Конец Наборы
			
			// УКД
			Если ДанныеДокументов.Колонки.Найти("СуммаНДСДоИзмененияДоставка")<>Неопределено
				И ДанныеДокументов.Колонки.Найти("СуммаНДСПослеИзмененияДоставка")<>Неопределено
				И ДанныеДокументов.Колонки.Найти("СтоимостьСНДСДоИзмененияДоставка")<>Неопределено
				И ДанныеДокументов.Колонки.Найти("СтоимостьСНДСПослеИзмененияДоставка")<>Неопределено Тогда
				СтоимостьБезНДСДоИзменения = (ТекСтрока.СтоимостьСНДСДоИзмененияДоставка-ТекСтрока.СуммаНДСДоИзмененияДоставка);
				СтоимостьБезНДСПослеИзменения = (ТекСтрока.СтоимостьСНДСПослеИзмененияДоставка-ТекСтрока.СуммаНДСПослеИзмененияДоставка);
				ДанныеСтроки.Вставить("СуммаНДСДоИзменения", ТекСтрока.СуммаНДСДоИзмененияДоставка);
				ДанныеСтроки.Вставить("СуммаНДСПослеИзменения", ТекСтрока.СуммаНДСПослеИзмененияДоставка);
				ДанныеСтроки.Вставить("СтоимостьСНДСДоИзменения", ТекСтрока.СтоимостьСНДСДоИзмененияДоставка);
				ДанныеСтроки.Вставить("СтоимостьСНДСПослеИзменения", ТекСтрока.СтоимостьСНДСПослеИзмененияДоставка);
				ДанныеСтроки.Вставить("РазницаБезНДСУвеличение", ?(СтоимостьБезНДСПослеИзменения>СтоимостьБезНДСДоИзменения, СтоимостьБезНДСПослеИзменения-СтоимостьБезНДСДоИзменения, 0));
				ДанныеСтроки.Вставить("РазницаБезНДСУменьшение", ?(СтоимостьБезНДСДоИзменения>СтоимостьБезНДСПослеИзменения, СтоимостьБезНДСДоИзменения-СтоимостьБезНДСПослеИзменения, 0));
				ДанныеСтроки.Вставить("РазницаНДСУвеличение", ?(ТекСтрока.СуммаНДСПослеИзмененияДоставка>ТекСтрока.СуммаНДСДоИзмененияДоставка, ТекСтрока.СуммаНДСПослеИзмененияДоставка-ТекСтрока.СуммаНДСДоИзмененияДоставка, 0));
				ДанныеСтроки.Вставить("РазницаНДСУменьшение", ?(ТекСтрока.СуммаНДСДоИзмененияДоставка>ТекСтрока.СуммаНДСПослеИзмененияДоставка, ТекСтрока.СуммаНДСДоИзмененияДоставка-ТекСтрока.СуммаНДСПослеИзмененияДоставка, 0));
				ДанныеСтроки.Вставить("РазницаСНДСУвеличение", ?(ТекСтрока.СтоимостьСНДСПослеИзмененияДоставка>ТекСтрока.СтоимостьСНДСДоИзмененияДоставка, ТекСтрока.СтоимостьСНДСПослеИзмененияДоставка-ТекСтрока.СтоимостьСНДСДоИзмененияДоставка, 0));
				ДанныеСтроки.Вставить("РазницаСНДСУменьшение", ?(ТекСтрока.СтоимостьСНДСДоИзмененияДоставка>ТекСтрока.СтоимостьСНДСПослеИзмененияДоставка, ТекСтрока.СтоимостьСНДСДоИзмененияДоставка-ТекСтрока.СтоимостьСНДСПослеИзмененияДоставка, 0));
				ДанныеСтроки.Вставить("СтоимостьБезНДСДоИзменения", СтоимостьБезНДСДоИзменения);
				ДанныеСтроки.Вставить("СтоимостьБезНДСПослеИзменения", СтоимостьБезНДСПослеИзменения);
				ДанныеСтроки.Вставить("КоличествоДоИзменения", ?(ТекСтрока.СтоимостьСНДСДоИзмененияДоставка>0, 1, 0));
				ДанныеСтроки.Вставить("КоличествоПослеИзменения", ?(ТекСтрока.СтоимостьСНДСПослеИзмененияДоставка>0, 1, 0));
			КонецЕсли;
			// Конец УКД
			
			ЗаполнитьЗначенияСвойств(НоваяСтрокаДоставка, ДанныеСтроки);
			
		КонецЦикла; 
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Функция возвращает надпись с информацией о маршрутном листе для вывода в форме документа, например,
// в форме заказа покупателя на странице "Доставка".
// 
// Параметры:
//  МаршрутныеЛисты - Таблица значений - таблица значений со ссылками на документ "Маршрутный лист" и представлением.
// 
// Возвращаемое значение:
//   ФорматированнаяСтрока - представление маршрутных листов
//
Функция СформироватьНадписьМаршрутныйЛистМассив(МаршрутныеЛисты) Экспорт
	
	КомпонентыФС = Новый Массив;
	
	Если МаршрутныеЛисты.Количество() = 0 Тогда
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Маршрутный лист: заказ не включен в маршрутный лист'")));
	Иначе
		
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Маршрутный лист: '")));
		Разделитель = ", ";
		НуженРазделитель = Ложь;
		МаршрутныйЛистТекст = "";
		
		Для Каждого МаршрутныйЛист Из МаршрутныеЛисты Цикл
			Если НуженРазделитель Тогда
				МаршрутныйЛистТекст = МаршрутныйЛистТекст + Разделитель;
			Иначе
				НуженРазделитель = Истина;
			КонецЕсли;
			
			МаршрутныйЛистПредставление = ПолучитьПредставлениеМаршрутногоЛиста(МаршрутныйЛист);
			
			Если МаршрутныйЛистПредставление = "НетОбъекта" Тогда
				
				МаршрутныйЛистТекст = МаршрутныйЛистТекст + НСтр("ru = 'не найден'");
				
			ИначеЕсли ЗначениеЗаполнено(МаршрутныйЛист.Ссылка) Тогда
				
				МаршрутныйЛистПредставление = УбратьСлужебнуюИнформациюИзПредставления(МаршрутныйЛистПредставление);
				МаршрутныйЛистТекст = МаршрутныйЛистТекст + МаршрутныйЛистПредставление;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ПравоДоступа("Просмотр", Метаданные.Документы.МаршрутныйЛист) Тогда
			КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(МаршрутныйЛистТекст, , , , "открыть"));
		Иначе
			КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(МаршрутныйЛистТекст));
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(КомпонентыФС);
	
КонецФункции

// Функция - Получить представление маршрутного листа
//
// Параметры:
//  МаршрутныйЛист	 - ДокументСсылка.МаршрутныйЛист - маршрутный лист для которого необходимо 
//														получить представление
// 
// Возвращаемое значение:
//   Строка - Представление маршрутного листа
//
Функция ПолучитьПредставлениеМаршрутногоЛиста(МаршрутныйЛист) Экспорт
	
	МаршрутныйЛистПредставление = "";
	
	Если НЕ ЗначениеЗаполнено(МаршрутныйЛист.Ссылка) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ОбщегоНазначения.СсылкаСуществует(МаршрутныйЛист.Ссылка) Тогда
		МаршрутныйЛистПредставление = СокрЛП(МаршрутныйЛист.Представление);
		МаршрутныйЛистПредставление = СтрЗаменить(МаршрутныйЛистПредставление, НСтр("ru = 'Маршрутный лист '"), "");
	Иначе
		МаршрутныйЛистПредставление = "НетОбъекта";
	КонецЕсли;
	
	Возврат МаршрутныйЛистПредставление;
	
КонецФункции

// Функция возвращает надпись с информацией о маршрутном листе для вывода в форме документа, например,
// в форме заказа покупателя на странице "Доставка".
//
// Параметры:
// МаршрутныйЛист - ДокументСсылка.МаршрутныйЛист - - массив ссылок на документ "Маршрутный лист".
// 
// Возвращаемое значение:
//   Булево - Признак формирования маршрутного листа
//
Функция СформироватьНадписьМаршрутныйЛист(МаршрутныйЛист, КомпонентыФС) Экспорт
	
	МаршрутныйЛистПредставление = ПолучитьПредставлениеМаршрутногоЛиста(МаршрутныйЛист);
	
	Если МаршрутныйЛистПредставление = "НетОбъекта" Тогда
		
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'не найден'")));
		
	ИначеЕсли ЗначениеЗаполнено(МаршрутныйЛист) Тогда
		
		МаршрутныйЛистТекст = УбратьСлужебнуюИнформациюИзПредставления(МаршрутныйЛистПредставление);
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(МаршрутныйЛистТекст, , , , "открыть"));
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Функция - Получить маршрутные листы с заказом
//
// Параметры:
//  ЗаказПокупателяСсылка	 - 	ДокументСсылка.ЗаказПокупателя - заказ покупателя
// 
// Возвращаемое значение:
//   ТаблицаЗначений - маршрутные листы с заказом
//
Функция ПолучитьМаршрутныеЛистыСЗаказом(ЗаказПокупателяСсылка) Экспорт
	
	Если Не ЗначениеЗаполнено(ЗаказПокупателяСсылка) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МаршрутныйЛистЗаказы.Ссылка КАК Ссылка,
		|	МаршрутныйЛистЗаказы.Ссылка.Представление КАК Представление
		|ИЗ
		|	Документ.МаршрутныйЛист.Заказы КАК МаршрутныйЛистЗаказы
		|ГДЕ
		|	МаршрутныйЛистЗаказы.Заказ = &ЗаказПокупателяСсылка
		|	И НЕ МаршрутныйЛистЗаказы.Ссылка.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ЗаказПокупателяСсылка", ЗаказПокупателяСсылка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РезультатЗапросаТаблицаЗначений = РезультатЗапроса.Выгрузить();
	Возврат РезультатЗапросаТаблицаЗначений;
	
КонецФункции

#Область ЗаказыНаДоставкуВСервисе

// Заполняет параметры заказа на доставку данными из объекта-основания заказа.
//
// Параметры:
//  Параметры - Структура - см. СервисДоставки.НовыйПараметрыЗаказаНаДоставку()
//
Процедура ЗаполнитьПараметрыЗаказаНаДоставку(Параметры) Экспорт
	
	ДокументОснование = Параметры.ДокументОснование;
	
	ТекстЗапроса = "";
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ТекстЗапроса = ТекстЗапросаДляЗаказаНаДоставкуПоЗаказуПокупателя(Параметры);
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЧекККМ") Тогда
		ТекстЗапроса = ТекстЗапросаДляЗаказаНаДоставкуПоЧекуККМ();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
		ТекстЗапроса = ТекстЗапросаДляЗаказаНаДоставкуПоРасходнойНакладной();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
		ТекстЗапроса = ТекстЗапросаДляЗаказаНаДоставкуПоПриходнойНакладной();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		ТекстЗапроса = ТекстЗапросаДляЗаказаНаДоставкуПоЗаказуПоставщику();
	КонецЕсли;
	
	Если ТекстЗапроса = "" Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	Запрос.УстановитьПараметр("Дата", ТекущаяДатаСеанса());
	
	УстановитьПривилегированныйРежим(Истина);
	Результаты = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	РезультатПоШапке = Результаты[0];
	
	Если РезультатПоШапке.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаПоДокументу = РезультатПоШапке.Выгрузить();
	ДанныеПоДокументу = ТаблицаПоДокументу[0];
	
	СервисДоставкиКлиентСервер.ЗаполнитьСтруктуруПоЛинейнымДанным(Параметры, ДанныеПоДокументу, ТаблицаПоДокументу.Колонки);
	
	Параметры.Вставить("ОрганизацияБизнесСетиСсылка", ДанныеПоДокументу.ОрганизацияБизнесСетиСсылка);
	
	ЗаполнитьАдресУчастникаГрузоперевозки(Параметры.Отправитель);
	ЗаполнитьАдресУчастникаГрузоперевозки(Параметры.Получатель);
	
	СервисДоставки.ЗаполнитьАдресПоПараметрам(Параметры.Отправитель.Адрес);
	СервисДоставки.ЗаполнитьАдресПоПараметрам(Параметры.Получатель.Адрес);
	СервисДоставки.ЗаполнитьАдресПоПараметрам(Параметры.Отправитель.Контрагент.ЮридическийАдрес);
	СервисДоставки.ЗаполнитьАдресПоПараметрам(Параметры.Получатель.Контрагент.ЮридическийАдрес);
	
	//Заполним телефоны контактных лиц
	СервисДоставкиПереопределяемый.ЗаполнитьТелефонКонтактногоЛица(Параметры.Отправитель.КонтактноеЛицо);
	СервисДоставкиПереопределяемый.ЗаполнитьТелефонКонтактногоЛица(Параметры.Получатель.КонтактноеЛицо);
	
	ОбъемИВес = РассчитатьОбъемИВесЗапасов(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Запасы"));
	
	Если НЕ ЗначениеЗаполнено(Параметры.Груз.Объем) И ЗначениеЗаполнено(ОбъемИВес.Объем) Тогда
		Параметры.Груз.Объем = ОбъемИВес.Объем;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Параметры.Груз.Вес) И ЗначениеЗаполнено(ОбъемИВес.Вес) Тогда
		Параметры.Груз.Вес = ОбъемИВес.Вес;
	КонецЕсли;
	
	РезультатПоТоварнымПозициям = Результаты[1];
	
	Если НЕ РезультатПоТоварнымПозициям.Пустой() Тогда
		
		ВыборкаПоПозиции = РезультатПоТоварнымПозициям.Выбрать();
		
		Пока ВыборкаПоПозиции.Следующий() Цикл
			
			НоваяСтрокаПозиции = Параметры.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаПозиции, ВыборкаПоПозиции);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

//  Обновление данных по доставке из сервиса
//
// Параметры:
//  ДокументОснование	 - 	ОпределяемыйТип.ОснованиеЗаказаСервисДоставки - основание заказа
//  ИмяГруппыДоставка	 - 	Строка - имя группы доставки
//  Организация			 - 	СправочникСсылка.Организации - организация заказа
//  Форма				 - 	ФормаКлиентскогоПриложения - форма
//  ИмяГруппыСтраницы	 - 	Строка - имя группы страниц
//
Процедура ОбновитьДанныеПоДоставкеИзСервиса(ДокументОснование, ИмяГруппыДоставка, Организация, Форма,
	ИмяГруппыСтраницы = "Страницы") Экспорт
	
	ЗаказНаДоставку = РегистрыСведений.ЗаказыСервисовДоставки.ЗаказСервисаПоДокументуОснования(ДокументОснование);
	ЭлементыФормы   = Форма.Элементы;
	
	Если НЕ ЗаказНаДоставкуНайден(ЗаказНаДоставку) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭлементыФормы, ИмяГруппыДоставка, "Видимость", Ложь);
		Возврат;
	КонецЕсли;

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭлементыФормы, ИмяГруппыДоставка, "Видимость", Истина);
	
	Если ЭлементыФормы[ИмяГруппыСтраницы].ТекущаяСтраница <> ЭлементыФормы[ИмяГруппыДоставка] Тогда
		Возврат;
	КонецЕсли;
	
	Форма.ИдентификаторЗаказаНаДоставку = ЗаказНаДоставку.ИдентификаторЗаказа;
	ПараметрыЗаказаНаДоставку = ПолучитьЗаказНаДоставкуИзСервиса(Организация, ДокументОснование, ЗаказНаДоставку.ИдентификаторЗаказа);
	
	Если НЕ ПолученыДанныеПоЗаказуИзСервиса(ПараметрыЗаказаНаДоставку) Тогда
		Возврат;
	КонецЕсли;
	
	Форма.ТрекНомерСервисаДоставки = ЗаказНаДоставку.ТрекНомер;
	Форма.СтатусЗаказаИзСервиса    = ПараметрыЗаказаНаДоставку.Состояние;
	Форма.ТипГрузоперевозки 	   = ПараметрыЗаказаНаДоставку.ТипГрузоперевозки;
	
	Форма.СервисДоставки = ЗаголовокСервисаДоставкиПоТипуГрузоперевозки(Форма.ТипГрузоперевозки);
	УстановитьЗаголовкиКомандДоставки(ЭлементыФормы, ПараметрыЗаказаНаДоставку.ТипГрузоперевозки);
	
КонецПроцедуры

// Получение заказ на доставку из сервиса
//
// Параметры:
//  Организация			 - 	СправочникСсылка.Организации - организация заказа
//  ДокументОснование	 - 	ОпределяемыйТип.ОснованиеЗаказаСервисДоставки - документ основание заказа
//  ИдентификаторЗаказа	 - 	Строка - идентификатор заказа в сервисе
// 
// Возвращаемое значение:
//   Структура- данные по заказу из сервиса
//
Функция ПолучитьЗаказНаДоставкуИзСервиса(Организация, ДокументОснование, ИдентификаторЗаказа) Экспорт
	
	ПараметрыЗапроса = СервисДоставки.НовыйПараметрыЗапросаОбновитьЗаказНаДоставку();
	ПараметрыЗапроса.Идентификатор = ИдентификаторЗаказа;
	ПараметрыЗапроса.Вставить("ОрганизацияБизнесСетиСсылка", БизнесСеть.ПодключенныеОрганизации()[0].Организация);

	АдресХранилища = ПоместитьВоВременноеХранилище("");
	СервисДоставки.ПолучитьЗаказНаДоставку(ПараметрыЗапроса, АдресХранилища);
	ПараметрыЗаказаНаДоставку = ПолучитьИзВременногоХранилища(АдресХранилища);
		
	СервисДоставкиПереопределяемый.ОбработатьРезультатПолученияЗаказаНаДоставку(ПараметрыЗаказаНаДоставку, ДокументОснование);
	
	Если ПараметрыЗаказаНаДоставку = "" Тогда
		Возврат ПараметрыЗаказаНаДоставку;
	КонецЕсли;
	
	Возврат ПараметрыЗаказаНаДоставку.Данные;
		
КонецФункции

// Статус заказа из сервиса
//
// Параметры:
//  Организация			 - 	СправочникСсылка.Организации - организация заказа
//  ИдентификаторЗаказа	 - 	Строка - идентификатор заказа в сервисе
// 
// Возвращаемое значение:
//   Строка - Состояние заказа в сервисе
//
Функция СтатусЗаказаИзСервиса(Организация, ИдентификаторЗаказа) Экспорт
	
	ПараметрыЗапроса = СервисДоставки.НовыйПараметрыЗапросаПолучитьЗаказНаДоставку();
	ПараметрыЗапроса.Идентификатор = ИдентификаторЗаказа;
	ПараметрыЗапроса.Вставить("ОрганизацияБизнесСетиСсылка", БизнесСеть.ПодключенныеОрганизации()[0].Организация);

	АдресХранилища = ПоместитьВоВременноеХранилище("");
	СервисДоставки.ПолучитьЗаказНаДоставку(ПараметрыЗапроса, АдресХранилища);
	ПараметрыЗаказаНаДоставку = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Возврат ПараметрыЗаказаНаДоставку.Данные.Состояние;
	
КонецФункции

// Заполняет параметры контрагента данными по ссылке, указанной в Параметры.
//
// Параметры:
//  Параметры - Структура - см. СервисДоставки.НовыйПараметрыКонтрагента()
//
Процедура ЗаполнитьПараметрыКонтрагента(Параметры) Экспорт
	
	ИмяСправочника = Неопределено;
	ВидЮридическогоАдреса = Неопределено;
	Если ТипЗнч(Параметры.Ссылка) = Тип("СправочникСсылка.Организации") Тогда
		ИмяСправочника = "Организации";
		ИмяРеквизитаНаименование = "Наименование";
		ЭтоОрганизация = Истина;
	ИначеЕсли ТипЗнч(Параметры.Ссылка) = Тип("СправочникСсылка.Контрагенты") Тогда
		ИмяСправочника = "Контрагенты";
		ИмяРеквизитаНаименование = "Наименование";
		ЭтоОрганизация = Ложь;
	Иначе
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	
	Если ТипЗнч(Параметры.Ссылка) = Тип("СправочникСсылка.Контрагенты") Тогда
		ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОсновнаяТаблица.Ссылка КАК Ссылка,
		|	ОсновнаяТаблица.ИНН КАК ИНН,
		|	ОсновнаяТаблица.КПП КАК КПП,
		|	ОсновнаяТаблица.Наименование КАК Наименование,
		|	ВЫБОР
		|		КОГДА ОсновнаяТаблица.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ЮридическоеЛицо)
		|			ТОГДА 1
		|		КОГДА ОсновнаяТаблица.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
		|			ТОГДА 2
		|		КОГДА ОсновнаяТаблица.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель)
		|			ТОГДА 3
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЮрФизЛицо
		|ИЗ
		|	&ТаблицаДляЗапроса КАК ОсновнаяТаблица
		|ГДЕ
		|	ОсновнаяТаблица.Ссылка = &Ссылка";
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОсновнаяТаблица.Ссылка КАК Ссылка,
		|	ОсновнаяТаблица.ИНН КАК ИНН,
		|	ОсновнаяТаблица.КПП КАК КПП,
		|	ОсновнаяТаблица.Наименование КАК Наименование,
		|	ВЫБОР
		|		КОГДА ОсновнаяТаблица.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
		|			ТОГДА 1
		|		КОГДА ОсновнаяТаблица.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
		|			ТОГДА 3
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЮрФизЛицо
		|ИЗ
		|	&ТаблицаДляЗапроса КАК ОсновнаяТаблица
		|ГДЕ
		|	ОсновнаяТаблица.Ссылка = &Ссылка";
	КонецЕсли;
	Запрос.УстановитьПараметр("Ссылка", Параметры.Ссылка);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаДляЗапроса", "Справочник." + ИмяСправочника);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ОсновнаяТаблица.Наименование", "ОсновнаяТаблица." + ИмяРеквизитаНаименование);
	
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ЗаполнитьЗначенияСвойств(Параметры, РезультатЗапроса.Выгрузить()[0]);
		
		Если Параметры.ЮрФизЛицо <> 2 Тогда
			
			Параметры.ЮридическийАдрес.Владелец = Параметры.Ссылка;
			СервисДоставкиСлужебный.ЗаполнитьАдресПоПараметрам(Параметры.ЮридическийАдрес);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет параметры контактного лица данными по ссылке, указанной в Параметры.
//
// Параметры:
//  Параметры - Структура - см. СервисДоставки.НовыйПараметрыКонтактногоЛица()
//
Процедура ЗаполнитьПараметрыКонтактногоЛица(Параметры) Экспорт
	НаименованиеКонтакта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ссылка,"Наименование");
	Параметры.Наименование = НаименованиеКонтакта;
	ЗаполнитьТелефонКонтактногоЛица(Параметры);
КонецПроцедуры

// Заполняет параметры телефона контактной информацией по владельцу, указанному в Параметры.
//
// Параметры:
//  Параметры - Структура - см. СервисДоставки.НовыйПараметрыКонтактногоЛица().
//
Процедура ЗаполнитьТелефонКонтактногоЛица(Параметры) Экспорт
	
	Если ЗначениеЗаполнено(Параметры.Телефон.Представление) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Параметры.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ВидКИ = ВидКИТелефонПоОбъекту(Параметры.Ссылка);
	ТелефоныКонтактногоЛица = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Параметры.Ссылка),
		,
		ВидКИ,
		ТекущаяДатаСеанса());

	Если ТелефоныКонтактногоЛица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Телефон.Представление = ТелефоныКонтактногоЛица[0].Представление;
	Параметры.Телефон.Значение 		= ТелефоныКонтактногоЛица[0].Значение;

КонецПроцедуры

// Обрабатывает результат создания и изменения заказа на доставку в сервисе 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//  ДокументыОснования - СписокЗначений, Неопределено - список документов оснований заказа на доставку
//                                        (значение - ОпределяемыйТип.ОснованиеЗаказаСервисДоставки).
//
Процедура ОбработатьРезультатСозданияЗаказаНаДоставку(Результат, ДокументОснование = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) ИЛИ Результат = "" Тогда
		Возврат;
	КонецЕсли;
	
	НоваяЗаписьРегистра = РегистрыСведений.ЗаказыСервисовДоставки.СоздатьМенеджерЗаписи();
	НоваяЗаписьРегистра.ДокументОснование = ДокументОснование[0].Значение;
	НоваяЗаписьРегистра.ТрекНомер = Результат.ТрекНомер;
	НоваяЗаписьРегистра.ИдентификаторЗаказа = Результат.Идентификатор;
	НоваяЗаписьРегистра.Статус = Результат.Статус;
	НоваяЗаписьРегистра.Записать(Истина);
	
КонецПроцедуры

// Обрабатывает результат оформления заказа на доставку в сервисе 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//  ДокументыОснования - ОпределяемыйТип.ОснованиеЗаказаСервисДоставки, Неопределено - документ, основание заказа на доставку.
//
Процедура ОбработатьРезультатОформленияЗаказаНаДоставку(Результат, ДокументОснование = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) ИЛИ Результат = "" Тогда
		Возврат;
	КонецЕсли;
	
	НоваяЗаписьРегистра = РегистрыСведений.ЗаказыСервисовДоставки.СоздатьМенеджерЗаписи();
	НоваяЗаписьРегистра.ДокументОснование = ДокументОснование[0].Значение;
	НоваяЗаписьРегистра.ТрекНомер = Результат.ТрекНомер;
	НоваяЗаписьРегистра.ИдентификаторЗаказа = Результат.Идентификатор;
	НоваяЗаписьРегистра.Статус = Результат.Статус;
	НоваяЗаписьРегистра.Записать(Истина);

КонецПроцедуры

// Обрабатывает результат получения заказа на доставку из сервиса 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//  ДокументыОснования - ОпределяемыйТип.ОснованиеЗаказаСервисДоставки, Неопределено - документ, основание заказа на доставку.
//
Процедура ОбработатьРезультатПолученияЗаказаНаДоставку(Результат, ДокументОснование = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) ИЛИ НЕ ТипЗнч(Результат) = Тип("Структура") 
		ИЛИ НЕ Результат.Свойство("Данные") Тогда
		Возврат;
	КонецЕсли;
	
	НоваяЗаписьРегистра = РегистрыСведений.ЗаказыСервисовДоставки.СоздатьМенеджерЗаписи();
	НоваяЗаписьРегистра.ДокументОснование = ДокументОснование[0].Значение;
	НоваяЗаписьРегистра.ТрекНомер = Результат.Данные.ТрекНомер;
	НоваяЗаписьРегистра.ИдентификаторЗаказа = Результат.Данные.ИдентификаторЗаказа;
	НоваяЗаписьРегистра.ТипГрузоперевозки = Результат.Данные.ТипГрузоперевозки;
	НоваяЗаписьРегистра.Статус = Результат.Данные.Состояние;
	НоваяЗаписьРегистра.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПрограммныйСлужебныйИнтерфейс

// Процедура обновляет на форме поля параметров расчета доставки, которые требуется вводить вручную.
//
// Форма - ФормаКлиентскогоПриложения - форма, на которой следует обновить поля
// СлужбаДоставки - СправочникСсылка.СлужбыДоставки - выбранная служба доставки
// ТаблицаЗначений - ТаблицаЗначений - таблица текущих значений параметров расчета доставки
//		Колонки:
//		Параметр (СправочникСсылка.ПараметрыРасчетовДоставки) - параметр, значение которого содержится в таблице
//		Значение (Число) - значение параметра, заданного вручную
// Группа - ГруппаФормы - группа формы, в которую добавляются поля ввода параметров
//
Процедура ОбновитьРеквизитыПараметрыДоставки(Форма, СлужбаДоставки, ТаблицаЗначений, Группа) Экспорт
	
	ДобавляемыеРеквизиты = Новый Массив;
	УдаляемыеРеквизиты = Новый Массив;
	Для каждого СтрокаТабличнойЧасти Из Форма.ПараметрыДоставки Цикл
		УдаляемыеРеквизиты.Добавить(СтрокаТабличнойЧасти.ИмяРеквизита);
		Форма.Элементы.Удалить(Форма.Элементы[СтрокаТабличнойЧасти.ИмяРеквизита]);
	КонецЦикла;
	Форма.ПараметрыДоставки.Очистить();
	
	Если ЗначениеЗаполнено(СлужбаДоставки) Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СлужбаДоставки", СлужбаДоставки);
		Запрос.УстановитьПараметр("ТаблицаЗначений", ТаблицаЗначений);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Значения.Параметр,
		|	Значения.Значение
		|ПОМЕСТИТЬ Значения
		|ИЗ
		|	&ТаблицаЗначений КАК Значения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СлужбыДоставкиПараметрыРасчетов.Параметр,
		|	СлужбыДоставкиПараметрыРасчетов.Параметр.Идентификатор КАК Идентификатор,
		|	СлужбыДоставкиПараметрыРасчетов.Параметр.Наименование КАК Наименование,
		|	ЕСТЬNULL(Значения.Значение, 0) КАК Значение
		|ИЗ
		|	Справочник.СлужбыДоставки.ПараметрыРасчетов КАК СлужбыДоставкиПараметрыРасчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Значения КАК Значения
		|		ПО СлужбыДоставкиПараметрыРасчетов.Параметр = Значения.Параметр
		|ГДЕ
		|	СлужбыДоставкиПараметрыРасчетов.Ссылка = &СлужбаДоставки
		|	И СлужбыДоставкиПараметрыРасчетов.Параметр.ЗадаватьЗначениеПриРасчете";
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СтрокаТабличнойЧасти = Форма.ПараметрыДоставки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
			ИмяРеквизита = "ПараметрДоставки_"+СтрЗаменить(Новый УникальныйИдентификатор, "-", "");
			Реквизит = Новый РеквизитФормы(
			ИмяРеквизита,
			Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)),
			,
			Выборка.Наименование);
			ДобавляемыеРеквизиты.Добавить(Реквизит);
			СтрокаТабличнойЧасти.ИмяРеквизита = ИмяРеквизита;
		КонецЦикла; 
	КонецЕсли; 
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты, УдаляемыеРеквизиты);
	
	Для каждого СтрокаТабличнойЧасти Из Форма.ПараметрыДоставки Цикл
		Элемент = Форма.Элементы.Добавить(СтрокаТабличнойЧасти.ИмяРеквизита, Тип("ПолеФормы"), Группа);
		Элемент.Вид = ВидПоляФормы.ПолеВвода;
		Элемент.ПутьКДанным = СтрокаТабличнойЧасти.ИмяРеквизита;
		Элемент.АвтоМаксимальнаяШирина = Ложь;
		Элемент.МаксимальнаяШирина = 5;
		Элемент.Ширина = 10;
		Элемент.УстановитьДействие("ПриИзменении", "Подключаемый_ПриИзмененииПараметраДоставки");
		Форма[СтрокаТабличнойЧасти.ИмяРеквизита] = СтрокаТабличнойЧасти.Значение;
	КонецЦикла; 
	
КонецПроцедуры

Процедура ЗаполнитьДеревоПараметровРасчета(ПараметрыРасчетов, ТолькоЧисловые = Ложь) Экспорт
	
	ПараметрыРасчетов.ПолучитьЭлементы().Очистить();
	
	СтрокаРодитель = ПараметрыРасчетов.ПолучитьЭлементы().Добавить();
	СтрокаРодитель.Идентификатор = НСтр("ru = 'Заказ'");
	ИменаРеквизитов = "Дата,Автор,АдресДоставки,ВалютаДокумента,Вес,ВидЗаказа,ВидСкидкиНаценки,ВидЦен,ВремяДоставкиС,ВремяДоставкиПо,Высота,ДатаОтгрузки,Длина,ЗапаснойТелефон,
	|ЗонаТариф,КонтактныйТелефон,Контрагент,Курс,НалогообложениеНДС,НДСВключатьВСтоимость,Объем,ОбъявленнаяЦенность,Организация,Ответственный,ПочтаПолучателя,ПроцентСкидкиПоДисконтнойКарте,
	|СлужбаДоставки,СпособДоставки,СпособОтгрузки,СуммаВключаетНДС,СтруктурнаяЕдиницаПродажи,СтруктурнаяЕдиницаРезерв,СуммаДокумента,
	|ТрекНомер,Ширина,Ячейка,НоменклатураДоставки,Курьер";
	ДобавитьВложенныеРеквизиты(СтрокаРодитель, Метаданные.Документы.ЗаказПокупателя, Справочники.НаборыДополнительныхРеквизитовИСведений.Документ_ЗаказПокупателя, ИменаРеквизитов, ТолькоЧисловые);
	СтрокаРодитель = ПараметрыРасчетов.ПолучитьЭлементы().Добавить();
	СтрокаРодитель.Идентификатор = НСтр("ru = 'Контрагент'");
	СтрокаРодитель.Тип = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
	ИменаРеквизитов = "Ссылка,Наименование,НаименованиеПолное,ВидКонтрагента,ДатаРождения,ДатаСоздания,ИсточникПривлеченияПокупателя,Комментарий,Ответственный,Покупатель,Пол,Поставщик,СтранаРегистрации,СтатьяДДСПоУмолчанию";
	ДобавитьВложенныеРеквизиты(СтрокаРодитель, Метаданные.Справочники.Контрагенты, Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Контрагенты, ИменаРеквизитов, ТолькоЧисловые);
	Если СтрокаРодитель.ПолучитьЭлементы().Количество()=0 Тогда
		ПараметрыРасчетов.ПолучитьЭлементы().Удалить(СтрокаРодитель);
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПараметрыРасчетовДоставки.Ссылка КАК Параметр,
	|	ПараметрыРасчетовДоставки.Идентификатор
	|ИЗ
	|	Справочник.ПараметрыРасчетовДоставки КАК ПараметрыРасчетовДоставки
	|ГДЕ
	|	НЕ ПараметрыРасчетовДоставки.ПометкаУдаления";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаПараметр = ПараметрыРасчетов.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПараметр, Выборка);
		СтрокаПараметр.Тип = Новый ОписаниеТипов("Число");
	КонецЦикла; 
	
КонецПроцедуры

Процедура ЗаданиеРассчитатьСтоимостьДоставки(Знач Параметры, Знач АдресХранилища) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СтоимостьДоставки", 0);
	
	Объект = ПолучитьИзВременногоХранилища(Параметры.АдресОбъекта);
	
	Результат.СтоимостьДоставки = СтоимостьДоставки(Объект, Параметры);
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция ВидКИТелефонПоОбъекту(Ссылка)
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Организации") Тогда
		Возврат Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.Контрагенты") Тогда
		Возврат Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
		Возврат Справочники.ВидыКонтактнойИнформации.ТелефонКонтактногоЛица;
	Иначе
		Возврат Справочники.ВидыКонтактнойИнформации.ТелефонСтруктурнойЕдиницы;
	КонецЕсли;

КонецФункции

Функция УбратьСлужебнуюИнформациюИзПредставления(Знач МаршрутныйЛистПредставление)
	
	МаршрутныйЛистПредставление = СтрЗаменить(МаршрутныйЛистПредставление, НСтр("ru = ' (удален)'"),"");
	МаршрутныйЛистПредставление = СтрЗаменить(МаршрутныйЛистПредставление, НСтр("ru = ' (не проведен)'"),"");
	
	Возврат СокрЛП(МаршрутныйЛистПредставление);
	
КонецФункции

Функция ЗаголовокСервисаДоставкиПоТипуГрузоперевозки(ТипГрузоперевозки)
	Если ТипГрузоперевозки = 1 Тогда
		Возврат НСтр("ru = '1С:Доставка'");
	Иначе
		Возврат НСтр("ru = '1С:Курьер'");
	КонецЕсли;
КонецФункции

Процедура ЗаполнитьАдресУчастникаГрузоперевозки(УчастникГрузоперевозки)
	Если ЗначениеЗаполнено(УчастникГрузоперевозки.Адрес.Значение) Тогда
		Возврат;
	КонецЕсли;
	ДанныеАдреса = ПоляАдресаОтправления(УчастникГрузоперевозки.Адрес.Владелец);
	УчастникГрузоперевозки.Адрес.Представление = ДанныеАдреса.АдресОтправления;
	УчастникГрузоперевозки.Адрес.ЗначенияПолей = ДанныеАдреса.АдресОтправленияЗначенияПолей;
	УчастникГрузоперевозки.Адрес.Значение      = ДанныеАдреса.АдресОтправленияЗначение;
КонецПроцедуры

Функция ПолученыДанныеПоЗаказуИзСервиса(ПараметрыЗаказаНаДоставку)
	
	Возврат ПараметрыЗаказаНаДоставку <> Неопределено 
		И ПараметрыЗаказаНаДоставку <> ""
		И ТипЗнч(ПараметрыЗаказаНаДоставку) = Тип("Структура") 
		И ПараметрыЗаказаНаДоставку.Свойство("Состояние");
		
КонецФункции

Процедура УстановитьЗаголовкиКомандДоставки(ЭлементыФормы, ТипГрузоперевозки)
	
	Если ТипГрузоперевозки = 1 Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭлементыФормы, 
		"ПерейтиКЗаказуВСервисе", "Заголовок", НСтр("ru = 'Перейти к заказу в 1С:Доставке'"));
	ИначеЕсли ТипГрузоперевозки = 2 Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭлементыФормы, 
		"ПерейтиКЗаказуВСервисе", "Заголовок", НСтр("ru = 'Перейти к заказу в 1С:Курьер'"));
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭлементыФормы, 
		"ПерейтиКЗаказуВСервисе", "Заголовок", НСтр("ru = 'Перейти к заказу в сервисе'"));
	КонецЕсли;
	
КонецПроцедуры

Функция ЗаказНаДоставкуНайден(ЗаказНаДоставку)
	
	Возврат ТипЗнч(ЗаказНаДоставку) = Тип("Структура") 
		И ЗаказНаДоставку.ИдентификаторЗаказа <> "";
		
КонецФункции

Функция ТекстЗапросаДляЗаказаНаДоставкуПоЗаказуПокупателя(Параметры)
	
	Если Параметры.ДокументОснование.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд Тогда
		Возврат ТекстЗапросаДляЗаказаНаДоставкуПоЗаказНаряду();
	КонецЕсли;
		
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ШапкаДокумента.Ссылка КАК ДокументОснование,
	|	ШапкаДокумента.Организация КАК ОрганизацияБизнесСетиСсылка,
	|	1 КАК ФормаОплаты,
	|	"""" КАК ГрузоперевозчикИдентификатор,
	|	"""" КАК ГрузоперевозчикНаименование,
	|	"""" КАК ГрузоперевозчикТелефон,
	|	"""" КАК ТарифНаименование,
	|	"""" КАК ТарифИдентификатор,
	|	"""" КАК ТарифНеГабарит,
	|	ШапкаДокумента.ДатаОтгрузки КАК ДатаОтгрузки,
	|	ШапкаДокумента.ОжидаемаяДатаВручения КАК ДатаДоставки,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяОтгрузкиС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяОтгрузкиПо,
	|	ШапкаДокумента.ВремяДоставкиС КАК ВремяДоставкиС,
	|	ШапкаДокумента.ВремяДоставкиПо КАК ВремяДоставкиПо,
	|	2 КАК СпособОтгрузки,
	|	ВЫБОР
	|		КОГДА ШапкаДокумента.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.Курьер)
	|			ТОГДА 2
	|		КОГДА ШапкаДокумента.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ПунктВыдачи)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СпособДоставки,
	|	СправочникОрганизации.Ссылка КАК ОтправительКонтрагентСсылка,
	|	СправочникОрганизации.ИНН КАК ОтправительКонтрагентИНН,
	|	СправочникОрганизации.КПП КАК ОтправительКонтрагентКПП,
	|	СправочникОрганизации.Наименование КАК ОтправительКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтправительКонтрагентЮрФизЛицо,
	|	ИСТИНА КАК ОтправительКонтрагентЭтоОрганизация,
	|	ШапкаДокумента.Организация КАК ОтправительКонтрагентЮридическийАдресВладелец,
	|	ШапкаДокумента.СтруктурнаяЕдиницаРезерв.Ссылка КАК ОтправительАдресВладелец,
	|	ШапкаДокумента.СтруктурнаяЕдиницаРезерв.Наименование КАК ОтправительАдресВладелецНаименование,
	|	"""" КАК ОтправительАдресПредставление,
	|	"""" КАК ОтправительАдресЗначенияПолей,
	|	"""" КАК ОтправительАдресЗначение,
	|	ШапкаДокумента.Организация КАК ОтправительКонтактноеЛицоСсылка,
	|	СправочникОрганизации.Наименование КАК ОтправительКонтактноеЛицоНаименование,
	|	"""" КАК ОтправительКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ОтправительКонтактноеЛицоТелефонЗначение,
	|	СправочникКонтрагенты.Ссылка КАК ПолучательКонтрагентСсылка,
	|	СправочникКонтрагенты.ИНН КАК ПолучательКонтрагентИНН,
	|	СправочникКонтрагенты.КПП КАК ПолучательКонтрагентКПП,
	|	СправочникКонтрагенты.Наименование КАК ПолучательКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|			ТОГДА 2
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПолучательКонтрагентЮрФизЛицо,
	|	ЛОЖЬ КАК ПолучательКонтрагентЭтоОрганизация,
	|	ШапкаДокумента.Контрагент КАК ПолучательКонтрагентЮридическийАдресВладелец,
	|	ШапкаДокумента.Контрагент КАК ПолучательАдресВладелец,
	|	СправочникКонтрагенты.Наименование КАК ПолучательАдресВладелецНаименование,
	|	ШапкаДокумента.АдресДоставки КАК ПолучательАдресПредставление,
	|	ШапкаДокумента.АдресДоставкиЗначенияПолей КАК ПолучательАдресЗначенияПолей,
	|	ШапкаДокумента.АдресДоставкиЗначение КАК ПолучательАдресЗначение,
	|	ШапкаДокумента.КонтактноеЛицо КАК ПолучательКонтактноеЛицоСсылка,
	|	ШапкаДокумента.КонтактноеЛицо КАК ПолучательКонтактноеЛицоНаименование,
	|	ШапкаДокумента.КонтактныйТелефон КАК ПолучательКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ПолучательКонтактноеЛицоТелефонЗначение,
	|	СправочникКонтрагенты.Ссылка КАК ПлательщикКонтрагентСсылка,
	|	СправочникКонтрагенты.ИНН КАК ПлательщикКонтрагентИНН,
	|	СправочникКонтрагенты.КПП КАК ПлательщикКонтрагентКПП,
	|	СправочникКонтрагенты.НаименованиеПолное КАК ПлательщикКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|			ТОГДА 2
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПлательщикКонтрагентЮрФизЛицо,
	|	ЛОЖЬ КАК ПлательщикКонтрагентЭтоОрганизация,
	|	СправочникКонтрагенты.Ссылка КАК ПлательщикКонтрагентЮридическийАдресВладелец,
	|	ШапкаДокумента.КонтактноеЛицо КАК ПлательщикКонтактноеЛицоСсылка,
	|	ШапкаДокумента.КонтактноеЛицо КАК ПлательщикКонтактноеЛицоНаименование,
	|	ШапкаДокумента.КонтактныйТелефон КАК ПлательщикКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ПлательщикКонтактноеЛицоТелефонЗначение,
	|	2 КАК ПлательщикРоль,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ШапкаДокумента.Кратность, 0) > 0
	|				И ЕСТЬNULL(ШапкаДокумента.Курс, 0) > 0
	|			ТОГДА ШапкаДокумента.Курс / ШапкаДокумента.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ВалютаКоэффициент,
	|	1 КАК ГрузКоличествоГрузовыхМест,
	|	ШапкаДокумента.Объем КАК ГрузОбъем,
	|	ШапкаДокумента.Вес КАК ГрузВес,
	|	ШапкаДокумента.Длина КАК ГрузМаксимальнаяДлина,
	|	ШапкаДокумента.Ширина КАК ГрузМаксимальнаяШирина,
	|	ШапкаДокумента.Высота КАК ГрузМаксимальнаяВысота,
	|	ШапкаДокумента.ОбъявленнаяЦенность КАК ГрузСтоимость,
	|	"""" КАК ГрузОписание
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ШапкаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
	|		ПО ШапкаДокумента.Организация = ОрганизацииБизнесСеть.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникОрганизации
	|		ПО (СправочникОрганизации.Ссылка = ШапкаДокумента.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК СправочникКонтрагенты
	|		ПО (СправочникКонтрагенты.Ссылка = ШапкаДокумента.Контрагент)
	|ГДЕ
	|	ШапкаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Количество КАК Количество,
	|	СправочникНоменклатура.Вес КАК Вес,
	|	СправочникНоменклатура.Высота КАК Высота,
	|	СправочникНоменклатура.Длина КАК Длина,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СправочникНоменклатура.НаименованиеПолное КАК Наименование,
	|	СправочникНоменклатура.Объем КАК Объем,
	|	СправочникНоменклатура.Ширина КАК Ширина,
	|	СправочникНоменклатура.ТипНоменклатуры КАК ВидНоменклатуры,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТовары.Цена КАК Цена
	|ИЗ
	|	Документ.ЗаказПокупателя.Запасы КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.ТипНоменклатурыЗапас";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДляЗаказаНаДоставкуПоЗаказНаряду()
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ШапкаДокумента.Ссылка КАК ДокументОснование,
	|	ШапкаДокумента.Организация КАК ОрганизацияБизнесСетиСсылка,
	|	1 КАК ФормаОплаты,
	|	"""" КАК ГрузоперевозчикИдентификатор,
	|	"""" КАК ГрузоперевозчикНаименование,
	|	"""" КАК ГрузоперевозчикТелефон,
	|	"""" КАК ТарифНаименование,
	|	"""" КАК ТарифИдентификатор,
	|	"""" КАК ТарифНеГабарит,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОтгрузки,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаДоставки,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяОтгрузкиС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяОтгрузкиПо,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяДоставкиС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяДоставкиПо,
	|	1 КАК СпособОтгрузки,
	|	1 КАК СпособДоставки,
	|	СправочникОрганизации.Ссылка КАК ОтправительКонтрагентСсылка,
	|	СправочникОрганизации.ИНН КАК ОтправительКонтрагентИНН,
	|	СправочникОрганизации.КПП КАК ОтправительКонтрагентКПП,
	|	СправочникОрганизации.Наименование КАК ОтправительКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтправительКонтрагентЮрФизЛицо,
	|	ИСТИНА КАК ОтправительКонтрагентЭтоОрганизация,
	|	ШапкаДокумента.Организация КАК ОтправительКонтрагентЮридическийАдресВладелец,
	|	ШапкаДокумента.СтруктурнаяЕдиницаРезерв.Ссылка КАК ОтправительАдресВладелец,
	|	ШапкаДокумента.СтруктурнаяЕдиницаРезерв.Наименование КАК ОтправительАдресВладелецНаименование,
	|	"""" КАК ОтправительАдресПредставление,
	|	"""" КАК ОтправительАдресЗначенияПолей,
	|	"""" КАК ОтправительАдресЗначение,
	|	ШапкаДокумента.Организация КАК ОтправительКонтактноеЛицоСсылка,
	|	СправочникОрганизации.Наименование КАК ОтправительКонтактноеЛицоНаименование,
	|	"""" КАК ОтправительКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ОтправительКонтактноеЛицоТелефонЗначение,
	|	СправочникКонтрагенты.Ссылка КАК ПолучательКонтрагентСсылка,
	|	СправочникКонтрагенты.ИНН КАК ПолучательКонтрагентИНН,
	|	СправочникКонтрагенты.КПП КАК ПолучательКонтрагентКПП,
	|	СправочникКонтрагенты.Наименование КАК ПолучательКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|			ТОГДА 2
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПолучательКонтрагентЮрФизЛицо,
	|	ЛОЖЬ КАК ПолучательКонтрагентЭтоОрганизация,
	|	ШапкаДокумента.Контрагент КАК ПолучательКонтрагентЮридическийАдресВладелец,
	|	ШапкаДокумента.Контрагент КАК ПолучательАдресВладелец,
	|	СправочникКонтрагенты.Наименование КАК ПолучательАдресВладелецНаименование,
	|	"""" КАК ПолучательАдресПредставление,
	|	"""" КАК ПолучательАдресЗначенияПолей,
	|	"""" КАК ПолучательАдресЗначение,
	|	ШапкаДокумента.Контрагент КАК ПолучательКонтактноеЛицоСсылка,
	|	СправочникКонтрагенты.КонтактноеЛицо КАК ПолучательКонтактноеЛицоНаименование,
	|	"""" КАК ПолучательКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ПолучательКонтактноеЛицоТелефонЗначение,
	|	СправочникКонтрагенты.Ссылка КАК ПлательщикКонтрагентСсылка,
	|	СправочникКонтрагенты.ИНН КАК ПлательщикКонтрагентИНН,
	|	СправочникКонтрагенты.КПП КАК ПлательщикКонтрагентКПП,
	|	СправочникКонтрагенты.НаименованиеПолное КАК ПлательщикКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|			ТОГДА 2
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПлательщикКонтрагентЮрФизЛицо,
	|	ЛОЖЬ КАК ПлательщикКонтрагентЭтоОрганизация,
	|	СправочникКонтрагенты.Ссылка КАК ПлательщикКонтрагентЮридическийАдресВладелец,
	|	"""" КАК ПлательщикКонтактноеЛицоСсылка,
	|	"""" КАК ПлательщикКонтактноеЛицоНаименование,
	|	"""" КАК ПлательщикКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ПлательщикКонтактноеЛицоТелефонЗначение,
	|	2 КАК ПлательщикРоль,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ШапкаДокумента.Кратность, 0) > 0
	|				И ЕСТЬNULL(ШапкаДокумента.Курс, 0) > 0
	|			ТОГДА ШапкаДокумента.Курс / ШапкаДокумента.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ВалютаКоэффициент,
	|	1 КАК ГрузКоличествоГрузовыхМест,
	|	0 КАК ГрузОбъем,
	|	0 КАК ГрузВес,
	|	0 КАК ГрузДлина,
	|	0 КАК ГрузШирина,
	|	0 КАК ГрузВысота,
	|	"""" КАК ГрузОписание
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ШапкаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
	|		ПО ШапкаДокумента.Организация = ОрганизацииБизнесСеть.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникОрганизации
	|		ПО (СправочникОрганизации.Ссылка = ШапкаДокумента.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК СправочникКонтрагенты
	|		ПО (СправочникКонтрагенты.Ссылка = ШапкаДокумента.Контрагент)
	|ГДЕ
	|	ШапкаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Количество КАК Количество,
	|	СправочникНоменклатура.Вес КАК Вес,
	|	СправочникНоменклатура.Высота КАК Высота,
	|	СправочникНоменклатура.Длина КАК Длина,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СправочникНоменклатура.НаименованиеПолное КАК Наименование,
	|	СправочникНоменклатура.Объем КАК Объем,
	|	СправочникНоменклатура.Ширина КАК Ширина,
	|	СправочникНоменклатура.ТипНоменклатуры КАК ВидНоменклатуры,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТовары.Цена КАК Цена
	|ИЗ
	|	Документ.ЗаказПокупателя.Запасы КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.ТипНоменклатурыЗапас";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаДляЗаказаНаДоставкуПоЧекуККМ()
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ШапкаДокумента.Ссылка КАК ДокументОснование,
	|	ШапкаДокумента.Организация КАК ОрганизацияБизнесСетиСсылка,
	|	1 КАК ФормаОплаты,
	|	"""" КАК ГрузоперевозчикИдентификатор,
	|	"""" КАК ГрузоперевозчикНаименование,
	|	"""" КАК ГрузоперевозчикТелефон,
	|	"""" КАК ТарифНаименование,
	|	"""" КАК ТарифИдентификатор,
	|	"""" КАК ТарифНеГабарит,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОтгрузки,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаДоставки,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяОтгрузкиС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяОтгрузкиПо,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяДоставкиС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяДоставкиПо,
	|	1 КАК СпособОтгрузки,
	|	1 КАК СпособДоставки,
	|	СправочникОрганизации.Ссылка КАК ОтправительКонтрагентСсылка,
	|	СправочникОрганизации.ИНН КАК ОтправительКонтрагентИНН,
	|	СправочникОрганизации.КПП КАК ОтправительКонтрагентКПП,
	|	СправочникОрганизации.Наименование КАК ОтправительКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтправительКонтрагентЮрФизЛицо,
	|	ИСТИНА КАК ОтправительКонтрагентЭтоОрганизация,
	|	ШапкаДокумента.Организация КАК ОтправительКонтрагентЮридическийАдресВладелец,
	|	ШапкаДокумента.СтруктурнаяЕдиница КАК ОтправительАдресВладелец,
	|	ШапкаДокумента.СтруктурнаяЕдиница КАК ОтправительАдресВладелецНаименование,
	|	"""" КАК ОтправительАдресПредставление,
	|	"""" КАК ОтправительАдресЗначенияПолей,
	|	"""" КАК ОтправительАдресЗначение,
	|	ШапкаДокумента.Организация КАК ОтправительКонтактноеЛицоСсылка,
	|	СправочникОрганизации.Наименование КАК ОтправительКонтактноеЛицоНаименование,
	|	"""" КАК ОтправительКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ОтправительКонтактноеЛицоТелефонЗначение,
	|	СправочникКонтрагенты.Ссылка КАК ПолучательКонтрагентСсылка,
	|	СправочникКонтрагенты.ИНН КАК ПолучательКонтрагентИНН,
	|	СправочникКонтрагенты.КПП КАК ПолучательКонтрагентКПП,
	|	СправочникКонтрагенты.Наименование КАК ПолучательКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|			ТОГДА 2
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПолучательКонтрагентЮрФизЛицо,
	|	ЛОЖЬ КАК ПолучательКонтрагентЭтоОрганизация,
	|	ШапкаДокумента.Контрагент КАК ПолучательКонтрагентЮридическийАдресВладелец,
	|	ШапкаДокумента.Контрагент КАК ПолучательАдресВладелец,
	|	СправочникКонтрагенты.Наименование КАК ПолучательАдресВладелецНаименование,
	|	"""" КАК ПолучательАдресПредставление,
	|	"""" КАК ПолучательАдресЗначенияПолей,
	|	"""" КАК ПолучательАдресЗначение,
	|	ШапкаДокумента.Контрагент КАК ПолучательКонтактноеЛицоСсылка,
	|	СправочникКонтрагенты.КонтактноеЛицо КАК ПолучательКонтактноеЛицоНаименование,
	|	"""" КАК ПолучательКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ПолучательКонтактноеЛицоТелефонЗначение,
	|	СправочникКонтрагенты.Ссылка КАК ПлательщикКонтрагентСсылка,
	|	СправочникКонтрагенты.ИНН КАК ПлательщикКонтрагентИНН,
	|	СправочникКонтрагенты.КПП КАК ПлательщикКонтрагентКПП,
	|	СправочникКонтрагенты.НаименованиеПолное КАК ПлательщикКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|			ТОГДА 2
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПлательщикКонтрагентЮрФизЛицо,
	|	ЛОЖЬ КАК ПлательщикКонтрагентЭтоОрганизация,
	|	СправочникКонтрагенты.Ссылка КАК ПлательщикКонтрагентЮридическийАдресВладелец,
	|	"""" КАК ПлательщикКонтактноеЛицоСсылка,
	|	"""" КАК ПлательщикКонтактноеЛицоНаименование,
	|	"""" КАК ПлательщикКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ПлательщикКонтактноеЛицоТелефонЗначение,
	|	2 КАК ПлательщикРоль,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ШапкаДокумента.Кратность, 0) > 0
	|				И ЕСТЬNULL(ШапкаДокумента.Курс, 0) > 0
	|			ТОГДА ШапкаДокумента.Курс / ШапкаДокумента.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ВалютаКоэффициент,
	|	1 КАК ГрузКоличествоГрузовыхМест,
	|	0 КАК ГрузОбъем,
	|	0 КАК ГрузВес,
	|	0 КАК ГрузДлина,
	|	0 КАК ГрузШирина,
	|	0 КАК ГрузВысота,
	|	"""" КАК ГрузОписание
	|ИЗ
	|	Документ.ЧекККМ КАК ШапкаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
	|		ПО ШапкаДокумента.Организация = ОрганизацииБизнесСеть.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникОрганизации
	|		ПО (СправочникОрганизации.Ссылка = ШапкаДокумента.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК СправочникКонтрагенты
	|		ПО (СправочникКонтрагенты.Ссылка = ШапкаДокумента.Контрагент)
	|ГДЕ
	|	ШапкаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Количество КАК Количество,
	|	СправочникНоменклатура.Вес КАК Вес,
	|	СправочникНоменклатура.Высота КАК Высота,
	|	СправочникНоменклатура.Длина КАК Длина,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СправочникНоменклатура.НаименованиеПолное КАК Наименование,
	|	СправочникНоменклатура.Объем КАК Объем,
	|	СправочникНоменклатура.Ширина КАК Ширина,
	|	СправочникНоменклатура.ТипНоменклатуры КАК ВидНоменклатуры,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТовары.Цена КАК Цена
	|ИЗ
	|	Документ.ЧекККМ.Запасы КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаДляЗаказаНаДоставкуПоРасходнойНакладной()
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ШапкаДокумента.Ссылка КАК ДокументОснование,
	|	ШапкаДокумента.Организация КАК ОрганизацияБизнесСетиСсылка,
	|	1 КАК ФормаОплаты,
	|	"""" КАК ГрузоперевозчикИдентификатор,
	|	"""" КАК ГрузоперевозчикНаименование,
	|	"""" КАК ГрузоперевозчикТелефон,
	|	"""" КАК ТарифНаименование,
	|	"""" КАК ТарифИдентификатор,
	|	"""" КАК ТарифНеГабарит,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОтгрузки,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаДоставки,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяОтгрузкиС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяОтгрузкиПо,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяДоставкиС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяДоставкиПо,
	|	1 КАК СпособОтгрузки,
	|	1 КАК СпособДоставки,
	|	СправочникОрганизации.Ссылка КАК ОтправительКонтрагентСсылка,
	|	СправочникОрганизации.ИНН КАК ОтправительКонтрагентИНН,
	|	СправочникОрганизации.КПП КАК ОтправительКонтрагентКПП,
	|	СправочникОрганизации.Наименование КАК ОтправительКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтправительКонтрагентЮрФизЛицо,
	|	ИСТИНА КАК ОтправительКонтрагентЭтоОрганизация,
	|	ШапкаДокумента.Организация КАК ОтправительКонтрагентЮридическийАдресВладелец,
	|	ШапкаДокумента.СтруктурнаяЕдиница КАК ОтправительАдресВладелец,
	|	СправочникОрганизации.Ссылка КАК ОтправительАдресВладелецНаименование,
	|	"""" КАК ОтправительАдресПредставление,
	|	"""" КАК ОтправительАдресЗначенияПолей,
	|	"""" КАК ОтправительАдресЗначение,
	|	ШапкаДокумента.Организация КАК ОтправительКонтактноеЛицоСсылка,
	|	СправочникОрганизации.Наименование КАК ОтправительКонтактноеЛицоНаименование,
	|	"""" КАК ОтправительКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ОтправительКонтактноеЛицоТелефонЗначение,
	|	СправочникКонтрагенты.Ссылка КАК ПолучательКонтрагентСсылка,
	|	СправочникКонтрагенты.ИНН КАК ПолучательКонтрагентИНН,
	|	СправочникКонтрагенты.КПП КАК ПолучательКонтрагентКПП,
	|	СправочникКонтрагенты.Наименование КАК ПолучательКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|			ТОГДА 2
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПолучательКонтрагентЮрФизЛицо,
	|	ЛОЖЬ КАК ПолучательКонтрагентЭтоОрганизация,
	|	ШапкаДокумента.Контрагент КАК ПолучательКонтрагентЮридическийАдресВладелец,
	|	ШапкаДокумента.Контрагент КАК ПолучательАдресВладелец,
	|	СправочникКонтрагенты.Наименование КАК ПолучательАдресВладелецНаименование,
	|	"""" КАК ПолучательАдресПредставление,
	|	"""" КАК ПолучательАдресЗначенияПолей,
	|	"""" КАК ПолучательАдресЗначение,
	|	ШапкаДокумента.Контрагент КАК ПолучательКонтактноеЛицоСсылка,
	|	СправочникКонтрагенты.КонтактноеЛицо КАК ПолучательКонтактноеЛицоНаименование,
	|	"""" КАК ПолучательКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ПолучательКонтактноеЛицоТелефонЗначение,
	|	СправочникКонтрагенты.Ссылка КАК ПлательщикКонтрагентСсылка,
	|	СправочникКонтрагенты.ИНН КАК ПлательщикКонтрагентИНН,
	|	СправочникКонтрагенты.КПП КАК ПлательщикКонтрагентКПП,
	|	СправочникКонтрагенты.НаименованиеПолное КАК ПлательщикКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|			ТОГДА 2
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПлательщикКонтрагентЮрФизЛицо,
	|	ЛОЖЬ КАК ПлательщикКонтрагентЭтоОрганизация,
	|	СправочникКонтрагенты.Ссылка КАК ПлательщикКонтрагентЮридическийАдресВладелец,
	|	"""" КАК ПлательщикКонтактноеЛицоСсылка,
	|	"""" КАК ПлательщикКонтактноеЛицоНаименование,
	|	"""" КАК ПлательщикКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ПлательщикКонтактноеЛицоТелефонЗначение,
	|	2 КАК ПлательщикРоль,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ШапкаДокумента.Кратность, 0) > 0
	|				И ЕСТЬNULL(ШапкаДокумента.Курс, 0) > 0
	|			ТОГДА ШапкаДокумента.Курс / ШапкаДокумента.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ВалютаКоэффициент,
	|	1 КАК ГрузКоличествоГрузовыхМест,
	|	ШапкаДокумента.Объем КАК ГрузОбъем,
	|	ШапкаДокумента.Вес КАК ГрузВес,
	|	0 КАК ГрузДлина,
	|	0 КАК ГрузШирина,
	|	0 КАК ГрузВысота,
	|	"""" КАК ГрузОписание
	|ИЗ
	|	Документ.РасходнаяНакладная КАК ШапкаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
	|		ПО ШапкаДокумента.Организация = ОрганизацииБизнесСеть.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникОрганизации
	|		ПО (СправочникОрганизации.Ссылка = ШапкаДокумента.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК СправочникКонтрагенты
	|		ПО (СправочникКонтрагенты.Ссылка = ШапкаДокумента.Контрагент)
	|ГДЕ
	|	ШапкаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Количество КАК Количество,
	|	СправочникНоменклатура.Вес КАК Вес,
	|	СправочникНоменклатура.Высота КАК Высота,
	|	СправочникНоменклатура.Длина КАК Длина,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СправочникНоменклатура.НаименованиеПолное КАК Наименование,
	|	СправочникНоменклатура.Объем КАК Объем,
	|	СправочникНоменклатура.Ширина КАК Ширина,
	|	СправочникНоменклатура.ТипНоменклатуры КАК ВидНоменклатуры,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТовары.Цена КАК Цена
	|ИЗ
	|	Документ.РасходнаяНакладная.Запасы КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.ТипНоменклатурыЗапас
	|	И СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаДляЗаказаНаДоставкуПоПриходнойНакладной()
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ШапкаДокумента.Ссылка КАК ДокументОснование,
	|	ШапкаДокумента.Организация КАК ОрганизацияБизнесСетиСсылка,
	|	1 КАК ФормаОплаты,
	|	"""" КАК ГрузоперевозчикИдентификатор,
	|	"""" КАК ГрузоперевозчикНаименование,
	|	"""" КАК ГрузоперевозчикТелефон,
	|	"""" КАК ТарифНаименование,
	|	"""" КАК ТарифИдентификатор,
	|	"""" КАК ТарифНеГабарит,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОтгрузки,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаДоставки,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяОтгрузкиС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяОтгрузкиПо,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяДоставкиС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяДоставкиПо,
	|	1 КАК СпособОтгрузки,
	|	1 КАК СпособДоставки,
	|	СправочникОрганизации.Ссылка КАК ПолучательКонтрагентСсылка,
	|	СправочникОрганизации.ИНН КАК ПолучательКонтрагентИНН,
	|	СправочникОрганизации.КПП КАК ПолучательКонтрагентКПП,
	|	СправочникОрганизации.Наименование КАК ПолучательКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПолучательКонтрагентЮрФизЛицо,
	|	ИСТИНА КАК ПолучательКонтрагентЭтоОрганизация,
	|	ШапкаДокумента.Организация КАК ПолучательКонтрагентЮридическийАдресВладелец,
	|	ШапкаДокумента.СтруктурнаяЕдиница КАК ПолучательАдресВладелец,
	|	ШапкаДокумента.СтруктурнаяЕдиница КАК ПолучательАдресВладелецНаименование,
	|	"""" КАК ПолучательАдресПредставление,
	|	"""" КАК ПолучательАдресЗначенияПолей,
	|	"""" КАК ПолучательАдресЗначение,
	|	ШапкаДокумента.Организация КАК ПолучательКонтактноеЛицоСсылка,
	|	СправочникОрганизации.Наименование КАК ПолучательКонтактноеЛицоНаименование,
	|	"""" КАК ПолучательКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ПолучательКонтактноеЛицоТелефонЗначение,
	|	СправочникКонтрагенты.Ссылка КАК ОтправительКонтрагентСсылка,
	|	СправочникКонтрагенты.ИНН КАК ОтправительКонтрагентИНН,
	|	СправочникКонтрагенты.КПП КАК ОтправительКонтрагентКПП,
	|	СправочникКонтрагенты.Наименование КАК ОтправительКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|			ТОГДА 2
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтправительКонтрагентЮрФизЛицо,
	|	ЛОЖЬ КАК ОтправительКонтрагентЭтоОрганизация,
	|	ШапкаДокумента.Контрагент КАК ОтправительКонтрагентЮридическийАдресВладелец,
	|	ШапкаДокумента.Контрагент КАК ОтправительАдресВладелец,
	|	СправочникКонтрагенты.Наименование КАК ОтправительАдресВладелецНаименование,
	|	"""" КАК ОтправительАдресПредставление,
	|	"""" КАК ОтправительАдресЗначенияПолей,
	|	"""" КАК ОтправительАдресЗначение,
	|	ШапкаДокумента.Контрагент КАК ОтправительКонтактноеЛицоСсылка,
	|	СправочникКонтрагенты.КонтактноеЛицо КАК ОтправительКонтактноеЛицоНаименование,
	|	"""" КАК ОтправительКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ОтправительКонтактноеЛицоТелефонЗначение,
	|	СправочникОрганизации.Ссылка КАК ПлательщикКонтрагентСсылка,
	|	СправочникОрганизации.ИНН КАК ПлательщикКонтрагентИНН,
	|	СправочникОрганизации.КПП КАК ПлательщикКонтрагентКПП,
	|	СправочникОрганизации.НаименованиеПолное КАК ПлательщикКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПлательщикКонтрагентЮрФизЛицо,
	|	ИСТИНА КАК ПлательщикКонтрагентЭтоОрганизация,
	|	СправочникОрганизации.Ссылка КАК ПлательщикКонтрагентЮридическийАдресВладелец,
	|	"""" КАК ПлательщикКонтактноеЛицоСсылка,
	|	"""" КАК ПлательщикКонтактноеЛицоНаименование,
	|	"""" КАК ПлательщикКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ПлательщикКонтактноеЛицоТелефонЗначение,
	|	2 КАК ПлательщикРоль,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ШапкаДокумента.Кратность, 0) > 0
	|				И ЕСТЬNULL(ШапкаДокумента.Курс, 0) > 0
	|			ТОГДА ШапкаДокумента.Курс / ШапкаДокумента.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ВалютаКоэффициент,
	|	1 КАК ГрузКоличествоГрузовыхМест,
	|	0 КАК ГрузОбъем,
	|	0 КАК ГрузВес,
	|	0 КАК ГрузДлина,
	|	0 КАК ГрузШирина,
	|	0 КАК ГрузВысота,
	|	"""" КАК ГрузОписание
	|ИЗ
	|	Документ.ПриходнаяНакладная КАК ШапкаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
	|		ПО ШапкаДокумента.Организация = ОрганизацииБизнесСеть.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникОрганизации
	|		ПО (СправочникОрганизации.Ссылка = ШапкаДокумента.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК СправочникКонтрагенты
	|		ПО (СправочникКонтрагенты.Ссылка = ШапкаДокумента.Контрагент)
	|ГДЕ
	|	ШапкаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Количество КАК Количество,
	|	СправочникНоменклатура.Вес КАК Вес,
	|	СправочникНоменклатура.Высота КАК Высота,
	|	СправочникНоменклатура.Длина КАК Длина,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СправочникНоменклатура.НаименованиеПолное КАК Наименование,
	|	СправочникНоменклатура.Объем КАК Объем,
	|	СправочникНоменклатура.Ширина КАК Ширина,
	|	СправочникНоменклатура.ТипНоменклатуры КАК ВидНоменклатуры,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТовары.Цена КАК Цена
	|ИЗ
	|	Документ.ПриходнаяНакладная.Запасы КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДляЗаказаНаДоставкуПоЗаказуПоставщику()
	 ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ШапкаДокумента.Ссылка КАК ДокументОснование,
	|	ШапкаДокумента.Организация КАК ОрганизацияБизнесСетиСсылка,
	|	1 КАК ФормаОплаты,
	|	"""" КАК ГрузоперевозчикИдентификатор,
	|	"""" КАК ГрузоперевозчикНаименование,
	|	"""" КАК ГрузоперевозчикТелефон,
	|	"""" КАК ТарифНаименование,
	|	"""" КАК ТарифИдентификатор,
	|	"""" КАК ТарифНеГабарит,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОтгрузки,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаДоставки,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяОтгрузкиС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяОтгрузкиПо,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяДоставкиС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ВремяДоставкиПо,
	|	1 КАК СпособОтгрузки,
	|	1 КАК СпособДоставки,
	|	СправочникОрганизации.Ссылка КАК ПолучательКонтрагентСсылка,
	|	СправочникОрганизации.ИНН КАК ПолучательКонтрагентИНН,
	|	СправочникОрганизации.КПП КАК ПолучательКонтрагентКПП,
	|	СправочникОрганизации.Наименование КАК ПолучательКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПолучательКонтрагентЮрФизЛицо,
	|	ИСТИНА КАК ПолучательКонтрагентЭтоОрганизация,
	|	ШапкаДокумента.Организация КАК ПолучательКонтрагентЮридическийАдресВладелец,
	|	ШапкаДокумента.СтруктурнаяЕдиницаРезерв КАК ПолучательАдресВладелец,
	|	ШапкаДокумента.СтруктурнаяЕдиницаРезерв КАК ПолучательАдресВладелецНаименование,
	|	"""" КАК ПолучательАдресПредставление,
	|	"""" КАК ПолучательАдресЗначенияПолей,
	|	"""" КАК ПолучательАдресЗначение,
	|	ШапкаДокумента.Организация КАК ПолучательКонтактноеЛицоСсылка,
	|	СправочникОрганизации.Наименование КАК ПолучательКонтактноеЛицоНаименование,
	|	"""" КАК ПолучательКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ПолучательКонтактноеЛицоТелефонЗначение,
	|	СправочникКонтрагенты.Ссылка КАК ОтправительКонтрагентСсылка,
	|	СправочникКонтрагенты.ИНН КАК ОтправительКонтрагентИНН,
	|	СправочникКонтрагенты.КПП КАК ОтправительКонтрагентКПП,
	|	СправочникКонтрагенты.Наименование КАК ОтправительКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|			ТОГДА 2
	|		КОГДА СправочникКонтрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтправительКонтрагентЮрФизЛицо,
	|	ЛОЖЬ КАК ОтправительКонтрагентЭтоОрганизация,
	|	ШапкаДокумента.Контрагент КАК ОтправительКонтрагентЮридическийАдресВладелец,
	|	ШапкаДокумента.Контрагент КАК ОтправительАдресВладелец,
	|	СправочникКонтрагенты.Наименование КАК ОтправительАдресВладелецНаименование,
	|	"""" КАК ОтправительАдресПредставление,
	|	"""" КАК ОтправительАдресЗначенияПолей,
	|	"""" КАК ОтправительАдресЗначение,
	|	ШапкаДокумента.Контрагент КАК ОтправительКонтактноеЛицоСсылка,
	|	СправочникКонтрагенты.КонтактноеЛицо КАК ОтправительКонтактноеЛицоНаименование,
	|	"""" КАК ОтправительКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ОтправительКонтактноеЛицоТелефонЗначение,
	|	СправочникОрганизации.Ссылка КАК ПлательщикКонтрагентСсылка,
	|	СправочникОрганизации.ИНН КАК ПлательщикКонтрагентИНН,
	|	СправочникОрганизации.КПП КАК ПлательщикКонтрагентКПП,
	|	СправочникОрганизации.НаименованиеПолное КАК ПлательщикКонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА 1
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПлательщикКонтрагентЮрФизЛицо,
	|	ИСТИНА КАК ПлательщикКонтрагентЭтоОрганизация,
	|	СправочникОрганизации.Ссылка КАК ПлательщикКонтрагентЮридическийАдресВладелец,
	|	"""" КАК ПлательщикКонтактноеЛицоСсылка,
	|	"""" КАК ПлательщикКонтактноеЛицоНаименование,
	|	"""" КАК ПлательщикКонтактноеЛицоТелефонПредставление,
	|	"""" КАК ПлательщикКонтактноеЛицоТелефонЗначение,
	|	2 КАК ПлательщикРоль,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ШапкаДокумента.Кратность, 0) > 0
	|				И ЕСТЬNULL(ШапкаДокумента.Курс, 0) > 0
	|			ТОГДА ШапкаДокумента.Курс / ШапкаДокумента.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ВалютаКоэффициент,
	|	1 КАК ГрузКоличествоГрузовыхМест,
	|	0 КАК ГрузОбъем,
	|	0 КАК ГрузВес,
	|	0 КАК ГрузДлина,
	|	0 КАК ГрузШирина,
	|	0 КАК ГрузВысота,
	|	"""" КАК ГрузОписание
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ШапкаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
	|		ПО ШапкаДокумента.Организация = ОрганизацииБизнесСеть.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникОрганизации
	|		ПО (СправочникОрганизации.Ссылка = ШапкаДокумента.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК СправочникКонтрагенты
	|		ПО (СправочникКонтрагенты.Ссылка = ШапкаДокумента.Контрагент)
	|ГДЕ
	|	ШапкаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Количество КАК Количество,
	|	СправочникНоменклатура.Вес КАК Вес,
	|	СправочникНоменклатура.Высота КАК Высота,
	|	СправочникНоменклатура.Длина КАК Длина,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СправочникНоменклатура.НаименованиеПолное КАК Наименование,
	|	СправочникНоменклатура.Объем КАК Объем,
	|	СправочникНоменклатура.Ширина КАК Ширина,
	|	СправочникНоменклатура.ТипНоменклатуры КАК ВидНоменклатуры,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТовары.Цена КАК Цена
	|ИЗ
	|	Документ.ЗаказПоставщику.Запасы КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ДобавитьПараметрыВСтруктуру(СтрокаФормулы, МассивПараметров, Отказ = Ложь)

	Формула = СтрокаФормулы;
	
	НачОперанда = СтрНайти(Формула, "[");
	КонОперанда = СтрНайти(Формула, "]");
     
	ЕстьОперанд = Истина;
	Пока ЕстьОперанд Цикл
     
		Если НачОперанда <> 0 И КонОперанда <> 0 Тогда
			
            Идентификатор = СокрЛП(Сред(Формула, НачОперанда+1, КонОперанда - НачОперанда - 1));
            Формула = Прав(Формула, СтрДлина(Формула) - КонОперанда);   
			
			Попытка
				Если МассивПараметров.Найти(Идентификатор)=Неопределено Тогда
					МассивПараметров.Добавить(Идентификатор);
				КонецЕсли;
			Исключение
				Отказ = Истина;
			    Прервать;
			КонецПопытки 
			 
		КонецЕсли;     
          
		НачОперанда = СтрНайти(Формула, "[");
		КонОперанда = СтрНайти(Формула, "]");
          
		Если НЕ (НачОперанда <> 0 И КонОперанда <> 0) Тогда
			ЕстьОперанд = Ложь;
        КонецЕсли;     
               
	КонецЦикла;	

КонецПроцедуры

// Функция выполняет расчет по формуле.
// 
// Параметры:
// 	Контекст - ДанныеФормыСтруктура - данные заказа покупателя,
// 	Формула - Строка - формула для расчета,
// 	ДополнительныеПараметры - Структура - структура значений параметров, устанавливаемых вручную.
// Возвращаемое значение:
// 	Число - результат расчета по формуле.
Функция РассчитатьПоФормуле(Контекст, Формула, ДополнительныеПараметры)

	Отказ = Ложь;
	
	МассивПараметров = Новый Массив;
	ДобавитьПараметрыВСтруктуру(Формула, МассивПараметров, Отказ);
	
	Если Отказ Тогда
		Возврат 0;
	КонецЕсли;
	
	СтруктураОбщихПараметров = Новый Структура;
	СтруктураОбщихПараметров.Вставить("Заказ", Контекст.Ссылка);
	СтруктураОбщихПараметров.Вставить("Контрагент", Контекст.Контрагент);
	СтруктураОбщихПараметров.Вставить("ЗаказДата", Контекст.Дата);
	СтруктураОбщихПараметров.Вставить("ЗаказВидЦен", Контекст.ВидЦен);
	СтруктураОбщихПараметров.Вставить("ЗаказНоменклатураДоставки", Контекст.НоменклатураДоставки);
	СтруктураОбщихПараметров.Вставить("Запасы", Контекст.Запасы.Выгрузить());
	
	// 1. Выборка и расчет параметров
	Параметры = Новый Соответствие;
	МассивИдентификаторов = Новый Массив;
	Для каждого Параметр Из МассивПараметров Цикл
		МассивИдентификаторов.Добавить(Параметр); 
	КонецЦикла; 
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Идентификаторы", МассивИдентификаторов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПараметрыРасчетовДоставки.Ссылка КАК Параметр,
	|	ПараметрыРасчетовДоставки.Идентификатор,
	|	ПараметрыРасчетовДоставки.ЗадаватьЗначениеПриРасчете,
	|	ПараметрыРасчетовДоставки.Запрос,
	|	ПараметрыРасчетовДоставки.ПроизвольныйЗапрос
	|ИЗ
	|	Справочник.ПараметрыРасчетовДоставки КАК ПараметрыРасчетовДоставки
	|ГДЕ
	|	ПараметрыРасчетовДоставки.Идентификатор В(&Идентификаторы)";
	ВыборкаПараметры = Запрос.Выполнить().Выбрать();
	Пока ВыборкаПараметры.Следующий() Цикл
		Если ВыборкаПараметры.ЗадаватьЗначениеПриРасчете Тогда
			Продолжить;
		КонецЕсли; 
		ТекстОшибки = "";
		СтруктураОтборов = Новый Структура;
		Для каждого Элемент Из СтруктураОбщихПараметров Цикл
			СтруктураОтборов.Вставить(Элемент.Ключ, Элемент.Значение);
		КонецЦикла; 
		ЗначениеПараметра = ЗарплатаИПерсоналСлужебный.РассчитатьЗначениеПараметра(СтруктураОтборов,
			ВыборкаПараметры.Параметр, ТекстОшибки);
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			Возврат 0;
		КонецЕсли;
		Параметры.Вставить(ВыборкаПараметры.Идентификатор, ЗначениеПараметра);
	КонецЦикла;
	
	// 2. Добавление параметров из контекста
	РеквизитыКонтрагента = "";
	ДопРеквизитыЗаказа = Новый Массив;
	ДопРеквизитыКонтрагента = Новый Массив;
	Для каждого Параметр Из МассивПараметров Цикл
		Позиция = Найти(Параметр, ".");
		Если Позиция=0 Тогда
			Продолжить;
		КонецЕсли; 
		ИмяРеквизита = Сред(Параметр, Позиция+1);
		Если ВРег(Лев(Параметр, 5))="ЗАКАЗ" Тогда
			Если ВРег(ИмяРеквизита)="СУММАДОКУМЕНТА" Тогда
				// Сумму документа нужно предварительно пересчитать
				СуммаДокумента = 0;
				Для каждого Стр Из Контекст.Запасы Цикл
					Если Стр.ЭтоРазделитель ИЛИ Стр.НомерВариантаКП <> Контекст.ОсновнойВариантКП Тогда
						Продолжить;
					КонецЕсли;
					СуммаДокумента = СуммаДокумента + Стр.Всего;
				КонецЦикла;
				СуммаДокумента = СуммаДокумента + Контекст.Работы.Итог("Всего");
				Параметры.Вставить(Параметр, СуммаДокумента);
			ИначеЕсли Найти(ИмяРеквизита, " ")>0 ИЛИ НЕ Контекст.Свойство(ИмяРеквизита) Тогда
				ДопРеквизитыЗаказа.Добавить(ИмяРеквизита);
			Иначе
				Параметры.Вставить(Параметр, Контекст[ИмяРеквизита]);
			КонецЕсли; 
		ИначеЕсли ВРег(Лев(Параметр, 10))="КОНТРАГЕНТ" Тогда
			Если Найти(ИмяРеквизита, " ")>0 ИЛИ Метаданные.Справочники.Контрагенты.Реквизиты.Найти(ИмяРеквизита)=Неопределено Тогда
				ДопРеквизитыКонтрагента.Добавить(ИмяРеквизита);
			Иначе
				РеквизитыКонтрагента = РеквизитыКонтрагента+?(ПустаяСтрока(РеквизитыКонтрагента), "", ", ")+ИмяРеквизита;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	Если НЕ ПустаяСтрока(РеквизитыКонтрагента) Тогда
		СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контекст.Контрагент, РеквизитыКонтрагента);
		Для каждого Элемент Из СтруктураРеквизитов Цикл
			Параметры.Вставить("Контрагент."+Элемент.Ключ, Элемент.Значение);
		КонецЦикла; 
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДопРеквизитыКонтрагента", ДопРеквизитыКонтрагента);
	Запрос.УстановитьПараметр("ДопРеквизитыЗаказа", ДопРеквизитыЗаказа);
	Запрос.УстановитьПараметр("Контрагент", Контекст.Контрагент);
	Запрос.УстановитьПараметр("ЗначенияДопРеквизитов", Контекст.ДополнительныеРеквизиты.Выгрузить());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ЗначенияДопРеквизитов.Свойство КАК ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения) КАК Свойство,
	|	ЗначенияДопРеквизитов.Значение
	|ПОМЕСТИТЬ ЗначенияДопРеквизитов
	|ИЗ
	|	&ЗначенияДопРеквизитов КАК ЗначенияДопРеквизитов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Контрагент."" + КонтрагентыДополнительныеРеквизиты.Свойство.Заголовок КАК Идентификатор,
	|	КонтрагентыДополнительныеРеквизиты.Значение
	|ИЗ
	|	Справочник.Контрагенты.ДополнительныеРеквизиты КАК КонтрагентыДополнительныеРеквизиты
	|ГДЕ
	|	КонтрагентыДополнительныеРеквизиты.Свойство.Заголовок В(&ДопРеквизитыКонтрагента)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Заказ."" + ЗначенияДопРеквизитов.Свойство.Заголовок,
	|	ЗначенияДопРеквизитов.Значение
	|ИЗ
	|	ЗначенияДопРеквизитов КАК ЗначенияДопРеквизитов
	|ГДЕ
	|	ЗначенияДопРеквизитов.Свойство.Заголовок В(&ДопРеквизитыЗаказа)";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Параметры.Вставить(Выборка.Идентификатор, Выборка.Значение);	
	КонецЦикла;
	
	// 3. Добавление значений параметров, заданных вручную
	Для каждого ДополнительныйПараметр Из ДополнительныеПараметры Цикл
		Параметры.Вставить(ДополнительныйПараметр.Ключ, ДополнительныйПараметр.Значение);
	КонецЦикла; 
	
	Для каждого ДопРеквизит Из ДопРеквизитыКонтрагента Цикл
		ИмяПараметра = "Контрагент."+ДопРеквизит;
		Если Параметры.Получить(ИмяПараметра)=Неопределено Тогда
			Параметры.Вставить(ИмяПараметра, Неопределено);
		КонецЕсли; 
	КонецЦикла; 
	Для каждого ДопРеквизит Из ДопРеквизитыЗаказа Цикл
		ИмяПараметра = "Заказ."+ДопРеквизит;
		Если Параметры.Получить(ИмяПараметра)=Неопределено Тогда
			Параметры.Вставить(ИмяПараметра, Неопределено);
		КонецЕсли; 
	КонецЦикла; 
	
	// 4. Подстановка значений параметров и расчет по формуле
	Для каждого Параметр Из Параметры Цикл
		Формула = СтрЗаменить(Формула, "[" + Параметр.Ключ + "]", "Параметры.Получить("""+Параметр.Ключ+""")");
	КонецЦикла;
	
	// 5. Значения формулы
	//@skip-warning используется в ОбщегоНазначения.ВычислитьВБезопасномРежиме
	ЗначенияФормулы = Новый Структура;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СлужбаДоставки", Контекст.СлужбаДоставки);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СлужбыДоставкиЗначенияФормулы.Идентификатор,
	|	СлужбыДоставкиЗначенияФормулы.Значение
	|ИЗ
	|	Справочник.СлужбыДоставки.ЗначенияФормулы КАК СлужбыДоставкиЗначенияФормулы
	|ГДЕ
	|	СлужбыДоставкиЗначенияФормулы.Ссылка = &СлужбаДоставки";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ИмяПараметра = "ЗначениеФормулы."+Выборка.Идентификатор;
		Формула = СтрЗаменить(Формула, "[" + Выборка.Идентификатор + "]", "Параметры.Получить("""+ИмяПараметра+""")");
		Параметры.Вставить(ИмяПараметра, Выборка.Значение);
	КонецЦикла; 
	
	Попытка
		Результат = ОбщегоНазначения.ВычислитьВБезопасномРежиме(Формула, Параметры);
	Исключение
		ТекстСообщения = НСтр(
			"ru = 'Не удалось рассчитать параметры доставки.
			|Возможно, формула содержит ошибку или не заполнены показатели.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Результат = 0;
	КонецПопытки;
	
	Возврат Результат; 

КонецФункции // РассчитатьПоФормулам()

// Функция выполняет расчет объема и веса запасов
Функция РассчитатьОбъемИВесЗапасов(ТаблицаЗапасов)Экспорт
	
	Результат = Новый Структура("Объем, Вес",0,0);
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаНоменклатура.Номенклатура,
	|	ТаблицаНоменклатура.Количество
	|ПОМЕСТИТЬ ВТНоменклатура
	|ИЗ
	|	&ТаблицаНоменклатура КАК ТаблицаНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(СправочникНоменклатура.Вес * ТаблицаНоменклатура.Количество) КАК Вес,
	|	СУММА(СправочникНоменклатура.Объем * ТаблицаНоменклатура.Количество) КАК Объем
	|ИЗ
	|	ВТНоменклатура КАК ТаблицаНоменклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО (СправочникНоменклатура.Ссылка = ТаблицаНоменклатура.Номенклатура)");
	Запрос.УстановитьПараметр("ТаблицаНоменклатура", ТаблицаЗапасов);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьВложенныеРеквизиты(СтрокаРодитель, МетаданныеОбъекта, НаборСвойств, ИменаРеквизитов, ТолькоЧисловые)
	
	СписокРеквизитов = Новый СписокЗначений;
	Реквизиты = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрЗаменить(ИменаРеквизитов, Символы.ПС, ""));
	
	Для каждого Реквизит Из МетаданныеОбъекта.СтандартныеРеквизиты Цикл
		Если Реквизиты.Найти(Реквизит.Имя)=Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ТолькоЧисловые И НЕ Реквизит.Тип.СодержитТип(Тип("Число")) Тогда
			Продолжить;
		КонецЕсли; 
		СписокРеквизитов.Добавить(Реквизит.Тип, СтрокаРодитель.Идентификатор+"."+Реквизит.Имя);
	КонецЦикла; 	
	Для каждого Реквизит Из МетаданныеОбъекта.Реквизиты Цикл
		Если Реквизиты.Найти(Реквизит.Имя)=Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		Если ТолькоЧисловые И НЕ Реквизит.Тип.СодержитТип(Тип("Число")) Тогда
			Продолжить;
		КонецЕсли; 
		СписокРеквизитов.Добавить(Реквизит.Тип, СтрокаРодитель.Идентификатор+"."+Реквизит.Имя);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаборСвойств", НаборСвойств);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДополнительныеРеквизиты.Свойство КАК Параметр,
	|	ДополнительныеРеквизиты.Свойство.Заголовок КАК Имя,
	|	ДополнительныеРеквизиты.Свойство.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
	|ГДЕ
	|	ДополнительныеРеквизиты.Ссылка = &НаборСвойств
	|	И НЕ ДополнительныеРеквизиты.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДополнительныеСведения.Свойство,
	|	ДополнительныеСведения.Свойство.Заголовок,
	|	ДополнительныеСведения.Свойство.ТипЗначения
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|ГДЕ
	|	ДополнительныеСведения.Ссылка = &НаборСвойств
	|	И НЕ ДополнительныеСведения.ПометкаУдаления";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ТолькоЧисловые И Не Выборка.ТипЗначения.СодержитТип(Тип("Число")) Тогда
			Продолжить;
		КонецЕсли;
		СписокРеквизитов.Добавить(Выборка.ТипЗначения, СтрокаРодитель.Идентификатор + "." + Выборка.Имя);
	КонецЦикла;
	
	СписокРеквизитов.СортироватьПоПредставлению();
	Для каждого ЭлементСписка Из СписокРеквизитов Цикл
		СтрокаРеквизит = СтрокаРодитель.ПолучитьЭлементы().Добавить();
		СтрокаРеквизит.Идентификатор = ЭлементСписка.Представление;
		СтрокаРеквизит.Тип = ЭлементСписка.Значение;
	КонецЦикла; 
	
КонецПроцедуры

Функция АдресОтправленияИзСклада() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("РезервированиеЗапасов") И ПолучитьФункциональнуюОпцию("УчетПоНесколькимСкладам");
	
КонецФункции

Функция ПоляАдресаОтправления(Субъект) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("АдресОтправления", "");
	Результат.Вставить("АдресОтправленияИсточник", Субъект);
	Результат.Вставить("АдресОтправленияЗначение", "");
	Результат.Вставить("АдресОтправленияНавигационнаяСсылка", "");
	Результат.Вставить("АдресОтправленияПодсказка", Новый ФорматированнаяСтрока(""));
	Результат.Вставить("АдресОтправленияЗначенияПолей", "");
	
	Если Не ЗначениеЗаполнено(Субъект) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат.АдресОтправленияНавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Субъект);
	Результат.АдресОтправленияПодсказка = ПодсказкаДляАдресаОтправления(Субъект);
		
	Если ТипЗнч(Субъект) = Тип("СправочникСсылка.Организации") Тогда
		ВидКИ = Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации;
	ИначеЕсли ТипЗнч(Субъект) = Тип("СправочникСсылка.Контрагенты") Тогда
		ВидКИ = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
	Иначе
		ВидКИ = Справочники.ВидыКонтактнойИнформации.ФактАдресСтруктурнойЕдиницы;
	КонецЕсли;
	
	Адреса = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Субъект),
		,
		ВидКИ,
		ТекущаяДатаСеанса());
	
	Если Адреса.Количество() > 0 Тогда
		Результат.АдресОтправления = Адреса[0].Представление;
		Результат.АдресОтправленияЗначение = Адреса[0].Значение;
		Результат.АдресОтправленияЗначенияПолей = Адреса[0].ЗначенияПолей;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

Функция ПодсказкаДляАдресаОтправления(Субъект)
	
	Строки = Новый Массив;
	
	Строки.Добавить(НСтр("ru = 'В качестве адреса отправления используется фактический адрес'"));
	Строки.Добавить(" ");
	
	Если ТипЗнч(Субъект) = Тип("СправочникСсылка.Организации") Тогда
		Строки.Добавить(НСтр("ru = 'организации'"));
	ИначеЕсли ТипЗнч(Субъект) = Тип("СправочникСсылка.СтруктурныеЕдиницы") Тогда
		Строки.Добавить(НСтр("ru = 'склада отгрузки'"));
	КонецЕсли;
	
	Строки.Добавить(" ");
	Строки.Добавить(Новый ФорматированнаяСтрока(Строка(Субъект),,,, ПолучитьНавигационнуюСсылку(Субъект)));
	Строки.Добавить(".");
	
	Возврат Новый ФорматированнаяСтрока(Строки);
	
КонецФункции

Функция ОжидаемыйТипЗначенияКолонкиСтроки(ТекущийТипЗначения)
	
	ВычитаемыеТипы = Новый Массив;
	ВычитаемыеТипы.Добавить(Тип("Null"));
	Возврат Новый ОписаниеТипов(ТекущийТипЗначения,, ВычитаемыеТипы);
	
КонецФункции

Процедура ПривестиЗначенияКолонокСтроки(СтрокаТабличнойЧасти)
	
	Владелец = СтрокаТабличнойЧасти.Владелец();
	Для каждого Колонка Из Владелец.Колонки Цикл
		ТекущееЗначение = СтрокаТабличнойЧасти[Колонка.Имя];
		Если ЗначениеЗаполнено(ТекущееЗначение) Тогда
			Продолжить;
		КонецЕсли;
		СтрокаТабличнойЧасти[Колонка.Имя] = ОжидаемыйТипЗначенияКолонкиСтроки(Колонка.ТипЗначения).ПривестиЗначение(ТекущееЗначение);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти