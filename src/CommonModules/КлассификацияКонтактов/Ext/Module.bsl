
#Область ФормаОбъекта

// Процедура создает элементы отображения тегов в форме объекта
//
// Параметры:
//  Форма	 - 	 - 
Процедура ОбновитьОблакоТегов(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	МассивУдаляемых = Новый Массив;
	Для Каждого ЭлементТег Из Элементы.ОблакоТегов.ПодчиненныеЭлементы Цикл
		Если Лев(ЭлементТег.Имя, 12) = "СтрокаТегов_" И НЕ ЭлементТег.Имя = "СтрокаТегов_1" Тогда
			МассивУдаляемых.Добавить(ЭлементТег);
		КонецЕсли;
	КонецЦикла;
	Для Каждого ЭлементТег Из Элементы.СтрокаТегов_1.ПодчиненныеЭлементы Цикл
		Если Лев(ЭлементТег.Имя, 4) = "Тег_" Тогда
			МассивУдаляемых.Добавить(ЭлементТег);
		КонецЕсли;
	КонецЦикла;
	Для Каждого ЭлементТег Из МассивУдаляемых Цикл
		Элементы.Удалить(ЭлементТег);
	КонецЦикла;
	
	МаксДлинаПервойСтроки = 61;
	МаксДлинаСтроки = МаксДлинаПервойСтроки + 24;
	НомерЭлемента = 0;
	НомерСтрокиЭлементов = 1;
	ТекущаяДлинаСтроки = 0;
	ГруппаТегов = Элементы.СтрокаТегов_1;
	
	Для Каждого СтрокаТегов Из Объект.Теги Цикл
		
		НомерЭлемента = НомерЭлемента + 1;
		ПредставлениеТега = Строка(СтрокаТегов.Тег);
		Если СтрДлина(ПредставлениеТега) > 15 Тогда
			ПредставлениеТега = Лев(ПредставлениеТега, 15) + "...";
			ДлинаТега = 15 + 1;
		Иначе
			ДлинаТега = СтрДлина(ПредставлениеТега) + 2;
		КонецЕсли;
		
		ТекущаяДлинаСтроки = ТекущаяДлинаСтроки + ДлинаТега;
		
		Если (НомерСтрокиЭлементов = 1 И ТекущаяДлинаСтроки > МаксДлинаПервойСтроки) Или (НомерСтрокиЭлементов > 1 И ТекущаяДлинаСтроки > МаксДлинаСтроки) Тогда
			
			ТекущаяДлинаСтроки = ДлинаТега;
			НомерСтрокиЭлементов = НомерСтрокиЭлементов + 1;
			
			ГруппаТегов = Элементы.Добавить("СтрокаТегов_" + НомерСтрокиЭлементов, Тип("ГруппаФормы"), ?(НомерСтрокиЭлементов = 1, Элементы.ПерваяСтрока, Элементы.ОблакоТегов));
			ГруппаТегов.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ГруппаТегов.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно;
			ГруппаТегов.ОтображатьЗаголовок = Ложь;
			ГруппаТегов.Отображение = ОтображениеОбычнойГруппы.Нет;
			ГруппаТегов.РастягиватьПоВертикали = Ложь;
			ГруппаТегов.Высота = 1;
			
		КонецЕсли;
		
		КомпонентыТега = Новый Массив;
		КомпонентыТега.Добавить(Новый ФорматированнаяСтрока(ПредставлениеТега + " "));
		КомпонентыТега.Добавить(Новый ФорматированнаяСтрока(БиблиотекаКартинок.Очистить, , , , "ТегИД_" + СтрокаТегов.ПолучитьИдентификатор()));
		
		ЭлементТег = Элементы.Добавить("Тег_" + НомерЭлемента, Тип("ДекорацияФормы"), ГруппаТегов);
		ЭлементТег.Вид = ВидДекорацииФормы.Надпись;
		ЭлементТег.Заголовок = Новый ФорматированнаяСтрока(КомпонентыТега);
		ЭлементТег.Подсказка = Строка(СтрокаТегов.Тег);
		ЭлементТег.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
		ЭлементТег.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.Одинарная, 1);
		ЭлементТег.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
		ЭлементТег.Ширина = СтрДлина(ПредставлениеТега) + 2;
		ЭлементТег.УстановитьДействие("ОбработкаНавигационнойСсылки", "Подключаемый_ТегОбработкаНавигационнойСсылки");
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ФормаСписка

// Процедура создает элементы формы для отборов по периодам
//
// Параметры:
//  Форма	 - форма списка
Процедура ОбновитьЗначенияОтбораПериодов(ТаблицаОтборСозданы) Экспорт
	
	ТаблицаОтборСозданы.Очистить();
	
	ДатаСеанса = ТекущаяДатаСеанса();
	
	ПериодПроизвольный = ТаблицаОтборСозданы.Добавить();
	
	ПериодСегодня = ТаблицаОтборСозданы.Добавить();
	ПериодСегодня.Значение.Вариант = ВариантСтандартногоПериода.Сегодня;
	
	Период3Дня = ТаблицаОтборСозданы.Добавить();
	Период3Дня.Значение.ДатаНачала = НачалоДня(ДатаСеанса) - 2*24*3600;
	Период3Дня.Значение.ДатаОкончания = КонецДня(ДатаСеанса);
	
	ПериодНеделя = ТаблицаОтборСозданы.Добавить();
	ПериодНеделя.Значение.Вариант = ВариантСтандартногоПериода.Последние7Дней;
	
	ПериодМесяц = ТаблицаОтборСозданы.Добавить();
	ПериодМесяц.Значение.Вариант = ВариантСтандартногоПериода.Месяц;
	
КонецПроцедуры

// Процедура создает элементы формы для отборов по тегам
//
// Параметры:
//  Форма					 - форма списка
//  МаксимальнаяДлинаСтроки	 - число - максимальное количество символов помещающихся в одну строку
Процедура ОбновитьЗначенияОтбораТегов(Форма, ГруппаОтборТеги, МаксимальнаяДлинаСтроки = 85, Префикс = "") Экспорт
	
	Элементы = Форма.Элементы;
	
	ТаблицаОтборТеги = Форма[Префикс + "ОтборТеги"];
	ТаблицаОтборТеги.Очистить();
	
	МассивУдаляемыхЭлементов = Новый Массив;
	Для Каждого Элемент Из ГруппаОтборТеги.ПодчиненныеЭлементы Цикл
		Если Лев(Элемент.Имя, 4) = Префикс + "Тег_" Или Лев(Элемент.Имя, 11) = Префикс + "СтрокаТегов" Тогда
			МассивУдаляемыхЭлементов.Добавить(Элемент);
		КонецЕсли;
	КонецЦикла;
	Для Каждого Элемент Из МассивУдаляемыхЭлементов Цикл
		Элементы.Удалить(Элемент);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Теги.Ссылка,
		|	Теги.Представление КАК Представление
		|ИЗ
		|	Справочник.Теги КАК Теги
		|ГДЕ
		|	Теги.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Представление";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НомерЭлемента = 0;
	НомерСтрокиЭлементов = 0;
	ТекущаяДлинаСтроки = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Если СтрДлина(Выборка.Представление) > 15 Тогда
			ПредставлениеТега = Лев(Выборка.Представление, 15) + "...";
			ТекущаяДлинаСтроки = ТекущаяДлинаСтроки + 15 + 2;
		Иначе
			ПредставлениеТега = Выборка.Представление;
			ТекущаяДлинаСтроки = ТекущаяДлинаСтроки + СтрДлина(ПредставлениеТега) + 2;
		КонецЕсли;
		
		СтрокаОтбораТегов = ТаблицаОтборТеги.Добавить(Выборка.Ссылка, ПредставлениеТега);
		
		Если НомерСтрокиЭлементов = 0 Или ТекущаяДлинаСтроки > МаксимальнаяДлинаСтроки Тогда
			
			ТекущаяДлинаСтроки = СтрДлина(ПредставлениеТега) + 2;
			НомерСтрокиЭлементов = НомерСтрокиЭлементов + 1;
			
			ГруппаТегов = Элементы.Добавить(Префикс + "СтрокаТегов" + НомерСтрокиЭлементов, Тип("ГруппаФормы"), ГруппаОтборТеги);
			ГруппаТегов.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ГруппаТегов.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно;
			ГруппаТегов.ОтображатьЗаголовок = Ложь;
			ГруппаТегов.Отображение = ОтображениеОбычнойГруппы.Нет;
			ГруппаТегов.РастягиватьПоВертикали = Ложь;
			ГруппаТегов.Высота = 1;
			
		КонецЕсли;
		
		ЭлементТег = Элементы.Добавить(Префикс + "Тег_" + СтрокаОтбораТегов.ПолучитьИдентификатор(), Тип("ПолеФормы"), ГруппаТегов);
		ЭлементТег.Вид = ВидПоляФормы.ПолеНадписи;
		ЭлементТег.ПутьКДанным = Префикс + "ОтборТеги" + "[" + НомерЭлемента + "].Представление";
		ЭлементТег.Гиперссылка = Истина;
		ЭлементТег.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ЭлементТег.Подсказка = Выборка.Представление;
		ЭлементТег.ЦветТекста = ЦветаСтиля.ЦветТекстаПоля;
		ЭлементТег.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
		ЭлементТег.Ширина = СтрДлина(ПредставлениеТега);
		ЭлементТег.РастягиватьПоГоризонтали = Ложь;
		ЭлементТег.УстановитьДействие("Нажатие", "Подключаемый_" + Префикс + "ТегОтборНажатие");
		
		НомерЭлемента = НомерЭлемента + 1;
		
	КонецЦикла;
	
	Если Выборка.Количество() = 0 Тогда
		
		ЭлементПояснение = Элементы.Добавить(Префикс + "Тег_Пояснение", Тип("ДекорацияФормы"), ГруппаОтборТеги);
		ЭлементПояснение.Вид = ВидДекорацииФормы.Надпись;
		ЭлементПояснение.Гиперссылка = Истина;
		ЭлементПояснение.Заголовок = НСтр("ru='Как работать с тегами?'");
		ЭлементПояснение.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
		ЭлементПояснение.УстановитьДействие("Нажатие", "Подключаемый_" + Префикс + "ТегОтборНажатие");
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура создает элементы формы для отборов по сегментам
//
// Параметры:
//  Форма	 - форма списка
//  МаксимальнаяДлинаСтроки	 - число - максимальное количество символов помещающихся в одну строку
Процедура ОбновитьЗначенияОтбораСегментов(Форма, ГруппаОтборСегменты, МаксимальнаяДлинаСтроки = 85, Префикс = "") Экспорт
	
	Элементы = Форма.Элементы;
	
	ТаблицаОтборСегменты = Форма[Префикс + "ОтборСегменты"];
	ТаблицаОтборСегменты.Очистить();
	
	МассивУдаляемыхЭлементов = Новый Массив;
	Для Каждого Элемент Из ГруппаОтборСегменты.ПодчиненныеЭлементы Цикл
		Если Лев(Элемент.Имя, 8) = "Сегмент_" Или Лев(Элемент.Имя, 15) = "СтрокаСегментов" Тогда
			МассивУдаляемыхЭлементов.Добавить(Элемент);
		КонецЕсли;
	КонецЦикла;
	Для Каждого Элемент Из МассивУдаляемыхЭлементов Цикл
		Элементы.Удалить(Элемент);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сегменты.Ссылка,
		|	Сегменты.Представление КАК Представление
		|ИЗ
		|	Справочник.СегментыКонтрагентов КАК Сегменты
		|ГДЕ
		|	Сегменты.ПометкаУдаления = ЛОЖЬ
		|	И Сегменты.ЭтоГруппа = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Представление";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НомерЭлемента = 0;
	НомерСтрокиЭлементов = 0;
	ТекущаяДлинаСтроки = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Если СтрДлина(Выборка.Представление) > 15 Тогда
			ПредставлениеСегмента = Лев(Выборка.Представление, 15) + "...";
			ТекущаяДлинаСтроки = ТекущаяДлинаСтроки + 15 + 2;
		Иначе
			ПредставлениеСегмента = Выборка.Представление;
			ТекущаяДлинаСтроки = ТекущаяДлинаСтроки + СтрДлина(ПредставлениеСегмента) + 2;
		КонецЕсли;
		
		СтрокаОтбораСегментов = ТаблицаОтборСегменты.Добавить(Выборка.Ссылка, ПредставлениеСегмента);
		
		Если НомерСтрокиЭлементов = 0 Или ТекущаяДлинаСтроки > МаксимальнаяДлинаСтроки Тогда
			
			ТекущаяДлинаСтроки = СтрДлина(ПредставлениеСегмента) + 2;
			НомерСтрокиЭлементов = НомерСтрокиЭлементов + 1;
			
			ГруппаСегментов = Элементы.Добавить(Префикс + "СтрокаСегментов" + НомерСтрокиЭлементов, Тип("ГруппаФормы"), ГруппаОтборСегменты);
			ГруппаСегментов.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ГруппаСегментов.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно;
			ГруппаСегментов.ОтображатьЗаголовок = Ложь;
			ГруппаСегментов.Отображение = ОтображениеОбычнойГруппы.Нет;
			ГруппаСегментов.РастягиватьПоВертикали = Ложь;
			ГруппаСегментов.Высота = 1;
			
		КонецЕсли;
		
		ЭлементСегмент = Элементы.Добавить(Префикс + "Сегмент_" + СтрокаОтбораСегментов.ПолучитьИдентификатор(), Тип("ПолеФормы"), ГруппаСегментов);
		ЭлементСегмент.Вид = ВидПоляФормы.ПолеНадписи;
		ЭлементСегмент.ПутьКДанным = Префикс + "ОтборСегменты[" + НомерЭлемента + "].Представление";
		ЭлементСегмент.Гиперссылка = Истина;
		ЭлементСегмент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ЭлементСегмент.Подсказка = Выборка.Представление;
		ЭлементСегмент.ЦветТекста = ЦветаСтиля.ЦветТекстаПоля;
		ЭлементСегмент.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
		ЭлементСегмент.Ширина = СтрДлина(ПредставлениеСегмента);
		ЭлементСегмент.РастягиватьПоГоризонтали = Ложь;
		ЭлементСегмент.УстановитьДействие("Нажатие", "Подключаемый_" + Префикс + "СегментОтборНажатие");
		
		НомерЭлемента = НомерЭлемента + 1;
		
	КонецЦикла;
	
	Если Выборка.Количество() = 0 Тогда
		
		ЭлементПояснение = Элементы.Добавить("Сегмент_Пояснение", Тип("ДекорацияФормы"), ГруппаОтборСегменты);
		ЭлементПояснение.Вид = ВидДекорацииФормы.Надпись;
		ЭлементПояснение.Гиперссылка = Истина;
		ЭлементПояснение.Заголовок = НСтр("ru = 'Как работать с сегментами?'");
		ЭлементПояснение.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
		ЭлементПояснение.УстановитьДействие("Нажатие", "Подключаемый_" + Префикс + "СегментОтборНажатие");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

// Функция создания тега
//
// Параметры:
//  НаименованиеТега - строка - имя тега
// Возвращаемое значение:
//  СправочникСсылка.Теги - ссылка на созданный элемент
Функция СоздатьТег(НаименованиеТега) Экспорт
	
	НовыйТег = Справочники.Теги.СоздатьЭлемент();
	НовыйТег.Наименование = НаименованиеТега;
	НовыйТег.Записать();
	
	Возврат НовыйТег.Ссылка;
	
КонецФункции

// Процедура изменяет цвет элемента отбора в зависимости от признака использования
// Необходимо вызывать с сервера для подключаемых процедур, иначе цвет не рисуется
//
// Параметры:
//  Форма		 - форма списка
//  Пометка		 - булево - признак использования отбора по данному элементу
//  ИмяЭлемента	 - строка - имя элемента формы
Процедура ИзменитьЦветЭлементаОтбора(Форма, Пометка, ИмяЭлемента) Экспорт
	
	ЭлементОтбора = Форма.Элементы.Найти(ИмяЭлемента);
	Если ЭлементОтбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Пометка Тогда
		ЭлементОтбора.ЦветФона = ЦветаСтиля.ФонАктивногоЗначенияОтбора;
	Иначе
		ЭлементОтбора.ЦветФона = Новый Цвет;
	КонецЕсли;
	
КонецПроцедуры

Функция УстановитьТег(ВладельцыТегов, Тег) Экспорт
	
	УспешноУстановлено = 0;
	
	Для Каждого ВладелецТега Из ВладельцыТегов Цикл
		
		ВладелецОбъект = ВладелецТега.ПолучитьОбъект();
		
		Если ВладелецОбъект.Теги.Найти(Тег) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ВладелецОбъект.Теги.Добавить();
		НоваяСтрока.Тег = Тег;
		
		ВладелецОбъект.Записать();
		УспешноУстановлено = УспешноУстановлено + 1;
		
	КонецЦикла;
	
	Возврат УспешноУстановлено;
	
КонецФункции

#КонецОбласти

#Область Контрагенты

Функция ТипОтношенийСКонтрагентомПоВидуОперации(ВидОперации) Экспорт
	
	Результат = Новый Структура("Покупатель, Поставщик, ПрочиеОтношения", Ложь, Ложь, Ложь);
	
	Если ТипЗнч(ВидОперации) = Тип("ПеречислениеСсылка.ВидыОперацийПриходнаяНакладная") Тогда
		
		Если ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика Тогда
			Результат.Поставщик = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию Тогда
			Результат.Поставщик = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПриемВПереработку Тогда
			Результат.Покупатель = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПриемНаОтветХранение Тогда
			Результат.Покупатель = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя Тогда
			Результат.Покупатель = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера Тогда
			Результат.Покупатель = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика Тогда
			Результат.Поставщик = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратСОтветХранения Тогда
			Результат.Покупатель = Истина;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ВидОперации) = Тип("ПеречислениеСсылка.ВидыОперацийРасходнаяНакладная") Тогда
		
		Если ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю Тогда
			Результат.Покупатель = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПередачаНаКомиссию Тогда
			Результат.Покупатель = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПередачаВПереработку Тогда
			Результат.Поставщик = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПередачаНаОтветХранение Тогда
			Результат.Поставщик = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику Тогда
			Результат.Поставщик = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратКомитенту Тогда
			Результат.Поставщик = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратИзПереработки Тогда
			Результат.Покупатель = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратСОтветХранения Тогда
			Результат.Покупатель = Истина;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ВидОперации) = Тип("ПеречислениеСсылка.ВидыОперацийПоступлениеВКассу") Тогда
		
		Если ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПокупателя
			Или ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтНашейОрганизации Тогда
			Результат.Покупатель = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПоставщика Тогда
			Результат.Поставщик = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ПрочиеРасчеты Тогда
			Результат.ПрочиеОтношения = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.РасчетыПоКредитам Тогда
			Результат.ПрочиеОтношения = Истина;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ВидОперации) = Тип("ПеречислениеСсылка.ВидыОперацийРасходИзКассы") Тогда
		
		Если ВидОперации = Перечисления.ВидыОперацийРасходИзКассы.Покупателю 
			Или ВидОперации = Перечисления.ВидыОперацийРасходИзКассы.НашейОрганизации Тогда
			Результат.Покупатель = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходИзКассы.Поставщику Тогда
			Результат.Поставщик = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходИзКассы.ПрочиеРасчеты Тогда
			Результат.ПрочиеОтношения = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходИзКассы.РасчетыПоКредитам Тогда
			Результат.ПрочиеОтношения = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходИзКассы.Налоги Тогда
			Результат.ПрочиеОтношения = Истина;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ВидОперации) = Тип("ПеречислениеСсылка.ВидыОперацийПоступлениеНаСчет") Тогда
		
		Если ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтПокупателя 
			Или ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтНашейОрганизации Тогда
			Результат.Покупатель = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтПоставщика Тогда
			Результат.Поставщик = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ПрочиеРасчеты Тогда
			Результат.ПрочиеОтношения = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.РасчетыПоКредитам Тогда
			Результат.ПрочиеОтношения = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКартам Тогда
			Результат.ПрочиеОтношения = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКредитам Тогда
			Результат.ПрочиеОтношения = Истина;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ВидОперации) = Тип("ПеречислениеСсылка.ВидыОперацийРасходСоСчета") Тогда
		
		Если ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Покупателю 
			Или ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.НашейОрганизации Тогда
			Результат.Покупатель = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Поставщику Тогда
			Результат.Поставщик = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.ПрочиеРасчеты Тогда
			Результат.ПрочиеОтношения = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.РасчетыПоКредитам Тогда
			Результат.ПрочиеОтношения = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты Тогда
			Результат.ПрочиеОтношения = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Налоги Тогда
			Результат.ПрочиеОтношения = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.КомиссияБанка Тогда
			Результат.ПрочиеОтношения = Истина;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ВидОперации) = Тип("ПеречислениеСсылка.ВидыОперацийПлатежноеПоручение") Тогда
		
		Если ВидОперации = Перечисления.ВидыОперацийПлатежноеПоручение.Покупателю Тогда
			Результат.Покупатель = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПлатежноеПоручение.Оплата Тогда
			Результат.Поставщик = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПлатежноеПоручение.ПрочиеРасчеты
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПлатежноеПоручение.НаРасходы
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПлатежноеПоручение.Прочее
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога Тогда
			Результат.ПрочиеОтношения = Истина;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ВидОперации) = Тип("ПеречислениеСсылка.ВидыОперацийВзаимозачет") Тогда
		
		// Для вида операции "Взаимозачет" параметры выбора устанавливаются из формы документа "Взаимозачет",
		// т.к. имеется два реквизита с типом "Контрагент" - один покупатель, другой поставщик
		
		Если ВидОперации = Перечисления.ВидыОперацийВзаимозачет.ПереуступкаДолгаПокупателя Тогда
			Результат.Покупатель = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВзаимозачет.ПереуступкаДолгаПоставщику Тогда
			Результат.Поставщик = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВзаимозачет.КорректировкаДолгаПокупателя Тогда
			Результат.Покупатель = Истина;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВзаимозачет.КорректировкаДолгаПоставщику Тогда
			Результат.Поставщик = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти