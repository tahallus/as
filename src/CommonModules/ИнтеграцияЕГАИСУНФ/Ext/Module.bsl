
Процедура ЗаполнениеДокументовЕГАИС(Источник, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
		
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.АктСписанияЕГАИС") Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СписаниеЗапасов") Тогда
			
			РеквизитыОснования          = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, СтруктурнаяЕдиница");
			ТипСклада                   = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыОснования.СтруктурнаяЕдиница, "ТипСтруктурнойЕдиницы");
			Источник.ВидДокумента       = ?(ТипСклада = Перечисления.ТипыСтруктурныхЕдиниц.Склад,
		                                   Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра1,
		                                   Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра2);
			Источник.ДокументОснование  = ДанныеЗаполнения;
			Источник.ПричинаСписания    = Перечисления.ПричиныСписанийЕГАИС.Недостача;
			
			Если НЕ ЗначениеЗаполнено(Источник.ОрганизацияЕГАИС) Тогда
				Источник.ОрганизацияЕГАИС = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(
					РеквизитыОснования.Организация, РеквизитыОснования.СтруктурнаяЕдиница);
			КонецЕсли;
			
			ЗаполнитьТоварыЕГАИС(Источник, ДанныеЗаполнения);
			
			Если Источник.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра1 Тогда
				Документы.АктСписанияЕГАИС.ПодобратьСправки2(Источник);
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПередачаВРегистр2ЕГАИС") Тогда
			
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник, "ОрганизацияЕГАИС, ТорговыйОбъект, Организация");
			ЗаполнитьЗначенияСвойств(Источник, РеквизитыОснования);
			
			Источник.ДокументОснование  = ДанныеЗаполнения;
			Источник.ВидДокумента       = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра1;
			Источник.ПричинаСписания    = Перечисления.ПричиныСписанийЕГАИС.Реализация;
			
			ЗаполнитьТоварыАктаСписанияЕГАИСНаОснованииПередачиВРегистр2(Источник);
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
			
			РеквизитыОснования          = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, СтруктурнаяЕдиница");
			Источник.ВидДокумента       = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра2;
			Источник.ПричинаСписания    = Перечисления.ПричиныСписанийЕГАИС.Реализация;
			
			Если НЕ ЗначениеЗаполнено(Источник.ОрганизацияЕГАИС) Тогда
				Источник.ОрганизацияЕГАИС = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(
					РеквизитыОснования.Организация, РеквизитыОснования.СтруктурнаяЕдиница);
			КонецЕсли;
			
			ИсключитьМаркируемые = Истина;
			
			ЗаполнитьТоварыЕГАИС(Источник, ДанныеЗаполнения, ИсключитьМаркируемые);
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.АктПостановкиНаБалансЕГАИС") Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОприходованиеЗапасов") Тогда
			
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, СтруктурнаяЕдиница");
		
			Источник.ДокументОснование         = ДанныеЗаполнения;
			Источник.ПричинаПостановкиНаБаланс = Перечисления.ПричиныПостановкиНаБалансЕГАИС.Излишки;
			
			Если НЕ ЗначениеЗаполнено(Источник.ОрганизацияЕГАИС) Тогда
				Источник.ОрганизацияЕГАИС = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(
					РеквизитыОснования.Организация, РеквизитыОснования.СтруктурнаяЕдиница);
			КонецЕсли;
			
			ЗаполнитьТоварыЕГАИС(Источник, ДанныеЗаполнения);
		
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("АкцизныеМаркиВРегистр3") Тогда	
			
			ЗаполнитьЗначенияСвойств(Источник, ДанныеЗаполнения);
			Если ДанныеЗаполнения.Свойство("Склад") Тогда
				Источник.ТорговыйОбъект = ДанныеЗаполнения.Склад;
			КонецЕсли;
			
			Если ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
				ДанныеЗаполнения = ДанныеЗаполнения.ДокументОснование;
			КонецЕсли;
			
			ЗаполнитьАктПостановкиНаБалансЕГАИСНаОснованииСтруктуры(Источник, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ТТНИсходящаяЕГАИС") Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПеремещениеЗапасов") Тогда
			
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, СтруктурнаяЕдиница, СтруктурнаяЕдиницаПолучатель, Номер, Дата");
			
			Источник.ДокументОснование = ДанныеЗаполнения;
			Источник.ВидОперации       = Перечисления.ВидыОперацийТТНИсходящейЕГАИС.РасходнаяНакладная;
			Источник.Идентификатор     = Строка(ДанныеЗаполнения.УникальныйИдентификатор());
			Источник.НомерТТН          = СформироватьНомерИсходящейТТН(РеквизитыОснования.Номер, РеквизитыОснования.Дата);
			Источник.ДатаОтгрузки      = РеквизитыОснования.Дата;
			Источник.ДатаТТН           = РеквизитыОснования.Дата;
			Источник.Упакована         = Истина;
			Источник.Грузополучатель   = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(РеквизитыОснования.Организация, РеквизитыОснования.СтруктурнаяЕдиницаПолучатель);
			Источник.Грузоотправитель  = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(РеквизитыОснования.Организация, РеквизитыОснования.СтруктурнаяЕдиница);
			
			ЗаполнитьТоварыЕГАИС(Источник, ДанныеЗаполнения);
			Документы.ТТНИсходящаяЕГАИС.ПодобратьСправки2(Источник);
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
			
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, СтруктурнаяЕдиница, Контрагент, Номер, Дата, ВидОперации");
			Если РеквизитыОснования.ВидОперации <> Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику Тогда
				ВызватьИсключение НСтр("ru = 'Выгрузка в ЕГАИС возможна только для возврата поставщику'");
			КонецЕсли;
			
			Источник.ДокументОснование = ДанныеЗаполнения;
			Источник.ВидОперации       = Перечисления.ВидыОперацийТТНИсходящейЕГАИС.ВозвратПоставщику;
			Источник.Идентификатор     = Строка(ДанныеЗаполнения.УникальныйИдентификатор());
			Источник.НомерТТН          = СформироватьНомерИсходящейТТН(РеквизитыОснования.Номер, РеквизитыОснования.Дата);
			Источник.ДатаОтгрузки      = РеквизитыОснования.Дата;
			Источник.ДатаТТН           = РеквизитыОснования.Дата;
			Источник.Упакована         = Истина;
			Источник.Грузополучатель   = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(РеквизитыОснования.Контрагент, Справочники.СтруктурныеЕдиницы.ПустаяСсылка(), Ложь);
			Источник.Грузоотправитель  = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(РеквизитыОснования.Организация, РеквизитыОснования.СтруктурнаяЕдиница);
			Источник.Поставщик = Источник.Грузополучатель;
			
			ЗаполнитьТоварыЕГАИС(Источник, ДанныеЗаполнения);
			Документы.ТТНИсходящаяЕГАИС.ПодобратьСправки2(Источник);
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ОстаткиЕГАИС") Тогда
		
		
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ПередачаВРегистр2ЕГАИС") Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(Источник, ДанныеЗаполнения);
			Если ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
				ДанныеЗаполнения = ДанныеЗаполнения.ДокументОснование;
			КонецЕсли;
		КонецЕсли;
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
			И ДанныеЗаполнения.Свойство("ЗаполнитьТоварыКОформлению") Тогда
			
			ЗаполнитьПередачуВРегистр2ЕГАИСНаОснованииСтруктуры(Источник, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
			
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, СтруктурнаяЕдиница");
			
			Источник.ДокументОснование = ДанныеЗаполнения;
			Источник.Организация = РеквизитыОснования.Организация;
			Источник.Магазин = РеквизитыОснования.СтруктурнаяЕдиница;
			Источник.ОрганизацияЕГАИС = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(РеквизитыОснования.Организация, РеквизитыОснования.СтруктурнаяЕдиница);
			Источник.Ответственный = Пользователи.ТекущийПользователь();
			
			ЗаполнитьТоварыЕГАИС(Источник, ДанныеЗаполнения);
			Документы.ПередачаВРегистр2ЕГАИС.ПодобратьСправки2(Источник);
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПеремещениеЗапасов") Тогда
			
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, СтруктурнаяЕдиницаПолучатель");
			
			Источник.ДокументОснование = ДанныеЗаполнения;
			Источник.Организация = РеквизитыОснования.Организация;
			Источник.Магазин = РеквизитыОснования.СтруктурнаяЕдиницаПолучатель;
			Источник.ОрганизацияЕГАИС = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(РеквизитыОснования.Организация, РеквизитыОснования.СтруктурнаяЕдиницаПолучатель);
			Источник.Ответственный = Пользователи.ТекущийПользователь();
			
			ЗаполнитьТоварыЕГАИС(Источник, ДанныеЗаполнения);
			Документы.ПередачаВРегистр2ЕГАИС.ПодобратьСправки2(Источник);
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
		
			ЗаполнитьПередачуВРегистр2ЕГАИСНаОснованииТТНВходящейЕГАИС(Источник, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ВозвратИзРегистра2ЕГАИС") Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(Источник,ДанныеЗаполнения);
			Если ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
				ДанныеЗаполнения = ДанныеЗаполнения.ДокументОснование;
			КонецЕсли;
		КонецЕсли;
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПеремещениеЗапасов") Тогда
			
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, СтруктурнаяЕдиницаПолучатель");
			
			Источник.ДокументОснование = ДанныеЗаполнения;
			Источник.Организация = РеквизитыОснования.Организация;
			Источник.Магазин = РеквизитыОснования.СтруктурнаяЕдиницаПолучатель;
			Источник.ОрганизацияЕГАИС = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(РеквизитыОснования.Организация, РеквизитыОснования.СтруктурнаяЕдиницаПолучатель);
			Источник.Ответственный = Пользователи.ТекущийПользователь();
			
			ЗаполнитьТоварыЕГАИС(Источник, ДанныеЗаполнения);
			Документы.ВозвратИзРегистра2ЕГАИС.ПодобратьСправки2(Источник);
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ТТНИсходящаяЕГАИС") Тогда
			
			ЗаполнитьВозвратИзРегистра2ЕГАИСНаОснованииТТНИсходящейЕГАИС(Источник, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ЧекЕГАИС") Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
			
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, СтруктурнаяЕдиница, Контрагент, Номер, Дата, ВидОперации, НомерСменыККМ, НомерЧекаККМ");
			Если РеквизитыОснования.ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю Тогда
				
				Источник.ДокументОснование = ДанныеЗаполнения;
				Источник.ОрганизацияЕГАИС  = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(РеквизитыОснования.Организация, РеквизитыОснования.СтруктурнаяЕдиница);
				Источник.ВидОперации       = Перечисления.ВидыОперацийЧекаЕГАИС.РеализацияРозничномуПокупателю;
				Источник.НомерСмены        = РеквизитыОснования.НомерСменыККМ;
				Источник.НомерЧекаККМ      = РеквизитыОснования.НомерЧекаККМ;
				Источник.СерийныйНомерККМ  = "1";
				
				ЗаполнитьТоварыЕГАИС(Источник, ДанныеЗаполнения);
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ЧекЕГАИСВозврат") Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
			
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, СтруктурнаяЕдиница, Контрагент, Номер, Дата, ВидОперации, ЧекККМ.НомерСменыККМ, ЧекККМ.НомерЧекаККМ, ЧекККМ.КассаККМ");
			Если РеквизитыОснования.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя Тогда
				
				Источник.ДокументОснование = ДанныеЗаполнения;
				Источник.ОрганизацияЕГАИС  = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(РеквизитыОснования.Организация, РеквизитыОснования.СтруктурнаяЕдиница);
				Источник.ВидОперации       = Перечисления.ВидыОперацийЧекаЕГАИСВозврат.ВозвратОтРозничногоПокупателя;
				Источник.НомерСмены        = РеквизитыОснования.ЧекККМНомерСменыККМ;
				Источник.НомерЧекаККМ      = РеквизитыОснования.ЧекККМНомерЧекаККМ;
				
				СерийныйНомерККМ = РабочееМестоКассира.СерийныйНомерККМ(РеквизитыОснования.ЧекККМКассаККМ);
				Если Не ЗначениеЗаполнено(СерийныйНомерККМ) Тогда
					СерийныйНомерККМ = "1";
				КонецЕсли;
				Источник.СерийныйНомерККМ  = СерийныйНомерККМ;
				
				Для каждого СтрокаТаблицы Из ДанныеЗаполнения.Запасы Цикл
					
					НоваяСтрока = Источник.Товары.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
					
					НоваяСтрока.ИдентификаторСтроки = СтрокаТаблицы.КлючСвязи;
					НоваяСтрока.АлкогольнаяПродукция = СтрокаТаблицы.НоменклатураЕГАИС;
					НоваяСтрока.Упаковка = СтрокаТаблицы.ЕдиницаИзмерения;
					НоваяСтрока.КоличествоУпаковок = СтрокаТаблицы.Количество;
					НоваяСтрока.Сумма = СтрокаТаблицы.Всего;
					
				КонецЦикла;
				
				Для каждого СтрокаТаблицы Из ДанныеЗаполнения.АкцизныеМарки Цикл
					
					НоваяСтрока = Источник.АкцизныеМарки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
					
					НоваяСтрока.ИдентификаторСтроки = СтрокаТаблицы.КлючСвязи;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СтатусПроверкиИПодбора", Источник.Метаданные()) Тогда
		Источник.СтатусПроверкиИПодбора = Перечисления.СтатусыПроверкиИПодбораИС.НеВыполнялось;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Ответственный", Источник.Метаданные()) Тогда
		Источник.Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
		
КонецПроцедуры

Функция СформироватьНомерИсходящейТТН(Номер, Дата)
	
	НомерБезПрефиксов = ПрефиксацияОбъектовКлиентСервер.УдалитьПрефиксыИзНомераОбъекта(Номер, Истина, Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаТТН", НачалоДня(Дата));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТТНИсходящаяЕГАИС.НомерТТН КАК НомерТТН
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТТНИсходящаяЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО ТТНИсходящаяЕГАИС.Ссылка = СтатусыДокументовЕГАИС.Документ
	|ГДЕ
	|	ТТНИсходящаяЕГАИС.ДатаТТН = &ДатаТТН
	|	И НЕ ТТНИсходящаяЕГАИС.ПометкаУдаления";
	
	НомераТТН = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("НомерТТН");
	
	Результат = НомерБезПрефиксов;
	
	Сч = 0;
	Пока НомераТТН.Найти(Результат) <> Неопределено Цикл
		Сч = Сч + 1;
		Результат = НомерБезПрефиксов + "-" + Формат(Сч, "ЧГ=0");
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьТоварыЕГАИС(Источник, ДанныеЗаполнения, ИсключитьМаркируемые = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы"  , Документы.ТТНИсходящаяЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ЭтаСсылка"        , Источник.Ссылка);
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВозвратТоваровПоставщикуТовары.Номенклатура КАК Номенклатура,
	|	ВозвратТоваровПоставщикуТовары.Характеристика КАК Характеристика,
	|	ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)) КАК АлкогольнаяПродукция,
	|	ВозвратТоваровПоставщикуТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ПОМЕСТИТЬ СоответствиеТоваров
	|ИЗ
	|	Документ.ПриходнаяНакладная.Запасы КАК ВозвратТоваровПоставщикуТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО ВозвратТоваровПоставщикуТовары.Номенклатура = СоответствиеНоменклатурыЕГАИС.Номенклатура
	|ГДЕ
	|	ВозвратТоваровПоставщикуТовары.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО (СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка)
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И НЕ СтатусыДокументовЕГАИС.Статус В (&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровПоставщикуТовары.Номенклатура КАК Номенклатура,
	|	ВозвратТоваровПоставщикуТовары.Характеристика КАК Характеристика,
	|	СоответствиеТоваров.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ВозвратТоваровПоставщикуТовары.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ВозвратТоваровПоставщикуТовары.Ссылка.СуммаВключаетНДС
	|			ТОГДА ВозвратТоваровПоставщикуТовары.Сумма
	|		ИНАЧЕ ВозвратТоваровПоставщикуТовары.Сумма + ВозвратТоваровПоставщикуТовары.СуммаНДС
	|	КОНЕЦ КАК Сумма,
	|	ВозвратТоваровПоставщикуТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ПриходнаяНакладная.Запасы КАК ВозвратТоваровПоставщикуТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СоответствиеТоваров КАК СоответствиеТоваров
	|		ПО ВозвратТоваровПоставщикуТовары.Номенклатура = СоответствиеТоваров.Номенклатура
	|			И ВозвратТоваровПоставщикуТовары.Характеристика = СоответствиеТоваров.Характеристика
	|ГДЕ
	|	ВозвратТоваровПоставщикуТовары.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура,
	|	ОформленныеТовары.Характеристика,
	|	ОформленныеТовары.АлкогольнаяПродукция,
	|	-ОформленныеТовары.Количество,
	|	-ОформленныеТовары.Сумма,
	|	ОформленныеТовары.Упаковка
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				Т.Ссылка
	|			ИЗ
	|				ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТоварыКОформлению.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество) КАК Количество,
	|	СУММА(ТоварыКОформлению.Сумма) КАК Сумма,
	|	ЕСТЬNULL(ВидыАлкогольнойПродукции.Маркируемый, ИСТИНА) КАК Маркируемый
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыАлкогольнойПродукции КАК ВидыАлкогольнойПродукции
	|		ПО ТоварыКОформлению.АлкогольнаяПродукция.ВидПродукции = ВидыАлкогольнойПродукции.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.АлкогольнаяПродукция,
	|	ЕСТЬNULL(ВидыАлкогольнойПродукции.Маркируемый, ИСТИНА),
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.ЕдиницаИзмерения
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0";
	
	// Реквизиты основания
	МетаданныеДокумента  = ДанныеЗаполнения.Метаданные();
	ИмяДокумента         = МетаданныеДокумента.Имя;
	ЕстьСуммаВключаетНДС = ОбщегоНазначения.ЕстьРеквизитОбъекта("СуммаВключаетНДС", МетаданныеДокумента);
	
	Если МетаданныеДокумента.ТабличныеЧасти.Найти("Запасы") <> Неопределено Тогда
		ЕстьСумма            = (МетаданныеДокумента.ТабличныеЧасти["Запасы"].Реквизиты.Найти("Сумма") <> Неопределено);
	Иначе
		ЕстьСумма            = (МетаданныеДокумента.ТабличныеЧасти["Товары"].Реквизиты.Найти("Сумма") <> Неопределено);
	КонецЕсли;
	
	
	// Реквизиты получателя
	Если Источник.Метаданные().ТабличныеЧасти.Найти("Запасы") <> Неопределено Тогда
		ЕстьКоличествоУпаковок = (Источник.Метаданные().ТабличныеЧасти["Запасы"].Реквизиты.Найти("КоличествоУпаковок") <> Неопределено);
	Иначе
		ЕстьКоличествоУпаковок = (Источник.Метаданные().ТабличныеЧасти["Товары"].Реквизиты.Найти("КоличествоУпаковок") <> Неопределено);
	КонецЕсли;
	
	Если Источник.Метаданные().ТабличныеЧасти.Найти("Запасы") <> Неопределено Тогда
		ЕстьЦена               = (Источник.Метаданные().ТабличныеЧасти["Запасы"].Реквизиты.Найти("Цена") <> Неопределено);
	Иначе
		ЕстьЦена               = (Источник.Метаданные().ТабличныеЧасти["Товары"].Реквизиты.Найти("Цена") <> Неопределено);
	КонецЕсли;
	
	Если НЕ (ЕстьСумма И ЕстьСуммаВключаетНДС) Тогда
		ТекстССуммой = 
		"	ВЫБОР
		|		КОГДА ВозвратТоваровПоставщикуТовары.Ссылка.СуммаВключаетНДС
		|			ТОГДА ВозвратТоваровПоставщикуТовары.Сумма
		|		ИНАЧЕ ВозвратТоваровПоставщикуТовары.Сумма + ВозвратТоваровПоставщикуТовары.СуммаНДС
		|	КОНЕЦ КАК Сумма";
		
		ТекстБезСуммы = 
		"	0 КАК Сумма";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстССуммой, ТекстБезСуммы);
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПриходнаяНакладная", ИмяДокумента);
	Запрос.Текст = ТекстЗапроса;
			
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать();
	
	Источник.Товары.Очистить();
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Если ИсключитьМаркируемые И ВыборкаНоменклатура.Маркируемый Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Источник.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаНоменклатура);
		Если ЕстьКоличествоУпаковок Тогда
			НоваяСтрока.КоличествоУпаковок	= НоваяСтрока.Количество;
		КонецЕсли;
		Если ЕстьЦена Тогда
			НоваяСтрока.Цена = ?(НоваяСтрока.Количество = 0, НоваяСтрока.Сумма, НоваяСтрока.Сумма/НоваяСтрока.Количество);
		КонецЕсли;
		
		НоваяСтрока.Упаковка = ВыборкаНоменклатура.ЕдиницаИзмерения;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса, ДанныеЗаполнения, БезСопоставления = Ложь)
	
	ВыборкаТовары = РезультатЗапроса.Выбрать();
	
	Если ВыборкаТовары.Количество() = 0 Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru='В %1 отсутствует алкогольная продукция для заполнения.'"),
			ДанныеЗаполнения);
		
	КонецЕсли;
	
	Пока ВыборкаТовары.Следующий() Цикл
		
		Если ЗначениеЗаполнено(ВыборкаТовары.АлкогольнаяПродукция) ИЛИ БезСопоставления Тогда
			
			НоваяСтрока = ДокументОбъект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
			
		Иначе
			
			ДокументОбъект.Товары.Очистить();
			
			ТекстОшибки = НСтр("ru='Не выполнено сопоставление %Номенклатура% классификатору номенклатуры ЕГАИС.'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Номенклатура%", ВыборкаТовары.Номенклатура);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьАктПостановкиНаБалансЕГАИСНаОснованииСтруктуры(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ДокументОбъект.ОрганизацияЕГАИС = ДанныеЗаполнения.ОрганизацияЕГАИС;
	ДокументОбъект.ВидДокумента     = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр3;
	
	ТаблицаТовары = Новый ТаблицаЗначений;
	ТаблицаТовары.Колонки.Добавить("АлкогольнаяПродукция", Новый ОписаниеТипов("СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС"));
	ТаблицаТовары.Колонки.Добавить("Справка2",             Новый ОписаниеТипов("СправочникСсылка.Справки2ЕГАИС"));
	
	Для Каждого ДанныеАкцизнойМарки Из ДанныеЗаполнения.АкцизныеМарки Цикл
		
		НоваяСтрока = ДокументОбъект.АкцизныеМарки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеАкцизнойМарки);
		НоваяСтрока.Количество = 1;
		
		НоваяСтрокаТовары = ТаблицаТовары.Добавить();
		НоваяСтрокаТовары.АлкогольнаяПродукция = ДанныеАкцизнойМарки.АлкогольнаяПродукция;
		НоваяСтрокаТовары.Справка2             = ДанныеАкцизнойМарки.Справка2;
		
	КонецЦикла;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Т.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Т.Справка2 КАК Справка2,
	|	1 КАК Количество
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	&Товары КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.АлкогольнаяПродукция,
	|	Т.Справка2 КАК Справка2,
	|	СУММА(Т.Количество) КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	втТовары КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.АлкогольнаяПродукция,
	|	Т.Справка2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2 КАК Справка2,
	|	МАКСИМУМ(СоответствиеНоменклатурыЕГАИС.Номенклатура) КАК Номенклатура,
	|	МАКСИМУМ(СоответствиеНоменклатурыЕГАИС.Характеристика) КАК Характеристика
	|ПОМЕСТИТЬ СопоставленныеПозиции
	|ИЗ
	|	Товары КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция = ТабличнаяЧасть.АлкогольнаяПродукция
	|			И СоответствиеНоменклатурыЕГАИС.Справка2 = ТабличнаяЧасть.Справка2
	|ГДЕ
	|	НЕ СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция ЕСТЬ NULL
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеНоменклатурыЕГАИС.Номенклатура) = 1
	|		И КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеНоменклатурыЕГАИС.Характеристика) < 2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СопоставленныеПозиции.Номенклатура   КАК Номенклатура,
	|	СопоставленныеПозиции.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2             КАК Справка2,
	|	ТабличнаяЧасть.Количество           КАК Количество,
	|	ТабличнаяЧасть.Количество           КАК КоличествоУпаковок
	|ИЗ
	|	Товары КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеПозиции КАК СопоставленныеПозиции
	|		ПО ТабличнаяЧасть.АлкогольнаяПродукция = СопоставленныеПозиции.АлкогольнаяПродукция
	|			И ТабличнаяЧасть.Справка2 = СопоставленныеПозиции.Справка2
	|			И НЕ СопоставленныеПозиции.АлкогольнаяПродукция ЕСТЬ NULL
	|");
	Запрос.УстановитьПараметр("Товары", ТаблицаТовары);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДокументОбъект.Товары.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьТоварыАктаСписанияЕГАИСНаОснованииПередачиВРегистр2(ДокументОбъект)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОбъект.ДокументОснование);
	Запрос.УстановитьПараметр("КонечныеСтатусы"  , Документы.АктСписанияЕГАИС.КонечныеСтатусы());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО (СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка)
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И НЕ СтатусыДокументовЕГАИС.Статус В (&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Упаковка КАК Упаковка,
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2 КАК Справка2,
	|	ТабличнаяЧасть.Количество КАК Количество,
	|	ТабличнаяЧасть.КоличествоУпаковок КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ПередачаВРегистр2ЕГАИС.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И ЕСТЬNULL(НЕ ТабличнаяЧасть.АлкогольнаяПродукция.ВидПродукции.Маркируемый, ЛОЖЬ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура,
	|	ОформленныеТовары.Характеристика,
	|	ОформленныеТовары.Упаковка,
	|	ОформленныеТовары.АлкогольнаяПродукция,
	|	ОформленныеТовары.Справка2,
	|	-ОформленныеТовары.Количество,
	|	-ОформленныеТовары.КоличествоУпаковок
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				Т.Ссылка
	|			ИЗ
	|				ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Упаковка КАК Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2 КАК Справка2,
	|	СУММА(ТоварыКОформлению.Количество) КАК Количество,
	|	СУММА(ТоварыКОформлению.КоличествоУпаковок) КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0";
	
	ДокументОбъект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Для Каждого СтрокаТЧ Из ДокументОбъект.Товары Цикл
		СтрокаТЧ.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
	КонецЦикла;
	
	Если ДокументОбъект.Товары.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Нет данных для заполнения документа'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПередачуВРегистр2ЕГАИСНаОснованииСтруктуры(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ДокументОбъект.ОрганизацияЕГАИС = ДанныеЗаполнения.ОрганизацияЕГАИС;
	
	ТаблицаТовары = Новый ТаблицаЗначений;
	ТаблицаТовары.Колонки.Добавить("АлкогольнаяПродукция", Новый ОписаниеТипов("СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС"));
	ТаблицаТовары.Колонки.Добавить("Справка2",             Новый ОписаниеТипов("СправочникСсылка.Справки2ЕГАИС"));
	ТаблицаТовары.Колонки.Добавить("Количество",           Новый ОписаниеТипов("Число"));
	
	Для Каждого СтрокаТЧ Из ДанныеЗаполнения.Товары Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаТовары.Добавить(), СтрокаТЧ);
	КонецЦикла;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Т.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Т.Справка2 КАК Справка2,
	|	Т.Количество КАК Количество
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	&Товары КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.АлкогольнаяПродукция,
	|	Т.Справка2 КАК Справка2,
	|	СУММА(Т.Количество) КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	втТовары КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.АлкогольнаяПродукция,
	|	Т.Справка2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2 КАК Справка2,
	|	МАКСИМУМ(СоответствиеНоменклатурыЕГАИС.Номенклатура) КАК Номенклатура,
	|	МАКСИМУМ(СоответствиеНоменклатурыЕГАИС.Характеристика) КАК Характеристика
	|ПОМЕСТИТЬ СопоставленныеПозиции
	|ИЗ
	|	Товары КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция = ТабличнаяЧасть.АлкогольнаяПродукция
	|			И СоответствиеНоменклатурыЕГАИС.Справка2 = ТабличнаяЧасть.Справка2
	|ГДЕ
	|	НЕ СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция ЕСТЬ NULL
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеНоменклатурыЕГАИС.Номенклатура) = 1
	|		И КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеНоменклатурыЕГАИС.Характеристика) < 2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СопоставленныеПозиции.Номенклатура   КАК Номенклатура,
	|	СопоставленныеПозиции.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2             КАК Справка2,
	|	ТабличнаяЧасть.Количество           КАК Количество,
	|	ТабличнаяЧасть.Количество           КАК КоличествоУпаковок
	|ИЗ
	|	Товары КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеПозиции КАК СопоставленныеПозиции
	|		ПО ТабличнаяЧасть.АлкогольнаяПродукция = СопоставленныеПозиции.АлкогольнаяПродукция
	|			И ТабличнаяЧасть.Справка2 = СопоставленныеПозиции.Справка2
	|			И НЕ СопоставленныеПозиции.АлкогольнаяПродукция ЕСТЬ NULL
	|");
	Запрос.УстановитьПараметр("Товары", ТаблицаТовары);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДокументОбъект.Товары.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПередачуВРегистр2ЕГАИСНаОснованииТТНВходящейЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка          КАК ДокументОснование,
	|	НЕ Таблица.Проведен     КАК ЕстьОшибкиПроведен,
	|	Таблица.Грузополучатель КАК ОрганизацияЕГАИС,
	|	Таблица.Ответственный   КАК Ответственный
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ПередачаВРегистр2ЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус НЕ В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия,
	|	"""" КАК Упаковка,
	|	ТабличнаяЧасть.Количество                                  КАК Количество,
	|	ТабличнаяЧасть.Количество                                  КАК КоличествоУпаковок,
	|	ТабличнаяЧасть.АлкогольнаяПродукция                        КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2                                    КАК Справка2
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Справка2 <> ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)
	|	И ТабличнаяЧасть.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	ОформленныеТовары.Упаковка              КАК Упаковка,
	|	-ОформленныеТовары.Количество           КАК Количество,
	|	-ОформленныеТовары.КоличествоУпаковок   КАК КоличествоУпаковок,
	|	ОформленныеТовары.АлкогольнаяПродукция  КАК АлкогольнаяПродукция,
	|	ОформленныеТовары.Справка2              КАК Справка2
	|ИЗ
	|	Документ.ПередачаВРегистр2ЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура              КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика            КАК Характеристика,
	|	ТоварыКОформлению.Серия                     КАК Серия,
	|	ТоварыКОформлению.Упаковка                  КАК Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция      КАК АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2                  КАК Справка2,
	|	СУММА(ТоварыКОформлению.Количество)         КАК Количество,
	|	СУММА(ТоварыКОформлению.КоличествоУпаковок) КАК КоличествоУпаковок
	|ПОМЕСТИТЬ
	|	ВтОстатки
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	ТоварыКОформлению.Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АкцизныеМарки.Справка2 КАК Справка2,
	|	КОЛИЧЕСТВО(АкцизныеМарки.АкцизнаяМарка) КАК Количество
	|ПОМЕСТИТЬ
	|	АкцизныеМарки
	|ИЗ
	|	РегистрСведений.АкцизныеМаркиЕГАИС КАК АкцизныеМарки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОстатки КАК втОстатки
	|		ПО ВтОстатки.Справка2 = АкцизныеМарки.Справка2
	|ГДЕ
	|	АкцизныеМарки.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.ВНаличии),ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.КПостановкеНаБаланс))
	|СГРУППИРОВАТЬ ПО
	|	АкцизныеМарки.Справка2;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура              КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика            КАК Характеристика,
	|	ТоварыКОформлению.Серия                     КАК Серия,
	|	ТоварыКОформлению.Упаковка                  КАК Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция      КАК АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2                  КАК Справка2,
	|	ТоварыКОформлению.Количество         - ЕСТЬNULL(АкцизныеМарки.Количество, 0) КАК Количество,
	|	ТоварыКОформлению.КоличествоУпаковок - ЕСТЬNULL(АкцизныеМарки.Количество, 0) КАК КоличествоУпаковок
	|ИЗ
	|	ВтОстатки КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ АкцизныеМарки КАК АкцизныеМарки
	|		ПО ТоварыКОформлению.Справка2 = АкцизныеМарки.Справка2
	|ГДЕ
	|	ТоварыКОформлению.Количество - ЕСТЬNULL(АкцизныеМарки.Количество, 0) > 0
	|");
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ПередачаВРегистр2ЕГАИС.КонечныеСтатусы());
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыДокумента = РезультатЗапроса[0].Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса[РезультатЗапроса.Количество() - 1], ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ЗаполнитьВозвратИзРегистра2ЕГАИСНаОснованииТТНИсходящейЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка           КАК ДокументОснование,
	|	ЛОЖЬ                     КАК ЕстьОшибкиПроведен,
	|	Таблица.Грузоотправитель КАК ОрганизацияЕГАИС,
	|	Таблица.Ответственный    КАК Ответственный
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ВозвратИзРегистра2ЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус НЕ В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия,
	|	"""" КАК Упаковка,
	|	ТабличнаяЧасть.Количество                                  КАК Количество,
	|	ТабличнаяЧасть.Количество                                  КАК КоличествоУпаковок,
	|	ТабличнаяЧасть.АлкогольнаяПродукция                        КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2                                    КАК Справка2
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Справка2 = ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)
	|	И ТабличнаяЧасть.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	ОформленныеТовары.Упаковка              КАК Упаковка,
	|	-ОформленныеТовары.Количество           КАК Количество,
	|	-ОформленныеТовары.КоличествоУпаковок   КАК КоличествоУпаковок,
	|	ОформленныеТовары.АлкогольнаяПродукция  КАК АлкогольнаяПродукция,
	|	ОформленныеТовары.Справка2              КАК Справка2
	|ИЗ
	|	Документ.ВозвратИзРегистра2ЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура              КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика            КАК Характеристика,
	|	ТоварыКОформлению.Серия                     КАК Серия,
	|	ТоварыКОформлению.Упаковка                  КАК Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция      КАК АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2                  КАК Справка2,
	|	СУММА(ТоварыКОформлению.Количество)         КАК Количество,
	|	СУММА(ТоварыКОформлению.КоличествоУпаковок) КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	ТоварыКОформлению.Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ВозвратИзРегистра2ЕГАИС.КонечныеСтатусы());
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыДокумента = РезультатЗапроса[0].Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса[РезультатЗапроса.Количество() - 1], ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ТекстЗапросаОрганизацииЕГАИСИспользующиеРегистр2(
	ТекстЗапроса,
	ИмяВременнойТаблицы,
	ИмяПоляОрганизация) Экспорт
	
	//++ НЕ ГОСИС
	ТекстЗапроса = СтрШаблон(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОрганизацииЕГАИС.Ссылка КАК %1
		|ПОМЕСТИТЬ %2
		|ИЗ
		|	РегистрСведений.НастройкиОбменаЕГАИС КАК НастройкиОбменаЕГАИС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК ОрганизацииЕГАИС
		|		ПО ОрганизацииЕГАИС.Код = НастройкиОбменаЕГАИС.ИдентификаторФСРАР
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтруктурныеЕдиницы КАК Склады
		|		ПО ОрганизацииЕГАИС.ТорговыйОбъект = Склады.Ссылка
		|ГДЕ
		|	Склады.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Розница)
		|",
		ИмяПоляОрганизация,
		ИмяВременнойТаблицы);
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

Функция ТребуетсяОформлениеДокументовЕГАИС(Основание, МетаданныеДокументаЕГАИС) Экспорт
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.АктСписанияЕГАИС Тогда
			Возврат  ЕстьНеМаркируемаяАлкогольнаяПродукция(Основание, "Запасы");
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.СписаниеЗапасов") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.АктСписанияЕГАИС Тогда
			Возврат ЕстьАлкогольнаяПродукция(Основание, "Запасы");
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ОприходованиеЗапасов") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.АктПостановкиНаБалансЕГАИС Тогда
			Возврат ЕстьАлкогольнаяПродукция(Основание, "Запасы");
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.РасходнаяНакладная")
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ВидОперации") = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНИсходящаяЕГАИС Тогда
			Возврат ЕстьАлкогольнаяПродукция(Основание, "Запасы");
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПеремещениеЗапасов") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНИсходящаяЕГАИС Тогда
			Возврат ЕстьАлкогольнаяПродукция(Основание, "Запасы");
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНВходящаяЕГАИС Тогда
			Возврат Ложь;
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
		
		Возврат ЕстьАлкогольнаяПродукция(Основание, "Запасы");
		
	КонецЕсли;
	
	ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не реализован ввод %1 на основании %2 в процедуре ИнтеграцияЕГАИСБП.ТребуетсяОформлениеДокументовЕГАИС()'"), ТипЗнч(Основание), МетаданныеДокументаЕГАИС.ПолноеИмя());
	
	ВызватьИсключение ТекстОшибки;
	
КонецФункции

Функция ЕстьАлкогольнаяПродукция(ДокументСсылка, ИмяТабличнойЧасти, ИмяКолонки = "Номенклатура")
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	" + ДокументСсылка.Метаданные().ПолноеИмя() + "." + ИмяТабличнойЧасти + " КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументСсылка
	|	И    ТабличнаяЧасть." + ИмяКолонки + ".АлкогольнаяПродукция
	|");
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

Функция ЕстьНеМаркируемаяАлкогольнаяПродукция(ДокументСсылка, ИмяТабличнойЧасти = "Товары", ИмяКолонки = "Номенклатура")
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	" + ДокументСсылка.Метаданные().ПолноеИмя() + "." + ИмяТабличнойЧасти + " КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументСсылка
	|	И    ТабличнаяЧасть." + ИмяКолонки + ".АлкогольнаяПродукция
	|	И НЕ ТабличнаяЧасть." + ИмяКолонки + ".ВидАлкогольнойПродукции.Маркируемый
	|");
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

Функция ЗапросСтатусаОформленияЕГАИС(ДокументОснование, МетаданныеДокумента) Экспорт
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПередачаВРегистр2ЕГАИС") 
		ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ТТНИсходящаяЕГАИС") 
		ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
		
		Запрос = Новый Запрос;
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаДокументы.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ОформленныеДокументы
		|ИЗ
		|	Документ.ПередачаВРегистр2ЕГАИС КАК ТаблицаДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
		|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
		|ГДЕ
		|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
		|	И ТаблицаДокументы.Проведен
		|	И СтатусыДокументовЕГАИС.Статус НЕ В(&КонечныеСтатусы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	Товары.Количество           КАК План,
		|	0                           КАК Факт
		|ПОМЕСТИТЬ ТоварыКОформлению
		|ИЗ
		|	Документ.ТТНВходящаяЕГАИС.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка = &ДокументОснование
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОформленныеТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	0                                      КАК План,
		|	ОформленныеТовары.Количество           КАК Факт
		|ИЗ
		|	Документ.ПередачаВРегистр2ЕГАИС.Товары КАК ОформленныеТовары
		|ГДЕ
		|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеДокументы КАК Т)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыКОформлению.АлкогольнаяПродукция,
		|	СУММА(ТоварыКОформлению.План) КАК План,
		|	СУММА(ТоварыКОформлению.Факт) КАК Факт
		|ПОМЕСТИТЬ Результат
		|ИЗ
		|	ТоварыКОформлению КАК ТоварыКОформлению
		|СГРУППИРОВАТЬ ПО
		|	ТоварыКОформлению.АлкогольнаяПродукция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
		|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
		|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
		|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ Результат КАК Т ГДЕ Т.АлкогольнаяПродукция = &ПустаяСсылка) КАК ТребуетсяСопоставлениеНоменклатуры,
		|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
		|";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПередачаВРегистр2ЕГАИС", МетаданныеДокумента.Имя);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТТНВходящаяЕГАИС", ДокументОснование.Метаданные().Имя);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "КонечныеСтатусы", "КонечныеСтатусы"+МетаданныеДокумента.Имя);
		Запрос.Текст = ТекстЗапроса;
		
		Возврат Запрос;
		
	Иначе
		
		Запрос = Новый Запрос;
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаДокументы.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ОформленныеДокументы
		|ИЗ
		|	Документ.АктПостановкиНаБалансЕГАИС КАК ТаблицаДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
		|		ПО (СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка)
		|ГДЕ
		|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
		|	И ТаблицаДокументы.Проведен
		|	И НЕ СтатусыДокументовЕГАИС.Статус В (&КонечныеСтатусы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТабличнаяЧасть.Количество КАК План,
		|	0 КАК Факт
		|ПОМЕСТИТЬ ТоварыКОформлению
		|ИЗ
		|	Документ.ОприходованиеЗапасов.Запасы КАК ТабличнаяЧасть
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
		|ГДЕ
		|	ТабличнаяЧасть.Ссылка = &ДокументОснование
		|	И СправочникНоменклатура.АлкогольнаяПродукция
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОформленныеТовары.Номенклатура,
		|	ОформленныеТовары.Характеристика,
		|	ОформленныеТовары.Серия,
		|	0,
		|	ОформленныеТовары.Количество
		|ИЗ
		|	Документ.АктПостановкиНаБалансЕГАИС.Товары КАК ОформленныеТовары
		|ГДЕ
		|	ОформленныеТовары.Ссылка В
		|			(ВЫБРАТЬ
		|				Т.Ссылка
		|			ИЗ
		|				ОформленныеДокументы КАК Т)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыКОформлению.Номенклатура КАК Номенклатура,
		|	ТоварыКОформлению.Характеристика КАК Характеристика,
		|	ТоварыКОформлению.Серия КАК Серия,
		|	СУММА(ТоварыКОформлению.План) КАК План,
		|	СУММА(ТоварыКОформлению.Факт) КАК Факт
		|ПОМЕСТИТЬ Результат
		|ИЗ
		|	ТоварыКОформлению КАК ТоварыКОформлению
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыКОформлению.Номенклатура,
		|	ТоварыКОформлению.Характеристика,
		|	ТоварыКОформлению.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)       КАК ЕстьОформленныеТовары,
		|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт) КАК ЕстьНеОформленныеТовары,
		|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ Результат КАК Т ГДЕ Т.План <= Т.Факт)              КАК ЕстьПолностьюОформленныеТовары,
		|	ЛОЖЬ                                                                          КАК ТребуетсяСопоставлениеНоменклатуры,
		|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ Результат КАК Т ГДЕ Т.План < Т.Факт)               КАК ЕстьОшибкиОформления
		|";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "АктПостановкиНаБалансЕГАИС", МетаданныеДокумента.Имя);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ОприходованиеТоваров", ДокументОснование.Метаданные().Имя);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "КонечныеСтатусы", "КонечныеСтатусы"+МетаданныеДокумента.Имя);
		Запрос.Текст = ТекстЗапроса;
		
		Возврат Запрос;
		
	КонецЕсли;
		
КонецФункции

Функция ДанныеАлкогольнойПродукции(Номенклатура) Экспорт
	
	СтруктураРезультат = Новый Структура;
	СтруктураРезультат.Вставить("Маркируемый"                 , Ложь);
	СтруктураРезультат.Вставить("ПродаетсяВРозлив"            , Ложь);
	СтруктураРезультат.Вставить("ЭтоАлкогольнаяПродукция"     , Ложь);
	СтруктураРезультат.Вставить("ВидАлкогольнойПродукцииЕГАИС", Неопределено);
	СтруктураРезультат.Вставить("ЕдиницаИзмерения"            , Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СправочникНоменклатура.ВидАлкогольнойПродукции КАК ВидАлкогольнойПродукцииЕГАИС,
	|	ЕСТЬNULL(СправочникНоменклатура.ВидАлкогольнойПродукции.Маркируемый, ЛОЖЬ) КАК Маркируемый,
	|	ЛОЖЬ КАК ПродаетсяВРозлив,
	|	ЕСТЬNULL(СправочникНоменклатура.АлкогольнаяПродукция, ЛОЖЬ) КАК ЭтоАлкогольнаяПродукция,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|ГДЕ
	|	СправочникНоменклатура.Ссылка = &Номенклатура";
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураРезультат, Выборка);
	КонецЕсли;
	
	Возврат СтруктураРезультат;
	
КонецФункции

Процедура ЗаполнитьАлкогольнуюПродукцию(ТекущаяСтрока) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|ГДЕ
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура = &Номенклатура
	|	И СоответствиеНоменклатурыЕГАИС.Характеристика = &Характеристика
	|	И (СоответствиеНоменклатурыЕГАИС.Серия = &Серия 
	|		ИЛИ &СерияЗаполнена = ЛОЖЬ)
	|");
	
	Запрос.УстановитьПараметр("Номенклатура",   ТекущаяСтрока.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", ТекущаяСтрока.Характеристика);
	Запрос.УстановитьПараметр("Серия",          ТекущаяСтрока.Серия);
	Запрос.УстановитьПараметр("СерияЗаполнена", ЗначениеЗаполнено(ТекущаяСтрока.Серия));
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	КоличествоСопоставлено = Выборка.Количество();
	ТекущаяСтрока.НоменклатураДляВыбора.Очистить();
	
	Пока Выборка.Следующий() Цикл
		Если КоличествоСопоставлено = 1 Тогда
			ТекущаяСтрока.АлкогольнаяПродукция = Выборка.АлкогольнаяПродукция;
		КонецЕсли;
		ТекущаяСтрока.НоменклатураДляВыбора.Добавить(Выборка.АлкогольнаяПродукция);
	КонецЦикла;
	
	Если КоличествоСопоставлено = 0 Тогда
		ТекущаяСтрока.АлкогольнаяПродукция = Неопределено;
	КонецЕсли;
	Если КоличествоСопоставлено > 1 Тогда
		ТекущаяСтрока.СопоставлениеАлкогольнаяПродукция = СтрШаблон(НСтр("ru = '<Несколько позиций (%1)>'"), КоличествоСопоставлено);
	ИначеЕсли КоличествоСопоставлено = 1 Тогда
		ТекущаяСтрока.СопоставлениеАлкогольнаяПродукция = "";
	Иначе
		ТекущаяСтрока.СопоставлениеАлкогольнаяПродукция = НСтр("ru = '<Не сопоставлено>'");
	КонецЕсли;
	
КонецПроцедуры

// Подготавливает массив для передачи в алкогольных данных в ЕГАИС
// Параметры:
//  ПодготовленнаяСтрока - строка таблицы значений
//  ИНН - строка ИНН организации
//  КПП - строка КПП организации
// Возвращаемое значение
//  Массив
Функция ПараметрыАлкогольнойПродукции(ПодготовленнаяСтрока) Экспорт
	
	ПараметрыАлкогольнойПродукции = Новый Массив;
	Если ПодготовленнаяСтрока.АлкогольнаяПродукция Тогда
		ПараметрыАлкогольнойПродукции.Добавить(ПодготовленнаяСтрока.Маркируемый);      // 1 - Признак передаваемой акцизной продукции
		ПараметрыАлкогольнойПродукции.Добавить(ПодготовленнаяСтрока.КодАкцизнойМарки); // 2 - Штрихкод марки алкогольной продукции
		ПараметрыАлкогольнойПродукции.Добавить(10 * ПодготовленнаяСтрока.ОбъемДАЛ);    // 3 - Объем алкогольной продукции
		ПараметрыАлкогольнойПродукции.Добавить(ПодготовленнаяСтрока.Крепость);         // 4 - Крепость алкогольной продукции
		ПараметрыАлкогольнойПродукции.Добавить(ПодготовленнаяСтрока.КодВидаПродукции); // 5 - Код вида алкогольной продукции
	КонецЕсли;
	
	Возврат ПараметрыАлкогольнойПродукции;
	
КонецФункции

Функция НоменклатураПоКлассификаторуЕГАИС(Номенклатура, Характеристика = Неопределено, ИдентификаторУпаковки = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция
		|ИЗ
		|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
		|ГДЕ
		|	СоответствиеНоменклатурыЕГАИС.Номенклатура = &Номенклатура
		|	И (&Характеристика = НЕОПРЕДЕЛЕНО
		|			ИЛИ СоответствиеНоменклатурыЕГАИС.Характеристика = &Характеристика)
		|	И (&Упаковка = НЕОПРЕДЕЛЕНО
		|			ИЛИ СоответствиеНоменклатурыЕГАИС.ИдентификаторУпаковки = &ИдентификаторУпаковки)";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Запрос.УстановитьПараметр("ИдентификаторУпаковки", ИдентификаторУпаковки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.АлкогольнаяПродукция;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ОрганизацияЕГАИСИзНастроек(Организация, ТорговаяТочка = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КлассификаторОрганизацийЕГАИС.Ссылка КАК ОрганизацияЕГАИС
	|ИЗ
	|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|ГДЕ
	|	КлассификаторОрганизацийЕГАИС.Контрагент = &Организация
	|	И КлассификаторОрганизацийЕГАИС.ТорговыйОбъект = &ТорговаяТочка";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Если ТорговаяТочка = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "КлассификаторОрганизацийЕГАИС.ТорговыйОбъект = &ТорговаяТочка", "ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("ТорговаяТочка", ТорговаяТочка);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ОрганизацияЕГАИС;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

Процедура ПриОпределенииРеквизитовОснования(МетаданныеДокументаОснования, МетаданныеДокументаЕГАИС, Реквизиты) Экспорт

	// Зададим имена реквизитов по умолчанию.
	Реквизиты.Контрагент						= "Организация";
	Реквизиты.ТорговыйОбъект	= "СтруктурнаяЕдиница";
	
	// Определим имя реквизита "Ответственный".
	Если МетаданныеДокументаОснования.Реквизиты.Найти("Ответственный") <> Неопределено Тогда
		Реквизиты.Ответственный = "Ответственный";
	Иначе
		Реквизиты.Ответственный = "Автор";
	КонецЕсли;
	
	// Уточним имена реквизитов для конкретных документов.
	Если МетаданныеДокументаОснования = Метаданные.Документы.ПеремещениеЗапасов Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНИсходящаяЕГАИС Тогда
			
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Реквизиты, "ТорговыйИлиПроизводственныйОбъект") Тогда
				Реквизиты.ТорговыйИлиПроизводственныйОбъект = "СтруктурнаяЕдиница";
			КонецЕсли;
			
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНВходящаяЕГАИС Тогда
			
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Реквизиты, "ТорговыйИлиПроизводственныйОбъект") Тогда
				Реквизиты.ТорговыйИлиПроизводственныйОбъект = "СтруктурнаяЕдиницаПолучатель";
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ПриОпределенииЗапросаТоварыДокументаОснования(МетаданныеДокументаОснования, МетаданныеДокументаЕГАИС, ТекстЗапроса, ДополнительныеПараметрыЗапроса) Экспорт
	
	Если МетаданныеДокументаОснования = Метаданные.Документы.ОприходованиеЗапасов Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Запасы.Ссылка КАК Ссылка,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Количество КАК Количество,
		|	Запасы.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВТ_Запасы
		|ИЗ
		|	Документ.ОприходованиеЗапасов.Запасы КАК Запасы
		|ГДЕ
		|	Запасы.Ссылка В(&МассивДокументов)
		|	И Запасы.Номенклатура.АлкогольнаяПродукция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Серии.Ссылка КАК Ссылка,
		|	Серии.Количество КАК Количество,
		|	Серии.Серия КАК Серия,
		|	Серии.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВТ_Серии
		|ИЗ
		|	Документ.ОприходованиеЗапасов.СерииНоменклатуры КАК Серии
		|ГДЕ
		|	Серии.Ссылка В(&МассивДокументов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Запасы.Ссылка КАК Ссылка,
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ВТ_Запасы.Номенклатура КАК Номенклатура,
		|	ВТ_Запасы.Характеристика КАК Характеристика,
		|	ВЫБОР
		|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВТ_Серии.Серия
		|	КОНЕЦ КАК Серия,
		|	ВЫБОР
		|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
		|			ТОГДА ВТ_Запасы.Количество
		|		ИНАЧЕ ВТ_Серии.Количество
		|	КОНЕЦ КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	ВТ_Запасы КАК ВТ_Запасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
		|		ПО ВТ_Запасы.Ссылка = ВТ_Серии.Ссылка 
		|		И ВТ_Запасы.КлючСвязи = ВТ_Серии.КлючСвязи";		
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.СписаниеЗапасов Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Запасы.Ссылка КАК Ссылка,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Количество КАК Количество,
		|	Запасы.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВТ_Запасы
		|ИЗ
		|	Документ.СписаниеЗапасов.Запасы КАК Запасы
		|ГДЕ
		|	Запасы.Ссылка В(&МассивДокументов)
		|	И Запасы.Номенклатура.АлкогольнаяПродукция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Серии.Ссылка КАК Ссылка,
		|	Серии.Количество КАК Количество,
		|	Серии.Серия КАК Серия,
		|	Серии.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВТ_Серии
		|ИЗ
		|	Документ.СписаниеЗапасов.СерииНоменклатуры КАК Серии
		|ГДЕ
		|	Серии.Ссылка В(&МассивДокументов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Запасы.Ссылка КАК Ссылка,
		|	ЛОЖЬ КАК ЭтоДвижениеПриход,
		|	ВТ_Запасы.Номенклатура КАК Номенклатура,
		|	ВТ_Запасы.Характеристика КАК Характеристика,
		|	ВЫБОР
		|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВТ_Серии.Серия
		|	КОНЕЦ КАК Серия,
		|	ВЫБОР
		|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
		|			ТОГДА ВТ_Запасы.Количество
		|		ИНАЧЕ ВТ_Серии.Количество
		|	КОНЕЦ КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	ВТ_Запасы КАК ВТ_Запасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
		|		ПО ВТ_Запасы.Ссылка = ВТ_Серии.Ссылка 
		|		И ВТ_Запасы.КлючСвязи = ВТ_Серии.КлючСвязи";	
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ПересортицаЗапасов Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Запасы.Ссылка КАК Ссылка,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.НоменклатураОприходование КАК НоменклатураОприходование,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.ХарактеристикаОприходование КАК ХарактеристикаОприходование,
		|	Запасы.Количество КАК Количество,
		|	Запасы.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВТ_Запасы
		|ИЗ
		|	Документ.ПересортицаЗапасов.Запасы КАК Запасы
		|ГДЕ
		|	Запасы.Ссылка В(&МассивДокументов)
		|	И Запасы.Номенклатура.АлкогольнаяПродукция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Серии.Ссылка КАК Ссылка,
		|	Серии.Количество КАК Количество,
		|	Серии.Серия КАК Серия,
		|	Серии.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВТ_Серии
		|ИЗ
		|	Документ.ПересортицаЗапасов.СерииНоменклатуры КАК Серии
		|ГДЕ
		|	Серии.Ссылка В(&МассивДокументов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СерииОприходование.Ссылка КАК Ссылка,
		|	СерииОприходование.Количество КАК Количество,
		|	СерииОприходование.Серия КАК Серия,
		|	СерииОприходование.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВТ_СерииОприходование
		|ИЗ
		|	Документ.ПересортицаЗапасов.СерииНоменклатурыОприходование КАК СерииОприходование
		|ГДЕ
		|	СерииОприходование.Ссылка В(&МассивДокументов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Запасы.Ссылка КАК Ссылка,
		|	ЛОЖЬ КАК ЭтоДвижениеПриход,
		|	ВТ_Запасы.Номенклатура КАК Номенклатура,
		|	ВТ_Запасы.Характеристика КАК Характеристика,
		|	ВЫБОР
		|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВТ_Серии.Серия
		|	КОНЕЦ КАК Серия,
		|	ВЫБОР
		|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
		|			ТОГДА ВТ_Запасы.Количество
		|		ИНАЧЕ ВТ_Серии.Количество
		|	КОНЕЦ КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	ВТ_Запасы КАК ВТ_Запасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
		|		ПО ВТ_Запасы.Ссылка = ВТ_Серии.Ссылка 
		|		И ВТ_Запасы.КлючСвязи = ВТ_Серии.КлючСвязи
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_Запасы.Ссылка,
		|	ИСТИНА,
		|	ВТ_Запасы.НоменклатураОприходование,
		|	ВТ_Запасы.ХарактеристикаОприходование,
		|	ВЫБОР
		|		КОГДА ВТ_СерииОприходование.Серия ЕСТЬ NULL
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВТ_СерииОприходование.Серия
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВТ_СерииОприходование.Серия ЕСТЬ NULL
		|			ТОГДА ВТ_Запасы.Количество
		|		ИНАЧЕ ВТ_СерииОприходование.Количество
		|	КОНЕЦ
		|ИЗ
		|	ВТ_Запасы КАК ВТ_Запасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СерииОприходование КАК ВТ_СерииОприходование
		|		ПО ВТ_Запасы.Ссылка = ВТ_СерииОприходование.Ссылка 
		|		И ВТ_Запасы.КлючСвязи = ВТ_СерииОприходование.КлючСвязи";
			
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ПеремещениеЗапасов Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Запасы.Ссылка КАК Ссылка,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Количество КАК Количество,
		|	Запасы.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВТ_Запасы
		|ИЗ
		|	Документ.ПеремещениеЗапасов.Запасы КАК Запасы
		|ГДЕ
		|	Запасы.Ссылка В(&МассивДокументов)
		|	И Запасы.Номенклатура.АлкогольнаяПродукция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Серии.Ссылка КАК Ссылка,
		|	Серии.Количество КАК Количество,
		|	Серии.Серия КАК Серия,
		|	Серии.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВТ_Серии
		|ИЗ
		|	Документ.ПеремещениеЗапасов.СерииНоменклатуры КАК Серии
		|ГДЕ
		|	Серии.Ссылка В(&МассивДокументов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Запасы.Ссылка КАК Ссылка,
		|	ЛОЖЬ КАК ЭтоДвижениеПриход,
		|	ВТ_Запасы.Номенклатура КАК Номенклатура,
		|	ВТ_Запасы.Характеристика КАК Характеристика,
		|	ВЫБОР
		|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВТ_Серии.Серия
		|	КОНЕЦ КАК Серия,
		|	ВЫБОР
		|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
		|			ТОГДА ВТ_Запасы.Количество
		|		ИНАЧЕ ВТ_Серии.Количество
		|	КОНЕЦ КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	ВТ_Запасы КАК ВТ_Запасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
		|		ПО ВТ_Запасы.Ссылка = ВТ_Серии.Ссылка 
		|		И ВТ_Запасы.КлючСвязи = ВТ_Серии.КлючСвязи";
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.РасходнаяНакладная Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Запасы.Ссылка КАК Ссылка,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Количество КАК Количество,
		|	Запасы.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВТ_Запасы
		|ИЗ
		|	Документ.РасходнаяНакладная.Запасы КАК Запасы
		|ГДЕ
		|	Запасы.Ссылка В(&МассивДокументов)
		|	И Запасы.Номенклатура.АлкогольнаяПродукция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Серии.Ссылка КАК Ссылка,
		|	Серии.Количество КАК Количество,
		|	Серии.Серия КАК Серия,
		|	Серии.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВТ_Серии
		|ИЗ
		|	Документ.РасходнаяНакладная.СерииНоменклатуры КАК Серии
		|ГДЕ
		|	Серии.Ссылка В(&МассивДокументов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Запасы.Ссылка КАК Ссылка,
		|	ЛОЖЬ КАК ЭтоДвижениеПриход,
		|	ВТ_Запасы.Номенклатура КАК Номенклатура,
		|	ВТ_Запасы.Характеристика КАК Характеристика,
		|	ВЫБОР
		|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВТ_Серии.Серия
		|	КОНЕЦ КАК Серия,
		|	ВЫБОР
		|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
		|			ТОГДА ВТ_Запасы.Количество
		|		ИНАЧЕ ВТ_Серии.Количество
		|	КОНЕЦ КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	ВТ_Запасы КАК ВТ_Запасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
		|		ПО ВТ_Запасы.Ссылка = ВТ_Серии.Ссылка 
		|		И ВТ_Запасы.КлючСвязи = ВТ_Серии.КлючСвязи";
		
	КонецЕсли;
		
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

// Функция проверяет наличие расхождений между ТТН и товарами накладной.
//
// Параметры:
//  ТоварноТранспортнаяНакладнаяЕГАИС - ДокументСсылка.ТТНВходящаяЕГАИС - проверяемая ТТН,
//  ДокументСсылка - ДокументСсылка.ПоступлениеТоваров - проверяемое поступление товаров.
//
// Возвращаемое значение:
//  Булево - Истина, если есть расхождения, иначе - Ложь.
//
Функция ЕстьРасхожденияМеждуДокументомПоступлениеТоваровИТТНЕГАИС(ТоварноТранспортнаяНакладнаяЕГАИС, ДокументСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТабличнаяЧасть.Количество) КАК Количество
	|ПОМЕСТИТЬ ВтТоварыТТН
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ТТНСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Сопоставлено.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)) КАК АлкогольнаяПродукция,
	|	СУММА(ТабличнаяЧасть.Количество * ВЫБОР
	|			КОГДА Сопоставлено.АлкогольнаяПродукция.ТипПродукции = ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|				ТОГДА ТабличнаяЧасть.Номенклатура.ОбъемДАЛ
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ ВтТоварыПоступления
	|ИЗ
	|	Документ.ПриходнаяНакладная.Запасы КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК Сопоставлено
	|		ПО ТабличнаяЧасть.Номенклатура = Сопоставлено.Номенклатура
	|			И ТабличнаяЧасть.Характеристика = Сопоставлено.Характеристика
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ПоступлениеТоваров
	|	И ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(Сопоставлено.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтТоварыТТН.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ВтТоварыПоступления.Количество КАК Количество
	|ИЗ
	|	ВтТоварыПоступления КАК ВтТоварыПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыТТН КАК ВтТоварыТТН
	|		ПО ВтТоварыПоступления.АлкогольнаяПродукция = ВтТоварыТТН.АлкогольнаяПродукция
	|ГДЕ
	|	(ВтТоварыПоступления.Количество > ЕСТЬNULL(ВтТоварыТТН.Количество, 0)
	|			ИЛИ ВтТоварыТТН.АлкогольнаяПродукция = ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка))");
	
	Запрос.УстановитьПараметр("ТТНСсылка",          ТоварноТранспортнаяНакладнаяЕГАИС);
	Запрос.УстановитьПараметр("ПоступлениеТоваров", ДокументСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

// Функция проверяет наличие расхождений между ТТН и товарами перемещения.
//
// Параметры:
//  ТоварноТранспортнаяНакладнаяЕГАИС - ДокументСсылка.ТТНВходящаяЕГАИС - проверяемая ТТН,
//  ДокументСсылка - ДокументСсылка.ПеремещениеЗапасов - проверяемое поступление товаров.
//
// Возвращаемое значение:
//  Булево - Истина, если есть расхождения, иначе - Ложь.
//
Функция ЕстьРасхожденияМеждуДокументомПеремещениеТоваровИТТНЕГАИС(ТоварноТранспортнаяНакладнаяЕГАИС, ДокументСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТабличнаяЧасть.Количество) КАК Количество
	|ПОМЕСТИТЬ ВтТоварыТТН
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ТТНСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Сопоставлено.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)) КАК АлкогольнаяПродукция,
	|	СУММА(ТабличнаяЧасть.Количество * ВЫБОР
	|			КОГДА Сопоставлено.АлкогольнаяПродукция.ТипПродукции = ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|				ТОГДА ТабличнаяЧасть.Номенклатура.ОбъемДАЛ
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ ВтТоварыПеремещения
	|ИЗ
	|	Документ.ПеремещениеЗапасов.Запасы КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК Сопоставлено
	|		ПО ТабличнаяЧасть.Номенклатура = Сопоставлено.Номенклатура
	|			И ТабличнаяЧасть.Характеристика = Сопоставлено.Характеристика
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ПеремещениеТоваров
	|	И ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(Сопоставлено.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтТоварыТТН.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ВтТоварыПеремещения.Количество КАК Количество
	|ИЗ
	|	ВтТоварыПеремещения КАК ВтТоварыПеремещения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыТТН КАК ВтТоварыТТН
	|		ПО ВтТоварыПеремещения.АлкогольнаяПродукция = ВтТоварыТТН.АлкогольнаяПродукция
	|ГДЕ
	|	(ВтТоварыПеремещения.Количество > ЕСТЬNULL(ВтТоварыТТН.Количество, 0)
	|			ИЛИ ВтТоварыТТН.АлкогольнаяПродукция = ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка))");
	
	Запрос.УстановитьПараметр("ТТНСсылка",          ТоварноТранспортнаяНакладнаяЕГАИС);
	Запрос.УстановитьПараметр("ПеремещениеТоваров", ДокументСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

// Проверяет есть ли в таблице товары алкогольная продукция ЕГАИС по реквизиту НеобходимостьВводаАкцизнойМарки
Функция ЕстьАлкогольнаяПродукцияЕГАИС(ТаблицаТовары) Экспорт
	
	Для Каждого Стр Из ТаблицаТовары Цикл
		Если Стр.НеобходимостьВводаАкцизнойМарки Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ЧекККМ

Процедура ПроверитьДанныеЕГАИС(ТаблицаТоваров, ДанныеЕГАИСДостаточны, ДокументОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.КодАкцизнойМарки,
	|	ТаблицаТовары.Партия,
	|	ТаблицаТовары.ЕдиницаИзмерения
	|ПОМЕСТИТЬ ПолнаяТаблица
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПолнаяТаблица.НомерСтроки,
	|	ПолнаяТаблица.Номенклатура,
	|	ПолнаяТаблица.Характеристика,
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК Штрихкод,
	|	ПолнаяТаблица.Номенклатура.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ПолнаяТаблица.КодАкцизнойМарки,
	|	ПолнаяТаблица.Номенклатура.ВидАлкогольнойПродукции.Маркируемый КАК Маркируемый,
	|	ПолнаяТаблица.Номенклатура.ВидАлкогольнойПродукции.Код КАК КодВидаПродукции,
	|	ПолнаяТаблица.Номенклатура.ОбъемДАЛ КАК ОбъемДАЛ,
	|	ПолнаяТаблица.Номенклатура.Крепость КАК Крепость
	|ПОМЕСТИТЬ ТаблицаАлкогольнойПродукции
	|ИЗ
	|	ПолнаяТаблица КАК ПолнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ПолнаяТаблица.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И ПолнаяТаблица.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|			И ПолнаяТаблица.Партия = ШтрихкодыНоменклатуры.Партия
	|			И (ПолнаяТаблица.ЕдиницаИзмерения = ШтрихкодыНоменклатуры.ЕдиницаИзмерения
	|				ИЛИ ТИПЗНАЧЕНИЯ(ПолнаяТаблица.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения))
	|ГДЕ
	|	ПолнаяТаблица.Номенклатура.АлкогольнаяПродукция
	|
	|СГРУППИРОВАТЬ ПО
	|	ПолнаяТаблица.Номенклатура,
	|	ПолнаяТаблица.КодАкцизнойМарки,
	|	ПолнаяТаблица.Характеристика,
	|	ШтрихкодыНоменклатуры.Штрихкод,
	|	ПолнаяТаблица.Номенклатура.Крепость,
	|	ПолнаяТаблица.Номенклатура.ОбъемДАЛ,
	|	ПолнаяТаблица.НомерСтроки,
	|	ПолнаяТаблица.Номенклатура.АлкогольнаяПродукция,
	|	ПолнаяТаблица.Номенклатура.ВидАлкогольнойПродукции.Маркируемый,
	|	ПолнаяТаблица.Номенклатура.ВидАлкогольнойПродукции.Код,
	|	ШтрихкодыНоменклатуры.Штрихкод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаАлкогольнойПродукции.НомерСтроки,
	|	ТаблицаАлкогольнойПродукции.Номенклатура,
	|	ТаблицаАлкогольнойПродукции.Характеристика,
	|	ТаблицаАлкогольнойПродукции.Штрихкод,
	|	ТаблицаАлкогольнойПродукции.АлкогольнаяПродукция,
	|	ТаблицаАлкогольнойПродукции.КодАкцизнойМарки,
	|	ТаблицаАлкогольнойПродукции.Маркируемый,
	|	ТаблицаАлкогольнойПродукции.ОбъемДАЛ,
	|	ТаблицаАлкогольнойПродукции.Крепость,
	|	ТаблицаАлкогольнойПродукции.КодВидаПродукции,
	|	ВЫБОР
	|		КОГДА ТаблицаАлкогольнойПродукции.Штрихкод = """"
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаШтрихкода,
	|	ВЫБОР
	|		КОГДА ТаблицаАлкогольнойПродукции.ОбъемДАЛ = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаОбъема,
	|	ВЫБОР
	|		КОГДА ТаблицаАлкогольнойПродукции.Маркируемый
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаАлкогольнойПродукции.КодАкцизнойМарки = """"
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаМарки,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаАлкогольнойПродукции.Маркируемый
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаАлкогольнойПродукции.Крепость = 0
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаКрепости,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаАлкогольнойПродукции.Маркируемый
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаАлкогольнойПродукции.КодВидаПродукции = """"
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаКодаВидаПродукции
	|ПОМЕСТИТЬ ТаблицаОшибок
	|ИЗ
	|	ТаблицаАлкогольнойПродукции КАК ТаблицаАлкогольнойПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОшибок.НомерСтроки,
	|	ТаблицаОшибок.Номенклатура,
	|	ТаблицаОшибок.Характеристика,
	|	ТаблицаОшибок.Штрихкод,
	|	ТаблицаОшибок.АлкогольнаяПродукция,
	|	ТаблицаОшибок.КодАкцизнойМарки,
	|	ТаблицаОшибок.Маркируемый,
	|	ТаблицаОшибок.ОбъемДАЛ,
	|	ТаблицаОшибок.Крепость,
	|	ТаблицаОшибок.КодВидаПродукции,
	|	ТаблицаОшибок.ОшибкаШтрихкода,
	|	ТаблицаОшибок.ОшибкаОбъема,
	|	ТаблицаОшибок.ОшибкаМарки,
	|	ТаблицаОшибок.ОшибкаКрепости,
	|	ТаблицаОшибок.ОшибкаКодаВидаПродукции
	|ИЗ
	|	ТаблицаОшибок КАК ТаблицаОшибок
	|ГДЕ
	|	(ТаблицаОшибок.ОшибкаШтрихкода
	|			ИЛИ ТаблицаОшибок.ОшибкаОбъема
	|			ИЛИ ТаблицаОшибок.ОшибкаМарки
	|			ИЛИ ТаблицаОшибок.ОшибкаКрепости
	|			ИЛИ ТаблицаОшибок.ОшибкаКодаВидаПродукции)";
	
	Запрос.УстановитьПараметр("ТаблицаТовары", ТаблицаТоваров.Скопировать());
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		ДанныеЕГАИСДостаточны = Истина;
		Возврат;
	КонецЕсли;
	
	ДанныеЕГАИСДостаточны = Ложь;
	
	Выборка = Результат.Выбрать();
	Отступ = Символы.ПС + " ";
	
	ОбщегоНазначения.СообщитьПользователю(
		НСтр("ru = 'Не хватает данных номенклатуры для передачи в ЕГАИС:'")
	);
	
	ДополнительныйСимвол = "";
	Пока Выборка.Следующий() Цикл
		
		ТекстТекущейОшибки = НСтр("ru = 'Строка №%1:'");
		ТекстТекущейОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстТекущейОшибки,
			Выборка.НомерСтроки);
		
		Если Выборка.ОшибкаШтрихкода Тогда
			ТекстТекущейОшибки = ТекстТекущейОшибки + Отступ + НСтр("ru = 'штрихкод'");
		КонецЕсли;
		Если Выборка.ОшибкаОбъема Тогда
			ТекстТекущейОшибки = ТекстТекущейОшибки + Отступ + НСтр("ru = 'объем единицы продукции'");
		КонецЕсли;
		Если Выборка.ОшибкаМарки Тогда
			ТекстТекущейОшибки = ТекстТекущейОшибки + Отступ + НСтр("ru = 'акцизная марка'");
		КонецЕсли;
		Если Выборка.ОшибкаКрепости Тогда
			ТекстТекущейОшибки = ТекстТекущейОшибки + Отступ + НСтр("ru = 'крепость'");
		КонецЕсли;
		Если Выборка.ОшибкаКодаВидаПродукции Тогда
			ТекстТекущейОшибки = ТекстТекущейОшибки + Отступ + НСтр("ru = 'код вида алкогольной продукции'");
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(ТекстТекущейОшибки);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ЗаполнитьНовыеРеквизитыТТНИсходящаяЕГАИС(Параметры) Экспорт
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТТНИсходящаяЕГАИСТовары.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ТТНИсходящаяЕГАИСТовары.Количество <> ТТНИсходящаяЕГАИСТовары.КоличествоФакт) КАК ЕстьРасхождения
	|ПОМЕСТИТЬ ВТ_ДокументыКОбработке
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ТТНИсходящаяЕГАИСТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	ТТНИсходящаяЕГАИСТовары.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ТТНИсходящаяЕГАИС.Ссылка КАК Ссылка,
	|	ВТ_ДокументыКОбработке.ЕстьРасхождения КАК ЕстьРасхождения
	|ИЗ
	|	ВТ_ДокументыКОбработке КАК ВТ_ДокументыКОбработке
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТТНИсходящаяЕГАИС КАК ТТНИсходящаяЕГАИС
	|		ПО ВТ_ДокументыКОбработке.Ссылка = ТТНИсходящаяЕГАИС.Ссылка
	|ГДЕ
	|	ВТ_ДокументыКОбработке.ЕстьРасхождения <> ТТНИсходящаяЕГАИС.ЕстьРасхождения";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Для каждого СтрокаРезультата Из РезультатЗапроса Цикл
		
		НачатьТранзакцию();
		
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.ТТНИсходящаяЕГАИС");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", СтрокаРезультата.Ссылка);
			Блокировка.Заблокировать();
			
			
			ДокументОбъект = СтрокаРезультата.Ссылка.ПолучитьОбъект();
			Если ДокументОбъект = Неопределено Тогда
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			
			ДокументОбъект.ЕстьРасхождения = СтрокаРезультата.ЕстьРасхождения;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
				
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
		Исключение
			// Если не удалось обработать какой-либо объект, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать документ: %1 по причине:
					|%2'"),
					СтрокаРезультата.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				СтрокаРезультата.Ссылка.Метаданные(), СтрокаРезультата.Ссылка, ТекстСообщения);
		КонецПопытки; 
	КонецЦикла; 
	
	Если ОбъектовОбработано = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре ЗаполнитьНовыеРеквизитыДокументовЕГАИС
				|не удалось обработать некоторые документы (пропущены): %1'"),
				ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.ТТНИсходящаяЕГАИС,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура ЗаполнитьНовыеРеквизитыДокументовЕГАИС
					|обработала очередную порцию документов: %1'"), ОбъектовОбработано));
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьНоменклатуруЕГАИС(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

Процедура ПеренестиДанныеОрганизацияСкладЕГАИС() Экспорт
	Запрос = Новый Запрос;
	
	МассивОрганизаций = Новый Массив;
	МассивОрганизаций.Добавить(Справочники.Организации.ПустаяСсылка());
	МассивОрганизаций.Добавить(Справочники.Контрагенты.ПустаяСсылка());
	МассивОрганизаций.Добавить(Неопределено);
	
	Запрос.УстановитьПараметр("ПустоеЗначениеОрганизации", МассивОрганизаций);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КлассификаторОрганизацийЕГАИС.Ссылка КАК Ссылка,
	|	ИСТИНА КАК СоответствуетОрганизации,
	|	НастройкиОбменаЕГАИС.УдалитьОрганизация КАК Контрагент,
	|	НастройкиОбменаЕГАИС.УдалитьТорговаяТочка КАК ТорговыйОбъект
	|ИЗ
	|	РегистрСведений.НастройкиОбменаЕГАИС КАК НастройкиОбменаЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|		ПО НастройкиОбменаЕГАИС.ИдентификаторФСРАР = КлассификаторОрганизацийЕГАИС.Код
	|ГДЕ
	|	КлассификаторОрганизацийЕГАИС.Контрагент В(&ПустоеЗначениеОрганизации)
	|	И КлассификаторОрганизацийЕГАИС.ТорговыйОбъект = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)";
	
	ВыборкаИзРезультата = Запрос.Выполнить().Выбрать();
	
	МетаданныеОбъекта = Метаданные.Справочники.КлассификаторОрганизацийЕГАИС;
	
	Пока ВыборкаИзРезультата.Следующий() Цикл
		СправочникОбъект = ВыборкаИзРезультата.Ссылка.ПолучитьОбъект();
		
		Если СправочникОбъект = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СправочникОбъект, ВыборкаИзРезультата, , "Ссылка");
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать элемент справочника: %1 по причине:
					|%2'"),
					ВыборкаИзРезультата.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта, 
				ВыборкаИзРезультата.Ссылка, 
				ТекстСообщения);
		КонецПопытки; 
	КонецЦикла; 
КонецПроцедуры

Процедура УстановитьКонстантыЕГАИС() Экспорт
	МенеджерЗначения = Константы.ДатаНачалаРегистрацииРозничныхПродажВЕГАИС.СоздатьМенеджерЗначения();
	МенеджерЗначения.Значение = Дата(2018,1,1);
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерЗначения);
	
	МенеджерЗначения = Константы.ДатаНачалаРегистрацииРозничныхПродажВЕГАИСВСельскойМестности.СоздатьМенеджерЗначения();
	МенеджерЗначения.Значение = Дата(2018,1,1);
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерЗначения);
КонецПроцедуры

Процедура ОбработатьРеквизитыТоваровТТНИсходящаяЕГАИС(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ТТНИсходящаяЕГАИСТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ТТНИсходящаяЕГАИСТовары
	|ГДЕ
	|	ТТНИсходящаяЕГАИСТовары.КоличествоУпаковок = 0";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Для каждого СтрокаРезультата Из РезультатЗапроса Цикл
		
		НачатьТранзакцию();
		
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.ТТНИсходящаяЕГАИС");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", СтрокаРезультата.Ссылка);
			Блокировка.Заблокировать();
			
			
			ДокументОбъект = СтрокаРезультата.Ссылка.ПолучитьОбъект();
			Если ДокументОбъект = Неопределено Тогда
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			Для каждого СтрокаТаблицы Из ДокументОбъект.Товары Цикл
				
				Если СтрокаТаблицы.КоличествоУпаковок = 0 Тогда
					СтрокаТаблицы.КоличествоУпаковок = СтрокаТаблицы.Количество;
				КонецЕсли;
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
				
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
		Исключение
			// Если не удалось обработать какой-либо объект, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать документ: %1 по причине:
					|%2'"),
					СтрокаРезультата.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				СтрокаРезультата.Ссылка.Метаданные(), СтрокаРезультата.Ссылка, ТекстСообщения);
		КонецПопытки; 
	КонецЦикла; 
	
	Если ОбъектовОбработано = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре ОбработатьРеквизитыТоваровТТНИсходящаяЕГАИС
				|не удалось обработать некоторые документы (пропущены): %1'"),
				ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.ТТНИсходящаяЕГАИС,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура ОбработатьРеквизитыТоваровТТНИсходящаяЕГАИС
					|обработала очередную порцию документов: %1'"), ОбъектовОбработано));
	КонецЕсли;
КонецПроцедуры

Процедура ДозаполнитьСоответствиеНоменклатурыЕГАИС(Параметры) Экспорт
	
КонецПроцедуры


#КонецОбласти 

