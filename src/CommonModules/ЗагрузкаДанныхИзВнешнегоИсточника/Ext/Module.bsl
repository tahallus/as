
#Область ПрограммныйИнтерфейс

Процедура ПриСозданииНаСервере(ОбъектЗаполнения, НастройкиЗагрузкиДанных, ЭтотОбъект, ИспользоватьФормуБСП = Истина) Экспорт
	
	НастройкиЗагрузкиДанных = Новый Структура;
	
	НастройкиЗагрузкиДанных.Вставить("ИспользоватьСовместно", ИспользоватьСовместноСПоставляемойЧастьюБСП() И ИспользоватьФормуБСП И НЕ ОбщегоНазначения.ЭтоВебКлиент() И НЕ ОбщегоНазначения.ЭтоМобильныйКлиент());
	
	Если ОбщегоНазначения.ЭтоВебКлиент()
		ИЛИ ОбщегоНазначения.ЭтоМобильныйКлиент()
		ИЛИ НЕ НастройкиЗагрузкиДанных.ИспользоватьСовместно Тогда
		
		СпособЗагрузкиДанныхИзВнешнихИсточников = Перечисления.СпособЗагрузкиДанныхИзВнешнихИсточников.ВыборФайла;
		
	Иначе
	
		СпособЗагрузкиДанныхИзВнешнихИсточников = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("СпособЗагрузкиДанныхИзВнешнихИсточников");
		Если НЕ ЗначениеЗаполнено(СпособЗагрузкиДанныхИзВнешнихИсточников) Тогда
			
			СпособЗагрузкиДанныхИзВнешнихИсточников = Перечисления.СпособЗагрузкиДанныхИзВнешнихИсточников.ВыборФайла;
			РегистрыСведений.НастройкиПользователей.Установить(СпособЗагрузкиДанныхИзВнешнихИсточников, "СпособЗагрузкиДанныхИзВнешнихИсточников");
			
		КонецЕсли;
	
	КонецЕсли;
	
	Если СпособЗагрузкиДанныхИзВнешнихИсточников = Перечисления.СпособЗагрузкиДанныхИзВнешнихИсточников.Копирование Тогда
		
		ИмяФормыЗагрузкиДанныхИзВнешнихИсточников = "Обработка.ЗагрузкаДанныхИзФайла.Форма.ЗагрузкаДанныхИзФайла";
		
	ИначеЕсли СпособЗагрузкиДанныхИзВнешнихИсточников = Перечисления.СпособЗагрузкиДанныхИзВнешнихИсточников.ВыборФайла Тогда
		
		ИмяФормыЗагрузкиДанныхИзВнешнихИсточников = "Обработка.ЗагрузкаДанныхИзВнешнегоИсточника.Форма.ПомощникЗагрузкиДанныхИзВнешнегоИсточника";
		
	КонецЕсли;
	
	ПолноеИмяОбъектаЗаполнения = ОбъектЗаполнения.ПолноеИмя();
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПриОпределенииФормыЗагрузкиДанных(ИмяФормыЗагрузкиДанныхИзВнешнихИсточников, ПолноеИмяОбъектаЗаполнения, ОбъектЗаполнения);
	
	НастройкиЗагрузкиДанных.Вставить("ПолноеИмяОбъектаЗаполнения", 					ПолноеИмяОбъектаЗаполнения);
	НастройкиЗагрузкиДанных.Вставить("ИмяФормыЗагрузкиДанныхИзВнешнихИсточников",	ИмяФормыЗагрузкиДанныхИзВнешнихИсточников);
	
	ЭтоЗагрузкаТабличнойЧасти = (СтрНайти(ПолноеИмяОбъектаЗаполнения, "ТабличнаяЧасть") > 0);
	НастройкиЗагрузкиДанных.Вставить("ЭтоЗагрузкаТабличнойЧасти", ЭтоЗагрузкаТабличнойЧасти);
	
	ЭтоЗагрузкаСправочника = (СтрНайти(ПолноеИмяОбъектаЗаполнения, "Справочник") > 0);
	НастройкиЗагрузкиДанных.Вставить("ЭтоЗагрузкаСправочника", ЭтоЗагрузкаСправочника);
	
	ЭтоЗагрузкаРегистраСведений = (СтрНайти(ПолноеИмяОбъектаЗаполнения, "РегистрСведений") > 0);
	НастройкиЗагрузкиДанных.Вставить("ЭтоЗагрузкаРегистраСведений", ЭтоЗагрузкаРегистраСведений);
	
КонецПроцедуры

Процедура ИзменитьСпособЗагрузкиДанныхИзВнешнегоИсточника(ИмяФормыЗагрузкиДанныхИзВнешнихИсточников) Экспорт
	
	// Изменение способа загрузки НЕ доступно в ВЕБ клиенте.
	
	СпособЗагрузкиДанныхИзВнешнихИсточников = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("СпособЗагрузкиДанныхИзВнешнихИсточников");
	Если НЕ ЗначениеЗаполнено(СпособЗагрузкиДанныхИзВнешнихИсточников) Тогда
		
		СпособЗагрузкиДанныхИзВнешнихИсточников = Перечисления.СпособЗагрузкиДанныхИзВнешнихИсточников.Копирование;
		
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.АвторизованныйПользователь();
	Если СпособЗагрузкиДанныхИзВнешнихИсточников = Перечисления.СпособЗагрузкиДанныхИзВнешнихИсточников.Копирование Тогда
		
		СпособЗагрузкиДанныхИзВнешнихИсточников = Перечисления.СпособЗагрузкиДанныхИзВнешнихИсточников.ВыборФайла;
		ИмяФормыЗагрузкиДанныхИзВнешнихИсточников = "Обработка.ЗагрузкаДанныхИзВнешнегоИсточника.Форма.ПомощникЗагрузкиДанныхИзВнешнегоИсточника";
		
	ИначеЕсли СпособЗагрузкиДанныхИзВнешнихИсточников = Перечисления.СпособЗагрузкиДанныхИзВнешнихИсточников.ВыборФайла Тогда
		
		СпособЗагрузкиДанныхИзВнешнихИсточников = Перечисления.СпособЗагрузкиДанныхИзВнешнихИсточников.Копирование;
		ИмяФормыЗагрузкиДанныхИзВнешнихИсточников = "Обработка.ЗагрузкаДанныхИзФайла.Форма.ЗагрузкаДанныхИзФайла";
		
	КонецЕсли;
	
	РегистрыСведений.НастройкиПользователей.Установить(СпособЗагрузкиДанныхИзВнешнихИсточников, "СпособЗагрузкиДанныхИзВнешнихИсточников", ТекущийПользователь);
	
КонецПроцедуры

Функция ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна() Экспорт
	
	Возврат "_ЗагрузкаВПриложениеВозможна";
	
КонецФункции

Функция ИмяСлужебногоПоляЗаполненыНеПолностью() Экспорт
	
	Возврат "_ЗаполненыНеПолностью";
	
КонецФункции

Процедура ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, ИмяПоля, ПредставлениеПоля, ТипПоля, ТипПолучаемогоЗначения, ИмяГруппыПолей = "", Приоритет = 0, ОбязательноеЗаполнение = Ложь, ОбязательноеЗаполнениеГруппы = Ложь, Видимость = Истина, ДопРеквизитПризнак = Ложь, ДопРеквизитСсылка = Неопределено) Экспорт
	
	НоваяСтрока = ТаблицаПолейЗагрузки.Добавить();
	
	НоваяСтрока.ИмяПоля = ИмяПоля;
	НоваяСтрока.ПредставлениеПоля = ПредставлениеПоля;
	НоваяСтрока.ТипПоля = ТипПоля;
	НоваяСтрока.ТипПолучаемогоЗначения = ТипПолучаемогоЗначения;
	НоваяСтрока.ИмяГруппыПолей = ИмяГруппыПолей;
	НоваяСтрока.Приоритет = Приоритет;
	НоваяСтрока.ОбязательноеЗаполнение = ОбязательноеЗаполнение;
	НоваяСтрока.ОбязательноеЗаполнениеГруппы = ОбязательноеЗаполнениеГруппы;
	НоваяСтрока.Видимость = Видимость;
	НоваяСтрока.ДопРеквизитПризнак = ДопРеквизитПризнак;
	НоваяСтрока.ДопРеквизитСсылка = ДопРеквизитСсылка;
	
КонецПроцедуры

Процедура ПрогрессСопоставленияДанных(ИндексТекущейСтроки, РазмерТаблицыДанных, ТекстШаблона = "") Экспорт
	
	Если ПустаяСтрока(ТекстШаблона) Тогда
		
		ТекстШаблона = НСтр("ru ='Обработано %1 из %2 строк...'");
		
	КонецЕсли;
	
	ТекстПрогресса      = СтрШаблон(ТекстШаблона, ИндексТекущейСтроки, РазмерТаблицыДанных);
	ПроцентВыполнения   = Окр(ИндексТекущейСтроки * 100 / РазмерТаблицыДанных);
	
	ДлительныеОперации.СообщитьПрогресс(ПроцентВыполнения, ТекстПрогресса);
	
КонецПроцедуры

Функция ИмяПоляДобавленияДополнительныхРеквизитов() Экспорт
	
	Возврат "_ДополнительныеРеквизитыИСведения";
	
КонецФункции

// Создает таблицу дублирующих строк для поиска номенклатуры по ключевым полям
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица с ключевыми полями поиска номенклатуры
//
Функция ПустаяТаблицаДублирующихСтрокНоменклатуры() Экспорт
	
	ОписаниеТиповСтрока11 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(11));
	ОписаниеТиповСтрока25 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(25));
	ОписаниеТиповСтрока100 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(100));
	ОписаниеТиповСтрока200 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(200));
	ОписаниеТиповСтрока1000 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(1000));
	ОписаниеТиповЧисло10_0 = Новый ОписаниеТипов("Число", , , , Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Любой));
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	
	ТаблицаДублирующихСтрок = Новый ТаблицаЗначений;
	ТаблицаДублирующихСтрок.Колонки.Добавить("Код", ОписаниеТиповСтрока11);
	ТаблицаДублирующихСтрок.Колонки.Добавить("Штрихкод", ОписаниеТиповСтрока200);
	ТаблицаДублирующихСтрок.Колонки.Добавить("Артикул", ОписаниеТиповСтрока25);
	ТаблицаДублирующихСтрок.Колонки.Добавить("НоменклатураНаименование", ОписаниеТиповСтрока100);
	ТаблицаДублирующихСтрок.Колонки.Добавить("НоменклатураНаименованиеПолное", ОписаниеТиповСтрока1000);
	ТаблицаДублирующихСтрок.Колонки.Добавить("КлючСвязи", ОписаниеТиповЧисло10_0);
	ТаблицаДублирующихСтрок.Колонки.Добавить("Номенклатура", ОписаниеТиповКолонка); 
	
	Возврат ТаблицаДублирующихСтрок;
	
КонецФункции
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция МаксимальноеКоличествоПустыхСтрок() Экспорт 
	
	Возврат 1000;
	
КонецФункции

Процедура ДобавитьОшибку(Ошибки, ТекстОшибки, ЭтоКритичнаяОшибка = Ложь, МестоВозникновения = "") Экспорт
	
	ОписаниеОшибки = Ошибки.Добавить();
	
	ОписаниеОшибки.ОписаниеОшибки		= ТекстОшибки;
	ОписаниеОшибки.Критичная 			= ЭтоКритичнаяОшибка;
	ОписаниеОшибки.МестоВозникновения	= МестоВозникновения;
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуПолейЗагрузки(ТаблицаПолейЗагрузки, НастройкиЗагрузкиДанных) Экспорт
	Перем Менеджер;
	
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПереопределитьЗаполнениеПолейЗагрузкиДанных(ТаблицаПолейЗагрузки, НастройкиЗагрузкиДанных);
	
	Если ТаблицаПолейЗагрузки.Количество() = 0 Тогда
		
		ПолучитьМенеджерПоИмениОбъектаЗаполнения(НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения, Менеджер);
		Менеджер.ПоляЗагрузкиДанныхИзВнешнегоИсточника(ТаблицаПолейЗагрузки, НастройкиЗагрузкиДанных);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьДеревоПолейИГруппПолей(ГруппыИПоля)
	
	ГруппыИПоля = Новый ДеревоЗначений;
	ГруппыИПоля.Колонки.Добавить("ИмяГруппыПолей", Новый ОписаниеТипов("Строка"));
	ГруппыИПоля.Колонки.Добавить("ТипВходящихДанных");
	ГруппыИПоля.Колонки.Добавить("ТипПолучаемогоЗначения");
	ГруппыИПоля.Колонки.Добавить("ИмяПоля", Новый ОписаниеТипов("Строка"));
	ГруппыИПоля.Колонки.Добавить("ПредставлениеПоля", Новый ОписаниеТипов("Строка"));
	ГруппыИПоля.Колонки.Добавить("НомерКолонки", Новый ОписаниеТипов("Число"));
	ГруппыИПоля.Колонки.Добавить("ОбязательноеЗаполнение", Новый ОписаниеТипов("Булево"));
	ГруппыИПоля.Колонки.Добавить("ОбязательноеЗаполнениеГруппы", Новый ОписаниеТипов("Булево"));
	ГруппыИПоля.Колонки.Добавить("Видимость", Новый ОписаниеТипов("Булево"));
	ГруппыИПоля.Колонки.Добавить("ДопРеквизитПризнак", Новый ОписаниеТипов("Булево"));
	ГруппыИПоля.Колонки.Добавить("ДопРеквизитСсылка", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"));
	
КонецПроцедуры

Процедура СоздатьТаблицуПолейОписанияЗагрузки(ТаблицаПолейЗагрузки) Экспорт
	
	ТаблицаПолейЗагрузки = Новый ТаблицаЗначений;
	ТаблицаПолейЗагрузки.Колонки.Добавить("ИмяПоля");
	ТаблицаПолейЗагрузки.Колонки.Добавить("ПредставлениеПоля");
	ТаблицаПолейЗагрузки.Колонки.Добавить("ТипПоля"); 					// Тип входящих данных
	ТаблицаПолейЗагрузки.Колонки.Добавить("ТипПолучаемогоЗначения");	// Тип данных в приложении
	ТаблицаПолейЗагрузки.Колонки.Добавить("ИмяГруппыПолей");
	ТаблицаПолейЗагрузки.Колонки.Добавить("Приоритет");
	ТаблицаПолейЗагрузки.Колонки.Добавить("ОбязательноеЗаполнение");
	ТаблицаПолейЗагрузки.Колонки.Добавить("ОбязательноеЗаполнениеГруппы");
	ТаблицаПолейЗагрузки.Колонки.Добавить("Видимость");
	ТаблицаПолейЗагрузки.Колонки.Добавить("ДопРеквизитПризнак", Новый ОписаниеТипов("Булево"));
	ТаблицаПолейЗагрузки.Колонки.Добавить("ДопРеквизитСсылка", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"));
	
КонецПроцедуры

Процедура СоздатьТаблицуОписанияОшибок(Ошибки) Экспорт
	
	Ошибки = Новый ТаблицаЗначений;
	
	Ошибки.Колонки.Добавить("ОписаниеОшибки",		Новый ОписаниеТипов("Строка"));
	Ошибки.Колонки.Добавить("Критичная", 			Новый ОписаниеТипов("Булево"));
	Ошибки.Колонки.Добавить("МестоВозникновения",	Новый ОписаниеТипов("Строка"));
	
КонецПроцедуры

Процедура ПолучитьМенеджерПоИмениОбъектаЗаполнения(ПолноеИмяОбъектаЗаполнения, Менеджер) Экспорт
	
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяОбъектаЗаполнения);
	
КонецПроцедуры

Процедура ДобавитьСлужебныеПоля(ТаблицаПолейЗагрузки, ГруппаСлужебныхПолей, ПолноеИмяОбъектаЗаполнения, ЭтоЗагрузкаВТЧ) Экспорт
	
	// Обязательное служебное поле. Используется помощником.
	СлужебноеПоле						= ГруппаСлужебныхПолей.Строки.Добавить(); 
	СлужебноеПоле.ИмяПоля				= ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна();
	СлужебноеПоле.ТипПолучаемогоЗначения= Новый ОписаниеТипов("Булево");
	
	СлужебноеПоле						= ГруппаСлужебныхПолей.Строки.Добавить(); 
	СлужебноеПоле.ИмяПоля				= "_СтрокаСопоставлена";
	СлужебноеПоле.ТипПолучаемогоЗначения= Новый ОписаниеТипов("Булево");
	
	СлужебноеПоле						= ГруппаСлужебныхПолей.Строки.Добавить(); 
	СлужебноеПоле.ИмяПоля				= "_ЗаполненыНеПолностью";
	СлужебноеПоле.ТипПолучаемогоЗначения= Новый ОписаниеТипов("Булево");
	
	СлужебноеПоле						= ГруппаСлужебныхПолей.Строки.Добавить(); 
	СлужебноеПоле.ИмяПоля				= "_КлючСвязи";
	СлужебноеПоле.ТипПолучаемогоЗначения= Новый ОписаниеТипов("Число", , , , Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Любой));
	
	// Возможность описать пользовательские служебные поля
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПриДобавленииСлужебныхПолей(ГруппаСлужебныхПолей, ПолноеИмяОбъектаЗаполнения);
	
КонецПроцедуры

Процедура ЗаполнитьДеревоГруппыИПолей(ТаблицаПолейЗагрузки, ГруппыИПоля, ПолноеИмяОбъектаЗаполнения, ЭтоЗагрузкаВТЧ)
	
	ТаблицаГруппПолей = ТаблицаПолейЗагрузки.Скопировать(,"ИмяГруппыПолей");
	ТаблицаГруппПолей.Свернуть("ИмяГруппыПолей");
	
	СтрокаТаблицы = ТаблицаГруппПолей.Добавить();
	СтрокаТаблицы.ИмяГруппыПолей = ИмяГруппыПолейОбязательныхКЗаполнению();
	
	СтрокаТаблицы = ТаблицаГруппПолей.Добавить();
	СтрокаТаблицы.ИмяГруппыПолей = ИмяГруппыПолейНеобязательныхКЗаполнению();
	
	СтрокаТаблицы = ТаблицаГруппПолей.Добавить();
	СтрокаТаблицы.ИмяГруппыПолей = ИмяГруппыПолейСлужебные();
	
	Для каждого СтрокаТаблицы Из ТаблицаГруппПолей Цикл
		
		Если ПустаяСтрока(СтрокаТаблицы.ИмяГруппыПолей) Тогда
			
			// Поля не объединенные в группы, разбираются отдельно по свойству ОбязательноеЗаполнение
			Продолжить;
			
		КонецЕсли;
		
		НоваяСтрокаПервогоУровня= ГруппыИПоля.Строки.Добавить();
		НоваяСтрокаПервогоУровня.ИмяГруппыПолей = СтрокаТаблицы.ИмяГруппыПолей;
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ИмяГруппыПолей", СтрокаТаблицы.ИмяГруппыПолей);
		Если СтрокаТаблицы.ИмяГруппыПолей = ИмяГруппыПолейОбязательныхКЗаполнению() Тогда
			
			ПараметрыОтбора.Вставить("ИмяГруппыПолей", "");
			ПараметрыОтбора.Вставить("ОбязательноеЗаполнение", Истина);
			
		ИначеЕсли СтрокаТаблицы.ИмяГруппыПолей = ИмяГруппыПолейНеобязательныхКЗаполнению() Тогда
			
			ПараметрыОтбора.Вставить("ИмяГруппыПолей", "");
			ПараметрыОтбора.Вставить("ОбязательноеЗаполнение", Ложь);
			
		ИначеЕсли СтрокаТаблицы.ИмяГруппыПолей = ИмяГруппыПолейСлужебные() Тогда
			
			ДобавитьСлужебныеПоля(ТаблицаПолейЗагрузки, НоваяСтрокаПервогоУровня, ПолноеИмяОбъектаЗаполнения, ЭтоЗагрузкаВТЧ);
			Продолжить;
			
		КонецЕсли;
		
		ОбязательноеЗаполнениеГруппы = Ложь;
		МассивСтрок 			= ТаблицаПолейЗагрузки.НайтиСтроки(ПараметрыОтбора);
		Если МассивСтрок.Количество() > 0 Тогда
			
			ЭтоПользовательскаяГруппаПолей = ЭтоПользовательскаяГруппаПолей(СтрокаТаблицы.ИмяГруппыПолей);
			Если ЭтоПользовательскаяГруппаПолей Тогда
				
				НоваяСтрокаПервогоУровня.ТипПолучаемогоЗначения = МассивСтрок[0].ТипПолучаемогоЗначения; // тип получаемого значения одинаков во всех полях одной группы полей (ссылка, строка, число и т.д.)
				НоваяСтрокаПервогоУровня.Видимость = Ложь;
				
			КонецЕсли;
			
			Для каждого СтрокаМассива Из МассивСтрок Цикл
				
				НоваяСтрокаВторогоУровня = НоваяСтрокаПервогоУровня.Строки.Добавить();
				НоваяСтрокаВторогоУровня.ИмяПоля = СтрокаМассива.ИмяПоля;
				НоваяСтрокаВторогоУровня.ТипВходящихДанных = СтрокаМассива.ТипПоля; // входящий тип данных (строка, число)
				НоваяСтрокаВторогоУровня.ТипПолучаемогоЗначения = СтрокаМассива.ТипПолучаемогоЗначения;
				НоваяСтрокаВторогоУровня.ПредставлениеПоля = СтрокаМассива.ПредставлениеПоля;
				НоваяСтрокаВторогоУровня.ОбязательноеЗаполнение = СтрокаМассива.ОбязательноеЗаполнение;
				НоваяСтрокаВторогоУровня.ОбязательноеЗаполнениеГруппы = СтрокаМассива.ОбязательноеЗаполнениеГруппы;
				НоваяСтрокаВторогоУровня.Видимость = СтрокаМассива.Видимость;
				НоваяСтрокаВторогоУровня.ДопРеквизитПризнак = СтрокаМассива.ДопРеквизитПризнак;
				НоваяСтрокаВторогоУровня.ДопРеквизитСсылка = СтрокаМассива.ДопРеквизитСсылка;
				
				Если НоваяСтрокаВторогоУровня.Видимость Тогда
					
					НоваяСтрокаПервогоУровня.Видимость = Истина;
					
				КонецЕсли;
				
				ОбязательноеЗаполнениеГруппы = ОбязательноеЗаполнениеГруппы ИЛИ СтрокаМассива.ОбязательноеЗаполнениеГруппы;
				
			КонецЦикла;
			
		КонецЕсли;
		
		НоваяСтрокаПервогоУровня.ОбязательноеЗаполнениеГруппы = ОбязательноеЗаполнениеГруппы;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьИЗаполнитьДеревоГруппыИПолейПоИмениОбъекта(НастройкиЗагрузкиДанных, ГруппыИПоля) Экспорт
	Перем ТаблицаПолейЗагрузки;
	
	ПолноеИмяОбъектаЗаполнения = НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения;
	ЭтоЗагрузкаВТЧ = НастройкиЗагрузкиДанных.ЭтоЗагрузкаТабличнойЧасти;
	
	СоздатьТаблицуПолейОписанияЗагрузки(ТаблицаПолейЗагрузки);
	ЗаполнитьТаблицуПолейЗагрузки(ТаблицаПолейЗагрузки, НастройкиЗагрузкиДанных);
	СоздатьДеревоПолейИГруппПолей(ГруппыИПоля);
	ЗаполнитьДеревоГруппыИПолей(ТаблицаПолейЗагрузки, ГруппыИПоля, ПолноеИмяОбъектаЗаполнения, ЭтоЗагрузкаВТЧ);
	
КонецПроцедуры

Функция ЭтоПользовательскаяГруппаПолей(ИмяГруппыПолей) Экспорт
	
	Возврат (ИмяГруппыПолей <> ИмяГруппыПолейОбязательныхКЗаполнению() И ИмяГруппыПолей <> ИмяГруппыПолейНеобязательныхКЗаполнению() И ИмяГруппыПолей <> ИмяГруппыПолейСлужебные());
	
КонецФункции

Функция ИмяГруппыПолейОбязательныхКЗаполнению() Экспорт
	
	Возврат "_ГруппаПолейОбязательныхКЗаполнению";
	
КонецФункции

Функция ИмяГруппыПолейНеобязательныхКЗаполнению() Экспорт
	
	Возврат "_ГруппаПолейНеобязательныхКЗаполнению";
	
КонецФункции

Функция ИмяГруппыПолейСлужебные() Экспорт
	
	Возврат "_ГруппаПолейСлужебные";
	
КонецФункции

Функция ПостФиксИменПолейВходящихДанных() Экспорт
	
	Возврат "_ВходящиеДанные";
	
КонецФункции

Функция ИспользоватьСовместноСПоставляемойЧастьюБСП() Экспорт
	
	ИспользоватьСовместно = Истина;
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПриОпределенииРежимаИспользования(ИспользоватьСовместно);
	
	Возврат ИспользоватьСовместно;
	
КонецФункции

Функция ОбъектЗагрузкиПоПолномуИмени(ПолноеИмяОбъектаЗаполнения) Экспорт
	
	ИмяОбъектаЗаполнения = ПолноеИмяОбъектаЗаполнения;
	ИмяТабличнойЧасти = "";
	
	ИменаИспользуемыхТабличныхЧастей = ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ИменаИспользуемыхТабличныхЧастей();
	Для каждого ЭлементСоответствия Из ИменаИспользуемыхТабличныхЧастей Цикл
		
		Если Прав(ПолноеИмяОбъектаЗаполнения, СтрДлина(ЭлементСоответствия.Ключ)) = ЭлементСоответствия.Ключ Тогда
			
			ИмяОбъектаЗаполнения = СтрЗаменить(ПолноеИмяОбъектаЗаполнения, ЭлементСоответствия.Ключ, "");
			ИмяТабличнойЧасти = ЭлементСоответствия.Значение;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ОбъектЗагрузки",	Справочники.ИдентификаторыОбъектовМетаданных.НайтиПоРеквизиту("ПолноеИмя",ИмяОбъектаЗаполнения));
	Результат.Вставить("ИмяТабличнойЧасти", ИмяТабличнойЧасти);
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьДополнительныйРеквизитВОбъект(ЭлементСправочника, ЭтоСуществующийОбъект, Свойство, ЗначениеДопРеквизита);
	
	Если ТипЗнч(Свойство.НаборСвойств) = Тип("СправочникСсылка.НаборыДополнительныхРеквизитовИСведений") Тогда
		
		Если ТипЗнч(ЭлементСправочника) = Тип("СправочникСсылка.Номенклатура") Тогда
			
			Если Свойство.НаборСвойств.ИмяПредопределенныхДанных <> "Справочник_Номенклатура_Общие"
				И Свойство.НаборСвойств <> ЭлементСправочника.КатегорияНоменклатуры.НаборСвойств Тогда
				
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоСуществующийОбъект Тогда
		
		ОтборСтрок = Новый Структура("Свойство", Свойство);
		НайденныеСтрокиДопРеквизитов = ЭлементСправочника.ДополнительныеРеквизиты.НайтиСтроки(ОтборСтрок);
		СтрокаДопРеквизита = ?(НайденныеСтрокиДопРеквизитов.Количество() < 1, ЭлементСправочника.ДополнительныеРеквизиты.Добавить(), НайденныеСтрокиДопРеквизитов[0]);
		
	Иначе
		
		СтрокаДопРеквизита = ЭлементСправочника.ДополнительныеРеквизиты.Добавить();
		
	КонецЕсли;
	
	СтрокаДопРеквизита.Свойство = Свойство;
	СтрокаДопРеквизита.Значение = ЗначениеДопРеквизита;
	
КонецПроцедуры

Процедура ОбработатьВыбранныеДополнительныеРеквизиты(ЭлементСправочника, ЭтоСуществующийОбъект, СтрокаТаблицы, ВыбранныеДополнительныеРеквизиты) Экспорт
	
	ПостФикс = "_ВходящиеДанные";
	
	Для каждого ОписаниеДополнительногоРеквизита Из ВыбранныеДополнительныеРеквизиты Цикл
		
		СтроковоеЗначение = СтрокаТаблицы[ОписаниеДополнительногоРеквизита.Значение + ПостФикс];
		ЗначениеДопРеквизита = СтрокаТаблицы[ОписаниеДополнительногоРеквизита.Значение];
		Свойство = ОписаниеДополнительногоРеквизита.Ключ;
		
		Если НЕ ЗначениеЗаполнено(ЗначениеДопРеквизита) И НЕ ПустаяСтрока(СтроковоеЗначение) Тогда
			
			МассивТиповЗначений = Свойство.ТипЗначения.Типы();
			Если МассивТиповЗначений.Найти(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) <> Неопределено Тогда
				
				ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СоздатьДополнительноеСвойство(ЗначениеДопРеквизита, Свойство, Ложь, СтроковоеЗначение);
				
			ИначеЕсли МассивТиповЗначений.Найти(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия")) <> Неопределено Тогда
				
				ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СоздатьДополнительноеСвойство(ЗначениеДопРеквизита, Свойство, Истина, СтроковоеЗначение);
			КонецЕсли;
				
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЗначениеДопРеквизита) Тогда
			
			ДобавитьДополнительныйРеквизитВОбъект(ЭлементСправочника, ЭтоСуществующийОбъект, Свойство, ЗначениеДопРеквизита);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПодготовитьСоответствиеПоДополнительнымРеквизитам(НастройкиЗагрузкиДанных, Владелец) Экспорт
	
	ОписаниеДополнительныхРеквизитов = Новый Соответствие;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ 
	|	ДопРеквизиты.Свойство КАК Свойство
	|ИЗ Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ДопРеквизиты 
	|ГДЕ НЕ ДопРеквизиты.ПометкаУдаления И ДопРеквизиты.Ссылка = &Владелец";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		НомерДополнительногоРеквизита = 0;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НомерДополнительногоРеквизита = НомерДополнительногоРеквизита + 1;
			ОписаниеДополнительныхРеквизитов.Вставить(Выборка.Свойство, "ДополнительныйРеквизит" + Формат(НомерДополнительногоРеквизита, "ЧГ=0"));
			
		КонецЦикла;
		
	КонецЕсли;
	
	НастройкиЗагрузкиДанных.Вставить("ОписаниеДополнительныхРеквизитов", ОписаниеДополнительныхРеквизитов); // Для заполнения списка всех реквизитов
	НастройкиЗагрузкиДанных.Вставить("ВыбранныеДополнительныеРеквизиты", Новый Соответствие); // Только те, которые есть
	
КонецПроцедуры

Функция СтандартныеИменаПолейНеподлежащихОбновлению(НастройкиЗагрузкиДанных)
	
	Возврат ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СтандартныеИменаПолейНеподлежащихОбновлению(НастройкиЗагрузкиДанных);
	
КонецФункции

Процедура ДобавитьИмяПоляВСтрокуОписанияСвойств(СтрокаОписанияСвойств, ИмяПоля)
	
	ИменаПолейНеУчаствующиеВЗаполненииСвойств = ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ИменаПолейНеУчаствующиеВЗаполненииСвойств();
	Если СтрНайти(ИменаПолейНеУчаствующиеВЗаполненииСвойств, ИмяПоля) > 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СтрокаОписанияСвойств = СтрокаОписанияСвойств + ?(ПустаяСтрока(СтрокаОписанияСвойств), "", ",") + ИмяПоля;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокиИменПолей(ДеревоПолей, НастройкиЗагрузкиДанных, НастройкиОбновленияСвойств)
	
	ИменаПолейНеподлежащихОбновлению = НастройкиОбновленияСвойств.ИменаПолейНеподлежащихОбновлению;
	ИменаПолейНеподлежащихОбновлению = СтандартныеИменаПолейНеподлежащихОбновлению(НастройкиЗагрузкиДанных);
	
	ИменаПолейОбновляемые = НастройкиОбновленияСвойств.ИменаПолейОбновляемые;
	
	Для каждого СтрокиПервогоУровня Из ДеревоПолей.Строки Цикл
		
		Если СтрокиПервогоУровня.НомерКолонки <> 0 Тогда
			
			ДобавитьИмяПоляВСтрокуОписанияСвойств(ИменаПолейОбновляемые, СтрокиПервогоУровня.ИмяПоля);
			
		ИначеЕсли НЕ ПустаяСтрока(СтрокиПервогоУровня.ИмяПоля)
			И СтрокиПервогоУровня.ИмяПоля <> ИмяПоляДобавленияДополнительныхРеквизитов() Тогда
			
			ДобавитьИмяПоляВСтрокуОписанияСвойств(ИменаПолейНеподлежащихОбновлению, СтрокиПервогоУровня.ИмяПоля);
			
		ИначеЕсли НЕ ПустаяСтрока(СтрокиПервогоУровня.ИмяГруппыПолей) Тогда
			
			Для каждого СтрокиВторогоУровня Из СтрокиПервогоУровня.Строки Цикл
				
				Если НЕ ПустаяСтрока(СтрокиВторогоУровня.ИмяПоля)
					И СтрНайти(ИменаПолейНеподлежащихОбновлению, СтрокиВторогоУровня.ИмяПоля) > 0 Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				Если СтрокиВторогоУровня.НомерКолонки <> 0 Тогда
					
					ДобавитьИмяПоляВСтрокуОписанияСвойств(ИменаПолейОбновляемые, СтрокиВторогоУровня.ИмяПоля);
					
				ИначеЕсли НЕ ПустаяСтрока(СтрокиВторогоУровня.ИмяПоля) Тогда
					
					ДобавитьИмяПоляВСтрокуОписанияСвойств(ИменаПолейНеподлежащихОбновлению, СтрокиВторогоУровня.ИмяПоля);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	НастройкиОбновленияСвойств.ИменаПолейНеподлежащихОбновлению = ИменаПолейНеподлежащихОбновлению;
	НастройкиОбновленияСвойств.ИменаПолейОбновляемые = ИменаПолейОбновляемые;
	
КонецПроцедуры

Процедура СформироватьНастройкиОбновленияСвойств(ДеревоПолей, НастройкиЗагрузкиДанных) ЭКспорт
	
	НастройкиОбновленияСвойств = Новый Структура("ИменаПолейОбновляемые, ИменаПолейНеподлежащихОбновлению", "", "");
	ЗаполнитьСтрокиИменПолей(ДеревоПолей, НастройкиЗагрузкиДанных, НастройкиОбновленияСвойств);
	
	НастройкиЗагрузкиДанных.Вставить("НастройкиОбновленияСвойств", НастройкиОбновленияСвойств);
	
КонецПроцедуры

Функция ЧислоИПредметИсчисления(
		Число,
		ПараметрыПредметаИсчисления1,
		ПараметрыПредметаИсчисления2,
		ПараметрыПредметаИсчисления3,
		Род) Экспорт
	
	ПараметрыПредметаИсчисления = "%1,%2,%3,%4,,,,,0";
	ПараметрыПредметаИсчисления = СтрШаблон(
		ПараметрыПредметаИсчисления,
		ПараметрыПредметаИсчисления1,
		ПараметрыПредметаИсчисления2,
		ПараметрыПредметаИсчисления3,
		Род);
		
	ЧислоСтрокойИПредметИсчисления = НРег(ЧислоПрописью(Число, , ПараметрыПредметаИсчисления));
	
	ЧислоПрописью = ЧислоСтрокойИПредметИсчисления;
	ЧислоПрописью = СтрЗаменить(ЧислоПрописью, ПараметрыПредметаИсчисления3, "");
	ЧислоПрописью = СтрЗаменить(ЧислоПрописью, ПараметрыПредметаИсчисления2, "");
	ЧислоПрописью = СтрЗаменить(ЧислоПрописью, ПараметрыПредметаИсчисления1, "");
	
	ЧислоЦифройИПредметИсчисления = СтрЗаменить(ЧислоСтрокойИПредметИсчисления, ЧислоПрописью, "");
	
	Возврат ЧислоЦифройИПредметИсчисления;
	
КонецФункции

// ЗагрузкаДанных

Процедура ЗагрузитьДанные(ПараметрыВызоваСервера, АдресВременногоХранилища) Экспорт
	
	МаксимумПолезныхКолонок	= ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.МаксимумПолезныхКолонокТабличногоДокумента();
	ИмяВременногоФайла 		= ПараметрыВызоваСервера.ИмяВременногоФайла;
	Расширение 				= ПараметрыВызоваСервера.Расширение;
	ТабличныйДокумент 		= ПараметрыВызоваСервера.ТабличныйДокумент;
	НастройкиЗагрузкиДанных	= ПараметрыВызоваСервера.НастройкиЗагрузкиДанных;
	
	Если Расширение = "csv" Тогда 
		
		ЗагрузитьCSVФайлВТабличныйДокумент(ИмяВременногоФайла, ТабличныйДокумент);
		КоличествоКолонок = ТабличныйДокумент.ШиринаТаблицы;
		
	Иначе
		
		ИсходныйТабличныйДокумент = Новый ТабличныйДокумент;
		ИсходныйТабличныйДокумент.Прочитать(ИмяВременногоФайла);
		
		Если ПараметрыВызоваСервера.НастройкиЗагрузкиДанных.ФиксированныйШаблон Тогда
			// удалить первые 6 строк с Инструкцией по заполнению
			ИсходныйТабличныйДокумент.УдалитьОбласть(ИсходныйТабличныйДокумент.Область("R1:R6"), ТипСмещенияТабличногоДокумента.ПоВертикали);
		КонецЕсли;

		ОптимизироватьТабличныйДокумент(ИсходныйТабличныйДокумент, ТабличныйДокумент);
		
		КоличествоКолонок = Мин(ТабличныйДокумент.ШиринаТаблицы, МаксимумПолезныхКолонок);
		
	КонецЕсли;
	
	ЗаполнитьРасшифровкиВТабличномДокументе(ТабличныйДокумент, КоличествоКолонок, НастройкиЗагрузкиДанных);
	
	УдалитьФайлы(ИмяВременногоФайла);
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(ТабличныйДокумент, АдресВременногоХранилища);
	
КонецПроцедуры

// Взято из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов
//
// Используется своя процедура из-за ошибки разбора строки:
// Работы и услуги;;Проектирование систем кондиционирования и вентиляции;
// где второй и четвертый параметр будут пропущены, вместо заполнения пустым значением.
//
Функция РазложитьСтрокуВМассивПодстрок(Знач Строка, Разделитель) Экспорт
	
	Подстроки = Новый Массив;
	
	РазмерТекста = СтрДлина(Строка);
	НачалоПодстроки = 1;
	Для Позиция = 1 По РазмерТекста Цикл
		
		КодСимвола = КодСимвола(Строка, Позиция);
		Если СтроковыеФункцииКлиентСервер.ЭтоРазделительСлов(КодСимвола, Разделитель) Тогда
			
			Если Позиция <> НачалоПодстроки Тогда
				
				Подстроки.Добавить(Сред(Строка, НачалоПодстроки, Позиция - НачалоПодстроки));
				
			ИначеЕсли Позиция = НачалоПодстроки Тогда
				
				Подстроки.Добавить("");
				
			КонецЕсли;
			
			НачалоПодстроки = Позиция + 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Позиция <> НачалоПодстроки Тогда
		
		Подстроки.Добавить(Сред(Строка, НачалоПодстроки, Позиция - НачалоПодстроки));
		
	ИначеЕсли Позиция = НачалоПодстроки Тогда
		
		Подстроки.Добавить("");
		
	КонецЕсли;
	
	Возврат Подстроки;
	
КонецФункции

Процедура ОптимизироватьТабличныйДокумент(ИсходныйТабличныйДокумент, ТабличныйДокумент)
	
	МаксимумПолезныхКолонок = МИН(ИсходныйТабличныйДокумент.ШиринаТаблицы, ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.МаксимумПолезныхКолонокТабличногоДокумента());
	Пока Истина Цикл
		
		ЕстьЗаполненныеЯчейки = Ложь;
		СчетчикЯчеек = 0;
		
		Для СчетчикЯчеек = 1 По 12 Цикл
			
			Область = ИсходныйТабличныйДокумент.Область(СчетчикЯчеек, МаксимумПолезныхКолонок); // проверяем ячейку в последней колонке
			Если НЕ ПустаяСтрока(Область.Текст) Тогда
				
				ЕстьЗаполненныеЯчейки = Истина;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЕстьЗаполненныеЯчейки 
			ИЛИ МаксимумПолезныхКолонок < 7 Тогда
			
			Прервать;
			
		Иначе
			
			МаксимумПолезныхКолонок = МаксимумПолезныхКолонок - 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТабличныйДокумент.Вывести(ИсходныйТабличныйДокумент.ПолучитьОбласть(1, 1, ИсходныйТабличныйДокумент.ВысотаТаблицы, МаксимумПолезныхКолонок));
	
КонецПроцедуры

Процедура ЗагрузитьCSVФайлВТабличныйДокумент(ИмяВременногоФайла, ТабличныйДокумент)
	
	Файл = Новый Файл(ИмяВременногоФайла);
	Если НЕ Файл.Существует() Тогда 
		
		Возврат;
		
	КонецЕсли;
	
	Макет = Обработки.ЗагрузкаДанныхИзВнешнегоИсточника.ПолучитьМакет("ПростойШаблон");
	ОбластьЗначение = Макет.ПолучитьОбласть("Значение");
	
	ЧтениеТекста = Новый ЧтениеТекста(ИмяВременногоФайла, КодировкаТекста.ANSI);
	Строка = ЧтениеТекста.ПрочитатьСтроку();
	Пока Строка <> Неопределено Цикл
		
		ПерваяКолонка = Истина;
		МассивПодстрок = РазложитьСтрокуВМассивПодстрок(Строка, ";");
		Для каждого Подстрока Из МассивПодстрок Цикл
			
			ОбластьЗначение.Параметры.Значение = Строка(Подстрока);
			
			Если ПерваяКолонка Тогда
				
				ТабличныйДокумент.Вывести(ОбластьЗначение);
				ПерваяКолонка = Ложь;
				
			Иначе
				
				ТабличныйДокумент.Присоединить(ОбластьЗначение);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Строка = ЧтениеТекста.ПрочитатьСтроку();
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПреобразоватьДанныеКТипуКолонки(Значение, ТипКолонки)
	
	Результат = Значение;
	
	Для каждого Тип Из ТипКолонки.Типы() Цикл
		
		Если Тип = Тип("Дата") Тогда
			
			Если СтрДлина(Значение) < 11 Тогда
				Значение = Сред(Значение, 7, 4) + Сред(Значение,4,2) + Лев(Значение, 2);
			Иначе
				Значение = Сред(Значение, 7, 4) + Сред(Значение,4,2) + Лев(Значение, 2); 
			КонецЕсли;
				
			ЦелевойТип = Новый ОписаниеТипов("Дата");
			Результат = ЦелевойТип.ПривестиЗначение(Значение);
			
		ИначеЕсли Тип = Тип("Число") Тогда
			ЦелевойТип = Новый ОписаниеТипов("Число");
			Результат = ЦелевойТип.ПривестиЗначение(Значение);
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

Процедура ДанныеИзТаблицыЗначенийВТабличныйДокумент(ДанныеИзФайла, ТабличныйДокумент, ПолноеИмяОбъектаЗаполнения) Экспорт 
	
	КоличествоКолонок = ДанныеИзФайла.Колонки.Количество();
	Если КоличествоКолонок < 1 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТабличныйДокумент.Очистить();
	
	Макет = Обработки.ЗагрузкаДанныхИзВнешнегоИсточника.ПолучитьМакет("ПростойШаблон");
	ОбластьЗначение = Макет.ПолучитьОбласть("Значение");
	
	Для ИндексСтроки = 0 По ДанныеИзФайла.Количество() - 1 Цикл
		
		СтрокаТЗ = ДанныеИзФайла.Получить(ИндексСтроки);
		Для ИндексКолонки = 0 По КоличествоКолонок - 1 Цикл
			
			ОбластьЗначение.Параметры.Значение = ПреобразоватьДанныеКТипуКолонки(СтрокаТЗ[ИндексКолонки], ДанныеИзФайла.Колонки.Получить(ИндексКолонки).ТипЗначения);
			
			Если ИндексКолонки = 0 Тогда
				
				ТабличныйДокумент.Вывести(ОбластьЗначение);
				
			Иначе
				
				ТабличныйДокумент.Присоединить(ОбластьЗначение);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ЗаполнитьРасшифровкиВТабличномДокументе(ТабличныйДокумент, КоличествоКолонок, ПолноеИмяОбъектаЗаполнения);
	
КонецПроцедуры

Процедура ЗаполнитьРасшифровкиВТабличномДокументе(ТабличныйДокумент, КоличествоКолонок, НастройкиЗагрузкиДанных) Экспорт
	Перем ТаблицаПолейЗагрузки, ДоступныеПоляЗагрузки;
	
	Если Число(КоличествоКолонок) < 1 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СоздатьТаблицуПолейОписанияЗагрузки(ТаблицаПолейЗагрузки);
	ЗаполнитьТаблицуПолейЗагрузки(ТаблицаПолейЗагрузки, НастройкиЗагрузкиДанных);
	
	Расшифровка = Новый СписокЗначений;
	Расшифровка.Добавить("Не загружать", "Не загружать");
	Для каждого СтрокаТаблицы Из ТаблицаПолейЗагрузки Цикл
		Расшифровка.Добавить(СтрокаТаблицы.ИмяПоля, СтрокаТаблицы.ПредставлениеПоля);
	КонецЦикла;
	
	ОбластьЗаголовок = Обработки.ЗагрузкаДанныхИзВнешнегоИсточника.ПолучитьМакет("ПростойШаблон").ПолучитьОбласть("Заголовок");
	
	ИсходнаяОбласть = ОбластьЗаголовок.Области.Заголовок;
	ИсходнаяОбласть.ПараметрРасшифровки = "";
	ИсходнаяОбласть.Расшифровка			= Расшифровка;
	
	ПараметрыАвтоПодбораЗаголовка = Новый Структура;
	ПараметрыАвтоПодбораЗаголовка.Вставить("ПолученныйЗаголовок", Неопределено);
	ПараметрыАвтоПодбораЗаголовка.Вставить("ВыбранныеПоля", Новый Массив);
	ПараметрыАвтоПодбораЗаголовка.Вставить("ОписаниеДополнительныхРеквизитов", Новый Соответствие);
	ПараметрыАвтоПодбораЗаголовка.Вставить("ВыбранныеДополнительныеРеквизиты", Новый Соответствие);
	
	ОписаниеОбъектаЗагрузки = ОбъектЗагрузкиПоПолномуИмени(НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения);
	ПараметрыАвтоПодбораЗаголовка.Вставить("ОбъектЗагрузки", ОписаниеОбъектаЗагрузки.ОбъектЗагрузки);
	ПараметрыАвтоПодбораЗаголовка.Вставить("ИмяТабличнойЧасти", ОписаниеОбъектаЗагрузки.ИмяТабличнойЧасти);
	
	Если НастройкиЗагрузкиДанных.Свойство("ОписаниеДополнительныхРеквизитов") Тогда
		
		ПараметрыАвтоПодбораЗаголовка.ОписаниеДополнительныхРеквизитов = НастройкиЗагрузкиДанных.ОписаниеДополнительныхРеквизитов;
		ПараметрыАвтоПодбораЗаголовка.ВыбранныеДополнительныеРеквизиты = НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты;
		
	КонецЕсли;
	
	Для НомерКолонки = 1 По КоличествоКолонок Цикл
		
		ОбластьПриемник = ТабличныйДокумент.Область(СтрШаблон("R1C%1", НомерКолонки));
		ИсходныйТекстЗаголовка = ОбластьПриемник.Текст;
		
		ИсходнаяОбласть.Примечание.Текст = ?(ПустаяСтрока(ОбластьПриемник.Текст), "", ОбластьПриемник.Текст); // Перенесем текст из ячейки в примечание
		ТабличныйДокумент.ВставитьОбласть(ИсходнаяОбласть, ОбластьПриемник, ТипСмещенияТабличногоДокумента.БезСмещения, Истина);
		Если НастройкиЗагрузкиДанных.ФиксированныйШаблон И НЕ НастройкиЗагрузкиДанных.СамостоятельноеЗаполнение Тогда
			ОбластьПриемник.Текст	= ИсходныйТекстЗаголовка;
		Иначе
			ОбластьПриемник.Текст	= НСтр("ru ='Не загружать'");
		КонецЕсли;
		
		ОбластьКолонка = ТабличныйДокумент.Область(СтрШаблон("C%1", НомерКолонки));
		ОбластьКолонка.ШиринаКолонки = 22.75;
		
		ПараметрыАвтоПодбораЗаголовка.ПолученныйЗаголовок = СокрЛП(ОбластьПриемник.Примечание.Текст);
		Если НЕ ПустаяСтрока(ПараметрыАвтоПодбораЗаголовка.ПолученныйЗаголовок) Тогда
			
			АвтоматическийПодборЗаголовкаТабличногоДокумента(ОбластьПриемник, ПараметрыАвтоПодбораЗаголовка);
			Если НЕ ПустаяСтрока(ОбластьПриемник.ПараметрРасшифровки) Тогда
				
				ПараметрыАвтоПодбораЗаголовка.ВыбранныеПоля.Добавить(ОбластьПриемник.ПараметрРасшифровки);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбластьПерваяСтрока = ТабличныйДокумент.Область(1, , 1, );
	ОбластьПерваяСтрока.ВысотаСтроки = 26;
	
КонецПроцедуры

Процедура АвтоматическийПодборЗаголовкаТабличногоДокумента(ОбластьПриемник, ПараметрыАвтоПодбораЗаголовка)
	
	ИскатьДалее = Истина;
	ПоискВСопоставленныхРаннееЗаголовках(ОбластьПриемник, ПараметрыАвтоПодбораЗаголовка, ИскатьДалее);
	
	Если ИскатьДалее
		И ПустаяСтрока(ОбластьПриемник.ПараметрРасшифровки) Тогда
		
		ПоискВСтандартныхИменахПолей(ОбластьПриемник, ПараметрыАвтоПодбораЗаголовка);
		
	КонецЕсли;
	
	Если ИскатьДалее
		И ПустаяСтрока(ОбластьПриемник.ПараметрРасшифровки) Тогда
		
		ПоискВДополнительныхРеквизитах(ОбластьПриемник, ПараметрыАвтоПодбораЗаголовка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоискВСопоставленныхРаннееЗаголовках(ОбластьПриемник, ПараметрыАвтоПодбораЗаголовка, ИскатьДалее)
	
	Если НЕ ЗначениеЗаполнено(ПараметрыАвтоПодбораЗаголовка.ОбъектЗагрузки) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбъектЗагрузки", ПараметрыАвтоПодбораЗаголовка.ОбъектЗагрузки);
	Запрос.УстановитьПараметр("ИмяТабличнойЧасти", ПараметрыАвтоПодбораЗаголовка.ИмяТабличнойЧасти);
	Запрос.УстановитьПараметр("ПолученныйЗаголовок", СокрЛП(ПараметрыАвтоПодбораЗаголовка.ПолученныйЗаголовок));
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СоответствиеНаименований.Поле КАК ПараметрРасшифровки,
	|	СоответствиеНаименований.ДополнительныйРеквизит КАК ДополнительныйРеквизит
	|ИЗ
	|	РегистрСведений.СоответствиеНаименованияКолонокПолямЗагрузки КАК СоответствиеНаименований
	|ГДЕ
	|	СоответствиеНаименований.ОбъектЗагрузки = &ОбъектЗагрузки
	|	И СоответствиеНаименований.ИмяТабличнойЧасти = &ИмяТабличнойЧасти
	|	И СоответствиеНаименований.ЗаголовокКолонки = &ПолученныйЗаголовок";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Если ПараметрыАвтоПодбораЗаголовка.ВыбранныеПоля.Найти(Выборка.ПараметрРасшифровки) <> Неопределено Тогда
			
			Возврат;
			
		КонецЕсли;
		
		ПараметрРасшифровкиДополнительногоРеквизита = "";
		Если ЗначениеЗаполнено(Выборка.ДополнительныйРеквизит) Тогда
			
			ПараметрРасшифровкиДополнительногоРеквизита = ПараметрыАвтоПодбораЗаголовка.ОписаниеДополнительныхРеквизитов.Получить(Выборка.ДополнительныйРеквизит);
			Если ПараметрыАвтоПодбораЗаголовка.ВыбранныеПоля.Найти(ПараметрРасшифровкиДополнительногоРеквизита) <> Неопределено Тогда
				
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ИскатьДалее = Ложь;
		
		Если НЕ ПустаяСтрока(Выборка.ПараметрРасшифровки) Тогда
			
			ОбластьПриемник.ПараметрРасшифровки = Выборка.ПараметрРасшифровки;
			Для каждого ЭлементСписка Из ОбластьПриемник.Расшифровка Цикл
				
				Если ВРЕГ(СокрЛП(ЭлементСписка.Значение)) = ВРЕГ(ОбластьПриемник.ПараметрРасшифровки) Тогда
					
					ОбластьПриемник.Текст = ЭлементСписка.Представление;
					Возврат;
					
				КонецЕсли;
				
			КонецЦикла;
			
		ИначеЕсли ЗначениеЗаполнено(ПараметрРасшифровкиДополнительногоРеквизита) Тогда
			
			//ОбластьПриемник.ПараметрРасшифровки = ПараметрыАвтоПодбораЗаголовка.ОписаниеДополнительныхРеквизитов.Получить(Выборка.ДополнительныйРеквизит);
			ОбластьПриемник.ПараметрРасшифровки = ПараметрРасшифровкиДополнительногоРеквизита;
			ОбластьПриемник.Текст = Выборка.ДополнительныйРеквизит.Наименование;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоискВСтандартныхИменахПолей(ОбластьПриемник, ПараметрыАвтоПодбораЗаголовка)
	
	Для каждого ЭлементСписка Из ОбластьПриемник.Расшифровка Цикл
		
		Если ПараметрыАвтоПодбораЗаголовка.ВыбранныеПоля.Найти(ЭлементСписка.Значение) <> Неопределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если ВРЕГ(СокрЛП(ЭлементСписка.Значение)) = ВРЕГ(ПараметрыАвтоПодбораЗаголовка.ПолученныйЗаголовок)
			ИЛИ СтрНайти(ВРЕГ(СокрЛП(ЭлементСписка.Представление)), ВРЕГ(ПараметрыАвтоПодбораЗаголовка.ПолученныйЗаголовок)) > 0
			Тогда
			
			ОбластьПриемник.Текст = ЭлементСписка.Представление;
			ОбластьПриемник.ПараметрРасшифровки = ЭлементСписка.Значение;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПоискВДополнительныхРеквизитах(ОбластьПриемник, ПараметрыАвтоПодбораЗаголовка)
	
	Для каждого ЭлементСоответствия Из ПараметрыАвтоПодбораЗаголовка.ОписаниеДополнительныхРеквизитов Цикл
		
		Если ПараметрыАвтоПодбораЗаголовка.ВыбранныеПоля.Найти(ЭлементСоответствия.Значение) <> Неопределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если ВРЕГ(СокрЛП(ЭлементСоответствия.Ключ.Имя)) = ВРЕГ(ПараметрыАвтоПодбораЗаголовка.ПолученныйЗаголовок)
			ИЛИ ВРЕГ(СокрЛП(ЭлементСоответствия.Ключ.Наименование)) = ВРЕГ(ПараметрыАвтоПодбораЗаголовка.ПолученныйЗаголовок)
			ИЛИ ВРЕГ(СокрЛП(ЭлементСоответствия.Ключ.Заголовок)) = ВРЕГ(ПараметрыАвтоПодбораЗаголовка.ПолученныйЗаголовок)
			Тогда
			
			ОбластьПриемник.Текст = СокрЛП(ЭлементСоответствия.Ключ.Наименование);
			ОбластьПриемник.ПараметрРасшифровки = ЭлементСоответствия.Значение;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция УдалитьНесуществующиеСвойства(Источник, Приемник) Экспорт
	
	МассивУдалить = Новый Массив;
	Для каждого ИмяПоля Из Источник Цикл
		Если Приемник.Найти(ИмяПоля) = Неопределено Тогда
			МассивУдалить.Добавить(ИмяПоля);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого элементУдалить Из МассивУдалить Цикл
		Источник.Удалить(Источник.Найти(элементУдалить));
	КонецЦикла; 
	
	Возврат СтрСоединить(Источник, ",");
	
КонецФункции

#КонецОбласти