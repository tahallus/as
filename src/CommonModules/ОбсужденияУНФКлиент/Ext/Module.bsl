
#Область ПрограммныйИнтерфейс

// Возвращает текст HTML для навигационной ссылки для переданной ссылки
// Параметры:
//  Ссылка				Любая ссылка - Результат вопроса диалога записи в форме
Функция HTMLСсылка(Ссылка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат ПолучитьНавигационнуюСсылку(Ссылка);
	
КонецФункции

// Добавляет новое системное сообщение контекстного обсуждения о произвольном событии.
// Например, при изменении данных или прикреплении нового файла.
// 
// Параметры:
//  ТекстСообщения - Строка - текст сообщения
//  Ссылка - ЛюбаяСсылка - ссылка на объект.
// 
Процедура ДобавитьСистемноеСообщение(ТекстСообщения, Ссылка) Экспорт
	
	ОбсужденияУНФВызовСервера.ДобавитьСообщение(ТекстСообщения, Ссылка, Истина);
	
КонецПроцедуры

#Область ОбработчикиСобытий

// Обработка оповещения события добавления комментария в обсуждения объекта
// для обновления списка комментариев в форме
// Параметры:
//  ИмяСобытия				Строка - ожидается имя "Обсуждение_ДобавлениеКомментария"
//	Параметр				Параметр сообщения
//	Источник				Ссылка объекта - источника оповещения
//	Форма					Управляемая форма для обновления данных
//	Ссылка					Ссылка объекта формы для обновления данных
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник, Форма, Ссылка) Экспорт
	
	ДобавитьСообщениеОЗаписиПрисоединенногоФайла(ИмяСобытия, Параметр, Источник, Форма, Ссылка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьСообщениеОЗаписиПрисоединенногоФайла(ИмяСобытия, Параметр, Источник, Форма, Ссылка)
	
	Если ИмяСобытия <> "Запись_Файл" Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметр.Свойство("ВладелецФайла") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметр.ВладелецФайла <> Ссылка Тогда
		Возврат;
	КонецЕсли;
	
	// Для присоединенных файлов не сохраняем ссылки на файлы, потому что форма, которая по ним открывается, не
	// предназначена для использования
	Если ТипЗнч(Источник) = Тип("Массив") Тогда
		ИсточникСтрокой = "";
		Для каждого стр Из Источник Цикл
			ИсточникСтрокой = ИсточникСтрокой + Строка(стр) + " ";
		КонецЦикла;
	Иначе
		ИсточникСтрокой = Строка(Источник);
	КонецЕсли;
	
	Если Параметр.Свойство("ЭтоНовый") И Параметр.ЭтоНовый = Истина Тогда
		Комментарий = НСтр("ru = 'Присоединен файл %1'");
		Комментарий = СтрШаблон(Комментарий, ИсточникСтрокой);
	Иначе
		Комментарий = НСтр("ru = 'Записан файл %1'");
		Комментарий = СтрШаблон(Комментарий, ИсточникСтрокой);
	КонецЕсли;
	
	ОбсужденияУНФВызовСервера.ДобавитьСообщение(Комментарий, Ссылка, Истина);
	
КонецПроцедуры

#КонецОбласти
