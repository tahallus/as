
#Область ПрограммныйИнтерфейс

// См. ТекущиеДелаПереопределяемый.ПриОпределенииОбработчиковТекущихДел
// 
// Параметры:
//   ТекущиеДела - см. ТекущиеДелаСлужебный.ТекущиеДела
//
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	ГруппаДел = НСтр("ru = 'Напоминания'");
	ИмяФормы = "РегистрСведений.НапоминанияПользователя.Форма.МоиНапоминания";
	
	ОбщиеПараметрыЗапросов = ТекущиеДелаСервер.ОбщиеПараметрыЗапросов();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНапоминанияПользователя")
		Или Не ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.НапоминанияПользователя)
		Или ТекущиеДелаСервер.ДелоОтключено(ГруппаДел) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(*) КАК МоиНапоминанияВсегоНапоминаний
	|ИЗ
	|	РегистрСведений.НапоминанияПользователя КАК РегистрСведенийНапоминанияПользователя
	|ГДЕ
	|	РегистрСведенийНапоминанияПользователя.Пользователь = &Пользователь";
	
	ДанныеДел = ТекущиеДелаСервер.ЧисловыеПоказателиТекущихДел(Запрос, ОбщиеПараметрыЗапросов);
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "МоиНапоминанияВсегоНапоминаний";
	Дело.ЕстьДела       = ДанныеДел.МоиНапоминанияВсегоНапоминаний > 0;
	Дело.Важное         = Ложь;
	Дело.Представление  = НСтр("ru = 'Мои напоминания'");
	Дело.Количество     = ДанныеДел.МоиНапоминанияВсегоНапоминаний;
	Дело.Форма          = ИмяФормы;
	Дело.ПараметрыФормы = Новый Структура;
	Дело.Владелец       = ГруппаДел;
	Дело.Подсказка      = "";
	
КонецПроцедуры

#Область ИнтерфейсРабочегоПроцесса

// Определяет доступные поля для настройки правила рабочего процесса.
// 
// Параметры:
// 	ТаблицаОписанияПолей - ТаблицаЗначений:
// 	 * ИмяРеквизита           - Строка
// 	 * Заголовок              - Строка
// 	 * ВариантЗаполнения      - Строка
// 	 * Значение               - Произвольный
// 	 * СписокПолейОснования   - СписокЗначений
// 	 * ТипЗначения            - ОписаниеТипов
// 	 * ОбязательноеЗаполнение - Булево 
// 	ТипДействия - ПеречислениеСсылка.ТипыДействийРабочегоПроцесса
//
Процедура СоставПолейЗаполненияДляРабочегоПроцесса(ТаблицаОписанияПолей, Знач ТипДействия) Экспорт
	
	НовоеОписание = ТаблицаОписанияПолей.Добавить();
	НовоеОписание.ИмяРеквизита				= "Пользователь";
	НовоеОписание.Заголовок					= НСтр("ru='Кому напомнить'");
	НовоеОписание.ВариантЗаполнения			= "ИзОснования";
	НовоеОписание.ТипЗначения				= Метаданные.РегистрыСведений.НапоминанияПользователя.Измерения.Пользователь.Тип;
	НовоеОписание.ОбязательноеЗаполнение	= Истина;
	
	НовоеОписание = ТаблицаОписанияПолей.Добавить();
	НовоеОписание.ИмяРеквизита				= "ВремяСобытия";
	НовоеОписание.Заголовок					= НСтр("ru='Дата напоминания'");
	НовоеОписание.ВариантЗаполнения			= "Смещение";
	НовоеОписание.ТипЗначения				= ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя);
	НовоеОписание.ОбязательноеЗаполнение	= Истина;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти