#Область СлужебныйПрограммныйИнтерфейс

Функция ВидыПродукцииВЗапасах(Знач Запасы) Экспорт
	
	Возврат ИнтеграцияИСУНФ.ВидыПродукцииВЗапасах(Запасы);
	
КонецФункции

Функция ЗаполнитьРеквизитВидПродукцииИС() Экспорт
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Справочник.Номенклатура");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Номенклатура.ТабачнаяПродукция КАК ТабачнаяПродукция,
		|	Номенклатура.ОбувнаяПродукция КАК ОбувнаяПродукция,
		|	Номенклатура.ШиныИПокрышки КАК ШиныИПокрышки,
		|	Номенклатура.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	(Номенклатура.ТабачнаяПродукция
		|			ИЛИ Номенклатура.ОбувнаяПродукция
		|			ИЛИ Номенклатура.ШиныИПокрышки)
		|	И Номенклатура.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		
		Если РезультатЗапроса.Пустой() Тогда
			Константы.ЗаполнениеРеквизитаВидПродукцииИСВыполнено.Установить(Истина);
		Иначе
			Выборка = РезультатЗапроса.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
				Если Выборка.ТабачнаяПродукция Тогда
					СправочникОбъект.ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Табак;
				ИначеЕсли Выборка.ОбувнаяПродукция Тогда
					СправочникОбъект.ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Обувь;
				ИначеЕсли Выборка.ШиныИПокрышки Тогда
					СправочникОбъект.ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Шины;
					// отключаем механизм маркировки шин и покрышек в УНФ
					СправочникОбъект.ШиныИПокрышки = Ложь;
				КонецЕсли;
				
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
			КонецЦикла;
			
		КонецЕсли;
		
		Константы.ЗаполнениеРеквизитаВидПродукцииИСВыполнено.Установить(Истина);
		Результат = Истина;
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		Результат = Ложь;
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция НеобходимоЗавершитьЗаполнениеРеквизитаВидПродукцииИС() Экспорт
	
	ВедетсяУчетМаркируемойПродукции = ИнтеграцияИСМПВызовСервера.ВестиУчетМаркируемойПродукции();
	ЗаполнениеРеквизитаВидПродукцииИСВыполнено = Константы.ЗаполнениеРеквизитаВидПродукцииИСВыполнено.Получить();
	
	Возврат ВедетсяУчетМаркируемойПродукции И НЕ ЗаполнениеРеквизитаВидПродукцииИСВыполнено;
	
КонецФункции

#КонецОбласти