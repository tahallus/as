#Область ПрограммныйИнтерфейс

// Обрабатывает событие активизации строки списка документов.
//
Процедура ИнформационнаяПанельОбработатьАктивизациюСтрокиСписка(Форма, ПараметрыИнформПанели) Экспорт
	
	ТекущиеДанныеСписка = Форма.Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанныеСписка <> Неопределено
		И ТекущиеДанныеСписка.Свойство(ПараметрыИнформПанели.РеквизитКИ) Тогда
		
		ТекущийРеквизитКИ = ТекущиеДанныеСписка[ПараметрыИнформПанели.РеквизитКИ];
		
		Если Форма.ИнформацияСсылка <> ТекущийРеквизитКИ Тогда
			
			Если ЗначениеЗаполнено(ТекущийРеквизитКИ) Тогда
				
				ДанныеИП = ИнформационнаяПанельУНФСервер.ИнформационнаяПанельПолучитьДанные(ТекущийРеквизитКИ, ПараметрыИнформПанели);
				ИнформационнаяПанельЗаполнить(Форма, ПараметрыИнформПанели, ДанныеИП);
				
				Форма.ИнформацияСсылка = ТекущийРеквизитКИ;
				
			Иначе
				
				ИнформационнаяПанельЗаполнить(Форма, ПараметрыИнформПанели);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		ИнформационнаяПанельЗаполнить(Форма, ПараметрыИнформПанели);
		
	КонецЕсли;
	
КонецПроцедуры // ИнформационнаяПанельОбработатьАктивизациюСтрокиСписка()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура заполняет данные информационной панели списка.
//
Процедура ИнформационнаяПанельЗаполнить(Форма, ПараметрыИнформПанели, ДанныеИП = Неопределено)
	
	Если ДанныеИП = Неопределено Тогда
	
		Форма.ИнформацияСсылка = Неопределено;
		
		// Контактная информация контрагентов.
		Если ПараметрыИнформПанели.Свойство("Контрагент") Тогда
			
			Форма.ИнформацияКонтрагентТелефон = "";
			Форма.ИнформацияКонтрагентЭП = "";
			Форма.ИнформацияКонтрагентФакс = "";
			
			Форма.ИнформацияКонтрагентФактАдрес = "";
			Если Форма.Элементы.Найти("ИнформацияКонтрагентАдресДоставки") <> Неопределено
				ИЛИ Форма.Элементы.Найти("РасшифровкаСпискаКонтрагентАдресДоставки") <> Неопределено Тогда
				
				Форма.ИнформацияКонтрагентАдресДоставки = "";
				
			КонецЕсли;
			Форма.ИнформацияКонтрагентЮрАдрес = "";
			
			Форма.ИнформацияКонтрагентПочтовыйАдрес = "";
			Форма.ИнформацияКонтрагентДругаяИнформация = "";
			
			// Взаиморасчеты.
			Если ПараметрыИнформПанели.Свойство("Взаиморасчеты") Тогда
				
				Форма.ИнформацияДолгКонтрагента = 0;
				Форма.ИнформацияНашДолг = 0;
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Контактная информация контактных лиц.
		Если ПараметрыИнформПанели.Свойство("КонтактноеЛицо") Тогда
			
			Форма.ИнформацияКонтактноеЛицоТелефон = "";
			Форма.ИнформацияКонтактноеЛицоЭП = "";
			
		КонецЕсли;
		
	Иначе
		
		// Контактная информация контрагентов.
		Если ПараметрыИнформПанели.Свойство("Контрагент") Тогда
			
			Форма.ИнформацияКонтрагентТелефон 	= ДанныеИП.Телефон;
			Форма.ИнформацияКонтрагентЭП 		= ДанныеИП.ЭлПочта;
			Форма.ИнформацияКонтрагентФакс 		= ДанныеИП.Факс;
			
			Форма.ИнформацияКонтрагентФактАдрес = ДанныеИП.ФактАдрес;
			Если Форма.Элементы.Найти("ИнформацияКонтрагентАдресДоставки") <> Неопределено
				ИЛИ Форма.Элементы.Найти("РасшифровкаСпискаКонтрагентАдресДоставки") <> Неопределено Тогда
				
				Форма.ИнформацияКонтрагентАдресДоставки = ДанныеИП.АдресДоставки;
				
			КонецЕсли;
			Форма.ИнформацияКонтрагентЮрАдрес 	= ДанныеИП.ЮрАдрес;
			
			Форма.ИнформацияКонтрагентПочтовыйАдрес 	= ДанныеИП.ПочтовыйАдрес;
			Форма.ИнформацияКонтрагентДругаяИнформация 	= ДанныеИП.ДругаяИнформация;
			
			// Взаиморасчеты.
			Если ПараметрыИнформПанели.Свойство("Взаиморасчеты") Тогда
				
				Форма.ИнформацияДолгКонтрагента = ДанныеИП.Долг;
				Форма.ИнформацияНашДолг 		= ДанныеИП.НашДолг;
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Контактная информация контактных лиц.
		Если ПараметрыИнформПанели.Свойство("КонтактноеЛицо") Тогда
			
			Форма.ИнформацияКонтактноеЛицоТелефон 	= ДанныеИП.ТелефонКЛ;
			Форма.ИнформацияКонтактноеЛицоЭП 		= ДанныеИП.ЭлПочтаКЛ;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ИнформационнаяПанельЗаполнить()

#КонецОбласти