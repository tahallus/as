
#Область ПрограммныйИнтерфейс

Функция Используется() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользованиеЗадачАссистентаУправления");
	
КонецФункции

Процедура ПослеЗаписиНаСервере(ТекущийОбъект) Экспорт
	
	ВыполнитьТекущиеЗадачиСейчасВФоне(ТекущийОбъект.Ссылка);
	
КонецПроцедуры

// Запускает выполнение запланированных задач в текущем сеансе без ожидания очереди.
//
// Параметры:
//  Предмет - ОпределяемыйТип.АссистентУправленияСсылкаПредметЗадачи,
//            Массив из ОпределяемыйТип.АссистентУправленияСсылкаПредметЗадачи - Один или несколько
//                                  выбранных предметов по которым необходимо выполнить все текущие задачи.
//
Процедура ВыполнитьТекущиеЗадачиСейчас(Предмет = Неопределено) Экспорт
	
	Если Предмет = Неопределено Тогда
		ВыбранныеПредметы = Неопределено;
	ИначеЕсли ТипЗнч(Предмет) = Тип("Массив") Тогда
		ВыбранныеПредметы = Предмет;
	Иначе
		ВыбранныеПредметы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Предмет);
	КонецЕсли;
	
	ВыполнитьОчередьТекущихЗадач(ВыбранныеПредметы);
	ОбсужденияУНФ.ЗаписатьОтложенныеСообщенияВСистемуВзаимодействияСейчас();
	
КонецПроцедуры

// Запускает выполнение запланированных задач в фоновом задании без ожидания очереди.
//
// Параметры:
//  Предмет - ОпределяемыйТип.АссистентУправленияСсылкаПредметЗадачи,
//            Массив из ОпределяемыйТип.АссистентУправленияСсылкаПредметЗадачи - Один или несколько
//                                  выбранных предметов по которым необходимо выполнить все текущие задачи.
//
Процедура ВыполнитьТекущиеЗадачиСейчасВФоне(Предмет = Неопределено) Экспорт
	
	Если Не Используется() Тогда
		Возврат;
	КонецЕсли;
	
	Если Предмет = Неопределено Тогда
		ВыбранныеПредметы = Неопределено;
	ИначеЕсли ТипЗнч(Предмет) = Тип("Массив") Тогда
		ВыбранныеПредметы = Предмет;
	Иначе
		ВыбранныеПредметы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Предмет);
	КонецЕсли;
	
	Если ТекущиеЗадачиКВыполнению(ВыбранныеПредметы).Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПроцедуры = "АссистентУправления.ВыполнитьТекущиеЗадачиСейчас";
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.КлючФоновогоЗадания = КлючРегламентногоЗаданияВыполненияТекущихЗадач();
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяПроцедуры, Предмет);
	
КонецПроцедуры

// Запускает выполнение регулярных задач в текущем сеансе без ожидания очереди.
//
Процедура ВыполнитьРегулярныеЗадачиСейчас() Экспорт
	
	ВыполнитьОчередьРегулярныхЗадач();
	ОбсужденияУНФ.ЗаписатьОтложенныеСообщенияВСистемуВзаимодействияСейчас();
	
КонецПроцедуры

// Запускает выполнение регулярных задач в фоновом задании без ожидания очереди.
//
Процедура ВыполнитьРегулярныеЗадачиСейчасВФоне() Экспорт
	
	Если Не Используется() Тогда
		Возврат;
	КонецЕсли;
	
	Если РегулярныеЗадачиКВыполнению().Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПроцедуры = "АссистентУправления.ВыполнитьРегулярныеЗадачиСейчас";
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.КлючФоновогоЗадания = КлючРегламентногоЗаданияВыполненияРегулярныхЗадач();
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяПроцедуры);
	
КонецПроцедуры

// Добавляет новые задачи в очередь задач для регулярного (ежедневно, по расписанию) выполнения.
//
// Параметры:
//  ЗадачиКВыполнению - См. АссистентУправления.НовыйТаблицаТекущихЗадачКВыполнению
//
Процедура ЗапланироватьВыполнениеРегулярныхЗадач(ЗадачиКВыполнению) Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.ОчередьРегулярныхЗадачАссистентаУправления;
	
	ОжидаемыеТипыПараметров = Новый Соответствие;
	ОжидаемыеТипыПараметров.Вставить("Предмет", МетаданныеРегистра.Измерения.Предмет.Тип);
	ОжидаемыеТипыПараметров.Вставить("Задача", МетаданныеРегистра.Измерения.Задача.Тип);
	ОжидаемыеТипыПараметров.Вставить("Источник", МетаданныеРегистра.Ресурсы.Источник.Тип);
	
	ИмяПроцедуры = "АссистентУправления.ЗапланироватьВыполнениеРегулярныхЗадач";
	ПроверитьТипыПараметровКоллекции(ИмяПроцедуры, ЗадачиКВыполнению, ОжидаемыеТипыПараметров);
	
	Для каждого ОписаниеЗадачи Из ЗадачиКВыполнению Цикл
		Запись = РегистрыСведений.ОчередьРегулярныхЗадачАссистентаУправления.СоздатьМенеджерЗаписи();
		Запись.Период   = ОписаниеЗадачи.Дата;
		Запись.Предмет  = ОписаниеЗадачи.Предмет;
		Запись.Задача   = ОписаниеЗадачи.Задача;
		Запись.Источник = ОписаниеЗадачи.Источник;
		Запись.НеВыполнятьПосле = ОписаниеЗадачи.НеВыполнятьПосле;
		Если ОписаниеЗадачи.ДополнительныеПараметры <> Неопределено Тогда
			Запись.ДополнительныеПараметры = Новый ХранилищеЗначения(ОписаниеЗадачи.ДополнительныеПараметры);
		КонецЕсли;
		Запись.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Добавляет новые задачи в очередь задач для скорейшего выполнения.
//
// Параметры:
//  ЗадачиКВыполнению - См. АссистентУправления.НовыйТаблицаТекущихЗадачКВыполнению
//
Процедура ДобавитьТекущиеЗадачиКВыполнению(ЗадачиКВыполнению) Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.ОчередьЗадачАссистентаУправления;
	
	ОжидаемыеТипыПараметров = Новый Соответствие;
	ОжидаемыеТипыПараметров.Вставить("Предмет", МетаданныеРегистра.Измерения.Предмет.Тип);
	ОжидаемыеТипыПараметров.Вставить("Задача", МетаданныеРегистра.Измерения.Задача.Тип);
	ОжидаемыеТипыПараметров.Вставить("Источник", МетаданныеРегистра.Измерения.Источник.Тип);
	
	ИмяПроцедуры = "АссистентУправления.ДобавитьТекущиеЗадачиКВыполнению";
	ПроверитьТипыПараметровКоллекции(ИмяПроцедуры, ЗадачиКВыполнению, ОжидаемыеТипыПараметров);
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	Для каждого ОписаниеЗадачи Из ЗадачиКВыполнению Цикл
		Запись = РегистрыСведений.ОчередьЗадачАссистентаУправления.СоздатьМенеджерЗаписи();
		Запись.Предмет  = ОписаниеЗадачи.Предмет;
		Запись.Задача   = ОписаниеЗадачи.Задача;
		Запись.Событие = ОписаниеЗадачи.Событие;
		Запись.Источник = ОписаниеЗадачи.Источник;
		Если ОписаниеЗадачи.ДополнительныеПараметры <> Неопределено Тогда
			Запись.ДополнительныеПараметры = Новый ХранилищеЗначения(ОписаниеЗадачи.ДополнительныеПараметры);
		КонецЕсли;
		Запись.Дата = ТекущаяДатаСеанса;
		Запись.Записать();
	КонецЦикла;
	
	Если ЗадачиКВыполнению.Количество() <> 0 Тогда
		ОбновитьРегламентноеЗаданиеВыполнениеТекущихЗадач(Истина);
	КонецЕсли;
	
КонецПроцедуры

// Конструктор Параметра ЗадачиКВыполнению процедуры ЗапланироватьВыполнениеРегулярныхЗадач.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Коллекция новых задач ассистента для выполнения:
//
Функция НовыйТаблицаРегулярныхЗадачКВыполнению() Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.ОчередьРегулярныхЗадачАссистентаУправления;
	
	НовыйТаблицаЗадач = Новый ТаблицаЗначений;
	НовыйТаблицаЗадач.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	НовыйТаблицаЗадач.Колонки.Добавить("Задача", МетаданныеРегистра.Измерения.Задача.Тип);
	НовыйТаблицаЗадач.Колонки.Добавить("Предмет", МетаданныеРегистра.Измерения.Предмет.Тип);
	НовыйТаблицаЗадач.Колонки.Добавить("Источник", МетаданныеРегистра.Ресурсы.Источник.Тип);
	НовыйТаблицаЗадач.Колонки.Добавить("НеВыполнятьПосле", МетаданныеРегистра.Ресурсы.НеВыполнятьПосле.Тип);
	НовыйТаблицаЗадач.Колонки.Добавить("ДополнительныеПараметры");
	
	Возврат НовыйТаблицаЗадач;
	
КонецФункции

// Конструктор Параметра ЗадачиКВыполнению процедуры ДобавитьТекущиеЗадачиКВыполнению.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Коллекция новых задач ассистента для выполнения:
//
Функция НовыйТаблицаТекущихЗадачКВыполнению() Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.ОчередьЗадачАссистентаУправления;
	
	НовыйТаблицаЗадач = Новый ТаблицаЗначений;
	НовыйТаблицаЗадач.Колонки.Добавить("Предмет", МетаданныеРегистра.Измерения.Предмет.Тип);
	НовыйТаблицаЗадач.Колонки.Добавить("Задача", МетаданныеРегистра.Измерения.Задача.Тип);
	НовыйТаблицаЗадач.Колонки.Добавить("Событие", МетаданныеРегистра.Измерения.Событие.Тип);
	НовыйТаблицаЗадач.Колонки.Добавить("Источник", МетаданныеРегистра.Измерения.Источник.Тип);
	НовыйТаблицаЗадач.Колонки.Добавить("ДополнительныеПараметры");
	
	Возврат НовыйТаблицаЗадач;
	
КонецФункции

// Удаляет ранее запланированные регулярные задачи из очереди.
//
// Параметры:
//  Задача - ОпределяемыйТип.ЗадачиАссистентаУправления - Задача ассистента.
//
Процедура УдалитьЗапланированныеЗаписиЗадачи(Задача) Экспорт
	
	НаборЗаписей = РегистрыСведений.ОчередьРегулярныхЗадачАссистентаУправления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Задача.Установить(Задача);
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

// Удаляет ранее запланированные регулярные задачи из очереди.
//
// Параметры:
//  Предмет - ОпределяемыйТип.АссистентУправленияСсылкаПредметЗадачи - Предмет задачи.
//  УдаляемыеЗадачи - Массив Из ОпределяемыйТип.ЗадачиАссистентаУправления - Задачи ассистента.
//                                        Если не передано, то удаляются все задачи из очереди.
//
Процедура УдалитьЗапланированныеЗаписиПоПредмету(Предмет, УдаляемыеЗадачи = Неопределено) Экспорт
	
	Если УдаляемыеЗадачи <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("УдалитьЗапланированныеЗаписиПоПредмету", "УдаляемыеЗадачи", УдаляемыеЗадачи, Тип("Массив"));
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ОчередьРегулярныхЗадачАссистентаУправления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Предмет.Установить(Предмет);
	
	Если УдаляемыеЗадачи = Неопределено Тогда
		НаборЗаписей.Записать(Истина);
		Возврат;
	КонецЕсли;
	
	УдаляемыеЗаписи = Новый Массив;
	
	НаборЗаписей.Прочитать();
	Для каждого Запись Из НаборЗаписей Цикл
		Если УдаляемыеЗадачи.Найти(Запись.Задача) <> Неопределено Тогда
			УдаляемыеЗаписи.Добавить(Запись);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Запись Из УдаляемыеЗаписи Цикл
		НаборЗаписей.Удалить(Запись);
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Определяет является ли пользователем автором хоть одной задачи ассистента.
//
// Параметры:
//  Пользователь - Справочник.Пользователи - Проверяемый пользователь.
// 
// Возвращаемое значение:
//  Булево - Истина, если есть хоть одна задача.
//
Функция ЕстьЗадачиПользователя(Пользователь) Экспорт
	
	Для каждого СправочникЗадач Из СправочникиЗадачиАссистента() Цикл
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СправочникЗадач.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ЗадачиАссистентаУправления КАК СправочникЗадач
		|ГДЕ
		|	СправочникЗадач.Автор = &Автор";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.ЗадачиАссистентаУправления", СправочникЗадач.ПолноеИмя());
		Запрос.УстановитьПараметр("Автор", Пользователь);
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция Подключен() Экспорт
	
	ПользовательАссистент = ПользовательАссистент();
	
	Если ПользовательАссистент = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПользовательАссистентИдентификаторСВ = Неопределено;
	Попытка
		ПользовательАссистентИдентификаторСВ = ПользовательАссистентИдентификаторСВ();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
	КонецПопытки;
	
	Если ПользовательАссистентИдентификаторСВ = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура Подключить() Экспорт
	
	ПользовательАссистент = ПользовательАссистент();
	Если ПользовательАссистент = Неопределено Тогда
		НовыйСлужебныйПользовательАссистента();
		ОбновитьПовторноИспользуемыеЗначения();
		СоздатьОбсуждениеЖурналРаботыАссистента();
		Возврат;
	КонецЕсли;
	
	ПользовательАссистентИдентификаторСВ = Неопределено;
	Попытка
		ПользовательАссистентИдентификаторСВ = ПользовательАссистентИдентификаторСВ();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
	КонецПопытки;
	
	Если ПользовательАссистентИдентификаторСВ = Неопределено Тогда
		НовыйСлужебныйПользовательСВАссистента();
		ОбновитьУчастниковЖурналРаботыАссистента(ПользовательАссистент);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// Добавляет актуальные (в работе) задачи ассистента в очередь выполнения.
//
// Параметры:
//  Предмет                  - Ссылка - См. определяемый тип АссистентУправленияСсылкаПредметЗадачи.
//  СобытиеИдентификатор     - Строка - Идентификатор сработавшего события (см. ПриОпределенииСобытий).
//  Источник                 - Ссылка - См. определяемый тип АссистентУправленияСсылкаИсточникЗадачи.
//  ДополнительныеПараметры  - Структура - Дополнительные параметры.
//                                         Например, дополнительные параметры для формирования сообщения по шаблону (см.
//                                         Справочник.ШаблоныСообщений).
//
Процедура ПриСрабатыванииСобытия(Предмет, СобытиеИдентификатор, Источник = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если НЕ Используется() Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("АссистентУправления.ПриСрабатыванииСобытия", "Предмет", Предмет, Метаданные.ОпределяемыеТипы.АссистентУправленияСсылкаПредметЗадачи.Тип);
	
	Если Источник <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("АссистентУправления.ПриСрабатыванииСобытия", "Источник", Источник, Метаданные.ОпределяемыеТипы.АссистентУправленияСсылкаИсточникЗадачи.Тип);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиАссистентаУправления.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЗадачиАссистентаУправления КАК ЗадачиАссистентаУправления
	|ГДЕ
	|	ЗадачиАссистентаУправления.Используется
	|	И ЗадачиАссистентаУправления.События.СобытиеИдентификатор = &Событие
	|	И НЕ ЗадачиАссистентаУправления.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Событие", СобытиеИдентификатор);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Запись = РегистрыСведений.ОчередьЗадачАссистентаУправления.СоздатьМенеджерЗаписи();
		Запись.Предмет  = Предмет;
		Запись.Событие  = СобытиеИдентификатор;
		Запись.Источник = Источник;
		Запись.Задача   = Выборка.Ссылка;
		Запись.Дата     = ТекущаяДатаСеанса();
		Если ДополнительныеПараметры <> Неопределено Тогда
			Запись.ДополнительныеПараметры = Новый ХранилищеЗначения(ДополнительныеПараметры);
		КонецЕсли;
		Запись.Записать();
	КонецЦикла;
	
	Если Выборка.Количество() <> 0 Тогда
		ОбновитьРегламентноеЗаданиеВыполнениеТекущихЗадач(Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриСрабатыванииСобытия2(Менеджер, Событие, Предмет, Источник = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если НЕ Используется() Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("АссистентУправления.ПриСрабатыванииСобытия", "Предмет", Предмет, Метаданные.ОпределяемыеТипы.АссистентУправленияСсылкаПредметЗадачи.Тип);
	
	Если Источник <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("АссистентУправления.ПриСрабатыванииСобытия", "Источник", Источник, Метаданные.ОпределяемыеТипы.АссистентУправленияСсылкаИсточникЗадачи.Тип);
	КонецЕсли;
	
	ВключенныеЗадачи = ВключенныеЗадачи(Менеджер, Событие);
	Для каждого ЗадачаАссистента Из ВключенныеЗадачи Цикл
		Запись = РегистрыСведений.ОчередьЗадачАссистентаУправления.СоздатьМенеджерЗаписи();
		Запись.Предмет  = Предмет;
		Запись.Событие  = Событие;
		Запись.Источник = Источник;
		Запись.Задача   = ЗадачаАссистента.Ссылка;
		Запись.Дата     = ТекущаяДатаСеанса();
		Если ДополнительныеПараметры <> Неопределено Тогда
			Запись.ДополнительныеПараметры = Новый ХранилищеЗначения(ДополнительныеПараметры);
		КонецЕсли;
		Запись.Записать();
	КонецЦикла;
	
	Если ВключенныеЗадачи.Количество() <> 0 Тогда
		ОбновитьРегламентноеЗаданиеВыполнениеТекущихЗадач(Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция ДоступныеТипыПредметов() Экспорт
	
	ТипыПредметов = Новый Соответствие;
	
	Источники = Новый Массив;
	АссистентУправленияПереопределяемый.ПриОпределенииИсточников(Источники);
	
	Для каждого Источник Из Источники Цикл
		
		Модуль = ОбщегоНазначения.ОбщийМодуль(Источник);
		Модуль.ПриОпределенииТиповПредметовСобытий(ТипыПредметов);
		
	КонецЦикла;
	
	Возврат ТипыПредметов;
	
КонецФункции

Процедура ПриОпределенииСобытий(События) Экспорт
	
	Источники = Новый Массив;
	АссистентУправленияПереопределяемый.ПриОпределенииИсточников(Источники);
	
	Для каждого Источник Из Источники Цикл
		
		МодульИсточника = ОбщегоНазначения.ОбщийМодуль(Источник);
		
		ТипыПредметов = Новый Соответствие;
		МодульИсточника.ПриОпределенииТиповПредметовСобытий(ТипыПредметов);
		
		Для каждого ТипПредмета Из ТипыПредметов Цикл
			МодульИсточника.ПриОпределенииСобытий(События, ТипПредмета.Ключ);
		КонецЦикла;
		
		Для каждого Событие Из События Цикл
			Если НЕ ЗначениеЗаполнено(Событие.Предмет) Тогда
				Событие.Предмет = ТипПредмета.Ключ;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(Событие.Модуль) Тогда
				Событие.Модуль = Источник;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриОпределенииДействий(Действия) Экспорт
	
	АссистентУправленияПереопределяемый.ПриОпределенииДействий(Действия);
	
	Источники = Новый Массив;
	АссистентУправленияПереопределяемый.ПриОпределенииИсточников(Источники);
	
	Для каждого Источник Из Источники Цикл
		МодульИсточника = ОбщегоНазначения.ОбщийМодуль(Источник);
		МодульИсточника.ПриОпределенииДействий(Действия);
		
		Для каждого Действие Из Действия Цикл
			Если НЕ ЗначениеЗаполнено(Действие.Модуль) Тогда
				Действие.Модуль = Источник;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВыполненыУсловияЗадачи(Источник, Задача, Предмет)
	
	ПараметрыУсловия = Справочники.ЗадачиАссистентаУправления.ПараметрыУсловия(Задача);
	
	Если ПараметрыУсловия.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	УсловиеВыполнено = ИсточникПредмета(Задача.ТипПредмета).ВыполненыУсловия(Предмет, ПараметрыУсловия);
	
	Если УсловиеВыполнено = Ложь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучателиПредметаСообщения(ТипПредмета) Экспорт
	
	Получатели = Новый ТаблицаЗначений;
	Получатели.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Получатели.Колонки.Добавить("Путь", Новый ОписаниеТипов("Строка"));
	Получатели.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Получатели.Колонки.Добавить("Тип");
	
	ИсточникПредмета(ТипПредмета).ПриОпределенииПолучателейСообщения(Получатели);
	
	Возврат Получатели;
	
КонецФункции

Процедура ПроверитьНаличиеОбязательныхПараметров(СтруктураПараметров, ОбязательныеПараметры) Экспорт
	
	МассивПараметров = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ОбязательныеПараметры);
	
	ОтсутствующиеПараметры = Новый Массив;
	
	Для Каждого ОбязательныйПараметр Из МассивПараметров Цикл
		Если НЕ СтруктураПараметров.Свойство(ОбязательныйПараметр) Тогда
			ОтсутствующиеПараметры.Добавить(ОбязательныйПараметр);
		КонецЕсли;
	КонецЦикла;
	
	Если ОтсутствующиеПараметры.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = СтрШаблон(НСтр("ru='Отсутствуют обязательные параметры: %1'"), СтрСоединить(ОтсутствующиеПараметры, ","));
	ВызватьИсключение ТекстОшибки;
	
КонецПроцедуры

Функция ПользовательАссистент() Экспорт
	
	Возврат Пользователи.НайтиПоИмени(ИмяПользователяАссистента());
	
КонецФункции

Функция ПолучитьОтветственного(Объект) Экспорт
	
	РеквизитОтветственный = Объект.Метаданные().Реквизиты.Найти("Ответственный");
	
	Если РеквизитОтветственный = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если НЕ РеквизитОтветственный.Тип.СодержитТип(Тип("СправочникСсылка.Сотрудники")) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект, "Ответственный");
	
КонецФункции

Функция ВыполненоУсловиеСравнения(ЛевоеЗначение, ВидСравнения, ПравоеЗначение) Экспорт
	
	Если ВидСравнения = Перечисления.ВидСравненияЗначений.Равно Тогда
		Возврат ЛевоеЗначение = ПравоеЗначение;
	КонецЕсли;
	
	Если ВидСравнения = Перечисления.ВидСравненияЗначений.ВСписке Тогда
		Возврат ПравоеЗначение.Найти(ЛевоеЗначение) <> Неопределено;
	КонецЕсли;
	
	Если ВидСравнения = Перечисления.ВидСравненияЗначений.Больше Тогда
		Возврат ЛевоеЗначение > ПравоеЗначение;
	ИначеЕсли ВидСравнения = Перечисления.ВидСравненияЗначений.БольшеИлиРавно Тогда
		Возврат ЛевоеЗначение >= ПравоеЗначение;
	ИначеЕсли ВидСравнения = Перечисления.ВидСравненияЗначений.Меньше Тогда
		Возврат ЛевоеЗначение < ПравоеЗначение;
	ИначеЕсли ВидСравнения = Перечисления.ВидСравненияЗначений.МеньшеИлиРавно Тогда
		Возврат ЛевоеЗначение <= ПравоеЗначение;
	КонецЕсли;
	
	Если ВидСравнения = Перечисления.ВидСравненияЗначений.ВИнтервале Тогда
		Возврат ЛевоеЗначение > ПравоеЗначение[0] И ЛевоеЗначение < ПравоеЗначение[1];
	ИначеЕсли ВидСравнения = Перечисления.ВидСравненияЗначений.ВИнтервалеВключая Тогда
		Возврат ЛевоеЗначение >= ПравоеЗначение[0] И ЛевоеЗначение <= ПравоеЗначение[1];
	ИначеЕсли ВидСравнения = Перечисления.ВидСравненияЗначений.ВИнтервалеВключаяЛево Тогда
		Возврат ЛевоеЗначение >= ПравоеЗначение[0] И ЛевоеЗначение < ПравоеЗначение[1];
	ИначеЕсли ВидСравнения = Перечисления.ВидСравненияЗначений.ВИнтервалеВключаяПраво Тогда
		Возврат ЛевоеЗначение > ПравоеЗначение[0] И ЛевоеЗначение <= ПравоеЗначение[1];
	КонецЕсли;
	
КонецФункции

Процедура ДобавитьСообщениеВРезультат(Результат, Объект, Текст, Действия = Неопределено) Экспорт
	
	ДанныеСообщения = ОбсужденияУНФ.НовыйДанныеСообщения();
	ДанныеСообщения.Объект = Объект;
	ДанныеСообщения.Текст = Текст;
	
	Если Действия <> Неопределено Тогда
		ДанныеСообщения.Действия = Действия;
	КонецЕсли;
	
	Результат.ДанныеСообщений.Добавить(ДанныеСообщения);
	
КонецПроцедуры

Функция ПредложениеВыполнитьЗадачуИнтерактивно(Предмет, Событие, Действие, ЗначенияЗаполнения, ДополнительныеПараметры) Экспорт
	
	ДанныеСообщения = ОбсужденияУНФ.НовыйДанныеСообщения();
	ДанныеСообщения.Текст = СтрШаблон(НСтр("ru='Выполнить задачу %1?'"), ДополнительныеПараметры.ЗадачаАссистентаУправления);
	ДанныеСообщения.Получатель = ПолучитьОтветственного(Предмет);
	
	ДанныеСообщения.Действия.Добавить(АссистентУправленияКлиентСервер.КодДействияСообщенияВыполнитьДействие(), НСтр("ru='Да'"));
	ДанныеСообщения.Действия.Добавить(АссистентУправленияКлиентСервер.КодДействияСообщенияОтмена(), НСтр("ru='Нет'"));
	
	ДанныеСообщенияДанные = Новый Структура;
	ДанныеСообщенияДанные.Вставить("Действие", Действие);
	ДанныеСообщенияДанные.Вставить("Источник", ДополнительныеПараметры.Источник);
	ДанныеСообщенияДанные.Вставить("Событие", Событие);
	ДанныеСообщенияДанные.Вставить("Предмет", Предмет);
	ДанныеСообщенияДанные.Вставить("Задача", ДополнительныеПараметры.ЗадачаАссистентаУправления);
	ДанныеСообщения.Данные = Новый ХранилищеЗначения(Новый ФиксированнаяСтруктура(ДанныеСообщенияДанные));
	
	Возврат ДанныеСообщения;
	
КонецФункции

// Возвращает описание общего или частного действия из таблицы действий.
//
// Параметры:
//  Действия				 - ТаблицаЗначений, ДанныеФормыКоллекция - См. функцию НовыйТаблицаДействий.
//  Предмет					 - Строка
//  ДействиеИдентификатор	 - Строка
// 
// Возвращаемое значение:
//   - СтрокаТаблицыЗначений, ДанныеФормыЭлементКоллекции
//
Функция НайтиОписаниеДействия(Действия, Предмет, ДействиеИдентификатор) Экспорт
	
	ОписаниеДействия = ОписаниеОбщегоДействия(Действия, ДействиеИдентификатор);
	
	Если ОписаниеДействия <> Неопределено Тогда
		Возврат ОписаниеДействия;
	КонецЕсли;
	
	Возврат ОписаниеДействияПоПредмету(Действия, ДействиеИдентификатор, Предмет);
	
КонецФункции

#Область ОписаниеТаблиц

Функция НовыйТаблицаСобытий() Экспорт
	
	События = Новый ТаблицаЗначений;
	События.Колонки.Добавить("Предмет", Новый ОписаниеТипов("Строка"));
	События.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	События.Колонки.Добавить("Тип", Новый ОписаниеТипов("Строка"));
	События.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	События.Колонки.Добавить("ОписаниеПредставление", Новый ОписаниеТипов("Строка"));
	События.Колонки.Добавить("ОписаниеШаблонТекста", Новый ОписаниеТипов("Строка"));
	События.Колонки.Добавить("Комментарий", Новый ОписаниеТипов("Строка"));
	События.Колонки.Добавить("Модуль", Новый ОписаниеТипов("Строка"));
	
	Возврат События;
	
КонецФункции

Функция НовыйТаблицаДействий() Экспорт
	
	Действия = Новый ТаблицаЗначений;
	Действия.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Действия.Колонки.Добавить("Предмет", Новый ОписаниеТипов("Строка"));
	Действия.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Действия.Колонки.Добавить("Общее", Новый ОписаниеТипов("Булево"));
	Действия.Колонки.Добавить("Модуль", Новый ОписаниеТипов("Строка"));
	Действия.Колонки.Добавить("ОписаниеПолей", Новый ОписаниеТипов("ТаблицаЗначений"));
	
	Возврат Действия;
	
КонецФункции

Функция НовыйОписаниеПолейДействия()
	
	Описание = Новый ТаблицаЗначений;
	Описание.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	Описание.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Описание.Колонки.Добавить("ОписаниеТипов", Новый ОписаниеТипов("ОписаниеТипов"));
	Описание.Колонки.Добавить("Значение");
	Описание.Колонки.Добавить("ОбязательноеЗаполнение", Новый ОписаниеТипов("Булево"));
	Описание.Колонки.Добавить("ВариантЗаполнения", Новый ОписаниеТипов("Строка"));
	
	Возврат Описание;
	
КонецФункции

Процедура ДобавитьОписаниеПолейДействия(Действие) Экспорт
	
	Если ТипЗнч(Действие.ОписаниеПолей) = Тип("ТаблицаЗначений") Тогда
		Действие.ОписаниеПолей = НовыйОписаниеПолейДействия();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ИнтеграцияСистемаВзаимодействия

Процедура ОбработатьДействиеОтменаСообщенияАссистента(ОписаниеСообщения, ОтразитьРезультатСообщением = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СообщениеСВ = ОбсужденияУНФ.ПолучитьСообщение(ОписаниеСообщения.ОбсуждениеИдентификатор, ОписаниеСообщения.СообщениеИдентификатор);
	СообщениеСВ.Действия.Очистить();
	СообщениеСВ.Записать();
	
	Если ОтразитьРезультатСообщением Тогда
		СообщениеПодтверждение = СистемаВзаимодействия.СоздатьСообщение(ОписаниеСообщения.ОбсуждениеИдентификатор);
		СообщениеПодтверждение.Текст = Новый ФорматированнаяСтрока("#" + НСтр("ru='Нет'"));
		СообщениеПодтверждение.Дата = СообщениеСВ.Дата + 1;
		СообщениеПодтверждение.Автор = СистемаВзаимодействия.ПолучитьИдентификаторПользователя(ПользователиИнформационнойБазы.ТекущийПользователь().УникальныйИдентификатор);
		СообщениеПодтверждение.Получатели.Добавить(ПользовательАссистентИдентификаторСВ());
		СообщениеПодтверждение.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеОбработкиДействияСообщенияАссистента(ОписаниеСообщения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СообщениеСДействием = ОбсужденияУНФ.ПолучитьСообщение(ОписаниеСообщения.ОбсуждениеИдентификатор, ОписаниеСообщения.СообщениеИдентификатор);
	СообщениеСДействием.Действия.Очистить();
	СообщениеСДействием.Записать();
	
КонецПроцедуры

Функция СоздатьОбсуждениеЖурналРаботыАссистента() Экспорт
	
	ОбсуждениеЖурнал = ПолучитьОбсуждениеЖурналРаботыАссистента();
	
	Если ОбсуждениеЖурнал <> Неопределено Тогда
		Возврат ОбсуждениеЖурнал;
	КонецЕсли;
	
#Если НЕ ВнешнееСоединение Тогда
	
	Если НЕ СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		Возврат ОбсуждениеЖурнал;
	КонецЕсли;
	
	Ключ = КлючОбсуждениеЖурналРаботыАссистента();
	
	ОбсуждениеЖурнал = СистемаВзаимодействия.СоздатьОбсуждение();
	ОбсуждениеЖурнал.Ключ = Ключ;
	ОбсуждениеЖурнал.Отображаемое = Истина;
	ОбсуждениеЖурнал.Заголовок = НСтр("ru='Монитор ассистента УНФ'");
	ОбсуждениеЖурнал.Участники.Добавить(ПользовательАссистентИдентификаторСВ());
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗадачиАссистентаУправления.Автор КАК Автор
	|ИЗ
	|	Справочник.ЗадачиАссистентаУправления КАК ЗадачиАссистентаУправления
	|ГДЕ
	|	НЕ ЗадачиАссистентаУправления.ЭтоГруппа";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НовыйУчастник = Неопределено;
		Попытка
			НовыйУчастник = ОбсужденияУНФ.ИдентификаторПользователя(Выборка.Автор);
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		КонецПопытки;
		Если НовыйУчастник <> Неопределено Тогда
			ОбсуждениеЖурнал.Участники.Добавить(НовыйУчастник);
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	ОбсуждениеЖурнал.Записать();
	УстановитьПривилегированныйРежим(Ложь);
#КонецЕсли
	
	Возврат ОбсуждениеЖурнал;
	
КонецФункции

Функция ПолучитьОбсуждениеЖурналРаботыАссистента() Экспорт
	
	Обсуждение = Неопределено;
	
#Если НЕ ВнешнееСоединение Тогда
	Ключ = КлючОбсуждениеЖурналРаботыАссистента();
	
	Если НЕ СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		Возврат Обсуждение;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(Ключ);
	УстановитьПривилегированныйРежим(Ложь);
#КонецЕсли
	
	Возврат Обсуждение;
	
КонецФункции

Функция КлючОбсуждениеЖурналРаботыАссистента()
	
	Возврат "АссистентУправления";
	
КонецФункции

Процедура ОбновитьУчастниковЖурналРаботыАссистента(АвторЗадачи) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("АссистентУправления.ОбновитьУчастниковЖурналРаботыАссистента", "АвторЗадачи", АвторЗадачи, Тип("СправочникСсылка.Пользователи"));
	
	Обсуждение = ПолучитьОбсуждениеЖурналРаботыАссистента();
	
	Если Обсуждение = Неопределено Тогда
		Обсуждение = СоздатьОбсуждениеЖурналРаботыАссистента();
	КонецЕсли;
	
	Если Обсуждение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	АвторЗадачиПользовательСВ = ОбсужденияУНФ.ИдентификаторПользователя(АвторЗадачи);
	Если АвторЗадачиПользовательСВ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Обсуждение.Участники.Содержит(АвторЗадачиПользовательСВ) Тогда
		Возврат;
	КонецЕсли;
	
	Обсуждение.Участники.Добавить(АвторЗадачиПользовательСВ);
	
	Попытка
		Обсуждение.Записать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

Функция ПользовательАссистентИдентификаторСВ() Экспорт
	
	Возврат ОбсужденияУНФ.ИдентификаторПользователя(ПользовательАссистент());
	
КонецФункции

Процедура ОтветитьНаСообщениеПользователя(ОписаниеСообщения, ОбрабатываемоеСообщение) Экспорт
	
	НовоеСообщениеПользователя = ОбсужденияУНФ.ПолучитьСообщение(ОписаниеСообщения.ОбсуждениеИдентификатор, ОписаниеСообщения.СообщениеИдентификатор);
	
	РаспознанноеДействиеНадСообщением = РаспознатьДействиеНадСообщением(НовоеСообщениеПользователя.Текст);
	
	ИсходноеСообщениеАссистента = Неопределено;
	Если РаспознанноеДействиеНадСообщением <> Неопределено Тогда
		ИсходноеСообщениеАссистента = ОбрабатываемоеСообщениеСВопросомАссистента(НовоеСообщениеПользователя, РаспознанноеДействиеНадСообщением);
	КонецЕсли;
	
	СообщениеОбработано = Ложь;
	Если ИсходноеСообщениеАссистента <> Неопределено Тогда
		ОбрабатываемоеСообщение = НовыйОписаниеСообщенияСВ(ИсходноеСообщениеАссистента);
		
		Если РаспознанноеДействиеНадСообщением = АссистентУправленияКлиентСервер.КодДействияСообщенияОтмена() Тогда
			ОбработатьДействиеОтменаСообщенияАссистента(ОбрабатываемоеСообщение);
			СообщениеОбработано = Истина;
		КонецЕсли;
		
		Если РаспознанноеДействиеНадСообщением = АссистентУправленияКлиентСервер.КодДействияСообщенияВыполнитьДействие() Тогда
			РазыменоватьДанныеСообщенияСВ(ОбрабатываемоеСообщение);
			ОбрабатываемоеСообщение.ВыбранноеДействие = РаспознанноеДействиеНадСообщением;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если СообщениеОбработано Тогда
		ОбрабатываемоеСообщение = Неопределено;
		Возврат;
	КонецЕсли;
	
	ДанныеСообщения = ОбсужденияУНФ.НовыйДанныеСообщения();
	ДанныеСообщения.Объект = ОписаниеСообщения.ОбсуждениеИдентификатор;
	ДанныеСообщения.Дата   = НовоеСообщениеПользователя.Дата + 1;
	ДанныеСообщения.Текст  = ОтветНаПроизвольнуюРеплику(НовоеСообщениеПользователя.Текст);
	ДанныеСообщения.Автор  = ПользовательАссистент();
	ОбсужденияУНФ.СоздатьСообщение(ДанныеСообщения);
	
КонецПроцедуры

Функция ОбрабатываемоеСообщениеСВопросомАссистента(СообщениеПользователя, РаспознанноеДействиеНадСообщением)
	
	СообщениеНаКотороеОтветили = ОбсужденияУНФ.ПолучитьПоследнееСообщение(СообщениеПользователя.Обсуждение,
		ПользовательАссистентИдентификаторСВ(), СообщениеПользователя.Идентификатор);
	
	Если СообщениеНаКотороеОтветили = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СообщениеСодержитДействие = СообщениеНаКотороеОтветили.Действия.НайтиПоЗначению(РаспознанноеДействиеНадСообщением) <> Неопределено;
	
	Если НЕ СообщениеСодержитДействие Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат СообщениеНаКотороеОтветили;
	
КонецФункции

Функция НовыйОписаниеСообщенияСВ(Сообщение = Неопределено, РазыменоватьДанные = Истина) Экспорт
	
	ОписаниеСообщения = АссистентУправленияКлиентСервер.НовыйОписаниеСообщенияСВ();
	
	Если Сообщение = Неопределено Тогда
		Возврат ОписаниеСообщения;
	КонецЕсли;
	
	Если ТипЗнч(Сообщение) = Тип("СообщениеСистемыВзаимодействия") Тогда
		СообщениеСВ = Сообщение;
	ИначеЕсли ТипЗнч(Сообщение) = Тип("ИдентификаторСообщенияСистемыВзаимодействия") Тогда
		СообщениеСВ = СистемаВзаимодействия.ПолучитьСообщение(Сообщение);
	КонецЕсли;
	
	ОписаниеСообщения.СообщениеИдентификатор = СообщениеСВ.Идентификатор;
	ОписаниеСообщения.ОбсуждениеИдентификатор = СообщениеСВ.Обсуждение;
	ОписаниеСообщения.Данные = СообщениеСВ.Данные;
	
	РазыменоватьДанныеСообщенияСВ(ОписаниеСообщения);
	
	Возврат ОписаниеСообщения;
	
КонецФункции

Процедура РазыменоватьДанныеСообщенияСВ(ОписаниеСообщения) Экспорт
	
	ОписаниеСообщения.Предмет = ОбсужденияУНФ.РазыменоватьКонтекстОбсуждения(ОписаниеСообщения.ОбсуждениеИдентификатор);
	
	Если ТипЗнч(ОписаниеСообщения.Данные) = Тип("Строка") Тогда
		ОписаниеСообщения.РаспознанныеДанные = ПрочитатьJSONВСтруктуру(ОписаниеСообщения.Данные);
	КонецЕсли;
	
	ОписаниеСообщения.ДанныеРазыменованы = Истина;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Обработчик регламентного задания ВыполнениеЗадачАссистентаУправления.
//
Процедура ВыполнениеЗадачАссистентаУправления() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ВыполнениеРегулярныхЗадачАссистентаУправления);
	РегламентноеЗадание = Метаданные.РегламентныеЗадания.ВыполнениеЗадачАссистентаУправления;
	ЗаписьЖРПриНачалеРегламентногоЗадания(РегламентноеЗадание);
	
	ВыполнитьОчередьТекущихЗадач();
	
	ЗаписьЖРПослеВыполненияРегламентногоЗадания(РегламентноеЗадание);
	
КонецПроцедуры

// Обработчик регламентного задания ВыполнениеРегулярныхЗадачАссистентаУправления.
//
Процедура ВыполнениеРегулярныхЗадачАссистентаУправления() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ВыполнениеРегулярныхЗадачАссистентаУправления);
	РегламентноеЗадание = Метаданные.РегламентныеЗадания.ВыполнениеРегулярныхЗадачАссистентаУправления;
	ЗаписьЖРПриНачалеРегламентногоЗадания(РегламентноеЗадание);
	
	ВыполнитьОчередьРегулярныхЗадач();
	
	ЗаписьЖРПослеВыполненияРегламентногоЗадания(РегламентноеЗадание);
	
КонецПроцедуры

// Обработчик подписки на событие ПредметДокументЗадачиАссистентаПередЗаписью.
//
Процедура ПредметДокументЗадачиАссистентаПередЗаписью(Источник) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СправочникЗадач Из СправочникиЗадачиАссистента() Цикл
		Если СправочникЗадач = Метаданные.Справочники.ЗадачиАссистентаУправления Тогда
			Продолжить;
		КонецЕсли;
		МенеджерЗадач = МенеджерСправочникаЗадач(СправочникЗадач);
		МенеджерЗадач.ПередЗаписьюПредметаЗадачи(Источник);
	КонецЦикла;
	
КонецПроцедуры

// Обработчик подписки на событие ПредметДокументЗадачиАссистентаПриЗаписи.
//
Процедура ПредметДокументЗадачиАссистентаПриЗаписи(Источник) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СправочникЗадач Из СправочникиЗадачиАссистента() Цикл
		Если СправочникЗадач = Метаданные.Справочники.ЗадачиАссистентаУправления Тогда
			Продолжить;
		КонецЕсли;
		МенеджерЗадач = МенеджерСправочникаЗадач(СправочникЗадач);
		МенеджерЗадач.ПриЗаписиПредметаЗадачи(Источник);
	КонецЦикла;
	
КонецПроцедуры

// Обработчик подписки на событие ПредметДокументЗадачиАссистентаОбработкаПроведения.
//
Процедура ПредметДокументЗадачиАссистентаОбработкаПроведения(Источник) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СправочникЗадач Из СправочникиЗадачиАссистента() Цикл
		Если СправочникЗадач = Метаданные.Справочники.ЗадачиАссистентаУправления Тогда
			Продолжить;
		КонецЕсли;
		МенеджерЗадач = МенеджерСправочникаЗадач(СправочникЗадач);
		МенеджерЗадач.ОбработкаПроведенияПредметаЗадачи(Источник);
	КонецЦикла;
	
КонецПроцедуры

// Обработчик подписки на событие ПредметДокументЗадачиАссистентаОбработкаУдаленияПроведения.
//
Процедура ПредметДокументЗадачиАссистентаОбработкаУдаленияПроведения(Источник) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СправочникЗадач Из СправочникиЗадачиАссистента() Цикл
		Если СправочникЗадач = Метаданные.Справочники.ЗадачиАссистентаУправления Тогда
			Продолжить;
		КонецЕсли;
		МенеджерЗадач = МенеджерСправочникаЗадач(СправочникЗадач);
		МенеджерЗадач.ОбработкаУдаленияПроведенияПредметаЗадачи(Источник);
	КонецЦикла;
	
КонецПроцедуры

// Обработчик подписки на событие РегулярныеЗадачиАссистентаУправленияПриЗаписи.
//
Процедура РегулярныеЗадачиАссистентаУправленияПриЗаписи(Источник) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьВключенныеРегулярныеЗадачи = Ложь;
	
	Для каждого Тип Из Метаданные.ОпределяемыеТипы.РегулярныеЗадачиАссистентаУправления.Тип.Типы() Цикл
		ЕстьВключенныеРегулярныеЗадачи = ЕстьВключенныеЗадачиСправочника(Метаданные.НайтиПоТипу(Тип));
		Если ЕстьВключенныеРегулярныеЗадачи Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьРегламентноеЗаданиеВыполнениеРегулярныхЗадач(ЕстьВключенныеРегулярныеЗадачи);
	
КонецПроцедуры

// Обработчик подписки на событие АссистентУправленияПроверкаДокументаПередЗаписью.
//
Процедура АссистентУправленияПроверкаДокументаПередЗаписьюОбработчик(Источник) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьДополнительныеСвойства(Источник);
	
	Для каждого Менеджер Из Менеджеры() Цикл
		Настройки = НастройкиМенеджера(Менеджер);
		Если Настройки.ПроверкаДокументаПередЗаписью Тогда
			Менеджер.ПроверкаДокументаПередЗаписью(Источник);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Обработчик подписки на событие АссистентУправленияПроверкаДокументаПриЗаписи.
//
Процедура АссистентУправленияПроверкаДокументаПриЗаписиОбработчик(Источник) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьДополнительныеСвойства(Источник);
	
	Для каждого Менеджер Из Менеджеры() Цикл
		Настройки = НастройкиМенеджера(Менеджер);
		Если Настройки.ПроверкаДокументаПриЗаписи Тогда
			Менеджер.ПроверкаДокументаПриЗаписи(Источник);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура АссистентУправленияСинхронизацияДанныхЧерезУниверсальныйФорматПриЗаписиОбработчик(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ВариантНастройки <> "КабинетКлиента" Тогда
		Возврат;
	КонецЕсли;
	
	ФоновоеЗадание = ФоновыеЗадания.Выполнить("АссистентУправления.ВыполнитьТекущиеЗадачиСейчас",,,
		НСтр("ru = 'Выполнение задач ассистента'"));
	
КонецПроцедуры

#КонецОбласти

Функция ИмяСобытияЖР() Экспорт
	
	Возврат НСтр("ru='АссистентУправления'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаписьЖР

Процедура ОтразитьХодВыполнения(Комментарий, Знач ВложенноеИмяСобытия = "", Данные = Неопределено, ОбъектМетаданных = Неопределено, УровеньЖР = Неопределено)
	
	Если ВложенноеИмяСобытия = "" Тогда
		ВложенноеИмяСобытия = ИмяСобытияЖР();
	Иначе
		ВложенноеИмяСобытия = ИмяСобытияЖР() + "." + ВложенноеИмяСобытия;
	КонецЕсли;
	
	Если УровеньЖР = Неопределено Тогда
		УровеньЖР = УровеньЖурналаРегистрации.Примечание;
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(ВложенноеИмяСобытия, УровеньЖР, ОбъектМетаданных, Данные, Комментарий);
	
КонецПроцедуры

Процедура ЗаписьЖРПриНачалеРегламентногоЗадания(РегламентноеЗадание)
	
	ИмяСобытия = НСтр("ru='СтартРегламентногоЗадания'", ОбщегоНазначения.КодОсновногоЯзыка());
	Комментарий = НСтр("ru='Начало выполнения регламентного задания'");
	ОтразитьХодВыполнения(Комментарий, ИмяСобытия,, РегламентноеЗадание);
	
КонецПроцедуры

Процедура ЗаписьЖРПередНачаломОбработкиЗадач(КоличествоЗадач)
	
	ИмяСобытия = НСтр("ru='НачалоОбработки'", ОбщегоНазначения.КодОсновногоЯзыка());
	Комментарий = СтрШаблон(
		НСтр("ru='Старт обработки очереди задач. Количество к обработке: %1'"),
		КоличествоЗадач);
	ОтразитьХодВыполнения(Комментарий, ИмяСобытия);
	
КонецПроцедуры

Процедура ЗаписьЖРПередНачаломВыполненияЗадачи(Задача)
	
	ИмяСобытия = НСтр("ru='НачалоВыполненияЗадачи'", ОбщегоНазначения.КодОсновногоЯзыка());
	Комментарий = СтрШаблон(
		НСтр("ru='Старт выполнения Задачи ""%1""'"),
		Задача);
	ОтразитьХодВыполнения(Комментарий, ИмяСобытия, Задача, Задача.Метаданные());
	
КонецПроцедуры

Процедура ЗаписьЖРПослеВыполненияЗадачи(Задача)
	
	ИмяСобытия = НСтр("ru='КонецВыполненияЗадачи'", ОбщегоНазначения.КодОсновногоЯзыка());
	Комментарий = СтрШаблон(
		НСтр("ru='Завершено выполнение Задачи ""%1""'"),
		Задача);
	ОтразитьХодВыполнения(Комментарий, ИмяСобытия, Задача, Задача.Метаданные());
	
КонецПроцедуры

Процедура ЗаписьЖРЗавершениеОбработкиЗадач(КоличествоВыполненныхЗадач)
	
	ИмяСобытия = НСтр("ru='КонецОбработки'", ОбщегоНазначения.КодОсновногоЯзыка());
	Комментарий = СтрШаблон(
		НСтр("ru='Завершена обработка очереди задач. Выполнено успешно: %1'"),
		КоличествоВыполненныхЗадач);
	ОтразитьХодВыполнения(Комментарий, ИмяСобытия);
	
КонецПроцедуры

Процедура ЗаписьЖРПослеВыполненияРегламентногоЗадания(РегламентноеЗадание)
	
	ИмяСобытия = НСтр("ru='КонецОбработки'", ОбщегоНазначения.КодОсновногоЯзыка());
	Комментарий = НСтр("ru='Завершение выполнения регламентного задания'");
	ОтразитьХодВыполнения(Комментарий, ИмяСобытия,, РегламентноеЗадание);
	
КонецПроцедуры

Процедура ЗаписьЖРОшибкаВыполненияЗадачи(Задача, ТекстОшибки)
	
	ИмяСобытия = НСтр("ru='ОшибкаВыполненияЗадачи'", ОбщегоНазначения.КодОсновногоЯзыка());
	Комментарий = СтрШаблон(
		НСтр("ru='Выполнение Задачи ""%1"" завершено с ошибкой:
		|%2'"),
		Задача,
		ТекстОшибки);
	ОтразитьХодВыполнения(Комментарий, ИмяСобытия, Задача, Задача.Метаданные(), УровеньЖурналаРегистрации.Ошибка);
	
КонецПроцедуры

#КонецОбласти

#Область РегламентныеЗадания

Функция КоличествоЗапусковРегулярныхЗадачВСутки()
	
	Возврат 6;
	
КонецФункции

Функция КоличествоПовторовВыполненияЗадачиПриОшибке()
	
	Возврат 5;
	
КонецФункции

Процедура ОбновитьРегламентноеЗаданиеВыполнениеТекущихЗадач(Использование)
	
	УстановитьИспользованиеРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ВыполнениеЗадачАссистентаУправления,
		РасписаниеРегламентногоЗаданияТекущихЗадач(),
		Использование);
	
КонецПроцедуры

Процедура ОбновитьРегламентноеЗаданиеВыполнениеРегулярныхЗадач(Использование)
	
	УстановитьИспользованиеРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ВыполнениеРегулярныхЗадачАссистентаУправления,
		РасписаниеРегламентногоЗаданияРегулярныхЗадач(),
		Использование);
	
КонецПроцедуры

Процедура УстановитьИспользованиеРегламентногоЗадания(РегламентноеЗадание, Расписание, Использование)
	ИмяСобытия = НСтр("ru='ОбновлениеЗадания'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	УстановитьПривилегированныйРежим(Истина);
	Задание = РегламентныеЗаданияСервер.Задание(РегламентноеЗадание);
	
	НадоСоздатьЗадание = Использование И Задание = Неопределено;
	Если НадоСоздатьЗадание Тогда
		ПараметрыЗадания = Новый Структура;
		ПараметрыЗадания.Вставить("Метаданные", РегламентноеЗадание);
		ПараметрыЗадания.Вставить("Использование", Истина);
		ПараметрыЗадания.Вставить("Расписание", Расписание);
		ПараметрыЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", ?(ОбщегоНазначения.РежимОтладки(), 20, 3600));
		ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 3);
		Если ЗначениеЗаполнено(РегламентноеЗадание.Ключ) Тогда
			ПараметрыЗадания.Вставить("Ключ", РегламентноеЗадание.Ключ);
		КонецЕсли;
		
		РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
		ОтразитьХодВыполнения("РегламентныеЗаданияСервер.ДобавитьЗадание", ИмяСобытия);
		Возврат;
	КонецЕсли;
	
	НадоИзменитьЗадание = Задание <> Неопределено И Задание.Использование <> Использование;
	Если НадоИзменитьЗадание Тогда
		РегламентныеЗаданияСервер.ИзменитьЗадание(Задание, Новый Структура("Использование", Использование));
		Комментарий = СтрШаблон("РегламентныеЗаданияСервер.ИзменитьЗадание(%1)", Использование);
		ОтразитьХодВыполнения(Комментарий, ИмяСобытия);
	КонецЕсли;
	
КонецПроцедуры

Функция РасписаниеРегламентногоЗаданияТекущихЗадач()
	
	// Запуск задания через 30 секунд после возникновения нового события.
	// Если в процессе выполнения задания появились новые задачи в очереди -- повторное выполнение задания через 30 секунд.
	// Задание удалится, если задание выполнено успешно и очередь задач пустая.
	
	Месяцы = Новый Массив;
	Месяцы.Добавить(1);
	Месяцы.Добавить(2);
	Месяцы.Добавить(3);
	Месяцы.Добавить(4);
	Месяцы.Добавить(5);
	Месяцы.Добавить(6);
	Месяцы.Добавить(7);
	Месяцы.Добавить(8);
	Месяцы.Добавить(9);
	Месяцы.Добавить(10);
	Месяцы.Добавить(11);
	Месяцы.Добавить(12);
	
	ДниНедели = Новый Массив;
	ДниНедели.Добавить(1);
	ДниНедели.Добавить(2);
	ДниНедели.Добавить(3);
	ДниНедели.Добавить(4);
	ДниНедели.Добавить(5);
	ДниНедели.Добавить(6);
	ДниНедели.Добавить(7);
	
	Расписание = Новый РасписаниеРегламентногоЗадания;
	Расписание.ДниНедели   = ДниНедели;
	Расписание.Месяцы      = Месяцы;
	Расписание.ПериодПовтораВТечениеДня = ?(ОбщегоНазначения.РежимОтладки(), 5, 180);
	Расписание.ПериодПовтораДней        = 1; // каждый день
	
	Возврат Расписание;
	
КонецФункции

Функция РасписаниеРегламентногоЗаданияРегулярныхЗадач()
	
	Месяцы = Новый Массив;
	Месяцы.Добавить(1);
	Месяцы.Добавить(2);
	Месяцы.Добавить(3);
	Месяцы.Добавить(4);
	Месяцы.Добавить(5);
	Месяцы.Добавить(6);
	Месяцы.Добавить(7);
	Месяцы.Добавить(8);
	Месяцы.Добавить(9);
	Месяцы.Добавить(10);
	Месяцы.Добавить(11);
	Месяцы.Добавить(12);
	
	ДниНедели = Новый Массив;
	ДниНедели.Добавить(1);
	ДниНедели.Добавить(2);
	ДниНедели.Добавить(3);
	ДниНедели.Добавить(4);
	ДниНедели.Добавить(5);
	ДниНедели.Добавить(6);
	ДниНедели.Добавить(7);
	
	СекундВЧасе = 3600;
	
	Расписание = Новый РасписаниеРегламентногоЗадания;
	Расписание.ДниНедели   = ДниНедели;
	Расписание.Месяцы      = Месяцы;
	Расписание.ПериодПовтораВТечениеДня = 24 / КоличествоЗапусковРегулярныхЗадачВСутки() * СекундВЧасе;
	Расписание.ПериодПовтораДней        = 1; // Каждый день.
	
	Возврат Расписание;
	
КонецФункции

Функция КлючРегламентногоЗаданияВыполненияТекущихЗадач()
	
	Возврат Метаданные.РегламентныеЗадания.ВыполнениеЗадачАссистентаУправления.Ключ;
	
КонецФункции

Функция КлючРегламентногоЗаданияВыполненияРегулярныхЗадач()
	
	Возврат Метаданные.РегламентныеЗадания.ВыполнениеРегулярныхЗадачАссистентаУправления.Ключ;
	
КонецФункции

#КонецОбласти

#Область ОчередьЗадачАссистента

Процедура ПередНачаломВыполненияРегулярныхЗадач()
	
	НовыеЗадачиКВыполнению = НовыйТаблицаРегулярныхЗадачКВыполнению();
	
	Для каждого СправочникЗадач Из СправочникиЗадачиАссистента() Цикл
		Если СправочникЗадач = Метаданные.Справочники.ЗадачиАссистентаУправления Тогда
			Продолжить;
		КонецЕсли;
		МенеджерЗадач = МенеджерСправочникаЗадач(СправочникЗадач);
		МенеджерЗадач.ПередНачаломВыполненияЗадач(НовыеЗадачиКВыполнению);
	КонецЦикла;
	
КонецПроцедуры

Функция ТекущиеЗадачиКВыполнению(ОтборПредметы = Неопределено)
	
	Если ОтборПредметы = Неопределено Тогда
		ОтборПредметы = Новый Массив;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОчередьЗадачАссистентаУправления.Предмет КАК Предмет,
	|	ОчередьЗадачАссистентаУправления.Событие КАК СобытиеИдентификатор,
	|	ОчередьЗадачАссистентаУправления.Источник КАК Источник,
	|	ОчередьЗадачАссистентаУправления.Задача КАК Задача,
	|	ОчередьЗадачАссистентаУправления.Дата КАК Дата,
	|	ОчередьЗадачАссистентаУправления.ДополнительныеПараметры КАК ДополнительныеПараметры,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК НеВыполнятьПосле,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
	|	ИСТИНА КАК ЭтоОчередьТекущихЗадач,
	|	ЛОЖЬ КАК ЭтоОчередьРегулярныхЗадач,
	|	ОчередьЗадачАссистентаУправления.КоличествоПопыток КАК КоличествоПопыток
	|ИЗ
	|	РегистрСведений.ОчередьЗадачАссистентаУправления КАК ОчередьЗадачАссистентаУправления
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ОтборПоПредметам
	|				ТОГДА ОчередьЗадачАссистентаУправления.Предмет В (&ВыбранныеПредметы)
	|						ИЛИ ОчередьЗадачАссистентаУправления.Источник В (&ВыбранныеПредметы)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ОчередьЗадачАссистентаУправления.КоличествоПопыток <= &КоличествоПовторовВыполненияЗадачиПриОшибке
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	
	Запрос.УстановитьПараметр("ОтборПоПредметам", ОтборПредметы.Количество() <> 0);
	Запрос.УстановитьПараметр("ВыбранныеПредметы", ОтборПредметы);
	Запрос.УстановитьПараметр("КоличествоПовторовВыполненияЗадачиПриОшибке", КоличествоПовторовВыполненияЗадачиПриОшибке());
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция РегулярныеЗадачиКВыполнению()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОчередьРегулярныхЗадачАссистентаУправления.Период КАК Период,
	|	ОчередьРегулярныхЗадачАссистентаУправления.Задача КАК Задача,
	|	ОчередьРегулярныхЗадачАссистентаУправления.Предмет КАК Предмет,
	|	ОчередьРегулярныхЗадачАссистентаУправления.Источник КАК Источник,
	|	ОчередьРегулярныхЗадачАссистентаУправления.НеВыполнятьПосле КАК НеВыполнятьПосле,
	|	ОчередьРегулярныхЗадачАссистентаУправления.ДополнительныеПараметры КАК ДополнительныеПараметры,
	|	ЛОЖЬ КАК ЭтоОчередьТекущихЗадач,
	|	ИСТИНА КАК ЭтоОчередьРегулярныхЗадач
	|ИЗ
	|	РегистрСведений.ОчередьРегулярныхЗадачАссистентаУправления КАК ОчередьРегулярныхЗадачАссистентаУправления
	|ГДЕ
	|	ОчередьРегулярныхЗадачАссистентаУправления.Период <= &Период";
	Запрос.УстановитьПараметр("Период", ДатаКрайнейЗадачиДляВыполненияВТекущемИнтервале());
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Процедура ЗаблокироватьОчередьЗадач(ВыборкаЗадача, ОтборПредметы = Неопределено)
	
	Если ОтборПредметы <> Неопределено И ТипЗнч(ОтборПредметы) = Тип("Массив") Тогда
		БлокируемыеПредметы = ОтборПредметы;
	Иначе
		БлокируемыеПредметы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОтборПредметы);
	КонецЕсли;
	
	Если ВыборкаЗадача.ЭтоОчередьТекущихЗадач Тогда
		ЗаблокироватьОчередьТекущихЗадачКВыполнению(БлокируемыеПредметы);
	ИначеЕсли ВыборкаЗадача.ЭтоОчередьРегулярныхЗадач Тогда
		ЗаблокироватьОчередьРегулярныхЗадачКВыполнению();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаблокироватьОчередьТекущихЗадачКВыполнению(Знач ОтборПредметы = Неопределено)
	
	Если ОтборПредметы <> Неопределено И ТипЗнч(ОтборПредметы) <> Тип("Массив") Тогда
		ОтборПредметы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОтборПредметы);
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьЗадачАссистентаУправления");
	Если ОтборПредметы <> Неопределено Тогда
		Для каждого Предмет Из ОтборПредметы Цикл
			ЭлементБлокировки.УстановитьЗначение("Предмет", Предмет);
		КонецЦикла;
	КонецЕсли;
	Блокировка.Заблокировать();
	
КонецПроцедуры

Процедура ЗаблокироватьОчередьТекущихЗадачКВыполнениюПоИзмерениям(Источник, Предмет, Событие, Задача)
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьЗадачАссистентаУправления");
	ЭлементБлокировки.УстановитьЗначение("Источник", Источник);
	ЭлементБлокировки.УстановитьЗначение("Предмет", Предмет);
	ЭлементБлокировки.УстановитьЗначение("Событие", Событие);
	ЭлементБлокировки.УстановитьЗначение("Задача", Задача);
	Блокировка.Заблокировать();
	
КонецПроцедуры

Процедура ЗаблокироватьОчередьРегулярныхЗадачКВыполнению()
	
	Блокировка = Новый БлокировкаДанных;
	Блокировка.Добавить("РегистрСведений.ОчередьРегулярныхЗадачАссистентаУправления");
	Блокировка.Заблокировать();
	
КонецПроцедуры

Процедура ВыполнитьОчередьТекущихЗадач(ОтборПредметы = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущиеЗадачи = ТекущиеЗадачиКВыполнению(ОтборПредметы);
	Если ТекущиеЗадачи.Пустой() Тогда
		ОбновитьРегламентноеЗаданиеВыполнениеТекущихЗадач(Ложь);
		Возврат;
	КонецЕсли;
	
	ТекущиеЗадачиВыборка = ТекущиеЗадачи.Выбрать();
	ВремяНачалаВыполненияЗадач = ТекущаяДатаСеанса();
	
	ВыполненоЗадач = 0;
	ЗаписьЖРПередНачаломОбработкиЗадач(ТекущиеЗадачиВыборка.Количество());
	Пока ТекущиеЗадачиВыборка.Следующий() Цикл
		ВыполненоУспешно = ВыполнитьЗадачуИзОчереди(ТекущиеЗадачиВыборка, ВремяНачалаВыполненияЗадач);
		Если ВыполненоУспешно Тогда
			ВыполненоЗадач = ВыполненоЗадач + 1;
		КонецЕсли;
	КонецЦикла;
	ЗаписьЖРЗавершениеОбработкиЗадач(ВыполненоЗадач);
	
КонецПроцедуры

Процедура ВыполнитьОчередьРегулярныхЗадач()
	
	ПередНачаломВыполненияРегулярныхЗадач();
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущиеЗадачи = РегулярныеЗадачиКВыполнению();
	ТекущиеЗадачиВыборка = ТекущиеЗадачи.Выбрать();
	ВремяНачалаВыполненияЗадач = ТекущаяДатаСеанса();
	
	ВыполненоЗадач = 0;
	ЗаписьЖРПередНачаломОбработкиЗадач(ТекущиеЗадачиВыборка.Количество());
	Пока ТекущиеЗадачиВыборка.Следующий() Цикл
		ВыполненоУспешно = ВыполнитьЗадачуИзОчереди(ТекущиеЗадачиВыборка, ВремяНачалаВыполненияЗадач);
		Если ВыполненоУспешно Тогда
			ВыполненоЗадач = ВыполненоЗадач + 1;
		КонецЕсли;
	КонецЦикла;
	ЗаписьЖРЗавершениеОбработкиЗадач(ВыполненоЗадач);
	
КонецПроцедуры

Функция ВыполнитьЗадачуИзОчереди(ВыборкаЗадачи, ВремяНачалаВыполненияЗадач)
	
	ЗаписьЖРПередНачаломВыполненияЗадачи(ВыборкаЗадачи.Задача);
	
	Если ТипЗнч(ВыборкаЗадачи.Задача) = Тип("СправочникСсылка.ЗадачиАссистентаУправления") Тогда
		МодульМенеджера = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаЗадачи.Задача, "МодульМенеджера");
	Иначе
		МодульМенеджера = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МодульМенеджера) Тогда
		ЗадачаВыполненаУспешно = ВыполнитьЗадачуЧерезМенеджерАссистентаНовый(ВыборкаЗадачи);
	ИначеЕсли ТипЗнч(ВыборкаЗадачи.Задача) = Тип("СправочникСсылка.ЗадачиАссистентаУправления") Тогда
		ЗадачаВыполненаУспешно = ВыполнитьЗадачуЧерезМенеджерАссистента(ВыборкаЗадачи);
	Иначе
		ЗадачаВыполненаУспешно = ВыполнитьЗадачуЧерезМенеджерСправочника(ВыборкаЗадачи, ВремяНачалаВыполненияЗадач);
	КонецЕсли;
	
	ЗаписьЖРПослеВыполненияЗадачи(ВыборкаЗадачи.Задача);
	
	Возврат ЗадачаВыполненаУспешно;
	
КонецФункции

Функция ВыполнитьЗадачуЧерезМенеджерАссистента(ВыборкаЗадачи)
	
	ЗадачаВыполненаУспешно = Истина;
	
	Попытка
		Если ВыполненыУсловияЗадачи(ВыборкаЗадачи.Источник, ВыборкаЗадачи.Задача, ВыборкаЗадачи.Предмет) Тогда
			ВыполнитьДействияЗадачи(ВыборкаЗадачи.Источник, ВыборкаЗадачи.СобытиеИдентификатор, ВыборкаЗадачи.Предмет, ВыборкаЗадачи.Задача, ВыборкаЗадачи.ДополнительныеПараметры.Получить());
		Иначе
			УдалитьЗадачуИзОчередиВыполнения(ВыборкаЗадачи);
		КонецЕсли;
	Исключение
		ЗадачаВыполненаУспешно = Ложь;
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЗаписьЖРОшибкаВыполненияЗадачи(ВыборкаЗадачи.Задача, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		ОтразитьОшибкуВыполненияЗадачи(ВыборкаЗадачи, ИнформацияОбОшибке);
	КонецПопытки;
	
	Возврат ЗадачаВыполненаУспешно;
	
КонецФункции

Функция ВыполнитьЗадачуЧерезМенеджерАссистентаНовый(ВыборкаЗадачи)
	
	ЗадачаВыполненаУспешно = Истина;
	
	Попытка
		ДополнительныеПараметры = ВыборкаЗадачи.ДополнительныеПараметры.Получить();
		ВыполнитьДействияЗадачиНовый(
			ВыборкаЗадачи.Предмет,
			ВыборкаЗадачи.Задача,
			ВыборкаЗадачи.СобытиеИдентификатор,
			ВыборкаЗадачи.Источник,
			ДополнительныеПараметры);
	Исключение
		ЗадачаВыполненаУспешно = Ложь;
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЗаписьЖРОшибкаВыполненияЗадачи(ВыборкаЗадачи.Задача, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		ОтразитьОшибкуВыполненияЗадачи(ВыборкаЗадачи, ИнформацияОбОшибке);
	КонецПопытки;
	
	Возврат ЗадачаВыполненаУспешно;
	
КонецФункции

Функция ВыполнитьЗадачуЧерезМенеджерСправочника(ВыборкаЗадачи, ВремяНачалаВыполненияЗадач)
	
	ЗадачаВыполненаУспешно = Истина;
	ИнформацияОбОшибке = Неопределено;
	
	Задача = ВыборкаЗадачи.Задача.ПолучитьОбъект();
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("ПлановаяДатаВыполнения", ВыборкаЗадачи.Период);
	ПараметрыВыполнения.Вставить("НеВыполнятьПосле", ВыборкаЗадачи.НеВыполнятьПосле);
	ПараметрыВыполнения.Вставить("Отказ", Ложь);
	
	ДополнительныеПараметры = ВыборкаЗадачи.ДополнительныеПараметры.Получить();
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаЗадачи.НеВыполнятьПосле) И ВремяНачалаВыполненияЗадач > ВыборкаЗадачи.НеВыполнятьПосле Тогда
		ПараметрыВыполнения.Отказ = Истина;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		ЗаблокироватьОчередьЗадач(ВыборкаЗадачи, ВыборкаЗадачи.Предмет);
		
		Задача.ПередВыполнениемЗадачи(ВыборкаЗадачи.Предмет, ВыборкаЗадачи.Источник, ПараметрыВыполнения, ДополнительныеПараметры);
		
		Если ПараметрыВыполнения.Отказ <> Истина Тогда
			Задача.ВыполнитьЗадачу(ВыборкаЗадачи.Предмет, ВыборкаЗадачи.Источник, ПараметрыВыполнения, ДополнительныеПараметры);
		КонецЕсли;
		
		УдалитьЗадачуИзОчередиВыполнения(ВыборкаЗадачи);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ЗадачаВыполненаУспешно = Ложь;
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЗаписьЖРОшибкаВыполненияЗадачи(ВыборкаЗадачи.Задача, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
	КонецПопытки;
	
	ПараметрыВыполнения.Вставить("СтандартнаяОбработка", Истина);
	ПараметрыВыполнения.Вставить("ЗадачаВыполненаУспешно", ЗадачаВыполненаУспешно);
	Если ИнформацияОбОшибке <> Неопределено Тогда
		ПараметрыВыполнения.Вставить("ИнформацияОбОшибке", ИнформацияОбОшибке);
	КонецЕсли;
	
	Попытка
		Задача.ПослеВыполненияЗадачи(ВыборкаЗадачи.Предмет, ВыборкаЗадачи.Источник, ПараметрыВыполнения, ДополнительныеПараметры);
		Если НЕ ЗадачаВыполненаУспешно И ПараметрыВыполнения.СтандартнаяОбработка Тогда
			ОтразитьОшибкуВыполненияЗадачи(ВыборкаЗадачи, ИнформацияОбОшибке);
		КонецЕсли;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЗаписьЖРОшибкаВыполненияЗадачи(ВыборкаЗадачи.Задача, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
	КонецПопытки;
	
	Возврат ЗадачаВыполненаУспешно;
	
КонецФункции

Процедура ОтразитьОшибкуВыполненияЗадачи(ЗадачаИзОчереди, ИнформацияОбОшибке)
	
	Если ЗадачаИзОчереди.ЭтоОчередьТекущихЗадач Тогда
		ОтразитьОшибкуВыполненияТекущейЗадачи(ЗадачаИзОчереди, ИнформацияОбОшибке);
	КонецЕсли;
	
	Если ЗадачаИзОчереди.ЭтоОчередьРегулярныхЗадач Тогда
		ОтразитьОшибкуВыполненияРегулярнойЗадачи(ЗадачаИзОчереди, ИнформацияОбОшибке);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтразитьОшибкуВыполненияТекущейЗадачи(ЗадачаИзОчереди, ИнформацияОбОшибке)
	
	ХешТекстОшибки = ХешТекстОшибки(ИнформацияОбОшибке);
	ХешТекстОшибкиПоследнегоВыполнения = ХешТекстОшибкиПоследнегоВыполненияТекущейЗадачи(ЗадачаИзОчереди);
	
	Если ХешТекстОшибкиПоследнегоВыполнения <> Неопределено И ХешТекстОшибки = ХешТекстОшибкиПоследнегоВыполнения Тогда
		Возврат;
	КонецЕсли;
	
	НаписатьОбОшибкеВыполненияВОбсуждениях(ЗадачаИзОчереди.Задача, ЗадачаИзОчереди.Предмет, ЗадачаИзОчереди.Источник, ИнформацияОбОшибке);
	
	Попытка
		Запись = РегистрыСведений.ОчередьЗадачАссистентаУправления.СоздатьМенеджерЗаписи();
		Запись.Предмет  = ЗадачаИзОчереди.Предмет;
		Запись.Событие  = ЗадачаИзОчереди.СобытиеИдентификатор;
		Запись.Задача   = ЗадачаИзОчереди.Задача;
		Запись.Источник = ЗадачаИзОчереди.Источник;
		Запись.Прочитать();
		
		Запись.ХешТекстОшибки = ХешТекстОшибки;
		Запись.КоличествоПопыток = Запись.КоличествоПопыток + 1;
		Запись.ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		Запись.ДатаПоследнегоВыполнения = ТекущаяДатаСеанса();
		Запись.Записать();
	Исключение
		Комментарий = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
	КонецПопытки;
	
КонецПроцедуры

Процедура ОтразитьОшибкуВыполненияРегулярнойЗадачи(ЗадачаИзОчереди, ИнформацияОбОшибке)
	
	ХешТекстОшибки = ХешТекстОшибки(ИнформацияОбОшибке);
	ХешТекстОшибкиПоследнегоВыполнения = ХешТекстОшибкиПоследнегоВыполненияРегулярнойЗадачи(ЗадачаИзОчереди);
	
	Если ХешТекстОшибкиПоследнегоВыполнения <> Неопределено И ХешТекстОшибки = ХешТекстОшибкиПоследнегоВыполнения Тогда
		Возврат;
	КонецЕсли;
	
	НаписатьОбОшибкеВыполненияВОбсуждениях(ЗадачаИзОчереди.Задача, ЗадачаИзОчереди.Предмет, ЗадачаИзОчереди.Источник, ИнформацияОбОшибке);
	
	Попытка
		Запись = РегистрыСведений.ОчередьРегулярныхЗадачАссистентаУправления.СоздатьМенеджерЗаписи();
		Запись.Период   = ЗадачаИзОчереди.Период;
		Запись.Задача   = ЗадачаИзОчереди.Задача;
		Запись.Предмет  = ЗадачаИзОчереди.Предмет;
		Запись.Прочитать();
		
		Запись.ХешТекстОшибки = ХешТекстОшибки;
		Запись.Записать();
	Исключение
		Комментарий = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
	КонецПопытки;
	
КонецПроцедуры

Процедура НаписатьОбОшибкеВыполненияВОбсуждениях(Задача, Предмет, Источник, ИнформацияОбОшибке)
	
	ТекстОшибкиКороткий = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(СтрШаблон(НСтр("ru='Ошибка при выполнении задачи ""%1""'"), Задача));
	МассивСтрок.Добавить(Символы.ПС);
	Если ЗначениеЗаполнено(Источник) Тогда
		МассивСтрок.Добавить(ПолучитьНавигационнуюСсылку(Источник));
	КонецЕсли;
	Если ЗначениеЗаполнено(Источник) И ЗначениеЗаполнено(Предмет) Тогда
		МассивСтрок.Добавить(" → ");
	КонецЕсли;
	Если ЗначениеЗаполнено(Предмет) Тогда
		МассивСтрок.Добавить(ПолучитьНавигационнуюСсылку(Предмет));;
	КонецЕсли;
	МассивСтрок.Добавить(Символы.ПС);
	МассивСтрок.Добавить(ТекстОшибкиКороткий);
	
	ДанныеСообщения = ОбсужденияУНФ.НовыйДанныеСообщения();
	ДанныеСообщения.Объект = КлючОбсуждениеЖурналРаботыАссистента();
	ДанныеСообщения.Текст = СтрСоединить(МассивСтрок, "");
	ДанныеСообщения.Автор = ПользовательАссистент();
	ОбсужденияУНФ.СоздатьСообщениеОтложенно(ДанныеСообщения);
	
КонецПроцедуры

Функция ХешТекстОшибки(ИнформацияОбОшибке)
	
	ТекстОшибкиКороткий = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ХешТекстОшибки = ОбщегоНазначения.КонтрольнаяСуммаСтрокой(ТекстОшибкиКороткий, ХешФункция.MD5);
	Возврат ХешТекстОшибки;
	
КонецФункции

Функция ХешТекстОшибкиПоследнегоВыполненияТекущейЗадачи(ЗадачаИзОчереди)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОчередьЗадач.ХешТекстОшибки КАК ХешТекстОшибки
	|ИЗ
	|	РегистрСведений.ОчередьЗадачАссистентаУправления КАК ОчередьЗадач
	|ГДЕ
	|	ОчередьЗадач.Предмет = &Предмет
	|	И ОчередьЗадач.Событие = &Событие
	|	И ОчередьЗадач.Задача = &Задача
	|	И ОчередьЗадач.Источник = &Источник";
	
	Запрос.УстановитьПараметр("Предмет",  ЗадачаИзОчереди.Предмет);
	Запрос.УстановитьПараметр("Событие",  ЗадачаИзОчереди.СобытиеИдентификатор);
	Запрос.УстановитьПараметр("Задача",   ЗадачаИзОчереди.Задача);
	Запрос.УстановитьПараметр("Источник", ЗадачаИзОчереди.Источник);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ХешТекстОшибки;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ХешТекстОшибкиПоследнегоВыполненияРегулярнойЗадачи(ЗадачаИзОчереди)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОчередьЗадач.ХешТекстОшибки КАК ХешТекстОшибки
	|ИЗ
	|	РегистрСведений.ОчередьРегулярныхЗадачАссистентаУправления КАК ОчередьЗадач
	|ГДЕ
	|	ОчередьЗадач.Период = &Период
	|	И ОчередьЗадач.Задача = &Задача
	|	И ОчередьЗадач.Предмет = &Предмет";
	
	Запрос.УстановитьПараметр("Период",  ЗадачаИзОчереди.Период);
	Запрос.УстановитьПараметр("Задача",  ЗадачаИзОчереди.Задача);
	Запрос.УстановитьПараметр("Предмет", ЗадачаИзОчереди.Предмет);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ХешТекстОшибки;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура УдалитьЗадачуИзОчередиВыполнения(ЗадачаИзОчереди)
	
	Если ЗадачаИзОчереди.ЭтоОчередьТекущихЗадач Тогда
		УдалитьТекущуюЗадачуИзОчередиВыполнения(ЗадачаИзОчереди.Предмет, ЗадачаИзОчереди.СобытиеИдентификатор, ЗадачаИзОчереди.Источник, ЗадачаИзОчереди.Задача);
	КонецЕсли;
	
	Если ЗадачаИзОчереди.ЭтоОчередьРегулярныхЗадач Тогда
		УдалитьРегулярнуюЗадачуИзОчередиВыполнения(ЗадачаИзОчереди);
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьТекущуюЗадачуИзОчередиВыполнения(Предмет, Событие, Источник, Задача)
	
	ЗаписьВОчереди = РегистрыСведений.ОчередьЗадачАссистентаУправления.СоздатьМенеджерЗаписи();
	ЗаписьВОчереди.Предмет  = Предмет;
	ЗаписьВОчереди.Событие  = Событие;
	ЗаписьВОчереди.Источник = Источник;
	ЗаписьВОчереди.Задача   = Задача;
	ЗаписьВОчереди.Прочитать();
	ЗаписьВОчереди.Удалить();
	
КонецПроцедуры

Процедура УдалитьРегулярнуюЗадачуИзОчередиВыполнения(ЗадачаИзОчереди)
	
	ЗаписьВОчереди = РегистрыСведений.ОчередьРегулярныхЗадачАссистентаУправления.СоздатьМенеджерЗаписи();
	ЗаписьВОчереди.Период  = ЗадачаИзОчереди.Период;
	ЗаписьВОчереди.Задача  = ЗадачаИзОчереди.Задача;
	ЗаписьВОчереди.Предмет = ЗадачаИзОчереди.Предмет;
	ЗаписьВОчереди.Прочитать();
	ЗаписьВОчереди.Удалить();
	
КонецПроцедуры

Процедура ОтразитьУспешноеВыполнениеЗадачи(Задача, Предмет, Источник, Событие)
	
	НаборЗаписей = РегистрыСведений.ВыполненныеЗадачиАссистентаУправления.СоздатьНаборЗаписей();
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Период   = ТекущаяДатаСеанса();
	НоваяЗапись.Источник = Источник;
	НоваяЗапись.Предмет  = Предмет;
	НоваяЗапись.Событие  = Событие;
	НоваяЗапись.Задача   = Задача;
	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

Функция ДатаКрайнейЗадачиДляВыполненияВТекущемИнтервале()
	
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	СекундВЧасе = 3600;
	ШагИнтервала = 24 / КоличествоЗапусковРегулярныхЗадачВСутки() * СекундВЧасе;
	НачалоИнтервала = НачалоДня(ТекущаяДатаСеанса);
	
	Интервалы = Новый Массив;
	Для ТекущийИнтервал = 1 По КоличествоЗапусковРегулярныхЗадачВСутки() Цикл
		НачалоИнтервала = НачалоИнтервала + ШагИнтервала;
		Интервалы.Добавить(НачалоИнтервала);
	КонецЦикла;
	
	Для каждого Интервал Из Интервалы Цикл
		Если ТекущаяДатаСеанса <= Интервал Тогда
			Возврат Интервал;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТекущаяДатаСеанса;
	
КонецФункции

Функция ЕстьЗаписьОчередиТекущихЗадач(Источник, Предмет, Событие, Задача)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА КАК ЕстьЗапись
	|ИЗ
	|	РегистрСведений.ОчередьЗадачАссистентаУправления КАК ОчередьЗадачАссистентаУправления
	|ГДЕ
	|	ОчередьЗадачАссистентаУправления.Предмет = &Предмет
	|	И ОчередьЗадачАссистентаУправления.Задача = &Задача
	|	И ОчередьЗадачАссистентаУправления.Событие = &Событие
	|	И ОчередьЗадачАссистентаУправления.Источник = &Источник";
	
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.УстановитьПараметр("Задача", Задача);
	Запрос.УстановитьПараметр("Событие", Событие);
	Запрос.УстановитьПараметр("Источник", Источник);
	
	Результат = Запрос.Выполнить();
	
	Возврат НЕ Результат.Пустой();
	
КонецФункции

#КонецОбласти

#Область ПользовательАссистент

Функция ИмяПользователяАссистента()
	
	Возврат НСтр("ru='АссистентУправленияНашейФирмой'");
	
КонецФункции

Функция ПолноеИмяПользователяАссистента()
	
	Возврат НСтр("ru='Даша (ассистент УНФ)'");
	
КонецФункции

Функция НовыйСлужебныйПользовательАссистента()
	
	Если НЕ СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		ВызватьИсключение НСтр("ru='Информационная база не зарегистрирована в Системе взаимодействия.'");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		ОписаниеПользователяИБ = Пользователи.НовоеОписаниеПользователяИБ();
		ОписаниеПользователяИБ.Имя = ИмяПользователяАссистента();
		ОписаниеПользователяИБ.ПолноеИмя = ПолноеИмяПользователяАссистента();
		ОписаниеПользователяИБ.АутентификацияСтандартная = Истина;
		ОписаниеПользователяИБ.ПоказыватьВСпискеВыбора = Ложь;
		ОписаниеПользователяИБ.Вставить("Действие", "Записать");
		ОписаниеПользователяИБ.Вставить("ВходВПрограммуРазрешен", Истина);
		ОписаниеПользователяИБ.ЗапрещеноИзменятьПароль = Истина;
		ОписаниеПользователяИБ.Роли = Новый Массив;
		ОписаниеПользователяИБ.Роли.Добавить(Метаданные.Роли.ИспользованиеМетодовПровайдераТелефонии.Имя);
		ОписаниеПользователяИБ.Пароль = Пользователи.СоздатьПароль();
		
		НовыйПользователь = Справочники.Пользователи.СоздатьЭлемент();
		НовыйПользователь.Наименование = ОписаниеПользователяИБ.ПолноеИмя;
		НовыйПользователь.Служебный = Истина;
		НовыйПользователь.ДополнительныеСвойства.Вставить("ОписаниеПользователяИБ", ОписаниеПользователяИБ);
		НовыйПользователь.Записать();
		
		ПользовательСВ = СистемаВзаимодействия.СоздатьПользователя(ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(НовыйПользователь.ИдентификаторПользователяИБ));
		ПользовательСВ.Картинка = БиблиотекаКартинок.АссистентУправления;
		ПользовательСВ.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;
	
	Возврат НовыйПользователь.Ссылка;
	
КонецФункции

Функция НовыйСлужебныйПользовательСВАссистента()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПользовательИБАссистента = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПользовательАссистент(), "ИдентификаторПользователяИБ"));
	
	ПользовательСВ = СистемаВзаимодействия.СоздатьПользователя(ПользовательИБАссистента);
	ПользовательСВ.Картинка = БиблиотекаКартинок.АссистентУправления;
	ПользовательСВ.Записать();
	
	Возврат ПользовательСВ;
	
КонецФункции

#КонецОбласти

#Область ОбработкаТекстовОбсуждений

Процедура УдалитьЛишниеСимволы(Текст)
	
	Текст = НРег(Текст);
	Текст = СокрЛП(Текст);
	
КонецПроцедуры

Функция ЭтоКодОтветаДа(Знач Текст)
	
	УдалитьЛишниеСимволы(Текст);
	
	Если Текст = "+"
		ИЛИ Текст = "да"
		ИЛИ Текст = "ок"
		ИЛИ Текст = "хорошо"
		ИЛИ Текст = "давай"
		ИЛИ Текст = "if" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ЭтоКодОтветаОтмена(Знач Текст)
	
	УдалитьЛишниеСимволы(Текст);
	
	Если Текст = "-"
		ИЛИ Текст = "нет"
		ИЛИ СтрНайти(Текст, " ") = 0 И СтрНачинаетсяС(Текст, "отмен")
		ИЛИ Текст = "ytn" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ОтветНаПроизвольнуюРеплику(Знач Текст)
	
	ГенераторЧисел = Новый ГенераторСлучайныхЧисел;
	
	УдалитьЛишниеСимволы(Текст);
	Слова = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(Текст);
	МаскиСлов = РазобратьМассивСловПоМаскам(Слова);
	
	Ответ = "";
	
	// АПК:1223-вкл
	Если ТекстСодержитПоложительныйСмайлик(Текст) Тогда
		Ответ = ";-)";
	ИначеЕсли МаскиСлов.Найти("спсб") <> Неопределено // АПК:1036
		Или МаскиСлов.Найти("блгдр") <> Неопределено  // АПК:1036
		Или Слова.Найти("мерси") <> Неопределено
		Или МаскиСлов.Найти("спс") <> Неопределено    // АПК:1036
		И МаскиСлов.Количество() = 1 Тогда
		ВариантыФраз = Новый Массив;
		ВариантыФраз.Добавить(НСтр("ru='Благодарю за Ваше благодарю!'"));
		ВариантыФраз.Добавить(НСтр("ru=':-)'"));
		ВариантыФраз.Добавить(НСтр("ru='Приятно с Вами работать'"));
		ВариантыФраз.Добавить(НСтр("ru='Не забудьте рассказать обо мне своим друзьям!'"));
		ВариантыФраз.Добавить(НСтр("ru='Обращайтесь, всегда рада помочь'"));
		ВариантыФраз.Добавить(НСтр("ru='Без проблем'"));
		ВариантыФраз.Добавить(НСтр("ru='Не стоит благодарности, это моя работа'"));
		Ответ = СтрВыбрать(ГенераторЧисел, ВариантыФраз);
	ИначеЕсли Слова.Найти("даша") <> Неопределено Тогда
		Ответ = НСтр("ru='Д.а.ш.а. — дружелюбный ассистент шустрой автоматизации :)'");
	ИначеЕсли СтрЗаканчиваетсяНа(Текст, "?") Тогда
		ВариантыФраз = Новый Массив;
		ВариантыФраз.Добавить(НСтр("ru='Я бы и сама хотела знать ответ на такой вопрос'"));
		ВариантыФраз.Добавить(НСтр("ru='Возможно, стоит задать этот вопрос другим ассистентам?'"));
		ВариантыФраз.Добавить(НСтр("ru='Кажется мы обговаривали зону моей ответственности при приеме на работу?'"));
		Ответ = СтрВыбрать(ГенераторЧисел, ВариантыФраз);
	ИначеЕсли СтрЗаканчиваетсяНа(Текст, "!") Тогда
		ВариантыФраз = Новый Массив;
		Ответ = СтрШаблон(
			НСтр("ru='%1, что %2 у Вас %3'"),
			СтрВыбрать(ГенераторЧисел, НСтр("ru='Хорошо'"), НСтр("ru='Как же хорошо'"), НСтр("ru='Прекрасно'")),
			СтрВыбрать(ГенераторЧисел, НСтр("ru='вызываю'"), НСтр("ru='моя работа вызывает'"), НСтр("ru='мой труд вызывает'")),
			СтрВыбрать(ГенераторЧисел, НСтр("ru='такие эмоции'"), НСтр("ru='живой интерес'")));
	ИначеЕсли Слова.Количество() <= 1 Тогда
		ВариантыФраз = Новый Массив;
		ВариантыФраз.Добавить(НСтр("ru='Я прямо-таки не знаю как это прокомментировать'"));
		ВариантыФраз.Добавить(НСтр("ru='А?'"));
		ВариантыФраз.Добавить(НСтр("ru='Если бы Вы раскрыли свою мысль поподробнее..'"));
		ВариантыФраз.Добавить(НСтр("ru='Я дико извиняюсь, Вы еще спите или я уже обедаю?'"));
		Ответ = СтрВыбрать(ГенераторЧисел, ВариантыФраз);
	Иначе
		ВариантыФраз = Новый Массив;
		ВариантыФраз.Добавить(НСтр("ru='Эх, не понимаем мы друг друга..'"));
		ВариантыФраз.Добавить(НСтр("ru='А посмотрите видео презентацию, где мои коллеги рассказывают о моих способностях'"));
		ВариантыФраз.Добавить(НСтр("ru='Мне нужно немного подумать'"));
		ВариантыФраз.Добавить(НСтр("ru='Кажется, пора в отпуск'"));
		ВариантыФраз.Добавить(СтрШаблон(
			НСтр("ru='%1, чтобы %2'"),
			СтрВыбрать(ГенераторЧисел, НСтр("ru='Мне нужно время'"), НСтр("ru='Вероятно, нужно еще походить на курсы'")),
			СтрВыбрать(ГенераторЧисел, НСтр("ru='я смогла выполнять такие поручения'"), НСтр("ru='я научилась отвечать и на такие вопросы'"))));
		Ответ = СтрВыбрать(ГенераторЧисел, ВариантыФраз);
	КонецЕсли;
	// АПК:1223-выкл
	
	Возврат Ответ;
	
КонецФункции

Функция ТекстСодержитПоложительныйСмайлик(Текст)
	
	Возврат СтрНайти(Текст, ":-)") <> 0 ИЛИ СтрНайти(Текст, ":)") <> 0 ИЛИ СтрНайти(Текст, ";)") <> 0 ИЛИ СтрНайти(Текст, ";-)") <> 0;
	
КонецФункции

Функция СтрВыбрать(ГенераторСлучайныхЧисел, Значение1 = "", Значение2 = "", Значение3 = "", Значение4 = "", Значение5 = "", Значение6 = "", Значение7 = "", Значение8 = "", Значение9 = "", Значение10 = "")
	
	Если ТипЗнч(Значение1) = Тип("Массив") Тогда
		МассивСтрок = Значение1;
	Иначе
		МассивСтрок = Новый Массив;
		Если Значение1 <> "" Тогда
			МассивСтрок.Добавить(Значение1);
		КонецЕсли;
		Если Значение2 <> "" Тогда
			МассивСтрок.Добавить(Значение2);
		КонецЕсли;
		Если Значение3 <> "" Тогда
			МассивСтрок.Добавить(Значение3);
		КонецЕсли;
	КонецЕсли;
	
	Возврат МассивСтрок.Получить(ГенераторСлучайныхЧисел.СлучайноеЧисло(0, МассивСтрок.ВГраница()));
	
КонецФункции

Функция РазобратьМассивСловПоМаскам(Слова)
	
	МаскиСлов = ОбщегоНазначения.СкопироватьРекурсивно(Слова, Ложь);
	Для Итератор = 0 По МаскиСлов.ВГраница() Цикл
		ПолучитьМаскуСлова(МаскиСлов[Итератор]);
	КонецЦикла;
	
	Возврат МаскиСлов;
	
КонецФункции

Процедура ПолучитьМаскуСлова(Текст)
	
	УдаляемыеСимволы = УдаляемыеСимволы();
	Для каждого УдаляемыйСимвол Из УдаляемыеСимволы Цикл
		Текст = СтрЗаменить(Текст, УдаляемыйСимвол, "");
	КонецЦикла;
	
КонецПроцедуры

Функция УдаляемыеСимволы()
	
	УдаляемыеСимволы = Новый Массив;
	УдаляемыеСимволы.Добавить("а");
	УдаляемыеСимволы.Добавить("е");
	УдаляемыеСимволы.Добавить("ё"); // АПК:163 используется при анализе введенного пользователем текста.
	УдаляемыеСимволы.Добавить("и");
	УдаляемыеСимволы.Добавить("о");
	УдаляемыеСимволы.Добавить("у");
	УдаляемыеСимволы.Добавить("ы");
	УдаляемыеСимволы.Добавить("э");
	УдаляемыеСимволы.Добавить("ю");
	УдаляемыеСимволы.Добавить("я");
	
	Возврат УдаляемыеСимволы;
	
КонецФункции

Функция РаспознатьДействиеНадСообщением(ТекстПользователя)
	
	Если ЭтоКодОтветаДа(ТекстПользователя) Тогда
		Возврат АссистентУправленияКлиентСервер.КодДействияСообщенияВыполнитьДействие();
	ИначеЕсли ЭтоКодОтветаОтмена(ТекстПользователя) Тогда
		Возврат АссистентУправленияКлиентСервер.КодДействияСообщенияОтмена();
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

Функция ИсточникПредмета(ТипПредмета)
	
	ТипыПредметов = Новый Соответствие;
	
	Источники = Новый Массив;
	АссистентУправленияПереопределяемый.ПриОпределенииИсточников(Источники);
	
	Для каждого Источник Из Источники Цикл
		
		Модуль = ОбщегоНазначения.ОбщийМодуль(Источник);
		Модуль.ПриОпределенииТиповПредметовСобытий(ТипыПредметов);
		
		Если ТипыПредметов.Получить(ТипПредмета) <> Неопределено Тогда
			Возврат ОбщегоНазначения.ОбщийМодуль(Источник);
		КонецЕсли;
		
	КонецЦикла;
	
	ВызватьИсключение СтрШаблон(НСтр("ru='Для типа предмета ""%1"" не определен модуль выполнения.'"), ТипПредмета);
	
КонецФункции

Функция ОписаниеДействияПоПредмету(Действия, ДействиеИдентификатор, Предмет)
	
	НайденныеДействия = Действия.НайтиСтроки(Новый Структура("Идентификатор,Предмет", ДействиеИдентификатор, Предмет));
	
	Если НайденныеДействия.Количество() > 1 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru='Обнаружены неуникальные действия по идентификатору: ""%1"", Предмет: ""%2""'"), ДействиеИдентификатор, Предмет);
	КонецЕсли;
	
	Если НайденныеДействия.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат НайденныеДействия[0];
	
КонецФункции

Функция ОписаниеОбщегоДействия(Действия, ДействиеИдентификатор)
	
	НайденныеДействия = Действия.НайтиСтроки(Новый Структура("Идентификатор,Общее", ДействиеИдентификатор, Истина));
	
	Если НайденныеДействия.Количество() > 1 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru='Обнаружены неуникальные общие действия по идентификатору: ""%1""'"), ДействиеИдентификатор);
	КонецЕсли;
	
	Если НайденныеДействия.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат НайденныеДействия[0];
	
КонецФункции

Функция ОписаниеДействия(Предмет, ДействиеИдентификатор)
	
	Действия = НовыйТаблицаДействий();
	ПриОпределенииДействий(Действия);
	
	ОписаниеДействия = НайтиОписаниеДействия(Действия, Предмет, ДействиеИдентификатор);
	
	Если ОписаниеДействия = Неопределено Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru='Действие по идентификатору не обнаружено: ""%1"", Предмет: ""%2""'"), ДействиеИдентификатор, Предмет);
	КонецЕсли;
	
	Возврат ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ОписаниеДействия);
	
КонецФункции

Функция ОписаниеСобытия(Предмет, СобытиеИдентификатор)
	
	События = НовыйТаблицаСобытий();
	ПриОпределенииСобытий(События);
	
	НайденныеСобытия = События.НайтиСтроки(Новый Структура("Предмет,Идентификатор", Предмет, СобытиеИдентификатор));
	
	Если НайденныеСобытия.Количество() = 0 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru='Событие по идентификатору не обнаружено: ""%1"", Событие: ""%2""'"), СобытиеИдентификатор);
	КонецЕсли;
	
	Возврат ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(НайденныеСобытия[0]);
	
КонецФункции

Процедура ВыполнитьДействияЗадачи(Источник, СобытиеИдентификатор, Предмет, Задача, ДополнительныеПараметрыСобытия = Неопределено) Экспорт
	
	Действие = ОписаниеДействия(Задача.ТипПредмета, Задача.ДействиеИдентификатор);
	Событие = ОписаниеСобытия(Задача.ТипПредмета, СобытиеИдентификатор);
	
	ЗначенияЗаполнения = Справочники.ЗадачиАссистентаУправления.ЗначенияЗаполнения(Задача);
	
	НачатьТранзакцию();
	Попытка
		
		ЗаблокироватьОчередьТекущихЗадачКВыполнениюПоИзмерениям(Источник, Предмет, СобытиеИдентификатор, Задача);
		Если НЕ ЕстьЗаписьОчередиТекущихЗадач(Источник, Предмет, СобытиеИдентификатор, Задача) Тогда
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		
		Если ДополнительныеПараметрыСобытия = Неопределено Тогда
			ДополнительныеПараметрыСобытия = Новый Структура;
		КонецЕсли;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ЗадачаАссистентаУправления", Задача);
		ДополнительныеПараметры.Вставить("Источник", Источник);
		ДополнительныеПараметры.Вставить("Событие", СобытиеИдентификатор);
		ДополнительныеПараметры.Вставить("ДополнительныеПараметрыСобытия", ДополнительныеПараметрыСобытия);
		
		Результат = Новый Структура;
		Результат.Вставить("Успешно", Истина);
		Результат.Вставить("ДействиеВыполнено", Истина);
		Результат.Вставить("ТекстОшибки", "");
		Результат.Вставить("ДанныеСообщений", Новый Массив);
		Результат.Вставить("Отказ", Ложь);
		
		МодульСобытия = ОбщегоНазначения.ОбщийМодуль(Событие.Модуль);
		МодульДействия = ОбщегоНазначения.ОбщийМодуль(Действие.Модуль);
		
		МодульСобытия.ПередОбработкойСобытия(Предмет, СобытиеИдентификатор, Действие.Идентификатор, ЗначенияЗаполнения, ДополнительныеПараметры, Результат);
		Если НЕ Результат.Отказ Тогда
			МодульДействия.ВыполнитьДействие(Предмет, Действие, ЗначенияЗаполнения, ДополнительныеПараметры, Результат);
			МодульСобытия.ПослеОбработкиСобытия(Предмет, СобытиеИдентификатор, Действие.Идентификатор, ЗначенияЗаполнения, ДополнительныеПараметры, Результат);
		КонецЕсли;
		
		НужноОтправитьСообщение = НЕ ЗначениеЗаполнено(Результат.ТекстОшибки) И Результат.ДанныеСообщений.Количество() <> 0;
		Если НужноОтправитьСообщение Тогда
			ДобавитьСообщениеОбсужденийПослеВыполненияЗадачи(Предмет, Результат.ДанныеСообщений, Задача);
		КонецЕсли;
		
		Если Результат.Успешно ИЛИ Результат.Отказ Тогда
			ОтразитьУспешноеВыполнениеЗадачи(Задача, Предмет, Источник, СобытиеИдентификатор);
			УдалитьТекущуюЗадачуИзОчередиВыполнения(Предмет, СобытиеИдентификатор, Источник, Задача);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ВызватьИсключение ТекстОшибки;
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ВыполнитьДействияЗадачиНовый(Предмет, Задача, Событие, Источник, ДополнительныеПараметры = Неопределено) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		ЗаблокироватьОчередьТекущихЗадачКВыполнениюПоИзмерениям(Источник, Предмет, Событие, Задача);
		Если НЕ ЕстьЗаписьОчередиТекущихЗадач(Источник, Предмет, Событие, Задача) Тогда
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		
		Контекст = Новый Структура;
		Контекст.Вставить("Предмет",  Предмет);
		Контекст.Вставить("Задача",   Задача);
		Контекст.Вставить("Событие",  Событие);
		Контекст.Вставить("Источник", Источник);
		
		ПараметрыЗадачиАссистента = ПараметрыЗадачиАссистента(Задача);
		
		Результат = Новый Структура;
		Результат.Вставить("Успешно", Истина);
		Результат.Вставить("ДействиеВыполнено", Истина);
		Результат.Вставить("ТекстОшибки", "");
		Результат.Вставить("ДанныеСообщений", Новый Массив);
		Результат.Вставить("Отказ", Ложь);
		
		МодульМенеджера = ОбщегоНазначения.ОбщийМодуль(Задача.МодульМенеджера);
		МодульМенеджера.ВыполнитьДействие(Задача.ДействиеИдентификатор, Контекст, ПараметрыЗадачиАссистента, ДополнительныеПараметры, Результат);
		
		НужноОтправитьСообщение = НЕ ЗначениеЗаполнено(Результат.ТекстОшибки) И Результат.ДанныеСообщений.Количество() <> 0;
		Если НужноОтправитьСообщение Тогда
			ДобавитьСообщениеОбсужденийПослеВыполненияЗадачи(Предмет, Результат.ДанныеСообщений, Задача);
		КонецЕсли;
		
		Если Результат.Успешно ИЛИ Результат.Отказ Тогда
			ОтразитьУспешноеВыполнениеЗадачи(Задача, Предмет, Источник, Событие);
			УдалитьТекущуюЗадачуИзОчередиВыполнения(Предмет, Событие, Источник, Задача);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ВызватьИсключение ТекстОшибки;
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ДобавитьСообщениеОбсужденийПослеВыполненияЗадачи(Предмет, ДанныеСообщений, Задача)
	
	Для каждого ДанныеСообщения Из ДанныеСообщений Цикл
		Если НЕ ЗначениеЗаполнено(ДанныеСообщения.Объект) Тогда
			ДанныеСообщения.Объект = Предмет;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ДанныеСообщения.Автор) Тогда
			ДанныеСообщения.Автор = ПользовательАссистент();
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ДанныеСообщения.Получатель) Тогда
			
			ОповещениеЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Задача, "СпособОповещения,ПользовательДляОповещения");
			Если ОповещениеЗадачи.СпособОповещения = Перечисления.СпособОповещенияАссистентаУправления.СообщениеКонтекстногоОбсужденияОтветственному Тогда
				ДанныеСообщения.Получатель = ПолучитьОтветственного(Предмет);
			ИначеЕсли ОповещениеЗадачи.СпособОповещения = Перечисления.СпособОповещенияАссистентаУправления.СообщениеКонтекстногоОбсужденияПользователю Тогда
				ДанныеСообщения.Получатель = ОповещениеЗадачи.ПользовательДляОповещения;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ТипЗнч(ДанныеСообщения.Данные) = Тип("Структура") Тогда
			
			Если ДанныеСообщения.Данные.Количество() = 0 Тогда
				ДанныеСообщения.Данные = Неопределено;
			Иначе
				ДанныеСообщения.Данные = СоздатьJSONИзСтруктуры(ДанныеСообщения.Данные);
			КонецЕсли;
			
		КонецЕсли;
		
		ОбсужденияУНФ.СоздатьСообщениеОтложенно(ДанныеСообщения);
	КонецЦикла;
	
КонецПроцедуры

Функция СправочникиЗадачиАссистента()
	
	Результат = Новый Массив;
	Для каждого Тип Из Метаданные.ОпределяемыеТипы.ЗадачиАссистентаУправления.Тип.Типы() Цикл
		Результат.Добавить(Метаданные.НайтиПоТипу(Тип));
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция Менеджеры()
	
	Менеджеры = Новый Массив;
	АссистентУправленияПереопределяемый.ПриОпределенииМенеджеров(Менеджеры);
	Возврат Менеджеры;
	
КонецФункции

Функция НастройкиИспользования()
	
	Настройки = Новый Структура;
	Настройки.Вставить("ПроверкаРегистраСведенийПередЗаписью", Ложь);
	Настройки.Вставить("ПроверкаРегистраСведенийПриЗаписи", Ложь);
	Настройки.Вставить("ПроверкаДокументаПередЗаписью", Ложь);
	Настройки.Вставить("ПроверкаДокументаПриЗаписи", Ложь);
	Возврат Настройки;
	
КонецФункции

Функция НастройкиМенеджера(Менеджер)
	
	Настройки = НастройкиИспользования();
	Менеджер.ПриОпределенииНастроек(Настройки);
	Возврат Настройки;
	
КонецФункции

Функция МенеджерСправочникаЗадач(СправочникЗадач)
	
	Возврат ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(СправочникЗадач.ПолноеИмя());
	
КонецФункции

Функция ПараметрыЗадачиАссистента(ЗадачаАссистента) Экспорт
	
	ПараметрыЗадачи = Новый Структура;
	Для каждого ПараметрЗадачи Из ЗадачаАссистента.ЗначенияЗаполнения Цикл
		ПараметрыЗадачи.Вставить(ПараметрЗадачи.Параметр, ПараметрЗадачи.Значение.Получить());
	КонецЦикла;
	Возврат ПараметрыЗадачи;
	
КонецФункции

Функция ВключенныеЗадачи(Менеджер, Событие)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиАссистентаУправления.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЗадачиАссистентаУправления КАК ЗадачиАссистентаУправления
	|ГДЕ
	|	ЗадачиАссистентаУправления.МодульМенеджера = &МодульМенеджера
	|	И ЗадачиАссистентаУправления.События.СобытиеИдентификатор = &Событие
	|	И ЗадачиАссистентаУправления.Используется
	|	И НЕ ЗадачиАссистентаУправления.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("МодульМенеджера", Менеджер);
	Запрос.УстановитьПараметр("Событие", Событие);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	ЗадачиАссистента = Новый Массив;
	Пока Результат.Следующий() Цикл
		ЗадачиАссистента.Добавить(Результат.Ссылка);
	КонецЦикла;
	
	Возврат ЗадачиАссистента;
	
КонецФункции

Процедура ПроверитьТипыПараметровКоллекции(ИмяПроцедуры, КоллекцияПараметров, ОжидаемыеТипыПараметров)
	
	Для каждого СтрокаПараметров Из КоллекцияПараметров Цикл
		Для каждого ПроверяемыйПараметр Из ОжидаемыеТипыПараметров Цикл
			ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
				ИмяПроцедуры, ПроверяемыйПараметр.Ключ, СтрокаПараметров[ПроверяемыйПараметр.Ключ], ПроверяемыйПараметр.Значение);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ЕстьВключенныеЗадачиСправочника(СправочникЗадач)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗадачиАссистента.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЗадачиАссистентаУправления КАК ЗадачиАссистента
	|ГДЕ
	|	ЗадачиАссистента.Используется
	|	И НЕ ЗадачиАссистента.ПометкаУдаления";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.ЗадачиАссистентаУправления", СправочникЗадач.ПолноеИмя());
	Результат = Запрос.Выполнить();
	
	Возврат НЕ Результат.Пустой();
	
КонецФункции

Процедура ДобавитьДополнительныеСвойства(Объект)
	
	Если НЕ Объект.ДополнительныеСвойства.Свойство("АссистентУправления") Тогда
		Объект.ДополнительныеСвойства.Вставить("АссистентУправления", Новый Структура);
	КонецЕсли;
	
КонецПроцедуры

#Область РаботаСJSON

// Функция преобразования для платформенной процедуры глобального контекста ЗаписатьJSON(), см. СоздатьJSONИзСтруктуры()
//
Функция ПреобразоватьЗначениеJSON(Свойство, Значение, ДополнительныеПараметры, Отказ) Экспорт
	
	Если НЕ ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Значение)) Тогда
		Возврат Значение;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("type", Значение.Метаданные().ПолноеИмя());
	Результат.Вставить("guid", Строка(Значение.УникальныйИдентификатор()));
	
	Возврат Результат;
	
КонецФункции

// Функция восстановления для платформенной процедуры глобального контекста ПрочитатьJSON(), см. ПрочитатьJSONВСтруктуру()
//
Функция ВосстановитьЗначениеJSON(Свойство, Значение, ДополнительныеПараметры) Экспорт
	
	Если Свойство <> Неопределено И ТипЗнч(Значение) = Тип("Структура") Тогда
		
		Если Значение.Количество() = 2 И Значение.Свойство("type") И Значение.Свойство("guid") Тогда
			
			Возврат ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Значение.type).ПолучитьСсылку(Новый УникальныйИдентификатор(Значение.guid));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

Функция ПрочитатьJSONВСтруктуру(ТекстJSON)
	
	Если ТекстJSON = "" Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ТекстJSON);
	
	ПараметрыJSON = ПрочитатьJSON(ЧтениеJSON,,,, "ВосстановитьЗначениеJSON", АссистентУправления);
	
	ЧтениеJSON.Закрыть();
	
	Возврат ПараметрыJSON;
	
КонецФункции

Функция СоздатьJSONИзСтруктуры(Параметры)
	
	Если Параметры.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	
	ЗаписатьJSON(ЗаписьJSON, Параметры,, "ПреобразоватьЗначениеJSON", АссистентУправления);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

#КонецОбласти

#КонецОбласти
