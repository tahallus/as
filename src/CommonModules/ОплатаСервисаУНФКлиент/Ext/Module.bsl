////////////////////////////////////////////////////////////////////////////////
// Подсистема "Оплата сервиса УНФ" (клиент).
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура ОбработатьКомандуОплаты(СпособОплаты, СчетПоставщика, ПечатнаяФормаТабличногоДокумента, Организация) Экспорт
	
	Если СпособОплаты = "ОплатаНаличными" Тогда
		ИдентификаторПечатнойФормы = "ПечатнаяФорма";
		НазваниеПечатнойФормы = НСтр("ru = 'Счет поставщика'");
		
		КоллекцияПечатныхФорм = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(ИдентификаторПечатнойФормы);
		ПечатнаяФорма = УправлениеПечатьюКлиент.ОписаниеПечатнойФормы(КоллекцияПечатныхФорм, ИдентификаторПечатнойФормы);
		ПечатнаяФорма.СинонимМакета = НазваниеПечатнойФормы;
		ПечатнаяФорма.ТабличныйДокумент = ПечатнаяФормаТабличногоДокумента;
		ПечатнаяФорма.ИмяФайлаПечатнойФормы = НазваниеПечатнойФормы;
		
		УправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм);
		
	ИначеЕсли СпособОплаты = "ОплатаСРасчетногоСчетаОтправка" Тогда
		оп = Новый ОписаниеОповещения("ОповещениеВыбораСчета", ЭтотОбъект, Новый Структура("СчетПоставщика,Файл", СчетПоставщика, Истина));
		РегламентированнаяОтчетностьУСНКлиент.ПолучитьБанковскийСчетДляУплатыНалога(оп, Организация);
		
	ИначеЕсли СпособОплаты = "ОплатаСРасчетногоСчетаРаспечатать" Тогда
		оп = Новый ОписаниеОповещения("ОповещениеВыбораСчета", ЭтотОбъект, Новый Структура("СчетПоставщика,Файл", СчетПоставщика, Ложь));
		РегламентированнаяОтчетностьУСНКлиент.ПолучитьБанковскийСчетДляУплатыНалога(оп, Организация);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ОповещениеВыбораСчета(БанковскийСчет, Параметры) Экспорт
	Если НЕ ЗначениеЗаполнено(БанковскийСчет) Тогда
		Возврат;
	КонецЕсли;
	БанковскийСчетПоУмолчанию = БанковскийСчет;
	
	ПлатежноеПоручение = ОплатаСервисаУНФВызовСервера.ЗаполнитьБанковскийСчетИСоздатьПлатежноеПоручение(Параметры.СчетПоставщика, БанковскийСчетПоУмолчанию);
	Если Параметры.Файл Тогда
		СписокВыгрузки = Новый СписокЗначений;
		СписокВыгрузки.Добавить(ПлатежноеПоручение);
		
		ПараметрыОткрытия = Новый Структура("СписокПлатежек, БанковскийСчет", СписокВыгрузки, БанковскийСчетПоУмолчанию);
		ОткрытьФорму("Обработка.КлиентБанк.Форма.СохранениеПлатежек", ПараметрыОткрытия,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(ПлатежноеПоручение);
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.ПлатежноеПоручение", "ПлатежноеПоручение", МассивОбъектов, ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры


#КонецОбласти

