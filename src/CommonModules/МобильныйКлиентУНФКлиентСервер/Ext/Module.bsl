#Область ПрограммныйИнтерфейс

// Возвращает адрес публикации мобильного клиента в Google Play.
// 
// Параметры:
// 	ДляБраузера - Булево - сформировать ссылку для браузера настольного компьютера.
// Возвращаемое значение:
// 	Строка - адрес публикации мобильного клиента в Google Play.
Функция ПолучитьАдресПубликацииМобильногоКлиентаВGooglePlay(ДляБраузера = Истина) Экспорт

	Если ДляБраузера Тогда
		Возврат "https://play.google.com/store/apps/details?id=com.e1c.SmallBusiness";
	Иначе
		Возврат "market://details?id=com.e1c.SmallBusiness";
	КонецЕсли;

КонецФункции

// Возвращает адрес публикации мобильного клиента в App Store.
// Параметры:
// 	ДляБраузера - Булево - сформировать ссылку для браузера настольного компьютера.
// Возвращаемое значение:
// 	Строка - адрес публикации мобильного клиента в App Store.
Функция ПолучитьАдресПубликацииМобильногоКлиентаВAppStore(ДляБраузера = Истина) Экспорт

	Если ДляБраузера Тогда
		Протокол = "https";
	Иначе
		Протокол = "itms-apps";
	КонецЕсли;

	ПутьКПриложению = "1%D1%81-%D1%83%D0%BD%D1%84-%D0%B2-%D0%BE%D0%B1%D0%BB%D0%B0%D0%BA%D0%B5";

	Возврат СтрШаблон("%1://apps.apple.com/us/app/%2/id1440287699?l=ru&ls=1", Протокол, ПутьКПриложению);

КонецФункции

// Возвращает признак того, что мобильный клиент запущен на Android.
//
// Возвращаемое значение:
// 	Булево - признак того, что мобильный клиент запущен на Android.
Функция ЭтоAndroid() Экспорт

	СисИнфо = Новый СистемнаяИнформация;
	Возврат (СтрНайти(СисИнфо.ВерсияОС, "Android") > 0);

КонецФункции

// Возвращает признак того, что мобильный клиент запущен на iOS.
//
// Возвращаемое значение:
// 	Булево - признак того, что мобильный клиент запущен на iOS.
Функция ЭтоiOS() Экспорт // АПК:216 слово содержит и русские и английские символы.

	СисИнфо = Новый СистемнаяИнформация;
	Возврат (СтрНайти(СисИнфо.ВерсияОС, "pple") > 0);

КонецФункции

// Это большой экран или планшет.
// 
// Возвращаемое значение:
//  Булево - это большой экран или планшет
Функция ЭтоБольшойЭкранИлиПланшет() Экспорт

	ИнформацияЭкрана = ПолучитьИнформациюЭкрановКлиента();
	Если ИнформацияЭкрана <> Неопределено Тогда
		Разрешение = ИнформацияЭкрана[0].DPI;
		Высота = ИнформацияЭкрана[0].Высота;
		Ширина = ИнформацияЭкрана[0].Ширина;
		РазмерЭкрана = Sqrt((Высота / Разрешение * Высота / Разрешение) + (Ширина / Разрешение * Ширина / Разрешение));
		ЭтоПланшет = РазмерЭкрана >= 7;
		Если Ширина >= 1280 Или ЭтоПланшет Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;

	Возврат Ложь;

КонецФункции

#КонецОбласти