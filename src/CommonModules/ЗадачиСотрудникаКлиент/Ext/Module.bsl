
#Область ПрограммныйИнтерфейс

// См. ОбщегоНазначенияКлиентПереопределяемый.ПослеНачалаРаботыСистемы
//
Процедура ПослеНачалаРаботыСистемы() Экспорт
	
	ПодключитьОбработчикФормированияКомандОбсуждений();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриФормированииКоманд(Параметры, Команды, КомандаПОУмолчанию, ДополнительныеПараметры) Экспорт
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("Сообщение", Параметры.Сообщение);
	ПараметрыОбработчика.Вставить("Обсуждение", Параметры.Обсуждение);
	
	Обработчик = Новый ОписаниеОповещения("ОбработкаКомандыСоздатьЗадачу", ЗадачиСотрудникаКлиент, ПараметрыОбработчика);
	Команда = Новый ОписаниеКомандыСистемыВзаимодействия(Обработчик, НСтр("ru='Создать задачу'"));
	Команда.Картинка = БиблиотекаКартинок.ЗадачаСотрудника;
	Команды.Добавить(Команда);
	
КонецПроцедуры

Процедура ОбработкаКомандыСоздатьЗадачу(Параметры) Экспорт
	
	НавигационнаяСсылкаСообщения = ПолучитьНавигационнуюСсылку(Параметры.Сообщение);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура);
	ПараметрыФормы.ЗначенияЗаполнения.Вставить("Основание", НавигационнаяСсылкаСообщения);
	ПараметрыФормы.ЗначенияЗаполнения.Вставить("ОснованиеПредставление", Параметры.Сообщение.Текст);
	ОткрытьФорму("Документ.ЗадачаСотрудника.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодключитьОбработчикФормированияКомандОбсуждений()
	
	Вызов = Новый ОписаниеОповещения("ПриФормированииКоманд", ЗадачиСотрудникаКлиент);
	СистемаВзаимодействия.ПодключитьОбработчикФормированияКоманд(Вызов);
	
КонецПроцедуры

#КонецОбласти