#Область ПрограммныйИнтерфейс

Процедура УстановитьМинимальныйИнтерфейсРМК() Экспорт
	
	Настройки = Новый НастройкиИнтерфейсаКлиентскогоПриложения;
	НастройкиСостава = Новый НастройкиСоставаИнтерфейсаКлиентскогоПриложения;
	Настройки.УстановитьСостав(НастройкиСостава);
	ХранилищеСистемныхНастроек.Сохранить("Общее/НастройкиИнтерфейсаКлиентскогоПриложения", "", Настройки);
	
КонецПроцедуры

#Область ПроцедурыИФункцииОбработкиСправочникаНастройкиРМК

// Функция получает настройку РМУ для рабочего места
//
// Параметры:
//  РабочееМесто - Справочник.РабочиеМеста - текущее рабочее место (для работы с подключаемым оборудованием)
//
Функция ПолучитьНастройкуРМК(РабочееМесто, ВВидеСтруктуры = Ложь) Экспорт
	
	СтруктураДляВозврата = Новый Структура;
	СтруктураДляВозврата.Вставить("НеПоказыватьПриОткрытииФормуВыбораКассы", Ложь);
	СтруктураДляВозврата.Вставить("СверятьИтогиНаЭТПриЗакрытииСмены", Ложь);
	СтруктураДляВозврата.Вставить("Ссылка", Неопределено);
	СтруктураДляВозврата.Вставить("ПечататьТоварныйЧекПоУмолчанию", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	НастройкиРМК.Ссылка КАК Ссылка,
		|	НастройкиРМК.НеПоказыватьПриОткрытииФормуВыбораКассы КАК НеПоказыватьПриОткрытииФормуВыбораКассы,
		|	НастройкиРМК.СверятьИтогиНаЭТПриЗакрытииСмены КАК СверятьИтогиНаЭТПриЗакрытииСмены,
		|	НастройкиРМК.ПечататьТоварныйЧекПоУмолчанию КАК ПечататьТоварныйЧекПоУмолчанию
		|ИЗ
		|	Справочник.НастройкиРМК КАК НастройкиРМК
		|ГДЕ
		|	НастройкиРМК.РабочееМесто = &РабочееМесто
		|	И НЕ НастройкиРМК.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("РабочееМесто", РабочееМесто);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	СсылкаНаНастройки = Справочники.НастройкиРМК.ПустаяСсылка();
	Если Выборка.Следующий() Тогда
		СсылкаНаНастройки = Выборка.Ссылка;
		ЗаполнитьЗначенияСвойств(СтруктураДляВозврата, Выборка);
	Иначе
		НоваяНастройкаРМК = Справочники.НастройкиРМК.СоздатьЭлемент();
		НоваяНастройкаРМК.ЗаполнитьТаблицуКнопокИзМакета();
		НоваяНастройкаРМК.РабочееМесто = РабочееМесто;
		НоваяНастройкаРМК.Наименование = СокрЛП(РабочееМесто.Наименование);
		Попытка
			НоваяНастройкаРМК.Записать();
			
			СсылкаНаНастройки = НоваяНастройкаРМК.Ссылка;
			СтруктураДляВозврата.Вставить("Ссылка", СсылкаНаНастройки);
		Исключение
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ОписаниеОшибки();
			Сообщение.Сообщить();
			
			Возврат Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	Если ВВидеСтруктуры Тогда
		Возврат СтруктураДляВозврата;
	Иначе
		Возврат СсылкаНаНастройки;
	КонецЕсли;
	
КонецФункции

// Процедура записывает новое значение реквизита НеПоказыватьПриОткрытииФормуВыбораКассы
//
// Параметры:
//  НастройкаРМК - Справочник.НастройкиРМК - текущие настройки РМК (определяются по рабочему месту)
//  НеПоказыватьПриОткрытииФормуВыбораКассы - Булево - новое значение реквизита
//
Процедура ОбновитьНастройкиРМК(НастройкаРМК, НеПоказыватьПриОткрытииФормуВыбораКассы, СверятьИтогиНаЭТПриЗакрытииСмены = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не НастройкаРМК.Пустая() И
		(НастройкаРМК.НеПоказыватьПриОткрытииФормуВыбораКассы <> НеПоказыватьПриОткрытииФормуВыбораКассы 
		ИЛИ 
		НастройкаРМК.СверятьИтогиНаЭТПриЗакрытииСмены <> СверятьИтогиНаЭТПриЗакрытииСмены)
		Тогда
		
		НастройкаРМКОбъект = НастройкаРМК.ПолучитьОбъект();
		НастройкаРМКОбъект.НеПоказыватьПриОткрытииФормуВыбораКассы = НеПоказыватьПриОткрытииФормуВыбораКассы;
		НастройкаРМКОбъект.СверятьИтогиНаЭТПриЗакрытииСмены = СверятьИтогиНаЭТПриЗакрытииСмены;
		
		Попытка
			НастройкаРМКОбъект.Записать();
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Не удалось записать изменения.'") + Символы.ПС + ПодробноеПредставлениеОшибки(
				ИнформацияОбОшибке);
			Если НастройкаРМК.НеПоказыватьПриОткрытииФормуВыбораКассы <> НеПоказыватьПриОткрытииФормуВыбораКассы Тогда
				Сообщение.Поле = "НеПоказыватьПриОткрытииФормуВыбораКассы";
			Иначе
				Сообщение.Поле = "СверятьИтогиНаЭТПриЗакрытииСмены";
			КонецЕсли;
			Сообщение.Сообщить();
		КонецПопытки;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Функция определяет кассу кассу по умолчанию.
//
// Возвращает кассу ККМ, если найдена одна касса.
// Возвращает Неопределено, если касса не найдена или касс больше одной.
//
// Возвращаемое значение:
//	СправочникСсылка.КассыККМ - Найденная касса ККМ
//
Функция ПолучитьКассуККМИТерминалПоУмолчанию() Экспорт
	
	СтруктураПараметров = Новый Структура("КассаККМ, ЭквайринговыйТерминал, КоличествоЭквайринговыхТерминалов, Организация, СтруктурнаяЕдиница", 
		Справочники.КассыККМ.ПустаяСсылка(), Справочники.ЭквайринговыеТерминалы.ПустаяСсылка(), 0);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	КассыККМ.Ссылка КАК КассаККМ,
	|	КассыККМ.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	КассыККМ.Владелец КАК Организация
	|ПОМЕСТИТЬ КассыККМ
	|ИЗ
	|	Справочник.КассыККМ КАК КассыККМ
	|ГДЕ
	|	НЕ КассыККМ.ПометкаУдаления
	|	И КассыККМ.ТипКассы = &ТипКассы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЭквайринговыеТерминалы.Ссылка КАК ЭквайринговыйТерминал,
	|	ЭквайринговыеТерминалы.Касса КАК Касса
	|ИЗ
	|	Справочник.ЭквайринговыеТерминалы КАК ЭквайринговыеТерминалы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КассыККМ КАК КассыККМ
	|		ПО ЭквайринговыеТерминалы.Касса = КассыККМ.КассаККМ
	|ГДЕ
	|	НЕ ЭквайринговыеТерминалы.ПометкаУдаления
	|	И ЭквайринговыеТерминалы.Касса.ТипКассы = &ТипКассы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КассыККМ.КассаККМ КАК КассаККМ,
	|	КассыККМ.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	КассыККМ.Организация КАК Организация
	|ИЗ
	|	КассыККМ КАК КассыККМ");
	
	Запрос.УстановитьПараметр("ТипКассы", Перечисления.ТипыКассККМ.ФискальныйРегистратор);
	
	МРезультатов = Запрос.ВыполнитьПакет();
	Выборка = МРезультатов[2].Выбрать();
	КоличествоКассККМ = Выборка.Количество();
	Если КоличествоКассККМ = 1 
	   И Выборка.Следующий() Тогда
	
		СтруктураПараметров.КассаККМ = Выборка.КассаККМ;
		СтруктураПараметров.СтруктурнаяЕдиница = Выборка.СтруктурнаяЕдиница;
		СтруктураПараметров.Организация = Выборка.Организация;
		
	Иначе
		
		СтруктураПараметров.КассаККМ = Неопределено;
		
	КонецЕсли;
	
	Если КоличествоКассККМ = 1 Тогда
		ТЗ_ЭТ = МРезультатов[1].Выгрузить();
		
		ЭТКассыПоУмолчанию = ТЗ_ЭТ.НайтиСтроки(Новый Структура("Касса", СтруктураПараметров.КассаККМ));
		
		СтруктураПараметров.КоличествоЭквайринговыхТерминалов = ЭТКассыПоУмолчанию.Количество();
		Если ЭТКассыПоУмолчанию.Количество() = 1 Тогда
			
			СтруктураПараметров.ЭквайринговыйТерминал = ЭТКассыПоУмолчанию[0].ЭквайринговыйТерминал;
			
		Иначе
			
			СтруктураПараметров.ЭквайринговыйТерминал = Неопределено;
			
		КонецЕсли;
	Иначе
		СтруктураПараметров.ЭквайринговыйТерминал = Неопределено;
	КонецЕсли;
	
	Возврат СтруктураПараметров;

КонецФункции // ПолучитьКассуПоУмолчанию()

// Функция определяет кассу кассу по умолчанию.
//
// Возвращает кассу ККМ, если найдена одна касса.
// Возвращает Неопределено, если касса не найдена или касс больше одной.
//
// Возвращаемое значение:
//	СправочникСсылка.КассыККМ - Найденная касса ККМ
//
Функция ПолучитьТерминалПоУмолчанию(пКасса) Экспорт
	
	СтруктураПараметров = Новый Структура("ЭквайринговыйТерминал, КоличествоЭквайринговыхТерминалов", 
		Справочники.КассыККМ.ПустаяСсылка(), Справочники.ЭквайринговыеТерминалы.ПустаяСсылка(), 0);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЭквайринговыеТерминалы.Ссылка КАК ЭквайринговыйТерминал,
	|	ЭквайринговыеТерминалы.Касса КАК Касса
	|ИЗ
	|	Справочник.ЭквайринговыеТерминалы КАК ЭквайринговыеТерминалы
	|ГДЕ
	|	НЕ ЭквайринговыеТерминалы.ПометкаУдаления
	|	И ЭквайринговыеТерминалы.Касса.ТипКассы = &ТипКассы
	|	И ЭквайринговыеТерминалы.Касса = &Касса");
	
	Запрос.УстановитьПараметр("ТипКассы", Перечисления.ТипыКассККМ.ФискальныйРегистратор);
	Запрос.УстановитьПараметр("Касса", пКасса);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	СтруктураПараметров.КоличествоЭквайринговыхТерминалов = Выборка.Количество();
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		СтруктураПараметров.ЭквайринговыйТерминал = Выборка.ЭквайринговыйТерминал;
	Иначе
		СтруктураПараметров.ЭквайринговыйТерминал = Неопределено;
	КонецЕсли;
	
	Возврат СтруктураПараметров;

КонецФункции // ПолучитьКассуПоУмолчанию()

#КонецОбласти

#Область ПредпросмотрЧека

Функция ПолучитьПараметрыРегистрацииККТ(ККТ) Экспорт
	
	Возврат РабочееМестоКассира.ПолучитьПараметрыРегистрацииККТ(ККТ);
	
КонецФункции

#КонецОбласти

#Область ПроверкаФизлицаКассира

// Возвращает реквизиты кассира
//
// Параметры:
//  Реквизиты - Структура - содержит:
//   * КассаККМ      - СправочникСсылка.КассыККМ
//   * Ответственный - СправочникСсылка.Сотрудники
//   * Автор         - СправочникСсылка.Пользователи
//
// Возвращаемое значение:
//  Структура - содержит:
//   * Сотрудник - СправочникСсылка.Сотрудники
//   * ФизЛицо   - СправочникСсылка.ФизическиеЛица
//
Функция ПолучитьРеквизитыКассира(Реквизиты) Экспорт
	
	Результат = Новый Структура("Сотрудник, ФизЛицо");
	
	ИсточникФИОКассираВЧеке = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реквизиты.КассаККМ, "ИсточникФИОКассираВЧеке");
	
	Если ИсточникФИОКассираВЧеке = ПредопределенноеЗначение("Перечисление.ИсточникиФИОКассираВЧекеККМ.Ответственный") Тогда
		ПользовательКассирДляПечати = Реквизиты.Ответственный;
	ИначеЕсли ИсточникФИОКассираВЧеке = ПредопределенноеЗначение("Перечисление.ИсточникиФИОКассираВЧекеККМ.Автор") Тогда
		ПользовательКассирДляПечати = Реквизиты.Автор;
	КонецЕсли;
	
	РеквизитыКассира = РозничныеПродажиСервер.ПолучитьРеквизитыКассира(ПользовательКассирДляПечати);
	
	Результат.Сотрудник = РеквизитыКассира.Кассир;
	Результат.ФизЛицо = РеквизитыКассира.ФизЛицо;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область РаспределениеПродаж

Функция НадоПредупредитьОРаспределенииПоЗаказам(Идентификатор) Экспорт
	
	НесколькоОрганизаций = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	ЗаказыВРознице = ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыВРозничнойТорговле");
	УчетПоКомпании = Константы.УчетПоКомпании.Получить();
	НастройкиРаспределения = СуществуютНастройкиРаспределения();
	
	Если Идентификатор = "ИспользоватьНесколькоОрганизаций" Тогда
		НесколькоОрганизаций = Истина;
	ИначеЕсли Идентификатор = "УчетПоКомпании" Тогда
		УчетПоКомпании = Ложь;
	ИначеЕсли Идентификатор = "ФункциональнаяОпцияИспользоватьЗаказыВРозничнойТорговлеДляНастроек" Тогда
		ЗаказыВРознице = Истина;
	КонецЕсли;
	
	Если НесколькоОрганизаций И ЗаказыВРознице И Не УчетПоКомпании Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаспределениеПродаж

Функция СуществуютНастройкиРаспределения()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиРМКРаспределениеНоменклатуры.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиРМК.РаспределениеНоменклатуры КАК НастройкиРМКРаспределениеНоменклатуры");
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

#КонецОбласти

#КонецОбласти