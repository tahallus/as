
#Область ПрограммныйИнтерфейс

// См. СПАРКРискиКлиентСерверПереопределяемый.ПриОпределенииСвойствКонтрагентаВОбъекте
//
Процедура ПриОпределенииСвойствКонтрагентаВОбъекте(КонтрагентОбъект, Форма, СвойстваКонтрагента) Экспорт
	
	ЭтоИностранныйКонтрагент = ЗначениеЗаполнено(КонтрагентОбъект.СтранаРегистрации)
		И КонтрагентОбъект.СтранаРегистрации <> ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
	ПодлежитПроверке = Не ЭтоИностранныйКонтрагент И (ЭтоЮридическоеЛицо(КонтрагентОбъект.ВидКонтрагента)
		Или ЭтоИндивидуальныйПредприниматель(КонтрагентОбъект.ВидКонтрагента));
	
	СвойстваКонтрагента.ИНН = КонтрагентОбъект.ИНН;
	СвойстваКонтрагента.ПодлежитПроверке = ПодлежитПроверке;
	СвойстваКонтрагента.ВидКонтрагента = ВидКонтрагентаСПАРКРиски(КонтрагентОбъект.ВидКонтрагента);
	
КонецПроцедуры

// Определяет способ получения данных о контрагенте.
//
// Параметры:
//  ВидКонтрагента - ПеречислениеСсылка.ВидыКонтрагентов - Вид контрагента.
// 
// Возвращаемое значение:
//   - ПеречислениеСсылка.ВидыКонтрагентовСПАРКРиски
//
Функция ВидКонтрагентаСПАРКРиски(ВидКонтрагента) Экспорт
	
	Если ЭтоЮридическоеЛицо(ВидКонтрагента) Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыКонтрагентовСПАРКРиски.ЮридическоеЛицо");
	ИначеЕсли ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыКонтрагентовСПАРКРиски.ИндивидуальныйПредприниматель");
	Иначе
		Возврат ПредопределенноеЗначение("Перечисление.ВидыКонтрагентовСПАРКРиски.ПустаяСсылка");
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоЮридическоеЛицо(ВидКонтрагента)
	
	Возврат ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ЮридическоеЛицо")
		Или ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ГосударственныйОрган");
	
КонецФункции

Функция ЭтоИндивидуальныйПредприниматель(ВидКонтрагента)
	
	Возврат ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ИндивидуальныйПредприниматель");
	
КонецФункции

#КонецОбласти