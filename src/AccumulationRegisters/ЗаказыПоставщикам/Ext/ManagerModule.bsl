#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура создает пустую временную таблицу изменения движений.
//
Процедура СоздатьПустуюВременнуюТаблицуИзменение(ДополнительныеСвойства) Экспорт
	
	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
	 ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	ЗаказыПоставщикам.НомерСтроки КАК НомерСтроки,
	|	ЗаказыПоставщикам.Организация КАК Организация,
	|	ЗаказыПоставщикам.Склад КАК Склад,
	|	ЗаказыПоставщикам.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ЗаказыПоставщикам.Номенклатура КАК Номенклатура,
	|	ЗаказыПоставщикам.Характеристика КАК Характеристика,
	|	ЗаказыПоставщикам.Количество КАК КоличествоПередЗаписью,
	|	ЗаказыПоставщикам.Количество КАК КоличествоИзменение,
	|	ЗаказыПоставщикам.Количество КАК КоличествоПриЗаписи
	|ПОМЕСТИТЬ ДвиженияЗаказыПоставщикамИзменение
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам");
	
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураВременныеТаблицы.Вставить("ДвиженияЗаказыПоставщикамИзменение", Ложь);
	
КонецПроцедуры // СоздатьПустуюВременнуюТаблицуИзменение()

// Процедура - Изменяет таблицу движений с учетом текущих остатков в разрезе складов
//
// Параметры:
//  Регистратор		 - 	ДокументСсылка - Документ движений
//  ТаблицаДвижений	 - 	ТаблицаЗначений - Таблица движений регистра
//
Процедура ДобавитьДвиженияСУчетомСкладов(Регистратор, ТаблицаДвижений) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаДвижений", ТаблицаДвижений);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ТаблицаДвижений.Склад КАК Склад,
	|	ТаблицаДвижений.Номенклатура КАК Номенклатура,
	|	ТаблицаДвижений.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ
	|	&ТаблицаДвижений КАК ТаблицаДвижений";
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых заказов поставщикам
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.Номенклатура КАК Номенклатура,
	|	ТаблицаДвижений.Характеристика КАК Характеристика,
	|	ТаблицаДвижений.ЗаказПоставщику КАК ЗаказПоставщику
	|ИЗ
	|	ТаблицаДвижений КАК ТаблицаДвижений";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЗаказыПоставщикам");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ВложенныйЗапрос.Склад КАК Склад,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	СУММА(ВложенныйЗапрос.КоличествоОстаток) КАК Количество
	|ИЗ
	|	(ВЫБРАТЬ
	|		РегистрОстатки.Организация КАК Организация,
	|		РегистрОстатки.ЗаказПоставщику КАК ЗаказПоставщику,
	|		РегистрОстатки.Склад КАК Склад,
	|		РегистрОстатки.Номенклатура КАК Номенклатура,
	|		РегистрОстатки.Характеристика КАК Характеристика,
	|		РегистрОстатки.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.ЗаказыПоставщикам.Остатки(
	|				,
	|				(Организация, ЗаказПоставщику, Номенклатура, Характеристика) В
	|					(ВЫБРАТЬ
	|						ТаблицаДвижений.Организация,
	|						ТаблицаДвижений.ЗаказПоставщику,
	|						ТаблицаДвижений.Номенклатура,
	|						ТаблицаДвижений.Характеристика
	|					ИЗ
	|						ТаблицаДвижений
	|					ГДЕ
	|						ТаблицаДвижений.Склад <> ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка))) КАК РегистрОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокумента.Организация,
	|		ДвиженияДокумента.ЗаказПоставщику,
	|		ДвиженияДокумента.Склад,
	|		ДвиженияДокумента.Номенклатура,
	|		ДвиженияДокумента.Характеристика,
	|		ВЫБОР
	|			КОГДА ДвиженияДокумента.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ДвиженияДокумента.Количество
	|			ИНАЧЕ -ДвиженияДокумента.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ЗаказыПоставщикам КАК ДвиженияДокумента
	|	ГДЕ
	|		ДвиженияДокумента.Регистратор = &Регистратор
	|		И (ДвиженияДокумента.Организация, ДвиженияДокумента.ЗаказПоставщику, ДвиженияДокумента.Номенклатура, ДвиженияДокумента.Характеристика) В
	|				(ВЫБРАТЬ
	|					ТаблицаДвижений.Организация,
	|					ТаблицаДвижений.ЗаказПоставщику,
	|					ТаблицаДвижений.Номенклатура,
	|					ТаблицаДвижений.Характеристика
	|				ИЗ
	|					ТаблицаДвижений
	|				ГДЕ
	|					ТаблицаДвижений.Склад <> ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка))) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Склад,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.ЗаказПоставщику";
	ТаблицаОстатки = Запрос.Выполнить().Выгрузить();
	ТаблицаОстатки.Индексы.Добавить("Организация, ЗаказПоставщику, Номенклатура, Характеристика");
	ТаблицаОстатки.Индексы.Добавить("Организация, Склад, ЗаказПоставщику, Номенклатура, Характеристика");
	
	НоваяТаблицаДвижений = ТаблицаДвижений.СкопироватьКолонки();
	СтруктураОтбора = Новый Структура;
	
	// Итерация 1: Строки с пустым складом или приходные движения переносятся как есть
	Для каждого СтрокаТаблицы Из ТаблицаДвижений Цикл
		Если НЕ НеРазбиватьПоСкладам(СтрокаТаблицы) Тогда
			Продолжить;
		КонецЕсли; 
		Если СтрокаТаблицы.Количество = 0 Тогда
			Продолжить;	
		КонецЕсли;
		НоваяСтрока = НоваяТаблицаДвижений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
	КонецЦикла; 
	
	// Итерация 2: Поиск остатка по нужному складу
	Для каждого СтрокаТаблицы Из ТаблицаДвижений Цикл
		Если НеРазбиватьПоСкладам(СтрокаТаблицы) Тогда
			Продолжить;
		КонецЕсли; 
		СтруктураОтбора.Очистить();
		СтруктураОтбора.Вставить("Организация", СтрокаТаблицы.Организация);
		СтруктураОтбора.Вставить("ЗаказПоставщику", СтрокаТаблицы.ЗаказПоставщику);
		СтруктураОтбора.Вставить("Номенклатура", СтрокаТаблицы.Номенклатура);
		СтруктураОтбора.Вставить("Характеристика", СтрокаТаблицы.Характеристика);
		СтруктураОтбора.Вставить("Склад", СтрокаТаблицы.Склад);
		НайденныеСтроки = ТаблицаОстатки.НайтиСтроки(СтруктураОтбора);
		Для каждого СтрокаОстатка Из НайденныеСтроки Цикл
			Если СтрокаОстатка.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли; 
			Количество = Мин(СтрокаТаблицы.Количество, СтрокаОстатка.Количество);
			НоваяСтрока = НоваяТаблицаДвижений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Количество = Количество;
			СтрокаТаблицы.Количество = СтрокаТаблицы.Количество - Количество;
			СтрокаОстатка.Количество = СтрокаОстатка.Количество - Количество;
			Если СтрокаТаблицы.Количество <= 0 Тогда
				Прервать;
			КонецЕсли; 
		КонецЦикла; 
	КонецЦикла; 
	
	// Итерация 3: Поиск остатка на других складах
	Для каждого СтрокаТаблицы Из ТаблицаДвижений Цикл
		Если НеРазбиватьПоСкладам(СтрокаТаблицы) Тогда
			Продолжить;
		КонецЕсли; 
		СтруктураОтбора.Очистить();
		СтруктураОтбора.Вставить("Организация", СтрокаТаблицы.Организация);
		СтруктураОтбора.Вставить("ЗаказПоставщику", СтрокаТаблицы.ЗаказПоставщику);
		СтруктураОтбора.Вставить("Номенклатура", СтрокаТаблицы.Номенклатура);
		СтруктураОтбора.Вставить("Характеристика", СтрокаТаблицы.Характеристика);
		НайденныеСтроки = ТаблицаОстатки.НайтиСтроки(СтруктураОтбора);
		Для каждого СтрокаОстатка Из НайденныеСтроки Цикл
			Если СтрокаОстатка.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли; 
			Количество = Мин(СтрокаТаблицы.Количество, СтрокаОстатка.Количество);
			НоваяСтрока = НоваяТаблицаДвижений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Количество = Количество;
			СтрокаТаблицы.Количество = СтрокаТаблицы.Количество - Количество;
			СтрокаОстатка.Количество = СтрокаОстатка.Количество - Количество;
			ИндексВставки = НоваяТаблицаДвижений.Индекс(НоваяСтрока);
			НоваяСтрокаПриход = НоваяТаблицаДвижений.Вставить(ИндексВставки);
			ЗаполнитьЗначенияСвойств(НоваяСтрокаПриход, СтрокаТаблицы);
			НоваяСтрокаПриход.Количество = Количество;
			НоваяСтрокаПриход.ВидДвижения = ВидДвиженияНакопления.Приход;
			НоваяСтрокаРасход = НоваяТаблицаДвижений.Вставить(ИндексВставки);
			ЗаполнитьЗначенияСвойств(НоваяСтрокаРасход, НоваяСтрокаПриход);
			НоваяСтрокаРасход.Количество = - Количество;
			НоваяСтрокаРасход.Склад = СтрокаОстатка.Склад;
			Если СтрокаТаблицы.Количество <= 0 Тогда
				Прервать;
			КонецЕсли; 
		КонецЦикла; 
	КонецЦикла;
	
	// Итерация 4: Оставшиеся строки переносим как есть, требуется для корректной работы контроля остатков
	Для каждого СтрокаТаблицы Из ТаблицаДвижений Цикл
		Если НеРазбиватьПоСкладам(СтрокаТаблицы) Тогда
			Продолжить;
		КонецЕсли; 
		НоваяСтрока = НоваяТаблицаДвижений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
	КонецЦикла; 
	
	ТаблицаДвижений = НоваяТаблицаДвижений;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Склад)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НеРазбиватьПоСкладам(Запись)
	
	Если НЕ ЗначениеЗаполнено(Запись.Склад) Тогда
		Возврат Истина;	
	КонецЕсли;         
	Если Запись.ВидДвижения = ВидДвиженияНакопления.Приход И Запись.Количество > 0 Тогда
		Возврат Истина;
	КонецЕсли;
	Если Запись.ВидДвижения = ВидДвиженияНакопления.Расход И Запись.Количество < 0 Тогда
		Возврат Истина;
	КонецЕсли;
	Если Запись.Количество = 0 Тогда
		Возврат Истина;	
	КонецЕсли;
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецЕсли