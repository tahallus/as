#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ПередЗаписью набора записей.
//
Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка
  ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
  ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Установка исключительной блокировки текущего набора записей регистратора.
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДенежныеСредстваВРезерве.НаборЗаписей");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Регистратор", Отбор.Регистратор.Значение);
	Блокировка.Заблокировать();
	
	Если НЕ СтруктураВременныеТаблицы.Свойство("ДвиженияДенежныеСредстваВРезервеИзменение") ИЛИ
		СтруктураВременныеТаблицы.Свойство("ДвиженияДенежныеСредстваВРезервеИзменение") И НЕ СтруктураВременныеТаблицы.ДвиженияДенежныеСредстваВРезервеИзменение Тогда
		
		// Если временная таблица "ДвиженияДенежныеСредстваВРезервеИзменение" не существует или не содержит записей
		// об изменении набора, значит набор записывается первый раз или для набора был выполнен контроль остатков.
		// Текущее состояние набора помещается во временную таблицу "ДвиженияДенежныеСредстваВРезервеПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РегистрНакопленияДенежныеСредстваВРезерве.НомерСтроки КАК НомерСтроки,
		|	РегистрНакопленияДенежныеСредстваВРезерве.Организация КАК Организация,
		|	РегистрНакопленияДенежныеСредстваВРезерве.ТипДенежныхСредств КАК ТипДенежныхСредств,
		|	РегистрНакопленияДенежныеСредстваВРезерве.БанковскийСчетКасса КАК БанковскийСчетКасса,
		|	РегистрНакопленияДенежныеСредстваВРезерве.Валюта КАК Валюта,
		|	РегистрНакопленияДенежныеСредстваВРезерве.Документ КАК Документ,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияДенежныеСредстваВРезерве.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияДенежныеСредстваВРезерве.Сумма
		|		ИНАЧЕ -РегистрНакопленияДенежныеСредстваВРезерве.Сумма
		|	КОНЕЦ КАК СуммаПередЗаписью
		|ПОМЕСТИТЬ ДвиженияДенежныеСредстваВРезервеПередЗаписью
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваВРезерве КАК РегистрНакопленияДенежныеСредстваВРезерве
		|ГДЕ
		|	РегистрНакопленияДенежныеСредстваВРезерве.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
				
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	Иначе
		
		// Если временная таблица "ДвиженияДенежныеСредстваВРезервеИзменение" существует и содержит записи
		// об изменении набора, значит набор записывается не первый раз и для набора не был выполнен контроль остатков.
		// Текущее состояние набора и текущее состояние изменений помещаются во временную таблицу "ДвиженияДенежныеСредстваВРезервеПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно первоначального.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияДенежныеСредстваВРезервеИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияДенежныеСредстваВРезервеИзменение.Организация КАК Организация,
		|	ДвиженияДенежныеСредстваВРезервеИзменение.ТипДенежныхСредств КАК ТипДенежныхСредств,
		|	ДвиженияДенежныеСредстваВРезервеИзменение.БанковскийСчетКасса КАК БанковскийСчетКасса,
		|	ДвиженияДенежныеСредстваВРезервеИзменение.Валюта КАК Валюта,
		|	ДвиженияДенежныеСредстваВРезервеИзменение.Документ КАК Документ,
		|	ДвиженияДенежныеСредстваВРезервеИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью
		|ПОМЕСТИТЬ ДвиженияДенежныеСредстваВРезервеПередЗаписью
		|ИЗ
		|	ДвиженияДенежныеСредстваВРезервеИзменение КАК ДвиженияДенежныеСредстваВРезервеИзменение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РегистрНакопленияДенежныеСредстваВРезерве.НомерСтроки,
		|	РегистрНакопленияДенежныеСредстваВРезерве.Организация,
		|	РегистрНакопленияДенежныеСредстваВРезерве.ТипДенежныхСредств,
		|	РегистрНакопленияДенежныеСредстваВРезерве.БанковскийСчетКасса,
		|	РегистрНакопленияДенежныеСредстваВРезерве.Валюта,
		|	РегистрНакопленияДенежныеСредстваВРезерве.Документ,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияДенежныеСредстваВРезерве.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияДенежныеСредстваВРезерве.Сумма
		|		ИНАЧЕ -РегистрНакопленияДенежныеСредстваВРезерве.Сумма
		|	КОНЕЦ
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваВРезерве КАК РегистрНакопленияДенежныеСредстваВРезерве
		|ГДЕ
		|	РегистрНакопленияДенежныеСредстваВРезерве.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
				
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
	// Временная таблица "ДвиженияДенежныеСредстваВРезервеИзменение" уничтожается
	// Удаляется информация о ее существовании.
	
	Если СтруктураВременныеТаблицы.Свойство("ДвиженияДенежныеСредстваВРезервеИзменение") Тогда
		
		Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияДенежныеСредстваВРезервеИзменение");
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		СтруктураВременныеТаблицы.Удалить("ДвиженияДенежныеСредстваВРезервеИзменение");
	
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ПриЗаписи набора записей.
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
  ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
  ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда	
		Возврат;		
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу "ДвиженияДенежныеСредстваВРезервеИзменение".
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МИНИМУМ(ДвиженияДенежныеСредстваВРезервеИзменение.НомерСтроки) КАК НомерСтроки,
	|	ДвиженияДенежныеСредстваВРезервеИзменение.Организация КАК Организация,
	|	ДвиженияДенежныеСредстваВРезервеИзменение.ТипДенежныхСредств КАК ТипДенежныхСредств,
	|	ДвиженияДенежныеСредстваВРезервеИзменение.БанковскийСчетКасса КАК БанковскийСчетКасса,
	|	ДвиженияДенежныеСредстваВРезервеИзменение.Валюта КАК Валюта,
	|	ДвиженияДенежныеСредстваВРезервеИзменение.Документ КАК Документ,
	|	СУММА(ДвиженияДенежныеСредстваВРезервеИзменение.СуммаПередЗаписью) КАК СуммаПередЗаписью,
	|	СУММА(ДвиженияДенежныеСредстваВРезервеИзменение.СуммаИзменение) КАК СуммаИзменение,
	|	СУММА(ДвиженияДенежныеСредстваВРезервеИзменение.СуммаПриЗаписи) КАК СуммаПриЗаписи
	|ПОМЕСТИТЬ ДвиженияДенежныеСредстваВРезервеИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДвиженияДенежныеСредстваВРезервеПередЗаписью.НомерСтроки КАК НомерСтроки,
	|		ДвиженияДенежныеСредстваВРезервеПередЗаписью.Организация КАК Организация,
	|		ДвиженияДенежныеСредстваВРезервеПередЗаписью.ТипДенежныхСредств КАК ТипДенежныхСредств,
	|		ДвиженияДенежныеСредстваВРезервеПередЗаписью.БанковскийСчетКасса КАК БанковскийСчетКасса,
	|		ДвиженияДенежныеСредстваВРезервеПередЗаписью.Валюта КАК Валюта,
	|		ДвиженияДенежныеСредстваВРезервеПередЗаписью.Документ КАК Документ,
	|		ДвиженияДенежныеСредстваВРезервеПередЗаписью.СуммаПередЗаписью КАК СуммаПередЗаписью,
	|		ДвиженияДенежныеСредстваВРезервеПередЗаписью.СуммаПередЗаписью КАК СуммаИзменение,
	|		0 КАК СуммаПриЗаписи
	|	ИЗ
	|		ДвиженияДенежныеСредстваВРезервеПередЗаписью КАК ДвиженияДенежныеСредстваВРезервеПередЗаписью
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДенежныеСредстваВРезервеПриЗаписи.НомерСтроки,
	|		ДвиженияДенежныеСредстваВРезервеПриЗаписи.Организация,
	|		ДвиженияДенежныеСредстваВРезервеПриЗаписи.ТипДенежныхСредств,
	|		ДвиженияДенежныеСредстваВРезервеПриЗаписи.БанковскийСчетКасса,
	|		ДвиженияДенежныеСредстваВРезервеПриЗаписи.Валюта,
	|		ДвиженияДенежныеСредстваВРезервеПриЗаписи.Документ,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияДенежныеСредстваВРезервеПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияДенежныеСредстваВРезервеПриЗаписи.Сумма
	|			ИНАЧЕ ДвиженияДенежныеСредстваВРезервеПриЗаписи.Сумма
	|		КОНЕЦ,
	|		ДвиженияДенежныеСредстваВРезервеПриЗаписи.Сумма
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредстваВРезерве КАК ДвиженияДенежныеСредстваВРезервеПриЗаписи
	|	ГДЕ
	|		ДвиженияДенежныеСредстваВРезервеПриЗаписи.Регистратор = &Регистратор) КАК ДвиженияДенежныеСредстваВРезервеИзменение
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияДенежныеСредстваВРезервеИзменение.Организация,
	|	ДвиженияДенежныеСредстваВРезервеИзменение.ТипДенежныхСредств,
	|	ДвиженияДенежныеСредстваВРезервеИзменение.БанковскийСчетКасса,
	|	ДвиженияДенежныеСредстваВРезервеИзменение.Валюта,
	|	ДвиженияДенежныеСредстваВРезервеИзменение.Документ
	|
	|ИМЕЮЩИЕ
	|	СУММА(ДвиженияДенежныеСредстваВРезервеИзменение.СуммаИзменение) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ТипДенежныхСредств,
	|	БанковскийСчетКасса,
	|	Валюта,
	|	Документ");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	// Новые изменения были помещены во временную таблицу "ДвиженияДенежныеСредстваВРезервеИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияДенежныеСредстваВРезервеИзменение", ВыборкаИзРезультатаЗапроса.Количество > 0);
	
	// Временная таблица "ДвиженияДенежныеСредстваВРезервеПередЗаписью" уничтожается
	Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияДенежныеСредстваВРезервеПередЗаписью");
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры // ПриЗаписи()

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли