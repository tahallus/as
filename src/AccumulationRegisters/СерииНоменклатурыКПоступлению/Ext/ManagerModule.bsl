#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура создает пустую временную таблицу изменения движений.
//
// Параметры:
//  ДополнительныеСвойства	 - Структура - структура с ключами ДляПроведения.СтруктураВременныеТаблицы
//
Процедура СоздатьПустуюВременнуюТаблицуИзменение(ДополнительныеСвойства) Экспорт
	
	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
	 ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	СерииНоменклатурыКПоступлению.НомерСтроки КАК НомерСтроки,
	|	СерииНоменклатурыКПоступлению.Номенклатура КАК Номенклатура,
	|	СерииНоменклатурыКПоступлению.Характеристика КАК Характеристика,
	|	СерииНоменклатурыКПоступлению.Партия КАК Партия,
	|	СерииНоменклатурыКПоступлению.Серия КАК Серия,
	|	СерииНоменклатурыКПоступлению.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	СерииНоменклатурыКПоступлению.Ячейка КАК Ячейка,
	|	СерииНоменклатурыКПоступлению.Количество КАК КоличествоПередЗаписью,
	|	СерииНоменклатурыКПоступлению.Количество КАК КоличествоИзменение,
	|	СерииНоменклатурыКПоступлению.Количество КАК КоличествоПриЗаписи
	|ПОМЕСТИТЬ ДвиженияСерииНоменклатурыКПоступлениюИзменение
	|ИЗ
	|	РегистрНакопления.СерииНоменклатурыКПоступлению КАК СерииНоменклатурыКПоступлению");
	
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	//@skip-warning используется для создания временной таблицы
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураВременныеТаблицы.Вставить("ДвиженияСерииНоменклатурыКПоступлениюИзменение", Ложь);
	
КонецПроцедуры // СоздатьПустуюВременнуюТаблицуИзменение()

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(СтруктурнаяЕдиница)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли