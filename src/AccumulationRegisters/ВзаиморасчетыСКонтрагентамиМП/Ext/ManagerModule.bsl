#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получает остатки взаиморасчетов с контрагентами.
//
Функция ПолучитьОстатокВзаиморасчетовСКонтрагентом(Контрагент) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВзаиморасчетыСКонтрагентамиОстатки.СуммаОстаток
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыСКонтрагентамиМП.Остатки КАК ВзаиморасчетыСКонтрагентамиОстатки
	|ГДЕ
	|	ВзаиморасчетыСКонтрагентамиОстатки.Контрагент = &Контрагент";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	ВыборкаРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаРезультатаЗапроса.Следующий() Тогда
		Возврат ВыборкаРезультатаЗапроса.СуммаОстаток;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

// Получает остаток наших долгов.
//
Функция ПолучитьОстатокНашихДолгов() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВзаиморасчетыСКонтрагентамиОстатки.СуммаОстаток,
	|	ВзаиморасчетыСКонтрагентамиОстатки.Контрагент
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыСКонтрагентамиМП.Остатки КАК ВзаиморасчетыСКонтрагентамиОстатки
	|ГДЕ
	|	ВзаиморасчетыСКонтрагентамиОстатки.СуммаОстаток < 0";
	
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	
	СуммаОстаток = ТаблицаРезультатаЗапроса.Итог("СуммаОстаток");
	
	Возврат - СуммаОстаток;
	
КонецФункции

// Получает остаток долгов нам.
//
Функция ПолучитьОстатокДолговНам() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВзаиморасчетыСКонтрагентамиОстатки.СуммаОстаток,
	|	ВзаиморасчетыСКонтрагентамиОстатки.Контрагент
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыСКонтрагентамиМП.Остатки КАК ВзаиморасчетыСКонтрагентамиОстатки
	|ГДЕ
	|	ВзаиморасчетыСКонтрагентамиОстатки.СуммаОстаток > 0";
	
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	
	СуммаОстаток = ТаблицаРезультатаЗапроса.Итог("СуммаОстаток");
	
	Возврат СуммаОстаток;
	
КонецФункции

#КонецОбласти

#КонецЕсли
