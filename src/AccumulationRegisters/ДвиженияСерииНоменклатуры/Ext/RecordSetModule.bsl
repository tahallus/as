#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
// Процедура - обработчик события ПередЗаписью набора записей.
//
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Если НЕ СтруктураВременныеТаблицы.Свойство("ДвиженияСерииНоменклатурыОбщийИзменение") ИЛИ
		СтруктураВременныеТаблицы.Свойство("ДвиженияСерииНоменклатурыОбщийИзменение") И НЕ СтруктураВременныеТаблицы.ДвиженияСерииНоменклатурыОбщийИзменение Тогда
				
		// Если временная таблица "ДвиженияСерииНоменклатурыОбщийИзменение" не существует или не содержит записей
		// об изменении набора, значит набор записывается первый раз или для набора был выполнен контроль остатков.
		// Текущее состояние набора помещается во временную таблицу "ДвиженияСерииНоменклатурыОбщийПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СерииНоменклатуры.НомерСтроки КАК НомерСтроки,
		|	СерииНоменклатуры.Номенклатура КАК Номенклатура,
		|	СерииНоменклатуры.Характеристика КАК Характеристика,
		|	СерииНоменклатуры.Серия КАК Серия,
		|	СерииНоменклатуры.Операция КАК Операция
		|ПОМЕСТИТЬ ДвиженияСерииНоменклатурыОбщийПередЗаписью
		|ИЗ
		|	РегистрНакопления.ДвиженияСерииНоменклатуры КАК СерииНоменклатуры
		|ГДЕ
		|	СерииНоменклатуры.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	Иначе
		
		// Если временная таблица "ДвиженияСерииНоменклатурыОбщийИзменение" существует и содержит записи
		// об изменении набора, значит набор записывается не первый раз и для набора не был выполнен контроль остатков.
		// Текущее состояние набора и текущее состояние изменений помещаются во временную таблицу "ДвиженияСерииНоменклатурыОбщийПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно первоначального.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияСерииНоменклатурыОбщийИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияСерииНоменклатурыОбщийИзменение.Номенклатура КАК Номенклатура,
		|	ДвиженияСерииНоменклатурыОбщийИзменение.Характеристика КАК Характеристика,
		|	ДвиженияСерииНоменклатурыОбщийИзменение.Серия КАК Серия,
		|	ДвиженияСерииНоменклатурыОбщийИзменение.Операция КАК Операция
		|ПОМЕСТИТЬ ДвиженияСерииНоменклатурыОбщийПередЗаписью
		|ИЗ
		|	ДвиженияСерииНоменклатурыОбщийИзменение КАК ДвиженияСерииНоменклатурыОбщийИзменение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвиженияСерииНоменклатурыОбщий.НомерСтроки,
		|	ДвиженияСерииНоменклатурыОбщий.Номенклатура,
		|	ДвиженияСерииНоменклатурыОбщий.Характеристика,
		|	ДвиженияСерииНоменклатурыОбщий.Серия,
		|	ДвиженияСерииНоменклатурыОбщий.Операция
		|ИЗ
		|	РегистрНакопления.ДвиженияСерииНоменклатуры КАК ДвиженияСерииНоменклатурыОбщий
		|ГДЕ
		|	ДвиженияСерииНоменклатурыОбщий.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
	// Временная таблица "ДвиженияСерииНоменклатурыОбщийИзменение" уничтожается
	// Удаляется информация о ее существовании.
	
	Если СтруктураВременныеТаблицы.Свойство("ДвиженияСерииНоменклатурыОбщийИзменение") Тогда
		
		Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияСерииНоменклатурыОбщийИзменение");
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		СтруктураВременныеТаблицы.Удалить("ДвиженияСерииНоменклатурыОбщийИзменение");
	
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ПриЗаписи набора записей.
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу "ДвиженияЗапасыНаСкладахИзменение".
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДвиженияСерииНоменклатурыОбщийИзменение.Номенклатура КАК Номенклатура,
	|	ДвиженияСерииНоменклатурыОбщийИзменение.Характеристика КАК Характеристика,
	|	ДвиженияСерииНоменклатурыОбщийИзменение.Операция КАК Операция,
	|	ДвиженияСерииНоменклатурыОбщийИзменение.Серия КАК Серия,
	|	ДвиженияСерииНоменклатурыОбщийИзменение.Серия.Продан КАК СерияПродан,
	|	СУММА(ДвиженияСерииНоменклатурыОбщийИзменение.ВидИзменения) КАК ВидИзменения,
	|	ДвиженияСерииНоменклатурыОбщийИзменение.Номенклатура.ИспользоватьУникальныеСерии КАК ИспользоватьУникальныеСерии
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДвиженияСерииНоменклатурыОбщийПередЗаписью.Номенклатура КАК Номенклатура,
	|		ДвиженияСерииНоменклатурыОбщийПередЗаписью.Характеристика КАК Характеристика,
	|		ДвиженияСерииНоменклатурыОбщийПередЗаписью.Операция КАК Операция,
	|		ДвиженияСерииНоменклатурыОбщийПередЗаписью.Серия КАК Серия,
	|		-1 КАК ВидИзменения
	|	ИЗ
	|		ДвиженияСерииНоменклатурыОбщийПередЗаписью КАК ДвиженияСерииНоменклатурыОбщийПередЗаписью
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияСерииНоменклатурыОбщий.Номенклатура,
	|		ДвиженияСерииНоменклатурыОбщий.Характеристика,
	|		ДвиженияСерииНоменклатурыОбщий.Операция,
	|		ДвиженияСерииНоменклатурыОбщий.Серия,
	|		1
	|	ИЗ
	|		РегистрНакопления.ДвиженияСерииНоменклатуры КАК ДвиженияСерииНоменклатурыОбщий
	|	ГДЕ
	|		ДвиженияСерииНоменклатурыОбщий.Регистратор = &Регистратор) КАК ДвиженияСерииНоменклатурыОбщийИзменение
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияСерииНоменклатурыОбщийИзменение.Номенклатура,
	|	ДвиженияСерииНоменклатурыОбщийИзменение.Характеристика,
	|	ДвиженияСерииНоменклатурыОбщийИзменение.Операция,
	|	ДвиженияСерииНоменклатурыОбщийИзменение.Серия,
	|	ДвиженияСерииНоменклатурыОбщийИзменение.Серия.Продан
	|
	|ИМЕЮЩИЕ
	|	СУММА(ДвиженияСерииНоменклатурыОбщийИзменение.ВидИзменения) <> 0");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
		
		Если ДополнительныеСвойства.ДляПроведения.Свойство("РежимЗаписи") Тогда
			
			Если ДополнительныеСвойства.ДляПроведения.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
				Если ВыборкаИзРезультатаЗапроса.Операция = Перечисления.ОперацииСерийНоменклатуры.Расход Тогда
					
					Если Не ТипЗнч(Отбор.Регистратор.Значение) = Тип("ДокументСсылка.ПеремещениеЗапасов") 
						И Не ТипЗнч(Отбор.Регистратор.Значение) = Тип("ДокументСсылка.ПеремещениеПоЯчейкам") Тогда
						УстановитьСостояниеПродан = Истина;
					КонецЕсли;
					
				ИначеЕсли ВыборкаИзРезультатаЗапроса.Операция = Перечисления.ОперацииСерийНоменклатуры.Приход Тогда
					
					УстановитьСостояниеПродан = Неопределено;
					
				ИначеЕсли ВыборкаИзРезультатаЗапроса.Операция = Перечисления.ОперацииСерийНоменклатуры.Возврат Тогда
					
					УстановитьСостояниеПродан = Ложь;
					
				КонецЕсли;
			ИначеЕсли ДополнительныеСвойства.ДляПроведения.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
				Если ВыборкаИзРезультатаЗапроса.Операция = Перечисления.ОперацииСерийНоменклатуры.Расход Тогда
				
					УстановитьСостояниеПродан = Ложь;
					
				ИначеЕсли ВыборкаИзРезультатаЗапроса.Операция = Перечисления.ОперацииСерийНоменклатуры.Приход Тогда
					
					УстановитьСостояниеПродан = Неопределено;
					
				ИначеЕсли ВыборкаИзРезультатаЗапроса.Операция = Перечисления.ОперацииСерийНоменклатуры.Возврат Тогда	
					
					УстановитьСостояниеПродан = Истина;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если УстановитьСостояниеПродан <>Неопределено 
			И УстановитьСостояниеПродан <> ВыборкаИзРезультатаЗапроса.СерияПродан
			И ВыборкаИзРезультатаЗапроса.ИспользоватьУникальныеСерии Тогда
			СерияОбъект = ВыборкаИзРезультатаЗапроса.Серия.ПолучитьОбъект();
			СерияОбъект.Продан = УстановитьСостояниеПродан;
			СерияОбъект.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
	// Новые изменения были помещены во временную таблицу "ДвиженияСерииНоменклатурыОбщийИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	
	// Временная таблица "ДвиженияЗапасыНаСкладахПередЗаписью" уничтожается
	Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияСерииНоменклатурыОбщийПередЗаписью");
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры // ПриЗаписи()

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли