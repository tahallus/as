#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ПередЗаписью набора записей.
//
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Установка исключительной блокировки текущего набора записей регистратора.
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РезервыТоваровОрганизаций.НаборЗаписей");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Регистратор", Отбор.Регистратор.Значение);
	Блокировка.Заблокировать();
	
	Если НЕ СтруктураВременныеТаблицы.Свойство("ДвиженияРезервыТоваровОрганизацийИзменение")
		ИЛИ СтруктураВременныеТаблицы.Свойство("ДвиженияРезервыТоваровОрганизацийИзменение") 
			И НЕ СтруктураВременныеТаблицы.ДвиженияРезервыТоваровОрганизацийИзменение Тогда
		
		// Если временная таблица "ДвиженияРезервыТоваровОрганизацийИзменение" не существует или не содержит записей
		// об изменении набора, значит набор записывается первый раз или для набора был выполнен контроль остатков.
		// Текущее состояние набора помещается во временную таблицу "ДвиженияРезервыТоваровОрганизацийПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РезервыТоваровОрганизаций.НомерСтроки КАК НомерСтроки,
		|	РезервыТоваровОрганизаций.Организация КАК Организация,
		|	РезервыТоваровОрганизаций.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	РезервыТоваровОрганизаций.Номенклатура КАК Номенклатура,
		|	РезервыТоваровОрганизаций.Характеристика КАК Характеристика,
		|	РезервыТоваровОрганизаций.Партия КАК Партия,
		|	РезервыТоваровОрганизаций.Ячейка КАК Ячейка,
		|	ВЫБОР
		|		КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РезервыТоваровОрганизаций.Количество
		|		ИНАЧЕ -РезервыТоваровОрганизаций.Количество
		|	КОНЕЦ КАК КоличествоПередЗаписью
		|ПОМЕСТИТЬ ДвиженияРезервыТоваровОрганизацийПередЗаписью
		|ИЗ
		|	РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
		|ГДЕ
		|	РезервыТоваровОрганизаций.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	Иначе
		
		// Если временная таблица "ДвиженияРезервыТоваровОрганизацийИзменение" существует и содержит записи
		// об изменении набора, значит набор записывается не первый раз и для набора не был выполнен контроль остатков.
		// Текущее состояние набора и текущее состояние изменений помещаются во временную таблицу "ДвиженияРезервыТоваровОрганизацийПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно первоначального.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияРезервыТоваровОрганизацийИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияРезервыТоваровОрганизацийИзменение.Организация КАК Организация,
		|	ДвиженияРезервыТоваровОрганизацийИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ДвиженияРезервыТоваровОрганизацийИзменение.Номенклатура КАК Номенклатура,
		|	ДвиженияРезервыТоваровОрганизацийИзменение.Характеристика КАК Характеристика,
		|	ДвиженияРезервыТоваровОрганизацийИзменение.Партия КАК Партия,
		|	ДвиженияРезервыТоваровОрганизацийИзменение.Ячейка КАК Ячейка,
		|	ДвиженияРезервыТоваровОрганизацийИзменение.КоличествоПередЗаписью КАК КоличествоПередЗаписью
		|ПОМЕСТИТЬ ДвиженияРезервыТоваровОрганизацийПередЗаписью
		|ИЗ
		|	ДвиженияРезервыТоваровОрганизацийИзменение КАК ДвиженияРезервыТоваровОрганизацийИзменение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РезервыТоваровОрганизаций.НомерСтроки,
		|	РезервыТоваровОрганизаций.Организация,
		|	РезервыТоваровОрганизаций.СтруктурнаяЕдиница,
		|	РезервыТоваровОрганизаций.Номенклатура,
		|	РезервыТоваровОрганизаций.Характеристика,
		|	РезервыТоваровОрганизаций.Партия,
		|	РезервыТоваровОрганизаций.Ячейка,
		|	ВЫБОР
		|		КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РезервыТоваровОрганизаций.Количество
		|		ИНАЧЕ -РезервыТоваровОрганизаций.Количество
		|	КОНЕЦ
		|ИЗ
		|	РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
		|ГДЕ
		|	РезервыТоваровОрганизаций.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
	// Временная таблица "ДвиженияРезервыТоваровОрганизацийИзменение" уничтожается
	// Удаляется информация о ее существовании.
	
	Если СтруктураВременныеТаблицы.Свойство("ДвиженияРезервыТоваровОрганизацийИзменение") Тогда
		
		Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияРезервыТоваровОрганизацийИзменение");
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		СтруктураВременныеТаблицы.Удалить("ДвиженияРезервыТоваровОрганизацийИзменение");
	
	КонецЕсли;
	
	// Хозяйственная операция
	ХозяйственныеОперацииСервер.ЗаполнитьХозяйственнуюОперациюВНабореЗаписей(ЭтотОбъект);
	// Конец Хозяйственная операция
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ПриЗаписи набора записей.
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу "ДвиженияРезервыТоваровОрганизацийИзменение".
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МИНИМУМ(ДвиженияРезервыТоваровОрганизацийИзменение.НомерСтроки) КАК НомерСтроки,
	|	ДвиженияРезервыТоваровОрганизацийИзменение.Организация КАК Организация,
	|	ДвиженияРезервыТоваровОрганизацийИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ДвиженияРезервыТоваровОрганизацийИзменение.Номенклатура КАК Номенклатура,
	|	ДвиженияРезервыТоваровОрганизацийИзменение.Характеристика КАК Характеристика,
	|	ДвиженияРезервыТоваровОрганизацийИзменение.Партия КАК Партия,
	|	ДвиженияРезервыТоваровОрганизацийИзменение.Ячейка КАК Ячейка,
	|	СУММА(ДвиженияРезервыТоваровОрганизацийИзменение.КоличествоПередЗаписью) КАК КоличествоПередЗаписью,
	|	СУММА(ДвиженияРезервыТоваровОрганизацийИзменение.КоличествоИзменение) КАК КоличествоИзменение,
	|	СУММА(ДвиженияРезервыТоваровОрганизацийИзменение.КоличествоПриЗаписи) КАК КоличествоПриЗаписи
	|ПОМЕСТИТЬ ДвиженияРезервыТоваровОрганизацийИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДвиженияРезервыТоваровОрганизацийПередЗаписью.НомерСтроки КАК НомерСтроки,
	|		ДвиженияРезервыТоваровОрганизацийПередЗаписью.Организация КАК Организация,
	|		ДвиженияРезервыТоваровОрганизацийПередЗаписью.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ДвиженияРезервыТоваровОрганизацийПередЗаписью.Номенклатура КАК Номенклатура,
	|		ДвиженияРезервыТоваровОрганизацийПередЗаписью.Характеристика КАК Характеристика,
	|		ДвиженияРезервыТоваровОрганизацийПередЗаписью.Партия КАК Партия,
	|		ДвиженияРезервыТоваровОрганизацийПередЗаписью.Ячейка КАК Ячейка,
	|		ДвиженияРезервыТоваровОрганизацийПередЗаписью.КоличествоПередЗаписью КАК КоличествоПередЗаписью,
	|		ДвиженияРезервыТоваровОрганизацийПередЗаписью.КоличествоПередЗаписью КАК КоличествоИзменение,
	|		0 КАК КоличествоПриЗаписи
	|	ИЗ
	|		ДвиженияРезервыТоваровОрганизацийПередЗаписью КАК ДвиженияРезервыТоваровОрганизацийПередЗаписью
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияРезервыТоваровОрганизацийПриЗаписи.НомерСтроки,
	|		ДвиженияРезервыТоваровОрганизацийПриЗаписи.Организация,
	|		ДвиженияРезервыТоваровОрганизацийПриЗаписи.СтруктурнаяЕдиница,
	|		ДвиженияРезервыТоваровОрганизацийПриЗаписи.Номенклатура,
	|		ДвиженияРезервыТоваровОрганизацийПриЗаписи.Характеристика,
	|		ДвиженияРезервыТоваровОрганизацийПриЗаписи.Партия,
	|		ДвиженияРезервыТоваровОрганизацийПриЗаписи.Ячейка,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияРезервыТоваровОрганизацийПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияРезервыТоваровОрганизацийПриЗаписи.Количество
	|			ИНАЧЕ ДвиженияРезервыТоваровОрганизацийПриЗаписи.Количество
	|		КОНЕЦ,
	|		ДвиженияРезервыТоваровОрганизацийПриЗаписи.Количество
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций КАК ДвиженияРезервыТоваровОрганизацийПриЗаписи
	|	ГДЕ
	|		ДвиженияРезервыТоваровОрганизацийПриЗаписи.Регистратор = &Регистратор) КАК ДвиженияРезервыТоваровОрганизацийИзменение
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияРезервыТоваровОрганизацийИзменение.Организация,
	|	ДвиженияРезервыТоваровОрганизацийИзменение.СтруктурнаяЕдиница,
	|	ДвиженияРезервыТоваровОрганизацийИзменение.Номенклатура,
	|	ДвиженияРезервыТоваровОрганизацийИзменение.Характеристика,
	|	ДвиженияРезервыТоваровОрганизацийИзменение.Партия,
	|	ДвиженияРезервыТоваровОрганизацийИзменение.Ячейка
	|
	|ИМЕЮЩИЕ
	|	СУММА(ДвиженияРезервыТоваровОрганизацийИзменение.КоличествоИзменение) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	СтруктурнаяЕдиница,
	|	Номенклатура,
	|	Характеристика,
	|	Партия,
	|	Ячейка");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	// Новые изменения были помещены во временную таблицу "ДвиженияРезервыТоваровОрганизацийИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияРезервыТоваровОрганизацийИзменение", ВыборкаИзРезультатаЗапроса.Количество > 0);
	
	// Временная таблица "ДвиженияРезервыТоваровОрганизацийПередЗаписью" уничтожается
	Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияРезервыТоваровОрганизацийПередЗаписью");
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры // ПриЗаписи()

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли