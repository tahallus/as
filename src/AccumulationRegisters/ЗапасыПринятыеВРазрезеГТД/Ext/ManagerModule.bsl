#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ЕстьОстаткиПоЗапасамПереданнымВРазрезеГТД() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗапасыПринятыеВРазрезеГТД.НомерГТД
		|ИЗ
		|	РегистрНакопления.ЗапасыПринятыеВРазрезеГТД.Остатки КАК ЗапасыПринятыеВРазрезеГТД";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

// Процедура создает пустую временную таблицу изменения движений.
//
Процедура СоздатьПустуюВременнуюТаблицуИзменение(ДополнительныеСвойства) Экспорт
	
	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
	 ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	Если СтруктураВременныеТаблицы.Свойство("ДвиженияЗапасыПринятыеВРазрезеГТДИзменение") Тогда
		
		Возврат
		
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	ЗапасыПринятыеВРазрезеГТД.НомерСтроки КАК НомерСтроки,
	|	ЗапасыПринятыеВРазрезеГТД.Организация КАК Организация,
	|	ЗапасыПринятыеВРазрезеГТД.Номенклатура КАК Номенклатура,
	|	ЗапасыПринятыеВРазрезеГТД.НомерГТД КАК НомерГТД,
	|	ЗапасыПринятыеВРазрезеГТД.Партия КАК Партия,
	|	ЗапасыПринятыеВРазрезеГТД.Характеристика КАК Характеристика,
	|	ЗапасыПринятыеВРазрезеГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЗапасыПринятыеВРазрезеГТД.Количество КАК КоличествоПередЗаписью,
	|	ЗапасыПринятыеВРазрезеГТД.Количество КАК КоличествоИзменение,
	|	ЗапасыПринятыеВРазрезеГТД.Количество КАК КоличествоПриЗаписи
	|ПОМЕСТИТЬ ДвиженияЗапасыПринятыеВРазрезеГТДИзменение
	|ИЗ
	|	РегистрНакопления.ЗапасыПринятыеВРазрезеГТД КАК ЗапасыПринятыеВРазрезеГТД");
	
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураВременныеТаблицы.Вставить("ДвиженияЗапасыПринятыеВРазрезеГТДИзменение", Ложь);
	
КонецПроцедуры // СоздатьПустуюВременнуюТаблицуИзменение()

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли